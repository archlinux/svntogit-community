# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: t3ddy  <t3ddy1988 "at" gmail {dot} com>
# Contributor: Adrián Chaves Fernández (Gallaecio) <adriyetichaves@gmail.com>
pkgname=0ad
pkgver=a24.b
_pkgver=0.0.24b-alpha
pkgrel=7
pkgdesc="Cross-platform, 3D and historically-based real-time strategy game"
arch=('x86_64')
url="http://play0ad.com/"
license=('GPL2' 'CCPL')
depends=('0ad-data' 'binutils' 'boost-libs' 'curl' 'enet' 'libogg' 'libpng' 'libvorbis'
         'libxml2' 'openal' 'sdl2' 'wxgtk3' 'zlib' 'libgl' 'glu' 'fmt'
         'gloox' 'miniupnpc' 'libminiupnpc.so' 'icu' 'nspr' 'libsodium')
makedepends=('boost' 'cmake' 'mesa' 'zip' 'libsm' 'rust')
source=("https://releases.wildfiregames.com/$pkgname-$_pkgver-unix-build.tar.xz"
        $pkgname-rust-1.50.patch::https://github.com/0ad/0ad/commit/087a277e66c0456c2d6fdb7403d032ad09cbd18b.patch)
sha512sums=('8173d393eb1bc57c38bef0076ba973ef46c877c06644365189c0887cd3152adba51d26b553e42221fd14343e9830a3a275bfcd9b28f66281e282ea3f247730ea'
            '2e2763333dc23b746c4943894f9f0b8f526b7d85de7b8d02faebf95d6d39a54de3511447a679d04c5863ab886a53d66cecd7cac0c2bffc5922084445249e9b48')

prepare() {
  cd "$pkgname-$_pkgver"

  patch -Np1 -i ../$pkgname-rust-1.50.patch

}

build() {
  cd "$pkgname-$_pkgver/build/workspaces"

  export WX_CONFIG="wx-config-gtk3"
  ./update-workspaces.sh -j$(nproc) \
      --without-pch \
      --bindir=/usr/bin \
      --libdir=/usr/lib/0ad \
      --datadir=/usr/share/0ad/data

  cd gcc

  VERBOSE=1 make
}

package() {
  cd "$pkgname-$_pkgver"
  install -d "${pkgdir}"/usr/{bin,lib/0ad,share/"${pkgname}"/data}
  install -Dm755 binaries/system/pyrogenesis "${pkgdir}/usr/bin"
  install -Dm755 binaries/system/*.so "${pkgdir}/usr/lib/0ad"

  cp -r binaries/data/l10n/ "${pkgdir}/usr/share/${pkgname}/data/"

  install -Dm755 build/resources/${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 build/resources/${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 build/resources/${pkgname}.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
