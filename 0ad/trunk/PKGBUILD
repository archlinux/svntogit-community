# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: t3ddy  <t3ddy1988 "at" gmail {dot} com>
# Contributor: AdriÃ¡n Chaves FernÃ¡ndez (Gallaecio) <adriyetichaves@gmail.com>
pkgname=0ad
pkgver=a18
_pkgver=0.0.18-alpha
pkgrel=4
pkgdesc="Cross-platform, 3D and historically-based real-time strategy game"
arch=('i686' 'x86_64')
url="http://play0ad.com/"
license=('GPL2' 'CCPL')
depends=('binutils' 'boost-libs' 'curl' 'enet' 'libogg' 'libpng' 'libvorbis'
         'libxml2' 'openal' 'sdl' 'wxgtk2.8' 'zlib' 'libgl' '0ad-data' 'glu'
         'gloox' 'miniupnpc' 'icu' 'nspr')
makedepends=('boost' 'cmake' 'mesa' 'zip' 'python2' 'libsm')
source=("https://sourceforge.net/projects/zero-ad/files/releases/$pkgname-$_pkgver-unix-build.tar.xz"
        "9f24795fa4935aa1adcdea8c1008541011b3964f.patch")
md5sums=('84aedae3ab86955f87c55607600daa12'
         'dd14fef832a4eded98bbdd87cba00f8f')

prepare() {
  cd "$srcdir/$pkgname-$_pkgver/"
  sed -i "s/env python/env python2/g" libraries/source/cxxtest-4.4/bin/cxxtestgen
  patch -Np1 < $srcdir/9f24795fa4935aa1adcdea8c1008541011b3964f.patch
}

build() {
  cd "$srcdir/$pkgname-$_pkgver/build/workspaces"

  unset CPPFLAGS # for le spidermonkey

  export WX_CONFIG=/usr/bin/wx-config-2.8
  ./update-workspaces.sh \
      --with-system-miniupnpc \
      --bindir=/usr/bin \
      --libdir=/usr/lib/0ad \
      --datadir=/usr/share/${pkgname}/data

  cd "$srcdir/$pkgname-$_pkgver/build/workspaces/gcc"

  make
}

package() {
  install -d "${pkgdir}"/usr/{bin,lib/0ad,share/"${pkgname}"/data}
  cd "$srcdir/$pkgname-$_pkgver"

  install -Dm755 binaries/system/pyrogenesis "${pkgdir}/usr/bin"
  install -Dm755 binaries/system/*.so "${pkgdir}/usr/lib/0ad"

  cp -r binaries/data/l10n/ ${pkgdir}/usr/share/${pkgname}/data/

  install -Dm755 build/resources/${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 build/resources/${pkgname}.desktop \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 build/resources/${pkgname}.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

# Fails
#check() {
#  cd "$srcdir/$pkgname-$_pkgver"
#  LD_LIBRARY_PATH=binaries/system binaries/system/test
#}
