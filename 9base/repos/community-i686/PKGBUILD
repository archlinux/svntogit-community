# $Id$
# Maintainer: Jeff Mickey <j@codemac.net>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Aaron, phrakture, Griffin <aaron@archlinux.org>
# Contributor: Jeffrey 'jf' Lim <jfs.world@gmail.com>

pkgname=9base
pkgver=5
pkgrel=1
pkgdesc="Port of various original Plan9 tools to unix"
url="http://tools.suckless.org/9base"
source=(http://dl.suckless.org/tools/$pkgname-$pkgver.tar.gz 9 plan9.sh)
conflicts=('plan9port' '9rc-devel')
provides=('plan9')
arch=('i686' 'x86_64')
license=('custom')
build()
{
  cd "$srcdir/$pkgname-$pkgver"

  case $CARCH in
	i686) sed -i 's#^OBJTYPE\s.*$#OBJTYPE = 386#' config.mk ;;
	x86_64) sed -i 's#^OBJTYPE\s.*$#OBJTYPE = x86_64#' config.mk ;;
	esac

  sed -i 's#^PREFIX\s.*$#PREFIX = /opt/plan9#' config.mk

  make || return 1
  make DESTDIR="$pkgdir" install

  install -m755 ../9 "$pkgdir/opt/plan9/bin/"

  install -d -m755 "$pkgdir/etc/profile.d/"
  install -m755 ../plan9.sh "$pkgdir/etc/profile.d/"

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/9base/LICENSE"
}
md5sums=('6c9afc639e3c5b86e670b8eb50896e18'
         'ae7108b9f26bed388e9055f35eef2986'
         '62a9e52043d9c32967fcae9018fffb56')
