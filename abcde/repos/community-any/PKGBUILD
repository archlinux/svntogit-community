# $Id$
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=abcde
pkgver=2.5.4
pkgrel=5
pkgdesc='A frontend command-line utility that grabs tracks off a CD, encodes them to ogg or mp3 format, and tags them, all in one go.'
arch=('any')
url='http://code.google.com/p/abcde/'
license=('GPL')
depends=('bash' 'cd-discid' 'wget' 'vorbis-tools' 'python2-eyed3')
optdepends=('eject: Eject support'
            'cdparanoia: Paranoia ripping support'
            'cdrkit: icedax ripping support'
            'lame: MP3 encoding support'
            'flac: FLAC encoding support'
            'id3: ID3 v1 tag support'
            'perl-musicbrainz-discid: musicbrainz support'
            'perl-webservice-musicbrainz: musicbrainz support')
backup=("etc/${pkgname}.conf")
source=("http://abcde.googlecode.com/files/abcde-${pkgver}.tar.gz"
        'fix-mp3-tagging.patch'
        'python2-eyed3-0.7.0.patch')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    patch -Np2 < ${srcdir}/python2-eyed3-0.7.0.patch
    patch -Np0 < ${srcdir}/fix-mp3-tagging.patch
    sed -e "s:normalize-audio:normalize:g" -i ${pkgname}
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}

md5sums=('d761c5a07854ce24748be9aa10100e74'
         'c3975dc8389ff2edba6684bbc378d97d'
         '2d0d28069d0e42ef55232427214a665f')
