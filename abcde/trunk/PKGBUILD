# $Id$
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=abcde
pkgver=2.6
pkgrel=1
pkgdesc='A frontend command-line utility that grabs tracks off a CD, encodes them to ogg or mp3 format, and tags them, all in one go.'
arch=('any')
url='http://code.google.com/p/abcde/'
license=('GPL')
depends=('bash' 'cd-discid' 'wget' 'vorbis-tools' 'python2-eyed3')
optdepends=('cdparanoia: Paranoia ripping support'
            'cdrkit: icedax ripping support'
            'eject: Eject support'
            'flac: FLAC encoding support'
            'id3: ID3 v1 tag support'
            'lame: MP3 encoding support'
            'mp3gain: MP3 normalization support'
            'perl-musicbrainz-discid: musicbrainz support'
            'perl-webservice-musicbrainz: musicbrainz support'
            'vorbisgain: Ogg Vorbis normalization support')
backup=("etc/${pkgname}.conf")
source=("http://abcde.einval.com/download/abcde-${pkgver}.tar.gz"
        "http://abcde.einval.com/download/abcde-${pkgver}.tar.gz.sign"
        'fix-mp3-tagging.patch')
validpgpkeys=('CEBB52301D617E910390FE16587979573442684E') # Steve McIntyre

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    patch -Np0 < ${srcdir}/fix-mp3-tagging.patch
    sed -e "s:normalize-audio:normalize:g" -i ${pkgname}
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}

md5sums=('c8b91d5c53386cfc4345d9d49f67e1dd'
         'SKIP'
         'c3975dc8389ff2edba6684bbc378d97d')
