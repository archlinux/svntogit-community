# $Id$
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=abcde
pkgver=2.7.1
pkgrel=2
pkgdesc='A frontend command-line utility that grabs tracks off a CD, encodes them to ogg or mp3 format, and tags them, all in one go.'
arch=('any')
url='http://abcde.einval.com'
license=('GPL')
depends=('bash' 'cd-discid' 'wget' 'vorbis-tools' 'python2-eyed3')
optdepends=('cdparanoia: Paranoia ripping support'
            'cdrkit: icedax ripping support'
            'eject: Eject support'
            'flac: FLAC encoding support'
            'id3: ID3 v1 tag support'
            'lame: MP3 encoding support'
            'mp3gain: MP3 normalization support'
            'perl-musicbrainz-discid: musicbrainz support (AUR)'
            'perl-webservice-musicbrainz: musicbrainz support (AUR)'
            'vorbisgain: Ogg Vorbis normalization support')
backup=("etc/${pkgname}.conf")
source=("http://abcde.einval.com/download/abcde-${pkgver}.tar.gz"
        "http://abcde.einval.com/download/abcde-${pkgver}.tar.gz.sign"
        'fix-mp3-tagging.patch')
validpgpkeys=('CEBB52301D617E910390FE16587979573442684E'
              '742D444A5AFAF2951EF33E7BF8FB375D9CC820B3') # Steve McIntyre, Andrew Strong

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    patch -Np0 < ${srcdir}/fix-mp3-tagging.patch
    sed -e "s:normalize-audio:normalize:g" -i ${pkgname}
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" prefix="/usr" sysconfdir="/etc" install
}

md5sums=('09d5c9dc6c0bf6dd4b3fb159e1433018'
         'SKIP'
         'c3975dc8389ff2edba6684bbc378d97d')
