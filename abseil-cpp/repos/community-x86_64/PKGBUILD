# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Aleksandar TrifunoviÄ‡ <akstrfn at gmail dot com>

pkgname=abseil-cpp
pkgver=20220623.1
pkgrel=1
pkgdesc="Collection of C++ library code designed to augment the C++ standard library"
arch=('x86_64')
url='https://abseil.io'
license=('Apache')
makedepends=('cmake')
source=("https://github.com/abseil/abseil-cpp/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('91ac87d30cc6d79f9ab974c51874a704de9c2647c40f6932597329a282217ba8')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cmake -Bbuild \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DCMAKE_CXX_FLAGS="-DNDEBUG" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_STANDARD=17 \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"
  cmake --build .
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  DESTDIR="$pkgdir" cmake --install .
}
