# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>
# Contributor: kevin <kevin@archlinux.org>

pkgname=abuse
pkgver=0.8
pkgrel=1
pkgdesc='A side-scroller action game that pits you against ruthless alien killers.'
arch=('i686' 'x86_64')
url='http://abuse.zoy.org/'
license=('GPL' 'custom')
depends=('gcc-libs' 'libgl' 'sdl' 'sdl_mixer')
install='abuse.install'
source=("http://abuse.zoy.org/raw-attachment/wiki/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('ec678b8dc8d00e0382d8c805c6438489')

build() {
  cd "${srcdir}/abuse-${pkgver}"

  ./configure --prefix=/usr --with-assetdir=/usr/share/abuse/orig --with-x
  make
}

package() {
  cd "${srcdir}/abuse-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/licenses/abuse"
  sed -n '1,/^$/p' src/view.cpp > "${pkgdir}/usr/share/licenses/abuse/orig.code.license"
}
