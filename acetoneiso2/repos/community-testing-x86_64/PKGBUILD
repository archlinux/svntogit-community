# $Id$
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>

pkgname=acetoneiso2
pkgver=2.3
pkgrel=7
pkgdesc="An all in one ISO tool (bin mdf nrg img daa dmg cdi b5i bwi pdi iso)"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/acetoneiso"
license=('GPL3')
depends=('qtwebkit' 'fuseiso' 'cdrdao' 'cdrkit' 'dvd+rw-tools' 'gnupg' 'pinentry' 'mplayer' 'phonon')
optdepends=("p7zip: for ISO compress/decompress support")
conflicts=('acetoneiso')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/acetoneiso/AcetoneISO/AcetoneISO%202.3/acetoneiso_${pkgver}.tar.gz)
md5sums=('225f7af687016690ff28b9771a1ce179')

prepare() {
	cd ${srcdir}/acetoneiso_${pkgver}/acetoneiso/sources

	sed -i 's#<Phonon/#<phonon/#' erase_cd.h erase_cd.cpp erase_dvd.cpp \
	  burn_iso_2_cd.cpp burn_iso_2_cd.h burn_iso_2_dvd.cpp
}

build() {
	cd ${srcdir}/acetoneiso_${pkgver}/acetoneiso

	qmake-qt4
	# needed for phonon
	sed -i 's|-I/usr/include/QtCore|-I/usr/include/qt4/QtCore -I/usr/include/qt4/KDE|' Makefile
	make
}

package() {
	cd ${srcdir}/acetoneiso_${pkgver}/acetoneiso

	make INSTALL_ROOT="${pkgdir}" install
}
