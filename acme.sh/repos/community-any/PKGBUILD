# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gavin Lloyd <gavinhungry@gmail.com>

pkgname=acme.sh
pkgver=2.8.0
pkgrel=1
pkgdesc='An ACME Shell script, an acme client alternative to certbot'
arch=('any')
url='https://github.com/Neilpang/acme.sh'
license=('GPL3')
depends=('bash')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neilpang/acme.sh/archive/$pkgver.tar.gz")
sha256sums=('51d20c71e513e9fde0114de9ba3ada4e392e395981fb8d598b0ea610e183f009')

package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share/acme.sh/deploy
  install -dm755 "$pkgdir"/usr/share/acme.sh/dnsapi
  install -dm755 "$pkgdir"/usr/bin

  install -m755 acme.sh "$pkgdir"/usr/share/acme.sh/
  install -m755 deploy/*.sh "$pkgdir"/usr/share/acme.sh/deploy/
  install -m755 dnsapi/*.sh "$pkgdir"/usr/share/acme.sh/dnsapi/

  ln -s /usr/share/acme.sh/acme.sh "$pkgdir"/usr/bin/acme.sh
}
