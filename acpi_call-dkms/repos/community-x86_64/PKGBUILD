# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: mortzu <me@mortzu.de>
# Contributor: fnord0 <fnord0@riseup.net>

pkgname=acpi_call-dkms
pkgver=1.1.0
pkgrel=287
pkgdesc='A linux kernel module that enables calls to ACPI methods through /proc/acpi/call - module sources'
url='https://github.com/mkottman/acpi_call'
arch=('x86_64')
license=('GPL')
depends=('dkms')
makedepends=('linux-headers')
provides=("acpi_call=$pkgver-$pkgrel")
conflicts=('acpi_call')
source=("acpi_call-${pkgver}.tar.gz::https://github.com/mkottman/acpi_call/archive/v${pkgver}.tar.gz"
        '0001-linux-5.6.patch'
        'dkms.conf')
sha256sums=('d0d14b42944282724fca76f57d598eed794ef97448f387d1c489d85ad813f2f0'
            '1f397f087fd96d5286907c90f518bbe68b0d19f83ab30bb124f76a19693a932c'
            '32e6ea6523b13132c6c7838bba7fbf3d040ba2d35a892c2c356245612720df8a')

prepare() {
  cd acpi_call-$pkgver

  # Fix build with Linux >= 5.6
  patch -Np1 < ../0001-linux-5.6.patch
}

package() {
  cd acpi_call-$pkgver

  install -Dt "$pkgdir/usr/src/acpi_call-$pkgver" -m0644 Makefile acpi_call.c ../dkms.conf

  echo acpi_call | install -Dm0644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/acpi_call.conf"

  mkdir -p "$pkgdir/usr/share/acpi_call"
  cp -t "$pkgdir/usr/share/acpi_call" -dr --no-preserve=ownership examples support
}

# vim:set ts=2 sw=2 et:
