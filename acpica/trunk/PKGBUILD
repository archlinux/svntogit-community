# Maintainer : Bruno Pagani <archange@archlinux.org>
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=acpica
pkgver=20210331
pkgrel=1
pkgdesc="ACPI tools, including Intel ACPI Source Language compiler"
arch=(x86_64)
url="https://acpica.org"
license=(GPL2)
depends=(glibc)
makedepends=(flex bison)
provides=(iasl)
conflicts=(iasl)
replaces=(iasl)
options=(!makeflags)
source=(${pkgname}-unix-${pkgver}.tar.gz::https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz)
sha256sums=('b49237a2c3df58b57310612ec3a6ebee69e1a525b5efeec7152faf32a03b7068')

build() {
  cd acpica-unix-${pkgver}
  make
}

package() {
  cd acpica-unix-${pkgver}
  make DESTDIR="${pkgdir}" install
}
