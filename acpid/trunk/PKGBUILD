# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us

pkgname=acpid
pkgver=2.0.17
pkgrel=4
pkgdesc='A daemon for delivering ACPI power management events with netlink support'
arch=('i686' 'x86_64')
url='http://tedfelix.com/linux/acpid-netlink.html'
license=('GPL')
depends=('bash')
optdepends=('perl: use perl based examples')
replaces=('acpid2')
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything')
source=("http://www.tedfelix.com/linux/$pkgname-$pkgver.tar.xz"
        'acpid.socket'
        'acpid.service'
        'anything'
        'handler.sh'
        '0001-Fix-format-of-video-tabletmode-event-string.patch')
md5sums=('d858729b7d984c5e3a0c7558e9de1584'
         'ee6cb99e50e580c50331a73045412ae9'
         '10290f69491206cd104654e5b900b661'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '47f44ff5f02685dce8bcdab8568f0c38'
         'a813e00553104f2129f64cae95a7983f')

build() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/0001-Fix-format-of-video-tabletmode-event-string.patch"
  ./configure --prefix=/usr
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  popd

  # default config
  install -Dm644 anything "$pkgdir/etc/acpi/events/anything"
  install -Dm755 handler.sh "$pkgdir/etc/acpi/handler.sh"

  # systemd
  install -Dm644 acpid.socket "$pkgdir/usr/lib/systemd/system/acpid.socket"
  install -Dm644 acpid.service "$pkgdir/usr/lib/systemd/system/acpid.service"

  # fix acpid rights
  chmod 755 "$pkgdir/usr/sbin/acpid"
}

# vim:set ts=2 sw=2 et:
