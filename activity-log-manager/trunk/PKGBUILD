# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ner0

pkgname=activity-log-manager
pkgver=0.9.7
pkgrel=3
pkgdesc="A graphical user interface which lets you easily control what gets logged by Zeitgeist"
arch=('i686' 'x86_64')
url="https://launchpad.net/activity-log-manager"
license=('GPL2')
depends=('gtk3' 'libgee' 'zeitgeist')
makedepends=('vala' 'intltool')
install=$pkgname.install
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20140812.tar.gz
        fix-build.patch
        gee-0.8.patch)
md5sums=('d886be32db0fa351aaf0eb59912ee377'
         'a7ddf1d853f4942ebf701256d9e5a38f'
         '243c71b429e12172691dcb748055247d'
         'c3598a22822462d03083b3999748530d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  # Regenerate C files
  rm src/activity_log_manager_vala.stamp

  # Fix build
  patch -Np1 -i ../fix-build.patch

  # Port to gee-0.8
  patch -Np1 -i ../gee-0.8.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
