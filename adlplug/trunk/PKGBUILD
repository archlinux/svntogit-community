# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ADLplug
pkgbase=adlplug
pkgname=('adlplug' 'opnplug')
pkgver=1.0.2
pkgrel=1
pkgdesc="FM synthesizer plugins, based on OPL3 and OPN2 sound chip emulations"
arch=('x86_64')
url="https://github.com/jpcima/ADLplug"
license=('Boost' 'GPL2' 'GPL3' 'LGPL2.1')
groups=('pro-audio' 'lv2-plugins' 'vst-plugins')
# TODO: devendor simpleini, if feasible
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11' 'libxext')
makedepends=('alsa-lib' 'cmake' 'fmt' 'freetype2' 'jack' 'liblo' 'libxrandr'
'libxinerama' 'libxcursor')
source=("https://github.com/jpcima/${pkgbase}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.xz"
        "${pkgbase}-1.0.1-gcc9.patch::https://github.com/WeAreROLI/JUCE/commit/4e0adb2af8b424c43d22bd431011c9a6c57d36b6.patch")
sha512sums=('53192a622de528d4dd06dbd8411b5d1ba8346e9f181fd160fa14c2cf402a819ff354212ced59e13b11738ef6bf696cb0927a67c2e97ef94ade70acb29807f5cc'
            '263fe21e7362f4b8c4009d33e8a086ddf57ddd44267343b924f1304a58580a38e6bd2d36c9548ec706be733476d27e5c89f8095f401b9b841ade63d9cb812a77')
b2sums=('ea049a6373fdd05f6401766f077819de704748055569a67aabb27e6064d922a28f635d8322914a5c418875b09a0152efa3ac4a3ae32400b12e31ae10166ceffb'
        '356fe202072436cab89fdde3a06a5140d286d7636b663f32432f36b98170bef1aa7a931aeb6bedb2ff4d6b8f4b4c730f1760211b2fa9b709fe30bfedbe8df62f')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgbase-$pkgver"
  cd "$pkgbase-$pkgver"
  # fix build with gcc9: https://github.com/jpcima/ADLplug/issues/54
  (
    cd thirdparty/JUCE
    patch -Np1 -i "${srcdir}/${pkgbase}-1.0.1-gcc9.patch"
  )
}

build() {
  cd "$pkgbase-$pkgver"
  # build opl3
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DADLplug_CHIP=OPL3 \
        -DADLplug_USE_SYSTEM_FMT=ON \
        -W no-dev \
        -B "build-${pkgname[0]}" \
        -S .
  make VERBOSE=1 -C "build-${pkgname[0]}"
  # build opn2
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DADLplug_CHIP=OPN2 \
        -DADLplug_USE_SYSTEM_FMT=ON \
        -W no-dev \
        -B "build-${pkgname[1]}" \
        -S .
  make VERBOSE=1 -C "build-${pkgname[1]}"
}

package_adlplug() {
  pkgdesc="FM synthesizer for ADLMIDI with OPL3 chip emulation"
  depends+=('libasound.so' 'libfmt.so' 'libfreetype.so')
  optdepends=('jack: for the standalone application'
              'liblo: for new-session-manager support'
              'new-session-manager: for session management')
  cd "$pkgbase-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C "build-${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_opnplug() {
  pkgdesc="FM synthesizer for OPNMIDI with OPN2 chip emulation"
  depends+=('libasound.so' 'libfmt.so' 'libfreetype.so')
  optdepends=('jack: for the standalone application'
              'liblo: for new-session-manager support'
              'new-session-manager: for session management')
  cd "$pkgbase-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C "build-${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
