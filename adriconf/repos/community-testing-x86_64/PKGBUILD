# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=adriconf
pkgver=1.5.1
pkgrel=2
pkgdesc="GUI tool to configure Mesa drivers by setting options and writing them to the standard drirc file"
arch=(x86_64)
url="https://github.com/jlHertel/adriconf"
license=(GPL3)
depends=(boost-libs gtkmm3 libxml++ pciutils)
makedepends=(boost cmake git)
_commit=bfdd9128771a5c46c61b3bc36270161824d35ffd  # tags/v1.5.1
source=("git+https://github.com/jlHertel/adriconf#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_UNIT_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  cd ../$pkgname
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.appdata.xml "$pkgdir/usr/share/metainfo/br.com.jeanhertel.adriconf.appdata.xml"
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.desktop "$pkgdir/usr/share/applications/br.com.jeanhertel.adriconf.desktop"
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/br.com.jeanhertel.adriconf.png"
}
