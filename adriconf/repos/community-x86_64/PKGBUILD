# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=adriconf
pkgver=2.4.1
pkgrel=1
pkgdesc="GUI tool to configure Mesa drivers by setting options and writing them to the standard drirc file"
arch=(x86_64)
url="https://gitlab.freedesktop.org/mesa/adriconf"
license=(GPL3)
depends=(boost-libs gtkmm3 libxml++ pciutils)
makedepends=(boost cmake)
source=(https://gitlab.freedesktop.org/mesa/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha256sums=('a0b6d9c2165cde5a1753fe7885246fbd17e51a768c88bc5b14ab83ac61d869f7')

build() {
  cmake -B build -S $pkgbase-v$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_UNIT_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgbase-v$pkgver/flatpak/org.freedesktop.adriconf.metainfo.xml \
    "$pkgdir/usr/share/metainfo/org.freedesktop.adriconf.metainfo.xml"
  install -Dm644 $pkgbase-v$pkgver/flatpak/org.freedesktop.adriconf.desktop \
    "$pkgdir/usr/share/applications/org.freedesktop.adriconf.desktop"
  install -Dm644 $pkgbase-v$pkgver/flatpak/org.freedesktop.adriconf.png \
    "$pkgdir/usr/share/icons/hicolor/256x256/apps/org.freedesktop.adriconf.png"
}
