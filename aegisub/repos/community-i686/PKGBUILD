# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.0.2
pkgrel=6
pkgdesc="A general-purpose subtitle editor with ASS/SSA support"
arch=('i686' 'x86_64')
url="http://www.aegisub.org"
license=('GPL' 'BSD')
depends=('desktop-file-utils' 'ffmpegsource' 'fftw' 'hicolor-icon-theme' 'hunspell' 'lua51' 'wxgtk2.9' 'hicolor-icon-theme')
makedepends=('mesa')
install=aegisub.install
source=("http://ftp.aegisub.org/pub/releases/${pkgname}-${pkgver}.tar.xz" 'crash-on-deatach.patch' 'lua51.patch')
sha256sums=('68ec20cf49f513608cec400bd462ebb762d6057109968fb01d842e10e6e7fb39'
            '85e2c2a6bf091f5b4ca29a0149df8d8ca6219d98a4095f79809f2e698983bca4'
            'ce67b143cc2949fbbd7d72862c54005030339d1c364753335acec271ca9e179d')

build() {
  cd "${srcdir}"/${pkgname}/${pkgname}

# Patch
  patch -Np1 -i "${srcdir}"/crash-on-deatach.patch
  patch -Np1 -i "${srcdir}"/lua51.patch

# Build
  autoreconf -ivf
  ./configure --prefix=/usr --without-{portaudio,openal,oss} --with-wxdir=/usr/include/wx-2.9 --with-wx-config=/usr/bin/wx-config-2.9
  make
}

package() {
  cd "${srcdir}"/${pkgname}/${pkgname}

# Install
  make DESTDIR="$pkgdir" install

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
