# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.2.0
pkgrel=3
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=('i686' 'x86_64')
url='http://www.aegisub.org'
license=('GPL' 'BSD')
depends=('boost-libs' 'desktop-file-utils' 'ffms2' 'fftw' 'hunspell' 'wxgtk')
makedepends=('boost' 'intltool' 'mesa')
install="${pkgname}.install"
source=("http://ftp.aegisub.org/pub/archives/releases/source/${pkgname}-${pkgver}.tar.xz"
        'aegisub-boost1.56.patch')
sha256sums=('d9e5fd48b243e29832361a13f8be82b271ef0c606b13593ec72461156ee19159'
            '998ffba66c5acd879c4bd87e339ee66cea875d37171a269ad5f29eedc2997ec0')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../aegisub-boost1.56.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --without-{portaudio,openal,oss} \
              --disable-update-checker
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
