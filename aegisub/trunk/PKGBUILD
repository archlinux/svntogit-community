# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.3.3
pkgrel=3
pkgdesc='A general-purpose subtitle editor with ASS/SSA support'
arch=(x86_64)
url=http://www.aegisub.org
license=(
  GPL
  BSD
)
depends=(
  alsa-lib
  boost-libs
  fftw
  fontconfig
  hunspell
  icu
  libass.so
  libffms2.so
  libgl
  libpulse
  uchardet
  wxwidgets-gtk3
  zlib
)
makedepends=(
  boost
  cmake
  git
  mesa
  ninja
)
_tag=a596a88115890a6f8ecd41518f1dbcdea1f6cd61
source=(git+https://github.com/wangqr/Aegisub#tag=${_tag}
        boost-1.81.0.patch)
#validpgpkeys=(6DD9508BCB1CE31AF295FF9B6A889F50A8B00C08) # Thomas Goyne <plorkyeran@aegisub.org>
b2sums=('SKIP'
        '5b6874ebb89f6ed3ab7929796bd1f59e1748c418ae5397df265fe3007d9f971eec7388f5f6a2dcac020d71c9cffece931a1ae5eb78f8ff4ab61fdc4c5e65328a')

pkgver() {
  cd Aegisub
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd Aegisub
  patch -Np1 -i ../boost-1.81.0.patch # Patch from Gentoo]
}

build() {
  cmake -S Aegisub -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_OSS=OFF
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -dm 755 "${pkgdir}"/usr/share/aegisub/automation/include
  cp -dr --no-preserve=ownership Aegisub/automation/{autoload,demos} "${pkgdir}"/usr/share/aegisub/automation/
  cp -dr --no-preserve=ownership Aegisub/automation/include/{aegisub,*.lua} "${pkgdir}"/usr/share/aegisub/automation/include/
  install -Dm 644 Aegisub/LICENCE -t "${pkgdir}"/usr/share/licenses/aegisub/
}

# vim: ts=2 sw=2 et:
