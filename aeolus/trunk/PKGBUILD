# Maintainer : David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Christoph Zeiler <arch at moonblade dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

_stopver=0.3.0
pkgname=aeolus
pkgver=0.9.7
pkgrel=1
pkgdesc="Synthesised pipe organ emulator"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/aeolus/"
license=('GPL3')
groups=('pro-audio')
depends=('clthreads' 'clxclient' 'jack' 'zita-alsa-pcmi')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2"
        "https://kokkinizita.linuxaudio.org/linuxaudio/downloads/stops-$_stopver.tar.bz2"
        "$pkgname.conf")
sha512sums=('ea81b784a26441e93535c9e3cd7b8c9c6b4f9946bd8aac0b84adc89e986986fd7f45b9b45d70f211323a4c616de8b8d8d16508e9553c694a4a6a74aeaf833ec2'
            'a6572a42c6796aa08b9085bfc4cb9e252091310224a0b8867eaf5d41f399b2649744c357506ae5fb64a82018f0da702bacbdc7e96965a1116f08d0a248d28ad3'
            'b04689673aec928e2594a86b595a6f6d5d70408467c99379f6282f98dd9fbc1cab42b196927193a708e26229e665a121a22edf42e8ab49bb24d68d45bde3181c')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip -march=native
  # call ldconfig in local context
  sed -e '/native/d' \
      -e 's/ldconfig/& -N $(DESTDIR)\/$(LIBDIR)/' \
      -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr SUFFIX=''
}

package(){
  cd "${pkgname}-${pkgver}/source"
  make DESTDIR="${pkgdir}/" PREFIX=/usr SUFFIX='' install
  # docs
  install -vDm 644 ../{AUTHORS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # system config file
  install -vDm 644 "${srcdir}/${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"
  # stops
  cd "${srcdir}/stops-${_stopver}"
  find . -name definition -exec install -vDm 644 {} \
    "${pkgdir}/usr/share/${pkgname}/stops/"{} \;
  find . -name presets -exec install -vDm 644 {} \
    "${pkgdir}/usr/share/${pkgname}/stops/"{} \;
  find . -name '*.ae0' -exec install -vDm 644 {} \
    "${pkgdir}/usr/share/${pkgname}/stops" \;
}
