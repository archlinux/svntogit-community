# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: Gökberk Yaltıraklı <aur at gkbrk dot com>
# Contributor: Drew DeVault <sir@cmpwn.com>

pkgname=aerc
pkgver=0.9.0
pkgrel=2
pkgdesc='Email Client for your Terminal'
arch=('x86_64')
url='https://git.sr.ht/~rjarry/aerc'
license=('MIT')
depends=('notmuch-runtime' 'gawk')
makedepends=('go' 'scdoc')
optdepends=('w3m: for showing HTML messages'
            'dante: for showing HTML messages')
provides=('aerc')
conflicts=('aerc')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~rjarry/${pkgname}/archive/${pkgver}.tar.gz"
        mk-rename-ambiguous-LDFLAGS-variable.patch)
sha512sums=('5821a35bc9c893ac800532b24cce0dcee5113fe41cce057e3ce952c7d7e9add680cbb11b4700cc8896d852817534d979df44f1cb82d93ae5438ebed826bab7bb'
            'd93538b775b8bbb97b966110558c640e02248c425da001a7b0a6d262a91e551f8ebd4c4a7ea430873915245676c97852b79a94671cdd5fd57c60f2b7a40355c2')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i ../mk-rename-ambiguous-LDFLAGS-variable.patch
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    export CGO_LDFLAGS="${LDFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -modcacherw -tags=notmuch"
    make PREFIX=/usr
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make PREFIX=/usr DESTDIR=$pkgdir install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/aerc/"
}
