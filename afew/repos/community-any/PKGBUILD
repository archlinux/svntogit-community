# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Lucas Hoffmann
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Kazuo Teramoto <kaz.rag at gmail.com>

pkgname=afew
pkgver=2.0.0
pkgrel=1
pkgdesc='Initial tagging script for notmuch mail'
arch=(any)
url=https://github.com/afewmail/afew
license=('custom:BSD')
depends=('notmuch' 'python-chardet' 'python-dkim' 'python-setuptools')
makedepends=('python-setuptools-scm' 'python-sphinx')
source=("https://files.pythonhosted.org/packages/source/a/afew/afew-$pkgver.tar.gz")
sha256sums=('1a685b97ddeba66d4af5ee9b2b76b1a8bccccd3d10bc285c8b82886a0328c048')

build() {
  cd afew-$pkgver
  python setup.py build
  python setup.py build_sphinx -b man
}

check() {
  cd afew-$pkgver
  python -m unittest discover afew/tests
}

package() {
  cd afew-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 build/sphinx/man/afew.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/afew LICENSE
}

# vim:set ts=2 sw=2 et:
