# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=agave
pkgver=0.4.7
pkgrel=8
pkgdesc="Colorscheme designer tool for GNOME"
arch=('i686' 'x86_64')
url="http://web.archive.org/web/20170308183221/http://home.gna.org/colorscheme/"
license=('GPL')
depends=('libglademm' 'gconfmm')
makedepends=('gnome-doc-utils' 'intltool' 'boost' 'cppunit')
source=("http://web.archive.org/web/20170227035931/http://download.gna.org/colorscheme/releases/$pkgname-$pkgver.tar.bz2"{,.sig}
        agave-0.4.7-mdv-fix-str-fmt.patch
        schemebox.patch
        drop-libgnome.patch)
validpgpkeys=('CAD6D32E41FEE86D519CBC60945C0F11A1C5E0E3') # Jonathon Jongsma
md5sums=('93097881f28dcae1eca2800a763f77c3'
         'SKIP'
         'd96ddfe751d8fd4d13a443b94fafb0b6'
         'c41adfd52ecbf837f4338e67693640fc'
         '7419d23329f847dcde12baa467b52135')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Build fix from Fedora
  patch -Np1 -i "$srcdir/agave-0.4.7-mdv-fix-str-fmt.patch"

  # Another build fix
  patch -Np1 -i "$srcdir/schemebox.patch"

  # Remove deprecated libgnome dependency
  patch -Np1 -i "$srcdir/drop-libgnome.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper --disable-gnome --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
