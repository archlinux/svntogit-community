# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=agda-stdlib
pkgver=1.4
_agdaver=2.6.1.3
pkgrel=3
pkgdesc="Agda standard libraries"
url="https://github.com/agda/agda-stdlib"
license=("MIT")
arch=('x86_64')
depends=("agda=$_agdaver")
makedepends=('ghc' 'uusi' 'haskell-filemanip')
options=('staticlibs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agda/agda-stdlib/archive/v$pkgver.tar.gz"
        standard-library.agda-lib.in)
sha512sums=('90c05d54eaf1d7c78d00f73044afa1f3b4d99df622d40b3cea88fe280f0770ccaeddd787d8fb384c150123f592bb6a7ec24b842a9fcc2936f581d847928223ca'
            '92fad2f9b941af4506bb0d5d9d533d09823df89f7530ee5d787b741816ba07e68f25b96d6653ae6ed949ec23484c1fba47a63cc6401311462a55b5e06cfc032c')

prepare() {
    cd $pkgname-$pkgver
    rm standard-library.agda-lib
    uusi lib.cabal
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla
    runhaskell Setup build $MAKEFLAGS

    dist/build/GenerateEverything/GenerateEverything

    agda -i. -isrc Everything.agda
}

package() {
    install -Dm644 standard-library.agda-lib.in "$pkgdir"/usr/share/agda/lib/standard-library.agda-lib

    cd $pkgname-$pkgver
    install -dm755 "$pkgdir"/usr/share/agda/lib/stdlib
    cp -pr src/* "$pkgdir"/usr/share/agda/lib/stdlib/

    # Symlink to avoid writing to this place when agda-lib is present
    install -dm755 "$pkgdir"/usr/share/agda/lib/_build/$_agdaver/agda
    ln -s ../../../stdlib "$pkgdir"/usr/share/agda/lib/_build/$_agdaver/agda/

    install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
