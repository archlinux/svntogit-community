# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Agda
pkgname=agda
pkgver=2.6.1.1
pkgrel=8
pkgdesc="A dependently typed functional programming language and proof assistant"
url="https://wiki.portal.chalmers.se/agda/"
license=("custom")
arch=('x86_64')
depends=('ghc-libs' 'ghc' 'haskell-aeson' 'haskell-async' 'haskell-blaze-html' 'haskell-boxes'
         'haskell-cpphs' 'haskell-data-hash' 'haskell-edit-distance' 'haskell-equivalence'
         'haskell-geniplate-mirror' 'haskell-gitrev' 'haskell-hashable'
         'haskell-hashtables' 'haskell-ieee754' 'haskell-murmur-hash' 'haskell-regex-tdfa'
         'haskell-split' 'haskell-strict' 'haskell-unordered-containers' 'haskell-uri-encode'
         'haskell-zlib')
optdepends=('agda-stdlib: for standard library')
makedepends=('alex' 'happy' 'ghc' 'haskell-filemanip')
source=("$_hkgname-$pkgver.tar.gz::https://github.com/agda/agda/archive/v$pkgver.tar.gz"
        agda-strict-0.4.patch::https://github.com/felixonmars/agda/commit/0497ec9fdde159a56ee42821e500b52d66374201.patch
        agda-aeson-1.5.3.patch::https://github.com/agda/agda/commit/292f7d8c3def7e69ce9e79c67c895d6a4956dc5f.patch)
sha512sums=('fb5bb2d1bc2ea253e709da3a29920a06f35523fda88b9cdc3f0e957e240871d0e535ca4ca92e17d67f112324c0ad90058edcbac76a0bdf2baccdcd16b9af7c1f'
            '6968a367d8d8cfa9a89d4760ba13aa5236ff1d3597ad8ef9301c33d3696decec092d4fd3556ae8c0bf4c09cd1f157ddb38b1443c2d340487a7eef93cf23a3a7b'
            '9d8c233c87161ba80d283db0e301c2ff80f771a9e63416fce5c0e846146cd6a4aeae91a97dff869a724afc021c26d4c3088ebfc3a11c814d637fac5a5b677776')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../agda-strict-0.4.patch
    patch -p1 -i ../agda-aeson-1.5.3.patch
    sed -i '/fno-warn-overlapping-patterns/a \        , "-dynamic"' src/full/Agda/Compiler/MAlonzo/Compiler.hs
    sed -e "s|(\"Agda_datadir\", agda_datadir) : e|[(\"Agda_datadir\", agda_datadir), (\"LD_LIBRARY_PATH\", \"$PWD/dist/build\")] ++ e|" \
        -i Setup.hs
    sed -i 's/< *1.5/<2/' $_hkgname.cabal
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcpphs -f-debug -f-enable-cluster-counting
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"

    # Symlink to avoid writing to this place when agda-lib is present
    install -dm755 "$pkgdir"/usr/share/agda/lib/_build/$pkgver/agda
    ln -s ../../../prim "$pkgdir"/usr/share/agda/lib/_build/$pkgver/agda/

    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
