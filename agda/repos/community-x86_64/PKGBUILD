# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Agda
pkgname=agda
pkgver=2.5.99.20190207
_commit=3048fdaa1840c352fc00aa7222c50bcd622580d8
pkgrel=12
pkgdesc="A dependently typed functional programming language and proof assistant"
url="https://wiki.portal.chalmers.se/agda/"
license=("custom")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-async' 'haskell-blaze-html' 'haskell-boxes'
         'haskell-cpphs' 'haskell-data-hash' 'haskell-edisoncore' 'haskell-edit-distance'
         'haskell-equivalence' 'haskell-geniplate-mirror' 'haskell-gitrev' 'haskell-hashable'
         'haskell-hashtables' 'haskell-ieee754' 'haskell-murmur-hash' 'haskell-regex-tdfa'
         'haskell-split' 'haskell-strict' 'haskell-unordered-containers' 'haskell-uri-encode'
         'haskell-zlib')
optdepends=('agda-stdlib: for standard library')
makedepends=('alex' 'happy' 'ghc' 'haskell-filemanip')
source=("$_hkgname-$_commit.tar.gz::https://github.com/agda/agda/archive/$_commit.tar.gz")
sha512sums=('21b061fada9b3ddfb1348366f270990192eefe49e79943a061d843d6fe78c049b87fde73ff3cf70c586e955c8aa5007365723b380686931a55547deae87e0043')

prepare() {
    mv $pkgname-{$_commit,$pkgver}

    mkdir -p lib-target
    cp -a $pkgname-$pkgver/src/data/lib lib-target/lib

    cd $pkgname-$pkgver
    sed -e "s|rawSystem agda \\[|rawSystem \"env\" [\"Agda_datadir=$PWD/../lib-target\", \"LD_LIBRARY_PATH=$PWD/dist/build\", agda,|" \
        -e "s|datadir dirs|\"$PWD/../lib-target\"|" \
        -e 's|"Builtins"|"Builtin"|' \
        -i Setup.hs

    sed -i 's/== 0.5/>= 0.5/' Agda.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcpphs -f-debug -f-enable-cluster-counting
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    install -m644 "$srcdir"/lib-target/lib/prim/Agda/*.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/
    install -m644 "$srcdir"/lib-target/lib/prim/Agda/Builtin/*.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Builtin/
    install -m644 "$srcdir"/lib-target/lib/prim/Agda/Builtin/Cubical/*.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Builtin/Cubical/
    install -m644 "$srcdir"/lib-target/lib/prim/Agda/Primitive/*.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Primitive/
}
