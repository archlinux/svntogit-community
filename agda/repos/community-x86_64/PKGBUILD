# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Agda
pkgname=agda
pkgver=2.4.2.5
pkgrel=4
pkgdesc="A dependently typed functional programming language and proof assistant"
url="http://wiki.portal.chalmers.se/agda/"
license=("custom")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib')
makedepends=("ghc=7.10.3" "alex" "happy" "haskell-boxes" "haskell-cpphs"
             "haskell-data-hash" "haskell-edit-distance" "haskell-equivalence"
             "haskell-geniplate-mirror" "haskell-hashable" "haskell-hashtables"
             "haskell-src-exts" "haskell-mtl" "haskell-parallel" "haskell-quickcheck"
             "haskell-strict" "haskell-text" "haskell-transformers-compat"
             "haskell-unordered-containers" "haskell-zlib")
optdepends=('agda-stdlib: for standard library')
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        unordered-containers-0.2.6.patch)
sha256sums=('959658a372d93b735d92191b372d221461026c98de4f92e56d198b576dfb67ee'
            '51e2d2160861ec6b8bd879f85cbbd00f9ed7a564b307870992370457e307f3ac')

prepare() {
    sed -e 's/transformers-compat >= 0.3.3.3 && < 0.5/transformers-compat >= 0.3.3.3 \&\& < 0.6/' \
        -e 's/cpphs >= 1.19 && < 1.20/cpphs >= 1.19 \&\& < 1.21/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal

    cd ${_hkgname}-${pkgver}
    # https://github.com/agda/agda/issues/1779
    patch -p1 -i ../unordered-containers-0.2.6.patch

    cd ..
    # TODO: Find a better way!
    # Build it twice to compile the agdai file.
    cp -a ${_hkgname}-${pkgver}{,-tmp}
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fcpphs
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    LC_CTYPE=en_US.UTF-8 runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh

    cd "${srcdir}/${_hkgname}-${pkgver}-tmp"
    runhaskell Setup configure --prefix="$PWD/target" --datasubdir="$pkgname"
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    runhaskell Setup copy
    target/bin/agda target/share/agda/lib/prim/Agda/Primitive.agda
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    install -m644 "${srcdir}/${_hkgname}-${pkgver}-tmp"/target/share/agda/lib/prim/Agda/Primitive.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Primitive.agdai

    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
