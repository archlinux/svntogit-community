# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Agda
pkgname=agda
pkgver=2.6.2.2
pkgrel=37
pkgdesc="A dependently typed functional programming language and proof assistant"
url="https://wiki.portal.chalmers.se/agda/"
license=("custom")
arch=('x86_64')
depends=('ghc-libs' 'ghc' 'haskell-aeson' 'haskell-async' 'haskell-blaze-html' 'haskell-boxes'
         'haskell-case-insensitive' 'haskell-cpphs' 'haskell-data-hash' 'haskell-edit-distance'
         'haskell-equivalence' 'haskell-gitrev' 'haskell-hashable' 'haskell-hashtables'
         'haskell-monad-control' 'haskell-murmur-hash' 'haskell-parallel' 'haskell-regex-tdfa'
         'haskell-split' 'haskell-strict' 'haskell-unordered-containers' 'haskell-uri-encode'
         'haskell-zlib')
optdepends=('agda-stdlib: for standard library')
makedepends=('alex' 'happy' 'ghc' 'uusi' 'haskell-filemanip')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('f54dcc0fd6dea106db4a04cc3b0b80f404f0613b5075849db17a1b4b5e176ed6d183bfdaf464fbcbc6e0807c1af0c8748f96552d48e537444109be54730685a8')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u aeson $_hkgname.cabal
    sed -i '/fno-warn-overlapping-patterns/a \        , "-dynamic"' src/full/Agda/Compiler/MAlonzo/Compiler.hs
}

build() {
    cd $_hkgname-$pkgver

    # -fllvm is disabled because ghc+llc hangs since LLVM 13
    # https://gitlab.haskell.org/ghc/ghc/-/issues/20559
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fasm \
            -fcpphs -f-debug -f-enable-cluster-counting
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    LD_LIBRARY_PATH="$PWD/dist/build" runhaskell Setup copy --destdir="${pkgdir}"

    # Symlink to avoid writing to this place when agda-lib is present
    install -dm755 "$pkgdir"/usr/share/agda/lib/_build/$pkgver/agda
    ln -s ../../../prim "$pkgdir"/usr/share/agda/lib/_build/$pkgver/agda/

    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
