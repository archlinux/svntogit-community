# $Id$
# Maintainer: Jonathan Conder <jonno dot conder at gmail dot com>

pkgname=agg
pkgver=2.5
pkgrel=4
pkgdesc="A High Quality Rendering Engine for C++"
arch=('i686' 'x86_64')
url="http://www.antigrain.com/"
license=('GPL')
depends=('gcc-libs' 'sdl' 'freetype2')
provides=('antigrain')
replaces=('antigrain')
options=('!libtool' '!makeflags')
source=("http://www.antigrain.com/${pkgname}-${pkgver}.tar.gz"
        agg-2.4-depends.patch
        agg-2.5-pkgconfig.patch)
md5sums=('0229a488bc47be10a2fee6cf0b2febd6'
         '22f8e48c137d25038181c86d5e40b110'
         '43a19a7b1564c591e56c8d09a0fd8da5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/agg-2.4-depends.patch"
  patch -Np1 -i "${srcdir}/agg-2.5-pkgconfig.patch"
  sh ./autogen.sh
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
