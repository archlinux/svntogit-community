# $Id$
# Maintainer: Jonathan Conder <jonno dot conder at gmail dot com>

pkgname=agg
_svnver=93
pkgver=2.4r$_svnver
pkgrel=1
epoch=1
pkgdesc="A High Quality Rendering Engine for C++"
arch=('i686' 'x86_64')
url="http://sourceforge.net/p/agg/"
license=('BSD')
depends=('gcc-libs' 'sdl' 'freetype2')
makedepends=('subversion')
provides=('antigrain')
replaces=('antigrain')
source=("$pkgname::svn://svn.code.sf.net/p/agg/svn#revision=$_svnver"
        autotools.patch)
md5sums=('SKIP'
         'e042231955a8daee5f3cdfa4ee4e866a')

prepare() {
  cd $pkgname/$pkgname-2.4
  patch -p0 -i "${srcdir}/autotools.patch"
}

build() {
  cd $pkgname/$pkgname-2.4
  sh ./autogen.sh
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname/$pkgname-2.4
  make DESTDIR="${pkgdir}" install

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  sed -n 5,11p README > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
