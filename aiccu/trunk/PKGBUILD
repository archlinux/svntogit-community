# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=aiccu
pkgver=20070115
pkgrel=6
pkgdesc='SixXS Automatic IPv6 Connectivity Client Utility'
arch=('i686' 'x86_64')
url='http://www.sixxs.net/tools/aiccu/'
license=('custom')
depends=('gnutls' 'iproute2')
backup=('etc/aiccu.conf')
source=("http://www.sixxs.net/archive/sixxs/aiccu/unix/${pkgname}_${pkgver}.tar.gz"
        'aiccu.service'
        '02-allow-tunnels.patch'
        '03-no-quiet-gcc.patch'
        '04-skip-strip.patch'
        '05-spelling-error.patch'
        '06-setup-script.patch'
        'gnutls-3.4.0.patch')
md5sums=('c9bcc83644ed788e22a7c3f3d4021350'
         '8b76ec84d494f3752b54c3aebcf53c6a'
         'b9b2c0e7186f3f96366caaa39252dccc'
         'b38db1d95760cd9687330b7db5f4ea1d'
         '6dfa2df27bb4859c7511bfea91337925'
         '21a37c376ebfcf787c0e7ee8552053ac'
         '98e73756609f4e09c45c4e5139fd5aed'
         '592de37319b62e6a85129735a5dab564')

prepare() {
  cd $pkgname
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
}

build() {
  cd $pkgname
  [[ $LDFLAGS ]] && LDFLAGS="$LDFLAGS,--no-as-needed"
  make
}

package() {
  install -Dm644 aiccu.service "$pkgdir/usr/lib/systemd/system/aiccu.service"
  cd $pkgname
  install -Dm755 unix-console/aiccu "$pkgdir/usr/bin/aiccu"
  install -Dm640 doc/aiccu.conf "$pkgdir/etc/aiccu.conf"
  install -Dm644 doc/aiccu.1 "$pkgdir/usr/share/man/man1/aiccu.1"
  install -Dm644 doc/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 doc/{README,HOWTO,changelog} "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
