# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.copm>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us> 
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=aide
pkgver=0.15.1
pkgrel=4
pkgdesc='A file integrity checker and intrusion detection program.'
arch=('i686' 'x86_64')
url="http://aide.sourceforge.net/"
license=('GPL')
depends=('mhash' 'elfutils')
backup=('etc/aide.conf')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz{,.asc} \
        aide.conf)
md5sums=('d0b72535ff68b93a648e4d08b0ed7f07'
         'SKIP'
         'd3ac69ad7c12c1686f8accf2717139f6')
validpgpkeys=('2BBBD30FAAB29B3253BCFBA6F6947DAB68E7B931') # Hannes von Haugwitz <hannes@vonhaugwitz.com>

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --with-mhash \
        --with-posix-acl \
        --with-prelink \
        --with-xattr \
        --with-zlib \
        --with-e2fsattrs \
        --disable-static
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m644 $srcdir/aide.conf $pkgdir/etc/aide.conf
    mkdir -p $pkgdir/var/{log,lib}/aide/
}
