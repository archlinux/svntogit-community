# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=alembic
pkgver=1.7.1
pkgrel=3
pkgdesc="A open framework for storing and sharing scene data"
arch=('i686' 'x86_64')
url="http://www.alembic.io/"
license=('BSD3')
depends=('openexr' 'boost-libs' 'hdf5' 'zlib')
makedepends=('cmake' 'boost')
source=(https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('89e30b681a76eaf79b20ebeff62c495971b0eb64b28f249a14bbcf3bdb40df7eda93b0ede299dd5511bd4587a2cc2d4ebd851fb89bf999fdccc31fee3cffbba2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=OFF
    # Temporarily disable HDF5 until upstream fixes the linking or until we make a patch.

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
