# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.0.5
pkgrel=2
pkgdesc='Lexical analyser generator for Haskell'
arch=('x86_64' 'i686')
url='http://hackage.haskell.org/package/alex'
license=('custom:BSD3')
depends=('gmp')
makedepends=('ghc=7.6.3-1' 'haskell-quickcheck>=2')
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c3326417a0538e355e5ad1871ab931b9c8145391e8b2ff79668eb643d9b99659')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  runghc Setup.lhs configure --prefix=/usr --datasubdir="$pkgname"
  runghc Setup.lhs build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  runghc Setup.lhs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"
  # doc only contains the license
  rm -rf $pkgdir/usr/share/doc
}

# vim:set ts=2 sw=2 et:
