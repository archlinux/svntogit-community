# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.1.7
pkgrel=1
pkgdesc='Lexical analyser generator for Haskell'
arch=('x86_64' 'i686')
url='http://hackage.haskell.org/package/alex'
license=('custom:BSD3')
depends=('gmp' 'libffi')
makedepends=('ghc=7.10.3' 'haskell-quickcheck' 'happy' 'libffi')
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('89a1a13da6ccbeb006488d9574382e891cf7c0567752b330cc8616d748bf28d1')

build() {
  cd "$pkgname-$pkgver"

  runghc Setup.lhs configure --prefix=/usr --datasubdir="$pkgname"
  runghc Setup.lhs build
}

package() {
  cd "$pkgname-$pkgver"

  runghc Setup.lhs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"
  # Doc only contains the license
  rm -rf $pkgdir/usr/share/doc
}

# vim:set ts=2 sw=2 et:
