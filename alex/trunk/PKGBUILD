# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.2.3
pkgrel=5
pkgdesc='Lexical analyser generator for Haskell'
arch=('x86_64')
url='https://hackage.haskell.org/package/alex'
license=('custom:BSD3')
depends=('ghc-libs' 'haskell-quickcheck')
makedepends=('happy' 'ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('193c68b1cc21b14a1db81dfdcb1ed71d508a94ac94df1762f140768e00b316aee9f3172357fead302a150248001a95c3cb80622b12423a92a6d50e10541799d7')

build() {
  cd "$pkgname-$pkgver"

  runghc Setup.lhs configure --prefix=/usr --datasubdir="$pkgname" --enable-executable-dynamic
  runghc Setup.lhs build
}

package() {
  cd "$pkgname-$pkgver"

  runghc Setup.lhs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"

  # Doc only contains the license
  rm -rf "$pkgdir/usr/share/doc"
}

# vim:set ts=2 sw=2 et:
