# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Aetf <aetf at unlimitedcodeworks dot xyz>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Peng Zhang <pczhang@gmail.com>

pkgname=alglib
pkgver=3.15.0
pkgrel=2
pkgdesc="A cross-platform numerical analysis and data processing library - Free Version"
arch=('x86_64')
url="http://www.alglib.net"
license=('GPL2')
makedepends=('cmake')
source=(http://www.alglib.net/translator/re/alglib-${pkgver}.cpp.gpl.tgz)
sha256sums=('55435decad45f4b5acdc9651f40c6e9f99a4f148c983e0e4370c9f23bf3329f9')

prepare() {
    cd "${srcdir}"/cpp

# generate a simple cmake file to build the library since the release
# doesn't provide any build system
    
    cat > CMakeLists.txt <<END
cmake_minimum_required(VERSION 3.0)
project(alglib VERSION ${pkgver})

file(GLOB SRCS src/*.cpp)
file(GLOB HEADERS src/*.h)

add_library(alglib_static STATIC \${SRCS})
set_target_properties(alglib_static PROPERTIES
    OUTPUT_NAME alglib
    PUBLIC_HEADER "\${HEADERS}"
)

add_library(alglib SHARED \${SRCS})
set_target_properties(alglib PROPERTIES
    VERSION "\${PROJECT_VERSION}"
    SOVERSION "\${PROJECT_VERSION_MAJOR}"
    PUBLIC_HEADER "\${HEADERS}"
)

include(GNUInstallDirs)
install(TARGETS alglib alglib_static
    LIBRARY DESTINATION "\${CMAKE_INSTALL_LIBDIR}"
    ARCHIVE DESTINATION "\${CMAKE_INSTALL_LIBDIR}"
    PUBLIC_HEADER DESTINATION "\${CMAKE_INSTALL_INCLUDEDIR}/libalglib"
)
install(FILES manual.cpp.html DESTINATION "\${CMAKE_INSTALL_DOCDIR}")
END

    mkdir -p build
}

build() {
    cd "${srcdir}"/cpp/build

    cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib ..
    make VERBOSE=1
}

package() {
    cd "${srcdir}"/cpp/build
    
    make DESTDIR="${pkgdir}" install
}
