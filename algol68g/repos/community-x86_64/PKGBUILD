# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: karolina.lindqvist@kramnet.se

pkgname=algol68g
pkgver=2.8
pkgrel=1
pkgdesc='Algol 68 Genie, an Algol 68 compiler-interpreter'
arch=('x86_64' 'i686')
url='http://www.xs4all.nl/~jmvdveer/algol.html'
license=('GPL')
depends=('gsl' 'plotutils')
optdepends=('postgresql-libs: for postgresql support')
source=("http://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
      	'plotutils.patch'
      	'http://www.xs4all.nl/~jmvdveer/a68gdoc.pdf')
sha256sums=('0c6fc7213f5a00f978faef2d09622d2c3ee01c87594dd6f5c51cd532b719861a'
            '50afcc7812d117e5f0a2f5240c9cd58f8bfcc04b3da0634b9aa0ab06183ed0aa'
            '74e2cf28074a7588608209de1a38faec7649e4f8d341ea914ea613ce167aed19')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../plotutils.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" docdir="/usr/share/doc/$pkgname" install
  install "$srcdir/a68gdoc.pdf" "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
