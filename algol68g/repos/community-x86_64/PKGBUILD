# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: <karolina.lindqvist@kramnet.se>

pkgname=(algol68g algol68g-doc)
pkgver=3.0.7
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(x86_64)
url='https://jmvdveer.home.xs4all.nl/algol.html'
license=(GPL)
depends=(gsl plotutils)
optdepends=('postgresql-libs: for postgresql support')
# This URL was removed:
source=("https://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
        'https://ftp.tw.freebsd.org/distfiles/a68g-doc.pdf')
b2sums=('e9c1185d709b94a701797a0224b6b2e106d0fcb66e94a34ea8d482b8ccfc86c6134c30a0311d6630b23c4d4e219950fdac0c4c2e200f4dab3aef279ecd850470'
        'f8ca710d9a4f611873de41785e70359bd566645268ebb848803d3cd2e9a9f873f8ac423e3e08699998083444b7f6d98df32fdb7a4c6b36692462ee50ddb1e410')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver \
    DESTDIR="$pkgdir" docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 a68g-doc.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
