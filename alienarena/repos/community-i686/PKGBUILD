# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: godane <slaxemulator@gmail.com>
# Contributor: damian01w <damian01w@gmail.com>

pkgname=alienarena
pkgver=7.50
pkgrel=2
_verdate=20101214
pkgdesc="Multiplayer retro sci-fi deathmatch game"
arch=('i686' 'x86_64')
url='http://icculus.org/alienarena/rpa/'
license=('GPL')
depends=('curl' 'freetype2' 'libgl' 'libjpeg' 'libvorbis' 'libxxf86dga' 'libxxf86vm' 'mesa' 'ode' 'sdl' 'openal' 'alienarena-data')
source=(http://icculus.org/alienarena/Files/alienarena-7_50-linux${_verdate}.tar.gz
	'alienarena.desktop')

md5sums=('40e44cb49f5bc6ff6c8abcead897b29a' 
         'f9b50e27e8615a6010b4cc91e9eb010f')
 
build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mv -f "$pkgdir/usr/bin/crx" "$pkgdir/usr/bin/$pkgname"
  mv -f "$pkgdir/usr/bin/crx-ded" "$pkgdir/usr/bin/$pkgname-ded"
  install -D -m644 "$srcdir/alienarena.desktop" "${pkgdir}/usr/share/applications/alienarena.desktop"
  rm -rf "$pkgdir/usr/share/$pkgname"
}
