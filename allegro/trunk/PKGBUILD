# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.2.1.0
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64' 'i686')
url='http://liballeg.org/'
license=('custom')
makedepends=('cmake' 'mesa-libgl' 'glu' 'ninja' 'libtheora' 'git')
depends=('jack' 'libxpm' 'libxxf86dga' 'libgl' 'physfs' 'gtk2' 'libpulse' 'libtheora')
source=("git://github.com/liballeg/${pkgname}${pkgver%%.*}.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build
  cmake "../${pkgname}${pkgver%%.*}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS=OFF \
    -DWANT_PHYSFS=ON \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "${pkgname}${pkgver%%.*}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
