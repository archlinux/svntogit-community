# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro4
pkgver=4.4.2
pkgrel=6
pkgdesc='Portable library mainly aimed at video game and multimedia programming (legacy version)'
arch=('x86_64')
url='http://liballeg.org/'
license=('custom')
depends=('jack' 'libxcursor' 'libxpm' 'libxxf86dga' 'libxxf86vm')
makedepends=('cmake' 'glu' 'libpng' 'mesa' 'ninja')
options=('staticlibs')
source=("https://github.com/liballeg/allegro5/releases/download/$pkgver/allegro-$pkgver.tar.gz"
        'LICENSE')
sha256sums=('1b21e7577dbfada02d85ca4510bd22fedaa6ce76fde7f4838c7c1276eb840fdc'
            '76b9cd52eab572495e8b3b64313d8e40c0359135491e981321d15c3e707ba41a')

build() {
  mkdir -p build && cd build
  cmake "../allegro-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS=OFF \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "allegro-$pkgver/misc/allegro.m4" "$pkgdir/usr/share/aclocal/allegro.m4"
}

# vim: ts=2 sw=2 et:
