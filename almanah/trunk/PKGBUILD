# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=almanah
pkgver=0.11.1
pkgrel=6
pkgdesc="Small GTK+ application to allow you to keep a diary of your life"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Almanah_Diary"
license=('GPL')
depends=('evolution' 'libcryptui')
makedepends=('intltool')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('00a66b31763fecea7d45a6127494ec997df5d35c8002867be8432f84a7f70ebc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
