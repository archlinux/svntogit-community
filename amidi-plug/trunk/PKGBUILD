# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: yosh64 <yosh64.at.gmail.dot.com>

pkgname=amidi-plug
pkgver=0.7
pkgrel=6
pkgdesc="A MIDI input plugin for XMMS"
arch=('i686' 'x86_64')
url="http://www.develia.org/projects.php?l=2&f=1&p=amidiplug"
license=('GPL2')
depends=('xmms' 'fluidsynth')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://www.develia.org/files/$pkgname-$pkgver.tar.bz2)
md5sums=('bb128c40dce0ab4afda79da01f985a09')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --with-player=xmms
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
