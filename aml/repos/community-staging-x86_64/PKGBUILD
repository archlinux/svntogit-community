# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=aml
pkgver=0.2.1
pkgrel=2
pkgdesc='Another Main Loop'
arch=(x86_64)
url=https://github.com/any1/aml
license=(custom:ISC)
depends=(glibc)
makedepends=(
  git
  meson
  ninja
)
provides=(libaml.so)
_tag=08b6b18d1935fcc2567368c16d8a31d1d2b17aa8
source=(git+https://github.com/any1/aml.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd aml
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson aml build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 aml/COPYING -t "${pkgdir}"/usr/share/licenses/aml
}

# vim: ts=2 sw=2 et:
