# $Id$
# Maintainer : David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=ams
pkgver=2.1.2
pkgrel=2
pkgdesc="A real-time modular synthesizer and effect processor"
arch=('x86_64')
url="http://alsamodular.sourceforge.net/"
license=('GPL2')
depends=('qt4' 'fftw' 'jack' 'ladspa' 'liblo' 'zita-alsa-pcmi')
optdepends=('amb-plugins: ambisonic plugins'
            'mcp-plugins: phaser, chorus and moog vcf plugins'
            'rev-plugins: zita-rev1 based reverb plugins'
            'swh-plugins: Steve Harris plugins'
            'vco-plugins: oscillator plugins'
            'fil-plugins: equaliser plugins'
            'tap-plugins: toms audio plugins'
            'wah-plugins: wah audio plugins'
            'g2reverb: greverb based plugins'
            'cmt: Computer Music Toolkit plugins'
            'blop: bandlimited oscillator plugins'
            'pvoc: phase-vocoding plugins'
            'caps: the C* audio plugins')
source=("https://downloads.sourceforge.net/project/alsamodular/alsamodular/$pkgver/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop" "$pkgname.png")
sha512sums=('3a98ccb54505818b4add1b070e2230ba00d80732fdcc54caecba55ad83f352381ed165ab58ca01fc98550bd0691242fd2f733862ee77fd217141ecec52250e4e'
            'e18395622d20b7733609c5aa1b5cde25fbc0b75175d8a68449edb97f381461ee32f955f5ac888c31d89ac87e77401be0df9e83f260dae6118815cc2bab0b44d9'
            '991a68d1baa0523d9abddd03cea0532b8f9d5b5e81df62fe4b5501fdfc474ada8c811e8821fadcb82246f1b3d4707e2f4532dd226fd46495fc198f72780caa4e')

prepare() {
  cd "${pkgname}-${pkgver}"

#  # strip unwanted optimization flags
#  sed -i '/AMS_OPT_CXXFLAGS=/d' configure
#
  # fix libs concat
  sed -e 's/LIBS+=\$LIBSsave/LIBS="$LIBS $LIBSsave"/' \
      -e 's/CFLAGS+=\$CFLAGSsave/CFLAGS="$CFLAGS $CFLAGSsave"/' \
      -i configure

  export CXXFLAGS+=" -std=gnu++98"
  ./configure --prefix=/usr
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install

  # desktop file and icon
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "${srcdir}/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # documentation
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -Dm644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"

}
