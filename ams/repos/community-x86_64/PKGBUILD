# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

_name=alsamodular
pkgname=ams
pkgver=2.0.1
pkgrel=1
pkgdesc="A realtime modular synthesizer and effect processor"
arch=('i686' 'x86_64')
url="http://alsamodular.sourceforge.net/"
license=('GPL')
depends=('qt4' 'clalsadrv' 'ladspa' 'jack')
makedepends=('fftw')
optdepends=('amb-plugins: ambisonic plugins'
            'mcp-plugins: phaser, chorus and moog vcf plugins'
            'rev-plugins: reverb plugins'
            'swh-plugins: Steve Harris plugins'
            'vco-plugins: oscillator plugins'
            'fil-plugins: equaliser plugins'
            'tap-plugins: toms audio plugins'
            'wah-plugins: wah audio plugins'
            'cmt: Computer Music Toolkit plugins'
            'blop: bandlimited oscillator plugins'
            'pvoc: phase-vocoding plugins'
            'caps: the C* audio plugins')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/project/$_name/$_name/$pkgver/$pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop" "$pkgname.png")
md5sums=('0d41bd5aac066aa98be45fd7ab12d35f'
         'ffa277cffd52254f0297cbc2f200767e'
         '0349171d5431f1c6e56085f080eb8c68')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # strip unwanted flags
  sed -i 's/-m\(64\|arch=[a-z1-9]*\) *//' configure

  # DSO link flag
  export LIBS=" -ldl"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # desktop file
  install -Dm644 ../$pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # icon
  install -Dm644 ../$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
