# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=amtk
pkgver=5.4.0
pkgrel=1
pkgdesc="Actions, Menus and Toolbars Kit for GTK applications"
url="https://gitlab.gnome.org/swilmet/amtk"
arch=(x86_64)
license=(LGPL3)
depends=(gtk3)
makedepends=(meson git gobject-introspection gtk-doc)
options=(debug)
_commit=9feb1a70cf06a6fe837c27f4ddcdd492dcb4fe9b  # tags/5.4.0^0
source=("git+https://gitlab.gnome.org/swilmet/amtk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd amtk
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd amtk
}

build() {
  arch-meson amtk build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libgtk-3.so)
  provides+=("libamtk-${pkgver%%.*}.so")

  meson install -C build --destdir "$pkgdir"
}
