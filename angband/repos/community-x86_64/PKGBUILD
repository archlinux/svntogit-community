# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Adrian Stratulat <adrian.stratulat at inboxcom>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: KillaB <xGenoBlast@gmail.com>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Christian Schmidt <xmucknertx@googlemail.com>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=angband
pkgver=4.2.4
pkgrel=1
pkgdesc="A roguelike dungeon exploration game based on the writings of JRR Tolkien"
arch=('x86_64')
url="https://rephial.org/"
license=('GPL2' 'custom')
depends=('sdl2' 'sdl2_ttf' 'sdl2_image' 'sdl2_mixer' 'ncurses' 'xorg-fonts-misc')
makedepends=('python-docutils')
source=("$pkgname-$pkgver.tar.gz::https://github.com/angband/angband/archive/$pkgver.tar.gz")
sha512sums=('5b47cfa95f779aa3f1e89f02f203b8eafd2dc1376b7aab1b62e37122fb83640d1de25f39817863a74c246d7de35bb3f26868f5f5e8906e92788a6dd74a85a4c5')
b2sums=('97c19f23aff3aa1e9aedcbe33da0e9806f923c251e80dde54492d62dc5d54c58943483ea3b0da1913043deef9d3d52f89bdc3dced0870142f06c6ab6edc33615')

build() {
  cd "$pkgname-$pkgver"

  autoreconf -vfi

  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --sysconfdir=/usr/share/angband \
    --with-configpath=/usr/share/angband \
    --with-libpath=/usr/share/angband \
    --enable-sdl2 \
    --enable-sdl2-mixer

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -vDm644 docs/copying.rst "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
