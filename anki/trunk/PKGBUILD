# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

pkgname=anki
pkgver=2.1.0beta3
pkgrel=2
pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently"
url="https://ankisrs.net/"
license=('AGPL3')
arch=('any')
depends=('qt5-webengine' 'python-beautifulsoup4' 'python-pyaudio' 'python-pyqt5' 'python-requests' 'python-send2trash')
optdepends=('lame: record sound'
            'mplayer: play sound')
source=(https://apps.ankiweb.net/downloads/beta/anki-$pkgver-source.tgz)
sha256sums=('0ff3e4b65693fe0498e3192eaa206961b8e7d271c51b8db261479a822862cec5')

package() {
  cd "$srcdir"/anki-$pkgver
  mkdir -p "$pkgdir"/usr/{bin,share/{anki,pixmaps,applications,man/man1}}
  cp -av * "$pkgdir"/usr/share/anki/
  cd "$pkgdir"/usr/share/anki && (
    mv tools/runanki.system ../../bin/anki
    mv anki.xpm anki.png ../pixmaps/
    mv anki.desktop ../applications/
    mv anki.1 ../man/man1/
  )
  rm -rf "$pkgdir"/usr/share/anki/{tests,tools,anki.bat}
}
