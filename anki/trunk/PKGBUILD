# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Dave Pretty <david dot pretty at gmail dot com>

pkgname=anki
pkgver=2.1.0beta29
pkgrel=2

pkgdesc="Helps you remember facts (like words/phrases in a foreign language) efficiently"
url="https://ankisrs.net/"
arch=('any')
license=('AGPL3')

depends=('qt5-webengine' 'python-beautifulsoup4' 'python-pyaudio'
         'python-pyqt5' 'python-requests' 'python-send2trash'
         'python-decorator' 'python-markdown')
optdepends=('lame: record sound'
            'mplayer: play sound')

source=(https://apps.ankiweb.net/downloads/beta/anki-$pkgver-source.tgz
        0001-Revert-refuse-to-run-on-qt-5.10.patch)
sha256sums=('7b412ff40a7fc347a85a5ca6c234c5aa4d3919d1ca9dc1729095abdc4e8ca6dd'
            '9f10126ffc9e9a3552bee20b750d6a43ec631280ad0838c9f7d2b188998c5ac1')

prepare() {
  cd anki-$pkgver

  patch -p1 <"$srcdir"/0001-Revert-refuse-to-run-on-qt-5.10.patch
}

package() {
  cd anki-$pkgver
  mkdir -p "$pkgdir"/usr/{bin,share/{anki,pixmaps,applications,man/man1}}
  cp -av * "$pkgdir"/usr/share/anki/
  cd "$pkgdir"/usr/share/anki
  mv tools/runanki.system "$pkgdir"/usr/bin/anki
  mv anki.xpm anki.png "$pkgdir"/usr/share/pixmaps
  mv anki.desktop "$pkgdir"/usr/share/applications
  mv anki.1 "$pkgdir"/usr/share/man/man1
  rm -rf "$pkgdir"/usr/share/anki/{tests,tools,anki.bat}
}
