# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible-core
pkgver=2.11.0
pkgrel=3
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://pypi.org/project/ansible-core'
license=('GPL3')
depends=('python' 'python-yaml' 'python-paramiko' 'python-jinja' 'python-resolvelib')
provides=('python-ansible' 'ansible-base')
replaces=('ansible-base')
optdepends=('sshpass: for ssh connections with password'
            'python-passlib: crypt values for vars_prompt'
            'python-netaddr: for the ipaddr filter'
            'python-systemd: log to journal'
            'python-pywinrm: connect to Windows machines'
            'python-dnspython: for dig lookup'
            'python-jmespath: json_query support')
makedepends=('python-setuptools')
backup=('etc/ansible/ansible.cfg')
source=("https://pypi.python.org/packages/source/a/ansible-core/ansible-core-${pkgver}.tar.gz"
        "https://github.com/ansible/ansible/pull/74562.patch")
sha512sums=('292be62bd4b0314d61eb7471f60f450be216c729a034f44f967ef827486b56ef1850de26928508f214ab430ea1584fc174cc4e8833f68d6e4de845e8565fd46a'
            'fd3f87def5e7f5dc2468845c7f78161da35b9a6dcc8c45f8d79764697376dc1b27edac9318491401fd1b115b2e225bffc4d8eeca76feab1f33404823d13b1394')

prepare() {
  cd ansible-core-${pkgver}

  patch -Np1 -i "$srcdir"/74562.patch
}

build() {
  cd ansible-core-${pkgver}
  python setup.py build
}

package() {
  cd ansible-core-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING

  install -d "${pkgdir}"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "${pkgdir}"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "${pkgdir}"/etc/ansible/ansible.cfg

  install -d "${pkgdir}"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "${pkgdir}"/usr/share/man/man1
}
