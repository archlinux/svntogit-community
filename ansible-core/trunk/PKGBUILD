# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible-core
pkgver=2.14.0
pkgrel=2
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://pypi.org/project/ansible-core'
license=('GPL3')
depends=('python' 'python-jinja' 'python-packaging' 'python-paramiko' 'python-pyyaml' 'python-resolvelib' 'python-typing-extensions')
optdepends=(
  'python-dnspython: for dig lookup'
  'python-jmespath: json_query support'
  'python-netaddr: for the ipaddr filter'
  'python-passlib: crypt values for vars_prompt'
  'python-pip: for module to manage Python libarary dependencies'
  'python-pywinrm: connect to Windows machines'
  'python-setuptools: for module to manage Python libarary dependencies'
  'python-systemd: log to journal'
  'sshpass: for ssh connections with password'
)
makedepends=('python-build' 'python-docutils' 'python-installer' 'python-setuptools' 'python-straight.plugin' 'python-wheel')
provides=('python-ansible' 'ansible-base')
replaces=('ansible-base')
backup=('etc/ansible/ansible.cfg')
source=("https://pypi.python.org/packages/source/a/ansible-core/ansible-core-${pkgver}.tar.gz")
sha512sums=('09e5b45608f7e1f341100dd89a6277a71a5ff0a45244cd6862a10d0f35a993c3cf649a5f9936ac726f7e903269f98a975804418516e602b8dd983bfca23c66cb')
b2sums=('7b68d72c292d8f3e8fb2d0d66fce0eca1de1b74ec1dc3ecaa2c4b37177d693567272260b3e98c9d81f7ff5e5cc4972dfa1f24c9f5e0119bb1da528388081d5fb')

prepare() {
  cd $pkgname-$pkgver
  # remove the resolvelib upper boundary (at least for < 0.9.0): https://github.com/ansible/ansible/pull/79399
  sed -e 's/resolvelib.*/resolvelib/' -i requirements.txt
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation

  make docs
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 COPYING -t "${pkgdir}"/usr/share/doc/${pkgname}/
  install -Dm644 examples/ansible.cfg -t "${pkgdir}"/etc/ansible/
  install -Dm644 examples/{ansible.cfg,hosts} -t "${pkgdir}"/usr/share/ansible/doc/examples/
  install -Dm644 docs/man/man1/*.1 -t "${pkgdir}"/usr/share/man/man1/
}
