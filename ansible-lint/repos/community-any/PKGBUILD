# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=6.3.0
pkgrel=1
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/ansible/ansible-lint"
license=('GPL3')
depends=('python' 'ansible-core' 'python-ruamel-yaml' 'python-pyaml' 'python-rich' 'python-packaging'
          'python-wcmatch' 'python-enrich' 'python-ansible-compat' 'python-jsonschema' 'yamllint')
makedepends=('python-build' 'python-installer' 'python-wheel')
optdepends=('ansible: check official ansible collections')
source=(https://github.com/ansible/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        disable_use_scm_version.patch)
b2sums=('3243e593a73762d4e47b670c1d9181f156d01cf58a6ebf0116ad38241bbd67a72e26dd9c0064af070e745c40eb49ca318ed4b1090c68821fbfbdaa842f1e8a53'
        'cf911567b040c8938ca589fde3c93b39cfba29ca1a373b8af354450a4f880d475463b0c81440fb9bfacc7b1f93736a5ae5ab58c7fdb30d8f03d1996754a738f3')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop setuptools from using the scm version which doesn't exist in this context
  patch -Np1 < "${srcdir}"/disable_use_scm_version.patch
  sed -i "/^\[metadata\]/a version = ${pkgver}" setup.cfg
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
