# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=6.8.2
pkgrel=2
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/ansible/ansible-lint"
license=('GPL3')
depends=(python ansible-core yamllint 
  python-{ansible-compat,black,enrich,filelock,jsonschema,pyaml,packaging,rich,ruamel-yaml,wcmatch})
makedepends=(python-{build,installer,setuptools,wheel})
optdepends=('ansible: check official ansible collections')
source=(https://github.com/ansible/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        disable_use_scm_version.patch)
b2sums=('4ae9c344d834a3d69a05689df296c7915e6eb7cb2a85b773f3adcc4efd898c3e1c9f32d8b1efda31afb7a5466d1966aa754cd5529c7dcfd6c1f45517d39e1750'
        'cf911567b040c8938ca589fde3c93b39cfba29ca1a373b8af354450a4f880d475463b0c81440fb9bfacc7b1f93736a5ae5ab58c7fdb30d8f03d1996754a738f3')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop setuptools from using the scm version which doesn't exist in this context
  patch -Np1 < "${srcdir}"/disable_use_scm_version.patch
  sed -i "/^\[metadata\]/a version = ${pkgver}" setup.cfg
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
