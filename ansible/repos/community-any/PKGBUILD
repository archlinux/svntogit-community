# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=ansible
pkgver=3.4.0
pkgrel=2
pkgdesc='Official assortment of Ansible collections'
arch=('any')
url='https://pypi.org/project/ansible/'
license=('GPL3')
depends=('python' 'ansible-core')
provides=('python-ansible_collections')
optdepends=('python-pyopenssl: openssl modules'
            'python-dnspython: for dig lookup'
            'python-ovirt-engine-sdk: ovirt support'
            'python-boto3: aws_s3 module'
            'python-ldap: ldap support'
            'python-proxmoxer: proxmos modules'
            'python-openstacksdk: OpenStack module'
            'acme-tiny: openssl_certificate module')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/a/ansible/ansible-${pkgver}.tar.gz")
sha512sums=('0316c6bde7f6be80f7a5855cc711989483b202fd434a5aca9e23045f26671e9b9927a1100ef7b29098a533dd02704ade906dd5b17d2c870f727aa51c5c5ba5a7')

build() {
  cd ansible-${pkgver}
  export PYTHONHASHSEED=0
  python setup.py build
}

package() {
  cd ansible-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING
}
