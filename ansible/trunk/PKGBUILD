# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=ansible
pkgver=3.3.0
pkgrel=1
pkgdesc='Official assortment of Ansible collections'
arch=('any')
url='https://pypi.org/project/ansible/'
license=('GPL3')
depends=('python' 'ansible-base')
provides=('python-ansible_collections')
optdepends=('python-pyopenssl: openssl modules'
            'python-dnspython: for dig lookup'
            'python-ovirt-engine-sdk: ovirt support'
            'python-boto3: aws_s3 module'
            'python-ldap: ldap support'
            'python-proxmoxer: proxmos modules'
            'python-openstacksdk: OpenStack module'
            'acme-tiny: openssl_certificate module')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/a/ansible/ansible-${pkgver}.tar.gz")
sha512sums=('3cba3c9a373f0f71ca0182294f7ec2d438f6767b665a5e7a3741962d3eb0fefb4212b3a63570d6e51f28e1b780422c135e8295c853a38f6ea7e5d40023b65041')

build() {
  cd ansible-${pkgver}
  python setup.py build
}

package() {
  cd ansible-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING
}
