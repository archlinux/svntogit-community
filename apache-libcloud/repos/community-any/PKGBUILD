# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgbase=apache-libcloud
pkgname=('python-apache-libcloud' 'python2-apache-libcloud')
pkgver=2.3.0
pkgrel=2
pkgdesc="Apache Libcloud is a standard Python library that abstracts away differences among multiple cloud provider APIs"
arch=('any')
url="https://libcloud.apache.org"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
checkdepends=('python-mock' 'python2-mock' 'python-pytest-runner' 'python2-pytest-runner' 'openssh'
              'python-requests-mock' 'python2-requests-mock')
source=("https://pypi.io/packages/source/a/apache-libcloud/apache-libcloud-${pkgver}.tar.gz"
        py3.7.diff)
sha512sums=('3c77b0261a9386af7b215f354126d0f3189209eeab3cac8e02eabddcb36ccc979159e148854bf0118ba2d8b9fa5bf57f493b144b860b1809c9aeff9efb0161a5'
            '080da5aab7ca75c0041dab927526921cb4ebcbb9fdd4a8bf35baca214a378f962c3a9ba7ea55b262ffb4ff2d02b84fddee3483cdaa7c869d386cc24e2834cd71')

prepare() {
  patch -d apache-libcloud-$pkgver -p1 < py3.7.diff
  cp -a apache-libcloud-$pkgver{,-py2}
}

build() {
  cd "$srcdir/apache-libcloud-$pkgver"
  python setup.py build

  cd "$srcdir/apache-libcloud-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/apache-libcloud-$pkgver"
  cp libcloud/test/secrets.py-dist libcloud/test/secrets.py
  python setup.py test
  rm libcloud/test/secrets.py

  cd "$srcdir/apache-libcloud-$pkgver-py2"
  cp libcloud/test/secrets.py-dist libcloud/test/secrets.py
  python2 setup.py test
  rm libcloud/test/secrets.py
}

package_python-apache-libcloud() {
  depends=('python-requests')

  cd apache-libcloud-$pkgver
  python setup.py install --root="$pkgdir"
}

package_python2-apache-libcloud() {
  depends=('python2-requests')
  replaces=('apache-libcloud')
  conflicts=('apache-libcloud')

  cd apache-libcloud-$pkgver-py2
  python2 setup.py install --root="$pkgdir"
}
