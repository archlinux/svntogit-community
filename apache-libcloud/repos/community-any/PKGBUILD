# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgbase=apache-libcloud
pkgname=(python{,2}-$pkgbase)
pkgver=0.18.0
pkgrel=1
pkgdesc="Apache Libcloud is a standard Python library that abstracts away differences among multiple cloud provider APIs"
arch=('any')
url="https://libcloud.apache.org"
license=('apache')
makedepends=('python2-setuptools' 'python-setuptools')
optdepends=()
source=(http://www.mirrorservice.org/sites/ftp.apache.org/libcloud/$pkgbase-$pkgver.tar.bz2)
sha256sums=('af87e9ccd5577f7a7cf8b0f642889c40738561eb7ab2f23039c0d3d648b80d2a')

prepare() {
    cp -a "$srcdir/$pkgbase-$pkgver" "$srcdir/${pkgbase}2-$pkgver"
}

package_python2-apache-libcloud() {
  depends=('python2')
  replaces=(apache-libcloud)
  conflicts=(apache-libcloud)
  cd "$srcdir/${pkgbase}2-$pkgver"
  python2 setup.py install --root="$pkgdir"
}

package_python-apache-libcloud() {
  depends=('python')
  replaces=(apache-libcloud)
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir"
}
