# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=apache-orc
pkgver=1.6.7
pkgrel=4
pkgdesc="Columnar storage for Hadoop workloads."
arch=(x86_64)
url="https://orc.apache.org"
license=(Apache)
depends=(lz4 protobuf snappy zlib zstd)
makedepends=(cmake)
checkdepends=(gtest)
options=(staticlibs)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/apache/orc/archive/rel/release-$pkgver.tar.gz
        ${pkgname}-dynamic-linking.patch::https://github.com/apache/orc/commit/ca007d57cb6b399671bc71328f29d040032575df.patch)
sha256sums=('abdffe48b8d2e7776c3b541ee2241401e49774941ca4a8c759e5d795daec8a45'
            'b79ff208beecaa52cf0e5f7f3bab2ce637ea642cb087e1d9c72384d21dbf13a1')

prepare(){
  cd orc-rel-release-${pkgver}
  patch -Np1 -i ../${pkgname}-dynamic-linking.patch
  sed -i "s|ByteSize|ByteSizeLong|g" c++/test/CreateTestFiles.cc
  sed -i "s|ByteSize|ByteSizeLong|g" c++/test/TestBufferedOutputStream.cc 
}

build(){
  cmake -B build -S orc-rel-release-${pkgver} \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fPIC" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DLZ4_HOME="/usr" \
    -DPROTOBUF_HOME="/usr" \
    -DSNAPPY_HOME="/usr" \
    -DZLIB_HOME="/usr" \
    -DZSTD_HOME="/usr" \
    -DORC_PREFER_STATIC_ZLIB=OFF \
    -DBUILD_LIBHDFSPP=OFF \
    -DBUILD_JAVA=OFF \
    -DINSTALL_VENDORED_LIBS=OFF
  make -C build
}

check(){
  # TestDecompression.testLzoLong is failing
  make -C build test-out || echo "Warning: Tests failed"
}

package(){
  make DESTDIR="${pkgdir}" -C build install
}
