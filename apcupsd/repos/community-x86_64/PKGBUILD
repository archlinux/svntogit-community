# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Todd Musall <tmusall@comcast.net>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=apcupsd
pkgver=3.14.10
pkgrel=4
pkgdesc="Power mangement and controlling most of APC's UPS models"
arch=(i686 x86_64)
url="http://www.apcupsd.org"
license=('GPL')
depends=('gcc-libs')
optdepends=('gd: for CGI'
            'gconf: for frontend'
            'gtk2: for frontend')
makedepends=('pkgconfig' 'gd' 'gconf' 'gtk2' 'systemd-sysvcompat')
backup=(etc/apcupsd/apcupsd.conf etc/apcupsd/hosts.conf
        etc/apcupsd/multimon.conf)
source=(http://downloads.sourceforge.net/apcupsd/$pkgname-$pkgver.tar.gz
        apcupsd.service
        apcupsd-tmpfiles.conf)
sha256sums=('0707b5ec9916fbde9e44eb8d18037c8d8f75dfd6aeef51aba5487e189eef2032'
            'cae38570847f5d90f38a0634e502f35d6c76a9c928b3a322e3fe1a097fbbc04a'
            'fef0b088daa7d652b8c30e6105318cff547ad2504627efabd014083eabc7182d'
            'b00cd1a31de70a172698d486583aa79113b99c1bb4b882ab906d0e37504d7f55')
sha256sums=('0707b5ec9916fbde9e44eb8d18037c8d8f75dfd6aeef51aba5487e189eef2032'
            'fef0b088daa7d652b8c30e6105318cff547ad2504627efabd014083eabc7182d'
            'b00cd1a31de70a172698d486583aa79113b99c1bb4b882ab906d0e37504d7f55')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr  --libexecdir=/usr/lib/$pkgname \
		--enable-cgi --enable-usb --enable-net \
		--with-upstype=usb --with-upscable=usb \
		--with-serial-dev=/dev/usb/hid/hiddev[0-9] \
		--enable-pthreads --enable-gapcmon
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/apcupsd.service" "$pkgdir/usr/lib/systemd/system/apcupsd.service"
	install -Dm644 "$srcdir/apcupsd-tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/apcupsd.conf"
	chmod 755 "$pkgdir"/sbin/*
	install -dm755 "$pkgdir/usr/lib/$pkgname/cgi-bin"
	mv "$pkgdir"/etc/apcupsd/*.cgi "$pkgdir/usr/lib/$pkgname/cgi-bin"
}