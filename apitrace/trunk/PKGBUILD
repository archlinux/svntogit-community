# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace
pkgver=5.0
pkgrel=2
pkgdesc="Graphics API Tracing"
arch=('i686' 'x86_64')
url="https://github.com/apitrace/apitrace"
license=('custom')
makedepends=('cmake' 'mesa' 'libgl' 'python2' 'qtwebkit' 'qjson')
optdepends=('qtwebkit: GUI support' 'qjson: GUI support')
source=("https://github.com/apitrace/apitrace/archive/${pkgver}.zip"
        fix-python-executable.patch)
md5sums=('2a60e8a4c90f841dc95fe659ba26607e')

prepare() {
  cd ${srcdir}/apitrace-*

  patch -Np1 -i ../fix-python-executable.patch
}

build() {
  cd ${srcdir}/apitrace-*

  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DPYTHON_EXECUTABLE='/usr/bin/python2'
  make -C build
}

package() {
  depends=('python2' 'libgl')
  cd ${srcdir}/apitrace-*

  make -C build DESTDIR="${pkgdir}/" install

  install -m755 -d "${pkgdir}/usr/share/licenses/apitrace"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/apitrace/"
}
md5sums=('2a60e8a4c90f841dc95fe659ba26607e'
         '3e96a8057550e86207fa075ca78392b7')
