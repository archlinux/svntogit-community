# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Marcus Hoffmann <bubu@bubu1.eu>

pkgname=apksigcopier
pkgver=1.1.1
pkgrel=2
pkgdesc='copy/extract/patch android apk signatures & compare apks'
url='https://github.com/obfusk/apksigcopier'
arch=('any')
license=('GPL3')
depends=('python' 'python-click')
makedepends=('python-setuptools' 'pandoc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/obfusk/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('9a88e8c39e764a77887e484d40980cdeffaeb3d2e0a1d848c318c485149071e955f8e1d4cbf3578ea45346d3cecd458762a94f16d2cc0b342a6660e0d56efd02')
b2sums=('b66ae97935908179a80e0d4999714aa3a1b4edf7d78ae17422e9d3b79de7050015e339a084bb8560ebdb2e8047a98809d1033d65d3f0c278439f5190b92651e3')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
  make
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 apksigcopier.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim:set ts=2 sw=2 et:
