# $Id$
# Maintainer: BlackIkeEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Zom <zom@eevul.org>

pkgname=apper
pkgver=0.9.1
pkgrel=1
pkgdesc="Get and manage software in KDE"
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Apper?content=84745"
license=('GPL')
depends=('kdebase-workspace' 'packagekit-qt4')
makedepends=('cmake' 'automoc4' 'chrpath')
install="$pkgname.install"
provides=('kpackagekit')
conflicts=('kpackagekit')
replaces=('kpackagekit')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('ef143b663b21185f83876a2a8daaff3402d8ec45629fbec949cfcdc979a99466')

prepare() {
	cd "$pkgname-$pkgver"

	mkdir build
}

build() {
	cd "$pkgname-$pkgver/build"

	cmake ../ \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$pkgname-$pkgver/build"
	make DESTDIR="$pkgdir" install
	# avoid gnome-packagekit conflict (stolen from fedora as suggested)
	mv "$pkgdir/usr/share/dbus-1/services/org.freedesktop.PackageKit.service" \
		"$pkgdir/usr/share/dbus-1/services/kde-org.freedesktop.PackageKit.service"
	# Hack around cmake rpath bug (debian)
	chrpath --list "$pkgdir/usr/bin/apper"
	chrpath --replace "/usr/lib/apper" "$pkgdir/usr/bin/apper"
}
