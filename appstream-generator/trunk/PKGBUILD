# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.3.0
pkgrel=1
pkgdesc="A fast AppStream metadata generator"
arch=(i686 x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream curl libarchive librsvg lmdb)
makedepends=(gdc dub)
source=("https://github.com/ximion/appstream-generator/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "https://github.com/ximion/d-bindings/archive/3a31df3da5977cfabb7cd076e02ab83b525bbf34.zip"
	https://github.com/ximion/appstream-generator/commit/b463e430.patch)
sha256sums=('1cb396d4716a7e1089f865211f7d5fb5125f7f86b0a468326222df663b76c7fc'
            '786997e7c9693657c51591875a94f039976e005f7c12ed33f777de73393a3072'
            '9aed4dbf712a46fbb0ddf5c2dedb0d7b1bce162ebb86b8bfe030a05365d2b34f')

prepare() {
  cd $pkgname-$pkgver
# Fix build
  patch -p1 -i ../b463e430.patch
}

build() {
  cd $pkgname-$pkgver
  dub build
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
