# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.7.6
pkgrel=1
pkgdesc="Provides a standard for creating app stores across distributions"
arch=('i686' 'x86_64')
url="http://distributions.freedesktop.org/wiki/AppStream"
license=('GPL')
depends=('xapian-core' 'polkit' 'libyaml' 'libxml2' 'qt5-base')
makedepends=('cmake' 'xmlto' 'intltool' 'gobject-introspection' 'python2' 'docbook-xsl')
source=("http://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz")
sha256sums=('7b3b907049ae908a3dd240b5ea7f814988d80d8fe44fa93eee4e2d3d4517ce30')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../AppStream-$pkgver -DQT=ON -DAPPSTREAM_QT_VERSION=5 -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package_appstream() {
  cd build

  make DESTDIR="$pkgdir" install
  
# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppstreamQt,lib/cmake,lib/libAppstreamQt.*}
}

package_appstream-qt() {
  pkgdesc=('Qt5 interface for AppStream')
  depends=('appstream')

  cd build/qt
  make DESTDIR="$pkgdir" install
}
