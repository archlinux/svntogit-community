# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=arch-audit
pkgver=0.1.19
pkgrel=1
pkgdesc='A utility like pkg-audit based on Arch Security Team data'
url='https://gitlab.com/ilpianista/arch-audit'
depends=('glibc' 'gcc-libs' 'curl' 'libcurl.so' 'libalpm.so')
makedepends=('cargo' 'pkg-config' 'scdoc')
arch=('x86_64')
license=('MIT')
backup=('etc/arch-audit/settings.toml')
source=("https://gitlab.com/ilpianista/arch-audit/-/archive/${pkgver}/arch-audit-${pkgver}.tar.gz")
sha512sums=('b57facb0ae3488d318a4f2dcd2e86d5b245515f04d3d1627995ec983668a842bf837a7cb1c50515794c7f3900b766930fc7e14b6e7483d0136f1dca9509f4a87')
b2sums=('ffaa520e9ff94c56f113630662d4cae51b77c96c76406d5a2dba2d4b74498a83839a939be328513b9ab3bb2378f7c62483ede990b764d4132558bc690590ff8f')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" PREFIX=/usr install

  install -Dm 644 contrib/hooks/arch-audit.hook -t "${pkgdir}/usr/share/${pkgname}"
  install -Dm 644 contrib/config/settings.toml -t "${pkgdir}/etc/arch-audit"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
