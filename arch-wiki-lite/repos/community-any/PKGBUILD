# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=arch-wiki-lite
pkgver=20130214
_pkgver=20121125
pkgrel=1
pkgdesc="The wiki without html.  1/9 as big, easily searched and viewable on console."
arch=('any')
url="http://kmkeen.com/arch-wiki-lite/"
_watch="http://www.archlinux.org/packages/community/any/arch-wiki-docs/"
license=('GPL')
depends=('bash' 'gzip')
makedepends=('python2' 'arch-wiki-docs')
optdepends=('arch-wiki-docs: for wiki-search-html')
source=(http://kmkeen.com/$pkgname/$pkgname-$_pkgver.tar.gz)
md5sums=('60019045596252535a0a4accec3f724a')

build() {
  cd "$srcdir/$pkgname"
  python2 wiki_lite.py
}

package() {
  cd "$srcdir/$pkgname"
  install -d "$pkgdir/usr/share/doc/arch-wiki/text/"
  install -m 0644 wiki/* "$pkgdir/usr/share/doc/arch-wiki/text/"
  install -Dm 0755 wiki-search      "$pkgdir/usr/bin/wiki-search"
  install -Dm 0755 wiki-search-html "$pkgdir/usr/bin/wiki-search-html"
}

