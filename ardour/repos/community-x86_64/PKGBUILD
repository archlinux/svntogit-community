# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_name=Ardour
pkgname=ardour
pkgver=6.0
pkgrel=1
pkgdesc="Professional-grade digital audio workstation"
arch=('x86_64')
url="https://ardour.org/"
license=('GPL2')
groups=('pro-audio')
depends=('atkmm' 'cairo' 'cairomm' 'fontconfig' 'gdk-pixbuf2' 'gcc-libs'
'glibc' 'glibmm' 'gtkmm' 'libx11' 'libxml2' 'pangomm' 'taglib')
# TODO: add fluidsynth, hidapi, libltc and qm-dsp and use --use-external-libs
# NOTE: currently qm-dsp (1.8.0) is not compatible with the included vamp-plugins
makedepends=('atk' 'aubio' 'boost' 'cppunit' 'curl' 'dbus' 'doxygen' 'fftw'
'flac' 'freetype2' 'git' 'glib2' 'graphviz' 'gtk2' 'harfbuzz' 'itstool'
'libarchive' 'liblo' 'liblrdf' 'libogg' 'libpulse' 'libsamplerate' 'libsndfile'
'libusb' 'lilv' 'lv2' 'pango' 'readline' 'rubberband' 'serd' 'sord' 'sratom'
'suil' 'vamp-plugin-sdk' 'waf')
optdepends=('xjadeo: video monitoring'
            'harvid: video timeline'
            'non-session-manager: for session management')
source=("${pkgname}-${pkgver}::git+https://github.com/${pkgname}/${pkgname}.git#tag=${pkgver}")
sha512sums=('SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"

  # unsetting gtk2 rc (FS#54389)
  sed -e '8iexport GTK2_RC_FILES=/dev/null' -i gtk2_ardour/ardour.sh.in

  # Fixes to use system waf:
  # making sure, that subdirectories can be picked up with local includes
  touch __init__.py
  # making ancient 'misc.py' include available to system installed waf
  sed -e "s/('misc')/('misc', tooldir='tools')/" \
      -i {gtk2_ardour,headless,luasession,session_utils,libs/fst}/wscript
  # make custom 'autowaf' include compatible with system installed waf
  find . -type f \
         -iname "*wscript*" \
         -exec sed -e 's/from waflib.extras import autowaf/from tools import autowaf/g' \
                   -e 's/import waflib.extras.autowaf/from tools import autowaf/g' \
                   -i {} \;
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --configdir=/etc \
                --with-backends="alsa,dummy,jack,pulseaudio" \
                --libjack=weak \
                --nls \
                --optimize \
                --cxx11 \
                --freedesktop \
                --ptformat \
                --lxvst \
                --no-phone-home
  waf build -v
}

package() {
  depends+=('libFLAC.so' 'libasound.so' 'libatk-1.0.so' 'libaubio.so'
  'libarchive.so' 'libcurl.so' 'libdbus-1.so' 'libfftw3f.so'
  'libfftw3f_threads.so' 'libfreetype.so' 'libgdk-x11-2.0.so' 'libgio-2.0.so'
  'libglib-2.0.so' 'libgobject-2.0.so' 'libgtk-x11-2.0.so' 'libharfbuzz.so'
  'liblo.so' 'liblrdf.so' 'liblilv-0.so' 'libogg.so' 'libpango-1.0.so'
  'libpangocairo-1.0.so' 'libpangoft2-1.0.so' 'libpulse.so' 'libreadline.so'
  'librubberband.so' 'libsamplerate.so' 'libserd-0.so' 'libsndfile.so'
  'libsord-0.so' 'libsratom-0.so' 'libsuil-0.so' 'libusb-1.0.so'
  'libvamp-hostsdk.so' 'libvamp-sdk.so')
  cd "${pkgname}-${pkgver}"
  waf i18n --destdir="${pkgdir}"
  waf install --destdir="${pkgdir}"
  # xdg integration
  install -vDm 644 "build/gtk2_${pkgname}/${pkgname}.xml" \
    -t "${pkgdir}/usr/share/mime/packages/"
  install -vDm 644 "build/gtk2_${pkgname}/${pkgname}${pkgver%%.*}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  for size in 16 22 32 48 256 512; do
    install -vDm 644 "gtk2_${pkgname}/resources/${_name}-icon_${size}px.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}${pkgver%%.*}.png"
  done
  # man pages
  install -vDm 644 "${pkgname}.1"* -t "${pkgdir}/usr/share/man/man1/"
}
# vim:set ts=2 sw=2 et:
