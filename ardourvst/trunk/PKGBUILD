# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ardour, liblrdf and raptor are heavily dependent on each other. Updating of
# one package mostly will require an update of all the other packages. I vote
# for fixed dependencies speaking in terms of versions

pkgname=ardourvst
_realname=ardour
pkgver=2.8.11
pkgrel=1
pkgdesc="Ardour with win32 VST support"
arch=(i686)
url="http://ardour.org"
license=('GPL')
depends=('liblrdf' 'liblo' 'aubio' 'libusb' 'wine'
         'slv2' 'rubberband' 'libgnomecanvas')
makedepends=('scons' 'boost' 'pkg-config')
conflicts=('ardour')
provides=('ardour')
source=(ftp://ftp.archlinux.org/other/ardour/${_realname}-${pkgver}.tar.bz2
        ${pkgname}.desktop)
md5sums=('f451a8d0abc133a1700c3932e07a5612'
         '8b32e128a1e894143c5f966c890b5435')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  sed -i '/-O3/d' SConstruct

  scons ${MAKEFLAGS} ARCH="${CFLAGS}" \
                     PREFIX="/usr" \
                     FREEDESKTOP=0 \
                     FREESOUND=1 \
                     VST=1 \
                     DESTDIR="${pkgdir}"
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"

  scons PREFIX="/usr" \
        FREEDESKTOP=0 \
        FREESOUND=1 \
        VST=1 \
        DESTDIR="${pkgdir}" install

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

# vim:set ts=2 sw=2 et:
