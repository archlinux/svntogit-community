# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=0.7.2
pkgrel=1
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
makedepends=('go-pie' 'git')
source=("git+$url.git#tag=$pkgver")
sha512sums=('SKIP')

prepare(){
  cd $pkgname

  # Fix version string
  sed -i "s|versionString        = \".*\"|versionString        = \"$pkgver-arch\"|
          s|commit               = \".*\"|commit               = \"$(git rev-parse HEAD)\"|" \
          version/version.go

  export GOPATH="$srcdir"/gopath

  mkdir -p "$GOPATH"/src/github.com/arduino
  ln -rTsf . "$GOPATH"/src/github.com/arduino/arduino-cli
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  export GO111MODULE=on

  go mod vendor
  go mod download
}

build(){
  export GOPATH="$srcdir"/gopath
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  go build \
  	-gcflags "all=-trimpath=$GOPATH" \
  	-asmflags "all=-trimpath=$GOPATH" \
  	-ldflags "-extldflags $LDFLAGS" \
  	-v .
}

package() {
  install -Dm 755 $pkgname/$pkgname "$pkgdir"/usr/bin/$pkgname
}

