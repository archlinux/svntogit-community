# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=argocd-cli
pkgver=1.7.6
pkgrel=1
pkgdesc="Declarative continuous deployment for Kubernetes command line utility"
arch=('x86_64')
url='https://argoproj.github.io/argo-cd/'
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("argo-cd-${pkgver}.tar.gz::https://github.com/argoproj/argo-cd/archive/v${pkgver}.tar.gz"
        "argocd.bash"
        "argocd.zsh")
sha512sums=('6f57d042fe2c38614c1d1b1e30f4209cd8aa437860f756fd9096539494680706f6ef9c28007a53246d3ee521ec454d23af22bf1683bf52c504cf9d05b78a5c32'
            '5e751e78965b00f767aff90df435aba57aefa6f6d599807ddfda243ff95e0366636da2b2b5af2e9b389ab9efec23ee049f27e6a7defb8d3cb308693495b8a762'
            '28568c83720793cdb5e77ec9c4aa5f1f4757ed665e84d807b8a4fe8377497f462fd33faf4bf076cf0a910e6b4d756ab31d66b1cf5643a3744bd0fa2b62287754')

build() {
  cd "argo-cd-$pkgver"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  go run github.com/gobuffalo/packr/packr build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X github.com/argoproj/argo-cd/common.version=${pkgver} \
              -X github.com/argoproj/argo-cd/common.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
              -X github.com/argoproj/argo-cd/common.gitTag=${pkgver} \
              -linkmode=external
              -extldflags \"${LDFLAGS}\"" \
    ./cmd/argocd
}

package() {
  install -Dm644 argocd.bash "$pkgdir/usr/share/bash-completion/completions/argocd"
  install -Dm644 argocd.zsh "$pkgdir/usr/share/zsh/site-functions/_argocd"

  cd "argo-cd-$pkgver"

  install -D argocd "$pkgdir/usr/bin/argocd"
}
