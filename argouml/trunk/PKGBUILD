# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Claudio Riva <firetux83@gmail.com>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=argouml
_realname=ArgoUML
pkgver=0.32.2
pkgrel=1
pkgdesc="A UML design tool with cognitive support"
arch=('any')
license=('BSD')
url="http://argouml.tigris.org/"
depends=('java-runtime' 'xdg-utils' 'hicolor-icon-theme')
install=argouml.install
source=(http://argouml-downloads.tigris.org/nonav/$pkgname-$pkgver/${_realname}-$pkgver.tar.gz
        argo.sh license.txt argouml.desktop argouml2.svg)
md5sums=('f1bf6b046fea6e57d43c06bbd1f487d0'
         '8640699e0bf100141a1b20ca1bb09f6c'
         '0ea0250f7fb6bafa3a48224e66ce7098'
         '7078b1a2eab169cc5ce342cd0e8cef35'
         '24b382bfe742e949c587efa93dfd0fdb')

package() {
    mkdir -p "$pkgdir"{/usr/bin,/usr/share/java/$pkgname}

    # Copying the argo script to /usr/bin
    install -m755 argo.sh "$pkgdir"/usr/bin/argouml

    cd "$srcdir/$pkgname-$pkgver"

    # Copying necessary files
    mv *.jar "$pkgdir"/usr/share/java/$pkgname

    # Thanks firetux for poiting this out
    mv ext/ "$pkgdir"/usr/share/java/$pkgname

    # Installing license
    install -D -m644 "$srcdir"/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt

    install -D -m644 "$srcdir"/argouml2.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/argouml2.svg
    install -D -m644 "$srcdir"/argouml.desktop "$pkgdir"/usr/share/applications/argouml.desktop
}
