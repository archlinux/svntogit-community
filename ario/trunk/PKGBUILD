# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=ario
pkgver=1.5.1
pkgrel=5
pkgdesc="A GTK client for MPD inspired by Rhythmbox but much lighter and faster"
arch=('x86_64')
url="http://ario-player.sourceforge.net/"
license=('GPL')
depends=('avahi' 'curl' 'dbus-glib' 'gnutls' 'gtk2'
         'libmpdclient' 'libnotify' 'libsoup' 'taglib' 'xdg-utils')
makedepends=('intltool' 'perl-xml-parser' 'patch')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/sourceforge/$pkgname-player/$pkgname-$pkgver.tar.gz
        glib.patch
        drop-libunique.patch)
sha256sums=('0831281ab8634eb92424b61230eee035822f7f92d8b0526da7316c898239c91e'
            '16481cc5fe3fb52ee36bae63522e319193285542fe0c8ab4281ecc5794a82184'
            '6531b09c8e9d1b077a25ca4a37e34a09fdf8d1d0966cf3b0a93f539c324c6a8b')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -p1 -i ../glib.patch
  patch -p1 -i ../drop-libunique.patch
  autoreconf -fi
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc
  make
 }

package () {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
