# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: SergHeart

pkgname=arista
pkgver=0.9.7
pkgrel=9
pkgdesc="Easy to use multimedia transcoder for the GNOME Desktop"
arch=('any')
url="http://www.transcoder.org/"
license=('LGPL2.1')
depends=('pygtk' 'python2-gconf' 'python2-dbus' 'python2-rsvg' 'python2-gudev' 'gnome-icon-theme'
         'gstreamer0.10-python' 'desktop-file-utils')
optdepends=('libdvdread: for DVD reading'
            'libdvdcss: for encrypted DVD reading'
            'pywebkitgtk: show in-app codecs help'
            'python2-notify: show notifications in your desktop environment'
            'python2-nautilus: Nautilus file manager integration'
            'gstreamer0.10-ffmpeg: enable FFMpeg based audio/video plugins'
            'gstreamer0.10-base-plugins: enable basic set of well-supported audio/video plugins'
            'gstreamer0.10-good-plugins: enable a set of well-supported free audio/video plugins'
            'gstreamer0.10-bad-plugins: enable a set of less supported audio/video plugins'
            'gstreamer0.10-ugly-plugins: enable a set of well-supported non-free audio/video plugins')
source=("http://programmer-art.org/media/releases/arista-transcoder/${pkgname}-${pkgver}.tar.gz"
        "fix-for-python2.patch"
        "gst-webkit-imports.patch")
sha256sums=('e6028e45516f0c5a736d6ec3c19f783e0a06d103cf2a816b8cc967ab964023ff'
            '7f783c54122eb4b47b121a3a47ee69d552cb6f1b5a78ed6836b48f12d2b1883d'
            '625c2767a36e591b38ef31e28dddb8e45218b632ce2a150f12ed2eed34746406')
conflicts=('arista-transcoder')
replaces=('arista-transcoder')
install="${pkgname}.install"

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "../fix-for-python2.patch"
  patch -p1 -i "../gst-webkit-imports.patch"
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
}
