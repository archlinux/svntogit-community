# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Filippo 'JoeyrS' Civiletti <joeyrs@gmail.com>

pkgname=armagetronad
pkgver=0.2.8.3
pkgrel=1
pkgdesc="A Tron Clone in 3D."
arch=('i686' 'x86_64')
url="http://armagetronad.net/"
license=('GPL')
depends=('sdl_image' 'libxml2' 'sdl_mixer' 'mesa' 'ftgl')
source=(http://downloads.sourceforge.net/sourceforge/armagetronad/armagetronad-$pkgver.src.tar.gz
	armagetronad.desktop
	gcc43.patch)
md5sums=('861f9b67ac2787d73807bca6c5fc535d'
         '29d1b1231acadff12cf3014a3867ba5c'
         '9045d99559ef33fc18136630e8de916c')

build()	{
	 cd $srcdir/armagetronad-$pkgver
	 sed -i 's|FTGL.h|ftgl.h|g' configure
	 sed -i 's|png_check_sig|png_sig_cmp|g' configure
	 patch -p1 < ../gcc43.patch
	 ./configure --prefix=/usr \
	 	--sysconfdir=/etc \
		--mandir=/usr/share/man \
	       	--disable-games \
		--enable-automakedefaults
	 make || return 1
	 make DESTDIR=$pkgdir install
	 install -D -m 644 $srcdir/armagetronad.desktop $pkgdir/usr/share/applications/armagetronad.desktop
	 rm -rf $pkgdir/usr/share/applnk
	 rm $pkgdir/usr/bin/armagetronad-uninstall
	 mv $pkgdir/usr/bin/armagetronad $pkgdir/usr/bin/armagetronad_bin
	 printf "#!/bin/bash\n/usr/bin/armagetronad_bin --configdir /etc/armagetronad --datadir /usr/share/armagetronad" > $pkgdir/usr/bin/armagetronad
	 chmod +x $pkgdir/usr/bin/armagetronad
}

