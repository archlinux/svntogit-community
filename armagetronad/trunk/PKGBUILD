# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Filippo 'JoeyrS' Civiletti <joeyrs@gmail.com>

pkgname=armagetronad
pkgver=0.2.9.0
pkgrel=1
pkgdesc='A Tron Clone in 3D.'
arch=('x86_64')
url='http://armagetronad.net/'
license=('GPL')
depends=('sdl_image' 'libxml2' 'sdl_mixer' 'ftgl')
makedepends=('python')
optdepends=('python: language updater')
#source=("https://downloads.sourceforge.net/sourceforge/armagetronad/armagetronad-$pkgver.src.tar.gz")
source=("https://downloads.sourceforge.net/sourceforge/armagetronad/armagetronad-$pkgver.tbz")
md5sums=('754159a3a7817610035d1729610b4369')

prepare() {
     cd "$srcdir/armagetronad-$pkgver"
     2to3 -w language/update.py

     sed -i 's|FTGL.h|ftgl.h|g' configure
     sed -i 's|png_check_sig|png_sig_cmp|g' configure
}

build() {
     cd "$srcdir/armagetronad-$pkgver"
     ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --mandir=/usr/share/man \
        --disable-games \
        --enable-automakedefaults \
        --disable-uninstall
     make
}

package() {
     cd "$srcdir/armagetronad-$pkgver"
     make DESTDIR="$pkgdir" install
     install -D -m 644 "desktop/armagetronad.desktop" "$pkgdir/usr/share/applications/armagetronad.desktop"
     install -d "$pkgdir/usr/share/pixmaps/"
     ln -s /usr/share/armagetronad/desktop/icons/large/armagetronad.png \
           "$pkgdir/usr/share/pixmaps/armagetronad.png"
     mv "$pkgdir/usr/bin/armagetronad" "$pkgdir/usr/bin/armagetronad_bin"
     printf "#!/bin/bash\n/usr/bin/armagetronad_bin --configdir /etc/armagetronad --datadir /usr/share/armagetronad" > "$pkgdir/usr/bin/armagetronad"
     chmod +x "$pkgdir/usr/bin/armagetronad"
}
