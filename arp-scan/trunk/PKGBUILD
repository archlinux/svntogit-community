# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: xav <xav at ethertricks dot net>

pkgname=arp-scan
pkgver=1.9
pkgrel=3
pkgdesc="A tool that uses ARP to discover and fingerprint IP hosts on the local network"
arch=('x86_64')
url="https://github.com/royhills/arp-scan"
license=('GPL')
depends=('libpcap' 'perl-libwww')
source=(https://github.com/royhills/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz
        socket-error.patch)

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -p1 -i "$srcdir"/socket-error.patch
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	autoreconf --install
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="${pkgdir}" install
}

sha256sums=('b2a446a170e4a2feeb825cd08db48a147f8dffae702077f33e456c4200e7afb0'
            'bcf605b29dac8d90ecda92558b676521ad764e048250602f7a61fd0c56fd053b')
