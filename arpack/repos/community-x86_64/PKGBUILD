# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=arpack
pkgver=3.5.0
pkgrel=4
arch=('x86_64')
pkgdesc='Fortran77 subroutines for solving large scale eigenvalue problems'
url='https://github.com/opencollab/arpack-ng'
license=('BSD')
depends=('lapack' 'openmpi')
makedepends=('git' 'gcc-fortran')
provides=('arpack-ng')
# use the latest commit while waiting for 3.6.0. Fixes #58123.
source=("git+https://github.com/opencollab/arpack-ng#commit=edce6343633edca871db7a25abb21479081813e4")
md5sums=('SKIP')

prepare() {
  cd "$pkgname-ng"

  ./bootstrap
}

build() {
  cd "$pkgname-ng"

  ./configure --prefix=/usr --enable-mpi

  make \
    F77="mpif77" \
    CFLAGS+=" `pkg-config --cflags ompi-f77` " \
    LIBS+=" `pkg-config --libs ompi-f77` "
}

package() {
  cd "$pkgname-ng"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
