# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SÃ©bastien Luttringer

pkgname=arpwatch
pkgver=2.1a15
pkgrel=14
pkgdesc='Ethernet/FDDI station activity monitor'
arch=('x86_64')
url='ftp://ftp.ee.lbl.gov/'
license=('BSD')
depends=('libpcap' 'smtp-forwarder')
source=("ftp://ftp.ee.lbl.gov/$pkgname-$pkgver.tar.gz"
        'ethercodes.dat.xz'
        'LICENSE'
        "$pkgname.service")
md5sums=('cebfeb99c4a7c2a6cee2564770415fe7'
         'c5abd91d8852f68bf5bf49ed9e80dba0'
         '9359518c500562281e76cdb0b2fa9062'
         '586c5d13b0ee177d927b8925a9332ebc')

prepare() {
  cd $pkgname-$pkgver
  # move arp database in /var/lib/arpwatch
  # /var is used because arpwatch write its learned mac into this folder
  sed -i 's|ARPDIR = $(prefix)/arpwatch|ARPDIR = /var/lib/arpwatch|' Makefile.in
  # binary ownership to root
  sed -i 's/-\(o\|g\) bin/-\1 root/g' Makefile.in
  # Update ethercodes with recent OUI. See gen_ethercodes.sh
  cp -f "$srcdir/ethercodes.dat" ethercodes.dat
  # Do not spam root user
  sed -i 's|root|arpwatch|' addresses.h.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --sbindir=/usr/bin
  make SENDMAIL=/usr/bin/sendmail
}

package() {
  cd $pkgname-$pkgver
  install -d -m 0755 "$pkgdir"/usr/{bin,share/man/man8}
  make DESTDIR="$pkgdir" install install-man
  # install ether prefix database
  install -Dm644 ethercodes.dat "$pkgdir/var/lib/$pkgname/ethercodes.dat"
  cd "$srcdir"
  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # systemd
  install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname@.service"
}

# vim:set ts=2 sw=2 et:
