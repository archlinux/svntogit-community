# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste@jonglez.org>
# Contributor: Serge Ziryukin <ftrvxmtrx@gmail.com>

pkgname=asciiportal
pkgver=1.3
pkgrel=3
pkgdesc='Text based puzzle game inspired by the popular video game'
arch=('x86_64')
url='https://github.com/cymonsgames/ASCIIpOrtal'
license=('GPL3')
depends=('pdcurses' 'sdl' 'sdl_mixer' 'yaml-cpp')
makedepends=('boost' 'gendesk' 'git' 'imagemagick')
source=("git+https://github.com/cymonsgames/ASCIIpOrtal.git#tag=v$pkgver-beta8"
        "${pkgname}.png::https://fc01.deviantart.net/fs71/f/2010/318/3/5/portal_ascii_icon___button_by_lightmystic-d32udqx.png") # icon by Matt White under a CC license
sha256sums=('SKIP'
            '390f21881377b331f1a65a3e54ccf91fba72a88146058cc146f914a1185de9d1')

prepare() {
  gendesk -n -f \
    --name ASCIIpOrtal \
    --pkgname $pkgname \
    --pkgdesc "$pkgdesc" \
    --exec $pkgname

  convert $pkgname.png -resize 48x48\! ${pkgname}48x48.png
}

build() {
  make -C ASCIIpOrtal linux
}

package() {
  make -C ASCIIpOrtal DESTDIR="$pkgdir" install

  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ${pkgname}48x48.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
} 

# vim:ts=2 sw=2 et:
