# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=aspell-uk
pkgver=1.6.5
_ftpver=1.4.0
pkgrel=3
pkgdesc='Ukrainian dictionary for aspell'
arch=('i686' 'x86_64')
url="http://ispell-uk.sourceforge.net/"
license=('GPL' 'LGPL' 'MPL')
depends=('aspell')
source=(ftp://ftp.gnu.org/gnu/aspell/dict/uk/aspell6-uk-${_ftpver}-0.tar.bz2
        http://downloads.sourceforge.net/project/ispell-uk/spell-uk/$pkgver/spell-uk-$pkgver.tgz)
sha256sums=('35f9a7e840c1272706bc6dd172bc9625cbd843d021094da8598a6abba525f18c'
            '90eff3b9e40d04b53717240b725e303f02cec4ee8bef9bded69b7481e41fa422')

build() {
  # needed when using a source package from ispell-uk project
  cd ${srcdir}/spell-uk-$pkgver
  make

  cd ${srcdir}/aspell6-uk-${_ftpver}-0
  ./configure
  make

  # needed when using a source package from ispell-uk project
  mv ${srcdir}/spell-uk-$pkgver/dist/aspell-uk-$pkgver/* \
    ${srcdir}/aspell6-uk-${_ftpver}-0
}

package() {
  cd ${srcdir}/aspell6-uk-${_ftpver}-0

  make DESTDIR="$pkgdir" install
}
