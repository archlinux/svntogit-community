# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Rabyte <rabyte [at] gmail (dot) com>

pkgname=assaultcube
pkgver=1.1.0.1
pkgrel=1
pkgdesc='A realistic team oriented multiplayer FPS based on the Cube engine'
arch=('i686' 'x86_64')
url='http://assault.cubers.net/'
license=('ZLIB' 'custom')
depends=('sdl_mixer' 'sdl_image' 'openal')
makedepends=('mesa')
source=("http://downloads.sourceforge.net/actiongame/AssaultCube_v${pkgver}.tar.bz2"
        "http://downloads.sourceforge.net/actiongame/AssaultCube_v${pkgver}.source.tar.bz2"
        'assaultcube'
        'assaultcube-server'
        'assaultcube.desktop'
        'assaultcube.png')
md5sums=('9ad55a9e032efc59eb1f2f89577647c3'
         '566388ec0cf5d3a910c90763656e5292'
         'ead0910f8ea4a4ce47b7da3091f9c609'
         '553e2c3b38c4d13e5d77c23efd51a6a6'
         'a37dfbe8263f4ef8fe41120196194eae'
         'f688c59ecee2ebc5c589720aa1480765')

build() {
  cd ${pkgver}

  cd source/src

  make
}

package() {
  cd ${pkgver}/source/src

  install -Dm755 ac_client ${pkgdir}/usr/bin/ac_client
  install -Dm755 ac_server ${pkgdir}/usr/bin/ac_server
  install -Dm755 ${srcdir}/assaultcube ${pkgdir}/usr/bin/assaultcube
  install -Dm755 ${srcdir}/assaultcube-server ${pkgdir}/usr/bin/assaultcube-server

  mkdir -p ${pkgdir}/usr/share/assaultcube/packages
  cd ${srcdir}/${pkgver}
  cp -rf config packages docs mods ${pkgdir}/usr/share/assaultcube

  install -Dm644 ${srcdir}/assaultcube.png ${pkgdir}/usr/share/pixmaps/assaultcube.png
  install -Dm644 ${srcdir}/assaultcube.desktop ${pkgdir}/usr/share/applications/assaultcube.desktop
  install -Dm644 docs/package_copyrights.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim: sw=2:ts=2 et:
