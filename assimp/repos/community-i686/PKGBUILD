# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: jepaan <jepaan at hotmail dot com>

pkgname=assimp
pkgver=3.1.1
pkgrel=2
pkgdesc="Portable Open Source library to import various well-known 3D model formats in an uniform manner"
arch=('i686' 'x86_64')
license=('BSD')
depends=('zlib' 'gcc-libs' 'boost-libs')
makedepends=('cmake' 'boost')
url=('http://assimp.sourceforge.net/index.html')
source=("$pkgname-$pkgver.tar.gz::https://github.com/assimp/assimp/archive/v${pkgver}.tar.gz")
md5sums=('ab9977edf95e9b2910d7cca286926e85')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	mkdir build && cd build
	cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DASSIMP_ENABLE_BOOST_WORKAROUND=OFF \
        -DASSIMP_BUILD_SAMPLES=OFF
	make 
}

package()  {
	cd ${srcdir}/${pkgname}-${pkgver}/build

	make DESTDIR=$pkgdir install
	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
