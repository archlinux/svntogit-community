# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Todd Musall <tmusall@comcast.net>

pkgname=at
pkgver=3.1.16
pkgrel=1
pkgdesc='AT and batch delayed command scheduling utility and daemon'
arch=('x86_64' 'i686')
url='http://packages.qa.debian.org/a/at.html'
license=('GPL')
depends=('pam' 'flex')
makedepends=('ssmtp' 'git')
backup=('etc/at.deny')
options=('!makeflags')
source=("git://anonscm.debian.org/collab-maint/at.git#tag=upstream/$pkgver"
        'atd.service')
sha256sums=('SKIP'
            '34cea1b7d109efe8b515d02092f73a7c5f1ce6992bba5855f9d428f5be55cdfd')

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --sbindir=/usr/bin \
              --with-jobdir=/var/spool/atd \
              --with-atspool=/var/spool/atd
  make
}

package() {
  make -C "$pkgname" IROOT="$pkgdir" docdir=/usr/share/doc install
  install -Dm644 atd.service "$pkgdir/usr/lib/systemd/system/atd.service"
}

# vim:set ts=2 sw=2 et:
