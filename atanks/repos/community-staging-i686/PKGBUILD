# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jacek Poplawski <jacekpoplawski@gmail.com>
# Contributor: Charlie Cox <ccoxiv@yahoo.com>

pkgname=atanks
pkgver=6.3
pkgrel=2
pkgdesc="Atomic Tanks"
url="http://atanks.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('sed')
depends=('allegro4' 'gcc-libs')
source=("http://downloads.sourceforge.net/project/atanks/atanks/atanks-${pkgver}/atanks-${pkgver}.tar.gz")
md5sums=('271158ee7ca7c6415859af1e5e020515')

build() {
  cd atanks-${pkgver}
  make
}

package() {
  cd atanks-${pkgver}
  make DESTDIR=${pkgdir} INSTALL="install -c" install

  sed -i 's/^Name=atanks/Name=Atomic Tanks/' atanks.desktop
  sed -i 's/^Categories=Game;/Categories=Game;StrategyGame;/' atanks.desktop

  install -Dm644 atanks.desktop $pkgdir/usr/share/applications/atanks.desktop
  install -Dm644 atanks.png $pkgdir/usr/share/pixmaps/atanks.png
}
