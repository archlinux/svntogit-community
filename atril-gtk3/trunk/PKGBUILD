# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=atril
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="MATE document viewer (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus' 'desktop-file-utils' 'gtk3' 'libsecret' 'libsm' 'libspectre'
         'mate-desktop-gtk3>=1.10' 'mate-icon-theme' 'poppler-glib' 'webkitgtk' 'zlib')
makedepends=('caja-gtk3>=1.10' 'djvulibre' 'gobject-introspection' 'libgxps'
             'mate-common' 'texlive-bin' 'yelp-tools')
optdepends=('caja-gtk3: Document Properties extension'
            'djvulibre: DjVu support'
            'libgxps: XPS support'
            'texlive-bin: DVI support'
            'yelp: for reading MATE help documents')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('7abd5b9310d88cb5da05248f1c55bb9ac069ac28')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=3.0 \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-epub \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-xps \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
