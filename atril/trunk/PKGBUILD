# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=atril
pkgver=1.8.1
pkgrel=2
pkgdesc="Simply a document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus' 'desktop-file-utils' 'gtk2' 'libsecret' 'libsm' 'libspectre'
         'mate-desktop' 'mate-icon-theme' 'poppler-glib' 'zlib')
makedepends=('caja' 'djvulibre' 'gobject-introspection' 'libgxps' 'mate-common'
             'perl-xml-parser' 'texlive-bin' 'yelp-tools')
optdepends=('caja: Document Properties extension'
            'djvulibre: DjVu support'
            'libgxps: XPS support'
            'p7zip: cbz and cb7 compressed comic books'
            'texlive-bin: DVI support'
            'unrar: cbr compressed comic books'
            'yelp: for reading MATE help documents')
replaces=('mate-document-viewer')
provides=('mate-document-viewer')
conflicts=('mate-document-viewer')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz")
sha1sums=('2051be961a84d118ad0a278ec22bf48ca5d3fc35')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2.0 \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-xps \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
