# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich tobias [funnychar] archlinux org

pkgbase=aubio
pkgname=('aubio' 'python-aubio' 'python2-aubio')
pkgver=0.4.6
pkgrel=7
pkgdesc="A tool for extracting annotations from audio signals"
arch=('x86_64')
url="https://aubio.org/"
license=('GPL3')
makedepends=('doxygen' 'ffmpeg' 'fftw' 'jack' 'libsamplerate' 'libsndfile'
'python-numpy' 'python-setuptools' 'python-sphinx' 'python2-numpy'
'python2-setuptools' 'python2-sphinx' 'txt2man')
checkdepends=('python-nose2' 'python2-nose2')
source=("https://${pkgname}.org/pub/${pkgname}-${pkgver}.tar.bz2"
        "0001-Fix-build-with-FFmpeg-4.0.patch")
sha512sums=('0e5cab0c88d51f2fb70c0d85e1c067a7b3758fbffdbd5026686726379aa36203adb63ed3f8e40c83ea02930e0ffc4e6c246803f1d518d448d47a01984678e77d'
            '46fadc289bf6ae5b57d9ba15de0e3a892e255b26c0420efea6d0acdf39cd553d7e8c4ae9a2cde0fdfe0d99b3f05ef18b41e07288174c0fbf49c8d16f8d766f9a')

prepare() {
  cd "${pkgname[0]}-${pkgver}"
  # apply patch to build with ffmpeg >= 4.0
  patch -Np1 -i "../0001-Fix-build-with-FFmpeg-4.0.patch"
  cd ..
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[2]}-${pkgver}"
  cd "${pkgname[2]}-${pkgver}"
  # python2 shebang
  sed -e '1s/python/&2/' \
    -i "python/lib/${pkgname}/"{__init__,cmd,cut}.py \
    -i "python/tests/"*.py
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  echo "$(pwd)"
  python2 waf configure --prefix=/usr \
    --enable-fftw3 \
    --htmldir=/usr/share/doc/${pkgname} \
    --docdir=/usr/share/doc/${pkgname} \
    --libdir=/usr/lib
  python2 waf build
  cd ../"${pkgname[1]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[2]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[1]}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-3.7:${PYTHONPATH}" python setup.py test

  cd ../"${pkgname[2]}-${pkgver}"
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-2.7:${PYTHONPATH}" python2 setup.py test
}

package_aubio() {
  pkgdesc="A tool for extracting annotations from audio signals"
  depends=('ffmpeg' 'fftw' 'jack' 'libsamplerate')
  optdepends=('python2-numpy: Python 2.x interface'
              'python-numpy: Python 3.x interface')
  groups=('pro-audio')
  cd "${pkgname}-${pkgver}"
  python2 waf --destdir="${pkgdir}" install
  # move doc, as waf configure doesn't honor the htmldir and docdir settings
  mv -v "${pkgdir}/usr/share/doc/lib${pkgname}-doc" \
    "${pkgdir}/usr/share/doc/${pkgname}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README.md}
}

package_python-aubio() {
  pkgdesc="Python 3.x bindings for aubio."
  depends=('aubio' 'python-numpy' 'python-setuptools')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README.md}
}

package_python2-aubio() {
  pkgdesc="Python 2.x bindings for aubio."
  depends=('aubio' 'python2-numpy' 'python2-setuptools')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README.md}
  # move duplicates
  mv -v "${pkgdir}/usr/bin/aubio" "${pkgdir}/usr/bin/aubio2"
  mv -v "${pkgdir}/usr/bin/aubiocut" "${pkgdir}/usr/bin/aubiocut2"
}

# vim:set ts=2 sw=2 et:
