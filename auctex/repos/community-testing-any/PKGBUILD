# $Id: PKGBUILD 1785 2009-09-03 01:54:18Z ebelanger $
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=auctex
pkgver=11.86
pkgrel=2
pkgdesc="An extensible package for writing and formatting TeX files in Emacs"
arch=('any') 
url="http://www.gnu.org/software/auctex/"
license=('GPL3')
depends=('emacs' 'ghostscript' 'texlive-core')
install=auctex.install
source=(http://ftp.gnu.org/pub/gnu/auctex/${pkgname}-${pkgver}.tar.gz ghostscript.9.0.0.patch)
md5sums=('6bc33a67b6ac59db1aa238f3693b36d2' 'bd19db9ec7792ddc232ba611986cb523')
sha1sums=('af3dd156f8b2db7764c86d9f7de8abd5811d888a' '5b7dc751024f48934e9c9e36b1106e4100ed3aae')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < ../ghostscript.9.0.0.patch
  ./configure --prefix=/usr --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
