# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=auctex
pkgver=11.89
pkgrel=2
pkgdesc="An extensible package for writing and formatting TeX files in Emacs"
arch=('any') 
url="http://www.gnu.org/software/auctex/"
license=('GPL3')
depends=('emacs' 'ghostscript' 'texlive-core')
options=('!makeflags')
install=auctex.install
source=(http://ftp.gnu.org/pub/gnu/auctex/${pkgname}-${pkgver}.tar.gz{,.sig})
sha1sums=('339244eee9ac932b6a6e342313ea7ea8653d86f7'
          'SKIP')
validpgpkeys=('70CDE487C45540307F86972BE2FD58BF20485F50')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/as_fn_error $? "--with-texmf-dir=/echo as_fn_error $? "--with-texmf-dir=/' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --localstatedir=/var --with-texmf-dir=/usr/share/texmf
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
