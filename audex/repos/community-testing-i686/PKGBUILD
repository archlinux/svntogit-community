# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mightyjaym <jmambrosino@gmail.com>
# Contributor: moostik <mooostik_at_gmail.com>

pkgname=audex
pkgver=0.74b1
pkgrel=4
pkgdesc="A CDDA extraction tool with a ftp upload function for KDE"
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php?content=77125"
license=('GPL')
depends=('kdebase-runtime' 'libkcddb' 'libkcompactdisc' 'cdparanoia')
makedepends=('cmake' 'automoc4')
optdepends=('vorbis-tools: OGG encoder'
	'faac: MP4/M4A/AAC encoder'
	'flac: FLAC encoder'
	'lame: MP3 encoder')
install=${pkgname}.install
source=("ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "google_regex.patch" 'gcc47.patch')
md5sums=('20fdf9a0f89c08f44a151b6bdaab8782'
         '77b33d8daead89d5f4f15b78a1cd1d55'
         '64107bcf90e22910e57804be774822dd')

build() {
  # fix bug FS#25193
  patch -Np0 -i "${srcdir}"/google_regex.patch

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/gcc47.patch
  cd ..

  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
