# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>

pkgname=audit
pkgver=2.3.2
pkgrel=2
pkgdesc='User space utilities for storing and searching the audit records generated by the audit subsystem in the Linux kernel.'
url="http://people.redhat.com/sgrubb/$pkgname"
arch=(i686 x86_64)
depends=(krb5 libcap-ng)
makedepends=(libldap swig linux-headers python2)
license=(GPL)
options=(!libtool emptydirs)
backup=(
	etc/libaudit.conf
	etc/audit/rules.d/audit.rules
	etc/audit/auditd.conf
	etc/audisp/audispd.conf
	etc/audisp/audisp-remote.conf
	etc/audisp/zos-remote.conf
	etc/audisp/plugins.d/af_unix.conf
	etc/audisp/plugins.d/audispd-zos-remote.conf
	etc/audisp/plugins.d/au-remote.conf
	etc/audisp/plugins.d/syslog.conf
)
source=("$url/$pkgname-$pkgver.tar.gz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	export PYTHON=/usr/bin/python2
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/audit \
		--with-python=yes \
		--enable-gssapi-krb5=yes \
		--enable-systemd=yes \
		--with-libcap-ng=yes #--with-apparmor=yes
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	cd "$pkgdir"
	install -d var/log/audit
	rm -rf etc/rc.d etc/sysconfig usr/lib/audit

	sed -ri 's|/sbin|/usr/bin|' \
		etc/audit/*.conf \
		etc/audisp/plugins.d/*.conf \
		usr/lib/systemd/system/auditd.service

	chmod 644 usr/lib/systemd/system/auditd.service
}

sha256sums=('8872e0b5392888789061db8034164305ef0e1b34543e1e7004d275f039081d29')
