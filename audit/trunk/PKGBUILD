# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Connor Behan <connor.behan@gmail.com>
# Contributor: henning mueller <henning@orgizm.net>

pkgname=audit
pkgver=2.2
pkgrel=1
pkgdesc='User space utilities for storing and searching the audit records generated by the audit subsystem in the Linux kernel.'
url=http://people.redhat.com/sgrubb/$pkgname
arch=(i686 x86_64)
depends=(krb5 libcap-ng)
makedepends=(libldap swig linux-headers python2)
license=(GPL)
options=(!libtool emptydirs)
backup=(
	etc/libaudit.conf
	etc/audit/audit.rules
	etc/audit/auditd.conf
	etc/audisp/audispd.conf
	etc/audisp/audisp-remote.conf
	etc/audisp/zos-remote.conf
	etc/audisp/plugins.d/af_unix.conf
	etc/audisp/plugins.d/audispd-zos-remote.conf
	etc/audisp/plugins.d/au-remote.conf
	etc/audisp/plugins.d/syslog.conf
)
source=(
	$url/$pkgname-$pkgver.tar.gz
	auditd.rc
	python2.patch
)
md5sums=('b60366dbe0c3a7497bdd8b3f7065b266'
         '5f86e87354d0c3b9b408a26daed7a906'
         '45a33c6bef84f8e1701cb0b5e187336b')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p0 -i "$srcdir/python2.patch"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--libexecdir=/usr/lib/audit \
		--with-python=yes \
		--enable-gssapi-krb5=yes \
		--with-libcap-ng=yes #--with-apparmor=yes
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	cd "$pkgdir"
	install -m755 "$srcdir/auditd.rc" etc/rc.d/auditd
	install -d var/log/audit
	rm -rf etc/rc.d/init.d etc/sysconfig
	mv usr/sbin sbin
}
