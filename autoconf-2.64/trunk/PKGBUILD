# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michael Witten <mfwitten>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgver=2.64
pkgname="autoconf-$pkgver"
pkgrel=1
pkgdesc="A GNU tool for automatically configuring source code"
arch=(any)
license=(GPL2 GPL3)
url=https://www.gnu.org/software/autoconf
depends=(coreutils awk m4 diffutils bash)
source=("https://ftp.gnu.org/pub/gnu/autoconf/autoconf-$pkgver.tar.gz"
        "https://ftp.gnu.org/pub/gnu/autoconf/autoconf-$pkgver.tar.gz.sig")
md5sums=('30a198cef839471dd4926e92ab485361'
         'SKIP')
validpgpkeys=('2B7C1A53420D4AF3BFF4738BF382AE19F4850180')

build() {
  cd autoconf-$pkgver
  ./configure --prefix="/opt/autoconf/$pkgver"
  make
}

package() {
  cd autoconf-$pkgver
  make DESTDIR="$pkgdir" install
}
