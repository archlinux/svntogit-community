# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Daniel MÃ¼llner <muellner@math.stanford.edu>

pkgname=autoconf-archive
pkgver=2018.03.13
pkgrel=2
epoch=1
pkgdesc="A collection of freely re-usable Autoconf macros"
arch=('any')
url="https://www.gnu.org/software/autoconf-archive/"
license=('GPL3')
depends=('autoconf')
optdepends=('automake: macros for use with it')
source=("https://ftpmirror.gnu.org/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('6175f90d9fa64c4d939bdbb3e8511ae0ee2134863a2c7bf8d9733819efa6e159'
            'SKIP')
validpgpkeys=('1A4F63A13A4649B632F65EE141BC28FE99089D72') # Peter Simons <simons@cryp.to>

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make check
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
