# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs
pkgver=5.0.8
pkgrel=2
pkgdesc='A kernel-based automounter for Linux.'
arch=('i686' 'x86_64')
url='http://freshmeat.net/projects/autofs'
license=('GPL2')
depends=('libxml2')
makedepends=('libldap' 'krb5' 'kmod')
optdepends=('krb5: for LDAP support')
backup=('etc/autofs/auto.master'
        'etc/autofs/auto.misc'
        'etc/autofs/auto.net'
        'etc/autofs/auto.smb'
        'etc/autofs/autofs_ldap_auth.conf'
        'etc/default/autofs')
options=(!makeflags)
source=("http://www.kernel.org/pub/linux/daemons/${pkgname}/v5/${pkgname}-${pkgver}.tar.xz"
        "git-fixes.patch")
sha256sums=('32c41fb4e3439f18400468e84dc82fbbd4b14156bef6b9a7f09196ecd9299d52'
            '09c41a73db6a352cb04b51795591e5ae1c4a3fff37257fc8bb117020fa0e2724')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Apply fixes from git
  patch -Np1 -i "${srcdir}/git-fixes.patch"

  sed -i -e 's|/etc/auto.misc|/etc/autofs/auto.misc|' \
         -e 's|/etc/auto.master.d|/etc/autofs/auto.master.d|' samples/auto.master
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc/autofs --sbindir=/usr/bin \
    --with-mapdir=/etc/autofs --without-hesiod \
    --enable-ignore-busy --with-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make INSTALLROOT="${pkgdir}" install
  rm -r "$pkgdir/run"
  install -dm755 "$pkgdir/etc/autofs/auto.master.d"
}

# vim:set ts=2 sw=2 et:
