# $Id$
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: JoÃ«l Schaerer <joel.schaerer@laposte.net>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=autojump
pkgver=12
pkgrel=2
pkgdesc="A faster way to navigate your filesystem from the command line"
arch=('any')
url="http://wiki.github.com/joelthelion/autojump"
license=('GPL3')
depends=('bash' 'python2')
optdepends=('python: needed for jumpapplet')
source=(http://github.com/joelthelion/autojump/tarball/release-v${pkgver})
install=autojump.install
md5sums=('cc275732b010afe07bc144168ed3a731')

package() {
    cd joelthelion-autojump-50543e5

    # python2 fix
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' autojump
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' jumpapplet

    install -Dm644 icon.png ${pkgdir}/usr/share/autojump/icon.png
    install -Dm755 jumpapplet ${pkgdir}/usr/bin/jumpapplet
    install -Dm755 autojump ${pkgdir}/usr/bin/autojump
    install -Dm644 autojump.1 ${pkgdir}/usr/share/man/man1/autojump.1
    install -Dm755 autojump.bash ${pkgdir}/etc/profile.d/autojump.bash
    install -Dm755 autojump.sh ${pkgdir}/etc/profile.d/autojump.sh
    install -Dm755 autojump.zsh ${pkgdir}/etc/profile.d/autojump.zsh
    install -Dm644 _j ${pkgdir}/usr/share/zsh/site-functions/_j
}
