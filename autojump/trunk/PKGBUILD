# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: graysky <graysky AT archlnux DOT us>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: JoÃ«l Schaerer <joel.schaerer@laposte.net>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=autojump
pkgver=22.2.2
pkgrel=2
pkgdesc="A faster way to navigate your filesystem from the command line"
arch=('any')
url="http://wiki.github.com/joelthelion/autojump"
license=('GPL3')
depends=('python')
install=$pkgname.install
source=(https://github.com/joelthelion/${pkgname}/archive/release-v${pkgver}.tar.gz)
sha256sums=('db2683a36f78af384bd0c0a48dd6b082bdfc75c23dd3b59d0eea1ecc88f01ccc')

prepare() {
  sed -i "s:/env python:/python3:g" ${pkgname}-release-v${pkgver}/bin/${pkgname}
}

package() {
  cd ${pkgname}-release-v${pkgver}

  ./install.py --destdir "${pkgdir}" --prefix 'usr/' --zshshare 'usr/share/zsh/site-functions'

  cd "${pkgdir}"/usr/share/$pkgname
  for i in $pkgname.*
    do ln -s /usr/share/$pkgname/$i \
      "${pkgdir}"/etc/profile.d/$i
  done

#https://github.com/joelthelion/autojump/pull/339
  sed -i "s:/usr/local/:/usr/:g" "${pkgdir}"/etc/profile.d/$pkgname.sh
  sed -i "s:/build/autojump/pkg/autojump/:/:g" "${pkgdir}"/etc/profile.d/$pkgname.sh
}
