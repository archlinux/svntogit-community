# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=autopep8
pkgver=1.7.1
pkgrel=1
epoch=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="https://github.com/hhatto/autopep8"
license=('MIT')
depends=('python-pycodestyle' 'python-tomli')
makedepends=('python-setuptools')
source=("https://github.com/hhatto/autopep8/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6a2a25ab9a45df21dd029f880cbc1aded822288d68a4f5f24d08bcc4d95a9ae58fe33e88fb0a004d223721f8467e6938a6daf313edf11e89809510b5e87260ec')

build() {
  cd autopep8-$pkgver
  python setup.py build
}

check() {
  cd autopep8-$pkgver
  python setup.py test
}

package() {
  cd autopep8-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
