# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=autopep8
pkgver=2.0.0
pkgrel=1
epoch=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="https://github.com/hhatto/autopep8"
license=('MIT')
depends=('python-pycodestyle' 'python-tomli')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/hhatto/autopep8/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
         pycodestyle-2.10.patch)
sha512sums=('66568e6fa97504631a28633cdbbab2dadccfbe27a78a44f3d0630b1cf4c09c29b2c2643f6640c6908edb6af5d2e94893872de63af7c1047ba8fe4c6f5fa613bb'
            'e76a4a9efb2ac19e8c033f3c1cbc9380a7656085f4bd1ee2995e8e85356044547949ea16a19c87b441866ddb633e62ffdfd4f24ad88bf39c3c6584c4b6c82334')

prepare() {
  cd autopep8-$pkgver
  patch -p1 -i ../pycodestyle-2.10.patch
}

build() {
  cd autopep8-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd autopep8-$pkgver
  pytest -v
}

package() {
  cd autopep8-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
