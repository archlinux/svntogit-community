# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Nathaniel Chin <thegamingorangutans+aur at gmail.com>

pkgname=av1an
pkgver=0.2.0
pkgrel=2
pkgdesc='Cross-platform command-line encoding framework with per scene quality encoding'
arch=('x86_64')
url='https://github.com/master-of-zen/Av1an'
license=('GPL3')
makedepends=('rust' 'nasm' 'clang')
depends=('ffmpeg' 'aom' 'vapoursynth')
optdepends=('svt-av1: SVT-AV1 encoder support'
            'rav1e: rav1e encoder support'
            'libvpx: vpx encoder support'
            'vmaf: VMAF calculation support'
            'mkvtoolnix-cli: mkvmerge support'
            'ffms2: FFMS2 chunk detection support'
            'vapoursynth-plugin-lsmashsource: L-SMASH chunk detection support')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/master-of-zen/Av1an/archive/refs/tags/$pkgver.tar.gz)
sha256sums=('68169f0c8760cb7d42ab484a969ca5f092551352998d89c602ac3527542e6538')

build() {
  cd "Av1an-${pkgver}"
  cargo build --release --locked
}

package() {
  cd "Av1an-${pkgver}"
  install -Dm755 target/release/av1an "${pkgdir}/usr/bin/av1an"
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE.md
}
