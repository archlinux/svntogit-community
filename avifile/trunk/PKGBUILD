# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=avifile
pkgver=0.7.45
pkgrel=9
pkgdesc="A library that allows you to read and write compressed AVI files"
arch=('i686' 'x86_64')
depends=('qt3' 'sdl' 'freetype2' 'libpng' 'libjpeg'
         'libvorbis' 'xvidcore' 'libxxf86dga' 'libxi' 'libxv')
makedepends=('lame' 'v4l-utils' 'chrpath')
url="http://avifile.sourceforge.net"
license=('GPL')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/avifile/$pkgname-0.7-$pkgver.tar.bz2"
	'gcc.patch'
	'videodev.patch')
md5sums=('7da94802f120d1b69e04a13170dcd21d'
         '79a488db80c82f0abb8a83f52e3b3aa0'
         '6cca050885afe85c2d98982f75f5e3b0')

build() {
  cd $pkgname-0.7-$pkgver

  patch -Np1 <$srcdir/gcc.patch
  patch -Np1 <$srcdir/videodev.patch

 ./configure \
    --prefix=/usr \
    --mandir=$pkgdir/usr/share/man \
    --with-qt-prefix=/usr \
    --with-qt-includes=/usr/include/qt3 \
    --with-qt-libraries=/usr/lib \
    --enable-oss

  make
}

package() {
  cd $pkgname-0.7-$pkgver
  make prefix=$pkgdir/usr install

  cd $pkgdir/usr/bin
  chrpath -d avibench avicap avicat avirec kv4lsetup avimake aviplay avitype avirecompress
}
