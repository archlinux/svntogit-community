# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=avldrums.lv2
_commit=00ef993200f2318d8124cd07a75ee0ef02f35f9c  # refs/tags/v0.5.0
pkgver=0.5.0
pkgrel=1
pkgdesc="A simple Drum Sample Player Plugin, dedicated to the AVLinux Drumkits"
arch=(x86_64)
url="https://x42-plugins.com/x42/x42-avldrums"
license=(GPL2)
groups=(pro-audio lv2-plugins)
depends=(cairo glibc libglvnd libx11 lv2-host)
makedepends=(fluidsynth git glib2 glu lv2 pango)
checkdepends=(lv2lint)
options=(debug)
source=(
  git+https://github.com/x42/$pkgname.git#commit=$_commit
  git+https://github.com/x42/robtk
  $pkgname-0.4.2-use-system-fluidsynth.patch
)
sha512sums=('SKIP'
            'SKIP'
            'a47ac0c731e843000bd0b05e58ed2f1159df596ac2303583ebecd3010cb381506cd704fcd7c187ba45961cffb0c5b73ea01b19fc4c75a2bf376e6d7596d17d33')
b2sums=('SKIP'
        'SKIP'
        'aeff7bd322e9e1fa36c36096cdbfc8f3db9a0a726eda27f9f38375c6b887acddaca4ab907ccf59c633d8813c4d992ffdc23524e5beb4209f851cacef9750c577')

prepare() {
  (
    cd $pkgname
    git submodule init
    git config submodule.robtk.url "$srcdir/robtk"
    git submodule update
  )

  # build against system fluidsynth
  patch -Np1 -d $pkgname -i ../$pkgname-0.4.2-use-system-fluidsynth.patch
}

build() {
  make -C $pkgname
}

check() {
  for _name in BlackPearl BlackPearlMulti RedZeppelin RedZeppelinMulti; do
    lv2lint -Mpack -I "$pkgname/build/" "http://gareus.org/oss/lv2/avldrums#$_name"
  done
}

package() {
  depends+=(libfluidsynth.so libgobject-2.0.so libpango-1.0.so libpangocairo-1.0.so)

  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
}
