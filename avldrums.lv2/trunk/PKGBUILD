# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=avldrums.lv2
pkgver=0.4.1
pkgrel=1
pkgdesc="A simple Drum Sample Player Plugin, dedicated to the AVLinux Drumkits"
arch=('x86_64')
url="https://x42-plugins.com/x42/x42-avldrums"
license=('GPL2')
groups=('pro-audio' 'lv2-plugins')
depends=('cairo' 'glib2' 'glibc' 'libfluidsynth.so' 'libglvnd' 'libx11' 'pango')
makedepends=('git' 'glu' 'lv2')
source=("$pkgname-$pkgver::git+https://github.com/x42/${pkgname}.git#tag=v${pkgver}"
        "git+https://github.com/x42/robtk"
        "${pkgname}-${pkgver}-use-system-fluidsynth.patch")
sha512sums=('SKIP'
            'SKIP'
            '849123389ea7373251ae02fd5fdac0483a0cd000360f324af11e29269df720a0ec5501911cb92ed41a3211dabd0c868c6b626923b8a648a7fc5ff308fe3c18e5')

prepare() {
  cd "${pkgname}-${pkgver}"
  git submodule init
  git config submodule.robtk.url "${srcdir}/robtk"
  git submodule update
  # build against system fluidsynth
  patch -Np1 -i "../${pkgname}-${pkgver}-use-system-fluidsynth.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
}
