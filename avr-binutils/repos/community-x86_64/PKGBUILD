# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-binutils
pkgver=2.24
pkgrel=3
pkgdesc="A set of programs to assemble and manipulate binary and object files for the AVR architecture"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('glibc>=2.17' 'zlib' 'binutils')
options=('staticlibs' '!distcc' '!ccache')
install=avr-binutils.install
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        binutils-2.24-CVE-2014-8484.patch
        binutils-2.24-CVE-2014-8485.patch
        binutils-2.24-CVE-2014-8501.patch
        binutils-2.24-CVE-2014-8502.patch
        binutils-2.24-CVE-2014-8503.patch
        binutils-2.24-CVE-2014-8504.patch
        binutils-2.24-CVE-2014-8737.patch
        binutils-2.24-CVE-2014-8738.patch
        binutils-2.24-strings-all-default.patch
        avr-size.patch)

_builddir=binutils-build

prepare() {
    cd ${srcdir}/binutils-${pkgver}

    # See https://bugs.archlinux.org/task/32504
    # And https://bugs.archlinux.org/task/40986
    patch -Np0 < ${srcdir}/avr-size.patch

    # https://bugs.archlinux.org/task/34629
    sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

    # CVE-2014-8484 - backport of commit bd25671c
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8484.patch

    # CVE-2014-8485 - commit 493a3386
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8485.patch

    # CVE-2014-8501 - commit 7e1e1988
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8501.patch

    # CVE-2014-8502 - commits bf67003b and 5a4b0ccc
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8502.patch

    # CVE-2014-8503 - commit 0102ea8c
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8503.patch

    # CVE-2014-8504 - commit 708d7d0d
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8504.patch

    # CVE-2014-8737 - commit dd9b91de
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8737.patch

    # CVE-2014-8738 - commit bb0d8671
    patch -p1 -i $srcdir/binutils-2.24-CVE-2014-8738.patch

    # strings -a default - commit 7fac9594 (fedora backport)
    patch -p0 -i $srcdir/binutils-2.24-strings-all-default.patch

    rm -rf ${srcdir}/${_builddir}
    mkdir ${srcdir}/${_builddir}
}

build() {
    cd ${srcdir}/binutils-${pkgver}

    config_guess=$(./config.guess)

    cd ${srcdir}/${_builddir}

    ${srcdir}/binutils-${pkgver}/configure \
        --prefix=/usr \
        --with-lib-path=/usr/lib:/usr/local/lib \
        --with-bugurl=https://bugs.archlinux.org/ \
        --enable-ld=default \
        --enable-gold \
        --enable-plugins \
        --enable-threads \
        --with-pic \
        --enable-shared \
        --disable-werror \
        --disable-multilib \
        --build=${config_guess} \
        --target=avr \
        $CONFIGFLAG

    # This checks the host environment and makes sure all the necessary tools are available to compile Binutils.
    make configure-host

    make tooldir=/usr
}

package() {
    cd ${srcdir}/${_builddir}

    make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install

    for bin in ar as nm objcopy objdump ranlib strip ; do
        rm -f ${pkgdir}/usr/bin/${bin}
    done

    for info in as bfd binutils configure gprof ld standards; do
        mv ${pkgdir}/usr/share/info/${info}.info ${pkgdir}/usr/share/info/avr-${info}.info
    done

    rm -rf ${pkgdir}/usr/share/locale
}

md5sums=('e0f71a7b2ddab0f8612336ac81d9636b'
         'SKIP'
         '125d8ce675ced84814d5f068106dbaa3'
         '6923e5279a84cb7134b07e2cf6686434'
         '4c590dc70829d8f4fc190f0fe7e3add8'
         '64efddd24d5628437cc457b468df8de3'
         '70540e2c6418a54ad88ab9c6b988d075'
         'acce1f1d28db6bdb84b430a05f331366'
         '12fb3e860203a06b5d1f4b023c06dcec'
         'd35b0ccb79d565757821b3e0206f3873'
         '99445d528110543d88b59a1ec7677592'
         '7f62e260650ceb0ff2f7cb44c3ee899c')
