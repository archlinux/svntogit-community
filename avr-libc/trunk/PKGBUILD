# $Id$
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-libc
pkgver=2.0.0
pkgrel=2
pkgdesc='The C runtime library for the AVR family of microcontrollers'
arch=(any)
url='http://savannah.nongnu.org/projects/avr-libc/'
license=(BSD)
depends=(avr-gcc)
options=(!strip) # it corrupts avr libraries
source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha1sums=('c46b4c92bd88339f4d8d9bce29893661be890845'
          'SKIP')
validpgpkeys=('5E84F980C3CAFD4BB5841070F48CA81B69A85873') # Joerg Wunsch

build() {
  cd avr-libc-$pkgver
  ./bootstrap
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd avr-libc-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
