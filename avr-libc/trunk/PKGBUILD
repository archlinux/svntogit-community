# $Id$
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

# Build order: avr-binutils -> avr-gcc -> avr-libc

pkgname=avr-libc
pkgver=1.8.1
pkgrel=2
pkgdesc="The C runtime library for the AVR family of microcontrollers"
arch=('any')
url="http://savannah.nongnu.org/projects/avr-libc/"
license=('BSD')
depends=('avr-gcc')
options=('!strip')
source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
md5sums=('0caccead59eaaa61ac3f060ca3a803ef'
         'SKIP')
validpgpkeys=('5E84F980C3CAFD4BB5841070F48CA81B69A85873') # Joerg Wunsch

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
