# $Id$
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=avrdude
pkgver=6.3
pkgrel=3
epoch=1
pkgdesc="Download/upload/manipulate the ROM and EEPROM contents of AVR microcontrollers"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/avrdude/"
license=('GPL')
depends=('readline' 'libftdi' 'libusb-compat' 'elfutils')
source=(http://download.savannah.gnu.org/releases/avrdude/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('EF497ABE47ED91B3FC3D7EA54D902FF7723BDEE9' '5E84F980C3CAFD4BB5841070F48CA81B69A85873')
md5sums=('58bb42049122cf80fe4f4d0ce36d92ee'
         'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

  	./configure --mandir=/usr/share/man \
		    --prefix=/usr \
		    --sysconfdir=/etc \
		    --enable-linuxgpio
  	make
}

package() {
  	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}
