# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org> 
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.4.6
pkgrel=1
pkgdesc="A tiling window manager initialy based on a dwm code rewriting."
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
source=($url/download/$pkgname-$pkgver.tar.bz2
	'awesome.desktop')
install=awesome.install
license=('GPL2')
depends=('pango' 'imlib2' 'lua' 'libev' 'dbus' 'libxdg-basedir' 'startup-notification')
makedepends=('docbook-xsl' 'asciidoc' 'xmlto' 'doxygen' 'gperf' 'cmake' 'luadoc' 'imagemagick')
optdepends=('rlwrap: readline support for awesome-client')
options=('!libtool' 'docs')
sha1sums=('48b40e23b56de7a7d51f815353aa4eb2ee5dab08'
          '896f3d70a03e51ef9d4d6121ce85bba9177b2fcb')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make CMAKE_ARGS=" -DPREFIX=/usr -DSYSCONFDIR=/etc \
	-DCMAKE_BUILD_TYPE=RELEASE"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

 # install desktop file so you can start awesome from your login manager
 install -Dm644 ${srcdir}/awesome.desktop \
        ${pkgdir}/usr/share/xsessions/awesome.desktop
}
