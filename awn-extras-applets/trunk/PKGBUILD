# $Id$
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Jonathan Liu <net147@hotmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=awn-extras-applets
pkgver=0.4.0
pkgrel=5
pkgdesc="A collection of applets for avant-window-navigator"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn-extras"
license=('GPL2')
depends=('avant-window-navigator' 'gnome-menus' 'libgtop' 'libnotify' 
	 'libsexy' 'vte' 'gstreamer0.10-python')
makedepends=('intltool' 'libdesktop-agnostic' 'vala')
optdepends=('python-notify: needed for some applets')
provides=('avant-window-navigator-extras')
install=awn-extras-applets.install
options=('!libtool')
source=(https://launchpad.net/awn-extras/0.4/${pkgver}/+download/${pkgname/-applets/}-${pkgver}.tar.gz)
md5sums=('b559d68cd6ad295c961c20fcc5d9f9c0')

build() {
	cd ${srcdir}/${pkgname/-applets/}-${pkgver}
	./configure --prefix=/usr --sysconfdir=/etc --disable-pymod-checks
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname/-applets/}-${pkgver}
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

	mkdir -p ${pkgdir}/usr/share/gconf/schemas
	gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/awn-extras.schemas \
	    ${pkgdir}/etc/gconf/schemas/*.schemas
	rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
	rmdir --ignore-fail-on-non-empty -p ${pkgdir}/usr/share/locale
	rmdir --ignore-fail-on-non-empty -p ${pkgdir}/etc/gconf/schemas
}
