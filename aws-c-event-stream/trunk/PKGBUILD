# Maintainer: Anatol Pomozov

pkgname=aws-c-event-stream
pkgver=0.2.15
pkgrel=1
pkgdesc='C99 implementation of the vnd.amazon.eventstream content-type'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-event-stream'
license=(Apache)
depends=(aws-c-common aws-checksums aws-c-io)
makedepends=(cmake)
source=(aws-c-event-stream-$pkgver.zip::https://github.com/awslabs/aws-c-event-stream/archive/v$pkgver.zip)
sha256sums=('7c932e977798c0d943ed5373642c30cd27b82cbd7a800104ded4493d192fa747')

build() {
  cd aws-c-event-stream-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-event-stream-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-event-stream-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
