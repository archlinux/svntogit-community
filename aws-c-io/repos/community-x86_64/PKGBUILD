# Maintainer: Anatol Pomozov

pkgname=aws-c-io
pkgver=0.13.7
pkgrel=1
pkgdesc='AWS SDK module to handle all IO and TLS work for application protocols'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-io'
license=(Apache)
depends=(aws-c-common aws-c-cal s2n-tls)
makedepends=(cmake)
source=(aws-c-io-$pkgver.zip::https://github.com/awslabs/aws-c-io/archive/v$pkgver.zip)
sha256sums=('9c57f39b5aff2650e571752da92e97ff54673d600d5679fd8361ee91ea7f8c6b')

build() {
  cd aws-c-io-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-io-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-io-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
