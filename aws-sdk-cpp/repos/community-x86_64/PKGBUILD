# Maintainer: Anatol Pomozov

pkgname=aws-sdk-cpp
pkgver=1.9.241
pkgrel=1
pkgdesc='AWS SDK for C++'
arch=(x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache)
depends=(openssl curl zlib util-linux-libs aws-c-common aws-c-event-stream libpulse aws-crt-cpp)
makedepends=(cmake ninja)
source=(aws-sdk-cpp-$pkgver.zip::https://github.com/aws/aws-sdk-cpp/archive/$pkgver.zip)
sha256sums=('85d06ed0dcac0869e89e1f00e84a63aa2fff8aef6bd1a743c652c020a2dd4772')

build() {
  cd aws-sdk-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCUSTOM_MEMORY_MANAGEMENT=OFF -DENABLE_TESTING=OFF -DBUILD_DEPS=OFF \
      -DCMAKE_MODULE_PATH=/usr/lib/cmake \
      -S . -B build -G Ninja
  ninja -C build -j 2 # the build is so heavy that it gets OOM killed even at powerful machines
}

package() {
  cd aws-sdk-cpp-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
