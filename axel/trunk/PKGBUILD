# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Roman G <drakosha@au.ru>

pkgname=axel
pkgver=2.16.1
pkgrel=1
pkgdesc="Light command line download accelerator"
arch=('x86_64')
url="https://github.com/axel-download-accelerator/axel"
license=('GPL')
depends=('glibc' 'openssl')
source=(https://github.com/axel-download-accelerator/axel/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('d34094eb63eaebc989f96eabdda091dcfb28e840bb8d274a5f216775941f59e4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
