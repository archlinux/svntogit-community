# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

pkgname=babel-cli
pkgver=7.16.8
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs' 'babel-core' 'semver')
makedepends=('npm')
provides=('nodejs-babel-cli')
conflicts=('nodejs-babel-cli' 'openbabel<3')
replaces=('nodejs-babel-cli')
source=("$pkgname-$pkgver.tgz::https://registry.npmjs.org/@babel/cli/-/cli-$pkgver.tgz"
        LICENSE)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('1532816f1ca4e537255ce1a6c18caa7a5a8fe4817a84cc5a789b2477ce636d1e6305f625c2a83001b2709e2c62d5905b4ea29f16e000f7999d99415e491c3224'
            'fc5471459770967e2632efb110b8370caa3424b7d87c1180f49605f7e6b1593746888b6c99c713c0c3046c5833c01c1a15bf3d549a343eaa077885bcc9f3d4e3')

package() {
  npm install -g --legacy-peer-deps --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  rm -r "$pkgdir"/usr/lib/node_modules/@babel/cli/node_modules/{,.bin/}semver

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
