# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=backuppc
pkgver=3.2.1
pkgrel=9.1
pkgdesc='Enterprise-grade system for backing up Linux, Windows and MacOS PCs'
url='http://backuppc.sourceforge.net/'
license=('GPL2')
arch=('any')
depends=(
  'openssh'
  'par2cmdline'
  'perl-archive-zip'
  'perl-file-listing'
  'perl-time-modules'
  'perl-xml-rss'
  'smbclient'
)
optdepends=(
  'rsync: used by rsync transfert method'
  'perl-file-rsyncp: used by rsync transfert method'
  'perl-io-dirent'
  'apache: used by web admin interface'
  'mod_perl: used by web admin interface'
)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/BackupPC-$pkgver.tar.gz"
        "$pkgname.service"
        "$pkgname.profile.sh"
        "$pkgname.profile.csh"
        "$pkgname.httpd"
        '01-fix-parenthesis-warnings.patch'
        '02-fix-cve-2011-4923.patch'
        '03-fix-FS#32642.patch')
install=$pkgname.install
backup=("etc/$pkgname/config.pl"
        "etc/$pkgname/hosts"
        "etc/httpd/conf/extra/$pkgname.conf"
        "etc/$pkgname/$pkgname.users")
md5sums=('2334fafb8e03284225a9b8a7fb230012'
         'fa5da622f87d8801065ba312f4d547b6'
         '67a939aa63740c52d12bbdca72d37891'
         'ef09e4dae5b4197998f5c3a74e0ec86d'
         'c61dea98b2a2a0c944a0a6be44e4caf3'
         'dff7ae1530929ae49f8b34a240a04131'
         'aca8392c5dea60c3cceeb02ebcc63497'
         '3917c474a275e262fde246daf59ffe31')

package() {
  cd BackupPC-$pkgver
  for _p in "$srcdir"/*.patch; do
    msg2 "Applying patch $_p"
    patch -p1 -N -i "$_p"
  done
  perl configure.pl \
    --batch \
    --no-set-perms \
    --uid-ignore \
    --hostname __HOSTNAME__ \
    --compress-level 5 \
    --backuppc-user=backuppc \
    --dest-dir "$pkgdir" \
    --fhs \
    --install-dir /usr/share/$pkgname \
    --config-dir /etc/$pkgname \
    --cgi-dir /usr/share/$pkgname/cgi-bin \
    --html-dir /usr/share/$pkgname/html \
    --html-dir-url /backuppc \
    --data-dir /var/lib/$pkgname \
    --log-dir /var/log/$pkgname
  cd "$srcdir"
  # systemd
  install -D -m 644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # install shell profiles
  install -D -m 755 $pkgname.profile.sh "$pkgdir/etc/profile.d/$pkgname.sh"
  install -D -m 755 $pkgname.profile.csh "$pkgdir/etc/profile.d/$pkgname.csh"
  # install apache http
  install -D -m 644 $pkgname.httpd "$pkgdir/etc/httpd/conf/extra/$pkgname.conf"
  install -D -m 640 /dev/null "$pkgdir/etc/$pkgname/$pkgname.users"
  # set correct permissions
  cd "$pkgdir"
  chown 126:126 -R etc/$pkgname
  chown 126:126 -R var/{log,lib}/$pkgname
  chmod 755 etc
  chmod 755 -R var usr/share/$pkgname/{cgi-,}bin
  chmod 750 var/log/$pkgname etc/$pkgname var/lib/$pkgname
  chmod 640 etc/$pkgname/{config.pl,hosts}
}

# vim:set ts=2 sw=2 et:
