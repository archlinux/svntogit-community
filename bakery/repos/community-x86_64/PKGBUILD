# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=bakery
pkgver=2.6.3
pkgrel=2
pkgdesc='A C++ Application Framework for use with gtkmm'
arch=('i686' 'x86_64')
url='http://bakery.sourceforge.net/'
license=('GPL')
depends=('libxml++' 'gnome-vfsmm' 'libglademm' 'gconfmm>=2.18.1' 'gtkmm')
makedepends=('intltool')
install=bakery.install
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('49d28fecf13252f4f2899461505e56e5')
options=('!libtool')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/share/gconf/schemas

  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    ${pkgdir}/etc/gconf/schemas/*.schemas

  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
