# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.5.2
pkgrel=3
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
url='http://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'webkitgtk' 'libesmtp' 'libnotify' 'gpgme' 'gtksourceview3' 'gtkspell3' 'desktop-file-utils')
makedepends=('yelp-tools' 'intltool')
changelog=$pkgname.changelog
source=(http://pawsa.fedorapeople.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('01218dd8cb2c052c2d221d96e4ef5e277271befb2b50bbf4913223913214d21d')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-ssl \
      --with-gpgme=gpgme-config \
      --with-gss \
      --with-ldap \
      --with-gtksourceview \
      --with-gtkspell \
      --with-rubrica \
      --with-sqlite \
      --without-nm \
      --without-gnome \
      --with-html-widget=webkit
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
