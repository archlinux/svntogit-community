# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Balló György <ballogyor+arch@gmail.com>

pkgname=bamf
pkgver=0.5.3
pkgrel=2
pkgdesc='Application matching framework'
arch=('i686' 'x86_64')
url='https://launchpad.net/bamf'
license=('GPL')
depends=('libgtop' 'libwnck3')
makedepends=('gobject-introspection' 'python2-lxml' 'vala')
source=("https://launchpad.net/bamf/${pkgver%.*}/${pkgver}/+download/bamf-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('45B1103FB93ACBD90296DBCAE83D089481836EBF') # Marco Trevisan
sha256sums=('340d7b5a463afcd33135ae73a7e4cf6a93a4daaf6f72b70abc09dc66d18a3b14'
            'SKIP')

build() {
  cd bamf-${pkgver}

  export PYTHON='/usr/bin/python2'
  export CFLAGS="$CFLAGS -Wno-deprecated-declarations"

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --disable-static \
    --disable-webapps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd bamf-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
