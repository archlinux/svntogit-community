# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Andre Schr√∂der <andre.schroedr at gmail dot com>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=bash-bats
pkgver=1.5.0
pkgrel=1
pkgdesc="Bash Automated Testing System"
arch=("any")
url="https://github.com/bats-core/bats-core"
license=("MIT")
depends=('bash')
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha256sums=('36a3fd4413899c0763158ae194329af8f48bb1ff0d1338090b80b3416d5793af')

check() {
  cd "bats-core-${pkgver}"
  TERM=linux bin/bats --tap test
}

package() {
  cd "bats-core-${pkgver}"

  # Also rename the bats-core/ folder to bats/
  sed -i 's/BATS_ROOT\/libexec\/bats-core\/bats/BATS_ROOT\/lib\/bats\/bats/g' bin/bats

  # Rename the bats-core/ folder to bats/
  sed -i 's/BATS_ROOT\/lib\/bats-core\//BATS_ROOT\/lib\/bats\//g' libexec/bats-core/*

  for fn in libexec/bats-core/*; do
    install -Dm755 ${fn} \
      "${pkgdir}"/usr/lib/bats/$(basename ${fn})
  done

  install -Dm755 bin/bats "${pkgdir}/usr/bin/bats"

  install -Dm755 lib/bats-core/* -t "${pkgdir}/usr/lib/bats"

  install -Dm644 man/bats.1 "${pkgdir}/usr/share/man/man1/bats.1"
  install -Dm644 man/bats.7 "${pkgdir}/usr/share/man/man7/bats.7"
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

}
