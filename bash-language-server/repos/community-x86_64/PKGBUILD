# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=2.0.0
pkgrel=5
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/bash-lsp/$pkgname"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "git")
optdepends=('bash-completion: for CLI args completion')
source=("$url/archive/server-$pkgver.tar.gz")
sha512sums=('e72df6f13ed4b861b81329059d41cdfada2729e9523e619ef26d6ed0a2a5400a3dda659b4f4af923a2a9fb2054b99b9d6db082508f2ba06ac0f9a421516e2ce9')

build() {
    cd "$pkgname-server-$pkgver"
    yarn
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$srcdir/$pkgname-server-$pkgver/LICENSE"

    rm -r node_modules/acorn
    mkdir -p node_modules/acorn/bin
    ln -s /usr/bin/acorn node_modules/acorn/bin/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
