# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=bash-language-server
pkgver=3.3.1
pkgrel=1
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/bash-lsp/bash-language-server"
license=("MIT")
depends=("nodejs")
makedepends=("git" "jq" "typescript" "yarn")
optdepends=('bash-completion: for CLI args completion'
            'man-db: for documentation via manual pages'
            'shellcheck: to enable linting')
source=("$pkgname-$pkgver.tar.gz::$url/archive/server-$pkgver.tar.gz")
b2sums=('f4f41b136a2cc95b2eb249b772de7403f8579a08d252fd4f0760089be36681d0704de753ebccc3fff66329f094fb8d5f9dc9b6d12e66a6f11310f4cb0cca6640')

build() {
    cd "$pkgname-server-$pkgver"
    yarn
    yarn run compile
}

package() {
    cd "$pkgname-server-$pkgver/server"

    # Emulate `npm prune --production`
    cp package.json{,.bak}
    read -ra devDependencies < <(jq -r '.devDependencies | keys | join(" ")' package.json)
    yarn remove --frozen-lockfile "${devDependencies[@]}"
    rm -r node_modules/@types
    mv package.json{.bak,}

    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$srcdir/$pkgname-server-$pkgver/LICENSE"

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
