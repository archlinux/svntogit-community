# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andreas Wagner <A.Wagner@stud.uni-frankfurt.de>

pkgname=bashburn
pkgver=3.0.1
pkgrel=2
pkgdesc="A cd burning shell script"
arch=('any')
url="http://bashburn.dose.se/"
license=('GPL2')
changelog=$pkgname.changelog
depends=('bash' 'cdrdao' 'cdrkit' 'eject')
optdepends=('dvd+rw-tools: support for burning DVDs'
            'cdparanoia: Ripping audio CDs'
            'vorbis-tools: Vorbis-Support'
            'flac: Flac-Support'
            'normalize: Normalize audio'
            'mpg123: MP3-Support'
            'sudo: For missing permissions')
source=(BashBurn-${pkgver}.tar.gz::"http://bashburn.dose.se/index.php?s=file_download&id=18")
sha256sums=('afb16a440d445ea375480af3a66bd0983dd67e7e8e8b093494c57bce24b2deff')

package() {
  cd ${srcdir}/${pkgver}

# let it install in the correct location
  sed -i 's|bblib=${lib}/lib|bblib=${lib}|' Install.sh

# install
  ./Install.sh --prefix=${pkgdir}/usr

# fix executable
  sed -i 's|'$pkgdir'||' ${pkgdir}/usr/bin/$pkgname

# remove useless docs (changelog and such)
  rm -rf ${pkgdir}/usr/lib/Bashburn/docs
}
