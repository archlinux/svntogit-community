# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=bashdb
pkgver=4.2_0.8
pkgrel=2
pkgdesc='Debugger for Bash scripts loosely modeled on the gdb command syntax'
arch=('any')
url="http://bashdb.sourceforge.net/"
license=('GPL')
depends=('bash' 'python2-pygments')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-${pkgver/_/-}.tar.bz2 python2.patch)
md5sums=('f55afbc518d82647e2989a86954fadda'
         '7e2fdd6ecb69cfd2cc2dba33c700c800')

build() {
  cd $pkgname-${pkgver//_/-}
  patch -Np1 -i $srcdir/python2.patch
  ./configure --prefix=/usr --disable-static
  make
}
package() {
  cd $pkgname-${pkgver//_/-}
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/usr/share/info/dir
}
