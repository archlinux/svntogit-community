# $Id$
# Maintainer: Thayer Williams <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=bbpager
pkgver=0.4.7
pkgrel=4
pkgdesc='Dockable pager for Blackbox and other window managers'
arch=(i686 x86_64)
url="http://bbtools.sourceforge.net/download.php?file=6"
license=("GPL")
depends=('gcc-libs' 'libxft' 'libxext')
makedepends=('gcc' 'blackbox')
source=(http://downloads.sourceforge.net/bbtools/$pkgname-$pkgver.tar.gz \
        02_gcc43.dpatch \
        fix-linking-ugly.diff)
md5sums=('4eaa91a4d66130af2f7a09c25f917386'
         'e985ef893f2080b3b2d47f900f861352'
         'd2258a0cfb580e20869574f373fe7ed4')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np1 -i ../02_gcc43.dpatch
  patch -Np1 -i ../fix-linking-ugly.diff
  autoreconf

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix="$pkgdir/usr" install
}
