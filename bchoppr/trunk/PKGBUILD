# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BChoppr
pkgname=bchoppr
pkgver=1.12.2
pkgrel=1
pkgdesc="An audio stream chopping LV2 plugin"
arch=(x86_64)
url="https://github.com/sjaehn/BChoppr"
license=(GPL3)
groups=(lv2-plugins pro-audio)
depends=(cairo gcc-libs glibc libx11)
makedepends=(lv2)
checkdepends=(lv2lint)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/sjaehn/$_name/archive/$pkgver.tar.gz)
sha512sums=('da1224a072e5f0edf9b842dfec3c3a7f5a3807319f7c3500f880d413eb3743757500ff2d45c82a089f1f407ba3ca9587765bba727b544ee6ec7089444506fabe')
b2sums=('dd3b2206041ee957277607ed43fa4f5a3e942682c3ef5cc18b66052ed1d40bf29a5873f238692b79483f3578547d89f50add1b3fc726208f2dc9fc6e5f0a791a')

build() {
  make PREFIX=/usr -C $_name-$pkgver
}

check() {
  cd $_name-$pkgver
  # ignore symbols that cause issues with higher versions of gcc: https://github.com/sjaehn/BChoppr/issues/15
  lv2lint -Mpack -s "*basic_string*" -s "*codecvt_utf8*" -I "$PWD/$_name.lv2/" "https://www.jahnichen.de/plugins/lv2/$_name"
}

package() {
  depends+=(lv2-host)

  make DESTDIR="$pkgdir" PREFIX=/usr install -C $_name-$pkgver
  install -vDm 644 $_name-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # remove license file
  find "$pkgdir/usr/lib/" -type f -iname "*LICENSE*" -delete
}
