# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Firef0x <Firefgx {at) gmail [dot} com>
# Contributor: MJsaka <qiuxuenan@gmail.com>
# Copyright
# This PKGBUILD released with the GPLv3 license

pkgname=bcloud
pkgver=3.7.1
pkgrel=1
pkgdesc='Baidu Pan client for Linux Desktop users'
arch=('any')
license=('GPL3')
url="https://github.com/LiuLang/bcloud"
depends=('python-crypto' 'libnotify' 'python-keyring' 'python-dbus' 'python-gobject'
         'gnome-icon-theme-symbolic' 'python-lxml' 'python-cssselect' 'gtk3')
optdepends=('gnome-keyring: To store password in GNOME Keyring'
            'libgnome-keyring: To store password in GNOME Keyring'
            'kdebindings-python: To store password in KWallet'
            'kwallet: To store password in KWallet')
install=bcloud.install
source=("https://pypi.python.org/packages/source/b/bcloud/${pkgname}-${pkgver}.tar.gz")
md5sums=('0fdcd8daa58f4fcf4f746a85226835e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python setup.py install -O1 --skip-build --root="$pkgdir"
}
