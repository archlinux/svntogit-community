# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bctoolbox
pkgver=4.5.20
pkgrel=1
pkgdesc='A library written in C that allows you to create and run audio and video streams'
arch=(x86_64)
url='https://github.com/BelledonneCommunications/bctoolbox'
license=(GPL)
depends=(mbedtls bcunit)
makedepends=(cmake)
source=(https://github.com/BelledonneCommunications/bctoolbox/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('93b855c518e3d324e40479834a8fddfc36abefd4fb6b519a7a01e942d7411c2d')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
