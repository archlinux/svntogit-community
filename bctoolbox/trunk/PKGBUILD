# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bctoolbox
pkgver=4.4.9
pkgrel=1
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=(x86_64)
url="https://github.com/BelledonneCommunications/bctoolbox"
license=(GPL)
depends=(mbedtls bcunit)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/BelledonneCommunications/bctoolbox/archive/$pkgver.tar.gz")
sha256sums=('d96586b30999fd41d720150b52d334bbb5bbb419b8d5900a8a037f2617bf1d6b')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
