# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tyler Langlois <ty |at| tjll |dot| net>

pkgbase=beats
pkgname=(filebeat packetbeat metricbeat heartbeat-elastic auditbeat journalbeat)
pkgver=6.6.0
pkgrel=1
pkgdesc='Data shippers for Elasticsearch'
arch=('x86_64')
url='https://www.elastic.co/products/beats'
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git' 'libpcap' 'rsync' 'python' 'audit')
optdepends=('elasticsearch: for standalone installation'
            'python: for migration script')
source=("https://github.com/elastic/beats/archive/v$pkgver/beats-$pkgver.tar.gz"
        "golang-crypto.patch" # golang/crypto patch
        "filebeat.sysusers"
        "filebeat.service"
        "packetbeat.service"
        "metricbeat.service"
        "heartbeat.service"
        "auditbeat.service"
        "journalbeat.service"
        "tmpfile.conf")
sha256sums=('0e55beab8945ba4c8035afcd22ae263d2c955dfc47e420d5231780ab53391342'
            '75583a195b43d7345449bbac60359bf54661ccbd699386a6e59bb483c6054338'
            '33feb3690f8b31563cc1e2da557c2aa326501ce9ccd7e0a142036902bfdb05ff'
            '4d5394cfe3299c9a6af5ac34c1ad4bcdc2e79162379cccbf00fb414eb28253c0'
            '9f4e33fc768d3500fa3428dcbfda241c6473a2cae8cc4dbadf8f61ca9f2bbec5'
            '26b123acd96808162a7f827ce6f0e9669140c2665fc28d76583a15996fbf853d'
            '212cb4ab5a02d7078025f7d1f50f48e5caa25debce0852cf68cd8f89ed0a8d09'
            'b7f1c61950f21c7fab31166c68d4a9e5027f0c5bcc40a97913d6d89eaf643b85'
            '57d2f5bf75b4b5214065ba408c98ec941fb0042f19a623add7f4396d640225ba'
            'f29361772f23ec42bb15b5e0ca7e93f17fe00d31cf08a9e2c0d0ab27f2277bbc')

prepare() {
    export GOPATH="$srcdir"/gopath
    mkdir -p "$GOPATH"
    go get golang.org/x/sys/cpu

    cd "$srcdir"/beats-$pkgver/vendor/golang.org/x/crypto
    patch -p1 -i "$srcdir"/golang-crypto.patch

    cd "$srcdir"/beats-$pkgver

    # Avoid installing extraneous configs
    sed '/[- ]win/d ; /[- ]darwin/d ; /[- ]binary/d' -i */Makefile

    # Install the Linux config as default
    sed -r 's#/([a-z]+)-linux.yml#/\1.yml#' -i */Makefile

    # Perform some timestomping to avoid make warnings
    LANG=C _t="$(date -r Makefile +'%Y-%m-%d %k:%M:%S')"
    touch -m -d "$_t" */Makefile

    # Use version instead of commit id
    sed -ri "s/^COMMIT_ID=.*/COMMIT_ID=$pkgver/" libbeat/scripts/Makefile

    # Workaround to place extracted release into GOPATH
    mkdir -p "$GOPATH"/src/github.com/elastic
    ln -sf ../../../../beats-$pkgver "$GOPATH"/src/github.com/elastic/beats
}

build() {
    export GOPATH="$srcdir"/gopath
    export PATH="$GOPATH/bin:$PATH"

    cd "$GOPATH"/src/github.com/elastic/beats
    git init # git root required by one of the build scripts

    cd libbeat
    make fields

    for beat in ${pkgname[@]}; do
        beat="${beat%-elastic}"
        msg2 "Building $beat..."
        cd ../$beat
        make
        make fields

        if grep -Eq '^kibana:' Makefile; then
            make kibana
            mv _meta/kibana{.generated,}
        fi
    done
}

_do_package_beat() {
    _pkgname="${pkgname%-elastic}"

    backup=(etc/$_pkgname/$_pkgname.yml)

    install -dm755 "$pkgdir"/{etc,usr/share,usr/share/licenses,var/lib}/$_pkgname
    install -dm755 "$pkgdir"/usr/lib/tmpfiles.d

    cd "$srcdir"/beats-$pkgver
    install -Dm644 NOTICE.txt "$pkgdir"/usr/share/licenses/$pkgname/NOTICE.txt

    cd $_pkgname

    cp $_pkgname.{yml,reference.yml} "$pkgdir"/etc/$_pkgname
    install -Dm644 fields.yml "$pkgdir"/etc/$_pkgname/fields.yml

    install -Dm755 $_pkgname \
                   "$pkgdir"/usr/bin/$_pkgname
    install -Dm644 "$srcdir"/$_pkgname.service \
                   "$pkgdir"/usr/lib/systemd/system/$_pkgname.service

    sed "s/BEATNAME/$_pkgname/g" "$srcdir"/tmpfile.conf > "$pkgdir"/usr/lib/tmpfiles.d/$_pkgname.conf

    if [ -d _meta/kibana ]; then
        cp -R _meta/kibana "$pkgdir"/usr/share/$_pkgname/
    fi

    if [ -d module ]; then
        cp -R module "$pkgdir"/usr/share/$_pkgname/
        rm -rf "$pkgdir"/usr/share/$_pkgname/module/*/{,info/}_meta
    fi

    if [ -d modules.d ]; then
        cp -R modules.d "$pkgdir"/etc/$_pkgname/
    fi
}

package_filebeat() {
    pkgdesc='Sends log files to Logstash or directly to Elasticsearch'

    _do_package_beat

    cd "$srcdir"/beats-$pkgver/$pkgname
    install -Dm644 "$srcdir"/$pkgname.sysusers \
                   "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
}

package_packetbeat() {
    pkgdesc='Analyzes network traffic and sends the data to Elasticsearch'
    depends=('libpcap')

    _do_package_beat
}

package_metricbeat() {
    pkgdesc='Server monitoring agent that sends metrics to Elasticsearch'
    conflicts=('topbeat')
    replaces=('topbeat')

    _do_package_beat
}

package_heartbeat-elastic() {
    pkgdesc='Ping remote services for availability and log results to Elasticsearch or send to Logstash'
    conflicts=('heartbeat')
    replaces=('heartbeat')

    _do_package_beat
}

package_auditbeat() {
    pkgdesc='Audit the activities of users and processes on your system'
    depends=('audit')
    install='auditbeat.install'

    _do_package_beat
}

package_journalbeat() {
    pkgdesc='Data collector to read and forward journal entries from journald to Elasticsearch'

    _do_package_beat
}
