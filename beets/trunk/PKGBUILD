# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets
pkgver=1.3.6
pkgrel=1
pkgdesc='Flexible music library manager and tagger'
arch=('any')
url='http://beets.radbox.org/'
license=('MIT')
depends=('mutagen' 'python2-enum34' 'python2-munkres' 'python2-musicbrainzngs' 'python2-setuptools' 'python2-unidecode' 'python2-yaml')
optdepends=('bash-completion: Bash completion'
            'gstreamer0.10-python: BPD audio player plugin'
            'python2-flask: Web interface'
            'python2-pyacoustid: Acoustic fingerprinting'
            'python2-pylast: LastGenre plugin'
            'python2-requests: FetchArt plugin')
source=("https://github.com/sampsyo/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('a62160585ce7f4784088db11b5f91d1ae886601e8ba3aab584b11dbbc43e3ce3')

build() {
  cd ${pkgname}-${pkgver}

  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

# Manpages
  install -dm 755 "${pkgdir}"/usr/share/man/man{1,5}
  install -m 644 man/beet.1 "${pkgdir}"/usr/share/man/man1/
  install -m 644 man/beetsconfig.5 "${pkgdir}"/usr/share/man/man5/

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
