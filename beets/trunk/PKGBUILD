# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets
pkgver=1.3.19
pkgrel=1
pkgdesc='Flexible music library manager and tagger'
arch=('any')
url='http://beets.radbox.org/'
license=('MIT')
depends=('mutagen' 'python2-enum34' 'python2-jellyfish' 'python2-munkres'
         'python2-musicbrainzngs' 'python2-setuptools' 'python2-unidecode'
         'python2-yaml')
optdepends=('bash-completion: Bash completion'
            'gst-plugins-bad: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-plugins-good: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-plugins-ugly: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-libav: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'gst-python: Chromaprint/Acoustid, BPD, ReplayGain plugins'
            'imagemagick: Thumbnails plugin'
            'python2-beautifulsoup4: Lyrics plugin'
            'python2-flask: Web plugin'
            'python2-gobject: ReplayGain plugin'
            'python2-mpd: MPDStats plugin'
            'python2-pathlib: Thumbnails plugin'
            'python2-pyacoustid: Chromaprint/Acoustid plugin'
            'python2-pylast: LastGenre plugin'
            'python2-requests: Chromaprint/Acoustid, BPD, FetchArt plugins'
            'python2-xdg: Thumbnails plugin')
source=("https://github.com/sampsyo/beets/releases/download/v${pkgver}/beets-${pkgver}.tar.gz"
        'https://raw.githubusercontent.com/sampsyo/beets/master/extra/_beet'
        'beet')
sha256sums=('6b241e62764a9c4bf9bf570acacc4681d30513f0f2268faa5da494ec066c21ee'
            'dd13bbe2270ac1fce76e7ba9e749823f0fc664b6746b5536e2edf22f32a91e3b'
            'b081285b202f82d09802ebca73d28bbf1902179ad3a820e4c0157f80bf959d89')

build() {
  cd beets-${pkgver}

  python2 setup.py build
}

package() {
  cd beets-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/bash-completion/completions/
  install -m 644 ../beet "${pkgdir}"/usr/share/bash-completion/completions/
  install -dm 755 "${pkgdir}"/usr/share/zsh/site-functions
  install -m 644 ../_beet "${pkgdir}"/usr/share/zsh/site-functions/

  install -dm 755 "${pkgdir}"/usr/share/man/man{1,5}
  install -m 644 man/beet.1 "${pkgdir}"/usr/share/man/man1/
  install -m 644 man/beetsconfig.5 "${pkgdir}"/usr/share/man/man5/

  install -dm 755 "${pkgdir}"/usr/share/licenses/beets
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
