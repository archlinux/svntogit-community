# $Id: PKGBUILD 99895 2013-10-31 02:20:19Z allan $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=belle-sip
pkgver=1.4.0
pkgrel=3
pkgdesc="A Voice-over-IP phone"
arch=('i686' 'x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('libantlr3c' 'gcc-libs' 'mbedtls')
makedepends=('java-runtime')
options=('!emptydirs')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/belle-sip/belle-sip-$pkgver.tar.gz"
	"antlr.jar::https://github.com/antlr/website-antlr3/blob/gh-pages/download/antlr-3.4-complete.jar?raw=true")
noextract=('antlr.jar')
md5sums=('17aed9525c4ab8916fbfc76c57372f9c'
         '1b91dea1c7d480b3223f7c8a9aa0e172')

prepare() {
  cd $pkgname-$pkgver
  sed -i \
    -e "s#antlr_java_prefixes=.*#antlr_java_prefixes=$srcdir#" \
    -e "s|-Werror||g" \
    configure{,.ac}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr  --libexecdir=/usr/lib/$pkgname \
    --disable-tests --disable-static --enable-tls
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
