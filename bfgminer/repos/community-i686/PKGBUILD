# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bfgminer
pkgver=5.4.2
pkgrel=2
pkgdesc="Bitcoin miner featuring overclocking, monitoring, fan speed control and remote management. For FPGA/GPU/CPU Bitcoin mining."
arch=('i686' 'x86_64')
depends=('curl' 'jansson' 'libevent' 'libmicrohttpd' 'libusb' 'lm_sensors')
makedepends=('uthash' 'yasm')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA')
url='http://bfgminer.org/'
license=('GPL3')
source=(http://luke.dashjr.org/programs/bitcoin/files/$pkgname/$pkgver/$pkgname-$pkgver.txz
        remove-dangerous-rpath.patch)
sha256sums=('7030632a08cfc5a3fdca485fd8d7213d8f41560134c54d356426b974d91ce01a'
            'ec5289b837cc3b5de7f08603e4ae4d75cfff969a2389fd2aee036486f4aa7420')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i "${srcdir}"/remove-dangerous-rpath.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --enable-cpumining \
    --enable-bfsb \
    --enable-metabank \
    --enable-scrypt \
    --enable-opencl \
    --with-udevrulesdir=/usr/lib/udev/rules.d
  # --enable-knc (FS#33640)
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
