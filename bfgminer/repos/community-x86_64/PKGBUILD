# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=bfgminer
pkgver=3.10.0
pkgrel=1
pkgdesc="Bitcoin miner featuring overclocking, monitoring, fan speed control and remote management. For FPGA/GPU/CPU Bitcoin mining."
arch=('i686' 'x86_64')
depends=('curl' 'jansson' 'libevent' 'libmicrohttpd' 'libusbx' 'lm_sensors')
makedepends=('uthash' 'yasm')
optdepends=('opencl-nvidia: OpenCL implementation for NVIDIA')
url='https://bitcointalk.org/index.php?topic=168174.0'
license=('GPL3')
source=(http://luke.dashjr.org/programs/bitcoin/files/$pkgname/$pkgver/$pkgname-$pkgver.tbz2
        remove-dangerous-rpath.patch)
sha256sums=('12458aed84abc4a953532fe0645772492701e5d627f56f62d382aa0f458a10d5'
            'd26d07ca23ae25798c4db37cfda1a6a76f22d06e73efab1ddf62b3e1a7eef04c')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -p0 < "${srcdir}"/remove-dangerous-rpath.patch
  NOSUBMODULES=1 ./autogen.sh
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure	--prefix=/usr \
    --enable-cpumining \
    --enable-bfsb \
    --enable-metabank \
    --enable-scrypt \
    --enable-opencl \
    --with-udevrulesdir=/usr/lib/udev/rules.d
  # --enable-knc (FS#33640)
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
