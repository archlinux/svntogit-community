# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=biber
epoch=1
pkgver=2.12
pkgrel=2
pkgdesc="A Unicode-capable BibTeX replacement for biblatex users"
arch=('any')
license=('PerlArtistic' 'GPL')
url="https://github.com/plk/biber"
# Encode::* modules are not real dependencies.
depends=('perl-autovivification'
         'perl-business-isbn'
         'perl-business-ismn'
         'perl-business-issn'
         'perl-class-accessor'
         'perl-data-compare'
         'perl-data-dump'
         'perl-data-uniqid'
         'perl-datetime-calendar-julian'
         'perl-datetime-format-builder'
         'perl-file-slurper'
         'perl-io-string' # required by Biber::Output::bblxml
         'perl-ipc-run3'
         'perl-lingua-translit'
         'perl-list-allutils'
         'perl-log-log4perl'
         'perl-lwp-protocol-https'
         'perl-mozilla-ca'
         'perl-perlio-utf8-strict'
         'perl-regexp-common'
         'perl-sort-key'
         'perl-text-bibtex'
         'perl-text-csv'
         'perl-text-roman'
         'perl-unicode-linebreak'
         'perl-xml-libxml-simple'
         'perl-xml-libxslt'
         'perl-xml-writer')
# ExtUtils::LibBuilder and Config::AutoConf are not actual makedepends
makedepends=('perl-module-build' 'perl-test-differences')
checkdepends=('perl-capture-tiny' 'perl-file-which'
              'texlive-bibtexextra'
              'texlive-latexextra')
options=('!emptydirs')
# The sourceforge tarball is missing bblxml.pm
source=("https://github.com/plk/biber/archive/v${pkgver}.tar.gz"
        "test.tex"
        "test.bib"
        biber-fix-tests.patch biber-fix-tests-2.patch)
sha512sums=('72f1a05224dc2735b32e92ac376d392fb631bf788a15de449471fdc5a94182ad4434ce71f95281e4cfc3a06cd275d2ff98e526d540c2979cd3e0dfa15b5c75ff'
            '06babeb4ec2fd76b8a339070eb208c3c8c16665a085e4674ef3b61b3816d4408ca636b925fabe467adc5c32bf8eeb23d357f7ef979ac0a1722dc7f91142ec541'
            '68a00e41f200b8e24a674538b6581b88f84daef38a765790c1781365329203f60f4461a56dfa092bf336cc00c8801df1ee85f60d86fd384a8c039f913bdeeb2d'
            '0facb4e32e454e95ee1d72368d88ad8db2b5d94d64157f4fa4f62ccca485f9aa5186966fb4af8207f6ce87599bb98c19cc1c51df500dd0e71c0217ed54cc445a'
            '566e56b3498fbf5fd03cbec9a62472e8e34b517b101e84df123b9b79ccd46c55a37820f3a06cac6a86f86c8e65db4857c9f17a43399bd7b2b7a4a6b663e9c3e4')

prepare() {
  cd "${srcdir}/biber-${pkgver}"
  # Disable long year tests on 32 bit
  if [[ $CARCH = "i686" ]]; then
    sed -i '/17000002/ s+.*+eq_or_diff("dummy", "dummy", "skip 32 bit");+' t/dateformats.t
  fi

  patch -p1 -i ../biber-fix-tests.patch # Update hashes to fix tests
  patch -p1 -i ../biber-fix-tests-2.patch # Remove broken test source

  mkdir -p "test"
  cp "${srcdir}"/test.{tex,bib} test/
}

build() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  perl Build.PL
  ./Build
}

check() {
  cd "${srcdir}/biber-${pkgver}"
  # Test a sample file
  pushd test
  latex test
  PERL5LIB=../blib/lib ../bin/biber test
  latex test
  popd
  # Run test suite
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  LC_CTYPE=en_US.UTF-8 BIBER_DEV_TESTS=1 ./Build test
}

package() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  ./Build install installdirs=vendor destdir="${pkgdir}"
}
