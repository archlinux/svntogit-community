# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime
pkgver=3.0.2
pkgrel=1
pkgdesc='Bible study tool'
arch=('x86_64')
url='http://www.bibletime.info/'
license=('GPL2')
depends=('sword' 'clucene' 'qt5-webengine' 'qt5-svg')
makedepends=('cmake' 'docbook-xsl' 'expac' 'fop' 'ninja' 'po4a' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bibletime/bibletime/archive/v$pkgver.tar.gz")
sha512sums=('1c60d7e8e96f1d8ff93076250f36f0cba0f496317ce9a825547699214ccdafb628260fdd5acbe000b321f825df752c68f2d80c5c37fa759b80e796caf1c0bf01')

prepare() {
  mkdir -p build
}

build() {
  cd build
  local _xsl_shared=/usr/share/xml/docbook/xsl-stylesheets-$(expac %v docbook-xsl | cut -d - -f 1)
  cmake -GNinja ../bibletime-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBT_DOCBOOK_XSL_HTML_CHUNK_XSL=$_xsl_shared/html/chunk.xsl \
    -DBT_DOCBOOK_XSL_PDF_DOCBOOK_XSL=$_xsl_shared/fo/docbook.xsl
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
