# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime
pkgver=2.11.0
pkgrel=2
pkgdesc='Bible study tool'
arch=('x86_64')
url='http://www.bibletime.info/'
license=('GPL2')
depends=('sword' 'clucene' 'qt5-webengine' 'qt5-svg')
makedepends=('cmake' 'qt5-tools' 'git')
source=("git://github.com/bibletime/bibletime.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../bibletime \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
