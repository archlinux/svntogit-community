# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=binutils-avr
pkgver=2.20.1
pkgrel=3
pkgdesc="A set of programs to assemble and manipulate binary and object files for the avr architecture"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('glibc>=2.12-1' 'zlib')
options=('!libtool' '!distcc' '!ccache')
source=(http://ftp.gnu.org/gnu/${pkgname/-avr}/${pkgname/-avr}-${pkgver}.tar.bz2 avr-size.patch)
md5sums=('9cdfb9d6ec0578c166d3beae5e15c4e5' '5ff0b596a6e87a906491cca9860f67df')

build() {
	cd ${srcdir}/${pkgname/-avr}-${pkgver}
	patch binutils/size.c -p1 < ../avr-size.patch

	mkdir build
	cd build

  	CC="gcc -L$(pwd)/bfd/.libs/"
  	if [ "${CARCH}" = "x86_64" ]; then
    		../configure --build=$(../config.guess) \
                	--disable-multilib \
                 	--disable-nls \
                 	--enable-64-bit-bfd \
                 	--enable-install-libbfd \
                 	--includedir=/usr/$(../config.guess)/include \
                 	--infodir=/usr/share/info \
                 	--libdir=/usr/lib \
                 	--mandir=/usr/share/man \
                 	--prefix=/usr \
                 	--target=avr
	else
		../configure --build=$(../config.guess) \
                	--disable-nls \
                	--enable-install-libbfd \
                 	--includedir=/usr/$(../config.guess)/include \
                 	--infodir=/usr/share/info \
                 	--libdir=/usr/lib \
                 	--mandir=/usr/share/man \
                 	--prefix=/usr \
                 	--target=avr
  	fi

  	# This checks the host environment and makes sure all the necessary tools are available to compile Binutils.
  	make configure-host

  	make tooldir=/usr
}

package() {
 	cd ${srcdir}/${pkgname/-avr}-${pkgver}

	cd build
	make DESTDIR=${pkgdir} tooldir=/usr install

  	rm -f ${pkgdir}/usr/lib/libiberty.a

  	for bin in addr2line ar as c++filt gprof ld nm objcopy objdump ranlib readelf size strings strip ; do
    		rm -f ${pkgdir}/usr/bin/${bin} || return 1
  	done

  	for info in as bfd binutils configure gprof ld standards; do
    		mv ${pkgdir}/usr/share/info/${info}.info ${pkgdir}/usr/share/info/avr-${info}.info
 	done
}

