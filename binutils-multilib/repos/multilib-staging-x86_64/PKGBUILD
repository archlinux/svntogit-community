# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils-multilib
pkgver=2.22
pkgrel=8
_date=20120323
pkgdesc="A set of programs to assemble and manipulate binary and object files for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('multilib-devel')
provides=("binutils=$pkgver-$pkgrel")
conflicts=('binutils')
depends=('glibc>=2.16' 'zlib')
makedepends=('gcc-multilib') # Make sure we compile this with gcc-multilib
checkdepends=('dejagnu')
options=('!libtool' '!distcc' '!ccache')
install=binutils.install
source=(http://mirrors.kernel.org/archlinux/other/binutils/binutils-${pkgver}_${_date}.tar.bz2)
md5sums=('de2ac4298732827f8af706fc24020330')

build() {
  cd ${srcdir}
  mkdir binutils-build && cd binutils-build

  ${srcdir}/binutils/configure --prefix=/usr \
    --enable-ld=default --enable-gold \
    --enable-plugins --enable-threads \
    --enable-shared \
    --enable-64-bit-bfd --enable-multilib

  # check the host environment and makes sure all the necessary tools are available
  make configure-host

  make tooldir=${pkgdir}/usr

  # Rebuild libiberty.a with -fPIC
  cp -a libiberty libiberty-pic
  make -C libiberty-pic clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty-pic

  # Rebuild libbfd.a with -fPIC
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  cp -a bfd bfd-pic
  make -C bfd-pic clean
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd-pic

  # Rebuild libopcodes.a with -fPIC
  cp -a opcodes opcodes-pic
  make -C opcodes-pic clean 
  make CFLAGS="$CFLAGS -fPIC" -C opcodes-pic
}

check() {
  cd ${srcdir}/binutils-build

  # do not abort on errors - manually check log files
  # gold testsuite does not build with _FORTIFY_SOURCE (due to -O0 -Werror)
  make CFLAGS="${CFLAGS/-D_FORTIFY_SOURCE=2/}" \
       CXXFLAGS="${CXXFLAGS/-D_FORTIFY_SOURCE=2/}" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
  
  # Add some useful headers
  install -m644 ${srcdir}/binutils/include/libiberty.h ${pkgdir}/usr/include
  install -m644 ${srcdir}/binutils/include/demangle.h ${pkgdir}/usr/include

  # install libraries rebuilt with -fPIC
  install -m644 libiberty-pic/libiberty.a ${pkgdir}/usr/lib
  install -m644 bfd-pic/libbfd.a ${pkgdir}/usr/lib
  install -m644 opcodes/libopcodes.a ${pkgdir}/usr/lib

  # Remove Windows/Novell specific man pages
  rm -f ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm -f ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >${pkgdir}/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >${pkgdir}/usr/lib/libopcodes.so
}
