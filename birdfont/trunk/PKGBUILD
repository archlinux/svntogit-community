# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Mohamed Sobh <mohamed.alhusieny@gmail.com>
# Contributor: Drew Noel <drewmnoel@gmail.com>
# Contributor: Manuel Hüsers <manuel.huesers@uni-ol.de>
# Contributor: fstirlitz <felix.von.s@posteo.de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Matej Lach <matej.lach@gmail.com>

pkgname=birdfont
pkgver=2.29.6
pkgrel=1
pkgdesc='Font editor which can generate fonts in TTF, EOT and SVG formats'
arch=('x86_64')
url='https://birdfont.org/'
license=('GPL3')
depends=('libgee' 'libxmlbird' 'webkit2gtk')
makedepends=('python' 'vala')
source=("https://github.com/johanmattssonm/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('5212b57b65b2b20cd343fe4595f4bb381a4c8f40a27c150f7cfc22bc1afe590e')
validpgpkeys=('FB3BEFA59A6FF7F0E0682B68BCD31D4CCCEB9DD4') # Johan Mattsson

build() {
  cd $pkgname-$pkgver
  ./configure --prefix /usr --cflags="$CFLAGS" --ldflags="$LDFLAGS"
  ./build.py
}

package() {
  cd $pkgname-$pkgver
  ./install.py --dest "$pkgdir" --libdir /lib --manpages-directory /share/man/man1
}
