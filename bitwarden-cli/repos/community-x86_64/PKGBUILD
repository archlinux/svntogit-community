# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.com>
# Contributor: libertylocked <libertylocked@disroot.org>

pkgname=bitwarden-cli
pkgver=1.18.0
_jslibcommit='0a2ff12bedd55f5982fb8fe9102c4f20522f72e1'
pkgrel=2
pkgdesc="The command line vault"
arch=('x86_64')
url="https://github.com/bitwarden/cli"
license=('GPL3')
depends=('gcc-libs')
makedepends=('git' 'npm' 'node-gyp' 'nodejs-lts-fermium')
source=("bitwarden-cli-${pkgver}.tar.gz::https://github.com/bitwarden/cli/archive/v${pkgver}.tar.gz"
        "jslib-${_jslibcommit}.tar.gz::https://github.com/bitwarden/jslib/archive/${_jslibcommit}.tar.gz")
sha512sums=('333c3c4d6b5311b256e34988a201f317fea6aa2d8c7186d80f7d0a1c68e3b40a957bcad66f97e2ca795420ced030390794d14ea173f9c9372d8886bc45a8dd95'
            '686489c2a5d3847dc6e8c25bd7fb878fc82243cd20b83489ba89f129358c4715b5007d6526cd392383a9ddb84c57910b19fc6a3773fe56694e194c990c05dbd1')

prepare() {
	cd cli-${pkgver}
	rmdir -v jslib
	ln -sv ../jslib-${_jslibcommit} jslib
}

build() {
	cd cli-${pkgver}
	export npm_config_cache="$srcdir/npm_cache"
	pushd jslib
	npm install
	popd
	npm install
	npm run build:prod
	npm run clean
	npm run package:lin
	./dist/linux/bw completion --shell zsh > _bw
}

package() {
	cd cli-${pkgver}
	install -vDm755 dist/linux/bw -t "${pkgdir}/usr/bin"
	# package zsh completions
	install -vDm644 "${srcdir}/cli-${pkgver}/_bw" -t "${pkgdir}/usr/share/zsh/site-functions"
}
