# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=bitwarden_rs-web
pkgver=2.18.1b
_upstreamver=2.18.1
_patchver=2.18.1
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with bitwarden_rs'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('bitwarden_rs')
makedepends=('git' 'npm' 'python2')
provides=('bitwarden_rs-vault')
replaces=('bitwarden_rs-vault')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        'git+https://github.com/bitwarden/jslib.git'
        "$pkgname-v$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        'SKIP'
        '8346c501c09b4764898b0a50d777eadfed725b931b2f9400343d1f4dbf01fa8f9bf1ce0ae18056ff2f1e66e81b9d609082d627aebf58522f468bb044ec266f11')

prepare() {
  cd bitwarden-web
  git submodule init
  git config submodule.jslib.url ../jslib
  git submodule update
  patch --forward --strip=1 --input=../$pkgname-v$_patchver.patch
}

build() {
  cd bitwarden-web
  npm install --frozen-lockfile
  npm audit fix
  npm run dist
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/$pkgname
  cp -R build/* "$pkgdir"/usr/share/$pkgname
}

# vim:set ts=2 sw=2 et:
