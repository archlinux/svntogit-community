# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr

pkgname=bitwarden_rs
pkgver=1.17.0
pkgrel=4
pkgdesc='Unofficial Bitwarden compatible server written in Rust'
arch=('x86_64')
url=https://github.com/dani-garcia/bitwarden_rs
license=('GPL3')
depends=('mariadb-libs' 'openssl' 'postgresql-libs' 'sqlite')
makedepends=('rustup')
optdepends=('bitwarden_rs-vault: for the web app')
conflicts=("$pkgname-mysql" "$pkgname-postgresql")
backup=('etc/bitwarden_rs.env')
install=$pkgname.install
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname.service"
        "$pkgname.sysusers.conf"
        "$pkgname.tmpfiles")
b2sums=('faf4a3e0cba6905547c347bd8d7939e2412116d5c9b226e49cddd04306b6e69e00e1f5d7b1b09493ff02614d5417b34cd9c54cb3efffbf238e23e3f54bacd5d1'
        'c344164792bc9f9d5b485f932d2c476515d783cb54478e60fb8ca3c17f5781e067af0d2dff0670886fd186427c78e986f544f66d34e936db9f719c7f0be156e3'
        'c44af94e19724ba23a11cec3ccc46ff9db307a058564d539dc533308e75ff43cfb5e42515bd49fdeb86e02cbc7575dc87c3b132d9d28d49f7e8fedab598c06f5'
        'a2a6a128a405b4dbd06eb84c25b1971a5dcab4b918d6fec74da317b76485eda6b4b16ad972a85d9c8267b0a848787761fae75cd6bbb81d970a8cbc8683a2fc42')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's,# DATA_FOLDER=data,DATA_FOLDER=/var/lib/bitwarden_rs,
  s,# WEB_VAULT_ENABLED=true,WEB_VAULT_ENABLED=false,
  s,# LOG_FILE=/path/to/log,LOG_FILE=/var/log/bitwarden_rs.log,
  /^# ROCKET_TLS/a ROCKET_LIMITS={json=10485760}' .env.template
}

build() {
  cd $pkgname-$pkgver
  rustup set profile minimal
  BWRS_VERSION="$pkgver" cargo build --release --locked --features sqlite,mysql,postgresql
}

check() {
  cd $pkgname-$pkgver
  cargo test --release --locked --features sqlite,mysql,postgresql
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 .env.template "$pkgdir"/etc/bitwarden_rs.env
  install -Dt "$pkgdir"/usr/bin target/release/bitwarden_rs
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system ../bitwarden_rs.service
  install -Dm644 ../$pkgname.sysusers.conf "$pkgdir"/usr/lib/sysusers.d/bitwarden_rs.conf
  install -Dm644 ../bitwarden_rs.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/bitwarden_rs.conf
}

# vim:set ts=2 sw=2 et:
