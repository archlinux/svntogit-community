# $Id$
# Maintainer:  TDY <tdy@gmx.com>
# Contributor: dcraven <dcraven@gmail.com>

pkgname=bless
pkgver=0.6.0
pkgrel=4
pkgdesc="A high-quality, full-featured hex editor"
arch=('any')
url="http://home.gna.org/bless/"
license=('GPL')
depends=('gtk-sharp-2>=2.8')
makedepends=('gettext' 'pkgconfig' 'rarian')
validpgpkeys=('3C6854719142638FAF86DAEF0011ABB5F6E68924')
source=(http://http.debian.net/debian/pool/main/b/bless/bless_$pkgver.orig.tar.gz)
md5sums=('b4c20d113281dfedb67d9f3ff8b174e3')

prepare() {
  cd $pkgname-$pkgver

  # Build for mono 4.0 (FS#37299)
  sed -i 's/gmcs/dmcs/' configure{,.ac} builder/ModuleBuilder.cs
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-scrollkeeper
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
