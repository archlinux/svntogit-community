# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=bliss-graphs
pkgver=0.73
pkgrel=2
pkgdesc="A library for computing automorphism groups and canonical forms of graphs"
arch=(i686 x86_64)
url="http://www.tcs.tkk.fi/Software/bliss/index.html"
license=(GPL3)
depends=(gcc-libs)
source=("http://www.tcs.hut.fi/Software/bliss/bliss-$pkgver.zip" 'digraph_heuristic.patch')
md5sums=('72f2e310786923b5c398ba0fc40b42ce'
         '242e9ffffff299178681a211c417c2db')

prepare() {
  cd bliss-$pkgver
# Fix splitting heuristics for digraphs
  patch -p1 -i ../digraph_heuristic.patch
}

build() {
  cd bliss-$pkgver

  make
}

package() {
  cd bliss-$pkgver

  mkdir -p "$pkgdir"/usr/{include/bliss,lib,bin}
  cp *.hh "$pkgdir"/usr/include/bliss/
  cp libbliss.a "$pkgdir"/usr/lib/
  cp bliss "$pkgdir"/usr/bin/
}

