# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=blosc
pkgver=1.19.0
pkgrel=1
pkgdesc='A blocking, shuffling and loss-less compression library.'
arch=('x86_64')
url='http://blosc.org/'
license=('BSD')
depends=('snappy' 'zstd')
makedepends=('cmake')
options=(staticlibs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Blosc/c-blosc/archive/v${pkgver}.tar.gz")
sha256sums=('cf3d6193fece9becddb62248b9981bf78cf1c7faf58150fd520d4755cd622488')

build() {
  cd "${srcdir}"
  rm -rf build
  mkdir build
  cd build

  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DPREFER_EXTERNAL_LZ4="ON" \
	-DPREFER_EXTERNAL_ZLIB="ON" \
	-DPREFER_EXTERNAL_SNAPPY="ON" \
	-DPREFER_EXTERNAL_ZSTD="ON" \
        -DBUILD_TESTS="OFF" \
        ../c-blosc-${pkgver}

  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir"/c-blosc-${pkgver}/LICENSES/BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
