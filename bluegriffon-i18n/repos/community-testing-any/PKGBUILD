# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Balló György <ballogyor+arch@gmail.com>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Michal Hybner <dta081@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=bluegriffon-i18n
pkgname=()
pkgver=2.4
pkgrel=1
pkgdesc="The next-generation Web Editor based on the rendering engine of Firefox"
arch=('any')
url="http://bluegriffon.org/"
license=('MPL' 'GPL' 'LGPL')
depends=("bluegriffon>=${pkgver}")
groups=('bluegriffon')
provides=('bluegriffon-i18n')
source=("http://bluegriffon.org/freshmeat/${pkgver}/bluegriffon-${pkgver}.Ubuntu16.04-${CARCH}.tar.bz2")
sha256sums=('ed3ef933aafb768afe420af65ad13b5eb98550bb792f9f385bcb6910fa52f614')

_languages=(
  'cs     "Czech"'
  'de     "German"'
  'es-ES  "Spanish (España)"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galego (España)"'
  'he     "Hebrew"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'ru     "Russian"'
  'sl     "Slovenian"'
  'sr     "Russian"'
  'sv-SE  "Svenska"'
  'zh-CN  "Chinese Simplified"'
  'zh-TW  "Traditional Chinese"'
)

for _lang in "${_languages[@]}"
do
  _locale="${_lang%% *}"
  _pkgname="${pkgbase}-${_locale,,}"

  pkgname+=("${_pkgname}")
  eval "package_${_pkgname}() {
    _package ${_lang}
  }"
done

_package() {
  _locale="$1"
  _language="$2"
  pkgdesc="${_language} language pack for Bluegriffon"

  cd "bluegriffon/distribution/extensions"
  install -m 755 -d "${pkgdir}/usr/lib/bluegriffon/distribution/extensions"
  install -m 644 -t "${pkgdir}/usr/lib/bluegriffon/distribution/extensions" "langpack-${_locale}@bluegriffon.org.xpi"
}
