# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=blueman
pkgver=1.23
pkgrel=4
pkgdesc="GTK+ bluetooth manager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://blueman-project.org"
depends=('python-notify' 'bluez' 'dbus-python' 'gtk2' 'pygobject' 'startup-notification'
	 'obex-data-server' 'polkit-gnome' 'xdg-utils' 'hicolor-icon-theme' 'libpulse'
	 'notification-daemon')
makedepends=('pyrex' 'intltool')
optdepends=('dnsmasq: For creating PAN network'
	    'gconf: For gconf configuration support'
	    'pulseaudio: To use the pulseaudio plugin'
	    'notification-daemon: for desktop notifications'
	    'libnotify: for desktop notifications'
	    'modemmanager: for modem management service'
	    'gnome-icon-theme: icon theme')
options=('!libtool')
install=$pkgname.install
source=(http://download.tuxfamily.org/blueman/${pkgname}-${pkgver}.tar.gz)
md5sums=('f0bee59589f4c23e35bf08c2ef8acaef')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=python2
  for file in apps/blueman*; do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done
  sed -i 's|"blueman-tray"|"blueman"|' blueman/plugins/applet/StatusIcon.py

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/blueman \
	--with-no-runtime-deps-check
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  echo "OnlyShowIn=GNOME;XFCE;" >> ${pkgdir}/etc/xdg/autostart/blueman.desktop
}
