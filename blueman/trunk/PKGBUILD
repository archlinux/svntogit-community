# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=blueman
pkgver=20131021git
pkgrel=1
pkgdesc="GTK+ bluetooth manager"
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/cschramm/blueman"
depends=('bluez' 'bluez-libs' 'gnome-icon-theme' 'libpulse' 'obex-data-server'
         'python2-dbus' 'python2-notify' 'startup-notification' 'xdg-utils')
makedepends=('pyrex' 'intltool' 'git')
optdepends=('dnsmasq: For creating PAN network'
	    'modemmanager: For modem management service'
	    'notification-daemon: For desktop notifications'
	    'polkit-gnome: For privileged actions'
	    'pulseaudio: To use the pulseaudio plugin'
	    'python2-gconf: For gconf configuration support')
install=$pkgname.install
source=("blueman::git://github.com/cschramm/blueman.git")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"
  for file in apps/blueman*; do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done
  sed -i 's|"blueman-tray"|"blueman"|' blueman/plugins/applet/StatusIcon.py
}

build() {
  cd "${srcdir}/${pkgname}"
  export PYTHON=python2
  ./autogen.sh
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/blueman \
	--with-no-runtime-deps-check
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  echo "NotShowIn=KDE;" >> ${pkgdir}/etc/xdg/autostart/blueman.desktop
}
