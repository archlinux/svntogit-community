# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=blueman
pkgver=1.23
pkgrel=6
pkgdesc="GTK+ bluetooth manager"
arch=('i686' 'x86_64')
license=('GPL')
url="https://launchpad.net/blueman"
depends=('bluez' 'gnome-icon-theme' 'libpulse' 'obex-data-server' 'python2-dbus'
         'python2-notify' 'startup-notification' 'xdg-utils')
makedepends=('pyrex' 'intltool')
optdepends=('dnsmasq: For creating PAN network'
	    'modemmanager: For modem management service'
	    'notification-daemon: For desktop notifications'
	    'polkit-gnome: For privileged actions'
	    'pulseaudio: To use the pulseaudio plugin'
	    'python2-gconf: For gconf configuration support')
options=('!libtool')
install=$pkgname.install
source=(https://launchpad.net/${pkgname}/1.2/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('f0bee59589f4c23e35bf08c2ef8acaef')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=python2
  for file in apps/blueman*; do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done
  sed -i 's|"blueman-tray"|"blueman"|' blueman/plugins/applet/StatusIcon.py

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/blueman \
	--with-no-runtime-deps-check
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  echo "NotShowIn=KDE;" >> ${pkgdir}/etc/xdg/autostart/blueman.desktop
}
