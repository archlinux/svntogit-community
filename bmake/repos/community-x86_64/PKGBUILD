# Maintainer: Ivy Foster <code@escondida.tk>
# Contributor: Alex Szczuczko <alex at szc dot ca>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Imanol Celaya <ilcra1989@gmail.com>

pkgname=bmake
pkgver=20210314
pkgrel=1
pkgdesc='Portable version of the NetBSD make build tool'
arch=(x86_64)
url='http://www.crufty.net/help/sjg/bmake.html'
license=(BSD)

# upstream recommends using python for improved meta2deps script
depends=(python)

source=("http://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz")
# checksum source: http://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz.sha1
sha1sums=(65400b068f2b9551eabfc5184adfb6ab53a83adc)

build() {
	cd bmake
	./boot-strap --prefix=/usr op=build
}

# op=build also runs unit tests; thus no check()

package() {
	cd bmake

	# Fix directory permissions on install
	install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/licenses/$pkgname"

	./boot-strap --install-destdir="$pkgdir" --prefix=/usr op=install

	rm -rf "$pkgdir/usr/share/man/cat1"
	install -D -m 644 bmake.1 "$pkgdir/usr/share/man/man1/bmake.1"

	head -n70 main.c >"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
