# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=bmon
pkgver=3.6
pkgrel=1
pkgdesc="Portable bandwidth monitor and rate estimator"
arch=('i686' 'x86_64')
url="http://github.com/tgraf/bmon/"
license=('MIT' 'BSD')
depends=('ncurses' 'libnl' 'confuse')
makedepends=('autoconf')
source=(http://github.com/tgraf/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('7d9d9b5af751cb61e267b43309506f03e82636282742fce89209f2378ae6a4b1')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# Licenses
  install -d "${pkgdir}"/usr/share/licenses/$pkgname
  cp LICENSE.{BSD,MIT} "${pkgdir}"/usr/share/licenses/$pkgname
}
