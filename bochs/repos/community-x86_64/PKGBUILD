# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bochs
pkgver=2.6.6
pkgrel=1
pkgdesc="A portable x86 PC emulation software package"
arch=('i686' 'x86_64')
url="http://bochs.sourceforge.net/"
license=('LGPL')
depends=('gcc-libs' 'libxrandr')
#makedepends=('sdl')
#optdepends=('sdl: debuggers')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('f5c8483bc478381dd5979f6b3972d44e')

build() {
    cd "$srcdir/$pkgname-$pkgver"

    # typos in 2.6.5
    #sed -i 's/Bx3DNowOpcode\[i->modRMForm.Ib/&\[0\]/' cpu/fetchdecode{,64}.cc

    # todo, figure out why debuggers won't build (FS#38274)
    ./configure \
        --prefix=/usr \
        --without-wx \
        --with-x11 \
        --disable-docbook \
        --enable-cpu-level=6 \
        --enable-fpu \
        --enable-3dnow \
        --enable-disasm \
        --enable-smp \
        --enable-x86-64 \
        --enable-avx \
        --enable-long-phy-address \
        --enable-disasm \
        --enable-pcidev \
        --enable-usb
        #--with-sdl
        #--enable-debugger
        #--enable-x86-debugger
        #--enable-all-optimizations
        #--enable-plugins
    #sed -i 's/^LIBS = /LIBS = -lpthread/g' Makefile
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm644 .bochsrc "$pkgdir/etc/bochsrc-sample.txt"
}
