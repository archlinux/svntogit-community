# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=boinc
pkgver=7.0.28
pkgrel=1
pkgdesc="Berkeley Open Infrastructure for Network Computing for desktop with X"
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
depends=('curl' 'wxgtk' 'libnotify' 'libxss' 'sqlite')
makedepends=('curl' 'libxslt' 'perl-xml-sax' 'subversion' 'wxgtk' 'libnotify')
options=('!libtool')
install=$pkgname.install
source=(boinc.rc boinc.bash boinc.desktop boinc.service boinc-AM_CONDITIONAL.patch)

_svnroot="http://boinc.berkeley.edu/svn/tags"
_svnmod="boinc_core_release_${pkgver//./_}"

build() {
  cd ${srcdir}

  msg "Connecting to boinc.berkeley.edu SVN server..."
  svn co ${_svnroot}/${_svnmod} || (error "SVN checkout failed" && return 1)
  msg "SVN checkout done or server timeout"

  msg "Starting make..."
  rm -fr ${_svnmod}-build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build
  
  patch -p1 -i ${srcdir}/boinc-AM_CONDITIONAL.patch

  ./_autosetup

  ./configure --prefix=/usr --disable-server --enable-unicode --with-ssl \
              --with-wxdir=/usr/lib --with-wx-config=$(which wx-config) \
              --disable-static --enable-client --enable-manager --with-x LDFLAGS='-lX11'
  make
}

package() {
  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install

#install rc-script
  install -D -m755 ${srcdir}/$pkgname.rc ${pkgdir}/etc/rc.d/$pkgname

#install systemd unit
  install -D -m644 ${srcdir}/$pkgname.service ${pkgdir}/usr/lib/systemd/system/$pkgname.service

#install bash-completion
  install -D -m644 ${srcdir}/$pkgname.bash ${pkgdir}/usr/share/bash-completion/completions/$pkgname

#install .desktop File
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop

#install icons
  install -D -m644 ${srcdir}/${_svnmod}-build/clientgui/res/boincmgr.48x48.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d
}

md5sums=('c6dadc333f982ea7b548602a70bd1e93'
         '4d00e1aa4090a3f51feb20f5a541b9ee'
         '17969d849f3cf27c2100b20a7b7a7e64'
         '3d5cbab785cc8b004661b17c65883fd5'
         'e27047518dec54d4db38816487a28661')
