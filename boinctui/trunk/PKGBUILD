# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=boinctui
pkgver=2.6.0
pkgrel=1
pkgdesc="Curses based fullscreen BOINC manager"
arch=('x86_64')
url="https://sourceforge.net/projects/boinctui/"
license=('GPL')
depends=('expat' 'gcc-libs' 'ncurses' 'openssl')
makedepends=("cmake")
source=("https://sourceforge.net/projects/boinctui/files/${pkgname}_${pkgver}.tar.gz"{,.asc})
sha512sums=('5d0665dc4a00bc788873b4175d3e052967c93942bd73be44e29bc3278f180f8a198079a83148b2486ce5895ce65accd2aaaaa24fe485576b64bc695aefcba8b1'
            'SKIP')
validpgpkeys=('4D2DBBB4F06EC2C977B26BFD80878E59832BEC15')  # Sergey Suslov

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/\\ $/\\/' configure.in
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-gnutls --mandir=/usr/share/man
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
