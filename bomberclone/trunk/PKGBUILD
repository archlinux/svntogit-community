# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: arjan <arjan@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=bomberclone
pkgver=0.11.9
pkgrel=2
pkgdesc='Clone of the game AtomicBomberMan'
arch=('i686' 'x86_64')
url="http://www.bomberclone.de/"
license=('GPL')
depends=('sdl_mixer' 'sdl_image')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'bomberclone-0.11.9-build-fix.patch'
        'bomberclone.desktop')
md5sums=('3edcfcf69b88dbd2eab42541f236e072'
         '88def2b6faf7a6ac7acd96f617fc460e'
         '2c0151859e7e9aebd1fc95a1455f0c3b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # add build fix extracted from upstream CVS (fixes FS#26942)
  patch -p0 -i ../bomberclone-0.11.9-build-fix.patch

  ./configure --prefix=/usr
  make AM_CFLAGS=-lm
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 "${srcdir}/bomberclone.desktop" \
    "${pkgdir}/usr/share/applications/bomberclone.desktop"
}
