# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=bore
pkgver=0.4.0
pkgrel=1
pkgdesc="A command line tool for making tunnels to localhost"
arch=('x86_64')
url="https://github.com/ekzhang/bore"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('4e89080addee2d854966d972cc4bbeaebfef10ad4f06bec1605a53974a2bd66af53dc611f0bd1f66845a90f5152d71a465e4dd57efefe2e3cc50b29799a676d9')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
