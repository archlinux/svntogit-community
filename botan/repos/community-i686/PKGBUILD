# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>

pkgname=botan
pkgver=2.1.0
pkgrel=2
pkgdesc='Crypto library written in C++'
license=('BSD')
arch=('x86_64' 'i686')
url='https://botan.randombit.net/'
depends=('gcc-libs' 'sh')
makedepends=('python' 'git')
source=("https://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc})
#source=("git://github.com/randombit/botan.git#commit=8e19ecf11c")
md5sums=('2c91a5d65e0f9ddf4fce38bcd2734e70'
         'SKIP')
# Botan Distribution Key. To import: gpg --import botan.key
validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')

build() {
  cd "${pkgname^}-$pkgver"

  python configure.py \
    --prefix=/usr \
    --destdir="$pkgdir/usr"
  make
}

check() {
  cd "${pkgname^}-$pkgver"

  ./botan-test
}

package() {
  cd "${pkgname^}-$pkgver"

  make DESTDIR="$pkgdir/usr" install
  find "$pkgdir/usr/share/doc" -type f -exec chmod 0644 {} \;
  install -Dm644 license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/randombit/botan
# vim:set ts=2 sw=2 et:
