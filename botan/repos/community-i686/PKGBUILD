# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: d'Ronin <daronin@2600.com>
# Contributor: Hexchain Tong <richard0053@gmail.com>

pkgname=botan
pkgver=1.11.18
pkgrel=1
pkgdesc='Crypto library written in C++'
license=('BSD')
arch=('x86_64' 'i686')
url='http://botan.randombit.net/'
depends=('gcc-libs' 'sh' 'asio')
makedepends=('python2' 'asio' 'git')
#source=("http://botan.randombit.net/releases/Botan-${pkgver}.tgz"{,.asc})
source=("git://github.com/randombit/botan.git#commit=8e19ecf11c")
md5sums=('SKIP')
# Botan Distribution Key. To import: gpg --import botan.key
#validpgpkeys=('621DAF6411E1851C4CF9A2E16211EBF1EFBADFBC')

prepare() {
  # Use python2 for the installation scripts
  find botan/src/scripts -name '*.py' -exec sed -i -e '1s,python$,python2,' {} +
}

build() {
  cd botan
  python2 configure.py --prefix=/usr --enable-modules=cvc --destdir="$pkgdir/usr"
  make
}

package() {
  cd botan
  make DESTDIR="$pkgdir/usr" install
  find "$pkgdir/usr/share/doc" -type f -exec chmod 0644 {} \;
  install -Dm644 doc/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
