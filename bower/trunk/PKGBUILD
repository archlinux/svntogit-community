# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Micha Alt <micha.tucker@gmail.com> 
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Tom Vincent <http://tlvince.com/contact>

pkgname=bower
pkgver=1.8.2
pkgrel=1
pkgdesc='A package manager for the web'
arch=('any')
url='http://bower.io/'
license=('MIT')
depends=('nodejs' 'semver' 'uglify-js')
makedepends=('npm' 'git')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        LICENSE)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('e09baa78e446b7da4f91ff29b122715775d9b0aa95e43f8248c747f915195683fa5f4bb6ae7324afdd3be9d1d7f730bee97965e4148472bac1d374363c3b1b4b'
            'a932f56639679947f1ce67ca7c27855ae08257876880864079077db825fa402ac7d544a192e02683d38de60ed9ac708a366e05c81bd350ace1440c1588086bc1')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules
  for dep in semver uglify-js; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules/package-json/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/lib/node_modules/semver-diff/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done
}
