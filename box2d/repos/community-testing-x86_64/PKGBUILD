# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=box2d
pkgver=2.3.1
pkgrel=2
pkgdesc="2D rigid body simulation library for games"
url="http://www.box2d.org/"
license=('zlib')
arch=('i686' 'x86_64')
#depends=('freeglut')
makedepends=('cmake' 'doxygen' 'subversion')
optdepends=()
source=("${pkgname}-${pkgver}::svn+http://box2d.googlecode.com/svn/tags/v${pkgver}")
md5sums=('SKIP')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/Box2D
 
  msg "Starting build"
  [[ -d build ]] && rm -r build
  mkdir build && cd build 
	cmake .. \
    -DBOX2D_INSTALL=ON \
    -DBOX2D_INSTALL_DOC=ON \
    -DBOX2D_BUILD_SHARED=ON \
    -DBOX2D_BUILD_STATIC=OFF \
    -DBOX2D_BUILD_EXAMPLES=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/Box2D

  cd Documentation
  doxygen

  cd ../build

  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/Box2D/License.txt ${pkgdir}/usr/share/licenses/box2d/LICENSE
}
# vim: sw=2 ts=2 et:
