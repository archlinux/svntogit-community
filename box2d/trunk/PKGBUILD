# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=box2d
pkgver=2.4.0
pkgrel=1
pkgdesc="2D rigid body simulation library for games"
url="http://www.box2d.org/"
license=('custom:zlib')
arch=('x86_64')
depends=('gcc-libs')
makedepends=('cmake' 'doxygen' 'ninja')
# We're going to this alternate fork until the patches are upstreamed.
# See https://github.com/erincatto/box2d/issues/621
#source=("$pkgname-$pkgver.tar.gz::https://github.com/erincatto/Box2D/archive/v${pkgver}.tar.gz"
source=("https://github.com/jube/box2d/archive/adaedf1772ede7420bc9ee569c8cba7952db91a0.tar.gz")
sha512sums=('0caf1d0a391cdd4805949f5a2ba7ddd8cbc4dbe853bbf53f0a125eb07d4121ad851575db19f3705b087b222b9c7fcbaec964940b401dcb3df17116ffb6b10b9d')

build() {
  cd $pkgname-*

  cmake . \
    -Bbuild \
    -GNinja \
    -DBOX2D_BUILD_DOCS=ON \
    -DBOX2D_BUILD_TESTBED=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

check() {
  cd $pkgname-*

  build/unit_test
}

package() {
  cd $pkgname-*

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/box2d/LICENSE
}

# vim: sw=2 ts=2 et:
