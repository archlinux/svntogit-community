# $Id$
# Maintainer: Kyle Keen < keenerd at gmail >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=('bpython' 'bpython2')
pkgver=0.15
pkgrel=1
pkgdesc='Fancy ncurses interface to the Python interpreter'
arch=('any')
url='http://bpython-interpreter.org/'
license=('MIT')
depends=('python-pygments' 'python-requests' 'python-curtsies' 'python-greenlet' 'python-six')
optdepends=('python-urwid: for bpython-urwid'
            'python-jedi: multiline completion')
makedepends=('python-distribute' 'python2-distribute')
source=(http://bpython-interpreter.org/releases/bpython-${pkgver}.tar.gz)
md5sums=('0e0665aee5ed020a1ca97084cbe18787')

# python-watchdog optdep...
# python-curtsies a required dep, use old version for now

build() {
  cp -r bpython-$pkgver bpython2-$pkgver

  cd bpython-$pkgver
  python setup.py build

  cd "$srcdir/bpython2-$pkgver"
  python2 setup.py build
}

package_bpython() {
  cd bpython-$pkgver

  python setup.py install --root="$pkgdir" --optimize=0

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bpython/LICENSE"
}

package_bpython2() {
  depends=('python2-pygments' 'python2-requests' 'python2-curtsies' 'python2-greenlet' 'python2-six')
  optdepends=('python2-urwid: for bpython2-urwid'
              'python2-jedi: multiline completion')

  cd bpython2-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=0

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/bpython2/LICENSE"

  # conflicting files with bpython
  cd "$pkgdir/usr/bin/"
  mv bpython bpython2
  mv bpython-urwid bpython2-urwid
  mv bpython-curses bpython2-curses
  mv bpdb bpdb2

  cd "$pkgdir/usr/share/applications/"
  mv bpython.desktop bpython2.desktop
  # fix the .desktop file for the rename
  sed -i 's/bpython/&2/' bpython2.desktop

  cd "$pkgdir/usr/share/appdata/"
  mv bpython.appdata.xml bpython2.appdata.xml
  sed -i 's/bpython/&2/' bpython2.appdata.xml

  cd "$pkgdir/usr/share/pixmaps/"
  mv bpython.png bpython2.png
}
