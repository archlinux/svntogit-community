# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=brial
pkgver=1.2.7
pkgrel=1
pkgdesc="Library for polynomials over boolean rings"
arch=(x86_64)
url="https://github.com/BRiAl/BRiAl"
license=(GPL)
depends=(m4ri)
makedepends=(boost python)
optdepends=('python: Python bindings')
source=("https://github.com/BRiAl/BRiAl/releases/download/$pkgver/brial-$pkgver.tar.bz2")
sha256sums=('f9ff2b1bb21417a65b4adc5a957d3f152bd450e9207ede1ad3f72ecdd5eeaa2b')

build() {
  cd $pkgname-$pkgver 

  ./configure --prefix=/usr --enable-shared
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  make

  cd sage-brial
  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  cd sage-brial
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
