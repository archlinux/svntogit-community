# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=brial
pkgver=0.8.7
pkgrel=1
pkgdesc="Library for polynomials over boolean rings"
arch=(i686 x86_64)
url="https://github.com/BRiAl/BRiAl"
license=(GPL)
depends=(python2 m4ri)
makedepends=(boost)
conflicts=(polybori)
replaces=(polybori)
source=("https://github.com/BRiAl/BRiAl/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2")
sha256sums=('ed7d1d15e247b86aab02c1b744e0f0f4c7ce7aa89de7ceb15b07a4141762aefc')

build() {
  cd $pkgname-$pkgver 

  export PYTHON=python2
  export CXXFLAGS="$CXXFLAGS -std=c++98"
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
