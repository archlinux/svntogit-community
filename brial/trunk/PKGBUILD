# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=brial
pkgver=1.2.1
pkgrel=1
pkgdesc="Library for polynomials over boolean rings"
arch=(x86_64)
url="https://github.com/BRiAl/BRiAl"
license=(GPL)
depends=(python2 m4ri)
makedepends=(boost)
source=("https://github.com/BRiAl/BRiAl/releases/download/$pkgver/brial-$pkgver.tar.bz2")
sha256sums=('4382ed615c4fbd3770d4b92c6a7dd0557b199ad98abe31a41442be8460de95ab')

build() {
  cd $pkgname-$pkgver 

  export PYTHON=python2
  ./configure --prefix=/usr --enable-shared
  make

  cd sage-brial
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  cd sage-brial
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}
