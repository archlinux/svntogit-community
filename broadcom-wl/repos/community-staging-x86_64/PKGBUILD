# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=broadcom-wl            # Build against stock -ARCH kernel
#pkgname=broadcom-wl-custom    # Build agains kernel with a different name
_kernelname=${pkgname#broadcom-wl}
pkgver=6.30.223.271
pkgrel=25
pkgdesc='Broadcom 802.11 Linux STA wireless driver'
arch=('x86_64')
url='https://www.broadcom.com/support/download-search/?pf=Wireless+LAN+Infrastructure'
license=('custom')
depends=("linux${_kernelname}")
makedepends=("linux${_kernelname}-headers" "${pkgname%$_kernelname}-dkms=${pkgver}")

build() {
    _kernmajor="$(pacman -Q linux${_kernelname} | sed -r 's/linux'"${_kernelname}"' ([0-9]*.[0-9]*).*/\1/')"
    _kernver="$(</usr/lib/modules/extramodules-"${_kernmajor}${_kernelname:--ARCH}"/version)"

    dkms build --dkmstree "$srcdir" -m $pkgname/$pkgver -k $_kernver
}

package() {
    _kernmajor="$(pacman -Q linux${_kernelname} | sed -r 's/linux'"${_kernelname}"' ([0-9]*.[0-9]*).*/\1/')"
    _kernver="$(</usr/lib/modules/extramodules-${_kernmajor}${_kernelname:--ARCH}/version)"

    install -Dm644 -t "${pkgdir}/usr/lib/modules/extramodules-${_kernmajor}${_kernelname:--ARCH}" \
        ${pkgname}/${pkgver}/${_kernver}/${CARCH}/module/*.ko
    find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

    _license="/usr/share/licenses/${pkgname}-dkms/"
    if [[ -d $_license ]]; then
        install -Dm644 -t "${pkgdir}"/${_license/-dkms/} ${_license}/*
    fi
    _modprobe="/usr/lib/modprobe.d/${pkgname}-dkms.conf"
    if [[ -r ${_modprobe} ]]; then
        install -Dm644 ${_modprobe} "${pkgdir}"/${_modprobe/-dkms/}
    fi
}
