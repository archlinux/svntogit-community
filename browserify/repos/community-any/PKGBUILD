# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=browserify
pkgdesc='browser-side require() the node way'
pkgver=16.0.0
pkgrel=1
arch=('any')
url='http://browserify.org/'
license=('MIT' 'BSD')
depends=('nodejs' 'acorn')
makedepends=('npm' 'git')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
install=browserify.install
sha512sums=('fada5cfe504fac348cff90ce3ed296091bc50ef0baea550a5268dfb7a25ebc64bf9a1589c2b87f299f8bb15ab391b4e6ae37aa02c1836c84123190873c2cd43e')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done

  # Fix npm security derp
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
