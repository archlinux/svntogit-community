# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

_id=browserpass@maximbaz.com
_name=browserpass-extension
pkgname=browserpass-firefox
pkgver=3.4.1
pkgrel=1
pkgdesc="Firefox extension for Browserpass, browser extension for zx2c4's pass (password manager)"
arch=('any')
url="https://github.com/browserpass/${_name}"
license=('ISC')
depends=('browserpass')
source=("${pkgname}-${pkgver}.zip::${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.zip"
        "${pkgname}-${pkgver}.zip.asc::${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.zip.asc"
        "https://raw.githubusercontent.com/browserpass/browserpass-extension/master/LICENSE")
noextract=("${pkgname}-${pkgver}.zip")
sha256sums=('99164d7aeb851e05d325b1824f4008d89a415daf5371885d01234971f5ec5901'
            'SKIP'
            'f9fc49e2b3977f857bf3cbfbeb193bab8b2e17545978c162409d5270e6e0405a')
validpgpkeys=('EB4F9E5A60D32232BB52150C12C87A28FEAC6B20')

prepare() {
    mkdir -p "${pkgname}-${pkgver}"
    bsdtar -vxf "${pkgname}-${pkgver}.zip" -C "${pkgname}-${pkgver}"
}

package() {
    _dest="${pkgdir}/usr/share/mozilla/extensions/{ec8030f7-c20a-464f-9b0e-13a3a9e97384}/${_id}"
    install -dm755 "${_dest}"
    cp -a "${pkgname}-${pkgver}"/* "${_dest}"

    # Install host json from browserpass package
    install -dm755 "${pkgdir}/usr/lib/mozilla/native-messaging-hosts/"
    ln -sf "/usr/lib/browserpass/hosts/firefox/com.github.browserpass.native.json" "${pkgdir}/usr/lib/mozilla/native-messaging-hosts/"

    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}

# vim:set ts=4 sw=4 et:
