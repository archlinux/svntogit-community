# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BSEQuencer
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BSEQuencer"
pkgname=bsequencer
pkgver=1.8.8
pkgrel=1
pkgdesc="Multi channel MIDI step sequencer LV2 plugin with a variable matrix"
arch=('x86_64')
url="https://github.com/sjaehn/BSEQuencer"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('0c8c504ac126d0654aab02434564f2002544ef0cac0d9e276177f9af6306c0d9b94b35b6c851c3ee41683fd110750934ca57765449cf1d1a71fb1ea5bdd8d0b4')
b2sums=('992ce3d5d3f96a1405e33f26f6f8d134f66179f327410b43a18877407c082a2915fcacf4eb59ef65d617c606bb8dc2169b2dd8cd8e731315098319d5922c2218')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${_name}.lv2/" "${_plugin_uri}"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
