# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BShapr
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BShapr"
pkgname=bshapr
pkgver=0.8
pkgrel=2
pkgdesc="Beat/ envelope shaper LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BShapr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('60d6fe9a9db8e6e0c4f53deee0bc067b2ed8fae4fe85320e19c379be2378278367e535c7ce4dfeefa33c6a389670ab4ad4cd17c79bb97caf6745b2a7c64ef631')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -I "${_name}.lv2/" "${_plugin_uri}" || echo "Known to fail: https://github.com/sjaehn/BShapr/issues/7"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
