# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.18
pkgrel=2
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse2' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python2: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('d29f3816527c1eed105b45da559d025d61c8652144358227ed0ca3e516582cd8186fdcdf730b19fde6e7e9db1477690237444d2efe7c740f81f91032f1a12f86')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' scripts/btplay
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
