# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Campbell Jones <serebit at archlinux dot org>

pkgname=budgie-desktop-view
pkgver=1.2
pkgrel=2
pkgdesc='Desktop icons implementation for the Budgie Desktop'
arch=('x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop-view'
license=('Apache')
depends=('gtk3')
makedepends=('intltool' 'meson' 'vala')
source=("https://github.com/BuddiesOfBudgie/budgie-desktop-view/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc})
validpgpkeys=('96B4A0291094A86A2B7E3367DD672FE9A2BE5892'
              '0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl (Personal)
sha256sums=('8399fae4326e5a21dda889bd89cb16ed8201f2854dc5cbc12394b90420d4cf2e'
            'SKIP')

build() {
    arch-meson "$pkgname-$pkgver" build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
}
