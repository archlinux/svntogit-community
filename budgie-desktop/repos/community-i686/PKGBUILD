# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
_helper_pkgname=budgie-helper
pkgver=10.2.5
_helper_gitrev=4eb0d504101342c020c080ff9c9eb9f70ac39367
pkgrel=2
pkgdesc="Modern desktop environment from the Solus Project"
arch=('i686' 'x86_64')
url="https://solus-project.com/budgie"
license=('GPL' 'LGPL')
depends=('gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
install=$pkgname.install
source=("https://github.com/solus-project/budgie-desktop/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$_helper_pkgname-$_helper_gitrev.tar.gz::https://codeload.github.com/City-busz/$_helper_pkgname/tar.gz/$_helper_gitrev"
        "fix-theme.patch"
	"icon-tasklist.patch"
        "lightdm.patch")
sha256sums=('65bc36c67e4b0a68997fcd27b4c9058031f50b1b91d4b19923bf14f6b443b154'
            'bce565b20626828cf6d12eddfad5d7284b1fd0fbb099fd57e80c151203dad3d8'
            '3a5e05627f440ecda4f1c0e2634fc9438923b7d8f1eeb1d13c23f303b25af109'
            '45bd2947bd728398dbd3a904435dfb4012afcc2f72e54433e336b8b2a87b5f22'
            'f922cdea38f02e694c399e9a7cb23fbf4bb823c42cd77bb6e955b222a354f911')

prepare() {
	cd $pkgname-$pkgver

	# Theme: Use :checked pseudoclass for active switches
	# https://github.com/solus-project/budgie-desktop/pull/379
	patch -Np1 -i ../fix-theme.patch

	# icon-tasklist: support more GNOME applications
	patch -Np1 -i ../icon-tasklist.patch

	# Disable workspaces applet as it's broken
	# https://github.com/solus-project/budgie-desktop/issues/302
	sed -i ':a;N;$!ba;s/ \\\n\tworkspaces//' panel/applets/Makefile.am
	autoreconf -fi

	# Revert using LightDM instead of GNOME Screensaver
	patch -RNp1 -i ../lightdm.patch

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	make DESTDIR="$pkgdir" install
}
