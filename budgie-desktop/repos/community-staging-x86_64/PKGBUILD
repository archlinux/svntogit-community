# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.5.3
pkgrel=2
pkgdesc='Modern desktop environment from the Solus Project'
arch=('x86_64')
url='https://github.com/solus-project/budgie-desktop'
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-extra'
         'graphene' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('budgie-screensaver' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('budgie-desktop-view: Desktop icons'
            'budgie-screensaver: Lock screen'
            'gnome-control-center: System settings'
            'network-manager-applet: Network management')
source=("https://github.com/solus-project/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc}
        budgie-desktop-mutter9.patch::https://github.com/solus-project/budgie-desktop/commit/7a2835f2fa247e7201bb9e4a434bade8f4bfe86e.patch)
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F'  # Ikey Doherty (Solus Project Founder)
              '96B4A0291094A86A2B7E3367DD672FE9A2BE5892') # Joshua Strobl (Personal)
sha256sums=('878f4e6460c29740bf633c3b11ba97bcb788068c1460f82569938af2f1633b25'
            'SKIP'
            '65430e84e33b1529aebae169ad8e0392abf1b5c4bb1b454d46f17dc37469d763')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../budgie-desktop-mutter9.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
