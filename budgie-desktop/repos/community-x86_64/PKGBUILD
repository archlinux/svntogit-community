# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
_helper_pkgname=budgie-helper
pkgver=10.4+123+gf2dbc215
_helper_gitrev=3b52434b6d1cfe00c5bac1046d323813ce531a55
pkgrel=1
pkgdesc="Modern desktop environment from the Solus Project"
arch=('x86_64')
url="https://budgie-desktop.org/"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=f2dbc215fdb43de88e5b0876413b4a9dc77b2153  # master
source=("git+https://github.com/budgie-desktop/budgie-desktop.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
	0001-Port-to-mutter-3-from-GNOME-3.30.patch
	0002-Correct-GNOME-button-layout-schema-path.patch
        0003-Make-sure-vapi-workspace-def-use-real-c-header-filen.patch
	0004-Vala-0.41.92-does-not-allow-custom-getter-default-va.patch
	0005-Point-vapi-cheaders-to-their-upstream-equiv.patch
	0007-Check-if-schema-is-defined-for-volume-slider-changes.patch
        "$_helper_pkgname-$_helper_gitrev.tar.gz::https://codeload.github.com/City-busz/$_helper_pkgname/tar.gz/$_helper_gitrev")
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            '31f9284a9cf0cb5376fbdc22d32c1d05da4c4a83e3320c5d0d11fc5a6cb3e73e'
            '97d9b4e5f3133da36bd6e0bc2bdc30381adfdaf3b4735264b4d1fc845322ceef'
            '0967bf0213b501755cc9b011d1bd1d2d4a0d53b7ee3c744456bee098be5732b6'
            '25ad9407ee8b4147d726f6eb4f357bd57dcf46fa1e3ab8a67abff688c8a87192'
            '587b5cf6f69af96e1b214c371153d202f416a12dfabd6a49f3d24e179f595a32'
            '3025abcefa333a1aafb88f73305b7634162c4ace02e16291dc87fac2f56848fc'
            '3a4d7dd7c95ccba4e2916adf4a14769ffe54e8f86ed302d0268cd312b2a85c0e')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
	mkdir build
	cd $pkgname

	# Various fixes for GNOME 3.30
	patch -Np1 -i ../0001-Port-to-mutter-3-from-GNOME-3.30.patch
	patch -Np1 -i ../0002-Correct-GNOME-button-layout-schema-path.patch
	patch -Np1 -i ../0003-Make-sure-vapi-workspace-def-use-real-c-header-filen.patch
	patch -Np1 -i ../0004-Vala-0.41.92-does-not-allow-custom-getter-default-va.patch
	patch -Np1 -i ../0005-Point-vapi-cheaders-to-their-upstream-equiv.patch
	patch -Np1 -i ../0007-Check-if-schema-is-defined-for-volume-slider-changes.patch

	git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
	git submodule update --init

	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	arch-meson $pkgname build -D with-desktop-icons=none
	ninja -C build

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	DESTDIR="$pkgdir" ninja -C build install

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	make DESTDIR="$pkgdir" install
}
