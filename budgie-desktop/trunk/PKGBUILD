# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.6.2
pkgrel=1
pkgdesc='Modern desktop environment from the Solus Project'
arch=('x86_64')
url='https://github.com/solus-project/budgie-desktop'
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-extra'
         'graphene' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('budgie-screensaver' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('budgie-desktop-view: Desktop icons'
            'budgie-screensaver: Lock screen'
            'gnome-control-center: System settings'
            'network-manager-applet: Network management')
options=('debug')
source=("https://github.com/BuddiesOfBudgie/budgie-desktop/releases/download/v$pkgver/budgie-desktop-v$pkgver.tar.xz"{,.asc})
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F'  # Ikey Doherty (Solus Project Founder)
              '96B4A0291094A86A2B7E3367DD672FE9A2BE5892'  # Joshua Strobl (Personal)
              '0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl <me@joshuastrobl.com> 
sha256sums=('42b2b359d0c7f7cf08fb869f3b616d8f1a4d643013b216aab39544035e6a9d64'
            'SKIP')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
