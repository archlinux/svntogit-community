# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=buildah
pkgver=1.14.6
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache)
depends=(runc skopeo slirp4netns)
makedepends=(go-pie git bash-bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man runc skopeo)
_commit=09cce6292b7f4f6ee16da3e2001f34b1e4126679 # tags/v1.14.6
source=(git+https://github.com/containers/buildah.git#commit=$_commit)
md5sums=('SKIP')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^[vV]//;s/-/+/g'
}

build() {
  cd $pkgname 
  export GOFLAGS="-trimpath"
  export CGO_LDFLAGS="$LDFLAGS"
  make PREFIX=/usr BUILDTAGS="containers_image_ostree_stub seccomp"
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=usr install
  make DESTDIR="$pkgdir" PREFIX=usr install.completions
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
