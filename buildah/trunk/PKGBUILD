# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=buildah
pkgver=0.12
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/projectatomic/buildah"
license=(Apache)
depends=(ostree runc)
makedepends=('go-pie' 'git' 'bash-bats' 'btrfs-progs' 'device-mapper' 'gpgme'
             'libassuan' 'bzip2' 'go-md2man' 'runc' 'skopeo')
_commit=e073df11aa75bd20a8e01cad1956e1fabc7c9895  # tags/v0.12
source=(git://github.com/projectatomic/buildah.git#commit=$_commit
        0001-Revert-ostree-add-selinux-label-for-each-file.patch)
md5sums=('SKIP'
         'f59d0b2a1def58f509b277706533c393')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  pushd buildah/vendor/github.com/containers/image/
  patch -p1 -i "$srcdir/0001-Revert-ostree-add-selinux-label-for-each-file.patch"
  popd

  mkdir -p src/github.com/projectatomic
  cp -r $pkgname src/github.com/projectatomic
}

build() {
  export GOPATH="${srcdir}"
  cd src/github.com/projectatomic/buildah
  make PREFIX=/usr
}

package() {
  cd src/github.com/projectatomic/buildah
  make DESTDIR="$pkgdir" PREFIX=usr install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
