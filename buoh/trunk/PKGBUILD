# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=buoh
pkgver=0.8.2
pkgrel=6
pkgdesc="Reader for online strips comics"
arch=('i686' 'x86_64')
url="http://buoh.steve-o.org/"
license=('GPL')
depends=('gtk2' 'libsoup>=2.4.0' 'gconf' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!emptydirs')
install=buoh.install
source=("http://buoh.steve-o.org/downloads/$pkgname-$pkgver.tar.bz2"
        buoh-libsoup24.patch)
md5sums=('50474a8712ad20ab36d8f8058a4647fb'
         'c44f0e1828492a5d7d8efee764558fb4')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Port to libsoup 2.4
  patch -p1 < ../buoh-libsoup24.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas --without-libgnomeui
  make AM_CFLAGS=-lxml2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
