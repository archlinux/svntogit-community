# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jakob Matthes <jakob.matthes@gmail.com>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: rakoo (AUR)
# Contributor: SanskritFritz (gmail)

pkgname=bup
pkgver=0.26
pkgrel=1
pkgdesc='Highly efficient file backup system based on the git packfile format'
arch=('i686' 'x86_64')
url='https://github.com/bup/bup'
license=('GPL')
depends=('python2-fuse' 'par2cmdline' 'pylibacl' 'python2-pyxattr' 'git')
makedepends=('ruby-ronn')
source=($url/archive/0.26.tar.gz
        python2.patch
        avoid-using-pandoc.patch)
md5sums=('371ff8bb4c93c4282039ea564738a244'
         'e4eb211fa932db8969b25c0d567abc90'
         '948e60e903446f408067120fc8bf25e9')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../python2.patch
  patch -p1 -i ../avoid-using-pandoc.patch
}

build() {
  make -C $pkgname-$pkgver
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
