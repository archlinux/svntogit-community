# $Id$
# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jakob Matthes <jakob.matthes@gmail.com>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: rakoo (AUR)
# Contributor: SanskritFritz (gmail)

pkgname=bup
pkgver=0.29.1
pkgrel=1
pkgdesc='Efficient backup system based on the git packfile format'
arch=('x86_64' 'i686')
url='https://bup.github.io/'
license=('GPL')
depends=('python2-fuse' 'par2cmdline' 'pylibacl' 'python2-pyxattr' 'git')
makedepends=('ruby-ronn' 'git' 'setconf' 'pandoc')
checkdepends=('rsync' 'python2-tornado')
optdepends=('python2-tornado: for bup web')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bup/bup/archive/${pkgver}.tar.gz")
sha512sums=('2ad142214cf5ee61e2113b49d02761cdf8fdd5928eb4f885230af71f149ba33ee748e1d96e240903f54d3264dc2ad1614e6ece66027b4c7ed1e97b6147a9685e')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}
