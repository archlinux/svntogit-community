# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=bustle
pkgver=0.8.0
pkgrel=12
pkgdesc="Draw sequence diagrams of D-Bus traffic"
url="https://gitlab.freedesktop.org/bustle/bustle#readme"
license=("LGPL")
arch=('x86_64')
depends=('ghc-libs' 'glib2' 'libpcap' 'haskell-cairo' 'haskell-gio' 'haskell-glib' 'haskell-gtk3'
         'haskell-pango')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-test-framework'
             'haskell-test-framework-hunit')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('2fb3e9d9a97db3cd9a3ce763e86947b642bd7733b46206177a1677f08726573c')

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -f-hgettext -f-interactivetests -fthreaded

  runhaskell Setup build $MAKEFLAGS
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
