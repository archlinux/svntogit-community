# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.19.2
pkgrel=2
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="http://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
source=($url/downloads/$pkgname-$pkgver.tar.bz2
	config)
md5sums=('50267054345f1a0b77fe65f6e0e5ba29'
         'edd5c66c974ccc1b0030fd00beaa1b02')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp $srcdir/config .config
  # if you want to run menuconfig uncomment the following line:
  # make menuconfig ; return 1
  sed -i '1,2i#define __packed __attribute__((packed))' miscutils/ubi_tools.c
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm4755 busybox $pkgdir/bin/busybox
}
