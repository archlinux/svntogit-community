# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sven Kauber <celeon@gmail.com>

pkgname=bwm-ng
pkgver=0.6.3
pkgrel=2
pkgdesc="A small and simple console-based live bandwidth monitor"
arch=('x86_64')
url="https://www.gropp.org/?id=projects&sub=bwm-ng"
license=('GPL')
depends=('ncurses')
#source=("https://www.gropp.org/bwm-ng/${pkgname}-${pkgver}.tar.gz")
source=("$pkgname-$pkgver.tgz::https://github.com/vgropp/bwm-ng/archive/v${pkgver}.tar.gz"
         https://github.com/vgropp/bwm-ng/commit/6a2087db.patch)
sha256sums=('c1a552b6ff48ea3e4e10110a7c188861abc4750befc67c6caaba8eb3ecf67f46'
            'ae8e4514355c8d53e5618ae1fd0d16d06d4192b10e4aa000d0827afa5579376c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../6a2087db.patch # Fix build with -Wformat-security
}

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr --mandir="$pkgdir/usr/share/man"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix="$pkgdir" install

  install -d "$pkgdir/usr"
  mv "$pkgdir/bin" "$pkgdir/usr/"
}
