# $Id$
# Maintainer:
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=bzrtp
pkgver=1.0.3
pkgrel=1
pkgdesc="Opensource implementation of ZRTP keys exchange protocol"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL2')
depends=('libxml2' 'mbedtls')
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')
source=("http://download.savannah.gnu.org/releases/linphone/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
	mbedtls2.patch)
md5sums=('0d0c694e114d85420d351b94f0e9d867'
         'SKIP'
         '5d181d8b367a4bb50a49222f00cb6751')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 <$srcdir/mbedtls2.patch
  autoreconf
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
