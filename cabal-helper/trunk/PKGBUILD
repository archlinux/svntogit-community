# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-helper
pkgver=0.8.0.2
pkgrel=5
pkgdesc="Simple interface to some of Cabals configuration state used by ghc-mod"
url="https://hackage.haskell.org/package/${pkgname}"
license=("AGPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions' 'haskell-mtl' 'haskell-semigroupoids' 'haskell-temporary'
         'haskell-unix-compat' 'haskell-utf8-string')
makedepends=('ghc' 'haskell-ghc' 'haskell-ghc-paths')
conflicts=('haskell-cabal-helper')
replaces=('haskell-cabal-helper')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d922422b2749be12fce97cc403e4aab812c9586f35bce89a08aefa2516fc58fc78a88df98355ec7be9140e24f0bf3cb230ac43ce484a60d90754d46c5e2d42ba')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --libexecdir="/usr/lib" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
