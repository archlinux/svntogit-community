# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=2.0.0.1
pkgrel=3
pkgdesc="The command-line interface for Cabal and Hackage."
url="https://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base16-bytestring' 'haskell-cryptohash-sha256'
         'haskell-echo' 'haskell-edit-distance' 'haskell-hackage-security' 'haskell-hashable'
         'haskell-http' 'haskell-mtl' 'haskell-network' 'haskell-network-uri' 'haskell-random'
         'haskell-stm' 'haskell-tar' 'haskell-zlib')
makedepends=('ghc' 'haskell-pretty-show' 'haskell-quickcheck' 'haskell-tagged' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
sha512sums=('136105745e22b83a9ab906ac71ebbf4a5992b1731dd152e88eaa8dfd0d3076ddaca80bf0077c8b59c085c7d2f2fd0a3786e6e4c70a47dc924995f23758952a16')

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/${pkgname}" --enable-tests \
    -fnetwork-uri -f-old-directory -f-old-bytestring -f-debug-expensive-assertions -f-debug-conflict-sets \
    -f-debug-tracetree -f-parsec -f-monolithic
  runhaskell Setup build
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test || warning "TODO: pass -dynamic somewhere"
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
