# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=2.4.0.0
pkgrel=146
pkgdesc="The command-line interface for Cabal and Hackage."
url="https://hackage.haskell.org/package/cabal-install"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base16-bytestring' 'haskell-cryptohash-sha256'
         'haskell-echo' 'haskell-edit-distance' 'haskell-hackage-security' 'haskell-hashable'
         'haskell-http' 'haskell-network' 'haskell-network-uri' 'haskell-random' 'haskell-resolv'
         'haskell-tar' 'haskell-zlib' 'haskell-zip-archive')
makedepends=('ghc')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
sha512sums=('caf49ec35d5665164b76db3a6219c0409243fb80fd7a740a91933732fbb26fc3efef88ddc3d769a76d0f76811ea83a6a7effdab5ad8264da808d4469c9faf125')

prepare() {
  cd $pkgname-$pkgver
  sed -i -e 's/< *0.4/<1/' -e 's/< *2.8/<4/' $pkgname.cabal
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/${pkgname}"
  runhaskell Setup build
}

package() {
  cd $pkgname-$pkgver
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
