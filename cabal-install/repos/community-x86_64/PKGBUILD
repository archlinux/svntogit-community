# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=2.0.0.0
pkgrel=2
pkgdesc="The command-line interface for Cabal and Hackage."
url="https://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' "haskell-async" "haskell-base16-bytestring" "haskell-cryptohash-sha256"
         "haskell-echo" "haskell-edit-distance"
         "haskell-hackage-security" "haskell-hashable" "haskell-http" "haskell-mtl"
         "haskell-network" "haskell-network-uri" "haskell-random" "haskell-stm" "haskell-tar"
         "haskell-zlib")
makedepends=('ghc')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
sha512sums=('4aac5f27d3d8650224bff5ae24e835d9b772596f3b455ef141bef77c831eebd252e1a17f19c3d534752aa03664273053f151fa3187bd7cd267eaab2a0e61b82d')

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --docdir="/usr/share/doc/${pkgname}" \
    -fnetwork-uri -f-old-directory -f-old-bytestring -f-debug-expensive-assertions -f-debug-conflict-sets \
    -f-debug-tracetree -f-parsec -f-monolithic
  runhaskell Setup build
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
