# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=3.0.0.0
pkgrel=31
pkgdesc="The command-line interface for Cabal and Hackage."
url="https://hackage.haskell.org/package/cabal-install"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base16-bytestring' 'haskell-cryptohash-sha256'
         'haskell-echo' 'haskell-edit-distance' 'haskell-hackage-security' 'haskell-hashable'
         'haskell-http' 'haskell-network' 'haskell-network-uri' 'haskell-random' 'haskell-resolv'
         'haskell-tar' 'haskell-zlib')
makedepends=('ghc')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz"
        cabal-install-hackage-security-0.6.patch::https://github.com/haskell/cabal/commit/0165f0a739a634ed03e6c87f258ebb5810005d21.patch)
sha512sums=('0b15313fc11bc6b6018e01b6a3a65242306977c16ce9333ea1191c1ce0020b180451235a7a65b47bc01791e05d1b246bf9d573fc7e981a46fcfc7da0fed04cca'
            'cbdb8af7c76424fd395f83e5beedc1bd4f97915e4b4cff007b4d69c515e2f9df8a100cdbabe7ec481ba177596bfb852555c65aa3fa5c1911ddaae9a5aa467fae')

prepare() {
  cd $pkgname-$pkgver
  patch -p2 -i ../cabal-install-hackage-security-0.6.patch || :
  sed -i -e 's/< *4/<5/' $pkgname.cabal
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/${pkgname}"
  runhaskell Setup build
}

package() {
  cd $pkgname-$pkgver
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
