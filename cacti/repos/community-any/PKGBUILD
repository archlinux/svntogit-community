# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tuxbubling <tuxbubling@jabber.fr>

pkgname=cacti
pkgver=1.2.5
pkgrel=1
pkgdesc="Network graphing solution using RRDTool"
arch=('any')
url="https://www.cacti.net"
license=('GPL2')
depends=('php-gd' 'php-snmp' 'rrdtool' 'ttf-dejavu')
optdepends=('mariadb: use local MySQL server'
            'php-fpm: run in fastCGI process manager'
            'uwsgi: run as local application container')
backup=('etc/webapps/cacti/.htaccess'
        'etc/webapps/cacti/config.php')
source=("https://www.${pkgname}.net/downloads/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.uwsgi"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
install="${pkgname}.install"
sha512sums=('991847ded6045c8c6111c58c5f8687f2497183a545158924bba92ee5d0b7de3e658ddbfce6f59c651fa29118aedb271a438c5668a3db4b93f97f82fe721f0a32'
            'a87241b12226fcad9e161d0f4cd344161015b5fa8e2f1f3af4431d22bd87aad8a4f9553226baed98d48376819e75266a50fd796b1c884b4e597ccf38a5e4de01'
            '847e2b791de44d0790a2fdb81c77c8af9a66da9d44500f3f8a8d1c0f406d3a20082cc8fef1c6afe4de93ad989d35c79c9809abe14693a9ac6ea74d4696e3b6c1'
            'e833e411f74e77773c32589ba83cb1b2f28ca9b35931626480ab7daa63420d47ecfc3061e6703323646b69e1d98536b6f3afdd36faa483fb13aac9b818af0c6e')

prepare() {
  # adding default .htaccess
  echo "Require all denied" > "${pkgname}-htaccess"
  cd "${pkgname}-${pkgver}"
  # setting correct install path for spine
  sed -e 's|/usr/local/spine/bin/spine|/usr/bin/spine|g' \
      -i install/functions.php
  # setting correct path for dejavu font
  sed -e 's|/usr/share/fonts/dejavu/|/usr/share/fonts/TTF/|g' -i lib/rrd.php
  # remove unneeded executable bits
  find . -executable -type f -and -not -path "*scripts*" -exec chmod -c 644 {} \;
}

package() {
  cd "${pkgname}-${pkgver}"
  # webapp
  install -vDm 644 include/config.php -t "${pkgdir}/etc/webapps/${pkgname}"
  rm -v include/config.php
  install -vDm 644 *.{php,sql} -t "${pkgdir}/usr/share/webapps/${pkgname}"
  install -vDm 644 cli/{*.php,.htaccess} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/cli"
  install -vDm 644 docs/*.{css,html,sql} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/docs"
  install -vDm 644 docs/images/*.png \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/docs/images"
  install -vDm 644 formats/*.{format,php} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/formats"
  install -vDm 644 images/*.{gif,ico,png,svg} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/images"
  install -vDm 644 include/{*.{php,js},cacti_version} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include"
  install -vDm 644 include/content/{*.{html,php},README} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/content"
  install -vDm 644 include/fa/index.php \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/fa"
  install -vDm 644 include/fa/css/*.{css,php} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/fa/css"
  install -vDm 644 include/fa/less/*.{less,php} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/fa/less"
  install -vDm 644 include/fa/scss/*.{php,scss} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/fa/scss"
  install -vDm 644 include/fa/webfonts/*.{eot,php,svg,ttf,woff,woff2} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/fa/webfonts"
  install -vDm 644 include/fonts/*.ttf \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/fonts"
  install -vDm 644 include/js/*.{js,php} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/js"
  install -vDm 644 include/js/LC_MESSAGES/*.{js,php} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/js/LC_MESSAGES"
  install -vDm 644 include/themes/index.php \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/include/themes"
  for theme in {classic,dark,modern,paper-plane,paw,sunrise}; do
    install -vDm 644 include/themes/${theme}/*.{css,js,php} \
      -t "${pkgdir}/usr/share/webapps/${pkgname}/include/themes/${theme}"
    install -vDm 644 include/themes/${theme}/default/*.{css,gif,php,png} \
      -t "${pkgdir}/usr/share/webapps/${pkgname}/include/themes/${theme}/default"
    install -vDm 644 include/themes/${theme}/images/*.{gif,ico,png,svg} \
      -t "${pkgdir}/usr/share/webapps/${pkgname}/include/themes/${theme}/images"
    install -vDm 644 include/themes/${theme}/images/128/*.png \
      -t "${pkgdir}/usr/share/webapps/${pkgname}/include/themes/${theme}/images/128"
  done
  cp -av include/vendor "${pkgdir}/usr/share/webapps/${pkgname}/include/"
  install -vDm 644 install/*.{css,csv,js,php} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/install"
  install -vDm 644 install/templates/*.gz \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/install/templates"
  install -vDm 644 install/upgrades/*.php \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/install/upgrades"
  install -vDm 644 lib/*.php \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/lib"
  install -vDm 644 locales/index.php \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/locales"
  install -vDm 644 locales/LC_MESSAGES/*.{mo,php} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/locales/LC_MESSAGES"
  install -vDm 644 locales/po/*.{php,po,pot} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/locales/po"
  install -vDm 644 mibs/{index.php,*-MIB} \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/mibs"
  install -vDm 644 plugins/index.php \
    -t "${pkgdir}/usr/share/webapps/${pkgname}/plugins"
  # config
  install -vDm 644 "${srcdir}/${pkgname}-htaccess" \
    "${pkgdir}/etc/webapps/${pkgname}/.htaccess"
  ln -sv "/etc/webapps/${pkgname}/.htaccess" \
    "${pkgdir}/usr/share/webapps/${pkgname}/.htaccess"
  ln -sv "/etc/webapps/${pkgname}/config.php" \
    "${pkgdir}/usr/share/webapps/${pkgname}/include/config.php"
  # state
  install -vdm 750 "${pkgdir}/var/lib/${pkgname}"
  install -vDm 644 rra/.htaccess -t "${pkgdir}/var/lib/${pkgname}/rra"
  ln -sv "/var/lib/${pkgname}/rra" \
    "${pkgdir}/usr/share/webapps/${pkgname}/rra"
  install -vDm 644 resource/index.php \
    -t "${pkgdir}/var/lib/${pkgname}/resource"
  install -vDm 644 resource/snmp_queries/*.{php,xml} \
    -t "${pkgdir}/var/lib/${pkgname}/resource/snmp_queries/"
  install -vDm 644 resource/script_server/*.{php,xml} \
    -t "${pkgdir}/var/lib/${pkgname}/resource/script_server/"
  install -vDm 644 resource/script_queries/*.{php,xml} \
    -t "${pkgdir}/var/lib/${pkgname}/resource/script_queries/"
  ln -sv "/var/lib/${pkgname}/resource" \
    "${pkgdir}/usr/share/webapps/${pkgname}/resource"
  install -vDm 644 scripts/*.{php,pl,sh} \
    -t "${pkgdir}/var/lib/${pkgname}/scripts"
  ln -sv "/var/lib/${pkgname}/scripts" \
    "${pkgdir}/usr/share/webapps/${pkgname}/scripts"
  # cache
  install -vdm 750 "${pkgdir}/var/cache/${pkgname}"
  install -vDm 644 cache/boost/index.php \
    -t "${pkgdir}/var/cache/${pkgname}/boost"
  install -vDm 644 cache/mibcache/index.php \
    -t "${pkgdir}/var/cache/${pkgname}/mibcache"
  install -vDm 644 cache/realtime/index.php \
    -t "${pkgdir}/var/cache/${pkgname}/realtime"
  install -vDm 644 cache/spikekill/index.php \
    -t "${pkgdir}/var/cache/${pkgname}/spikekill"
  ln -sv "/var/cache/${pkgname}" \
    "${pkgdir}/usr/share/webapps/${pkgname}/cache"
  # log
  install -vdm 750 "${pkgdir}/var/log/${pkgname}"
  install -vDm 644 log/.htaccess -t "${pkgdir}/var/log/${pkgname}"
  ln -sv "/var/log/${pkgname}" "${pkgdir}/usr/share/webapps/${pkgname}/log"
  # docs
  install -vDm 644 {CHANGELOG,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  # tmpfiles.d
  install -vDm 644 "${srcdir}/${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # sysusers.d
  install -vDm 644 "${srcdir}/${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # uwsgi config
  install -vDm 644 "${srcdir}/${pkgname}.uwsgi" \
    "${pkgdir}/etc/uwsgi/${pkgname}.ini"
}
