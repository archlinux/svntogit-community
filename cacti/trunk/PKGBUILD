# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tuxbubling <tuxbubling@jabber.fr>

pkgname=cacti
pkgver=1.1.37
pkgrel=1
pkgdesc="Complete network graphing solution designed to harness the power of RRDTool's data storage and graphing functionality."
arch=('any')
url="https://www.cacti.net"
license=('GPL2')
depends=('rrdtool' 'mariadb' 'php-gd' 'net-snmp' 'xorg-fonts-100dpi')
backup=('etc/webapps/cacti/.htaccess'
        'etc/webapps/cacti/config.php')
source=("https://www.${pkgname}.net/downloads/${pkgname}-${pkgver}.tar.gz"
        'tmpfiles.conf')
sha512sums=('d6f434fa8a5309d542ce1108ba6e9e79fd6f2203b42cb4aaeb429cbc3302c362cc24be566d09e0069dbe9d307ba20512d8096bd85b78e7f3b9f8cb298cb23e92'
            '6d25f182141ca7d13388f7241eb27d783ea99b991c0b147382f8ec9eeeb521303531f8ad9dd7c968cb36b7bb657cb517b8c7f46fd1d777e1b10923d76e4efbd6')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove unneeded executable bits
  find . -executable -type f -and -not -path "*scripts*" -exec chmod -c 644 {} \;
}

package() {
  # create directories
  install -vdm0755 "${pkgdir}/usr/share/webapps/${pkgname}" \
    "${pkgdir}/etc/webapps/${pkgname}" \
    "${pkgdir}/var/log/" "${pkgdir}/var/lib/${pkgname}/"

  # move sources
  mv -v "$srcdir/${pkgname}-${pkgver}"/* \
    "${pkgdir}/usr/share/webapps/${pkgname}"

  # move and symlink configuration
  echo "Require all denied" >> "${pkgdir}/etc/webapps/${pkgname}/.htaccess"
  mv "${pkgdir}/usr/share/webapps/${pkgname}/include/config.php" \
    "${pkgdir}/etc/webapps/${pkgname}"
  ln -sv "/etc/webapps/${pkgname}/.htaccess" \
    "${pkgdir}/usr/share/webapps/${pkgname}/.htaccess"
  ln -sv "/etc/webapps/${pkgname}/config.php" \
    "${pkgdir}/usr/share/webapps/${pkgname}/include/config.php"

  # move and symlink var directory
  mv -v "${pkgdir}/usr/share/webapps/${pkgname}/rra" \
    "${pkgdir}/var/lib/${pkgname}/rra"
  ln -sv "/var/lib/${pkgname}/rra" \
    "${pkgdir}/usr/share/webapps/${pkgname}/rra"

  # move and symlink log directory
  mv -v "${pkgdir}/usr/share/webapps/${pkgname}/log" \
    "${pkgdir}/var/log/${pkgname}"
  ln -sv "/var/log/${pkgname}" "${pkgdir}/usr/share/webapps/${pkgname}/log"

  # tmpfiles.d
  install -vDm 644 "${srcdir}/tmpfiles.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
