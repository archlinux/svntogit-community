# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: catalyst <catalyst@xaker.ru>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: jackobw
# Contributor: Gianni Vialetto <g dot vialetto at gmail dot com>

pkgname=cairo-compmgr
pkgver=git20110320
_commit=711fc7275b79cc802dc69658eb714ddc50666c06
pkgrel=2
epoch=1
pkgdesc="A Cairo based composite manager"
arch=(i686 x86_64)
url="http://cairo-compmgr.tuxfamily.org/"
license=(GPL)
makedepends=(gettext gtk-doc "intltool>=0.41" git)
depends=("gtk2>=2.16.0" "vala>=0.11.6" libsm gconf)
install=cairo-compmgr.install
options=(!libtool)
#source=(http://download.tuxfamily.org/ccm/$pkgname/$pkgname-$pkgver.tar.gz)
source=()
md5sums=()

__gitroot="git://git.tuxfamily.org/gitroot/ccm/cairocompmgr.git"
__gitname="cairo-compmgr"


build() {
#  cd "$srcdir/$pkgname-$pkgver"
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [ -d $__gitname ] ; then
    cd $__gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $__gitroot $__gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$__gitname-build"
  git clone "$srcdir/$__gitname" "$srcdir/$__gitname-build"
  cd "$srcdir/$__gitname-build"
  git checkout "$_commit"

  sed -i 's!vala-0.10!libvala-0.12!' vapi/cairo-compmgr.deps configure.ac
  ./autogen.sh --prefix=/usr
  make -j1
}

package() {
  #cd "$srcdir/$pkgname-$pkgver"
  cd "$srcdir/$__gitname-build"

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/gconf/schemas/"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/cairo-compmgr.schemas" "$pkgdir/etc/gconf/schemas/*.schemas"
  rm -rf "$pkgdir/etc/gconf/schemas/"
}
