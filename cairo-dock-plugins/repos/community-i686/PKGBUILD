# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Tofe <chris dot chapuis at gmail dot com>
# Contributor: zhuqin <zhuqin83 at gmail dot com>
# Contributor: tri1976 <trile7 at gmail dot com>
# Contributor: snoopy33 <snoopy33 at no dash log dot org>

pkgname=cairo-dock-plugins
pkgver=3.3.2
pkgrel=1
pkgdesc='Plugins for Cairo-Dock'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname%-*}-plug-ins"
license=('GPL')
depends=('cairo-dock')
makedepends=('cmake' 'alsa-lib' 'fftw' 'gnome-menus' 'gtk-sharp-2' 'gvfs' 'libetpan' 'libexif' 'libical' 'libpulse' 'libxklavier' 'libzeitgeist' 'lm_sensors' 'ndesk-dbus-glib' 'python' 'python2' 'ruby' 'upower' 'vala' 'vte3' 'webkitgtk3')
optdepends=('alsa-lib: Sound Control, Sound Effects applets'
            'fftw: Impulse applet'
            'gnome-menus: Applications Menu applet'
            'gtk-sharp-2: Mono API'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'libzeitgeist: Recent Events applet'
            'lm_sensors: System Monitor applet'
            'ndesk-dbus-glib: Mono API'
            'python: Python 3 API'
            'python2: Python 2 API'
            'ruby: Ruby API'
            'upower: Logout, PowerManager applets'
            'vte3: Terminal applet'
            'webkitgtk3: Weblets applet')
source=("${url}/${pkgver%.?}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('48f0caddb0a563d8a64176f66610ca35df5c952071e5574486d39bb574994643')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
