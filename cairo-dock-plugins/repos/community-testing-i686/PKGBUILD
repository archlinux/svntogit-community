# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Contributor: tri1976 <trile7@gmail.com>
# Contributor: snoopy33 <snoopy33@no-log.org>

pkgname=cairo-dock-plugins
pkgver=3.4.0
pkgrel=2
pkgdesc='Plugins for Cairo-Dock'
arch=('i686' 'x86_64')
url='http://glx-dock.org'
license=('GPL')
depends=('cairo-dock')
makedepends=('alsa-lib' 'cmake' 'fftw' 'gnome-menus' 'gtk-sharp-2' 'gvfs'
             'libetpan' 'libexif' 'libical' 'libpulse' 'libxklavier'
             'libzeitgeist' 'lm_sensors' 'ndesk-dbus-glib' 'python' 'python2'
             'ruby' 'vala' 'vte3' 'webkitgtk3')
optdepends=('alsa-lib: Sound Control, Sound Effects applets'
            'fftw: Impulse applet'
            'gnome-menus: Applications Menu applet'
            'gtk-sharp-2: Mono API'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'libzeitgeist: Recent Events applet'
            'lm_sensors: System Monitor applet'
            'ndesk-dbus-glib: Mono API'
            'python: Python 3 API'
            'python2: Python 2 API'
            'ruby: Ruby API'
            'vte3: Terminal applet'
            'webkitgtk3: Weblets applet')
source=("https://launchpad.net/cairo-dock-plug-ins/${pkgver%.?}/${pkgver}/+download/cairo-dock-plugins-${pkgver}.tar.gz")
sha256sums=('7ffcf40f07cd05bef826ad47f1f11940b9813d279c9e5117384f14f1f32d8dbd')

prepare() {
  cd cairo-dock-plugins-${pkgver}

  sed 's/vte-2.90/vte-2.91/' -i CMakeLists.txt
  sed 's/(2,91,0)/(0,38,1)/g' -i terminal/src/terminal-widget.c
}

build() {
  cd cairo-dock-plugins-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-plugins-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
