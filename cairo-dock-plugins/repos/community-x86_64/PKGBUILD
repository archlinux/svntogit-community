# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Contributor: tri1976 <trile7@gmail.com>
# Contributor: snoopy33 <snoopy33@no-log.org>

pkgname=cairo-dock-plugins
pkgver=3.2.1
pkgrel=2
pkgdesc="Plugins for Cairo-Dock"
url="https://launchpad.net/cairo-dock"
license=('GPL')
arch=('i686' 'x86_64')
depends=('cairo-dock')
makedepends=('cmake' 'alsa-lib' 'fftw' 'gnome-menus' 'gtk-sharp-2' 'gvfs' 'libetpan' 'libexif' 'libical' 'libpulse' 'libxklavier' 'libzeitgeist' 'lm_sensors' 'ndesk-dbus-glib' 'python' 'ruby' 'upower' 'vala' 'vte3' 'webkitgtk3')
optdepends=('alsa-lib: AlsaMixer, Sound Effects applets'
            'fftw: Impulse applet'
            'gnome-menus: GMenu applet'
            'gtk-sharp-2: Mono API'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'libzeitgeist: Recent Events applet'
            'lm_sensors: System Monitor applet'
            'ndesk-dbus-glib: Mono API'
            'python: Python API'
            'ruby: Ruby API'
            'upower: Logout, PowerManager applets'
            'vte3: Terminal applet'
            'webkitgtk3: Weblets applet')
options=('!libtool')
source=("http://launchpad.net/cairo-dock-plug-ins/3.2/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('11b4433e40bbb1507757b23e167c521d620827576c2f30bb1a9642e0bff57e01')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/build

  make DESTDIR=${pkgdir} install
}

# vim: ts=2 sw=2 et:
