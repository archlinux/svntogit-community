# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Contributor: tri1976 <trile7@gmail.com>
# Contributor: snoopy33 <snoopy33@no-log.org>

pkgname=cairo-dock-plugins
pkgver=3.3.2.r3121
pkgrel=1
pkgdesc='Plugins for Cairo-Dock'
arch=('i686' 'x86_64')
url='https://launchpad.net/cairo-dock-plug-ins'
license=('GPL')
depends=('cairo-dock')
makedepends=('bzr' 'cmake' 'alsa-lib' 'fftw' 'gnome-menus' 'gtk-sharp-2' 'gvfs'
             'libetpan' 'libexif' 'libical' 'libpulse' 'libxklavier'
             'libzeitgeist' 'lm_sensors' 'ndesk-dbus-glib' 'python' 'python2'
             'ruby' 'vala' 'vte3' 'webkitgtk3')
optdepends=('alsa-lib: Sound Control, Sound Effects applets'
            'fftw: Impulse applet'
            'gnome-menus: Applications Menu applet'
            'gtk-sharp-2: Mono API'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'libzeitgeist: Recent Events applet'
            'lm_sensors: System Monitor applet'
            'ndesk-dbus-glib: Mono API'
            'python: Python 3 API'
            'python2: Python 2 API'
            'ruby: Ruby API'
            'vte3: Terminal applet'
            'webkitgtk3: Weblets applet')
#source=("https://launchpad.net/cairo-dock-plug-ins/${pkgver%.?}/${pkgver}/+download/cairo-dock-plugins-${pkgver}.tar.gz")
source=("cairo-dock-plugins::bzr+lp:cairo-dock-plug-ins#revision=${pkgver#*.r}"
        'cd-terminal-vte2.91.patch')
sha256sums=('SKIP'
            '7f8cd8f5532f2d08e23aa98a3a8e161c08b6865d11c15a698986515465c1d263')

prepare() {
  cd cairo-dock-plugins

  patch -Np1 -i ../cd-terminal-vte2.91.patch
}

build() {
  cd cairo-dock-plugins

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-plugins/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
