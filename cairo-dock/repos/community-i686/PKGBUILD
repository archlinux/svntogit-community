# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.4.0
pkgrel=1
pkgdesc='Light eye-candy fully themable animated dock'
arch=('i686' 'x86_64')
url='https://launchpad.net/cairo-dock-core'
license=('GPL')
depends=('curl' 'dbus-glib' 'glu' 'gtk3' 'librsvg')
makedepends=('bzr' 'cmake')
optdepends=('cairo-dock-plugins: Plugins for Cairo-Dock')
source=("https://launchpad.net/cairo-dock-core/${pkgver%.?}/${pkgver}/+download/cairo-dock-${pkgver}.tar.gz")
sha256sums=('7843390bddce79497343e8bd3b8fbe528b7c6a65968d31595fd62ee12e284270')

build() {
  cd cairo-dock-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
