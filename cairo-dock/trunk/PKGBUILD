# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Tofe <chris dot chapuis at gmail dot com>
# Contributor: erm67 <erm67 at yahoo dot it>

pkgname=cairo-dock
pkgver=3.3.2
pkgrel=1
pkgdesc='Light eye-candy fully themable animated dock'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname}-core"
license=('GPL')
depends=('curl' 'dbus-glib' 'gtk3' 'librsvg')
makedepends=('cmake')
optdepends=('cairo-dock-plugins: Plugins for Cairo-Dock')
source=("${url}/${pkgver%.?}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('a2d3854771d7ef5b262593b37aed216ece7eeacfc3e2a08deb4a797f175f6af8')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
