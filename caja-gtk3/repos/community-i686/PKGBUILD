# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=caja
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.0
pkgrel=3
pkgdesc="File manager for the MATE desktop (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'exempi' 'gtk3' 'gvfs' 'libexif'
         'libunique3' 'libxt' 'mate-desktop-gtk3>=1.10' 'mate-icon-theme'
         'startup-notification')
makedepends=('gobject-introspection' 'mate-common' 'pangox-compat')
optdepends=('gstreamer: automatic media playback when mouseover'
            'gvfs-smb: To connect to Samba/Windows shares')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz"
        "0001_expand_list_columns_in_file_management_preferences.diff"
        "0002_set_default_window_size_for_file_management_preferences.diff")
sha1sums=('a8d9c4a479a2fea7bd2eda707e0fd4025f85f229'
          '3ae81639efe4dbbda0b6a41c2e1872996c76705d'
          'c0c7d0e71c9fb4d5bc448f0d1ee1a0bb4aacbb86')
install=${_pkgbase}.install

prepare() {
    # UI fixes for GTK3.
    #  - https://github.com/mate-desktop/caja/pull/401/commits
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    patch -Np1 -i "${srcdir}/0001_expand_list_columns_in_file_management_preferences.diff"
    patch -Np1 -i "${srcdir}/0002_set_default_window_size_for_file_management_preferences.diff"
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=3.0 \
        --enable-unique \
        --enable-introspection \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
