# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: wain <wain.at.gmx.net>

pkgname=calcurse
pkgver=4.1.0
pkgrel=1
pkgdesc='A text-based personal organizer.'
arch=('i686' 'x86_64')
url='http://calcurse.org/'
license=('BSD')
depends=('ncurses')
optdepends=('python: for CalDAV support')
install='calcurse.install'
source=("http://calcurse.org/files/${pkgname}-${pkgver}.tar.gz"{,.asc})
md5sums=('baf0b0f736b6ee6609701ef50ea588ad'
         'SKIP')
validpgpkeys=(2E36D8620221482FC45CB7F2A91764759326B440) # Lukas Fleischer

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --enable-docs --without-asciidoc --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
