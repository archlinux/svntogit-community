# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=camlp4
_ocaml_ver=4.13.1
pkgver=${_ocaml_ver%.*}+1
pkgrel=2
pkgdesc='Caml preprocessor and pretty-printer'
arch=(x86_64)
license=(GPL2)
url='https://github.com/ocaml/camlp4/releases'
depends=("ocaml>=${_ocaml_ver}")
makedepends=(git ocamlbuild ocaml-compiler-libs)
options=(!makeflags !emptydirs staticlibs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/ocaml/camlp4/archive/$pkgver.tar.gz")
b2sums=('78b1366163d8f2cca577c787162b950806b61a242afe7e088829f546a3a9fdc35698f4e29ac64e1fb815735f7049f8a3ee866aa675660da5ed415d531bdfb6ab')

build() {
  cd "$pkgname-${pkgver/+/-}"
  CFLAGS+=' -ffat-lto-objects -w'
  ./configure
  make all camlp4/META
}

package() {
  cd "$pkgname-${pkgver/+/-}"
  make \
    BINDIR="$pkgdir/usr/bin" \
    LIBDIR="$pkgdir/usr/lib/ocaml" \
    PKGDIR="$pkgdir/usr/lib/ocaml" \
    install install-META
}
