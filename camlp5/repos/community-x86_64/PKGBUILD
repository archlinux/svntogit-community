# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>

pkgname=('camlp5' 'camlp5-transitional')
_baseversion=6.02.3
_patchlevel=1
pkgver=${_baseversion}.${_patchlevel}
pkgrel=2
arch=('i686' 'x86_64')
url='http://pauillac.inria.fr/~ddr/camlp5/'
license=('BSD')
depends=('sh')
makedepends=('ocaml')
options=('!makeflags')
source=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/camlp5-${_baseversion}.tgz")
# upstream likes to release occasional patches instead of releasing new versions
for i in $(seq ${_patchlevel}); do
  source+=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/patch-${_baseversion}-${i}")
done
md5sums=('a4f5e0dc13c43d50eb655c8e929f556e'
         'bd5afff0952b407bc63a7b1928e5d6d6')

build() {
  cd camlp5-${_baseversion}

  for i in $(seq ${_patchlevel}); do
    patch -Np0 -i ${srcdir}/patch-${_baseversion}-${i}
  done

  cd ${srcdir}

  cp -r camlp5-${_baseversion} camlp5-transitional-${_baseversion}

  cd ${srcdir}/camlp5-${_baseversion}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict

  make world.opt

  cd ${srcdir}/camlp5-transitional-${_baseversion}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -transitional

  make world.opt
}

package_camlp5() {
  pkgdesc='A preprocessor-pretty-printer of OCaml.'
  conflicts=('camlp5-transitional')

  cd camlp5-${_baseversion}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/camlp5/LICENSE
}

package_camlp5-transitional() {
  pkgdesc='A preprocessor-pretty-printer of OCaml. (transitional)'
  conflicts=('camlp5')

  cd camlp5-transitional-${_baseversion}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/camlp5-transitional/LICENSE
}
