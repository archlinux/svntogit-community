# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>

pkgname=('camlp5' 'camlp5-transitional')
pkgver=6.02.3
pkgrel=2
arch=('i686' 'x86_64')
url='http://pauillac.inria.fr/~ddr/camlp5/'
license=('BSD')
depends=('sh')
makedepends=('ocaml')
options=('!makeflags')
source=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/camlp5-${pkgver}.tgz")
md5sums=('a4f5e0dc13c43d50eb655c8e929f556e')

build() {
  cp -r camlp5-${pkgver} camlp5-transitional-${pkgver}

  cd ${srcdir}/camlp5-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict

  make world.opt

  cd ${srcdir}/camlp5-transitional-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -transitional

  make world.opt
}

package_camlp5() {
  pkgdesc='A preprocessor-pretty-printer of OCaml.'
  conflicts=('camlp5-transitional')

  cd camlp5-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/camlp5/LICENSE
}

package_camlp5-transitional() {
  pkgdesc='A preprocessor-pretty-printer of OCaml. (transitional)'
  conflicts=('camlp5')

  cd camlp5-transitional-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/camlp5-transitional/LICENSE
}
