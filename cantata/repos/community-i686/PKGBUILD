# $Id$
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=1.4.1
pkgrel=1
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=('i686' 'x86_64')
url='https://code.google.com/p/cantata/'
license=('GPL')
depends=('qt5-multimedia' 'qt5-svg' 'libmtp' 'libcddb' 'libmusicbrainz5' 'mpg123'
         'taglib-extras' 'hicolor-icon-theme' 'cdparanoia')
optdepends=('perl-uri: dynamic playlist'
            'mpd: playback')
makedepends=('cmake' 'qt5-tools' 'qt5-svg' 'subversion')
install=$pkgname.install
source=($pkgname-$pkgver::svn+http://cantata.googlecode.com/svn/release/$pkgver)
md5sums=('SKIP')

build() {
  cd $pkgname-$pkgver
  mkdir build

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_HTTP_STREAM_PLAYBACK=ON \
    -DENABLE_KDE=OFF -DENABLE_QT5=ON

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX="/usr" install
}
