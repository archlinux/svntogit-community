# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=2.2.0
pkgrel=4
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=(x86_64)
url='https://github.com/CDrummond/cantata'
license=(GPL)
depends=(libmtp libmusicbrainz5 mpg123 vlc taglib-extras media-player-info libcdio-paranoia udisks2)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback' 'ffmpeg: ReplayGain support' 'libebur128: ReplayGain support')
makedepends=(cmake qt5-tools ffmpeg libebur128)
source=("https://github.com/CDrummond/cantata/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"
        cantata-qt5.10-crash.patch::"https://github.com/CDrummond/cantata/commit/4da7a912.patch"
        cantata-ffmpeg4.patch::"https://github.com/CDrummond/cantata/commit/ca10b5ff.patch")
sha256sums=('c84691eb17711118d3c512d70cb422267f7a137290082cdd1ff0a871b4a8e5c0'
            '13f7fc999333b28bc5dc07d8f50c16479c87e606045ef018cf3cc7dac7460305'
            'f0305315306a65df51b7d0bb304439d02a38ea6ed4f99f65c0f08314fe366cd9')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../cantata-qt5.10-crash.patch # Fix crash with Qt 5.10 https://github.com/CDrummond/cantata/issues/1130
  patch -p1 -i ../cantata-ffmpeg4.patch # Fix build with ffmpeg 4
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_TOUCH_SUPPORT=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
