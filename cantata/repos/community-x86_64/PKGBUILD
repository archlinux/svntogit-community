# $Id$
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=1.2.2
pkgdesc="A KDE client for the music player daemon (MPD)"
pkgrel=1
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Cantata?content=147733"
license=('GPL')
depends=('kdebase-runtime' 'libmtp' 'libcddb' 'libmusicbrainz5' 'mpg123' 'qjson' 'taglib-extras')
optdepends=('perl-uri: dynamic playlist'
	    'mpd: playback')
makedepends=('cmake' 'automoc4')
install="$pkgname.install"
source=("http://cantata.googlecode.com/files/$pkgname-$pkgver.tar.bz2")
sha1sums=('89d84736ba4a43be7dc892af6fcbad5957075e19')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir build

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_HTTP_STREAM_PLAYBACK=ON

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir" PREFIX="/usr" install
}
