# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=2.2.4
pkgrel=1
pkgdesc='Captive Portal Assistant'
arch=(x86_64)
url=https://launchpad.net/capnet-assist
license=(GPL3)
groups=(pantheon)
depends=(
  gcr
  glib2
  gtk3
  libsoup
  networkmanager
  webkit2gtk
  libgranite.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
source=(git+https://github.com/elementary/capnet-assist.git#tag=47735371387a1ae9b36179b526c438b92d84c157)
sha256sums=(SKIP)

pkgver() {
  cd capnet-assist

  git describe --tags
}

build() {
  arch-meson capnet-assist build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
