# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=capnet-assist
pkgver=2.3.0
pkgrel=1
pkgdesc='Captive Portal Assistant'
arch=(x86_64)
url=https://github.com/elementary/capnet-assist
license=(GPL3)
groups=(pantheon)
depends=(
  gcr
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  libsoup
  networkmanager
  webkit2gtk
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=83721840ccb6dc1080cbac85ec7f87a051d198fc
source=(git+https://github.com/elementary/capnet-assist.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd capnet-assist
  git describe --tags
}

build() {
  arch-meson capnet-assist build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
