# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: M0Rf30

pkgbase=capstone
pkgname=('capstone' 'python-capstone' 'python2-capstone')
pkgver=3.0.3
pkgrel=2
pkgdesc='A lightweight multi-platform, multi-architecture disassembly framework'
url='http://www.capstone-engine.org/index.html'
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('glibc' 'python' 'python2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/aquynh/${pkgbase}/archive/${pkgver}.tar.gz)
sha512sums=('5847d89efe2d139d1dacff73547bfe682def6c23310da89dc8b84686e2a718dd361da242417a895bb0fc425401b9baf15348f1994da052a05c2f64a238659c54')

build() {
  cd ${pkgbase}-${pkgver}
  make
}

package_capstone() {
  depends=('glibc')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-capstone() {
  depends=('capstone' 'python')
  cd ${pkgbase}-${pkgver}/bindings/python
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 ../../LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-capstone() {
  depends=('capstone' 'python2')
  cd ${pkgbase}-${pkgver}/bindings/python
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 ../../LICENSE.TXT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
