# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-audit
pkgver=0.17.0
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
options=(!lto)
source=("https://github.com/RustSec/rustsec/archive/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('18fc40de40b95adc55e4c4e767f969d62f60bba23805ea9455dd12c1c19f01c0')
b2sums=('1dce9dcc94c75e1df9f8d955c4f86bb08905e18de6eb875f6a4380ed55190874e512b89aa507350ee2d87ca93b05848ec07524168ff34819ce21cb7016b7ff50')

prepare() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked
}

check() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked
}

package() {
  cd rustsec-${pkgname}-v${pkgver}/${pkgname}
  install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
