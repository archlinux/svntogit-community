# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.19.4
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('86afc682f2881d310f8a0ee222c13d27771473fa83ab77d032d0988124104b7b9489ff23be23f1737a2b648ce0e79f01292086612b2f2025dec627e4eba688c9')
b2sums=('f01a8a76db9b7f8960d5c825a8222e6d70d3eb09cd92e7365f869a98c3429a29a07668562666bedb3ce0d6d23ff613cfdec241ebe51e0bfd26cb44fd2cd7b704')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
