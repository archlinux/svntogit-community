# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-deny
pkgver=0.12.2
pkgrel=1
pkgdesc='Cargo plugin for linting your dependencies'
arch=('x86_64')
url='https://github.com/EmbarkStudios/cargo-deny'
license=('MIT' 'APACHE')
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/EmbarkStudios/cargo-deny/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('941a38202ccc14210314d42bf1d9ae4e07d964831e607c0f0e2a50809766ea5a43bcc748ba8100513e6768695a82c0eb04339c90c07a4283c53184afc0c46054')

prepare() {
  cd cargo-deny-${pkgver}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd cargo-deny-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --frozen
}

check() {
  cd cargo-deny-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen
}

package() {
  cd cargo-deny-${pkgver}
  install -Dm755 target/release/cargo-deny -t "${pkgdir}"/usr/bin
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm644 LICENSE-MIT -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
