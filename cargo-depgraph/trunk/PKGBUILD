# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Vlad Frolov <frolvlad@gmail.com>

pkgname=cargo-depgraph
pkgver=1.2.2
pkgrel=3
pkgdesc="Creates dependency graphs for cargo projects using cargo metadata and graphviz"
arch=('x86_64')
url="https://sr.ht/~jplatte/cargo-depgraph"
license=('GPL3')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~jplatte/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('7f56739ca920d3be998bfd2365fd0f5d468b9297352269ea043169ae87005349')

prepare() {
  cd "$pkgname-v$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-v$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
