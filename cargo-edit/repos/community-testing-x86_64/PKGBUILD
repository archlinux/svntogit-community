# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-edit
pkgver=0.8.0
pkgrel=2
pkgdesc='Managing cargo dependencies from the command line'
url='https://github.com/killercup/cargo-edit/releases'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('cargo' 'libgit2.so')
source=(https://github.com/killercup/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('6594869358da51353bbde155a54285f8a64567aeea0381ab1b16adf4ae0cc7f325c35ef2451dd8b452767b7833c251d276d014d5b1932bd36c18139ba1b58e68')
b2sums=('5864ade34a747bfd1bca44cfa030be863632400f329894d569e5178bc503f647a1d698a0caf61e19175b8077bc771931d37f171d2974f92325e6d3139202cb1f')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  # tests depend on target/debug/
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 \
    target/release/cargo-add \
    target/release/cargo-rm \
    target/release/cargo-upgrade \
    -t "${pkgdir}/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
