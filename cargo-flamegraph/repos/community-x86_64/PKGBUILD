# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-flamegraph
pkgver=0.5.1
pkgrel=1
pkgdesc='Easy flamegraphs for Rust projects and everything else'
url="https://github.com/flamegraph-rs/flamegraph"
depends=('perf')
makedepends=('cargo')
arch=('x86_64')
license=('MIT' 'Apache')
source=(https://github.com/flamegraph-rs/flamegraph/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('1a1e76cb89f56bac6913a7e88e90ecdc96edc4006faa11dce17fbce6a965aaf8d55fdd4d21730cc3870094fdff91e41f8296bdc1378ad57495226cf479a959c3')

build() {
  cd "flamegraph-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "flamegraph-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "flamegraph-${pkgver}"
  install -Dm755 -t "${pkgdir}/usr/bin" \
    target/release/cargo-flamegraph \
    target/release/flamegraph
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
