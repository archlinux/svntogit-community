# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-fuzz
pkgver=0.10.1
pkgrel=1
pkgdesc="Command line helpers for fuzzing rust"
url="https://github.com/rust-fuzz/cargo-fuzz"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-fuzz/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('83fdf39ddbd856b93710d79ef98d9ff7a6eaf8061ff510eaf0fee386ed7e51f26b7eba53c4d3f50250d9e502bef861adc6e73998c9d9f9112402495a03104a82')
b2sums=('a2dbb9a8f524ca9a7fe41df3782b126d79d2ff6ff58352983e70050d03a6a2cb6f24752b251b477fae5d7fff1a047180a385de294f5155db459b14c3ba9199e2')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  # this is broken on linux-hardened
  #RUSTC_BOOTSTRAP=1 cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
