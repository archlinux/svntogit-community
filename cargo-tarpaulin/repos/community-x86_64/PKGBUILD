# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=cargo-tarpaulin
pkgver=0.18.0
pkgrel=1
pkgdesc='Tool to analyse test coverage of cargo projects'
arch=(x86_64)
url=https://github.com/xd009642/tarpaulin
license=(Apache)
depends=(
  gcc-libs
  glibc
  libcurl.so
  openssl
  zlib
)
makedepends=(
  git
  rust
)
_tag=8d5cd1a03c63033f9ee24ad640fb1715b702b71f
source=(git+https://github.com/xd009642/tarpaulin.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd tarpaulin

  git describe --tags
}

prepare() {
  cargo fetch \
    --locked \
    --manifest-path tarpaulin/Cargo.toml
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path tarpaulin/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path tarpaulin \
    --root "${pkgdir}"/usr
}

# vim: ts=2 sw=2 et:
