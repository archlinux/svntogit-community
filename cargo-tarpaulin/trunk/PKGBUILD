# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=cargo-tarpaulin
pkgver=0.14.3
pkgrel=1
pkgdesc='Tool to analyse test coverage of cargo projects'
arch=(x86_64)
url=https://github.com/xd009642/tarpaulin
license=(Apache)
depends=(
  gcc-libs
  glibc
  libcurl.so
  openssl
  zlib
)
makedepends=(
  git
  rust
)
_tag=039a1c5286443a3703f0b489b435388cc932c7f6
source=(git+https://github.com/xd009642/tarpaulin.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd tarpaulin

  git describe --tags
}

prepare() {
  cargo fetch \
    --locked \
    --manifest-path tarpaulin/Cargo.toml
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path tarpaulin/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path tarpaulin \
    --root "${pkgdir}"/usr
}

# vim: ts=2 sw=2 et:
