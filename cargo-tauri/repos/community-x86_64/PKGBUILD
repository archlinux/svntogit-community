# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-tauri
_pkgname=tauri
pkgver=1.0.5
pkgrel=1
pkgdesc="Command line interface for building Tauri apps"
arch=('x86_64')
url="https://github.com/tauri-apps/tauri"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('7432808d963023429e9f4249c8987b0b6ffcb86c4100bed0e56359a72c35e28a025a52e0433b729192459a7511c69ac2f63ac3dd4ea2fc230f44a32bffee3fd7')
options=('!lto')

prepare() {
  cd "$_pkgname-$pkgver/tooling/cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver/tooling/cli"
  cargo build --release --frozen
}

check() {
  cd "$_pkgname-$pkgver/tooling/cli"
  cargo test --frozen
}

package() {
  cd "$_pkgname-$pkgver/tooling/cli"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE_MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
