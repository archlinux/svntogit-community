# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-tauri
_pkgname=tauri
pkgver=1.1.1
pkgrel=1
pkgdesc="Command line interface for building Tauri apps"
arch=('x86_64')
url="https://github.com/tauri-apps/tauri"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgname-v$pkgver.tar.gz")
sha512sums=('799241058eadf565ff5d55d19fb4f0d927d42c3aeeb4b958522d91279aa531a6b8feeab9b05a01c0f96f2aa69d890b8729bf9165252a4ea72561ad90975561c5')
options=('!lto')

prepare() {
  mv "$_pkgname-$_pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver/tooling/cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver/tooling/cli"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver/tooling/cli"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver/tooling/cli"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE_MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
