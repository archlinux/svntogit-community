# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=cargo-watch
pkgver=7.8.0
pkgrel=1
pkgdesc="Utility for Cargo to compile projects when sources change"
url='https://github.com/passcod/cargo-watch'
arch=('x86_64')
license=('CC0-1.0')
depends=('cargo')
makedepends=('ruby-ronn')
source=("https://github.com/passcod/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c1e757bfdccfedec0b08dc52b86aa011c81c9d0d3318e4841a0027291c166423146aa03aca049a2953f62feb7f6173962a986833ba7be289010bb2c9e2224135')
b2sums=('af3a367f7e45a6a969c72dcd66d0b985b75bae13a76c970dc8360b7a76eb8d2681269d48586f8c9271ced284219131f55eb2b41053cc2978e8f5489a996910ac')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
  ronn --roff cargo-watch.1.ronn
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm 644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_cargo-watch"
  install -Dm 644 cargo-watch.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
