# $Id$
# Maintainer: Johannes Löthberg <johannes@kyrisis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Christopher Reimer <mail+aur@c-reimer.de>

pkgname=cargo
pkgver=0.20.0
pkgrel=1

pkgdesc='Rust package manager'
url='http://crates.io/'
arch=('x86_64' 'i686')
license=('APACHE' 'MIT' 'custom')

depends=('curl' 'rust')
makedepends=('git' 'python' 'cmake' 'cargo')
optdepends=('gcc: for compiling C source code with gcc'
            'clang: for compiling C source code with clang')

options=('!emptydirs')

source=("git+https://github.com/rust-lang/cargo.git#commit=a60d185c878c470876e123b0e40b0ba9f3271163")

md5sums=('SKIP')

build() {
  cd cargo

  cargo build --release
}

package() {
  # cargo upstream deleted the old autotools scripts in favor of their own
  # distribution tarball creation tools in the rust repo, which aren't suitable
  # for our use right now, so we'll have to install things manually for now.

  cd cargo

  cargo install --root "$pkgdir"/usr

  rm "$pkgdir"/usr/.crates.toml

  install -Dm644 src/etc/_cargo \
    "$pkgdir"/usr/share/zsh/site-functions/_cargo
  install -Dm644 src/etc/cargo.bashcomp.sh \
    "$pkgdir"/usr/share/bash-completion/completions/cargo

  install -d "$pkgdir"/usr/share/man/man1/
  for f in src/etc/man/*; do
    cp "$f" "$pkgdir"/usr/share/man/man1/
  done

  for f in LICENSE-APACHE LICENSE-MIT LICENSE-THIRD-PARTY; do
    install -Dm644 "$f" "$pkgdir/usr/share/licenses/$pkgname/$f"
  done
}

# vim:set ts=2 sw=2 et:
