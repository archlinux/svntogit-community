# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

_name=Carla
pkgname=carla
pkgver=1.9.8
pkgrel=9
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('file' 'fftw' 'fluidsynth' 'liblo' 'linuxsampler' 'mxml' 'ntk'
'projectm' 'python-pyqt5' 'qt5-svg')
makedepends=('gtk2' 'gtk3' 'pulseaudio' 'python-pyliblo' 'python-pyqt5'
'python-rdflib' 'qt4' 'qt5-svg' 'zynaddsubfx')
optdepends=('gtk2: LV2 GTK2 UI support'
            'gtk3: LV2 GTK3 UI support'
            'python-pyliblo: OSC support'
            'python-rdflib: LADSPA-RDF support'
            'qt4: LV2 QT4 UI support'
            'zynaddsubfx: ZynAddSubFX banks'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('0f801fdf7704bb3eb62e684134567df3e83a6c7dfad201479039158480f9fbc0c9a91bc007131091c29a87b4cbb2d831797c8c517de9c36399ec7348a8889f5d')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make features
  make DEFAULT_QT=5
}

package() {
  cd "${pkgname}-${pkgver}"
  make DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
