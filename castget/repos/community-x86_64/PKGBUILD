# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=castget
pkgver=1.2.4
pkgrel=2
pkgdesc="A simple, command-line based RSS enclosure downloader"
arch=('x86_64')
url="http://johndal.com/castget/"
license=('GPL')
depends=('glib2' 'libxml2' 'curl' 'id3lib')
changelog=$pkgname.changelog
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('91ee310ced5386495eb56b09b3cb4b6660f9cea1a7050a94e9a30d7bc3ba692d'
            'SKIP')
validpgpkeys=('561BCBC3DF8A53503A62416EA7DE43E1D881FF60') # Marius Larsen JÃ¸hndal <mariuslj@ifi.uio.no>

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
