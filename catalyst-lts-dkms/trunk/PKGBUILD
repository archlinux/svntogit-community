# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vi0L0, wonder, Eduardo "kensai" Romero
# Contributor: aidanlinz, Rip-Rip, OvsInc, Sebastian Siebert

pkgname=catalyst-dkms
pkgver=12.8
pkgrel=0.1
pkgdesc="AMD proprietary gpu kernel driver"
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
depends=('linux>=3.5' 'linux-headers>=3.5' 'dkms')
source=(http://www2.ati.com/drivers/linux/amd-driver-installer-${pkgver/./-}-x86.x86_64.zip
        dkms.conf)
md5sums=('41c5478322b13be6909eeb46412a3aa0'
         '972049dc247a74a7162f2227078abcb0')
install=catalyst.install

package() {
  depends=(${depends[@]} "catalyst-utils=${pkgver}")

  cd ${srcdir}
  
  if [ "${CARCH}" = "x86_64" ]; then
    _archdir=x86_64
  else
    _archdir=x86
  fi

  sh ./amd-driver-installer*.run --extract fglrx-install

  cd fglrx-install

  install -dm755 "${pkgdir}/usr/lib/modprobe.d"
  install -dm755 "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}"
  cp -r common/lib/modules/fglrx/build_mod/* "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  cp "arch/${_archdir}"/lib/modules/fglrx/build_mod/libfglrx_ip.a "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  cp ${srcdir}/dkms.conf "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  sed -i -e "s/@VERSION@/${pkgver}-${pkgrel}/" "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/dkms.conf"
  
  echo "blacklist radeon" >> "${pkgdir}/usr/lib/modprobe.d/catalyst-lts.conf"
  
  # license
  install -Dm644 "${srcdir}/fglrx-install/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
