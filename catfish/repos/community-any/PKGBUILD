# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=catfish
pkgver=1.0.2
pkgrel=1
pkgdesc='Versatile file searching tool'
arch=('any')
url='https://launchpad.net/catfish-search'
license=('GPL')
install=$pkgname.install 
depends=('gtk3' 'python2-gobject' 'python2-xdg' 'xdg-utils' 'python2-pexpect')
makedepends=('intltool')
optdepends=('zeitgeist: Zeitgeist integration'
            'findutils: filename search'
            'mlocate:    filename search'
            'tracker:   fulltext search'
            'strigi:    fulltext search'
            'pinot:     fulltext search')
source=(https://launchpad.net/catfish-search/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('7ea70b46ec98a4d1fc2a06bf2579acb2')

prepare() {
  cd $pkgname-$pkgver
  sed -i '1s/python/python2/' {bin,catfish,catfish_lib}/*.py build.py
  sed -i 's|ln -sf $(PREFIX)/share|ln -sf ..|' Makefile.in.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --python=python2
  make
}

package () {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/pixmaps
  ln -s ../icons/hicolor/scalable/apps/$pkgname.svg \
    "$pkgdir"/usr/share/pixmaps/$pkgname.svg
}
