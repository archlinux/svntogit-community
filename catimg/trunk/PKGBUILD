# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: sqlwwx <wwx_2012@live.com>
# Contributors: Eduardo San Martin Morote http://posva.net/

pkgname=catimg
pkgver=2.4.0
pkgrel=1
pkgdesc="Print images in a terminal with 256 colors support"
arch=('x86_64')
url="https://github.com/posva/catimg"
license=('MIT')
depends=('glibc')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/posva/catimg/archive/v$pkgver.tar.gz")
sha512sums=('fae0ff8436ce62cdf90c59aca603ff1850e2ccce8cb87420214fe62c0295f59d6af6191270010a3a437f9aa24542849bff3829e4d6e5e9c8c7bf7023282d462b')

build() {
	cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
	make
}

package() {
	cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
