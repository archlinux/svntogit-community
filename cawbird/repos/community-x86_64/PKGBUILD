# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=cawbird
pkgver=1.4.1
pkgrel=1
pkgdesc='Native GTK Twitter client'
arch=('x86_64')
url='https://ibboard.co.uk/cawbird/'
license=('GPL3')
depends=('gspell' 'gst-libav' 'gst-plugin-gtk' 'gst-plugins-bad' 'gst-plugins-good' 'liboauth')
makedepends=('meson' 'vala')
source=("https://github.com/IBBoard/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c527a07d447d189852582bbaff5e6d0d1e432b4b643e8a1703656bab25d31582')

build() {
  arch-meson $pkgname-$pkgver build \
    -D consumer_key_base64=YUVNOE14dE5xMVdHRzl2emo5SzNNSlNUeg== \
    -D consumer_secret_base64=c0J0cmc2clNJTFN6bHlSbFNXc2J1MmdvWWk3b0FzTGZHOGI4OG5QRzB3Tmx1Y0ZtOHQ=
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
