# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=ccgo
pkgver=0.3.6.4
pkgrel=4
pkgdesc='Play the game of Go with gnugo or against other players on IGS'
arch=('x86_64' 'i686')
url='http://ccdw.org/~cjj/prog/ccgo/'
license=('GPL')
depends=('gtkmm' 'gconfmm')
source=("http://ccdw.org/~cjj/prog/$pkgname/src/$pkgname-$pkgver.tar.gz")
sha256sums=('1975a4e9b6661f6613dd3a00b3ee7e176b80244f555a184b258b50ef4352a890')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  find -name '*.hh' -exec sed -i '/sigc++\/object.h/d' {} +
  find -name '*.cc' -exec sed -i 's/(bind(/(sigc::bind(/' {} +
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  CXXFLAGS+=' -std=c++11 -fpermissive'
  ./configure --prefix=/usr
  make
}

package() {
  make -C "$srcdir/$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
