# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Fangrui Song <i at maskray.me>

pkgname=ccls
pkgver=0.20210330
pkgrel=7
pkgdesc='C/C++/ObjC language server supporting cross references, hierarchies, completion and semantic highlighting'
arch=('x86_64')
url='https://github.com/MaskRay/ccls'
license=('Apache')
depends=('clang=14.0.6' 'llvm-libs' 'rapidjson')
makedepends=("cmake" "llvm")
conflicts=('ccls-git')
source=("https://github.com/MaskRay/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-llvm14-part1.patch::https://github.com/MaskRay/ccls/commit/3ce756e39ae4.patch
        $pkgname-llvm14-part2.patch::https://github.com/MaskRay/ccls/commit/8422f0a522b6.patch
        $pkgname-llvm14-part3.patch::https://github.com/MaskRay/ccls/commit/790daca4b2d9.patch
        $pkgname-add-language-c.patch::https://github.com/MaskRay/ccls/commit/dce86b1362ac.patch)
sha512sums=('dd78c040c2a51d6b47f8dd2d8bdc0661111f26b9233ffeb41216254b1b0ad0f634190784e6b8c2d7b36475c13020356342d83a9f80047d7da75ce21ca8885b16'
            '4303fe47aa2f30ded45a653a3564940d8d80837e90e722dd19216b3326465416074f744fe6e971ede737bad21dc36c6f9e2c600177c16219607934686c59ba62'
            'e40f354fab4cf0c5e153106f95fad58c4181cf172eceea0976466b10ed2d7e0f793c911f56a9cec587b9236628f7306af539613059b15d46c8378752bbae75bc'
            'd584872f2c344c497520fb39c9adfe880bdc8c48ea4c8ae19f800eec235f2ae346cab5f53ee14dc32f6b70e2c690203cace5e13beb88e09e5cd67efa28271802'
            '91151eb63b44c573fdd5de698c3aadf5f72ef72df07da447ca2d8fa6a871c0cca8be6bc1c163fc92ba37e449a2412dd4737037c3c433552141d1a529115d00e6')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../$pkgname-llvm14-part1.patch
  patch -Np1 -i ../$pkgname-llvm14-part2.patch
  patch -Np1 -i ../$pkgname-llvm14-part3.patch
  patch -Np1 -i ../$pkgname-add-language-c.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -H. -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_COMPILER=clang++ -DCLANG_LINK_CLANG_DYLIB=1
  cmake --build build
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
