# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org> 

pkgname=ccze
pkgver=0.2.1
pkgrel=9
pkgdesc="Robust and modular log colorizer with many plugins"
arch=('x86_64')
url="http://freshmeat.net/projects/ccze/"
license=('GPL')
depends=('ncurses' 'pcre')
makedepends=('patch')
source=(ftp://ftp.debian.org/debian/pool/main/c/$pkgname/${pkgname}_${pkgver}.orig.tar.gz \
        $pkgname-fix-segfault.diff)
sha256sums=('8263a11183fd356a033b6572958d5a6bb56bfd2dba801ed0bff276cfae528aa3'
            '730e63e312178cf9777970bff0fa8d3030ee40deb1ebdf753c5053824f9d011c')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  patch -Np0 -i "${srcdir}"/$pkgname-fix-segfault.diff

  #killing a bug
  sed -e 's/-Wswitch -Wmulticharacter/-Wswitch/' -i src/Makefile.in
  sed -e '/#undef error_t/d' -i system.h.in

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
