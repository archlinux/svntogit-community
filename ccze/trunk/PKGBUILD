# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org> 

pkgname=ccze
pkgver=0.2.1
pkgrel=6
pkgdesc='Robust and modular log colorizer with many plugins'
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/ccze/"
license=('GPL')
depends=('ncurses' 'pcre')
makedepends=('patch')
source=(ftp://ftp.debian.org/debian/pool/main/c/$pkgname/${pkgname}_${pkgver}.orig.tar.gz \
        $pkgname-fix-segfault.diff)
md5sums=('221966bce7c5f011eca38157241a0432'
         '294e82702d179264abc43596da955358')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np0 -i ${srcdir}/$pkgname-fix-segfault.diff

  #killing a bug
  sed -e 's/-Wswitch -Wmulticharacter/-Wswitch/' -i src/Makefile.in
  sed -e '/#undef error_t/d' -i system.h.in

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
}
