# $Id$
# Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor dorphell <dorphell@archlinux.org>
# Contributor Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=cdcd
pkgver=0.6.6
pkgrel=8
pkgdesc="A commandline cd-player"
arch=('i686' 'x86_64')
depends=('libcdaudio' 'readline')
url="http://libcdaudio.sourceforge.net/"
license=('GPL2')
source=("http://downloads.sourceforge.net/libcdaudio/$pkgname-$pkgver.tar.gz")
md5sums=('dc17f2e275c7214cb693f6abb50f29fb')

if [[ "$CARCH" == "x86_64" ]]; then
  source+=("cdcd-0.6.6-$CARCH.patch")
  md5sums+=('7c39f01408fc084e2c62da4da324370e')
fi

build() {
  cd "$srcdir/$pkgname-$pkgver" 

  if [[ "$CARCH" == "x86_64" ]]; then
    patch -Np1 -i "$srcdir/cdcd-0.6.6-$CARCH.patch"
  fi


  sed -i 's/(CPPFunction \*)/(rl_completion_func_t \*)/' cmd_cdcd.c

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  mv "$pkgdir/usr/info" "$pkgdir/usr/share/"
  mv "$pkgdir/usr/man/" "$pkgdir/usr/share/"
  rm "$pkgdir/usr/share/dir"

  install -dm755 "$pkgdir/usr/bin" "$pkgdir/usr/share"
}
