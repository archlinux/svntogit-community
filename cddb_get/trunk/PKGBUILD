# $Id$
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor  Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=cddb_get
_realname=CDDB_get
pkgver=2.27
pkgrel=6
pkgdesc="Fetches CDDB info for an audio cd."
arch=('any')
url="http://armin.emx.at/cddb/"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
options=('!emptydirs')
source=("http://armin.emx.at/cddb/$_realname-$pkgver.tar.gz")
md5sums=('405a3704ad5db45f117cc7cc5bd1ce7c')
sha1sums=('cec39851678bbb1fdd76f2cbd170628baf4971e6')

build() {
  cd "$srcdir/$_realname-$pkgver"
  
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor INSTALLVENDORSCRIPT=/usr/bin
  
  make 
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  make DESTDIR="$pkgdir" install
}
