# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=cddlib
pkgver=094h
pkgrel=2
pkgdesc="C library implementing Doudble Description Method for convex polyhedra"
arch=(x86_64)
url="https://www.inf.ethz.ch/personal/fukudak/cdd_home/"
license=(GPL)
depends=(gmp)
source=("ftp://ftp.math.ethz.ch/users/fukudak/cdd/cddlib-$pkgver.tar.gz" cdd_both_reps.c cdd_both_reps.patch)
sha256sums=('fe6d04d494683cd451be5f6fe785e147f24e8ce3ef7387f048e739ceb4565ab5'
            'f7cba3957d5d8198d07de5b64f40936da9914bc0a8040eca7ef01d66cd2d2377'
            'ec750d791829f3124787e2d4a376ff8316e179600505fee5513ca0e3320535d2')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/cdd_both_reps.patch
  ln -s "$srcdir"/cdd_both_reps.c src
  ln -s "$srcdir"/cdd_both_reps.c src-gmp

# Remove debug
  sed -e 's|localdebug=dd_TRUE|localdebug=dd_FALSE|g' \
    -i lib-src/cddlp.c -i lib-src-gmp/cddlp.c -i lib-src-gmp/cddlp_f.c
}

build() {
  cd $pkgname-$pkgver

  autoreconf -ifs
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
