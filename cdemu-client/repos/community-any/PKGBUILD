# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-client
pkgver=3.1.0
pkgrel=1
pkgdesc="Simple command-line client for controlling cdemu-daemon"
arch=(any)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(python-gobject cdemu-daemon)
makedepends=(intltool cmake)
conflicts=('cdemu')
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        fixes.diff)
sha256sums=('336a078b0b1f70de81d7ee92ee3e3a1e1957843accc4e90a876aeef28648d868'
            '908f919e779832eb05013cb0e3ecb49795afddaa4aa8cb946ec78c3218acf534')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np2 -i ../fixes.diff
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
