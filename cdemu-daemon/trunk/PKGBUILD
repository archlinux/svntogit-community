# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-daemon
pkgver=3.2.4
pkgrel=2
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=(x86_64)
url="https://cdemu.sourceforge.io/"
license=(GPL)
depends=(dbus VHBA-MODULE libao 'libmirage>=3.2.0')
makedepends=(cmake intltool)
optdepends=('alsa-lib: to enable the ALSA audio driver'
            'pulseaudio: to enable the PA audio driver')
backup=(etc/conf.d/cdemu-daemon)
install=cdemu-daemon.install
source=("https://downloads.sourceforge.net/cdemu/$pkgname/$pkgname-$pkgver.tar.bz2"
        0001-Patch-system-daemon-for-Arch-friendliness.patch)
sha256sums=('a6b32d4b78631e5f449f05f8d26b7f2f46dd3e82387f5855ccabb4933592de0b'
            '233dc79359e53da3a0df880984b5ea6033756070cfe84183f39c602274f4b2d6')

prepare() {
  cd $pkgname-$pkgver

  # It's disabled by default. Should we even ship it?
  patch -Np2 -i ../0001-Patch-system-daemon-for-Arch-friendliness.patch
}

build() {
  CFLAGS+=" -fcommon"
  cmake -S $pkgname-$pkgver -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/$pkgname
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  echo vhba | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/cdemu.conf"
}

# vim:set ts=2 sw=2 et:
