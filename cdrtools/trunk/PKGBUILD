# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=cdrtools
pkgver=3.01a13
pkgrel=1
pkgdesc="Original cdrtools supporting CD, DVD and BluRay burning"
arch=('i686' 'x86_64')
url="http://cdrecord.berlios.de/private/cdrecord.html"
license=('CDDL')
depends=('acl')
conflicts=('cdrkit')
provides=('cdrkit')
options=(!makeflags)
backup=('etc/default/cdrecord' 'etc/default/rscsi')
install=cdrtools.install
source=(http://ftp.berlios.de/pub/cdrecord/alpha/cdrtools-$pkgver.tar.bz2)
md5sums=('cc7af3bb94494a2900f44f0d83893921')

build() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}
  sed -i 's|/opt/schily|/usr|g' DEFAULTS/Defaults.linux
  sed -i 's|DEFINSGRP=.*|DEFINSGRP=root|' DEFAULTS/Defaults.linux
  
  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/
}

package() {
  cd "$srcdir"/cdrtools-${pkgver%%a*}

  export GMAKE_NOWARN=true
  make INS_BASE=/usr INS_RBASE=/ DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/include
  rm -rf "$pkgdir"/usr/lib/{*.a,profiled}

  # autoload 'sg' module needed by cdrecord
  install -d -m755 "$pkgdir"/usr/lib/modules-load.d/
  echo sg > "$pkgdir"/usr/lib/modules-load.d/cdrecord.conf

  # create symlinks for cdrkit compatibility
  cd "$pkgdir"/usr/bin
  ln -s cdrecord wodim
  ln -s readcd readom
  ln -s mkisofs genisoimage
  ln -s cdda2wav icedax
}

# vim:set ts=2 sw=2 et:
