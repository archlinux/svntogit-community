# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org> 
# Contributor: William Rea <sillywilly@gmail.com>, 
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>

pkgname=cegui
pkgver=0.7.6
pkgrel=4
pkgdesc="A free library providing windowing and widgets for graphics APIs/engines"
arch=('i686' 'x86_64')
url="http://crayzedsgui.sourceforge.net"
#options=('!libtool')
license=("MIT")
depends=('pcre' 'glew' 'expat' 'freetype2' 'libxml2' 'devil' 'freeglut' 'lua' 'silly')
makedepends=('python2' 'doxygen' 'ogre' 'irrlicht' 'gtk2' 'boost' 'graphviz')
optdepends=("python2: python bindings"
            "ogre: ogre module"
            "irrlicht: irrlicht module")
options=(!libtool)
source=(http://downloads.sourceforge.net/crayzedsgui/CEGUI-$pkgver.tar.gz
        cegui-0.7.6-python-detection.patch)
md5sums=('7ddb5145dc94fb7daf9aea1d30a6ffa3'
         '4600775f78b7349621a8a314ade8c902')

build() {
  cd $srcdir/CEGUI-${pkgver}

  patch -Np0 < $srcdir/cegui-0.7.6-python-detection.patch

  ./bootstrap

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-null-renderer \
    --with-gtk2

  make
}

package() {
  cd $srcdir/CEGUI-${pkgver}
 
  make DESTDIR=${pkgdir} install

  cd doc/doxygen && doxygen
  cd .. && make DESTDIR=${pkgdir} install-html

  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

