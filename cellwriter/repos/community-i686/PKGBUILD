# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: Alexander Markov <apsheronets@gmail.com>
# Contributor: Winston Weinert

pkgname=cellwriter
pkgver=1.3.5
pkgrel=3
pkgdesc='A grid-entry natural handwriting input panel.'
arch=('i686' 'x86_64')
license=('GPL')
url='http://v1.risujin.org/cellwriter/'
depends=('gtk2' 'libxtst' 'libgnome' 'hicolor-icon-theme')
source=("http://v1.risujin.org/pub/cellwriter/${pkgname}-${pkgver}.tar.gz"
        "http://launchpadlibrarian.net/18959127/${pkgname}-1.3.4-cellwidget-dont-disable-xinput.diff")
md5sums=('16db87415c3c669e0489f9c4d795ec7d'
         '5e8c61309c5ec8f67f0af7bb5ceb1aea')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"

  patch -p0 < "${srcdir}/${pkgname}-1.3.4-cellwidget-dont-disable-xinput.diff"

  cd "${srcdir}/${pkgname}-${pkgver}"

  env LDFLAGS='-lX11' ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
