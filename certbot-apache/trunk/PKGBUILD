# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-apache
pkgver=1.4.0
pkgrel=1
pkgdesc="Apache plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
replaces=("letsencrypt-apache")
depends=('certbot' 'python-acme' 'python-augeas' 'python-mock' 'python-setuptools'
         'python-zope-component' 'python-zope-interface')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('5d9307a7e56636ec2aa2110a47967480bf4459026644194fba937522c9994dd3b59af5ec923a503fb20a2ed06424f52bb8d5e8f3229b2f73f2a4cd9a1e191e2e'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  # https://github.com/certbot/certbot/issues/5510
  cd $pkgname-$pkgver
  python setup.py test
}

package() {
  cd $pkgname-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
