# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-dnsimple
pkgver=0.20.0
pkgrel=1
pkgdesc="DNSimple DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" 'dns-lexicon' "python-acme=$pkgver" 'python-mock' 'python-setuptools'
         'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('d9294b2ab60338ab8c8514a187641ec3279072b98c630254e19ad24f730ff0bac5096766fd5422f2347ea5a4552b8db229da2ad5a1a3bc8200c4f3f47d43503a'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
