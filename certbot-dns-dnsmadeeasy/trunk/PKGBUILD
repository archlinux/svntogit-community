# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-dnsmadeeasy
pkgver=0.26.0
pkgrel=1
pkgdesc="DNS Made Easy DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" 'dns-lexicon' "python-acme=$pkgver" 'python-mock' 'python-setuptools'
         'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('1c8b01091111fb3c527cbaf93503f66bf0ef0e0f5cebb9b13a0d1a2fc77dfd07d616417172f5a93aeee7cfe34c177092654eb27c210e0ee07610b0ef03fd2ee9'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
