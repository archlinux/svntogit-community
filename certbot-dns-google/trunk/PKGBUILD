# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-google
pkgver=0.32.0
pkgrel=1
pkgdesc="Google Cloud DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-mock' 'python-google-api-python-client'
         'python-oauth2client' 'python-setuptools' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('994127f395b6e1cd5419146845ac90d9634dfa24d1e9fe11d10a6d93dfcfe5ff1ba2e9e28f285254e27c81f8c15bd71e246755203a14f5659cffdada9656ee6a'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
