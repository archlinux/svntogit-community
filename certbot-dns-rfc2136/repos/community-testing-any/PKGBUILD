# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-rfc2136
pkgver=2.3.0
pkgrel=2
pkgdesc="RFC2136 DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=(
  "certbot=$pkgver"
  "python-acme=$pkgver"
  'python-dnspython'
)
makedepends=(
  'python-build'
  'python-wheel'
  'python-installer'
)
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2'
              '86379B4F0AF371B50CD9E5FF3402831161D1D280'
              '20F201346BF8F3F455A73F9A780CC99432A28621'
              'F2871B4152AE13C49519111F447BF683AA3B26C3')
sha512sums=('4a305e398ca16e3f4c4bd45197a4825a0b1d8d139c2ffe380e38be9bd576bde477e31c19d7998319051c1d471fea865b7eddf9f8940238f08784df44be056195'
            'SKIP')
b2sums=('d5e2c0ee27f0b9b29beebb194c78f7014b640c66c5b87427d88e91e5b83f20bed87415039ae8e013a9c2a64e4f4a9c33e8d1f01d855dac92702072bde7c8fd38'
        'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  # nuke setuptools from orbit ^W install_requires
  sed \
    -e '/setuptools>=/d' \
    -i setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"

  python -m pytest
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
