# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=cfacter
pkgver=0.4.1
pkgrel=1
pkgdesc='Implementation of facter functionality in C++11'
arch=('i686' 'x86_64')
url='https://github.com/puppetlabs/cfacter'
license=('APACHE')
depends=('boost-libs' 'curl' 'openssl' 'yaml-cpp' 'ruby')
makedepends=('boost' 'cmake' 'git')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
        git://github.com/puppetlabs/leatherman#commit=fb07598
        cfacter-0.4.1-boost-1.58.0.patch)
md5sums=('668f89143fd5e1767973f30bb1cee10a'
         'SKIP'
         'efd20861fbc289f32c677d308279c8a2')

prepare() {
  cd $pkgname-$pkgver
  rmdir vendor/leatherman
  ln -s "$srcdir"/leatherman vendor/leatherman

  patch -p1 -i "$srcdir"/cfacter-0.4.1-boost-1.58.0.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
