# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=cfacter
pkgver=0.4.0
pkgrel=1
pkgdesc='Implementation of facter functionality in C++11'
arch=('i686' 'x86_64')
url='https://github.com/puppetlabs/cfacter'
license=('APACHE')
depends=('boost-libs' 'curl' 'openssl' 'yaml-cpp' 'ruby')
makedepends=('boost' 'cmake' 'git')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
        git://github.com/puppetlabs/leatherman#commit=fb07598
)
md5sums=('d3f0de09384b41d5724b16d3c8b31f80'
         'SKIP')

prepare() {
  rmdir $pkgname-$pkgver/vendor/leatherman
  ln -s "$srcdir"/leatherman $pkgname-$pkgver/vendor/leatherman
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
