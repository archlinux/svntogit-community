# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dmitriy Morozov <foxcub>

pkgname=cgal
pkgver=4.7
pkgrel=2
pkgdesc="Computational Geometry Algorithms Library"
arch=('i686' 'x86_64')
url="http://www.cgal.org"
license=('GPL' 'LGPL')
source=(https://github.com/CGAL/cgal/releases/download/releases%2FCGAL-4.7/CGAL-$pkgver.tar.xz)
depends=('mpfr' 'boost-libs' 'gmp' 'mesa' 'glu')
optdepends=('qt5-svg: for CGAL_Qt5'
            'eigen: for some packages, see the CGAL manual')
makedepends=('cmake' 'qt5-svg' 'eigen' 'boost')
md5sums=('623d91fb2ab0a35049dc6098a0f235cc')

# consider building with swig for python stuff
# https://gforge.inria.fr/frs/?group_id=52  for _pkgid

build() {
  cd "$srcdir/CGAL-$pkgver"
  mkdir build
  cd build
  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/CGAL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  # The tarball still has all these licenses included
  for _license in LICENSE{,.FREE_USE,.GPL,.LGPL}; do
    install -D -m644 "$srcdir/CGAL-$pkgver/$_license" "$pkgdir/usr/share/licenses/$pkgname/$_license"
  done
}

