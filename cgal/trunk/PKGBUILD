# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dmitriy Morozov <foxcub>

pkgname=cgal
pkgver=4.5.1
_dumbver=4.5
pkgrel=1
_pkgid=34139
pkgdesc="Computational Geometry Algorithms Library"
arch=('i686' 'x86_64')
url="http://www.cgal.org"
license=('GPL' 'LGPL')
source=(http://gforge.inria.fr/frs/download.php/$_pkgid/CGAL-$pkgver.tar.xz)
depends=('mpfr' 'boost-libs' 'gmp' 'mesa' 'glu')
optdepends=('qt4: for CGAL_Qt4'
            'eigen: for some packages, see the CGAL manual')
makedepends=('cmake' 'qt4' 'eigen' 'boost')
md5sums=('1e79eff0897149c4f0ec85d912cfa82c')

# consider building with swig for python stuff

build() {
  cd "$srcdir/CGAL-$_dumbver"
  mkdir build
  cd build
  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/CGAL-$_dumbver/build"
  make install DESTDIR="$pkgdir"

  # The tarball still has all these licenses included
  for _license in LICENSE{,.FREE_USE,.GPL,.LGPL}; do
    install -D -m644 "$srcdir/CGAL-$_dumbver/$_license" "$pkgdir/usr/share/licenses/$pkgname/$_license"
  done
}

