# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Current Maintainer:  Patrick Palka <patrick@parcs.ath.cx>
# Previous Maintainer:  Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>
# Development: http://closure.ath.cx/aur-dev

pkgname=cgit
pkgver=0.9.2
_gitver=1.8.3
pkgrel=1
pkgdesc="A web interface for git written in plain C"
arch=('i686' 'x86_64')
url="http://hjemli.net/git/cgit"
license=('GPL')
depends=('openssl')
makedepends=('zlib' 'curl' 'asciidoc')
install=cgit.install
source=("http://git.zx2c4.com/cgit/snapshot/cgit-$pkgver.tar.xz"
	"git-${_gitver}.tar.gz::https://github.com/git/git/archive/v${_gitver}.tar.gz"
	"apache.example.conf")
md5sums=('fe11018eff8d79caad112f4fac64b90f'
         '73224d3dc86c4999cc49484b49c9acc4'
         'bd5696bea0654bfcf98f249b1a36737d')

build() {
  cd "$pkgname-$pkgver"
  _cgit_gitver=`grep 'GIT_VER = ' Makefile | cut -f3 -d\ `
  [ "x${_gitver}" == "x${_cgit_gitver}" ] || { msg "Bad git version"; return 1; }
  rm -rf git/*
  mv ../git-${_gitver}/* git/
  make
  make doc-man
}

package() {
  cd "$pkgname-$pkgver"
  make CGIT_SCRIPT_PATH=/usr/share/webapps/cgit DESTDIR="$pkgdir" install install-man
  install -vDm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -vd "$pkgdir/var/cache/$pkgname"
  install -vDm0644 $srcdir/apache.example.conf $pkgdir/etc/webapps/cgit/apache.example.conf
  mkdir -p $pkgdir/usr/lib/cgit
  mv $pkgdir/usr/share/webapps/cgit/cgit.cgi $pkgdir/usr/lib/cgit
  ln -sf ../../../lib/cgit/cgit.cgi $pkgdir/usr/share/webapps/cgit/cgit.cgi
}
