# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>
# Contributor: Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>

pkgname=cgit
pkgver=1.2.1
pkgrel=1
pkgdesc="A web interface for git written in plain C"
arch=('x86_64')
url="https://git.zx2c4.com/cgit/"
license=('GPL2')
depends=('openssl' 'luajit')
makedepends=('zlib' 'curl' 'asciidoc')
optdepends=('groff: about page using man page syntax'
	    'python-pygments: syntax highlighting support'
	    'python-docutils: about page formatted with reStructuredText'
	    'python-markdown: about page formatted with markdown'
	    'mime-types: serve file with correct content-type header')
install=cgit.install
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE')
source=("https://git.zx2c4.com/cgit/snapshot/cgit-$pkgver.tar."{xz,asc}
	"apache.example.conf")
sha256sums=('3c547c146340fb16d4134326e7524bfb28ffa681284f1e3914bde1c27a9182bf'
            'SKIP'
            '89927d462c0504863c163eb8a210e5d65db30ee6e4300ff6a2f92460e4f20a62')

prepare() {
  cd "$pkgname-$pkgver"
  make get-git
}

build() {
  cd "$pkgname-$pkgver"
  make
  make doc-man
}

package() {
  cd "$pkgname-$pkgver"

  make CGIT_SCRIPT_PATH=/usr/share/webapps/cgit DESTDIR="$pkgdir" prefix=/usr install install-man

  install -vd "$pkgdir/var/cache/$pkgname"
  install -vDm0644 "$srcdir/apache.example.conf" "$pkgdir/etc/webapps/cgit/apache.example.conf"
  mkdir -p "$pkgdir/usr/lib/cgit"
  mv "$pkgdir/usr/share/webapps/cgit/cgit.cgi" "$pkgdir/usr/lib/cgit"
  ln -sf ../../../lib/cgit/cgit.cgi "$pkgdir/usr/share/webapps/cgit/cgit.cgi"
}
