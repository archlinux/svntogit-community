# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cgrep
pkgver=6.6.3
pkgrel=8
pkgdesc="A context-aware grep for source codes"
url="http://awgn.github.io/cgrep/"
license=("GPL2")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'pcre')
makedepends=("ghc=7.10.3" "haskell-ansi-terminal" "haskell-async" "haskell-cmdargs"
             "haskell-dlist" "haskell-either" "haskell-mtl" "haskell-regex-base"
             "haskell-regex-pcre" "haskell-regex-posix" "haskell-safe" "haskell-split"
             "haskell-stm" "haskell-stringsearch" "haskell-unix-compat"
             "haskell-unordered-containers")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('cb603a7127c922a63a0c91f64f4d35782860a96cb1da66d5d9ae5fa8f91a634f')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
}
