# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cgrep
pkgver=6.6.17
pkgrel=25
pkgdesc="A context-aware grep for source codes"
url="http://awgn.github.io/cgrep/"
license=("GPL2")
arch=('x86_64')
depends=('ghc-libs' "haskell-ansi-terminal" "haskell-async" "haskell-cmdargs" "haskell-dlist"
         "haskell-either" "haskell-mtl" "haskell-regex-base" "haskell-regex-pcre"
         "haskell-regex-posix" "haskell-safe" "haskell-split" "haskell-stm" "haskell-stringsearch"
         "haskell-unix-compat" "haskell-unicode-show" "haskell-unordered-containers"
         "haskell-utf8-string")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('382649e70a112614ab270fb287cf289572411b33f3d89f72b855bd0aa515ee17d7589f019446b3cdd5024b4cbde4272c955520abe0bcf7f92890f575a937635d')

prepare() {
    sed -e 's/Either/Except/' \
        -e 's/left/throwError/' \
        -e '/import Data.List/a import Control.Monad.Error.Class (throwError)' \
        -i $pkgname-$pkgver/src/Main.hs
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
}
