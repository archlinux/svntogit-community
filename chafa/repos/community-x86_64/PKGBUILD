# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.10.2
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('imagemagick' 'libxslt')
makedepends=('gtk-doc')
license=("LGPL")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.asc})
sha512sums=('eaa0a13b1915774f25ed9fe6d7f465e695644c79e96387b71671c43876dad5f703383a876ad4ea2b5a13b2391056224cfe38cecba31264698da37af80ad540b9'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
