# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Link Dupont <link@subpop.net>

pkgname=cherokee
pkgver=1.0.12
pkgrel=1
pkgdesc="A very fast, flexible and easy to configure Web Server"
arch=('i686' 'x86_64')
url="http://www.cherokee-project.com/"
license=('GPL2')
depends=('openssl' 'pcre')
makedepends=('python2' 'gettext' 'libldap' 'pam' 'libmysqlclient'
             'ffmpeg' 'geoip')
optdepends=('python2: cherokee-admin (administrative web interface)'
            'libldap: ldap validator'
            'pam: pam validator'
            'libmysqlclient: mysql validator'
            'ffmpeg: Audio/Video streaming handler'
            'geoip: GeoIP rule module'
            'rrdtool: RRDtool based information collector')
backup=('etc/cherokee/cherokee.conf'
        'etc/logrotate.d/cherokee')
source=(http://www.cherokee-project.com/download/1.0/$pkgver/cherokee-$pkgver.tar.gz
        cherokee.rc
        cherokee.logrotate)
options=('!libtool')
md5sums=('40a95ac6184641230ea1aea9892a1ea3'
         'a2d2b69c6220fab57cda4f531b680f9f'
         '8d69341bd4002bffd69c6e82ff6c905f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Use subdirectory for logs
  sed -i -r 's|(%localstatedir%/log)|\1/cherokee|' cherokee.conf.sample.pre

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --with-wwwroot=/srv/http --disable-static \
              --with-wwwuser=http --with-wwwgroup=http \
              --enable-os-string="Arch Linux"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR="$pkgdir" install

  # PAM configuration file for cherokee
  install -D -m644 pam.d_cherokee "$pkgdir/etc/pam.d/$pkgname"

  # Fix ownership of /var/lib/cherokee/graphs
  chown -R http:http "$pkgdir/var/lib/$pkgname/graphs"

  # Use Python 2
  sed -i 's/env python$/\02/' \
    "$pkgdir/usr/share/cherokee/admin/"{server,upgrade_config}.py

  # Compile Python scripts
  python2 -m compileall "$pkgdir"
  python2 -O -m compileall "$pkgdir"

  install -d -o http -g http "$pkgdir/var/log/$pkgname"
  install -D "$srcdir/$pkgname.rc" "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"

  # Cleanup
  rm -rf "$pkgdir/srv"
}

# vim:set ts=2 sw=2 et:
