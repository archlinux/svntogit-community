# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Link Dupont <link@subpop.net>

pkgname=cherokee
pkgver=0.99.39
pkgrel=1
pkgdesc="A very fast, flexible and easy to configure Web Server"
arch=('i686' 'x86_64')
url="http://www.cherokee-project.com/"
license=('GPL2')
depends=('openssl' 'pcre')
makedepends=('python' 'gettext' 'libldap' 'pam' 'libmysqlclient' 'ffmpeg'
             'geoip')
optdepends=('python: cherokee-admin (administrative web interface)'
            'libldap: ldap validator'
            'pam: pam validator'
            'libmysqlclient: mysql validator'
            'ffmpeg: Audio/Video streaming handler'
            'geoip: GeoIP rule module'
            'rrdtool: RRDtool based information collector')
backup=('etc/cherokee/cherokee.conf'
        'etc/logrotate.d/cherokee')
install=cherokee.install
source=(http://www.cherokee-project.com/download/0.99/$pkgver/cherokee-$pkgver.tar.gz
        cherokee.rc
        cherokee.logrotate)
options=('!libtool')
md5sums=('2e9694268eda387489b10b2e237e291a'
         'd03a1faae555edb3b68ba246acfc4287'
         '213069423f1c4a6abbb6e13a5b261bcf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Use subdirectory for logs
  sed -i -r 's|(%localstatedir%/log)|\1/cherokee|' \
            cherokee.conf.sample.pre || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --with-wwwroot=/srv/http --disable-static \
              --with-wwwuser=http --with-wwwgroup=http \
              --enable-os-string="Arch Linux"
  make || return 1
  make -j1 DESTDIR="$pkgdir" install || return 1

  # PAM configuration file for cherokee
  install -D -m644 pam.d_cherokee "$pkgdir/etc/pam.d/$pkgname"

  # Copy configuration converters
  for converter in contrib/*to*.py; do
    install -D -m755 $converter "$pkgdir/usr/share/$pkgname/converters/$(basename $converter)"
  done

  # Fix ownership of /var/lib/cherokee/graphs
  chown -R http:http "$pkgdir/var/lib/$pkgname/graphs"

  # Compile Python scripts
  python -m compileall "$pkgdir" || return 1
  python -O -m compileall "$pkgdir" || return 1

  install -d -m755 -o http -g http "$pkgdir/var/log/$pkgname"
  install -D -m755 "$srcdir/$pkgname.rc" "$pkgdir/etc/rc.d/$pkgname"
  install -D -m644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
}
