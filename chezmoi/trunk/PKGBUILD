# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Tom Payne <twpayne@gmail.com>
# Contributor:  <godeater@gmail.com>

pkgname=chezmoi
pkgver=2.0.12
pkgrel=1
pkgdesc="Manage your dotfiles across multiple machines"
arch=('x86_64')
url='https://www.chezmoi.io/'
license=('MIT')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/twpayne/chezmoi/archive/v${pkgver}.tar.gz")
sha512sums=('8aa9bbfbd2274a04c07f9e37422bb71d8866a4955aa8db3762275238acd20a5e1e31b2b7bdece295ea358e5e4c5d68370c4d4d9ead033e61ffdde64905a78109')

build() {
  cd "$pkgname-$pkgver"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -tags noupgrade,noembeddocs \
    -ldflags "-X github.com/twpayne/chezmoi/cmd.DocsDir=/usr/share/doc/$pkgname \
              -X main.version=$pkgver \
              -extldflags ${LDFLAGS}" \
    .
}

check() {
  cd "$pkgname-$pkgver"

  go test -v ./...
}

package() {
  cd "$pkgname-$pkgver"

  install -D "$pkgname" "$pkgdir/usr/bin/$pkgname"

  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 docs/*.md "$pkgdir/usr/share/doc/$pkgname/"

  install -Dm644 completions/chezmoi-completion.bash "$pkgdir/usr/share/bash-completion/completions/chezmoi"
  install -Dm644 completions/chezmoi.fish "$pkgdir/usr/share/fish/vendor_completions.d/chezmoi.fish"
  install -Dm644 completions/chezmoi.zsh "$pkgdir/usr/share/zsh/site-functions/_chezmoi"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
