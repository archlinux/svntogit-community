# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: <david@pako.st>
# Contributor: Alexander Rødseth <rodseth@gmail.com>

pkgname=chicken
pkgver=4.9.0.1
pkgrel=1
pkgdesc='Compiler for R5RS scheme, featuring handy extensions'
arch=('x86_64' 'i686')
url='http://call-cc.org/'
license=('BSD')
makedepends=('inetutils' 'chicken')
options=('staticlibs')
#source=("http://code.call-cc.org/releases/$pkgver/$pkgname-$pkgver.tar.gz")
source=("http://code.call-cc.org/releases/${pkgver%.*}/$pkgname-$pkgver.tar.gz")
sha256sums=('04df7c439c36fc16446bdfa186e7a70258f911d2d826b5216a8e6b1cb2aa2815')

build() {
  make -C "$pkgname-$pkgver" -j1 PLATFORM=linux PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make -j1 PLATFORM=linux PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
