# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: <david@pako.st>
# Contributor: Alexander Rødseth <rodseth@gmail.com>

pkgname=chicken
pkgver=4.12.0
pkgrel=1
pkgdesc='Feature rich R5RS Scheme compiler and interpreter'
arch=('x86_64' 'i686')
url='http://call-cc.org/'
license=('BSD')
makedepends=('inetutils' 'chicken')
options=('staticlibs')
source=("https://code.call-cc.org/releases/$pkgver/$pkgname-$pkgver.tar.gz") # ${pkgver%.*}
sha256sums=('605ace459bc66e8c5f82abb03d9b1c9ca36f1c2295931d244d03629a947a6989')

build() {
  make -j1 -C "$pkgname-$pkgver" PLATFORM=linux PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make -j1 PLATFORM=linux PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: code.call-cc.org/releases/current/NEWS
# vim:set ts=2 sw=2 et:
