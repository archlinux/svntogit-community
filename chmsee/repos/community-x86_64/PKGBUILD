# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: dionydonny <dionydonny@gmail.com>
# Contributor: Ermanno <erm67@yahoo.it>

pkgname=chmsee
pkgver=1.99.09
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="A chm (MS HTML help file format) viewer based on xulrunner."
url="http://chmsee.googlecode.com/"
license=('GPL')
depends=('xulrunner>=11.0' 'chmlib' 'desktop-file-utils')
makedepends=('python2')
source=(chmsee-$pkgver.tar.gz::https://github.com/jungleji/chmsee/tarball/v$pkgver
        chmsee)
install=chmsee.install
md5sums=('f0ee2a87ffd3f6667d88eb89c31387e4'
         '242a9390bc943cc38237fa71137fb26c')

build() {
  cd ${srcdir}/jungleji-chmsee-*/src

  cp Makefile.arch Makefile
  sed -i -e 's/11.*/12.*/g' ../application.ini

  make
}

package() {
  cd ${srcdir}/jungleji-chmsee-*

  install -d ${pkgdir}/usr/share/chmsee
  cp -a * ${pkgdir}/usr/share/chmsee 
  rm -r ${pkgdir}/usr/share/chmsee/src
  install -Dm644 data/chmsee.desktop.in \
    ${pkgdir}/usr/share/applications/chmsee.desktop
  # Fix FS#25152
  sed -i -e 's/_//g' ${pkgdir}/usr/share/applications/chmsee.desktop
  #
  install -d ${pkgdir}/usr/share/pixmaps
  install -Dm644 data/chmsee-icon.png ${pkgdir}/usr/share/pixmaps
  install -Dm755 ${srcdir}/chmsee ${pkgdir}/usr/bin/chmsee
#  install -d ${pkgdir}/usr/share/icons
#  cp -a data/icons/* ${pkgdir}/usr/share/icons/
}
