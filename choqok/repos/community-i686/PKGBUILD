# $Id$
# Maintainer: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Emanuele Rossi <newdna1510@yahoo.it>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=choqok
pkgver=1.4
pkgrel=1
pkgdesc="A Twitter/identi.ca/laconica client for KDE"
url='http://choqok.gnufolks.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdebase-runtime' 'qjson' 'qoauth' 'qca-ossl')
makedepends=('cmake' 'automoc4')
optdepends=('kdebase-konqueror: proxy support')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('2efe34ed903b448a21d9233d8033dbdb')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
