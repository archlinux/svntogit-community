# $Id$
# Maintainer: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Emanuele Rossi <newdna1510@yahoo.it>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=choqok
pkgver=1.5
pkgrel=5
pkgdesc="A Twitter/Pump.io/GNU social client for KDE"
url='http://choqok.gnufolks.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdebase-runtime' 'qjson' 'qoauth')
makedepends=('cmake' 'automoc4' 'telepathy-qt')
optdepends=('kdebase-konqueror: proxy support'
            'telepathy-qt: IMstatus plugin')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('f2efdd76199f8276e8ac2f76dc600596')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
