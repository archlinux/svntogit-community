# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=chromium-bsu
pkgver=0.9.16
pkgrel=1
pkgdesc="A fast paced top scrolling shooter"
arch=('x86_64')
url="http://chromium-bsu.sourceforge.net/"
license=('custom:artistic')
depends=('freeglut' 'glu' 'sdl_image' 'openal' 'freealut' 'ftgl' 'fontconfig')
source=(https://downloads.sourceforge.net/project/chromium-bsu/Chromium%20B.S.U.%20source%20code/chromium-bsu-$pkgver.tar.gz use_fabs_for_floats.patch)
md5sums=('3d3fb58fa0503061605f51a0c2563bad'
         'f229516bbc25e7b2dc136674a4c090b4')

prepare() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np1 -i $srcdir/use_fabs_for_floats.patch
}

build(){
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  install -Dm644 COPYING $pkgdir/usr/share/licenses/chromium-bsu/LICENSE
}

