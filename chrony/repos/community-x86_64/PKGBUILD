# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=3.2
pkgrel=2
pkgdesc='Lightweight NTP client and server'
arch=('x86_64')
url="http://chrony.tuxfamily.org/"
options=(strip emptydirs)
license=('GPL')
depends=('nss' 'readline' 'libcap' 'libedit')
backup=('etc/chrony.conf')
source=(https://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        "${pkgname}-${pkgver}.tar.gz.asc::https://download.tuxfamily.org/chrony/${pkgname}-${pkgver}-tar-gz-asc.txt"
        service
        chrony.sysusers
        chrony.tmpfiles)
sha256sums=('329f6718dd8c3ece3eee78be1f4821cbbeb62608e7d23f25da293cfa433c4116'
            '4434f5f6eca4781ac906e260db2444f9ce08df4406621e0c0b58818b5a78cb9c'
            'dbd30435d5f51a92ecef779efa0eabaf56d09a35a18ddd30644d5418beada212'
            '56aa817638c8f959169d514f1e7fc727ef31e7d3beda4fca5165292c7602ee42'
            'c0b52787f68f3ef365eef64f53f22b4fb0046a91f0e38923f9d16f1ca19c3f3a')
validpgpkeys=('8B1F4A9ADA73D401E3085A0B5FF06F29BA1E013B') # Miroslav Lichvar <mlichvar@redhat.com> has a dsa1024 key *in the wild*

prepare() {
  cd $pkgname-$pkgver
  sed -i \
    -e 's|pool.ntp.org|3.arch.pool.ntp.org|g' \
    -e 's|server foo.example.net|server 0.arch.pool.ntp.org iburst|g' \
    -e 's|server bar.example.net|server 1.arch.pool.ntp.org iburst|g' \
    -e 's|server baz.example.net|server 2.arch.pool.ntp.org iburst|g' \
    examples/chrony.conf.example*
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-docs
  install -Dm0644 "$srcdir/chrony.sysusers" "$pkgdir/usr/lib/sysusers.d/chrony.conf"
  install -Dm0644 "$srcdir/chrony.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/chrony.conf"

  install -Dm0644 "$srcdir/$pkgname-$pkgver/examples/chrony.conf.example3" "$pkgdir/etc/chrony.conf"

  echo -e "\n# Enable kernel RTC sync\nrtcsync" >> "$pkgdir/etc/chrony.conf"

  sed -i '/^! rtconutc$/ s/^! //' "$pkgdir/etc/chrony.conf"

  mv "$pkgdir/usr/sbin/chronyd" "$pkgdir/usr/bin"
  rmdir "$pkgdir"{/usr/sbin,/var/lib/chrony,/var/lib}

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/chronyd.service"

  cp -a examples "$pkgdir"/usr/share/doc/chrony/examples

}
