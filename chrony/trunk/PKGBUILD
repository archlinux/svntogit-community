# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=1.30
pkgrel=2
pkgdesc='Lightweight NTP client and server'
arch=('i686' 'x86_64')
url="http://chrony.tuxfamily.org/"
options=(strip emptydirs)
license=('GPL')
depends=('readline' 'libcap')
backup=('etc/chrony.conf')
source=(http://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        service)
install=${pkgname}.install
sha256sums=('43aca956a0beb2c4847feffc847fec2c429ee9afb6252c7a499bbd68933046dd'
            '3c9d05aaba6c0bef43fd1923a6dd7ac5f9b9539d21efbd1fbe592b54dec8d493')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-docs

  install -Dm0644 "$srcdir/$pkgname-$pkgver/examples/chrony.conf.example" "$pkgdir/etc/chrony.conf"

  echo -e "\n# Enable kernel RTC sync\nrtcsync" >> "$pkgdir/etc/chrony.conf"

  sed -i '/^! rtconutc$/ s/^! //' "$pkgdir/etc/chrony.conf"

  mv "$pkgdir/usr/sbin/chronyd" "$pkgdir/usr/bin"
  rmdir "$pkgdir/usr/sbin"

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/chrony.service"

  install -dm755 "$pkgdir/usr/lib/systemd/ntp-units.d"
  echo "$pkgname.service" > "$pkgdir/usr/lib/systemd/ntp-units.d/10-$pkgname.list"

  chown 183:183 "$pkgdir/var/lib/chrony"
}
