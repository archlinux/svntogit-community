# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=cinelerra-cv
pkgver=20110810
pkgrel=1
pkgdesc="Professional video editing and compositing environment"
arch=('i686' 'x86_64')
url="http://cinelerra.org/"
license=('GPL')
depends=('e2fsprogs' 'a52dec' 'fftw' 'lame' 'libavc1394' 'libiec61883'
         'libraw1394' 'libsndfile' 'libvorbis' 'libogg' 'libpng>=1.4.0'
         'libjpeg>=8' 'libtiff' 'mjpegtools' 'openexr' 'x264' 'freetype2'
         'libxxf86vm' 'ffmpeg' 'libxv' 'faad2' 'faac' 'libtheora')
makedepends=('automake>=1.7' 'autoconf>=2.57' 'git'
             'libtool' 'nasm' 'libgl' 'mesa')
optdepends=('libgl: X11-OPENGL video driver')
options=('!libtool')
source=('cinelerra-v4l1_removal.patch')
md5sums=('bfa85e20809429d88eba4ab83e569612')

_gitroot="git://git.cinelerra.org/j6t/cinelerra.git"
_gitname="cinelerra"

confit() {
  ./configure --prefix=/usr \
              --with-buildinfo=git/recompile \
              --with-external-ffmpeg \
              --enable-opengl \
              --disable-esd $@
}

build() {
  unset LDFLAGS
  export CPPFLAGS="-D__STDC_CONSTANT_MACROS"

  # gcc 4.6 workaround
  export CFLAGS="$CFLAGS -Wwrite-strings"

  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  patch -Np1 -i "$srcdir/cinelerra-v4l1_removal.patch"

  sed -i -e '/Debian/d' admin/nasm

  # if you don't need OpenGL comment out the next line
  sed -i '/\/X11R6/s///' configure.in

  ./autogen.sh

  if [ "$CARCH" = 'x86_64' ]; then
    confit --disable-mmx
  else
    confit --enable-mmx
  fi

  make
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
