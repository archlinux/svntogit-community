# $Id$
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=cinelerra-cv
pkgver=20100121
pkgrel=1
pkgdesc="A complete audio and video production environment for Linux."
arch=('i686' 'x86_64')
url="http://cinelerra.org"
license=('GPL')
depends=('e2fsprogs' 'a52dec' 'fftw' 'lame' 'libavc1394' 'libiec61883' 'libraw1394' 'libsndfile' 'libvorbis' 'libogg' 'libpng>=1.4.0' 'libjpeg>=8' 'libtiff' 'esound' 'mjpegtools' 'openexr' 'x264' 'libxxf86vm' 'ffmpeg' 'libxv' 'faad2' 'faac' 'libtheora')
makedepends=('automake>=1.7' 'autoconf>=2.57' 'libtool' 'git' 'nasm' 'libgl' 'mesa')
options=('!libtool')
source=(cinelerra-libavutil50.patch  cinelerra-pkgconfig-x264.patch  cinelerra-x264.patch libpng14.patch)
md5sums=('fb4342dc5c640c8f73d7b4a780eee200'
         '2c5744b3059782adecf5bc0e2e1f8330'
         '1ce99ffb47a49bb3a294786dc823e021'
         'c3c83343b9ea64aed6e3a48f9bed329e')

_gitroot="git://git.cinelerra.org/j6t/cinelerra.git"
_gitname="cinelerra"

build() {
  cd "$srcdir"
  unset LDFLAGS
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"


  patch -Np1 -i $srcdir/cinelerra-libavutil50.patch || return 1
  patch -Np1 -i $srcdir/cinelerra-pkgconfig-x264.patch || return 1
  patch -Np1 -i $srcdir/cinelerra-x264.patch || return 1
  patch -Np1 -i $srcdir/libpng14.patch || return 1

  # If you don't need OpenGL comment out the next line.
  sed -i '/\/X11R6/s///' configure.in

  ./autogen.sh

  if [ "$CARCH" = "x86_64" ]; then
	  ./configure --prefix=/usr --with-buildinfo=git/recompile --with-external-ffmpeg --disable-mmx
  else
	  ./configure --prefix=/usr --with-buildinfo=git/recompile --with-external-ffmpeg --enable-mmx
  fi

  make || return 1
  make DESTDIR=$pkgdir install
} 
