# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-control-center:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-control-center
pkgver=5.2.1
pkgrel=5
pkgdesc="The Control Center for Cinnamon"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-settings-daemon cinnamon-menus colord
         libgnomekbd libmm-glib nm-connection-editor)
optdepends=('cinnamon-translations: i18n'
            'gnome-color-manager: for color management tasks'
            'gnome-online-accounts: for the online accounts module')
makedepends=(meson samurai gnome-online-accounts)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-openvpn-widget-fix.patch::${url}/commit/0f4d212874c4fbee18b860963d0a5c7bd54dcfd1.patch)
sha256sums=('977ac52cf8bcdea99c03c6455f29276a57a605839cf16d190db2088df243366d'
            '03a1e522046f45f15eb21eae386a100ad5f81840c2027477188679884b5b3dc2')
b2sums=('826494b8f8a4dead32d6031d7b4b2a44d357b3e8fedf1065b61193caa86ad433ba68fd9344f70bbce77bb94e086ae15f8854dc73916a5f60c49b1b2bd9e7101c'
        '4dd72e4f76ad9308083861a5bbca8f2742eeed544984ace88df1913311fa3f344f31f98ca67ca396ad0cb2879bdc4b31fdbc17adc03d1948d827279eb402439d')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../${pkgname}-openvpn-widget-fix.patch
}

build() {
  mkdir -p build
  cd build
  arch-meson ../${pkgname}-${pkgver}
  samu
}

package() {
  cd build
  DESTDIR="${pkgdir}" samu install
  # https://github.com/linuxmint/Cinnamon/pull/7382#issuecomment-374894901
  # /usr/bin/cinnamon-control-center is not meant for users, it is a development troubleshooting tool.
  # Just install the shell libs/headers.
  rm "${pkgdir}"/usr/bin/cinnamon-control-center
  rm "${pkgdir}"/usr/share/cinnamon-control-center/ui/shell.ui
}
