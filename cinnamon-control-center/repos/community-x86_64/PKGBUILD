# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-control-center:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-control-center
pkgver=5.0.2
pkgrel=2
pkgdesc="The Control Center for Cinnamon"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-settings-daemon cinnamon-menus colord
         libgnomekbd libmm-glib nm-connection-editor)
optdepends=('cinnamon-translations: i18n'
            'gnome-color-manager: for color management tasks'
            'gnome-online-accounts: for the online accounts module')
makedepends=(meson samurai gnome-online-accounts)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('30f697ebbf3d3d267b8ea09cdbfd358c994bf4b60a458f124dcfd415d2cb58a7')
b2sums=('d292d610516647e149198cbfce652bfd094338612ef6b4765fcb80bd1fe15f2d643bbaa5343f1d79d57f3327c87a3256cfc3900493daf6f80569b1a0bacd821a')

build() {
  mkdir -p build
  cd build
  arch-meson ../${pkgname}-${pkgver}
  samu
}

package() {
  cd build
  DESTDIR="${pkgdir}" samu install
  # https://github.com/linuxmint/Cinnamon/pull/7382#issuecomment-374894901
  # /usr/bin/cinnamon-control-center is not meant for users, it is a development troubleshooting tool.
  # Just install the shell libs/headers.
  rm "${pkgdir}"/usr/bin/cinnamon-control-center
  rm "${pkgdir}"/usr/share/cinnamon-control-center/ui/shell.ui
}
