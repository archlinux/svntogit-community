# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>

pkgname=cinnamon-menus
pkgver=4.2.0
pkgrel=1
pkgdesc="Cinnamon menu specifications"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('glib2')
makedepends=('gobject-introspection' 'meson')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('331bd599d7a863fa92329483637de159c25c57467305691f9768e675cd63f7c2')
b2sums=('80cf245fb6b6d02f4a609d68f2b35252da22601d7f7f2f42dd28a5bc193b1d0cd332bc85251f9c2e43038069453b3e61456319f4f50166e86c32a95339d88e11')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --buildtype=plain \
          ..
    ninja
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" ninja install
}
