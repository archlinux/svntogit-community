# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=5.0.6
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('accountsservice' 'cinnamon-desktop' 'libgnomekbd'
         'python-cairo' 'python-gobject' 'python-setproctitle' 'python-xapp' 'xapp')
optdepends=('cinnamon-translations: i18n')
makedepends=('gobject-introspection' 'meson' 'samurai')
backup=('etc/pam.d/cinnamon-screensaver')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('fce753d4805ad8e0e6767238b5b24b741a8e6b974fb0739cbe76105dfcd33621330257cdbfc34d5a33256659f5b2f9ef457cca9351ff6f73802fbffd6c0828b6')
b2sums=('b7bb32c3986f662e8ff9ff857ef8c8f2fb1945abbf6af39b6dd9e4f915dfb47be5b177d474a8199e07d31c8394d6fc1dcf3087cf30a9196d85380e195b8ee0ef')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/cinnamon-screensaver \
          --buildtype=plain \
          ..
    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
