# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=5.6.1
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(accountsservice cinnamon-desktop libgnomekbd
         python-cairo python-gobject python-setproctitle python-xapp xapp xdotool)
optdepends=('cinnamon-translations: i18n')
makedepends=(gobject-introspection meson samurai)
backup=(etc/pam.d/cinnamon-screensaver)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ee447ff7b1c1c9748f593f42af844039d383de85b52fdfaac3c9df50e56b427b1a4a77d2cec7bef5be28d372329a02c3fb19f46e90ba72ae99d1783e9c866d3f')
b2sums=('c5d0c6e5d06b52d28b9b9f9d503bf78618fba5ebc96b46986e5e2c9327ccde6d18d056882c8f283cd9d1e7e55dc6ed182312902744177d83e920df108795500b')

build() {
  arch-meson --libexecdir=lib/cinnamon-screensaver build ${pkgname}-${pkgver}
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
