# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=4.2.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('accountsservice' 'cinnamon-desktop' 'dbus-glib' 'libgnomekbd'
         'python-cairo' 'python-gobject' 'python-setproctitle' 'python-xapp' 'xapps')
optdepends=('cinnamon-translations: i18n')
makedepends=('gobject-introspection' 'intltool')
backup=('etc/pam.d/cinnamon-screensaver')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('276229156acd31c1f1f89cf590e83cdf4d2e87dbf246af68b114ac674f8f9b0d2a704a0485d0f7d742704315122e3b67020b6d47fab073acbfa2c48cd7fcc171')
b2sums=('a36f98e329d18d4865cb3a54f3398e1d13089595be48fc2291459bae43dd396dfb1b8bf087c863aae6e350897ed63904cdb81248083be03ba0b16720ce7182ce')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/cinnamon-screensaver \
                --localstatedir=/var
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
