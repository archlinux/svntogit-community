# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=2.0.3
pkgrel=2
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/linuxmint/cinnamon-screensaver"
backup=(etc/pam.d/cinnamon-screensaver)
depends=('cinnamon-desktop' 'dbus-glib' 'libgnomekbd')
makedepends=('intltool' 'gnome-common')
source=($pkgname-$pkgver.tar.gz::https://github.com/linuxmint/cinnamon-screensaver/archive/$pkgver.tar.gz
       cinnamon-screensaver.pam
       away-message-fixes.patch
       fix-systemd-lock.patch
       lock_screen_on_suspend.patch)
sha256sums=('00c866e98994afc8939d1d347de631cebaa6ec34ad59646a7ccd223bd757cc7d'
            'b6ea9e2eb586d94bcabb617a8f1c2958111df87afdbb51f645882bccdc15cbda'
            'f9f3d555b0c325322469be765b488da599fc850f8ff5a74f72fdf69f68e04c96'
            '14d3301a03883330bf044a40ea4dce74a829b03174833d21efe66ebeb940dc0c'
            '54f281eeb0e97c8c46250e287719051e0a580f6c0de384760aaa3532b42616f1')

prepare() {
  cd $pkgname-$pkgver

  # Various fixes for away message handling
  patch -Np1 -i ../away-message-fixes.patch

  # Use the session path instead of the session id
  patch -Np1 -i ../fix-systemd-lock.patch

  # Lock screen on suspend with systemd
  patch -Np1 -i ../lock_screen_on_suspend.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon-screensaver \
               --localstatedir=/var \
               --with-mit-ext --with-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../cinnamon-screensaver.pam "$pkgdir/etc/pam.d/cinnamon-screensaver"
}
