# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.6.0
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop colord dbus-glib libcanberra-pulse libcups
         libgnomekbd libgudev libnotify librsvg libwacom nss polkit
         pulseaudio-alsa upower)
optdepends=('cinnamon-translations: i18n')
makedepends=(meson samurai xf86-input-wacom)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('69aff73deb5ae0278642b8d630b7e460daf4c84f4745f8809c73d2a74effc0642f404589651939fd5132cfe99010259155a9809cb4b074e34d52bb89f1266fd6')
b2sums=('d8b9a4ff4574251fc142840e81070eebd66ed322b64eadca97efcf2f6bc302bfef18e933d3e7d9bdfe236cc9fb30b24b4b98cbb539ed50c53767d1d2fd7a4a26')

build() {
  arch-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
