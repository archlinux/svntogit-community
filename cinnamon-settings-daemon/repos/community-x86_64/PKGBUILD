# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.4.5
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop colord dbus-glib libcanberra-pulse libcups
         libgnomekbd libgudev libnotify librsvg libwacom nss polkit
         pulseaudio-alsa upower)
optdepends=('cinnamon-translations: i18n')
makedepends=(meson samurai xf86-input-wacom)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('7a3629361fac9d48b3ee64f4595e393b601553d8941762ccdb98bc362edc17ca27e1f8077e7b81b3c262445c4335f7740b8800745146237006547bdcf17a40d1')
b2sums=('a2075d74c7e05f9deda385577725d41b6d184a7de7b2c519c7c4167af99bd13d2882402b245ada5ff2e049189cce4a3b0f6e1a2c88ccad8c296fab9da8dd6b66')

build() {
  arch-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
