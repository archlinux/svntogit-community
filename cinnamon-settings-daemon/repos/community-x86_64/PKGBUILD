# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.4.3
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop colord dbus-glib libcanberra-pulse libcups
         libgnomekbd libgudev libnotify librsvg libwacom nss polkit
         pulseaudio-alsa upower)
optdepends=('cinnamon-translations: i18n')
makedepends=(meson samurai xf86-input-wacom)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('cdf3324aa013c6c6e6fa9ae906fc0eeec9166479b77a256867a786bb586fa0dd8be41f4556e5fd8ac7aa2935cc4900c52f078731e68df394e721a996314cecf7')
b2sums=('0f2b6a3161efe4e067224a7d752ce87d2ea5b5391b6c01cf29d52062d2d5124997591ba4c2f4f7b12c7b43fa17e8727fa3d916574cc0187854e48b1d7070cbaa')

build() {
  arch-meson --libexecdir=lib/${pkgname} ${pkgname}-${pkgver} build
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
