# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.4.2
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'samurai' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('a188b7362c750bc49bdabdc02b46281b34eeb0946360328bc5b27bbe29879418e0cf9984cb1f2fbbca8699aabdb5aab716a98aa04fb148185fbf4b79617e8f6f')
b2sums=('d096d407b5028008472cff3e5ee9d8975aab50cd26898b0e40b4fc2be3260d98175bd3f845136b52eaeb618489abe0a438f2b575bfcee4ec34411499d9a533b6')

build() {
    mkdir -p ${pkgname}-${pkgver}/builddir
    cd ${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir="/usr/lib/${pkgname}" \
          --buildtype=plain \
          ..

    samu
}

package() {
    cd ${pkgname}-${pkgver}/builddir

     DESTDIR="${pkgdir}" samu install
}
