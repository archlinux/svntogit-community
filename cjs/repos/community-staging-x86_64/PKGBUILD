# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=4.4.0
pkgrel=2
pkgdesc="Javascript Bindings for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('dbus-glib' 'gtk3' 'gobject-introspection-runtime' 'js52')
makedepends=('autoconf-archive' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('3b2bdc55ab572713d6b6f92978522b290819656f64fef620659065ff5673a04a0585b83a1a0248116570ac1cfee4f54acc228f355c02aa4e0a2a13b59fbafed2')
b2sums=('af276cd8f9ef0f66a6f4ab3e0072b485e33015e070bb796818907964df0696240b753f7a271bf45db788eb244e0cd1fb866f3b78798adf3ef8f9e76825fc4d74')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --libexecdir=/usr/lib \
                --disable-static

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # Needs a display
    xvfb-run make -k check || :
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
