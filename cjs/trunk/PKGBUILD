# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=3.8.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('dbus-glib' 'gtk3' 'gobject-introspection-runtime' 'js52')
makedepends=('autoconf-archive' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('7f776be2d4863ea7bb32e266efb0c5d5e0f86bab68bdabc1478e04809f8f887b4ff0853075baffa46055d347fc22c6252faefaceba750f61b0b21229f6130bd0')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --libexecdir=/usr/lib \
                --disable-static

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # Needs a display
    xvfb-run make -k check || :
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
