# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=cl-alexandria
_pkgname="${pkgname#cl-}"
pkgver=1.4.r17.g2f39fbf
pkgrel=1
pkgdesc='A set of Common Lisp Utilities'
arch=('any')
url='https://alexandria.common-lisp.dev'
license=('MIT')
depends=('cl-asdf' 'common-lisp')
makedepends=('git' 'sbcl' 'texlive-core')
checkdepends=('cl-rt') # sbcl required, but it's already in makedepends
_commit='2f39fbf34e258fcf8e1a1c0b1a3cc34e163e14b4'
source=(
  "$pkgname::git+https://gitlab.common-lisp.net/alexandria/alexandria.git#commit=$_commit"
  'run-tests.lisp'
)
b2sums=('SKIP'
        '4cc615a6d37da2e82222947be239c7e0191e3a7c6356b5c19970187d4f9110bbad82939d1cf67db7a94db4e5091d372e31d1bc71fa080e00b4f6cc6786129f8f')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed -e 's/^v//' -e 's/-/.r/' -e 's/-/./g'
}

build() {
  cd "$pkgname"

  make -C doc html info
}

check() {
  cd "$pkgname"

  sbcl --script ../run-tests.lisp
}

package() {
  cd "$pkgname"

  # library
  install -vd "$pkgdir/usr/share/common-lisp/source/$_pkgname"
  cp -vr "$_pkgname.asd" alexandria-{1,2} "$pkgdir/usr/share/common-lisp/source/$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" doc/alexandria.html
  install -vDm644  doc/alexandria.info "$pkgdir/usr/share/info/$pkgname.info"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENCE
}
