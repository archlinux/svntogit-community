# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Fabian Brosda <fabi3141@gmx.de>

pkgname=cl-asdf-flv
_pkgname="${pkgname#cl-}"
pkgver=2.1
pkgrel=2
pkgdesc='cl-asdf extension to provide support for file-local variables'
arch=('any')
url='https://github.com/didierverna/asdf-flv'
license=('custom:FSFAP') #SPDX-ID: FSFAP
depends=('common-lisp' 'cl-asdf')
makedepends=('git')
_commit='fc5b7399767ca35bfb420bbeb9e08494e441dc69'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^version-//'
}

prepare() {
  cd "$pkgname"

  # extract license
  sed -n '/;; Copyright/,/;; without/p' asdf-flv.lisp | sed -e 's/^;; //g' > LICENSE
}

package() {
  cd "$pkgname"

  # library
  install -vDm644 -t "$pkgdir/usr/share/common-lisp/source/$_pkgname" ./*.{lisp,asd}

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
