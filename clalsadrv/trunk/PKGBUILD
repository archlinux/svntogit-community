# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=clalsadrv
pkgver=2.0.0
pkgrel=1
pkgdesc="C++ wrapper around the ALSA API"
arch=('i686' 'x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL')
depends=('clthreads' 'alsa-lib')
source=("${url}downloads/$pkgname-$pkgver.tar.bz2")
md5sums=('be123e1701e4b6c6300907df949bd71c')

prepare() {
  cd "$pkgname-$pkgver/libs"

  # use ldconfig locally
  sed -i 's/ldconfig /&-N $(DESTDIR)/' Makefile
}

build() {
  cd "$pkgname-$pkgver/libs"

  # libs
  make

  # create lib link for building apps
  ln -sf lib$pkgname.so.$pkgver lib$pkgname.so

  # apps
  cd ../apps
  CXXFLAGS+=" -I../libs" \
  LDFLAGS+=" -L../libs" \
  LDLIBS="-lasound" \
  make PREFIX=/usr
}

package(){
  cd "$pkgname-$pkgver"

  # libs
  make -C libs DESTDIR="$pkgdir/" PREFIX=/usr LIBDIR=lib install

  # apps
  make -C apps DESTDIR="$pkgdir/" PREFIX=/usr LIBDIR=lib install
}
