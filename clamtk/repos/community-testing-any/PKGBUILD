# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Blaž Tomažič <blaz.tomazic@gmail.com>

pkgname=clamtk
pkgver=5.24
pkgrel=2
pkgdesc='Easy to use, light-weight, on-demand virus scanner for Linux systems'
url='https://dave-theunsub.github.io/clamtk/'
arch=('any')
license=('GPL')
depends=('clamav' 'perl' 'gtk2-perl' 'perl-locale-gettext' 'perl-libwww' 'perl-http-message'
         'perl-lwp-protocol-https' 'perl-text-csv' 'perl-json' 'python' 'zenity' 'desktop-file-utils'
         'gnome-icon-theme' 'cron')
source=(https://bitbucket.org/dave_theunsub/clamtk/downloads/clamtk-${pkgver}.tar.gz{,.asc})
sha512sums=('e5d03bdfb13165ccd84989d06a864629898c0edfbf3630c4110b220d01e363d53a86b01db8b7bc3021ae6271638d2d7a0a352ec65c872e61570c8cb44c5effb2'
            'SKIP')
validpgpkeys=('5DD47B3B121EE5C354A20305F51D19546ADA59DE') # Dave Mauroni <dave.nerd@gmail.com>

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 clamtk -t "${pkgdir}/usr/bin"
  for f in lib/* ; do
	  install -Dm 644 ${f} "${pkgdir}/usr/lib/perl5/vendor_perl/ClamTk/`basename ${f}`"
  done

  for f in po/*.mo ; do
	  install -Dm 644 "${f}" "${pkgdir}/usr/share/locale/`basename ${f} .mo`/LC_MESSAGES/clamtk.mo"
  done

  install -Dm 644 clamtk.1.gz -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 CHANGES DISCLAIMER README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm 644 images/clamtk.{xpm,png} -t "${pkgdir}/usr/share/pixmaps"
  install -Dm 644 clamtk.desktop -t "${pkgdir}/usr/share/applications"
}

# vim: ts=2 sw=2 et:
