# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=clash-ghc
pkgver=1.4.3
pkgrel=28
pkgdesc="CAES Language for Synchronous Hardware"
url="https://clash-lang.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-clash-lib' 'haskell-clash-prelude'
         'haskell-concurrent-supply' 'haskell-extra' 'haskell-ghc' 'haskell-ghc-typelits-extra'
         'haskell-ghc-typelits-knownnat' 'haskell-ghc-typelits-natnormalise' 'haskell-hashable'
         'haskell-lens' 'haskell-primitive' 'haskell-reflection' 'haskell-split' 'haskell-uniplate'
         'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc')
conflicts=('clash')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1805df7a57ec2b9f291f0561a35e55d776dab67717afb10293e0c29aa7607433b7f1fee30172450f93d3d6fc795b6245648b1241f6c46c79242cd0c6e2d14d4f')

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
