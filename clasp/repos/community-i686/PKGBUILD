# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent Bernardoff <vb@luminar.eu.org>

pkgname=clasp
pkgver=3.3.2
pkgrel=1
pkgdesc='Answer set solver'
arch=('x86_64' 'i686')
url='https://potassco.org/'
license=('GPL3')
makedepends=('cmake' 'ninja')
depends=('gcc-libs')
conflicts=("clingo")
source=("$pkgname-$pkgver.tar.gz::https://github.com/potassco/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('367f9f3f035308bd32d5177391a470d9805efc85a737c4f4d6d7b23ea241dfdf')

build() {
  mkdir -p build
  cd build
  cmake "../$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
