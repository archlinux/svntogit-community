# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cldr-emoji-annotation
pkgver=37.0.13.0.0.1
_tag=37.0_13.0_0_1
pkgrel=1
pkgdesc="Emoji annotation files in CLDR"
arch=('any')
url="https://github.com/fujiwarat/cldr-emoji-annotation"
license=('custom')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fujiwarat/cldr-emoji-annotation/archive/$_tag.tar.gz")
sha512sums=('3152544304d3339f18c0968ee33bdf77ef7f41cfa5ab19cf37c234811bf8486e512cf7424ca2bf94f53169a89f7bb6a2f1eca2268c4e18d4db8a54a54f7753c1')

build() {
  cd cldr-emoji-annotation-$_tag
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd cldr-emoji-annotation-$_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 unicode-license.txt "$pkgdir"/usr/share/licenses/$pkgname/unicode-license.txt
}
