# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=clementine
pkgver=1.3.1.r831.664c5a31f
pkgrel=2
pkgdesc='A modern music player and library organizer'
url=https://www.clementine-player.org/
license=(GPL)
arch=(x86_64)
depends=(
  chromaprint
  crypto++
  fftw
  glew
  glib2
  gst-plugins-base-libs
  gstreamer
  libcdio
  libgl
  libgpod
  liblastfm-qt5
  libmtp
  libmygpo-qt5
  libpulse
  libx11
  protobuf
  qt5-base
  qt5-x11extras
  sqlite
  zlib
)
makedepends=(
  boost
  cmake
  git
  mesa
  qt5-tools
  sparsehash
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'gst-libav: Libav plugin'
  'gvfs: Various devices support'
)
source=(
  git+https://github.com/clementine-player/Clementine.git#commit=664c5a31f9cf519af5a4c3adcb30dbbc73d037d6
  clementine-gcc6.patch
)
sha256sums=(
  SKIP
  05bd4dc0138eed084332fa1a688a96858418731f337f54e0d8ab0853123f40ee
)

pkgver() {
  cd Clementine

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd Clementine

  # Fix build with GCC 6
  patch -p1 -i ../clementine-gcc6.patch
  # https://github.com/clementine-player/Clementine/pull/5630
  git cherry-pick -n 8a6cc8b5069265e1e92e22def985e22c5955e503
  # FS#58413
  git cherry-pick -n -m 1 75f9439843a0e9cdc26cd739d5e1dbd93bd974bc
  # Fix DI.FM
  git cherry-pick -n ddfa5e63f1ef0e243a007efbe576fceb9b2442e8
}

build() {
  cd build

  cmake ../Clementine \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
