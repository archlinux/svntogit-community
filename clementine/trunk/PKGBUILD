# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=clementine
pkgver=1.4.0rc1
pkgrel=1
pkgdesc='A modern music player and library organizer'
url=https://www.clementine-player.org/
license=(GPL)
arch=(x86_64)
depends=(chromaprint crypto++ glew gst-plugins-base-libs libcdio libgpod liblastfm-qt5 libmtp libmygpo-qt5
         taglib protobuf qt5-x11extras projectm)
makedepends=(boost cmake qt5-tools sparsehash)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'gst-libav: Libav plugin'
  'gvfs: Various devices support'
)
source=("https://github.com/clementine-player/Clementine/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")

sha256sums=('96ba2f5f3ece47b2c1065e980dcc37ada93ea42cf37b794ee804514ce4a65459')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SYSTEM_PROJECTM=ON \
    -DUSE_SYSTEM_TAGLIB=ON
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
