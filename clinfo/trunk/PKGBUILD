# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Zanny <lordzanny@gmail.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=clinfo
pkgver=2.1.17.02.09
pkgrel=1
pkgdesc="A simple OpenCL application that enumerates all available platform and device properties"
arch=('x86_64')
url="https://github.com/Oblomov/clinfo"
license=('custom:Public Domain')
depends=('opencl-icd-loader')
makedepends=('opencl-headers')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('ae2b2ae0d287b1ec310844bbd3ae95f3cf8589dd356c736a6fe3133427df438d')

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 clinfo -t "${pkgdir}"/usr/bin/
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
    install -Dm644 man/clinfo.1 -t "${pkgdir}"/usr/share/man/man1/
}
