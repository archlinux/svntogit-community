# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gianmarco Brocchi <gianmarcobrocchi@gmail.com>

pkgname=clinica
pkgver=0.2.1
pkgrel=6
pkgdesc="Simple medical records manager"
arch=('i686' 'x86_64')
url="https://launchpad.net/clinica-project"
license=('GPL3')
depends=('libgee06' 'libpeas' 'dconf')
makedepends=('vala' 'intltool' 'python')
optdepends=('python2-gobject: for Agenzia del Farmaco plugin')
install=clinica.install
source=("http://launchpad.net/clinica-project/stable/${pkgver}/+download/${pkgname}-${pkgver}.tar.bz2"
        vala-0.16.patch)
md5sums=('327ce50d20b0e56d3de57270eed2ad23'
         'cd619b85a1758aad846c06b11961c6a3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/vala-0.16.patch"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' plugins/AgenziaDelFarmaco.py
  sed -i -e "s:Application;GTK;:Office;GTK;:g" clinica.desktop

  ./waf configure --prefix=/usr
  LC_ALL=C LANG=C ./waf build 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./waf --destdir="${pkgdir}" install
  rm -rf ${pkgdir}/usr/share/glib-2.0/schemas/gschemas.compiled
}
