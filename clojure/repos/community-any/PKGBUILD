# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>
# Contributor: Pierre-Yves Ritschard <pyr@spootnik.org>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=clojure
pkgver=1.10.1.763
# https://github.com/clojure/brew-install/1.10.1/CHANGELOG.md
pkgrel=1
pkgdesc='Lisp dialect for the JVM'
url='https://clojure.org/'
arch=(any)
license=(EPL)
depends=(java-environment)
optdepends=('leiningen: automate clojure projects'
            'rlwrap: friendly shell with readline support')
install=clojure.install
source=("https://download.clojure.org/install/clojure-tools-$pkgver.tar.gz"
        clojure.sh)
sha256sums=('2a3ec8a6c3639035c2bba10945ae9007ab0dc9136766b95d2161f354e62a4d10'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')

prepare() {
  sed -i -e 's,PREFIX,/usr/share/clojure,g' clojure-tools/clojure
}

package() {
  cd clojure-tools
  install -Dm644 deps.edn "$pkgdir/usr/share/clojure/deps.edn"
  install -Dm644 example-deps.edn "$pkgdir/usr/share/clojure/example-deps.edn"
  install -Dm644 clojure-tools-$pkgver.jar \
    "$pkgdir/usr/share/clojure/libexec/clojure-tools-$pkgver.jar"
  install -Dm755 clojure "$pkgdir/usr/bin/clojure"
  install -Dm755 clj "$pkgdir/usr/bin/clj"
  install -Dm644 clojure.1 "$pkgdir/usr/share/man/man1/clojure.1"
  install -Dm644 clj.1 "$pkgdir/usr/share/man/man1/clj.1"
  install -Dm755 "$srcdir/clojure.sh" "$pkgdir/etc/profile.d/clojure.sh"
  install -Dm755 exec.jar "$pkgdir/usr/share/clojure/libexec/exec.jar"
}

# getver: clojure.org/downloads
