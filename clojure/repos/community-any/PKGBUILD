# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>
# Contributor: Pierre-Yves Ritschard <pyr@spootnik.org>

pkgname=clojure
pkgver=1.9.0
_toolver=297
pkgrel=3
pkgdesc='LISP for the JVM'
url='http://clojure.org/'
arch=('any')
license=('CPL')
depends=('java-environment' 'bash')
makedepends=('java-environment' 'classpath' 'git' 'maven')
optdepends=('rlwrap: friendlier shell with readline support')
install='clojure.install'
source=("https://download.clojure.org/install/clojure-tools-$pkgver.$_toolver.tar.gz"
        clojure.sh)
sha256sums=('24996086d34f1436a35c4d336f702fb2af975874c2def968ae20fe3785194989'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')

prepare() {
  sed -i -e 's@PREFIX@/usr/share/clojure@g' clojure-tools/clojure
}

package() {
  install -Dm644 clojure-tools/deps.edn "$pkgdir/usr/share/clojure/deps.edn"
  install -Dm644 clojure-tools/example-deps.edn \
    "$pkgdir/usr/share/clojure/example-deps.edn"
  install -Dm644 clojure-tools/clojure-tools-$pkgver.$_toolver.jar \
    "$pkgdir/usr/share/clojure/libexec/clojure-tools-$pkgver.$_toolver.jar"
  install -Dm755 clojure-tools/clojure "$pkgdir/usr/bin/clojure"
  install -Dm755 clojure-tools/clj "$pkgdir/usr/bin/clj"
  install -Dm755 clojure.sh "$pkgdir/etc/profile.d/clojure.sh"
}

# getver: clojure.org/downloads
# vim: ts=2 sw=2 et:
