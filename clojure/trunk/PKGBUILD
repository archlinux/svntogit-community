# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>

pkgname=clojure
pkgver=1.6.0
pkgrel=2
pkgdesc='LISP for the JVM'
url='http://clojure.org/'
arch=('any')
license=('CPL')
depends=('java-environment')
makedepends=('java-environment' 'classpath' 'git' 'maven')
optdepends=('rlwrap: friendlier shell with readline support')
install='clojure.install'
source=("$pkgname::git://github.com/clojure/clojure.git#tag=$pkgname-$pkgver"
        'clj.sh'
        "$pkgname.sh")
sha256sums=('SKIP'
            '1ef67b23b1df37420a0af7261abc7d168031952fb3b3c44e8de53db0885a1621'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')

build() {
  cd "$pkgname"

  mvn package -Dmaven.test.skip=true -Dmaven.repo.local=m2
}

check() {
  cd "$pkgname"

  mvn test -Dmaven.repo.local=m2
}

package() {
  install -Dm755 clj.sh "$pkgdir/usr/bin/clj"
  install -Dm755 clj.sh "$pkgdir/usr/bin/clojure"
  install -Dm755 "$pkgname.sh" "$pkgdir/etc/profile.d/$pkgname.sh"
  install -Dm644 "$pkgname/target/$pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/$pkgname/$pkgname.jar"
}

# vim:set ts=2 sw=2 et:
