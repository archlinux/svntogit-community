# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: M0Rf30

pkgname=clonezilla
pkgver=3.10.11
pkgrel=5
pkgdesc="ncurses partition and disk imaging/cloning program"
arch=('any')
url="http://clonezilla.org"
license=('GPL2')
depends=('drbl' 'partclone' 'ntfs-3g' 'partimage' 'pigz' 'sshfs' 'parted'
         'gptfdisk' 'dosfstools' 'gzip' 'bzip2' 'pbzip2' 'lbzip2' 'lrzip'
         'xz' 'pixz' 'lzop')
backup=('etc/drbl/drbl-ocs.conf')
source=(http://free.nchc.org.tw/drbl-core/src/stable/$pkgname-$pkgver.tar.bz2
        usrbin.patch)
md5sums=('3820052fbb32a5b2c1de9e7595c412f0'
         'a976c9209f7bbccd352a9d0a5c8b41ae')

prepare() {
  cd $pkgname-$pkgver

  patch -Np0 -i "$srcdir"/usrbin.patch

  # remove unavailable gzip option
  sed -i '69s/ --rsyncable//' conf/drbl-ocs.conf

  # FS#44927
  sed -i 's/sfdisk -R/blockdev --rereadpt/g' scripts/sbin/ocs-functions \
    sbin/ocs-makeboot sbin/ocs-onthefly
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install  
}
