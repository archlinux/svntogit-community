# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=cloud-init
pkgver=0.7.5
pkgrel=1
pkgdesc="Cloud Bootstrapping Application"
arch=(any)
license=("GPL3")
url=https://launchpad.net/cloud-init
# cloud-init python lib requirements (according to Requires file):
depends=(systemd python2-yaml python2-cheetah python2-prettytable python2-oauth python2-boto python2-configobj python2-jsonpatch python2-jsonpointer)
makedepends=('python2' 'python2-setuptools')
options=(!emptydirs)
# Archlinux specific cloud.cfg
source=(archlinux.cloud.cfg
  https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        )
noextract=(archlinux.cloud.cfg)
sha1sums=('4e32767ac0e18f3b6f34cfb184af17c8a84d563c'
          '9f21617451ec45b6997470f5c3974de0896d8d67')
backup=(etc/cloud/cloud.cfg etc/cloud/cloud.cfg.d/05_logging.cfg)

prepare(){
    find $pkgname-$pkgver -name \*.py -exec sed -i '1s/python$/&2/' {} +
    sed -i '1s/python$/&2/' $pkgname-$pkgver/tools/read*
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 ./setup.py install --root=${pkgdir} --init-system systemd
  # Use a cloud.cfg crafted for archlinux
  mv $pkgdir/etc/cloud/cloud.cfg ${pkgdir}/etc/cloud/cloud.cfg.ubuntu_default
  install -Dm644 $srcdir/archlinux.cloud.cfg ${pkgdir}/etc/cloud/cloud.cfg
}
