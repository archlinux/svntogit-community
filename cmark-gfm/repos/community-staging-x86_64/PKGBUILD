# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark-gfm
pkgver=0.29.0.gfm.0
pkgrel=1
pkgdesc="GitHub's fork of cmark, a CommonMark parsing and rendering library and program in C"
arch=('x86_64')
url="https://github.com/github/cmark"
license=('custom:BSD2')
depends=('glibc')
makedepends=('cmake' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/github/cmark/archive/$pkgver.tar.gz")
sha512sums=('54e396e035a43e4d0c86fa7f1c48a6a1283c0caaabdbc56dfcecee92e89e69f6d2e016ae2d9cf4f40258a6455fba7b813c1c6e6d37e53d33a381088ccbc5673e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib ../cmark-gfm-$pkgver
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"

  install -D -m 644 "$srcdir"/cmark-gfm-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
