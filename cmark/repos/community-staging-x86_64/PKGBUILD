# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark
pkgver=0.28.0
pkgrel=1
pkgdesc="CommonMark parsing and rendering library and program in C"
arch=('i686' 'x86_64')
url="https://github.com/jgm/cmark"
license=('custom:BSD2')
depends=('glibc')
makedepends=('cmake' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jgm/cmark/archive/$pkgver.tar.gz")
sha512sums=('86aeb42f17440c8e743057851bc3cee9ed4b3fa27dacc37d6af54dee7739210a712020e59c102ed1852b7c3f904881cb4e9b80ea475772095514c4025284cbb6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../cmark-$pkgver
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"

  install -D -m 644 "$srcdir"/cmark-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
