# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jonathan Steel <jsteel at archlinux.org>

pkgname=cmatrix
pkgver=1.2a
pkgrel=12
pkgdesc="A curses-based scrolling 'Matrix'-like screen"
arch=('x86_64')
url="http://www.asty.org/cmatrix/"
license=('GPL2')
depends=('ncurses')
makedepends=('fontconfig')
optdepends=('fontconfig: custom font'
            'kbd: cmatrix-tty custom font'
            'xterm: cmatrix-tty custom font')
source=("http://www.asty.org/$pkgname/dist/$pkgname-$pkgver.tar.gz"
        "cmatrix-tty")
md5sums=('ebfb5733104a258173a9ccf2669968a1'
         '6bb2431c23350cb2082e14b6ba761760')

build() {
  cd $pkgname-$pkgver

  CPPFLAGS="-D_FORTIFY_SOURCE=0" ./configure \
    --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  
  install -Dm644 mtx.pcf "$pkgdir/usr/share/fonts/misc/mtx.pcf"
  install -Dm644 matrix.fnt "$pkgdir/usr/share/kbd/consolefonts/matrix.fnt"
  install -Dm644 matrix.psf.gz "$pkgdir/usr/share/kbd/consolefonts/matrix.psf.gz"
  install -Dm755 "$srcdir/cmatrix-tty" "$pkgdir/usr/bin/cmatrix-tty"

  for i in AUTHORS NEWS COPYING README ChangeLog TODO INSTALL; do
    install -Dm644 $i "$pkgdir/usr/share/doc/$pkgname/$i"
  done
}
