# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: ml <>
pkgname=cmctl
pkgver=1.8.0
pkgrel=1
pkgdesc='Automatically provision and manage TLS certificates in Kubernetes'
arch=('x86_64' 'aarch64')
url='https://github.com/jetstack/cert-manager'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
groups=('kubectl-plugins')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ef00d503ab05c27568e284e3f164a4e08bde8cbe14413ac7d468782c8727d212c28b654ebc16dbef53867ae558d3e5e2e57fe28557871120243ca7698ca73904')
b2sums=('88f77f10a6314d6d701ce2f5666ee8b65176e3bef67212ccb971ba8544da1969ed0d10a06210c4fe85aad4e0b5411024d03f3e50120f70a422c01a6deee686f5')

build() {
  local _x _commit
  _commit=$(bsdcat "$pkgname-$pkgver.tar.gz" | git get-tar-commit-id)
  _x=(
    AppVersion="v$pkgver"
    AppGitCommit="${_commit:?}"
    AppGitState="clean"
  )

  cd cert-manager-"$pkgver"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-linkmode=external ${_x[*]/#/-X=github.com/jetstack/cert-manager/pkg/util.} 
  -X github.com/jetstack/cert-manager/cmd/ctl/pkg/build.name=cmctl 
  -X github.com/jetstack/cert-manager/cmd/ctl/pkg/build/commands.registerCompletion=true" ./cmd/ctl
}

check() {
  cd cert-manager-"$pkgver"
  # we don't want e2e and other tests that explicitly want bazel
  go test -short ./cmd/ctl/...
}

package() {
  cd cert-manager-"$pkgver"
  install -Dm755 ctl "$pkgdir"/usr/bin/cmctl

  # build bash completions
  #mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  #"$pkgdir"/usr/bin/cmctl completion bash > "${pkgdir}/usr/share/bash-completion/completions/cmctl"

  ## build zsh completions
  #mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  #"$pkgdir"/usr/bin/cmctl completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_cmctl"

  ## build fish completions
  #mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  #"$pkgdir"/usr/bin/cmctl completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/cmctl.fish"
}
