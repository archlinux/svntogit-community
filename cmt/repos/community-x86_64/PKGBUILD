#Maintainer: David Runge <dvzrv@archlinux.org>
#Contributor: Damir Perisa <damir.perisa@bluewin.ch>
#Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=cmt
pkgver=1.18
pkgrel=1
pkgdesc="LADSPA plugins for use with software synthesis and recording packages on Linux"
arch=('x86_64')
license=('GPL2')
url="https://www.ladspa.org/cmt/overview.html"
depends=('gcc-libs')
makedepends=('ladspa')
groups=('ladspa-plugins' 'pro-audio')
source=("https://www.ladspa.org/download/${pkgname}_${pkgver}.tgz")
sha512sums=('0c9af744b5f7a8aa1fbb69b48d2edf0d24acb8d2b874f3728af6c750b6175f9c138e717701716bd3d4e1fea31c720928a45f2f00e6fb707a9f88036982830686')
b2sums=('96d146bf1c85e7177f61b7080bb9db45c2f3a5161e4a24acf1635cce1ccb01fd31b60bb898562ea7f805b926ba692b3166786cfd8ce6725cc268bae9011b0668')

prepare(){
  mv -v ${pkgname}_${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  # inserting CFLAGS and LDFLAGS for full RELRO
  sed -e "s|-O2|${CFLAGS} ${LDFLAGS}|" -i src/Makefile
}

build() {
  cd ${pkgname}-${pkgver}/src
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -vDm 755 plugins/cmt.so -t "${pkgdir}/usr/lib/ladspa/"
  # docs
  install -vDm 644 {README,doc/*.html} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
