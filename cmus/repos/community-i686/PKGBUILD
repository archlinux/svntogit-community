# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=cmus
pkgver=2.5.0
pkgrel=3
pkgdesc='Very feature-rich ncurses-based music player'
arch=('i686' 'x86_64')
url="http://cmus.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('faad2' 'ffmpeg' 'flac' 'libao' 'libmad' 'libmodplug'
             'libmp4v2' 'libmpcdec' 'libpulse' 'libvorbis' 'opusfile' 'wavpack')
optdepends=('alsa-lib: for ALSA output plugin support'
            'libao: for AO output plugin support'
            'libpulse: for PulseAudio output plugin support'
            'faad2: for AAC input plugin support'
            'ffmpeg: for ffmpeg input plugin support'
            'flac: for flac input plugin support'
            'libmad: for mp3 input plugin support'
            'libmodplug: for modplug input plugin support'
            'libmp4v2: for mp4 input plugin support'
            'libmpcdec: for musepack input plugin support'
            'libvorbis: for vorbis input plugin support'
            'opusfile: for opus input plugin support'
            'wavpack: for wavpack input plugin support')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-v$pkgver.tar.bz2
        cmus-2.5.0-opus-support.patch)
sha1sums=('244975a5ff642567acb047f6bd518e4a3271c25b'
          '5e524f54d565dfa30760562c223bc0e2e8ed00a0')

build() {
  cd $srcdir/$pkgname-v$pkgver
  patch -Np1 -i $srcdir/cmus-2.5.0-opus-support.patch
  ./configure prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-v$pkgver
  make DESTDIR=$pkgdir install

  install -Dm 644 contrib/_cmus $pkgdir/usr/share/zsh/site-functions/_cmus
}
