# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=codeblocks
pkgver=12.11
pkgrel=4
pkgdesc='Open source and cross-platform C/C++ IDE'
arch=('i686' 'x86_64')
url='http://www.codeblocks.org'
license=('GPL3')
depends=('boost-libs' 'bzip2' 'desktop-file-utils' 'gamin' 'gtk-update-icon-cache'
         'hicolor-icon-theme' 'hunspell' 'shared-mime-info' 'wxgtk')
makedepends=('boost' 'zip')
install=codeblocks.install
source=(http://download.berlios.de/$pkgname/${pkgname}_${pkgver}-1.tar.gz)
md5sums=('4fa8980a8c578b8b5589fceb9b0890d6')

build() {
  cd $pkgname-${pkgver}release*
 
  LDFLAGS+=' -lX11'
  ./configure --prefix=/usr --with-contrib-plugins=all
  make 
}

package() {
  cd $pkgname-${pkgver}release*
  make DESTDIR="$pkgdir" install
}
