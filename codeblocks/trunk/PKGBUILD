# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Roman Mikhayloff <rimf@inbox.ru>

# WIP: Only builds for i686

pkgname=codeblocks
pkgver=16.01
pkgrel=11
pkgdesc='Cross-platform C/C++ IDE'
arch=('x86_64' 'i686')
url='http://codeblocks.org/'
license=('GPL3')
depends=('boost-libs' 'gamin' 'hunspell' 'wxgtk')
makedepends=('boost' 'zip')
source=("https://sourceforge.net/projects/$pkgname/files/Sources/$pkgver/${pkgname}_$pkgver.tar.gz")
sha256sums=('30fb9d206df4ab3c2e2fec2451a0a7c9723bfed9a673cc9be71e881c529d5a03')

build() {
  cd "$pkgname-$pkgver.release"

  # Fix build with boost 1.59
  export CPPFLAGS+=" -DBOOST_ERROR_CODE_HEADER_ONLY"
  export WX_CONFIG_PATH=/usr/bin/wx-config

  # FS#49799
  export CPPFLAGS+=" -fno-delete-null-pointer-checks"

  CXXFLAGS+=' -std=c++11 -fpermissive -w'
  ./bootstrap
  ./configure --prefix=/usr --with-contrib-plugins=all
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-$pkgver.release" DESTDIR="$pkgdir" install
}

# getver: -u 2 codeblocks.org/downloads/source
# vim:ts=2 sw=2 et:
