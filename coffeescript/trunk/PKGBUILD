# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: sloppybench <sloppybench@eml.cc>
# Contributor: der_FeniX <derfenix@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
# Contributor: Simon Lipp <aur@simon.lipp.name>
# Contributor: Chris Bolton <http://scr.im/chris-kun>
# Contributor: Sergei Lebedev <superbobry at gmail dot com>
# Contributor: flying sheep <flying-sheep@web.de>

pkgname=coffeescript
pkgver=2.5.0
pkgrel=1
pkgdesc='CoffeeScript is a little language that compiles into JavaScript'
arch=('any')
url='https://coffeescript.org'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
provides=("coffee-script=$pkgver")
conflicts=('coffee-script')
replaces=('coffee-script')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('4604ca66101e29516e1ad71efdddd4d71d61e56ef902860542cccd946aed4886dec42f63a30699a39ef8b94bdcf73a0d4120cb3165d5b6cbacf6eb0cb5b154fb')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
