# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cbc
pkgver=2.10.5
pkgrel=2
pkgdesc="COIN-OR branch-and-cut mixed integer programming solver"
arch=(x86_64)
url="https://projects.coin-or.org/Cbc"
license=(EPL)
groups=(coin-or)
depends=(coin-or-cgl)
makedepends=(nauty)
source=("https://www.coin-or.org/download/source/Cbc/Cbc-${pkgver}.tgz")
sha256sums=('da1a945648679b21ba56b454b81e939451dc7951d9beb3c3e14f18f64dde6972')

build() {
  cd Cbc-$pkgver/Cbc
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-clp-lib="$(pkg-config --libs clp)" \
              --with-clp-incdir="/usr/include/coin/" \
              --with-cgl-lib="$(pkg-config --libs cgl)" \
              --with-cgl-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --enable-dependency-linking \
              --enable-cbc-parallel \
              --with-nauty-lib=/usr/lib/libnauty.a --with-nauty-incdir=/usr/include/nauty
  make
}

check() {
  cd Cbc-$pkgver/Cbc
#  make test
}

package() {
  cd Cbc-$pkgver/Cbc
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
