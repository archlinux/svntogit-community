# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cbc
pkgver=2.10.7
pkgrel=1
pkgdesc='COIN-OR branch-and-cut mixed integer programming solver'
arch=(x86_64)
url='https://github.com/coin-or/Cbc'
license=(EPL)
groups=(coin-or)
depends=(coin-or-cgl coin-or-asl)
makedepends=(nauty)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Cbc/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('5aa5490e2bc39c3c03f3636c9bca459cb3f8f365e0280fd0c4759ce3119e5b19')

build() {
  cd Cbc-releases-$pkgver
  ./configure --prefix=/usr \
              --enable-cbc-parallel \
              --with-nauty-lib=/usr/lib/libnauty.a --with-nauty-incdir=/usr/include/nauty
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd Cbc-releases-$pkgver/Cbc
  make test
}

package() {
  cd Cbc-releases-$pkgver
  make DESTDIR="$pkgdir" install

# Remove nauty from linker flags in pc file, it is statically compiled
#  sed -e 's|/usr/lib/libnauty.a||' -i "$pkgdir"/usr/lib/pkgconfig/cbc.pc
}
