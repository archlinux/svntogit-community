# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.10.2
pkgrel=1
pkgdesc="COIN-OR collection of utility classes"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/CoinUtils"
license=('EPL')
depends=('zlib' 'bzip2' 'glpk' 'lapack')
makedepends=('gcc-fortran')
groups=('coin-or')
source=("http://www.coin-or.org/download/source/CoinUtils/CoinUtils-${pkgver}.tgz" 'CoinRational.patch' 'CoinRational2.patch')
sha1sums=('bba1f30169dae236f44b94a573956ce7bf3e2ce0'
          'a8900004098d7705b082ac89591d9c45e259dd33'
          'ef5ee274c75893d0f0b69cdf376996154b03780e')

prepare() {
  cd CoinUtils-$pkgver

# install missing header
  patch -p0 -i "$srcdir"/CoinRational.patch
  patch -p0 -i "$srcdir"/CoinRational2.patch
}

build() {
  cd CoinUtils-$pkgver

  ./configure --prefix=/usr --with-blas-lib='-lblas' --with-lapack-lib='-llapack' --with-glpk-lib='-lglpk' --enable-dependency-linking
  make
}

check() {
  cd CoinUtils-$pkgver
  make test
}

package() {
  cd CoinUtils-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
