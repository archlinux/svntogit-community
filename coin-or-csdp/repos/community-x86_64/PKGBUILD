# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ido Rosen <ido@kernel.org>

pkgname='coin-or-csdp'
pkgdesc="A C library for Semidefinite Programming"
pkgver=6.2.0
pkgrel=1
arch=(i686 x86_64)
url='https://projects.coin-or.org/Csdp/'
license=(CPL)
depends=(lapack)
source=("http://www.coin-or.org/download/source/Csdp/Csdp-$pkgver.tgz")
sha256sums=('7f202a15f33483ee205dcfbd0573fdbd74911604bb739a04f8baa35f8a055c5b')

prepare() {
  cd Csdp-$pkgver
# don't override CFLAGS
  sed -e '/CFLAGS/d' -i Makefile
# don't build static lib
  sed -e 's|-static||' -i Makefile
}

build() {
  cd Csdp-$pkgver

  CFLAGS+=" -I../include" make
}

check() {
  cd Csdp-$pkgver

  make unitTest
}

package() {
  cd Csdp-$pkgver
  
  install -dm755 "$pkgdir"/usr/bin
  install -m755 solver/csdp "$pkgdir"/usr/bin
  install -m755 theta/{theta,graphtoprob,complement,rand_graph} "$pkgdir"/usr/bin

  install -dm755 "$pkgdir"/usr/share/licenses/csdp
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/csdp
}
