# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coin-or-osi
pkgver=0.107.6
pkgrel=2
pkgdesc="COIN-OR Open Solver Interface"
arch=(i686 x86_64)
url="https://projects.coin-or.org/Osi"
license=(EPL)
groups=(coin-or)
depends=(coin-or-coinutils)
makedepends=(gcc-fortran)
source=("http://www.coin-or.org/download/source/Osi/Osi-${pkgver}.tgz")
sha1sums=('defc1b9cdd7a2016cc4c6be75afef91ae69a03d5')


build() {
  cd Osi-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr --enable-dependency-linking \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/"
  make
}

package() {
  cd Osi-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
