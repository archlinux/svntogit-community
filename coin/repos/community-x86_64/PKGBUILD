# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Gabriel Souza Franco
# Contributor: Florian Pritz
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Mickele
# Contributor: Marcus Fritzsch <fritschy@googlemail.com>

pkgname=coin
pkgver=4.0.0.f4e446
pkgrel=3
pkgdesc='3D graphics library compatible with Open Inventor'
url='https://github.com/coin3d/coin'
license=(GPL)
arch=(x86_64)
depends=(libgl)
makedepends=(boost cmake doxygen ninja git glu)
optdepends=('fontconfig: dynamic linking support'
            'freetype2: dynamic linking support'
            'openal: audio/dynamic linking support'
            'zlib: dynamic linking support')
# f4e446e is the same as version 4.0.0 + patches
source=("git+$url#commit=f4e446e3d510a880d1b7808fb7e45d781e8b8a4c"
        'man3.patch::https://github.com/coin3d/coin/commit/5a97506a7fa80c5ebcc8aa4685d5cd07b7d95bed.patch')
sha256sums=('SKIP'
            '527619771572adf3c57da2e19690e595c66997555a9f3fef22c637a87c61602c')

prepare() {
  cd coin
  patch -p1 -i ../man3.patch || true
}

build() {
  mkdir -p build
  cd build
  cmake ../coin \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCOIN_THREADSAFE=ON \
    -DCOIN_BUILD_DOCUMENTATION=ON \
    -DCOIN_BUILD_DOCUMENTATION_CHM=OFF \
    -DCOIN_BUILD_DOCUMENTATION_MAN=ON \
    -DUSE_EXTERNAL_EXPAT=ON \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -fPIC -w" \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  # Skip the HTML documentation
  rm -rf "$pkgdir/usr/share/doc"
  # Remove an empty directory
  rmdir "$pkgdir/usr/share/Coin/profiler"
}
