# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Gabriel Souza Franco
# Contributor: Florian Pritz
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Thomas Dziedzic <gostrc at gmail>
# Contributor: Mickele
# Contributor: Marcus Fritzsch <fritschy@googlemail.com>

pkgname=coin
pkgver=4.0.0
pkgrel=1
pkgdesc='3D graphics library compatible with Open Inventor'
url='https://github.com/coin3d/coin'
license=(GPL)
arch=(x86_64)
depends=(libgl)
makedepends=(boost cmake doxygen ninja git glu)
optdepends=('fontconfig: dynamic linking support'
            'freetype2: dynamic linking support'
            'openal: audio/dynamic linking support'
            'zlib: dynamic linking support')
source=("git+$url#commit=acbf0d422dbccdd80caeb68aec4060efb916f9e0") # tag: Coin-4.0.0
sha256sums=('SKIP')

prepare() {
  # This will not be needed for the next release
  mkdir -p coin/cpack.d
  touch coin/cpack.d/CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  cmake ../coin \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCOIN_THREADSAFE=ON \
    -DCOIN_BUILD_DOCUMENTATION=ON \
    -DCOIN_BUILD_DOCUMENTATION_CHM=OFF \
    -DCOIN_BUILD_DOCUMENTATION_MAN=ON \
    -DUSE_EXTERNAL_EXPAT=ON \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -fPIC -w" \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  # Skip the HTML documentation
  rm -rf "$pkgdir/usr/share/doc"
  # Rename some man pages
  for f in threads errors events; do
    mv -v "$pkgdir/usr/share/man/man3/$f.3" "$pkgdir/usr/share/man/man3/coin-$f.3"
  done
  # Remove an empty directory
  rmdir "$pkgdir/usr/share/Coin/profiler"
}
