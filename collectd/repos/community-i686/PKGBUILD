# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Gerhard Brauer <gerhard.brauer@web.de>

pkgname=collectd
pkgver=5.0.2
pkgrel=1
pkgdesc='Daemon which collects system performance statistics periodically'
arch=('i686' 'x86_64')
url='http://collectd.org/'
license=('GPL')
options=('!libtool')
backup=('etc/collectd.conf')

optdepends=('curl: apache, ascent, curl, nginx, and write_http plugins'
            'libdbi: dbi plugin'
            'libesmtp: notify_email plugin'
            'libgcrypt: encryption and authentication for network plugin'
            'iptables: iptables plugin'
            'libmemcached: memcachec plugin'
            'libmysqlclient: mysql plugin'
            'iproute2: netlink plugin'
            'net-snmp: snmp plugin'
            'libnotify: notify_desktop plugin'
            'liboping: ping plugin'
            'libpcap: dns plugin'
            'perl: perl plugin'
            'postgresql-libs: postgresql plugin'
            'python2: python plugin'
            'rrdtool: rrdtool and rrdcached plugins'
            'lm_sensors: lm_sensors and sensors plugins'
            'libvirt: libvirt plugin'
            'libxml2: ascent and libvirt plugins'
            'xmms: xmms plugin'
            'yajl: curl_json plugin')

makedepends=('curl' 'libdbi' 'libesmtp' 'libgcrypt' 'iptables' 'libmemcached'
             'libmysqlclient' 'iproute2' 'net-snmp' 'libnotify' 'liboping'
             'libpcap' 'postgresql-libs' 'python2' 'rrdtool' 'lm_sensors'
             'libvirt' 'libxml2' 'xmms' 'yajl')

depends=('libltdl')

source=("${url}files/${pkgname}-${pkgver}.tar.gz"
        'rtnl_dump_filter.patch'
        'libperl.patch'
        'rc.d')
sha1sums=('96b2b2c285b2334d41b6ebb2a58acd28757e69fc'
          'c92b8dacff0a71f2cc8645c2e350ff9bdc1cbd5f'
          '245c098d121a4a05594553583310953b3a2f6461'
          '0f441718d5519cb043b1130e5a1d0379078adbcc')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../rtnl_dump_filter.patch
	patch -p1 -i ../libperl.patch
	autoconf

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-static=no \
		--with-python=/usr/bin/python2

	make all
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -D -m 755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
	install -D -m 644 contrib/collectd2html.pl "${pkgdir}"/usr/share/collectd/collectd2html.pl
}
