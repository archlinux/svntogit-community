# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Gerhard Brauer <gerhard.brauer@web.de>

pkgname=collectd
pkgver=5.7.1
pkgrel=4
pkgdesc='Daemon which collects system performance statistics periodically'
url='https://collectd.org/'
arch=('i686' 'x86_64')
license=('GPL')

optdepends=('curl: apache, ascent, curl, nginx, and write_http plugins'
            'libdbi: dbi plugin'
            'libesmtp: notify_email plugin'
            'libgcrypt: encryption and authentication for network plugin'
            'libmemcached: memcachec plugin'
            'libmariadbclient: mysql plugin'
            'iproute2: netlink plugin'
            'net-snmp: snmp plugin'
            'libnotify: notify_desktop plugin'
            'openipmi: ipmi plugin'
            'liboping: ping plugin'
            'libpcap: dns plugin'
            'perl: perl plugin'
            'postgresql-libs: postgresql plugin'
            'python2: python plugin'
            'rrdtool: rrdtool and rrdcached plugins'
            'lm_sensors: lm_sensors and sensors plugins'
            'libvirt: libvirt plugin'
            'libxml2: ascent and libvirt plugins'
            'yajl: curl_json plugin'
            'libatasmart: smart plugin'
            'lvm2: lvm plugin'
            'protobuf-c: write_riemann plugin'
            'mosquitto: MQTT plugin')

makedepends=('curl' 'libdbi' 'libesmtp' 'libgcrypt' 'libmemcached'
             'libmariadbclient' 'iproute2' 'net-snmp' 'libnotify' 'openipmi'
             'liboping' 'libpcap' 'postgresql-libs' 'python2' 'rrdtool'
             'lm_sensors' 'libvirt' 'libxml2' 'yajl' 'libatasmart' 'lvm2'
             'protobuf-c' 'mosquitto')

depends=('libltdl' 'iptables')

source=("${url}files/${pkgname}-${pkgver}.tar.bz2"
        'service')
sha256sums=('7edd3643c0842215553b2421d5456f4e9a8a58b07e216b40a7e8e91026d8e501'
            'e45e261146563b0bf6967c669dff7ac2400f066f0eb4c022ac3fdb0ccadca2f9')

backup=('etc/collectd.conf')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/-Werror//g' -i *.ac */*.{am,in} */*/*.{am,in}
	autoreconf
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export MAKEFLAGS='-j1'
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--with-python=/usr/bin/python2 \
		--with-perl-bindings='INSTALLDIRS=vendor'
	make all
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rmdir "${pkgdir}/var/run" # FS#30201
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/collectd.service
	install -Dm644 contrib/collectd2html.pl "${pkgdir}"/usr/share/collectd/collectd2html.pl
}
