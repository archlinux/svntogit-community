# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Gerhard Brauer <gerhard.brauer@web.de>

pkgname=collectd
pkgver=5.3.0
pkgrel=1
pkgdesc='Daemon which collects system performance statistics periodically'
url='http://collectd.org/'
arch=('i686' 'x86_64')
license=('GPL')

optdepends=('curl: apache, ascent, curl, nginx, and write_http plugins'
            'libdbi: dbi plugin'
            'libesmtp: notify_email plugin'
            'libgcrypt: encryption and authentication for network plugin'
            'libmemcached: memcachec plugin'
            'libmariadbclient: mysql plugin'
            'iproute2: netlink plugin'
            'net-snmp: snmp plugin'
            'libnotify: notify_desktop plugin'
            'liboping: ping plugin'
            'libpcap: dns plugin'
            'perl: perl plugin'
            'postgresql-libs: postgresql plugin'
            'python2: python plugin'
            'rrdtool: rrdtool and rrdcached plugins'
            'lm_sensors: lm_sensors and sensors plugins'
            'libvirt: libvirt plugin'
            'libxml2: ascent and libvirt plugins'
            'xmms: xmms plugin'
            'yajl: curl_json plugin')

makedepends=('curl' 'libdbi' 'libesmtp' 'libgcrypt' 'libmemcached'
             'libmariadbclient' 'iproute2' 'net-snmp' 'libnotify' 'liboping'
             'libpcap' 'postgresql-libs' 'python2' 'rrdtool' 'lm_sensors'
             'libvirt' 'libxml2' 'xmms' 'yajl')

depends=('libltdl' 'iptables')

source=("${url}files/${pkgname}-${pkgver}.tar.gz"
        'libperl.patch'
        'service'
        'rc.d')
sha1sums=('53879095aa51b7dd0b30882b0c9b6ce8c93a8539'
          '245c098d121a4a05594553583310953b3a2f6461'
          'b56907f532b9174e1b6995aadb518228d7464d3b'
          '0f441718d5519cb043b1130e5a1d0379078adbcc')

backup=('etc/collectd.conf')
options=('!libtool')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../libperl.patch
	autoconf
	sed 's/-Werror//g' -i src/Makefile.in
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-static=no \
		--with-python=/usr/bin/python2
	make all
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rmdir "${pkgdir}/var/run" # FS#30201
	install -Dm755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/collectd.service
	install -Dm644 contrib/collectd2html.pl "${pkgdir}"/usr/share/collectd/collectd2html.pl
}
