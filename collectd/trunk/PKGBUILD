# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Gerhard Brauer <gerhard.brauer@web.de>

pkgname=collectd
pkgver=5.4.1
pkgrel=1
pkgdesc='Daemon which collects system performance statistics periodically'
url='http://collectd.org/'
arch=('i686' 'x86_64')
license=('GPL')

optdepends=('curl: apache, ascent, curl, nginx, and write_http plugins'
            'libdbi: dbi plugin'
            'libesmtp: notify_email plugin'
            'libgcrypt: encryption and authentication for network plugin'
            'libmemcached: memcachec plugin'
            'libmariadbclient: mysql plugin'
            'iproute2: netlink plugin'
            'net-snmp: snmp plugin'
            'libnotify: notify_desktop plugin'
            'liboping: ping plugin'
            'libpcap: dns plugin'
            'perl: perl plugin'
            'postgresql-libs: postgresql plugin'
            'python2: python plugin'
            'rrdtool: rrdtool and rrdcached plugins'
            'lm_sensors: lm_sensors and sensors plugins'
            'libvirt: libvirt plugin'
            'libxml2: ascent and libvirt plugins'
            'xmms: xmms plugin'
            'yajl: curl_json plugin')

makedepends=('curl' 'libdbi' 'libesmtp' 'libgcrypt' 'libmemcached'
             'libmariadbclient' 'iproute2' 'net-snmp' 'libnotify' 'liboping'
             'libpcap' 'postgresql-libs' 'python2' 'rrdtool' 'lm_sensors'
             'libvirt' 'libxml2' 'xmms' 'yajl')

depends=('libltdl' 'iptables')

source=("${url}files/${pkgname}-${pkgver}.tar.bz2"
        'libperl.patch'
        'service')
sha1sums=('cea47e3936ed081bd71efacf7ba825fc837dc347'
          'efa6450c20ce314e9000a69b17520211f6a471e1'
          '04f676d0b76c34df0bbf94629813e035b1febe04')

backup=('etc/collectd.conf')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/-Werror//g' -i *.ac */*.{am,in} */*/*.{am,in}
	patch -p1 -i ../libperl.patch
	autoreconf
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sbindir=/usr/bin \
		--with-python=/usr/bin/python2
	make all
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rmdir "${pkgdir}/var/run" # FS#30201
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/collectd.service
	install -Dm644 contrib/collectd2html.pl "${pkgdir}"/usr/share/collectd/collectd2html.pl
}
