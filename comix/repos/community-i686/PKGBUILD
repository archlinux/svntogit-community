# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=comix
pkgver=4.0.4
pkgrel=5
pkgdesc="A comic book viewer"
arch=('i686' 'x86_64')
url="http://comix.sourceforge.net"
license=('GPL')
depends=('pygtk' 'pil' 'unrar' 'xdg-utils')
makedepends=('gettext' 'intltool')
install=comix.install
source=(http://downloads.sourceforge.net/sourceforge/comix/comix-${pkgver}.tar.gz)

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # python2 fix
    for file in src/comix.py mime/comicthumb; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done
}

package() {
    cd ${srcdir}/comix-${pkgver}
    mkdir -p ${pkgdir}/usr
    python2 install.py install --no-mime --dir ${pkgdir}/usr
    install -D -m644 mime/comicbook.schemas \
        ${pkgdir}/usr/share/gconf/schemas/comix.schemas
    install -D mime/comicthumb ${pkgdir}/usr/bin/comicthumb
    install -D -m644 mime/comicthumb.1.gz \
        ${pkgdir}/usr/share/man/man1/comicthumb.1.gz
}
md5sums=('029227a77b122f7080ee0280d41bee78')
