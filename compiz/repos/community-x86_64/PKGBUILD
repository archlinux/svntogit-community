# $Id: PKGBUILD 17032 2010-05-17 11:13:51Z rvanharen $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Hussam Al-Tayeb ht990332@gmail.com
# Contributor: Khashayar Naderehvandi <khashayar [at] naderehvandi [dot] net>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>

pkgbase=compiz
pkgname=('compiz-core'
	'compiz-decorator-kde'
	'compiz-decorator-gtk')
pkgver=0.8.8
pkgrel=2
pkgdesc="Composite manager for Aiglx and Xgl"
url="http://www.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
groups=('compiz' 'compiz-kde' 'compiz-gtk' 'compiz-fusion')
makedepends=('intltool' 'gnome-control-center' 'libwnck' 'kdebase-workspace'
 'startup-notification' 'librsvg' 'libgl' 'dbus' 'mesa' 'libxslt' 'fuse' 'metacity')
options=(!libtool !emptydirs)
conflicts=('compiz' 'compiz-core-git' 'compiz-git')
replaces=('compiz')
source=(http://releases.compiz.org/0.8.8/compiz-$pkgver.tar.gz)
sha1sums=('01d065db07f6fd6bcad51811ffba69221aff656e')

build()
{
  cd $srcdir/compiz-$pkgver

  ./configure   --prefix=/usr \
	    --enable-gnome \
	    --enable-gtk \
	    --enable-metacity \
	    --enable-gconf \
            --with-gconf-schema-file-dir=/etc/gconf/schemas \
	    --enable-dbus \
	    --enable-librsvg \
	    --disable-kde \
	    --enable-kde4

  make
}

package_compiz-core()
{

  pkgdesc="Composite manager for Aiglx and Xgl"
  depends=('startup-notification' 'librsvg' \
         'libgl' 'dbus' 'mesa' 'libxslt' 'fuse')

  cd $srcdir/compiz-$pkgver
  make DESTDIR=$pkgdir install

  # remove stuff which is in one of the decorations
   rm -rf $pkgdir/usr/bin/{gtk-window-decorator,kde4-window-decorator}
   rm -rf ${pkgdir}/usr/share/{applications,gnome,gnome-control-center}
   rm -rf ${pkgdir}/usr/lib/{window-manager-settings,pkgconfig/compiz-gconf.pc,compiz/libgconf*}
   rm -rf ${pkgdir}/etc

  # install MIT license
  install -Dm644 $srcdir/compiz-$pkgver/COPYING.MIT \
	$pkgdir/usr/share/licenses/compiz-core/COPYING.MIT
}

package_compiz-decorator-gtk()
{
  pkgdesc="Compiz decorator for GNOME"
  depends=('gnome-control-center' 'libwnck' 'compiz-core' 'metacity')
  install=compiz-decorator-gtk.install

  cd $srcdir/compiz-$pkgver

  pushd gtk
  make DESTDIR=$pkgdir install
  install -Dm644 window-decorator/gwd.schemas \
                $pkgdir/usr/share/gconf/schemas/gwd.schemas
  popd

  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm $srcdir/compiz-$pkgver/metadata/compiz-$i.schemas
  done

  gconf-merge-schema \
    ${pkgdir}/usr/share/gconf/schemas/compiz-decorator-gtk.schemas \
        ${srcdir}/compiz-$pkgver/metadata/*.schemas

  make DESTDIR=$pkgdir install

  # remove stuff which is in one of the other packages
   rm -rf ${pkgdir}/etc
   rm -rf $pkgdir/usr/bin/{compiz,kde4-window-decorator}
   rm -rf $pkgdir/usr/include
   cd $pkgdir/usr/lib
   rm libdecoration*
   rm pkgconfig/{compiz-cube.pc,compiz-scale.pc,compiz.pc,libdecoration.pc}
   cd ${pkgdir}/usr/lib/compiz
   rm `ls --hide=*gconf*`
   rm -rf ${pkgdir}/usr/share/{compiz,locale}
   
  # install MIT license
  install -Dm644 $srcdir/compiz-$pkgver/COPYING.MIT \
        $pkgdir/usr/share/licenses/compiz-decorator-gtk/COPYING.MIT
}

package_compiz-decorator-kde()
{
  pkgdesc="Compiz decorator for KDE"
  depends=('kdebase-workspace' 'compiz-core')

  cd $srcdir/compiz-$pkgver
  make DESTDIR=$pkgdir install

  # Let's remove stuff that are in compiz-core package or gtk decorator
  rm -rf $pkgdir/usr/{include,lib}
  rm -rf $pkgdir/usr/bin/{compiz,gtk-window-decorator}
  rm -rf ${pkgdir}/etc
  rm -rf ${pkgdir}/usr/share/{applications,gnome,gnome-control-center,locale,compiz}

  # install MIT license
  install -Dm644 $srcdir/compiz-$pkgver/COPYING.MIT \
        $pkgdir/usr/share/licenses/compiz-decorator-kde/COPYING.MIT
}

