# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=compton
pkgver=0.1_beta2.5
pkgrel=3
pkgdesc='X compositor that may fix tearing issues'
arch=('x86_64' 'i686')
url='https://github.com/chjj/compton'
license=('MIT')
install='compton.install'
depends=('libgl' 'libdbus' 'libxcomposite' 'libxdamage' 'libxrandr' 'pcre' 'libconfig' 'libxinerama' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('asciidoc' 'mesa' 'git' 'cmake' 'setconf')
backup=('etc/xdg/compton.conf')
optdepends=('dbus:          To control compton via D-Bus'
            'xorg-xwininfo: For compton-trans'
            'xorg-xprop:    For compton-trans')
#source=("git://github.com/chjj/compton#tag=v$pkgver"
source=("git://github.com/chjj/compton#commit=2343e4b"
        'compton.conf')
sha256sums=('SKIP'
            '89f920e429a28443bf81275927e402b3a953ef8a7160af468ae05913dee5658b')

prepare() {
  cd "$pkgname"

  setconf "$pkgname.desktop" Exec 'compton --xrender-sync-fence'
  setconf "$pkgname.desktop" TryExec 'compton --xrender-sync-fence'
  setconf "$pkgname.desktop" Comment 'Compositor for X11'
}

build() {
  COMPTON_VERSION=v$pkgver make -C "$pkgname" PREFIX=/usr compton docs
}

package() {
  cd "$pkgname"

  make PREFIX="$pkgdir/usr" install
  install -Dm644 compton.sample.conf "$pkgdir/etc/xdg/compton.conf.example"
  install -Dm644 ../compton.conf "$pkgdir/etc/xdg/compton.conf"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
