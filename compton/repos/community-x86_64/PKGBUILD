# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=compton
pkgver=0.1_beta2.5
pkgrel=6
pkgdesc='X compositor that may fix tearing issues'
arch=('x86_64' 'i686')
url='https://github.com/chjj/compton'
license=('MIT')
depends=('libgl' 'libdbus' 'libxcomposite' 'libxrandr' 'pcre' 'libconfig' 'libxinerama' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('asciidoc' 'mesa' 'git' 'cmake' 'setconf')
backup=('etc/xdg/compton.conf')
optdepends=('dbus:          To control compton via D-Bus'
            'xorg-xwininfo: For compton-trans'
            'xorg-xprop:    For compton-trans')
source=("git+https://github.com/chjj/compton#commit=316eac0613bf342ff91cc645a6c3c80e6b9083fb"
        'compton.conf')
sha256sums=('SKIP'
            '89f920e429a28443bf81275927e402b3a953ef8a7160af468ae05913dee5658b')

prepare() {
  setconf "$pkgname/$pkgname.desktop" Exec 'compton --xrender-sync-fence'
  setconf "$pkgname/$pkgname.desktop" TryExec 'compton --xrender-sync-fence'
  setconf "$pkgname/$pkgname.desktop" Comment 'Compositor for X11'
}

build() {
  COMPTON_VERSION=v$pkgver PREFIX=/usr make -C "$pkgname" compton docs
}

package() {
  PREFIX="$pkgdir/usr" make -C "$pkgname" install
  install -Dm644 "$pkgname/compton.sample.conf" "$pkgdir/etc/xdg/compton.conf.example"
  install -Dm644 compton.conf "$pkgdir/etc/xdg/compton.conf"
  install -Dm644 "$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:ts=2 sw=2 et
