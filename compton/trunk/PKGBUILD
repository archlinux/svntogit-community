# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=compton
pkgver=3.0
pkgrel=4
pkgdesc='X compositor that may fix tearing issues'
arch=('x86_64')
url='https://github.com/yshui/compton'
license=('MIT')
makedepends=('asciidoc' 'git' 'mesa' 'meson' 'ninja' 'setconf')
depends=('hicolor-icon-theme' 'libconfig' 'libdbus' 'libev' 'libgl' 'pcre'
         'pixman' 'xcb-util-image' 'xcb-util-renderutil')
backup=('etc/xdg/compton.conf')
optdepends=('dbus:          For controlling compton via D-Bus'
            'xorg-xwininfo: For compton-trans'
            'xorg-xprop:    For compton-trans')
# Using the latest commit from the "next" branch until the next release of compton.
# See FS#60647
#source=("git+https://github.com/yshui/compton#tag=v${pkgver%.0}"
source=('git+https://github.com/yshui/compton#commit=b18d46bcbdc35a3b5620d817dd46fbc76485c20d'
        'compton.conf')
sha256sums=('SKIP'
            '55d3929cb9c37edf2aaea8266dda197dbd35949246d1f88c1a168156c60c5324')

prepare() {
  cd $pkgname

  setconf $pkgname.desktop Exec 'compton'
  setconf $pkgname.desktop Comment 'Compositor for X11'
  setconf $pkgname.desktop TryExec '/usr/bin/compton'
}

build() {
  cd $pkgname

  mkdir -p ../build
  arch-meson ../build \
    -D distributor_name='Arch Linux'
  ninja -v -C ../build
}

check() {
  cd $pkgname

  ninja -C ../build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 compton.conf "$pkgdir/etc/xdg/compton.conf"

  cd $pkgname
  install -Dm644 compton.sample.conf "$pkgdir/etc/xdg/compton.conf.example"
  install -Dm644 LICENSES/MIT "$pkgdir/usr/share/licenses/$pkgname/MIT"
  install -Dm644 LICENSES/MPL-2.0 "$pkgdir/usr/share/licenses/$pkgname/MPL-2.0"
}

# vim: ts=2 sw=2 et
