# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=compton
pkgver=5.0
pkgrel=1
pkgdesc='X compositor that may fix tearing issues'
arch=(x86_64)
url='https://github.com/yshui/compton'
license=(MIT)
makedepends=(asciidoc git mesa meson ninja setconf)
depends=(hicolor-icon-theme libconfig libdbus libev libgl libxdg-basedir pcre
         pixman xcb-util-image xcb-util-renderutil)
backup=(etc/xdg/compton.conf)
optdepends=('dbus:          For controlling compton via D-Bus'
            'xorg-xwininfo: For compton-trans'
            'xorg-xprop:    For compton-trans')
source=(compton.conf
        "git+https://github.com/yshui/compton#tag=v${pkgver%.0}")
sha256sums=('b5ec9c21a5b71ccca70917b1a2c99cd863d638d881a4d8f8e12171e80486db7f'
            'SKIP')

prepare() {
  cd $pkgname
  setconf $pkgname.desktop Exec compton
  setconf $pkgname.desktop Comment 'Compositor for X11'
  setconf $pkgname.desktop TryExec /usr/bin/compton
}

build() {
  mkdir -p build
  arch-meson build $pkgname -D build_docs=true
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 compton.conf "$pkgdir/etc/xdg/compton.conf"
  cd $pkgname
  install -Dm644 compton.sample.conf "$pkgdir/etc/xdg/compton.conf.example"
  install -Dm644 LICENSES/MIT "$pkgdir/usr/share/licenses/$pkgname/MIT"
  install -Dm644 LICENSES/MPL-2.0 "$pkgdir/usr/share/licenses/$pkgname/MPL-2.0"
}

# vim: ts=2 sw=2 et
