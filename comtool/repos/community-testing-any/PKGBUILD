# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=1.4
pkgrel=3
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL')
depends=('python-pyqt5' 'python-pyserial' 'python-beautifulsoup4' 'python-setuptools')
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neutree/COMTool/archive/V$pkgver.tar.gz")
sha512sums=('3f27bba898968c1df48dde38a310f95fb3dd9d1d549a08e5a96267029320f42249db9221c6c16aca9a1e0e67596a70f8cda67b64ef60676bcb5a5e56847375a5')

prepare() {
  cd COMTool-$pkgver
  mv README.MD README.md
  sed -i "s/'pyqt5>=5',//" setup.py
  sed -e 's|^strDataAssetsDirName = .*|strDataAssetsDirName = "share/COMToolData/assets"|' \
      -e 's|^strDataDirName = .*|strDataDirName = "share/COMToolData"|' \
      -i COMTool/parameters.py
}

build() {
  cd COMTool-$pkgver
  python setup.py build
}

check() {
  cd COMTool-$pkgver
  # Disabled: Tests hang
  # xvfb-run python -m unittest discover
}

package() {
  cd COMTool-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
