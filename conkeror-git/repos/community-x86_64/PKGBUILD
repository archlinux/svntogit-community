# Contributor: Heeru Kiyura <M8R-p9i5nh@mailinator.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=conkeror-git
pkgver=20101009
pkgrel=1
pkgdesc="A highly programmable web browser based on Mozilla XULRunner."
arch=('i686' 'x86_64')
url="http://conkeror.mozdev.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('bash' 'xulrunner>=1.9.1')
makedepends=('git')
provides=(conkeror)
install=conkeror.install
source=(http://ftp.eenet.ee/gentoo-portage/www-client/conkeror/files/conkeror.desktop)
md5sums=('cabe2d3de7e3ddad6268de711aa186ee')

_gitroot="git://repo.or.cz/conkeror.git"
_gitname="conkeror"

build() {
  cd $srcdir

 ## Git checkout
  if [ -d $srcdir/${_gitname} ] ; then
    msg "Git checkout:  Updating existing tree"
    cd ${_gitname} && git pull origin
    msg "Git checkout:  Tree has been updated"
  else
    msg "Git checkout:  Retrieving sources"
    git clone ${_gitroot}  
  fi
  msg "Checkout completed"

 ## Build
  rm -rf $srcdir/${_gitname}-build
  cp -r $srcdir/${_gitname} $srcdir/${_gitname}-build
  cd $srcdir/${_gitname}-build

  make 
}
package() { 
  cd $srcdir/${_gitname}-build
  install -d $pkgdir/usr/share/conkeror
  install -d $pkgdir/usr/bin 
  install -d $pkgdir/usr/share/man/man1
  cp -a  $srcdir/${_gitname}-build/{branding,chrome,components,content,contrib,defaults,locale,modules,search-engines,COPYING,CREDITS,application.ini,help,style,tests,conkeror-spawn-helper} \
    $pkgdir/usr/share/conkeror 
  ln -s /usr/share/conkeror/contrib/run-conkeror \
    $pkgdir/usr/bin/conkeror 
  install -Dm644 $pkgdir/usr/share/conkeror/contrib/man/conkeror.1 \
    $pkgdir/usr/share/man/man1/conkeror.1
  rm -r $pkgdir/usr/share/conkeror/contrib/man \
    $pkgdir/usr/share/conkeror/contrib/debian/*.sh
  install -Dm644 $srcdir/conkeror.desktop \
    $pkgdir/usr/share/applications/conkeror.desktop
} 
