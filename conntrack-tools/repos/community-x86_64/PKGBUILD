# $Id$
# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Valere Monseur <valere_monseur@hotmail.com>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=conntrack-tools
pkgver=1.0.0
pkgrel=3
pkgdesc='Userspace tools to interact with the Netfilter connection tracking system'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/conntrack-tools'
license=('GPL2')
depends=('libnetfilter_conntrack')
backup=('etc/conntrackd/conntrackd.conf')
source=("http://www.netfilter.org/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.bz2"
        'conntrackd.rc'
        'conntrackd.conf')
md5sums=('5add24d4761baf17af630d5627a71752'
         '619ab1e15837436b47cbdc739d69ee78'
         '92a7fc6ab5cba5e48cde4cac0ac403b1')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  # initscripts scripts
  install -D -m 755 conntrackd.rc "$pkgdir/etc/rc.d/conntrackd"
  install -D -m 644 conntrackd.conf "$pkgdir/etc/conf.d/conntrackd"

  # install empty configuration files
  install -D -m 644 /dev/null "${pkgdir}/etc/conntrackd/conntrackd.conf"

  # install binaries
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 ft=sh et:
