# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=contacts
pkgver=0.12
pkgrel=6
pkgdesc="Small, lightweight GTK+ address book app"
arch=('i686' 'x86_64')
url="http://pimlico-project.org/contacts.html"
license=('GPL')
depends=('gtk2' 'evolution-data-server' 'desktop-file-utils' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2
        0001-Fix-Makefile-whitespace.patch)
sha256sums=('0993f34819b92e09ab64e8a90231afc3178a19a4a99cf02c3050f51a44850782'
            'f6e3879c1aacbb62e481d9c1c33679ae12f986a26a96b112ec4d7f5f3358efbf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/0001-Fix-Makefile-whitespace.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install --disable-gnome-vfs \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
