# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.31.0
_image_pkgver=5.9.0
_podman_pkgver=2.2.0
_skopeo_pkgver=1.2.0
_storage_pkgver=1.24.3
pkgrel=2
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('073c689bf4e7e96ddd78ff9c66cd274a2d787ed0d07a2bb55f23bb568b6eb807f1e321bfaecb16db3eeceb475ad87372ae69eef5ab8418301017e2af2cbf866b'
            '88f34992d3467aacc411f53394a18a197aeb4277af2d5d6b8fede730bbd9be04bb2b1e2b0dfdff1cbcd12c3618a17a5c873997f7a8105fc0793896ba02a340b0'
            '8c8a8ec73c8d454450d83f1f0429b3e09e2a6a331c6ee2ce1733ebc30850a95a265669fbebf02b15508c4cbf036a03f849dcf1918875424f139f2706ee0c1b8d'
            '630f4209eb6e45c7e99731f98e32705cc7e8f476a5b288f74d7596dd449e0e30e7cebb64c1ecd2562fb1516d91b8ce0e8a41a479a7b1e337d16f8b1fa5b29f9e'
            '3f497adbb1767d72cf299b2462298f972e5d360cbfd8933cf2cf92d0913801a68dabb4df64349115672f077d883cfd84e98138c4d70336179b6db9fb5ca6211b'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('e4453bb8c9e108f5df527d9815cdc1d0826cfc670d9d16eaf0b04f9bf3b0c172bfa971fad92dbbc93881ca221b586cb717ff4ee6f2b3e9c37045d7b93775d3d4'
        '4e058d6ac1f0687d8bd2c2875b81e302de1d64c6af7bf164fd4e52ff212842c38ade237c31ea16627010a9d5d1553aebd01a49f29d1b569b6c56817c7ef5edc0'
        'a445f8325e312da62dc327d13f5aaa4b289da16c72f038d5fca105d86fa88d04dcd19d0962b0607abd1984abfdd51d8f947cd7999648d37156cd82c848ac4ca8'
        '0c5c0b5e35e278913efef316835c105f49fc0552a1a21159591cc11ec201a306d392c2e8af81c2636b86c7cc52b60f986658516034836128f17265b0f0f2edc6'
        '62fc3bd375630bb2e69c2175dfb32fc18c3522bc1285b6fcef91d85f8b2c677a1c07bba2e264f3c12f750c2aeeef598f7bbabb05c69f7418de27aa830bd05b03'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
