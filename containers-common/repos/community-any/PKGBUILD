# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.43.0
_image_pkgver=5.15.0
_podman_pkgver=3.2.3
_skopeo_pkgver=1.4.0
_storage_pkgver=1.34.0
pkgrel=1
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('487c39643551cd45dc71d81577632bf5ed7f72d58044681c9eaa847f448c8d4cd6d7cdd94f7ccb3ce381d6a07864a0a0dec2dffee83fc54a5410365be3fd9e37'
            '65acadc3863e2d21b56c7a60d12e57a78281c75af2bfe69cce861a28b18d4cfff06d4c637c196622e5863438afe05df883cd4c3c3233633c359952b2188c1058'
            'e9df45b0f6afbcdce1f5ef0b2fa7dfc0f374699d0a080b4989c47bbff54d2e5a85d6eb899e17726948e8d233393454b4441d83c2be5a8068fa75c40e1dc1c2bf'
            '2a21ed33ef39c700fc8247876d33ee7592129d8454eac09ee0a896d7c7c59ee02dd714c38bf4cab03c51b5060ea638ee0da6a2e6ed37f1846de296ce4c843076'
            '1a7f62aab5f666db256942bfabc7fa6bcb608328ad0240141ff0c7f9894b921ac0db6476de7f97bc801c72e9dac4b1b24224525d1febe04c3559def0a0f9fa02'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('dd4eb57d032eeb4eab17be5e195f3676949b40e44f4aee3e8404377540e062a60614a535aabafbc64c938239bd407f99714698e100ce224fef9ef085c6867d0d'
        '9b0449802b079bd7a0c0b98e34125b1d91e8ec5f6d49ed771ab03df62f151c5d3e72f9f805ad33bc7a799a7b04fa8101ed5a94c86dd7dc3cd638864df5bea1d9'
        '544a2053618c1ff2f77604fdbb2291d45701b8a02b2aef50b67d34cde8bfd366fb4c283c8f918e5d70a0548cb9b7d80e554fb148313cda8dca5bc3081fb3c765'
        '2150742ebf146d42b841e8e3f1b28e0c6875b10214fd323ce6915f6b776dca85e8cbf1fcd8477dd600cf4104fba2b2aca8358bbb091e3c9ff3a3e89ddbdc6cb7'
        '1582c7510ef3b91ca4b36377f883031af715f7cb08fb047bbfcaa2b53b8e72e2450dd2d6be8ad4d4e2c1f42c9b099d0d42523a11cfe8df20252a67c9a5e29d29'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
