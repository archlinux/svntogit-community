# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=contractor
pkgver=0.3.5
pkgrel=2
pkgdesc=' A desktop-wide extension service'
arch=('x86_64')
url='https://github.com/elementary/contractor'
license=('GPL3')
groups=('pantheon')
depends=('glib2' 'libgee')
makedepends=('git' 'meson' 'vala')
_tag=8d7ae1940930a4b4777599bbac3954e67b45caa0
source=(git+https://github.com/elementary/contractor.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd contractor
  git describe --tags
}

build() {
  arch-meson contractor build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
