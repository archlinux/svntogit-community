# Maintainer: David Runge <dave@sleepmap.de>
_name=convoLV2
pkgname=convolv2
pkgver=0.6.0
pkgrel=2
pkgdesc="An LV2 plugin to convolve audio signals with zero latency."
arch=('x86_64')
url="https://x42-plugins.com/x42/x42-convolver"
license=('GPL')
groups=('lv2-plugins' 'pro-audio')
depends=('libsamplerate' 'zita-convolver')
makedepends=('lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('60fab90a31c845ac64c9fb84bd5ee84f8b5743d15eb40cfc2c66d7a50dd4f5ae0bbeb7e0f3b630fdda8b7f13d6fecc26fa25995ac009d53d6aa600e58eb40f5d')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
