# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>

pkgname=coq
pkgver=8.3pl2
pkgrel=3
pkgdesc='Formal proof management system.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
depends=('gtk2')
makedepends=('ocaml' 'lablgtk2' 'camlp5-transitional' 'netpbm' 'hevea')
source=("http://coq.inria.fr/distrib/V${pkgver}/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('db415f6c5372f5a443699c62f5affcb4')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -opt \
    -with-doc yes

  make world

  make doc-html
}

package() {
  cd ${pkgname}-${pkgver}

  make COQINSTALLPREFIX=${pkgdir} install

  make COQINSTALLPREFIX=${pkgdir} install-doc-html
}
