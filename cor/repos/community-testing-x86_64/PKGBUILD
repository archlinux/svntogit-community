# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cor
pkgver=0.1.17
pkgrel=2
pkgdesc="Just another C++/C library"
arch=('i686' 'x86_64')
url="https://git.merproject.org/mer-core/cor"
license=('LGPL')
depends=('systemd')
makedepends=('git' 'cmake' 'tut')
source=("git+https://git.merproject.org/mer-core/cor.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  mkdir build
}

build() {
  cd build

  cmake -DVERSION=$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ../cor
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
