# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cordova
pkgver=8.0.0
pkgrel=1
pkgdesc='Cordova command line interface tool'
arch=('any')
url='http://cordova.apache.org'
license=('Apache')
depends=('npm' 'acorn' 'semver' 'browserify')
install=cordova.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('907a94328f4decdad7cfcccb169f7e32c435cab5e742ead0fe6182e903c3dd321560fc4223fa7bc60c0a56067d0161ba7f7d85dd9874afbbaca919ad94678f89')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver browserify; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/syntax-error/node_modules
  for dep in acorn; do
    rm -r $dep;
  done
}
