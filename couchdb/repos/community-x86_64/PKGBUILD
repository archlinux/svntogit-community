# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=couchdb
pkgver=1.6.1
pkgrel=6
pkgdesc="A document-oriented database that can be queried and indexed in a MapReduce fashion using JSON"
arch=('i686' 'x86_64')
url="http://couchdb.apache.org"
license=('APACHE')
depends=('icu' 'erlang-nox' 'js185' 'openssl' 'curl' 'util-linux')
makedepends=('autoconf-archive' 'git')
install=couchdb.install
backup=('etc/couchdb/local.ini'
	'etc/conf.d/couchdb'
	'etc/logrotate.d/couchdb')
validpgpkeys=('E0AF0A194D55C84E4A19A801CDB0C0F904F4EE9B')
#source=("git://github.com/apache/couchdb.git#commit=8d7ab8b18dd20f8785e69f4420c6f93a2edbfa60"
source=("http://www.eu.apache.org/dist/couchdb/source/$pkgver/apache-couchdb-$pkgver.tar.gz"{,.asc}
        "erlang18.patch"
	"couchdb.service"
	"couchdb.tmpfiles")
md5sums=('01a2c8ab4fcde457529428993901a060'
         'SKIP'
         '8488bf9f7b07b40a39ff5448e3509905'
         '8914a7ffc6745f94106d96206709acb4'
         '1e254ebe32eeb061be64193bafa35dbf')

prepare() {
  cd "$srcdir/apache-couchdb-$pkgver"
  sed -i 's|-Werror||g' src/couchdb/priv/Makefile.*
  patch -Np1 -i ../erlang18.patch
  autoreconf -vi
}

build() {
  cd "$srcdir/apache-couchdb-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "$srcdir/apache-couchdb-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 etc/default/couchdb $pkgdir/etc/conf.d/couchdb
  sed -i 's|\(COUCHDB_OPTIONS=\)|\1"-p /var/run/couchdb/couchdb.pid"|' $pkgdir/etc/conf.d/couchdb

  rm -rf $pkgdir/etc/default/ $pkgdir/var/run $pkgdir/etc/init.d

  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  install -Dm0644 $srcdir/$pkgname.tmpfiles $pkgdir/usr/lib/tmpfiles.d/$pkgname.conf
}
