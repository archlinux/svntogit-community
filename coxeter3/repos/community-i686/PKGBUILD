# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coxeter3
pkgver=3.0beta3
_pkgver=3.0_beta3
pkgrel=3
pkgdesc="A computer program for the study of combinatorial aspects of Coxeter group theory"
arch=('i686' 'x86_64')
url="http://math.univ-lyon1.fr/~ducloux/coxeter/coxeter3/english/coxeter3_e.html"
license=('GPL3')
depends=('gcc-libs')
source=("http://math.univ-lyon1.fr/~ducloux/coxeter/coxeter3/english/coxeter_$_pkgver.tgz"
 'makefile.patch' 'sage.cpp' 'sage.h' 'gcc5.patch')
md5sums=('5e0ac6f2fa179d94fcf33f18ef282dde'
         'f757ee88e333a5bf850af1d810d7cb7b'
         '79756f28b67b1f678e3ac5dd8d67fb49'
         '57e8806f9fc6bb4604016ee727210993'
         'd93367abeb8a51b364c96ff669cd5158')

prepare() {
  cd coxeter_$_pkgver

  cp ../sage.* .
  patch -p1 -i "$srcdir"/makefile.patch

# fix build with GCC 5
  patch -p0 -i "$srcdir"/gcc5.patch
}  

build() {
  cd coxeter_$_pkgver

  make all
}

package() {
  cd coxeter_$_pkgver
  
  mkdir -p "$pkgdir"/usr/{bin,lib}
  make install INSTALL_DIR="$pkgdir"/usr
}

