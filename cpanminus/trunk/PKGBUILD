# Maintainer: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=cpanminus
pkgver=1.5001
pkgrel=2
pkgdesc="Get, unpack, build and install modules from CPAN."
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-extutils-install>=1.46' 'perl-extutils-makemaker>=6.31'
         'perl-module-build>=0.36')
provides=("perl-app-cpanminus=${pkgver}")
url=https://metacpan.org/release/App-cpanminus
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/App-cpanminus-${pkgver}.tar.gz")
md5sums=(fa9c7f7deac052f082bc0ed2474d589f)
sha512sums=(1ced9f8cd1261a714b4992809d64c6ef10bd57a15c1b065764cbcc60158c7240aa61181ba90029c11e0ec0c380e3075136fe7c292419e32b9612f86e0d761b72)
_distdir="${srcdir}/App-cpanminus-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
