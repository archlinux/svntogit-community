# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=cppcheck
pkgver=1.65
pkgrel=1
pkgdesc="A tool for static C/C++ code analysis"
arch=('i686' 'x86_64')
url="http://cppcheck.wiki.sourceforge.net/"
license=('GPL')
depends=('qt5-base')
makedepends=('docbook-xsl' 'qt5-tools')
source=(https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz)
sha1sums=('df1ebc45defb24c7f21af64bbf9515cedb5f2d8e')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make CFGDIR=/usr/share/cppcheck/cfg
   make DB2MAN=/usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl man

   cd gui
   qmake HAVE_RULES=yes CFGDIR=/usr/share/cppcheck/cfg
   make CFGDIR=/usr/share/cppcheck/cfg
}

check() {
  cd "$srcdir"/cppcheck
  make CFGDIR=/usr/share/cppcheck/cfg test
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR=${pkgdir} CFGDIR=/usr/share/cppcheck/cfg install 

   install -D -p -m 644 cppcheck.1 "${pkgdir}"/usr/share/man/man1/cppcheck.1

   install -m755 gui/cppcheck-gui "${pkgdir}"/usr/bin

   for _f in cfg/*; do
     install -D -p -m 644 $_f "${pkgdir}"/usr/share/"${pkgname}"/$_f
   done
}
