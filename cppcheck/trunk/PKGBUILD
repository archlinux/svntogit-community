# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=cppcheck
pkgver=1.66
pkgrel=1
pkgdesc="A tool for static C/C++ code analysis"
arch=('i686' 'x86_64')
url="http://cppcheck.wiki.sourceforge.net/"
license=('GPL')
depends=('qt5-base' 'python-pygments')
makedepends=('docbook-xsl' 'qt5-tools')
source=(https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz)
sha1sums=('277a214aa8a2bf30180645aca09c1dc9d3069977')

build() {
   cd "${pkgname}-${pkgver}"
   make CFGDIR=/usr/share/cppcheck/cfg
   make DB2MAN=/usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl man

   cd gui
   qmake HAVE_RULES=yes CFGDIR=/usr/share/cppcheck/cfg
   make CFGDIR=/usr/share/cppcheck/cfg
}

check() {
   cd "${pkgname}-${pkgver}"
  make CFGDIR=./cfg  HAVE_RULES=yestest
}

package() {
   cd "${pkgname}-${pkgver}"
   make DESTDIR=${pkgdir} install 

   install -D -p -m 644 cppcheck.1 "${pkgdir}"/usr/share/man/man1/cppcheck.1

   install -m755 gui/cppcheck-gui "${pkgdir}"/usr/bin

   for _f in cfg/*; do
     install -D -p -m 644 $_f "${pkgdir}"/usr/share/"${pkgname}"/$_f
   done
}
