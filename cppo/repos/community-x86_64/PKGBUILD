# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=cppo
pkgver=1.6.5
pkgrel=2
pkgdesc="C-style preprocessor for OCaml"
arch=('x86_64')
url="https://github.com/mjambon/cppo"
license=('BSD')
depends=('glibc')
makedepends=('ocamlbuild' 'dune')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('0ebfe3261037a14c9f53b1deb2772af45fda2317b3b17746f2a640034b1b75b6')

build() {
    cd ${pkgname}-${pkgver}
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    DESTDIR="${pkgdir}" dune install --prefix=/usr --libdir="$(ocamlfind printconf destdir)"
    install -Dm644 LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
    rm -r "${pkgdir}"/usr/doc
}
