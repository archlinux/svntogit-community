# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=cpulimit
pkgver=20160304
_commit=f4d2682804931e7aea02a869137344bb5452a3cd
pkgrel=1
pkgdesc="Limit cpu usage in %. Actualy sends SIGSTOP/SIGCONT"
arch=('x86_64')
url="https://github.com/opsengine/cpulimit"
license=("GPL")
depends=()
makedepends=('git')
source=("$pkgname-$pkgver::git://github.com/opsengine/cpulimit.git#commit=${_commit}")
md5sums=('SKIP')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i 's/#ifdef __APPLE__ || __FREEBSD__/#if defined __APPLE__ || defined __FREEBSD__/g' \
    src/cpulimit.c tests/process_iterator_test.c
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  install -Dm0755 src/cpulimit "$pkgdir"/usr/bin/cpulimit
  install -Dm0644 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
}
