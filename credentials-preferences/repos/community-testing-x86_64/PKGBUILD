# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=credentials-preferences
_pkgname=credentials-control-center
pkgver=0.1.1
pkgrel=1
pkgdesc="Control panel for configuring online account credentials and settings"
arch=('i686' 'x86_64')
url="https://launchpad.net/online-accounts-gnome-control-center"
license=('GPL' 'LGPL')
depends=('libaccounts-glib' 'libsignon-glib' 'gtk3')
makedepends=('gobject-introspection' 'vala' 'intltool' 'itstool')
options=('!libtool')
install=$pkgname.install
source=(https://launchpad.net/online-accounts-gnome-control-center/13.04/$pkgver/+download/$_pkgname-$pkgver.tar.bz2
        http://pkgbuild.com/~bgyorgy/sources/$_pkgname-translations-20121018.tar.gz)
md5sums=('4e42fb2350393c6f8661e38f07d0b765'
         '702d6b39136e10ba6dfad0afd300b2af')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Install language files
  echo 'af ar ast bn bs ca cy cs da de el en_AU en_GB eo es eu fa fi fr gd gl gu he hu it ja ko lt lv ms my nb nl oc pl pt pt_BR ro ru se sk sl sq sv tr ug uk vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $_pkgname- '' ../po/$_pkgname-*.po
  mv -f -t po ../po/*

  # Fix desktop file
  sed -i 's/X-GNOME-Settings-Panel;//' data/credentials-preferences.desktop.in.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
