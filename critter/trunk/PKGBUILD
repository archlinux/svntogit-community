# $Id$
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Maintainer: Daniel J Griffiths <griffithsdj@archlinux.us>

pkgname=critter
_origname=CriticalMass
pkgver=1.0.2
pkgrel=5
pkgdesc="Critical Mass (aka Critter) is an SDL/OpenGL space shoot'em up game"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/criticalmass"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'mesa')
source=(http://downloads.sourceforge.net/sourceforge/criticalmass/$_origname-${pkgver}.tar.bz2
        critter-gcc43.patch
        critter.desktop)
md5sums=('e2aff114bffa717fb79c82e1dc473ebe'
         'e936920acce56bfa3b0123ca8b1193a6'
         '98c17809aed964c445adad09827035df')

build() {
  cd ${srcdir}/$_origname-${pkgver}

  patch -Np1 -i ../critter-gcc43.patch
  sed -i 's|-lpng12|-lpng|g' configure

  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd ${srcdir}/$_origname-${pkgver}

  make DESTDIR=${pkgdir} install

  install -Dm644 critter.png ${pkgdir}/usr/share/pixmaps/critter.png
  install -Dm644 ../critter.desktop \
    ${pkgdir}/usr/share/applications/critter.desktop
}
