# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Pavel Borzenkov <pavel@voidptr.ru>
# Contributor: aksr <aksr at t-com dot me>

pkgname=criu
pkgver=3.16.1
pkgrel=3
pkgdesc="Utilities to checkpoint and restore processes in userspace"
url="http://criu.org"
license=('GPL2')
arch=('x86_64')
source=("$pkgname-$pkgver.tar.gz::https://github.com/checkpoint-restore/criu/archive/v$pkgver.tar.gz")
depends=('libbsd' 'libnet' 'libnl' 'protobuf-c' 'python-protobuf' 'gnutls' 'nftables')
makedepends=('xmlto' 'asciidoc')
options=("!buildflags")
sha512sums=('a558af41f1927a1b1d87f8d11163a5f3d2e4f83a74aba00619b45a6d464d5477a6a0aac3c1b9d9a17e3343a4bf21a3fd6b279b51fa0c347c86c307381a4d0739')
b2sums=('45aa87ee00a18b91474e767dc0326cce6c0942af7771a0c5536751361c171c86ba2065c54587e82b51fd6d7ab116e659324369955086c5237cd7fd26b60d6ce6')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" \
    PREFIX=/usr \
    SBINDIR=/usr/bin \
    LIBDIR=/usr/lib \
    LIBEXECDIR=/usr/lib \
    install

  # remove empty directory
  rmdir "$pkgdir/usr/include/compel/common/asm"
}
