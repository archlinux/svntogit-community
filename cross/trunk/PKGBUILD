# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cross
pkgver=0.2.2
pkgrel=1
pkgdesc="'Zero setup' cross compilation and 'cross testing' of Rust crates"
url="https://github.com/rust-embedded/cross"
arch=('x86_64')
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
optdepends=('docker: provide container'
            'podman: provide container')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-embedded/cross/archive/v${pkgver}.tar.gz")
sha256sums=('a338090e5b2004ae5ea0620d57b8f46446da7ae62782c1a4173bcc696ef774f0')
b2sums=('b0e4d1edfef1b6949cc43fecb2625be92ae54ec982a3d583c25e676f68d6dfa48a6e46780129c6923fb92325563767c603a7d0056f170447ed07b052c9aa7778')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  depends+=('rustup')
  cd ${pkgname}-${pkgver}
  install -Dm755 "target/release/cross" -t "${pkgdir}/usr/bin"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    LICENSE-APACHE LICENSE-MIT
}

# vim: ts=2 sw=2 et:
