# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cross
pkgver=0.2.4
pkgrel=1
pkgdesc="'Zero setup' cross compilation and 'cross testing' of Rust crates"
url="https://github.com/rust-embedded/cross"
arch=('x86_64')
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
optdepends=('docker: provide container'
            'podman: provide container')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-embedded/cross/archive/v${pkgver}.tar.gz")
sha256sums=('ad07c95b2e873f260661478670fb7ebc84ae9b8227ee509b905c28f6ed418477')
b2sums=('acb2dd5db104b4d02c2f98ec64761c936a0f873429220da6d60092e03e68fcd4a9ec528de7d4755b73269f71786577a49663dfb6a3bfe395f8d79d9bacc721aa')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  depends+=('rustup')
  cd ${pkgname}-${pkgver}
  install -Dm755 "target/release/cross" -t "${pkgdir}/usr/bin"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" \
    LICENSE-APACHE LICENSE-MIT
}

# vim: ts=2 sw=2 et:
