# Maintainer:
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: kaylyn
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cryfs
pkgver=0.9.7
pkgrel=7
pkgdesc="Cryptographic filesystem for the cloud"
arch=(x86_64)
depends=(crypto++ fuse2 curl)
makedepends=(cmake python boost)
url="https://www.cryfs.org"
license=(LGPL3)
source=($pkgname-$pkgver.tar.gz::https://codeload.github.com/cryfs/$pkgname/tar.gz/$pkgver
        spdlog.diff::https://github.com/cryfs/cryfs/commit/f1c6fa044f44e33c0c9e6eab78877d47ac4c87be.diff)
sha256sums=('305807828ec498fca68bf60c61707e17824b3ef7425de897b042d50311becfa9'
            '13c93eae25e969db0bbe2182b509bc4890e38c847d97818711fbb40e7c9e8ff9')

prepare() {
  mkdir -p build
  cd $pkgname-$pkgver

# Update spdlog to 0.12.0
  patch -p1 -i ../spdlog.diff
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCRYFS_UPDATE_CHECKS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/$pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 "$srcdir"/$pkgname-$pkgver/README.md -t "$pkgdir"/usr/share/doc/$pkgname
}
