# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=5.6.5
_srcver=${pkgver//./}
pkgrel=3
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('i686' 'x86_64')
url="http://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
source=("https://www.cryptopp.com/cryptopp${_srcver}.zip"
        "fix-cxxflags.patch"
        'libcrypto++.pc')
# Checksums from https://www.cryptopp.com/release565.html
sha1sums=('a74f207a315b6a133af9640a45bde9d6e378af4d'
          'f044b55c615ed0d6c0239bd43dda67db1fdb4cca'
          'e4e2fc3172b4a85bffad812a0de8531843912ff1')
sha256sums=('a75ef486fe3128008bbb201efee3dcdcffbe791120952910883b26337ec32c34'
            'e27679cd5e1068e84c84c18395d4f32254fa900887ee89a15c38580236490dc5'
            'aee5ca989bfd25236095fccb245a5c7f559eae97c41979e952ac35b29453d8b5')
sha512sums=('f13718d02ca69b0129aaf9e767c9d2e0333aa7538355f9c63d9eaf1ff369062084a18dc01489439ebf37797b3ea81b01beb072057d47ec962bfb824ddc72abc7'
            '49a81998489e65dcb270de77f6149a3dcf03c9a04bd2c9baf2e92f6bd749fe69aca5de0803b94757778f272357d2357e873b652c9bb06e8f03b7bf9b55bbc9d4'
            '798ff42e3dcc4edd33b9fb1b605e608dd53a668afb28032620927b49f17fcb5b212f6cf4e9af30cc2b36bf74bb9f0152dad4f40fdb19d8f557dfed196ea0921d')

prepare() {
  # Upstream patch for https://bugs.archlinux.org/task/51605
  patch -p1 < "$srcdir"/fix-cxxflags.patch
}

build() {
  CXXFLAGS+=" -DNDEBUG -fPIC" make -f GNUmakefile-cross  
  make libcryptopp.so
}

package() {
  install -d ${pkgdir}/usr/{lib/pkgconfig,include/cryptopp}
  install -m644 *.h ${pkgdir}/usr/include/cryptopp/
  install -m644 libcryptopp.so "${pkgdir}/usr/lib/libcryptopp.so"
  install -m644 ${srcdir}/libcrypto++.pc "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
