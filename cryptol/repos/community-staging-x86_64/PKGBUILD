# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cryptol
pkgver=2.8.0
pkgrel=54
pkgdesc="The Language of Cryptography"
url="https://www.cryptol.net"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'z3' 'haskell-base-compat' 'haskell-cryptohash-sha1' 'haskell-gitrev'
         'haskell-graphscc' 'haskell-heredoc' 'haskell-monad-control' 'haskell-monadlib'
         'haskell-panic' 'haskell-random' 'haskell-sbv' 'haskell-simple-smt' 'haskell-strict'
         'haskell-tf-random' 'haskell-transformers-base' 'haskell-ansi-terminal'
         'haskell-blaze-html')
makedepends=('ghc' 'alex' 'happy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GaloisInc/cryptol/archive/$pkgver.tar.gz"
        ghc-8.8.patch::https://github.com/GaloisInc/cryptol/pull/655.patch)
sha512sums=('ad2cbb67dbcd10dba12457b24558af4ef7066817f18e162432607e7d887071fef13a1c958e37a3f7e8e3fd04079c20cc2a8abdcf1c9dd35a9a93e7e0a2d383b0'
            'f5399cfde04c2645e0bc80d80a39ac570c9649961e22bf9808bb55ba8457f9a124469d699de73624b2debabe8d672879bd0b2279b60dfcb08d328de67ca78e84')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../ghc-8.8.patch
    sed -i 's/< *0.11/<1/' $pkgname.cabal
    #sed -i 's/import Prelude.Compat/import Prelude.Compat hiding ( fail )/' src/Cryptol/Parser/{NoPat,ParserUtils}.hs
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-static -f-relocatable --ghc-option='-pie'
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
