# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cryptol
pkgver=2.5.0.20180515
_commit=9fccc0086700b0e409bd9b0989e52d62863c714e
pkgrel=32
pkgdesc="The Language of Cryptography"
url="http://www.cryptol.net"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'z3' 'haskell-base-compat' 'haskell-async' 'haskell-gitrev' 'haskell-graphscc'
         'haskell-heredoc' 'haskell-monad-control' 'haskell-monadlib' 'haskell-random' 'haskell-sbv'
         'haskell-simple-smt' 'haskell-strict' 'haskell-tf-random' 'haskell-transformers-base'
         'haskell-ansi-terminal' 'haskell-blaze-html')
makedepends=('ghc' 'alex' 'happy')
source=("$pkgname-$_commit.tar.gz::https://github.com/GaloisInc/cryptol/archive/$_commit.tar.gz")
sha512sums=('d9e98541b978acb8dafc5daf35a3fc75a523914e713b4cdc2e8e01a424b39556c625e2956dd495cabf67cf99cac0b4021563f0b62b2f355b1af0645dc8d9de1b')

prepare() {
    mv $pkgname-$_commit $pkgname-$pkgver

    cd $pkgname-$pkgver
    sed -i 's/<.*0.10/<1/' $pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-static -f-relocatable
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
