# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

# cms2 had a manpage, where did it go?
# need to hack up the pythonpath and get the tests to run

pkgname=cryptominisat5
pkgver=5.6.3
pkgrel=1
pkgdesc="A modern, multi-threaded, feature-rich, simplifying SAT solver"
arch=('x86_64')
#url="https://www.msoos.org/2016/07/cryptominisat-5-0-0-released/"
url="https://github.com/msoos/cryptominisat/"
license=('MIT')
depends=('zlib' 'gcc-libs' 'boost-libs' 'intel-tbb')
makedepends=('python2' 'boost' 'cmake' 'vim')
# vim for xxd
optdepends=('python2: python module')
source=("cms5-$pkgver.tgz::https://github.com/msoos/cryptominisat/archive/$pkgver.tar.gz")
md5sums=('13b9abbbf1ac51921a327e5bfde6d5f1')

# many fancy features requiring makedeps
# intel-tbb, python2, m4ri, libmysqlclient, valgrind

# todo, upstream python3 support

prepare() {
  cd cryptominisat-$pkgver
  sed -i 's/python$/python2/' python/Makefile
  sed -i 's/\(CRYPTOMINISAT4_EXECUTABLE\).*/\1 cryptominisat5\)/' *.cmake.in
}

build() {
  cd cryptominisat-$pkgver
  mkdir -p build
  cd build
  # options to play with:
  # -DUSE_TBB -DUSE_ZLIB -DUSE_MYSQL
  # NOMYSQL NOSTATS NOM4RI ENABLE_TESTING
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DNOM4RI=on -DNOMYSQL=on ../
  make
  cd pycryptosat
  python2 setup.py build
}

check() {
  return
  python2 -c "from pycryptosat import Solver"
  return
  cd "$srcdir/cryptominisat-$pkgver/build/pycryptosat"
  ln -s pycryptosat.so libcryptominisat5.so.5.6
  cd "$srcdir/cryptominisat-$pkgver/python"
  ln -s "$srcdir/cryptominisat-$pkgver/build/pycryptosat/pycryptosat.so" libcryptominisat5.so.5.6
  PYTHONPATH=$PYTHONPATH:$srcdir/cryptominisat-$pkgver/build/pycryptosat python2 test_pycryptosat.py
}

package() {
  cd cryptominisat-$pkgver
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cd build
  make install DESTDIR="$pkgdir"
  # todo, tweak the build tools to respect DESTDIR
  cd pycryptosat
  python2 setup.py install --record files.txt --root="${pkgdir}"
}

