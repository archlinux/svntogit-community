# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=crystal
pkgver=0.19.3
pkgrel=1
_binary_rel=1
pkgdesc='The Crystal Programming Language'
arch=(i686 x86_64)
url='http://crystal-lang.org'
license=(Apache)
depends=(gc libatomic_ops pcre libevent)
makedepends=(libxml2 llvm)
checkdepends=(libyaml libxml2 gmp inetutils git)
optdepends=('shards: crystal language package manager'
            'libyaml: For YAML support'
            'gmp: For BigInt support'
            'libxml2: For XML support')
source=(crystal-$pkgver.tar.gz::https://github.com/crystal-lang/crystal/archive/$pkgver.tar.gz)
source_i686+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-i686.tar.gz)
source_x86_64+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-x86_64.tar.gz)
sha256sums=('72954087131bd648735bc397cfd585204087a4b8ab7f927f0a054741381ea01f')
sha256sums_i686=('583796b46cf6cf569d9292c7b781818a046fea4ad59346d397ad728dc0fad06d')
sha256sums_x86_64=('e4284e4796e543f90e8e7bef8fb94455c5d98d1e29ed8f5519d13f07a962d335')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|Please install shards: https://github.com/ysbaddaden/shards|Please install shards: pacman -S shards|' src/compiler/crystal/command.cr
}

build() {
  cd $pkgname-$pkgver

  make release=1 \
       PATH="$srcdir/$pkgname-$pkgver-$_binary_rel/bin:$PATH" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="libs:/usr/lib/crystal" \
       CRYSTAL_CACHE_DIR="/tmp/crystal"
  make doc CRYSTAL_CACHE_DIR="/tmp/crystal"
}

check() {
  cd $pkgname-$pkgver

  make all_spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver" \
            CRYSTAL_CACHE_DIR="/tmp/crystal"
}

package() {
  cd $pkgname-$pkgver

  # /usr/bin/crystal                compiled executable
  # /usr/lib/crystal/               compiler src & core libs
  # /usr/share/doc/crystal/api      api docs
  # /usr/share/doc/crystal/samples/ samples

  install -Dm755 ".build/crystal" "$pkgdir/usr/bin/crystal"

  install -dm755 "$pkgdir/usr/lib"
  cp -r src "$pkgdir/usr/lib/crystal"

  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -r doc "$pkgdir/usr/share/doc/crystal/api"
  cp -r samples "$pkgdir/usr/share/doc/crystal/"

  install -Dm644 etc/completion.bash "$pkgdir/usr/share/bash-completion/completions/crystal"
  install -Dm644 etc/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_crystal"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
