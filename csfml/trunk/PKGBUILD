# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=csfml
pkgver=2.5
_commit=bff4a69a73da8be79b786c6d249e4a36296a58fe
pkgrel=1
pkgdesc='C bindings for sfml'
arch=('x86_64')
url='http://www.sfml-dev.org/'
license=('zlib')
depends=("sfml")
makedepends=('cmake' 'doxygen')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/LaurentGomila/CSFML/archive/${_commit}.tar.gz")
sha512sums=('5eca2052e901d52eb3a0f3c796f949f63263ef6d55be441c6bd7e153427c997a0f5bb28c0096979515d327d313637dd9537bcb407204b0a583f25515115362bf')

build() {
  cd CSFML-*

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCSFML_BUILD_DOC=true
  make
  make doc
}

package() {
  cd CSFML-*/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

