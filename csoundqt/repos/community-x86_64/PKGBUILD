# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ainola
# Contributor: rtfreedman

_name=CsoundQt
pkgname=csoundqt
pkgver=1.1.0
pkgrel=1
epoch=1
pkgdesc="A frontend for Csound"
arch=(x86_64)
url="https://csoundqt.github.io/"
license=(CCPL FDL GPL LGPL2.1)
groups=(pro-audio)
depends=(csound-doc gcc-libs glibc hicolor-icon-theme  qt5-base qt5-declarative
qt5-webengine)
makedepends=(csound rtmidi)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('c4af0ed9bdd584e7d529d0e0c540920c39bf4f4053db0ec11edb17b7ec99f2b21d2f994563d5a37343687c5f88dcccb37b61e34ad022c1bacf9f35f59c207468')
b2sums=('8db5051c3117240c041247f567183d55a0c78c82f9d5f3f0d08c991c484fe4e8273109ce697a426e7a82e797bd293659f61a17ecf7719e59b38dfbefadf89e99')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  qmake-qt5 qcs.pro \
    SHARE_DIR="${pkgdir}/usr/share" \
    INSTALL_DIR="${pkgdir}/usr" \
    CONFIG+=rtmidi \
    CONFIG+=html_webengine \
    CONFIG+=record_support
  make
}

package() {
  depends+=(libcsnd6.so libcsound64.so librtmidi.so)

  make DESTDIR="${pkgdir}" install -C "${pkgname}-${pkgver}"
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
