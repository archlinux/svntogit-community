# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=ct
pkgver=0.9.2
pkgrel=1
pkgdesc='config transpiler for Flatcar Container Linux'
arch=('x86_64' 'aarch64')
url='https://github.com/flatcar-linux/container-linux-config-transpiler'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flatcar-linux/container-linux-config-transpiler/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('8c780d47cbd336a68dc1d0734478d192ca7eadb099361e80da64277d00565d5b9a882ab599b12f0379b6daa3f6eb5a08142224c1a4dc91fd01bce576bf29bea6')
b2sums=('5a1e3fac395533f2854a82ddcd356e66358cf86be69ca4ca0b14056067e05d8d7fc65b75c0332dbb14fc01fecfbbed8f557e1be9c0d99be0548c1d92ab8c5bad')

build() {
  cd "container-linux-config-transpiler-${pkgver}/internal"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external -X github.com/flatcar-linux/container-linux-config-transpiler/internal/version.Raw=${pkgver}" -o ct
}

check() {
  cd "container-linux-config-transpiler-${pkgver}"
  go test ./...
}

package() {
  cd "container-linux-config-transpiler-${pkgver}/internal"
  install -Dm755 ct "${pkgdir}/usr/bin/ct"
}
