# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=ctemplate
pkgver=2.3
pkgrel=3
pkgdesc='A library implementing a simple but powerful template language for C++'
arch=('i686' 'x86_64')
url='http://code.google.com/p/ctemplate/'
license=('BSD')
makedepends=('subversion' 'python2')
depends=('gcc-libs' 'perl')
source=("svn+http://${pkgname}.googlecode.com/svn/tags/${pkgname}-${pkgver}/")
sha256sums=('SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# use python2
	sed -i '1s/python/&2/' \
		src/htmlparser/fsm_config.py \
		src/htmlparser/generate_fsm.py
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
