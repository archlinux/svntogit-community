# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=ctop
pkgver=0.7.5
pkgrel=3
pkgdesc='Top-like interface for container metrics'
arch=('x86_64')
url='https://ctop.sh/'
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("https://github.com/bcicen/ctop/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('ecaba01815d4c25076ae62eb8880824df70647f567d3230d31dbe33b4901969a2e7723341250d6d02239266e0be5dcbbaed2191fa8ff8cfcbe2bf4a795133359')


build() {
  cd ${pkgname}-${pkgver}
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build .
}

package() {
  install -Dm755 ${pkgname}-${pkgver}/${pkgname} "${pkgdir}"/usr/bin/${pkgname}
  install -Dm644 ${pkgname}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

