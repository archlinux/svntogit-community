# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=7.6.5.32
_cudaver=10.1
pkgrel=1
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>=10')
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/cuda/tree/centos7 for the appropriate cuda version.
source=("https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('ef6e96902bdeaf99615f4d5b9e93c3d5627dac239f465a17c58175964131d5c56ff3469000ce40e7f6412d083a075ede0d9cdb447761df6980c88a61bfe571ce'
            'eef97e540946907f3b6f3c4e600b39e4c8e7578bb41210442a0e6a7d43c5f2002e192690abd311c4088af57b28247f305b1906ee8754780ad562c1c31c045b1d'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
           "libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb")

package() {
  mkdir -p "${pkgdir}"/usr/{lib,include}

  mkdir libcudnn7-dev
  cd libcudnn7-dev
  ar xv "${srcdir}"/libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/include/x86_64-linux-gnu/* "${pkgdir}"/usr/include/
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  mkdir libcudnn7
  cd libcudnn7
  ar xv "${srcdir}"/libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  # Get rid of some Ubuntu-isms but at the same time still allow for them to
  # work because nobody is ever quite sure what the right names are.
  ln -s cudnn_v7.h "${pkgdir}"/usr/include/cudnn.h
  ln -s libcudnn.so.7 "${pkgdir}"/usr/lib/libcudnn.so
  ln -s libcudnn_static_v7.a "${pkgdir}"/usr/lib/libcudnn_static.a

  install -Dm644 \
      "${srcdir}"/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      "${pkgdir}"/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf
}

# vim: ft=sh syn=sh et
