# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=8.5.0.96
_pkgver=8.5.0
_cudaver=11.7
_majorver=8
_archive="cudnn-linux-x86_64-${pkgver}_cuda11-archive.tar.xz"
pkgrel=1
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>='"${_cudaver}")
options=(!strip staticlibs)
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/container-images/cuda/-/tree/master/dist for the appropriate cuda version
# or make an NVIDIA Developer account.
# Alternatively, check https://github.com/pytorch/builder/blob/master/common/install_cuda.sh
# or https://developer.download.nvidia.com/compute/cuda/repos/ubuntu2004/x86_64/
source=("https://developer.download.nvidia.com/compute/redist/cudnn/v${_pkgver}/local_installers/${_cudaver}/${_archive}"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('b206b9871b0d877005bd0ee91d8f661776c493e8464e3ce9ebf605e49a4bc759b00eccf078b31519fb8697a3f4a2082731cb88c5f5a6c3649c05f562b271a1cb'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("${_archive}")

package() {
  mkdir -p "${pkgdir}"/usr/share/licenses/
  tar xf "${_archive}" --strip-components 1 -C "${pkgdir}/usr"

  find "${pkgdir}/usr/lib" -type f \( -iname "*.a" -or -iname "*.a.*" \) -print0 | while read -rd $'\0' _so_file; do
    # make sure static libraries have proper permissions
    chmod 644 "${_so_file}"
  done

  install -Dm644 \
      "${srcdir}"/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      "${pkgdir}"/usr/share/licenses/"${pkgname}"/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf

  mv "${pkgdir}"/usr/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

  # fix permissions
  chown -R root:root "${pkgdir}"/*
}

# vim: ts=2 sw=2 ft=sh syn=sh et
