# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=cuetools
pkgver=1.4.0
pkgrel=1
pkgdesc="Cue and toc file parsers and utilities"
arch=('i686' 'x86_64')
url="https://github.com/svend/cuetools"
#url="http://developer.berlios.de/projects/cuetools/"
license=('GPL')
depends=('glibc')
source=("https://github.com/svend/cuetools/archive/${pkgver}.tar.gz"
	"fix_build_with_automake-1.12.diff")
md5sums=('4492dae2b3f9e077f6455a1f1cddef3b'
         '67a3b32711d1a1f508be6ed0f1bdadf3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 < "$srcdir/fix_build_with_automake-1.12.diff" || true

  aclocal
  autoheader
  automake --force-missing --add-missing
  autoconf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
