# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Jianfeng Zhang <swordfeng123@gmail.com>

pkgname=curlie
pkgver=1.5.4
pkgrel=2
pkgdesc='The power of curl, the ease of use of httpie.'
arch=('x86_64')
url="https://curlie.io"
license=('MIT')
depends=('glibc' 'curl')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rs/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('6a8ea001733d337214fbea3031efb651b96838d1db7a0d6b0ce9aa8bb8138c1e')

build() {
    cd "${pkgname}-${pkgver}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 -t "${pkgdir}/usr/bin/" "${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
