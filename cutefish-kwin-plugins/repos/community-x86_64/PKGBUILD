# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cutefish-kwin-plugins
pkgver=0.5
pkgrel=1
pkgdesc="CutefishOS KWin Plugins"
arch=('x86_64')
url="https://github.com/cutefishos/kwin-plugins"
license=('GPL')
groups=('cutefish')
conflicts=('deepin-kwin')
depends=('kconfig' 'kdecoration' 'kguiaddons' 'kcoreaddons' 'kconfigwidgets' 'kwindowsystem'
         'kwayland' 'kwin'
         # qml:
         'fishui' 'kdeclarative' 'plasma-framework')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools')
source=("https://github.com/cutefishos/kwin-plugins/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-kwin-5.23.patch::https://github.com/cutefishos/kwin-plugins/pull/10.patch)
sha512sums=('454b165ecc108fdfa6b7c101fa353d252d14a0b4deef5d8d90b6d79d6101d661e9e4903c7ffb1590df03c102ae616a860b1e37f24224cd8a94ba5831198ff57d'
            'ea87f3241edb781066af69f524725b1342be32506f7e4a670bd4fc78da99c14efec63ca05fdf1ca0b8a682c5c3e3a4eed2371e2371ce9d4855fccc43c349803f')

prepare() {
  patch -d kwin-plugins-$pkgver -p1 < $pkgname-kwin-5.23.patch
}

build() {
  cd kwin-plugins-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd kwin-plugins-$pkgver
  DESTDIR="$pkgdir" ninja install
}
