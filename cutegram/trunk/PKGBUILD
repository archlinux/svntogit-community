# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Contributor: Llumex03
# Maintainer in Chakra: gnastyle
# Contributor in Chakra: FranzMari from Chakra
# Contributor in Chakra: danyf90 <daniele.formihelli@gmail.com>
# Contributor in Chakra: totoloco <totoloco@gmx.com>

pkgname=cutegram
pkgver=2.9.5
pkgrel=2
pkgdesc="Telegram client by Aseman Land"
arch=('x86_64')
url="http://aseman.co/cutegram"
license=('GPL')
depends=('libqtelegram-ae' 'telegramqml' 'aseman-qt-tools')
optdepends=('gst-plugins-good: for audio and notification support'
            'gst-plugins-bad: for audio support')
makedepends=('git')
source=("${pkgname}"::"git+https://github.com/Aseman-Land/Cutegram.git#commit=1f986dd1f71e3c26e19be59b9c26fe4e6e713b6c")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"

  # modify desktop file for binaryMode
  sed -i "s|qmlscene /usr/share/cutegram/3.0/main.qml|/usr/bin/cutegram|" share/Cutegram.desktop

  mkdir -p build
}

build() {
  cd "${srcdir}/${pkgname}/build"
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr CONFIG+=binaryMode ..
  make
}

package() {
  cd "${srcdir}/${pkgname}/build"

  install -Dm755 cutegram "${pkgdir}/usr/bin/cutegram"
  install -Dm644 ../share/Cutegram.desktop "${pkgdir}/usr/share/applications/Cutegram.desktop"
}
