# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=cutter
pkgver=1.04
pkgrel=2
pkgdesc='TCP/IP Connection cutting on Linux Firewalls and Routers'
url='http://www.digitage.co.uk/digitage/software/linux-security/cutter'
arch=('x86_64')
license=('GPL2')
depends=('glibc')
source=(http://www.digitage.co.uk/digitage/files/cutter/cutter-${pkgver}.tgz)
sha256sums=('1b336c69eb3393f6ca118a529773014c41126ac6dabd04b77151daa1c2f01180')
sha512sums=('79c17ec8c17e696a79aa8b11fda347f2e6929eb35f9a37da450acf6aff7ab9471e13eaef24708f90f3f0c2791fade2eb944443b98df3d3ae4eef0c408abc7b3a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|cc|cc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS}|g' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 0755 cutter "${pkgdir}/usr/bin/tcp-cutter"
}

# vim: ts=2 sw=2 et:
