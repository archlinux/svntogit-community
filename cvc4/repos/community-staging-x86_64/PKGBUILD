# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Philipp Sieweck

pkgname=cvc4
pkgver=1.4
pkgrel=4
pkgdesc="An automatic theorem prover"
arch=('i686' 'x86_64')
url="http://cvc4.cs.nyu.edu/web/"
depends=('boost' 'libantlr3c' 'cln')
optdepends=('readline: for an improved interaction support')
license=('GPL' 'BSD' 'MIT' 'custom:HPND')
source=("http://cvc4.cs.nyu.edu/builds/src/cvc4-$pkgver.tar.gz"
        configure-boost-version.patch)
md5sums=('581c559c02b94fcb18b2e5b11432e009'
         'd9cbfad2415d4a675ebabf3f7c5cdd5d')

prepare() {
  patch "$srcdir"/cvc4-$pkgver/configure configure-boost-version.patch
}

build() {
  cd "$srcdir"/cvc4-$pkgver
  ./configure --prefix=/usr --enable-optimization --disable-doxygen-doc --with-cln --enable-gpl
  make
}

package() {
  cd "$srcdir"/cvc4-$pkgver
  make prefix="$pkgdir"/usr install
  install -D COPYING "$pkgdir"/usr/share/licenses/cvc4/LICENCE

  cd "$pkgdir"
  rmdir "usr/share/perl5" "usr/lib/pyshared" "usr/lib/php" "usr/lib/jni" \
        "usr/lib/csharp" "usr/share/java" "usr/share/php" "usr/share/csharp" \
        "usr/lib/ruby" "usr/lib/tcltk" "usr/lib/perl5" "usr/share/pyshared" \
        "usr/lib/ocaml/cvc4" "usr/lib/ocaml"
}
