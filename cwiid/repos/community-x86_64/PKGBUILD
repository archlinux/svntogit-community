# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Birger Moellering <bmoellering@googlemail.com>

pkgname=cwiid
pkgver=0.6.00
pkgrel=9
pkgdesc="Linux Nintendo Wiimote interface"
arch=('i686' 'x86_64')
url="http://abstrakraft.org/cwiid"
depends=('bluez' 'gtk2' 'python2')
makedepends=('flex' 'bison')
license=('GPL')
install=cwiid.install
source=("http://abstrakraft.org/cwiid/downloads/$pkgname-$pkgver.tgz"
        'bluez4.patch')
md5sums=('8d574afdeedc5e5309c87a72d744316a'
         '19b288723d1f2b97a3e5288ab9de3313')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # bluez v4 compatibility
  patch -Np1 -i "$srcdir/bluez4.patch"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --disable-ldconfig \
              --with-python=python2

  LDFLAGS+="$(pkg-config --libs bluez) -lrt -pthread" make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  chmod 644 "$pkgdir/usr/lib/libcwiid.a"
}

# vim:set ts=2 sw=2 et:
