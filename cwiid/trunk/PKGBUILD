# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Birger Moellering <bmoellering@googlemail.com>
# 2012/03/21 : <dropped from community; broken + see bug #28344>
# 2012/06/05 : <added into community as supercollider dependency>

pkgname=cwiid
pkgver=0.6.00+svn201
pkgrel=1
pkgdesc="Linux Nintendo Wiimote interface"
arch=('x86_64')
url="http://abstrakraft.org/cwiid"
depends=('bluez-libs' 'gtk2' 'python2')
license=('GPL')
install="$pkgname.install"
source=("https://github.com/abstrakraft/cwiid/archive/svn_history.tar.gz")
md5sums=('e1bc290504dc53f02581866eed0b85c3')

prepare() {
  cd $pkgname-svn_history
  sed -i '/ldconfig/s/WITH/ENABLE/' configure.ac
  aclocal
  autoconf -f
}

build() {
  cd $pkgname-svn_history
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-python=python2 \
              --enable-ldconfig=no

  LDFLAGS+="$(pkg-config --libs bluez) -lpthread" \
  make
}

package() {
  cd $pkgname-svn_history
  make DESTDIR="$pkgdir" install

  # wminput README
  install -Dm644 wminput/README \
    "$pkgdir/usr/share/doc/$pkgname/wminput/README"
}

# vim:set ts=2 sw=2 et:
