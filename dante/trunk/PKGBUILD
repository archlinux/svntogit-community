# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=dante
pkgver=1.4.2
pkgrel=1
pkgdesc="SOCKS v4 and v5 compatible proxy server and client"
url="https://www.inet.no/dante"
arch=(x86_64)
license=('custom')
depends=('pam' 'krb5')
backup=('etc/socks.conf'
	'etc/sockd.conf')
source=(ftp://ftp.inet.no/pub/socks/${pkgname}-${pkgver}.tar.gz
	sockd.tmpfiles.d
	sockd.service)
sha256sums=('baa25750633a7f9f37467ee43afdf7a95c80274394eddd7dcd4e1542aa75caad'
            '40d578ef1b802e3813236459b2dfb13fbec6537ae8f55c0c7cbd4401d65da878'
            '7d5f1fddc7e81e5447db662bcc02bbe14f616b403da92e5183e82661d4bc92ae')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc \
	--sbindir=/usr/bin \
	--disable-libwrap
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -dm0755 "$pkgdir"/etc
  cp example/{socks,sockd}.conf "$pkgdir"/etc
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm0644 "$srcdir"/sockd.service "$pkgdir"/usr/lib/systemd/system/sockd.service
  install -Dm0644 "$srcdir"/sockd.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/sockd.conf
}
