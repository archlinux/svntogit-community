# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=darcs
pkgver=2.12.5.20170909
pkgrel=15
pkgdesc="A distributed, interactive, smart revision control system"
url="http://darcs.net"
license=("GPL")
arch=('x86_64')
depends=('curl' 'ghc' "haskell-async" "haskell-attoparsec" "haskell-base16-bytestring"
         "haskell-cryptohash" "haskell-data-ordlist" "haskell-fgl" "haskell-graphviz"
         "haskell-hashable" "haskell-html" "haskell-http" "haskell-knob" "haskell-mmap"
         "haskell-mtl" "haskell-network" "haskell-network-uri" "haskell-old-time" "haskell-parsec"
         "haskell-sandi" "haskell-random" "haskell-regex-applicative" "haskell-regex-compat-tdfa"
         "haskell-tar" "haskell-text" "haskell-unix-compat" "haskell-utf8-string" "haskell-vector"
         "haskell-zip-archive" "haskell-zlib")
makedepends=('ghc')
# Fetched from http://darcs.net/reviewed/reviewed.zip
source=("https://pkgbuild.com/~felixonmars/sources/darcs-2.12.5.20170909.zip")
#source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('64f44e9d9727b8f7e00402a8a5dde9b6dc8f103776c4decef90b28af9603830f')

prepare() {
    mv reviewed $pkgname-$pkgver
    sed -e 's/graphviz     >= 2999.18.1 && < 2999.19,/graphviz     >= 2999.18.1,/' \
        -e 's/process      >= 1.2.3.0 && < 1.5.0.0,/process      >= 1.2.3.0,/' \
        -e 's/time         >= 1.5.0.1 && < 1.8,/time         >= 1.5.0.1,/' \
        -i $pkgname-$pkgver/darcs.cabal
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        -fcurl -fpkgconfig -f-static -fterminfo -fthreaded -fexecutable \
        -f-rts -foptimize -f-warn-as-error -f-libiconv
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
    chmod 644 "${pkgdir}/usr/share/man/man1/darcs.1"

    install -Dm644 contrib/darcs_completion "${pkgdir}/usr/share/bash-completion/completions/darcs"
    install -Dm644 contrib/_darcs.zsh "${pkgdir}/usr/share/zsh/site-functions/_darcs"
}
