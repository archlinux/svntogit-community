# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=('dark-reader' 'firefox-dark-reader' 'thunderbird-dark-reader')
pkgbase=dark-reader
pkgver=4.9.31
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
url=https://darkreader.org/
arch=('any')
license=('MIT')
makedepends=('npm')
source=("https://github.com/darkreader/darkreader/archive/v$pkgver/darkreader-v$pkgver.tar.gz")
b2sums=('d2dfebd43fd81d000f6948601591fca6e0e415dbe0e72e624ba04bf60c56507ad9deca6b32b87adb169866165347f67202c0edfedf8d5173b0755fe25eef19f7')

build() {
  cd darkreader-$pkgver
  npm ci
  npm run build
}

check() {
  cd darkreader-$pkgver
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'
  cd darkreader-$pkgver
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r build/* "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')
  cd darkreader-$pkgver
  install -Dm644 build-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@darkreader.org.xpi
}

package_thunderbird-dark-reader() {
  cd darkreader-$pkgver
  install -Dm644 build-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@darkreader.org.xpi
}

# vim:set ts=2 sw=2 et:
