# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=('dark-reader' 'firefox-dark-reader' 'thunderbird-dark-reader')
pkgbase=dark-reader
pkgver=4.9.37.1
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
url=https://darkreader.org/
arch=('any')
license=('MIT')
makedepends=('npm' 'strip-nondeterminism')
source=("https://github.com/darkreader/darkreader/archive/v$pkgver/darkreader-$pkgver.tar.gz")
b2sums=('cf763e3cf601b2962724f2163ed662020beb8f8d3f936a6566c298dde5155442ce2d6649fb1a11b9a67c170af212876b62ee786cf18ccb78f05ac37e47625af8')

prepare() {
  cd darkreader-$pkgver
  npm ci
}

build() {
  cd darkreader-$pkgver
  npm run build
  strip-nondeterminism -t zip build/release/*.xpi
}

check() {
  cd darkreader-$pkgver
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'
  cd darkreader-$pkgver/build/release/chrome
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r -- * "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')
  cd darkreader-$pkgver/build/release
  install -Dm644 darkreader-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@darkreader.org.xpi
}

package_thunderbird-dark-reader() {
  cd darkreader-$pkgver/build/release
  install -Dm644 darkreader-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@darkreader.org.xpi
}

# vim:set ts=2 sw=2 et:
