# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=dates
pkgver=0.4.11
pkgrel=5
pkgdesc="Small, lightweight GTK+ calendar app"
arch=('i686' 'x86_64')
url="http://pimlico-project.org/dates.html"
license=('GPL')
depends=('gtk2' 'evolution-data-server' 'xdg-utils')
makedepends=('intltool')
options=('!emptydirs')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        0001-Replaced-the-calls-to-the-deprecated-functions-e_sou.patch
        0001-Fix-Makefile-whitespace.patch
        0001-Fix-FTBFS-with-newer-GCC.patch)
sha256sums=('60b0cfe1fa6dee684e255c2a0cd02febafb9d16607ba4b05196e983cd8012c03'
            '88ab8de4861253be1a8db1be4432f71307fcffa376be95e9495cff021b7ead62'
            'fc20f51200a78c74881b98f2b2ea9c3ba01f09393fcc2cf38ecab78ee54f4bb4'
            '0e32c4d9ac4c604330bf8672e47e44c710ae56d9f6dc496e736166c536a4787e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/0001-Replaced-the-calls-to-the-deprecated-functions-e_sou.patch"
  patch -Np1 -i "$srcdir/0001-Fix-Makefile-whitespace.patch"
  patch -Np1 -i "$srcdir/0001-Fix-FTBFS-with-newer-GCC.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-owl
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
