# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

pkgname=dateutils
pkgver=0.4.8
pkgrel=1
pkgdesc='nifty command line date and time utilities; fast date calculations and conversion in the shell'
url='https://www.fresse.org/dateutils/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.xz::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$pkgname-$pkgver.tar.asc::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.asc")
validpgpkeys=('6CB0D61E23A4275CC2CFA16194C9A1AC82C9390E')
sha1sums=('d58ef0c068cdec50ce909c443ce8d6420b750a7a'
          'SKIP')
sha512sums=('964204e2a04a5aa7fa73cbddd98e1a953d498a4b5e0447daa5f912845fb74825d1791b6bea3ebca47784a8edd2e549dfee1e48e3050632efed7f11f109b74be8'
            'SKIP')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-old-links=no
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
