# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=datree
pkgver=1.6.0
pkgrel=1
pkgdesc="CLI tool to ensure K8s manifests and Helm charts follow best practices as well as your organizationâ€™s policies"
arch=('x86_64')
url='https://github.com/datreeio/datree'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pgname}-${pkgver}.tar.gz::https://github.com/datreeio/datree/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('a6865c0698cc91cbbe9f07da62ee45e2bccf62fe16bd49a614446418015ccfc4e6be4af53179df9b64a3379da7ef6b96d5bb56714c8257725ca8687abebf9c8b')
b2sums=('f9eb1d49d44ba6f22e98d316009a51ccd61153ebd748d363048776a9ff1f2bee8d59268ce2faefe3ec011629928dce2c7bf131d0676a93157a9c82fda757609e')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external" -o datree .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 datree "${pkgdir}/usr/bin/datree"
}
