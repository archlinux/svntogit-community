# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=datree
pkgver=0.14.36
pkgrel=1
pkgdesc='CLI tool to ensure K8s manifests and Helm charts follow best practices as well as your organizationâ€™s policies'
arch=('x86_64')
url='https://github.com/datreeio/datree'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pgname}-${pkgver}.tar.gz::https://github.com/datreeio/datree/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('ade0bc2de17ca684bcbeabdb5e4d21907bd54e619368d8220318b3fa421b90e85d0ead3d338c7bbc1f260f847b27b2f9c138121b7ed94eeb5644771e0b007d5e')
b2sums=('18e73791185fc5a1698e293ff9d1058ae3eb3822fe96735272ce21b5c48680aeb17d4190469627a8a2dde3882e8b8b5d50d2c4b3cad21966b2a21d92fe1539ef')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external" -o datree .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 datree "${pkgdir}/usr/bin/datree"
}
