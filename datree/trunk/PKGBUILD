# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
pkgname=datree
pkgver=1.4.10
pkgrel=1
pkgdesc="CLI tool to ensure K8s manifests and Helm charts follow best practices as well as your organizationâ€™s policies"
arch=('x86_64')
url='https://github.com/datreeio/datree'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
source=("${pgname}-${pkgver}.tar.gz::https://github.com/datreeio/datree/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('fe988d5fa7467e963f086f8ca034049be330ab9a0251ed1da1526033deef53f0576b35f2c073b7862a908159f56506d2302d0bb4abcbb6bcdd4665b5a7b33cb0')
b2sums=('cd69fb9d83ee5affa2b909d8daa82ac434ff940757f31a0096c1e21fac43da0b939fee43015bbb04806701f430d21b7d084c9aa2059736bf5389b38ebd27c8eb')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-w -s -linkmode=external" -o datree .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 datree "${pkgdir}/usr/bin/datree"
}
