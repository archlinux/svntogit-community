# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=dbeaver-plugin-office
pkgver=1.1.0
pkgrel=2
pkgdesc='DBeaver plugin to export data to Microsoft Office format'
arch=('any')
url='http://dbeaver.jkiss.org/'
license=('Apache')
makedepends=('unzip')
depends=('dbeaver>=4.1.2' 'dbeaver-plugin-apache-poi')
source=("https://dbeaver.jkiss.org/update/latest/plugins/org.jkiss.dbeaver.data.office_${pkgver}.jar"
        "https://dbeaver.jkiss.org/update/latest/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar"
        "${pkgname}.info")
sha256sums=('e1a1e827aa3905954bc7f55d7c0b79dd0f6a8aece4696071cd15d3b8315d0320'
            'a6d13a076a550457780cf3811924dcde4837aab6fcee620e92c94ea12cf74ae4'
            '71572bc2403e251367c6096cc623e989f913ae143025bc79675f68aa9c2df695')
noextract=("org.jkiss.dbeaver.data.office_${pkgver}.jar"
           "org.jkiss.dbeaver.ext.office.feature_${pkgver}.jar")

package() {
  # Install plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" "org.jkiss.dbeaver.data.office_${pkgver}.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_${pkgver}"
  unzip -q "${srcdir}/org.jkiss.dbeaver.ext.office.feature_1.1.0.jar" \
        -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_1.1.0"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/dbeaver/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/dbeaver/bundles.d/20-${pkgname}.info"
}
