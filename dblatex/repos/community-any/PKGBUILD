# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

pkgname=dblatex
pkgver=0.3.2
pkgrel=1
pkgdesc='DocBook (XML and SGML) to DVI, PDF, PostScript converter using latex.'
arch=('any')
url='http://dblatex.sourceforge.net/'
license=('GPL')
depends=('docbook-xsl' 'docbook-xml' 'libxslt' 'python2' 'texlive-core' 'texlive-htmlxml' 'texlive-latexextra' 'texlive-pictures')
source=("http://downloads.sourceforge.net/project/dblatex/dblatex/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('95f81bc46690c4052818a82a32d05b7c')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --root "${pkgdir}" --catalogs=/etc/xml/docbook
  sed -i -e "s,${pkgdir},," -e "s,#!/usr/bin/env python,#!/usr/bin/env python2," \
    "${pkgdir}/usr/bin/dblatex"
}
