# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.99.8
pkgrel=2
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('glibc')
optdepends=('lzo: for libddr_lzo')
arch=('x86_64')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.bz2"{,.asc}
        'dd_rescue-1.99-disable-openssl.patch')
sha512sums=('a230e1df4532671ea631036012dd1e38614e45bed58b00757f0017b0ea60f14ac3bdac07777d175aa4929def593b3c8485e463b1fc25b5067adf4cf3f3ac040d'
            'SKIP'
            'fff318506e57a6737f523ecebd90751a35e908ca71fb29985132c9c2e6b476f341c241a2abb8a86b86bf24d03e185f15d2bc89f3c3d834cbb34aecb8e5e85d55')
validpgpkeys=('6669F7340D31E95EC5565490DE4F1B3A2BFFC5BF') # Kurt Garloff <kurt@garloff.de>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi

  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile

  # Disable OpenSSL support
  patch -p1 -i ../dd_rescue-1.99-disable-openssl.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}
