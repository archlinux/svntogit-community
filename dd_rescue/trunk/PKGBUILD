# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.42
pkgrel=1
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('glibc')
arch=('x86_64' 'i686')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.gz")

prepare() {
  cd $pkgname
  # Don't install to /usr/lib64
  sed -i '/LIBDIR = \/usr\/lib64/d' Makefile
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}

sha512sums=('ef9475c8b64d7d2faeee01544994746e91e7068041a2bd381e28588c15b722daebe142f9d87afe354a3d16783951b8c14a8db665b063d3891f99c60e32c7eaa8')
