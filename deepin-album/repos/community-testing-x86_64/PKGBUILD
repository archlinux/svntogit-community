# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.9.10
pkgrel=3
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-image-editor' 'ffmpegthumbnailer' 'gio-qt' 'hicolor-icon-theme' 'udisks2-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz")
sha512sums=('e24dfe0f9f20a720c5d2efe94a9bb32db51133b676c19d2d767d579c6b058a20cd5ca530d2cc78f6bfe13ca61b06fafaf52073889130bc277c8bd65389d45b18')
options=(!lto)
	
prepare() {
  cd deepin-album-$pkgver

  # Our build of freeimage disabled support for these formats
  sed -i '/FIF_FAXG3/d' libUnionImage/UnionImage/unionimage.cpp
}

build() {
  cd deepin-album-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-album-$pkgver
  DESTDIR="$pkgdir" ninja install
}
