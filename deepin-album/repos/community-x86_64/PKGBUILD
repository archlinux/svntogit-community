# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.10.7
pkgrel=2
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-image-editor' 'ffmpegthumbnailer' 'gio-qt' 'hicolor-icon-theme' 'udisks2-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz"
        $pkgname-ldflags.patch::https://github.com/linuxdeepin/deepin-album/pull/59.patch)
sha512sums=('c09fcab74fe9df77bde1096c8529f92aae8d4d057f8b921718914eab0ff2bc56b968ba3199fca43435da8d198cd9a5910bf79444fcaf057d665be47caf08c66a'
            'ef5e3905ca168aad581f2efac5fc0fad2fbd8a6d2aedce4cdf2cbdb16570be723763e1c9e85e87355fc5d6436dd5b7ab36bfabd78cb9f95ccb3c8efa1f54accb')
options=(!lto)

prepare() {
  cd deepin-album-$pkgver
  patch -p1 -i ../$pkgname-ldflags.patch

  # Our build of freeimage disabled support for these formats
  sed -i '/FIF_FAXG3/d' libUnionImage/UnionImage/unionimage.cpp
}

build() {
  cd deepin-album-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-album-$pkgver
  DESTDIR="$pkgdir" ninja install
}
