# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.8.9
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'hicolor-icon-theme' 'udisks2-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz")
sha512sums=('7bc6be5cc961454ae517c430b6287b7b9578a118ee088228a5c6bd0e561f41ee4145a1d6acb65822de96c00af0d48921b977759f40db4ac68fb8ed0169c0d875')

prepare() {
  cd deepin-album-$pkgver

  # Our build of freeimage disabled support for these formats
  sed -i '/FIF_FAXG3/d' libUnionImage/UnionImage/unionimage.cpp
}

build() {
  cd deepin-album-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-album-$pkgver
  DESTDIR="$pkgdir" ninja install
}
