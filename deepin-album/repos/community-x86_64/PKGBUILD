# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.6.9.16
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz"
         deepin-album-qt5.15.patch)
sha512sums=('57a13ad1ee46c70e6fc7f09b92be504695bae21873f6615ee7cefe5b6ee18f94a45c332b6195d71506fb32a23ca6f580e0a296860796aba21916f186a0a4ea4d'
            'bd263f21eef20b586b3c673bb9bb3fcdb53f8f708f57335ce27edcd6069048e8a2e03a46cd9849f24b37165ccd108cd96e20adfc83d5d82a544d61b96fc5d297')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../deepin-album-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-album-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-album-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
