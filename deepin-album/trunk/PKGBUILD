# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.10.6
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-image-editor' 'ffmpegthumbnailer' 'gio-qt' 'hicolor-icon-theme' 'udisks2-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz")
sha512sums=('08144172ee4bccde23d34fad34749beb4f106685c1784583b5f94d713be7b2c60189ebe818892e86b46fe775fb00b579c0fb032c33f82433e6451869d50267a6')
options=(!lto)

prepare() {
  cd deepin-album-$pkgver

  # Our build of freeimage disabled support for these formats
  sed -i '/FIF_FAXG3/d' libUnionImage/UnionImage/unionimage.cpp
}

build() {
  cd deepin-album-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-album-$pkgver
  DESTDIR="$pkgdir" ninja install
}
