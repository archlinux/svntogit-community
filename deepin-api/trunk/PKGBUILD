# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=3.1.20
pkgrel=1
pkgdesc='Go-lang bindings for dde-daemon'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill'
         'poppler-glib' 'xcur2png' 'blur-effect') # 'gcc-libs>=5.3.0-3')
makedepends=('git' 'deepin-gettext-tools' 'deepin-dbus-factory' 'deepin-gir-generator'
             'deepin-go-lib' 'bzr' 'go-pie') # 'gcc-go')
conflicts=('dde-api')
replaces=('dde-api')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-api/archive/$pkgver.tar.gz")
sha512sums=('fde59393cf9602288ad5631ae7721cdd8acf4ad7d946578066fe5c781faea52f36a507a7a1ace380a8deb3683bde3703598689935f999c1d68b241cadef22023')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p build/src/pkg.deepin.io/dde/api
  cp -a dde-api-$pkgver/* build/src/pkg.deepin.io/dde/api/

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  cd "$srcdir"/dde-api-$pkgver
  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2 github.com/nfnt/resize github.com/cryptix/wav
}

build(){
  cd dde-api-$pkgver
  make # USE_GCCGO=1
}

package() {
  cd dde-api-$pkgver
  make DESTDIR="$pkgdir" SYSTEMD_LIB_DIR=/usr/lib install
}
