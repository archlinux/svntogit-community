# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-artwork-themes
pkgver=15.10.1
pkgrel=1
pkgdesc='Deepin artwork themes'
arch=('any')
license=('LGPL3')
url="https://gitcafe.com/Deepin/deepin-artwork-themes"
depends=('deepin-icon-theme')
makedepends=('python2' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-artwork-themes.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd $pkgname
  make build
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
