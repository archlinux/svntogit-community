# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-artwork-themes
pkgver=15.10.1.20151202
_commit=e52d6203113b138b42c02b58674deaabb0310a08
_desktopbaserev=6cd46627bb62ce820991aa53439be8142b523ad8
pkgrel=1
pkgdesc='Deepin artwork themes'
arch=('any')
license=('LGPL3')
url="https://gitcafe.com/Deepin/deepin-artwork-themes"
depends=('deepin-icon-theme')
makedepends=('python2' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-artwork-themes.git#commit=$_commit"
        "git+https://github.com/linuxdeepin/deepin-desktop-base.git#commit=$_desktopbaserev")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  # fix default background url
  sed -i 's#^BackgroundFile=.*$#BackgroundFile=file:///usr/share/backgrounds/deepin_default_background.jpg#' themes/Deepin/theme.ini
}

build() {
  cd $pkgname
  make build
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -d "${pkgdir}"/usr/share/backgrounds/
  cp "${srcdir}"/deepin-desktop-base/backgrounds/desktop.jpg "${pkgdir}"/usr/share/backgrounds/deepin_default_background.jpg
}
