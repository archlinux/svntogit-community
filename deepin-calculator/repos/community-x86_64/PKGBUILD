# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.7.21
pkgrel=2
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-fix-ldflags.patch::https://github.com/linuxdeepin/deepin-calculator/pull/82.patch)
sha512sums=('12c92e5c77a94619e15a44a0fee84bde4b6d94b8bc75deb000ba006f5a2616233e8bd627bf400ad57f9e25062569d8ff642ddc69fa02d858c6beb0265ee76be0'
            '001b9ab4b7a9aac4ded51e9e2c0362824f3e38ad8a80c15c3cbe7cbfeef6ce65e85a4c3aa688fcdd0e396267a108fd6a192adece597e95775aece1c29a8c2a7c')

prepare() {
  patch -d deepin-calculator-$pkgver -p1 < $pkgname-fix-ldflags.patch
}

build() {
  cd deepin-calculator-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-calculator-$pkgver
  DESTDIR="$pkgdir" ninja install
}
