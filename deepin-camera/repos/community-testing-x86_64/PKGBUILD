# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-camera
pkgver=1.1.3
pkgrel=1
pkgdesc='Tool to view camera, take photo and video'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-camera"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'ffmpeg' 'ffmpegthumbnailer' 'portaudio' 'pulseaudio')
makedepends=('cmake' 'deepin-gettext-tools' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-camera/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('75d5b270d819c1cc517c815a19acce62e93d3981613c70579b3d0f4245c77a33f3e5db3a9f696b7f5ef669fb3f35840b8929431b7a38b15c94cfa778912fea56')

prepare() {
  cd deepin-camera-$pkgver
  # https://github.com/linuxdeepin/deepin-camera/issues/2
  sed -i 's/LIBAVFORMAT_VERSION_MAJOR >= 57 && LIBAVFORMAT_VERSION_MINOR <= 25/LIBAVFORMAT_VERSION_MAJOR >= 57/' src/src/imageitem.cpp
} 

build() {
  cd deepin-camera-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  ninja
}

package() {
  cd deepin-camera-$pkgver
  DESTDIR="$pkgdir" ninja install
}
