# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-camera
pkgver=1.4.4
pkgrel=1
pkgdesc='Tool to view camera, take photo and video'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-camera"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'deepin-image-editor' 'ffmpeg'
         'ffmpegthumbnailer' 'portaudio' 'pulseaudio')
makedepends=('cmake' 'deepin-gettext-tools' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-camera/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-ffmpeg-5.patch::https://github.com/linuxdeepin/deepin-camera/pull/211.patch)
sha512sums=('e529d7b4bc244aa71d5a75dda352fd635c31c89dccc0b13f5b5a4ab2bc7326ffb5d8d0050ebabff025c1ecec3a1c921de0b0ffcb093815d335c9e36f6a74fc09'
            '525e64b2595db019dc3f7974bfcfb32ffeed62c90b5b51eac31e486154b4e1af115f479d58ed97a17040152d58fc5cd4c3c82c48b4ff764c46183e5a7bdcff1a')

prepare() {
  cd deepin-camera-$pkgver
  patch -p1 -i ../$pkgname-ffmpeg-5.patch
  sed -i '/#include <QThread>/i #include <thread>' src/src/windowstatethread.h
} 

build() {
  cd deepin-camera-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  ninja
}

package() {
  cd deepin-camera-$pkgver
  DESTDIR="$pkgdir" ninja install
}
