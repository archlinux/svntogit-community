# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-camera
pkgver=1.2.1
pkgrel=1
pkgdesc='Tool to view camera, take photo and video'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-camera"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'ffmpeg' 'ffmpegthumbnailer' 'portaudio' 'pulseaudio')
makedepends=('cmake' 'deepin-gettext-tools' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-camera/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('30c7a883079aa48f4cf95731761d5a7a962c986414936d0adc7180f2f0b195f7643cc420b81af8508e01aa512949fd883360f33e4cdf66cc7b70fd58e1a093a4')

prepare() {
  cd deepin-camera-$pkgver
  sed -i '/#include <QThread>/i #include <thread>' src/src/windowstatethread.h

  # https://github.com/linuxdeepin/deepin-camera/issues/2
  sed -i 's/LIBAVFORMAT_VERSION_MAJOR >= 57 && LIBAVFORMAT_VERSION_MINOR <= 25/LIBAVFORMAT_VERSION_MAJOR >= 57/' src/src/imageitem.cpp
} 

build() {
  cd deepin-camera-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  ninja
}

package() {
  cd deepin-camera-$pkgver
  DESTDIR="$pkgdir" ninja install
}
