# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.3.0.3
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('66663fbc472ab38d141fd2d5606d58201299e80813b1ad0f997a8a950f35cb77251184c0a0c9822a8882f47c60f73f7cb9a9001030671c310268b6bbd9066a89')

prepare() {
  cd dde-clipboard-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' dde-clipboard/pixmaplabel.cpp dde-clipboard/itemwidget.cpp
  sed -i '$aOnlyShowIn=Deepin' dde-clipboard/dde-clipboard.desktop
}

build() {
  cd dde-clipboard-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-clipboard-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
