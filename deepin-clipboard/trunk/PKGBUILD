# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.4.22
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('cmake' 'ninja' 'gtest' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('f333b84b13cd4528a74b54907944da1bb4747dd7742b005693a751ee5de290c908894dc701acaa380b2ee63f7399bc8612bd00fd6761f1ace12bf65ea9433d67')

prepare() {
  mkdir -p build
  sed -i 's|DESTINATION /lib/systemd/user|DESTINATION /usr/lib/systemd/user|' dde-clipboard-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake -GNinja ../dde-clipboard-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
