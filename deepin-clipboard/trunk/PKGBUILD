# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.0.0.4
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('048fa4bb7a3816227b5411e8e53ab50f96dcce724d514d306f8a66a5bef5fa7af6198cc6110c87b9bca10a7424593dc02aa639c9c15ec6a5dc1ffeb06773e0c8')

prepare() {
  cd dde-clipboard-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' dde-clipboard/pixmaplabel.cpp dde-clipboard/itemwidget.cpp
}

build() {
  cd dde-clipboard-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-clipboard-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
