# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.8.0.9
pkgrel=2
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('deepin-qt5integration' 'disomaster' 'karchive' 'kcodecs' 'libarchive' 'libzip' 'minizip' 'udisks2-qt5')
makedepends=('cmake' 'ninja')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver.tar.gz")
sha512sums=('da8db12191589254615219bf9a16d1b4668e381bb3ef0901c293076c6797b2e45e9cb802ae4d8f2b06ac604cb1de9e62e671db387956fe69d6a619a2d1f78319')

prepare() {
  cd deepin-compressor-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' deepin-compressor/source/src/openwithdialog/openwithdialog.cpp deepin-compressor/source/src/logviewheaderview.cpp
}

build() {
  cd deepin-compressor-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-compressor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
