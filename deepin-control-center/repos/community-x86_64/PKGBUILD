# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=5.4.70
pkgrel=2
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip' 'libpwquality'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-network-utils' 'deepin-pw-check')
makedepends=('cmake' 'deepin-dock' 'gmock' 'gtest' 'ninja' 'qt5-tools')
optdepends=('redshift: automatic color temperature support'
            'networkmanager-openconnect: for OpenConnect support'
            'networkmanager-openvpn: for OpenVPN support'
            'networkmanager-pptp: for PPTP support'
            'networkmanager-strongswan: for StrongSwan support'
            'networkmanager-vpnc: for VPNC support'
            'network-manager-sstp: for SSTP support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-control-center/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-systeminfo-deepin-icon.patch
        $pkgname-no-user-experience.patch
        $pkgname-hide-end-user-license.patch::https://github.com/linuxdeepin/dde-control-center/pull/387.patch)
sha512sums=('95e10dbc450ef20224d36634e27535f3175a6ea3b802ad8089e2352d3a0ef9b3e337cc5a6981afeb1e4a60ace1e3b9c838ada1a2fe7f39e6f12a23fa34e62271'
            '74fd63391e923ca37f4559f30da967ba7f33d4426b60d58d1ece8cd9a154578e8184b1a376a8d7ff3ef81ffce530915f79d0845a2612ae4e06522b96855ab3dd'
            'b7f766e4dad9840a716ac22458b387626da712f56a546475f71639182273417bd3135b978a3926fe234bf7bae0c353c1d7cbb08ad386c2691f6b1223b1938bfa'
            'c3f2366d1ac361d3577548329910fe218ae076078084a72543d67b1804ebe437479bf0f592166d3b00d120fe9cf16e4e89fb6ddb82430d9e0e4d0a90266b51ee')

prepare() {
  rm dde-control-center-$pkgver/src/frame/window/icons/icons/dcc_nav_systeminfo_{42,84}px.svg
  patch -d dde-control-center-$pkgver -Np1 < $pkgname-systeminfo-deepin-icon.patch
  patch -d dde-control-center-$pkgver -Np1 < $pkgname-no-user-experience.patch
  patch -d dde-control-center-$pkgver -Np1 < $pkgname-hide-end-user-license.patch
  mkdir -p build

  cd dde-control-center-$pkgver
  # remove after they obey -DDISABLE_SYS_UPDATE properly
  sed -i '/new UpdateModule/d' src/frame/window/mainwindow.cpp

  sed -i '/#include <QPointer>/i #include <QDBusMetaType>' src/frame/window/modules/network/connectioneditpage.h
}

build() {
  cd build
  cmake -GNinja -DDISABLE_SYS_UPDATE=YES -DDISABLE_RECOVERY=YES -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../dde-control-center-$pkgver
  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
