# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=2.91.2
pkgrel=2
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'libdui' 'gtk3' 'deepin-account-faces' 'deepin-api'
         'deepin-daemon' 'deepin-dbus-factory' 'startdde')
makedepends=('deepin-dock' 'deepin-gettext-tools' 'git')
conflicts=('dde-control-center')
replaces=('dde-control-center')
groups=('deepin')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/dde-control-center.git#tag=$pkgver"
        'show-distro-info.patch'
        'optimize_plugins_unload.patch')
sha256sums=('SKIP'
            '076be93652adebe404a076036503b95140d70d6612e34f35502909c2617d1b1d'
            'a7c9d34538215ecd4c8f05820aadfd4c41bd33527699100c0aecad631fdef267')

prepare() {
  cd dde-control-center
  patch -p1 -i ../show-distro-info.patch
  patch -p1 -i ../optimize_plugins_unload.patch
}

build() {
  cd dde-control-center
  qmake-qt5 PREFIX=/usr WITH_MODULE_GRUB=NO
  make
}

package() {
  cd dde-control-center
  make INSTALL_ROOT="${pkgdir}" install
}
