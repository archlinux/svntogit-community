# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=5.5.77
pkgrel=2
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip' 'libpwquality'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-pw-check')
makedepends=('cmake' 'deepin-dock' 'gmock' 'gtest' 'ninja' 'qt5-tools')
optdepends=('redshift: automatic color temperature support'
            'networkmanager-openconnect: for OpenConnect support'
            'networkmanager-openvpn: for OpenVPN support'
            'networkmanager-pptp: for PPTP support'
            'networkmanager-strongswan: for StrongSwan support'
            'networkmanager-vpnc: for VPNC support'
            'network-manager-sstp: for SSTP support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("https://github.com/linuxdeepin/dde-control-center/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-systeminfo-deepin-icon.patch
        $pkgname-add-option-to-disable-deepin-authentication.patch::https://github.com/linuxdeepin/dde-control-center/pull/407.patch)
sha512sums=('f1be8efcb3ba3293612ebf191e424f6ac3e90c0d3708ba3079a25de794bdde2e8a49679ac0bfba6aab9e9208944ae4e525a7455e48017eacf997ad2f604bb41b'
            '74fd63391e923ca37f4559f30da967ba7f33d4426b60d58d1ece8cd9a154578e8184b1a376a8d7ff3ef81ffce530915f79d0845a2612ae4e06522b96855ab3dd'
            '6e7d1e3b23b0a0918be664506ed90248e484392f00188242cbd4bfe6eaf91ddd18bc9b2d9bba28d06f72f7deb46532eb4ae3c210ac17094d4b4607c086cc7a31')

prepare() {
  rm dde-control-center-$pkgver/src/frame/window/icons/icons/dcc_nav_systeminfo_{42,84}px.svg
  patch -d dde-control-center-$pkgver -Np1 < $pkgname-systeminfo-deepin-icon.patch
  mkdir -p build

  cd dde-control-center-$pkgver
  # remove after they obey -DDISABLE_SYS_UPDATE properly
  sed -i '/new UpdateModule/d' src/frame/window/mainwindow.cpp

  patch -p1 -i ../$pkgname-add-option-to-disable-deepin-authentication.patch
}

build() {
  cd build
  cmake -GNinja -DDISABLE_SYS_UPDATE=YES -DDISABLE_AUTHENTICATION=YES -DDISABLE_RECOVERY=YES -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../dde-control-center-$pkgver
  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
