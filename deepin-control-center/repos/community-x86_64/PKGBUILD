# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=5.1.0.12
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-network-utils')
makedepends=('deepin-dock' 'cmake' 'qt5-tools')
optdepends=('redshift: automatic color temperature support'
            'networkmanager-openconnect: for OpenConnect support'
            'networkmanager-openvpn: for OpenVPN support'
            'networkmanager-pptp: for PPTP support'
            'networkmanager-strongswan: for StrongSwan support'
            'networkmanager-vpnc: for VPNC support'
            'network-manager-sstp: for SSTP support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz"
        $pkgname-build-fix.patch)
sha512sums=('b5ef4eb6c0f202ddcf261b521ed9764d62f8a6d7b6aaf363642fa2b4ea558f3f2977e0e1e5de12b3d7668c924c7a4d56e3219fae67ba724b72b38048c9392967'
            'e36681ee79ba90360217c7dd3cf432ac2e04cc343d60c3580232a894d74edd73f455445bedaaf833a6092b710d4e98ef03e85edb213e3bf5d2f42261a0e769b1')

prepare() {
  patch -d dde-control-center-$pkgver -p1 < $pkgname-build-fix.patch
  mkdir -p build

  cd dde-control-center-$pkgver
  sed -i 's|/bin/restore-tool|/usr/bin/restore-tool|' src/frame/window/modules/systeminfo/backupandrestoreworker.cpp \
                                                      com.deepin.controlcenter.restore.policy
  sed -i 's| /bin| /usr/bin|' src/restore-tool/CMakeLists.txt

  # remove after they obey -DDISABLE_SYS_UPDATE properly
  sed -i '/new UpdateModule/d' src/frame/window/mainwindow.cpp
}

build() {
  cd build
  cmake -DDISABLE_SYS_UPDATE=YES -DCMAKE_INSTALL_PREFIX=/usr \
    ../dde-control-center-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
