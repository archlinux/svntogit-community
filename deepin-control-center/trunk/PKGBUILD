# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=5.2.0.3
pkgrel=2
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-network-utils')
makedepends=('deepin-dock' 'cmake' 'qt5-tools')
optdepends=('redshift: automatic color temperature support'
            'networkmanager-openconnect: for OpenConnect support'
            'networkmanager-openvpn: for OpenVPN support'
            'networkmanager-pptp: for PPTP support'
            'networkmanager-strongswan: for StrongSwan support'
            'networkmanager-vpnc: for VPNC support'
            'network-manager-sstp: for SSTP support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz"
        $pkgname-build-fix.patch
        $pkgname-systeminfo-deepin-icon.patch::https://github.com/linuxdeepin/dde-control-center/commit/2b282b5271ff01741f062a6eb8a74b386e2ada48.patch
        $pkgname-no-user-experience.patch::https://github.com/linuxdeepin/dde-control-center/commit/15a3b5101853ee0b1b62e5e7dde217cbf9a454bf.patch)
sha512sums=('30cf5c18ee50f62c0444b4b5089ef49a1fbe95b61f5ef99715226afc367a395b5ea6e5f6233dc00669e392b68ac2610927f8264192571a89443dda74463b28d8'
            'e36681ee79ba90360217c7dd3cf432ac2e04cc343d60c3580232a894d74edd73f455445bedaaf833a6092b710d4e98ef03e85edb213e3bf5d2f42261a0e769b1'
            'ca045eaf14079ce2fb8be264c0f5442c61a14ea54d3783d6275cd7da7ca258d21144349088caba8831fd7508cc429cb6f9507315b5446f00f9e1bbee446f02e5'
            '692061ec6d5cce41a41239bcb75aec3ee1d002f5d6e9e86df795808edb080caf9978de2a23d267bad960a8436670252957c767aa30a19d4a702719e7d9a6f94f')

prepare() {
  patch -d dde-control-center-$pkgver -Np1 < $pkgname-systeminfo-deepin-icon.patch || :
  patch -d dde-control-center-$pkgver -Np1 < $pkgname-no-user-experience.patch
  patch -d dde-control-center-$pkgver -Np1 < $pkgname-build-fix.patch
  mkdir -p build

  cd dde-control-center-$pkgver
  sed -i '/#include <QPainter>/a #include <QPainterPath>' src/frame/widgets/basiclistdelegate.cpp src/frame/window/modules/update/updatehistorybutton.cpp \
                                                          src/frame/window/modules/commoninfo/commonbackgrounditem.cpp src/frame/modules/accounts/useroptionitem.cpp \
                                                          src/frame/window/modules/sync/pages/avatarwidget.cpp src/frame/window/modules/accounts/avataritemdelegate.cpp \
                                                          src/frame/modules/accounts/avatarwidget.cpp src/frame/window/modules/accounts/accountswidget.cpp \
                                                          src/frame/modules/datetime/timezone_dialog/popup_menu.cpp src/frame/modules/display/recognizedialog.cpp \
                                                          src/frame/window/modules/personalization/roundcolorwidget.cpp
  sed -i '/#include <QRect>/a #include <QPainterPath>' src/frame/window/modules/personalization/personalizationgeneral.cpp

  sed -i 's|/bin/restore-tool|/usr/bin/restore-tool|' src/frame/window/modules/systeminfo/backupandrestoreworker.cpp \
                                                      com.deepin.controlcenter.restore.policy
  sed -i 's| /bin| /usr/bin|' src/restore-tool/CMakeLists.txt

  # remove after they obey -DDISABLE_SYS_UPDATE properly
  sed -i '/new UpdateModule/d' src/frame/window/mainwindow.cpp
}

build() {
  cd build
  cmake -DDISABLE_SYS_UPDATE=YES -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../dde-control-center-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/etc/xdg/autostart/deepin-ab-recovery.desktop
  rmdir "$pkgdir"/etc/xdg/autostart "$pkgdir"/etc/xdg "$pkgdir"/etc
}
