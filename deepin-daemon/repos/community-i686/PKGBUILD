# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=3.0.12
_golibver=0.4.9
_deepinapiver=3.0.7
_dbusfactoryver=3.0.3
_girgeneratorver=0.9.3
pkgrel=1
pkgdesc='Daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'poppler-glib'
         'rfkill' 'acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
         'mobile-broadband-provider-info' 'polkit-gnome' 'udisks2' 'upower' 'gcc-libs>=5.3.0-3'
         'libxkbfile' 'accountsservice' 'deepin-desktop-base' 'bamf' 'pulseaudio')
makedepends=('deepin-dbus-generator' 'deepin-gir-generator' 'sqlite' 'git' 'mercurial' 'gcc-go')
optdepends=('networkmanager: Network Management daemon'
            'deepin-grub2-themes: deepin theme for grub menu')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=("git+https://cr.deepin.io/dde/dde-daemon.git#tag=$pkgver"
        "git+https://cr.deepin.io/go-lib.git#tag=$_golibver"
        "git+https://cr.deepin.io/dde/dde-api.git#tag=$_deepinapiver"
        "git+https://cr.deepin.io/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://cr.deepin.io/go-gir-generator.git#tag=$_girgeneratorver"
        'deepin-daemon.sysusers'
        'polkit-gnome-authentication-agent-1-deepin.desktop')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '4482f2c82c3652040021dd43515f131184a0417e341dc37db487117012245e25'
            '014f4c1111530e7556c3a83ab4409ae7dd47a87550004128ed8b7d02c58357e7')

prepare() {
  sed -i "s|\"\${GOLDFLAGS}\"|\"\${GOLDFLAGS} $CFLAGS $LDFLAGS\"|" dde-daemon/Makefile
  sed -i 's| generator$||' go-gir-generator/makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/
  
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         github.com/BurntSushi/freetype-go/freetype github.com/BurntSushi/freetype-go/freetype/truetype \
         github.com/BurntSushi/graphics-go/graphics
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/go-gir-generator"
  make GENERATOR=gir-generator build
  make copyfile
  cp -r out/src/gir "$GOPATH/src"/

  cd "$srcdir/dde-daemon"
  make USE_GCCGO=1
}

package() {
  cd dde-daemon
  make USE_GCCGO=1 DESTDIR="$pkgdir" install

  install -Dm644 ../polkit-gnome-authentication-agent-1-deepin.desktop "$pkgdir/etc/xdg/autostart/polkit-gnome-authentication-agent-1-deepin.desktop"
  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
}
