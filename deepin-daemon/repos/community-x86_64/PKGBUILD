# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=3.1.0
pkgrel=1
pkgdesc='Daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'poppler-glib'
         'rfkill' 'acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
         'mobile-broadband-provider-info' 'polkit-gnome' 'udisks2' 'upower' # 'gcc-libs>=5.3.0-3'
         'libxkbfile' 'accountsservice' 'deepin-desktop-base' 'bamf' 'pulseaudio'
         'gnome-keyring')
makedepends=('deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib' 'deepin-api' 'sqlite'
             'git' 'mercurial' 'python-gobject' 'networkmanager' 'go') # 'gcc-go')
optdepends=('networkmanager: Network Management daemon'
            'deepin-grub2-themes: deepin theme for grub menu')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=("git+https://cr.deepin.io/dde/dde-daemon.git#tag=$pkgver"
        'deepin-daemon.sysusers'
        'polkit-gnome-authentication-agent-1-deepin.desktop')
sha256sums=('SKIP'
            '4482f2c82c3652040021dd43515f131184a0417e341dc37db487117012245e25'
            '014f4c1111530e7556c3a83ab4409ae7dd47a87550004128ed8b7d02c58357e7')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         github.com/BurntSushi/freetype-go/freetype github.com/BurntSushi/freetype-go/freetype/truetype \
         github.com/BurntSushi/graphics-go/graphics github.com/fsnotify/fsnotify golang.org/x/sys/unix \
         gopkg.in/yaml.v2
}

build() {
  cd dde-daemon
  make # USE_GCCGO=1
}

package() {
  cd dde-daemon
  make DESTDIR="$pkgdir" install # USE_GCCGO=1

  install -Dm644 ../polkit-gnome-authentication-agent-1-deepin.desktop "$pkgdir/etc/xdg/autostart/polkit-gnome-authentication-agent-1-deepin.desktop"
  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
}
