# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=3.2.24.5
pkgrel=1
pkgdesc='Daemon handling the DDE session settings'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'poppler-glib' 'rfkill' 'bluez'
         'deepin-notifications' 'iso-codes' 'lsb-release' 'mobile-broadband-provider-info'
         'deepin-polkit-agent' 'deepin-polkit-agent-ext-gnomekeyring' 'udisks2' 'upower'
         'libxkbfile' 'accountsservice' 'deepin-desktop-base' 'bamf' 'pulseaudio'
         'gnome-keyring' 'noto-fonts')
makedepends=('deepin-dbus-factory' 'deepin-go-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'sqlite' 'deepin-gettext-tools' 'git' 'mercurial' 'python-gobject'
             'networkmanager' 'go-pie')
optdepends=('networkmanager: for network management support'
            'deepin-grub2-themes: theme for grub module'
            'iw: for miracast module'
            'proxychains-ng: for proxy configuration module')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-daemon/archive/$pkgver.tar.gz"
        https://raw.githubusercontent.com/jouyouyun/tap-gesture-patches/master/patches/dde-daemon_3.2.3.patch
        'deepin-daemon.sysusers'
        nm-1.14.patch)
sha512sums=('e8dc5893cf2c271671a6b02df4e6ac11ff7e8a166a0052fd1366075fba9035d7f77cff4abceddc45ce0ae6b2a72fd4b564e7d6a330bcb98347456cf8a8b4bbe4'
            '29874d6b316bb678b6656827e696d65eef174a53bb8226ed4c2d3bc89e42d5edb466df3c2ac647a1f2eb05d6e2a199b8d07dcaa9302b9227a0692fde0d50bb52'
            '808c02d4fec4cbbb01119bbb10499090199e738b7dd72c28a57dde098eef6132723f3434c151f79e21d9f788c7f7bae8046573ac93ba917afe0e803fbffa6d5a'
            'b118eefb9f6cb1ecd2e8e2f0f59ab6fe3c6bf8e581b106c2d4fa86573f50a37549e6041726a3fe084dec1e8306e5498086e55fd928b64f614b4eac6c6a81920b')

prepare() {
  cd dde-daemon-$pkgver
  patch -p1 -i ../dde-daemon_3.2.3.patch
  patch -p1 -i ../nm-1.14.patch

  export GOPATH="$srcdir/build:/usr/share/gocode"

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/linuxdeepin/go-x11-client \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         github.com/BurntSushi/freetype-go/freetype github.com/BurntSushi/freetype-go/freetype/truetype \
         github.com/BurntSushi/graphics-go/graphics github.com/fsnotify/fsnotify golang.org/x/sys/unix \
         gopkg.in/yaml.v2 github.com/axgle/mahonia github.com/msteinert/pam github.com/nfnt/resize \
         golang.org/x/text/encoding/charmap github.com/cryptix/wav golang.org/x/net/context

  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' accounts/user.go
}

build() {
  cd dde-daemon-$pkgver
  make -C network/nm_generator gen-nm-code
  make
}

package() {
  cd dde-daemon-$pkgver
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"{,/usr}/lib/systemd
  rmdir "$pkgdir"/lib

  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
}
