# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=3.1.16
pkgrel=1
pkgdesc='Daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'poppler-glib'
         'rfkill' 'acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
         'mobile-broadband-provider-info' 'deepin-polkit-agent' 'udisks2' 'upower' # 'gcc-libs>=5.3.0-3'
         'libxkbfile' 'accountsservice' 'deepin-desktop-base' 'bamf' 'pulseaudio'
         'gnome-keyring')
makedepends=('deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib' 'deepin-api' 'sqlite'
             'git' 'mercurial' 'python-gobject' 'networkmanager' 'go') # 'gcc-go')
optdepends=('networkmanager: Network Management daemon'
            'deepin-grub2-themes: deepin theme for grub menu'
            'iw: for miracast module')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-daemon/archive/$pkgver.tar.gz"
        'deepin-daemon.sysusers')
sha512sums=('3b52234313a24d17f272eb5277c629e79eaab0f56583eb8dede4d01ea5ea9d1a8373c1f6e072b2657850cc919b079015e4f31aafa0efb935e28f4f0da93b4fec'
            '808c02d4fec4cbbb01119bbb10499090199e738b7dd72c28a57dde098eef6132723f3434c151f79e21d9f788c7f7bae8046573ac93ba917afe0e803fbffa6d5a')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         github.com/BurntSushi/freetype-go/freetype github.com/BurntSushi/freetype-go/freetype/truetype \
         github.com/BurntSushi/graphics-go/graphics github.com/fsnotify/fsnotify golang.org/x/sys/unix \
         gopkg.in/yaml.v2 github.com/axgle/mahonia github.com/msteinert/pam

  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' dde-daemon-$pkgver/accounts/user.go
}

build() {
  cd dde-daemon-$pkgver
  make # USE_GCCGO=1
}

package() {
  cd dde-daemon-$pkgver
  make DESTDIR="$pkgdir" install # USE_GCCGO=1

  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
}
