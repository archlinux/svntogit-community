# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=2.93.1
#_golibver=0.4.2
_golibrev=238151791673412dedf333363b3a37343c461acb
_deepinapiver=2.92.1
_dbusfactoryver=2.92.1
_desktopbaserev=df35ee9bbe33cca436c256a145b88e58e011b737
pkgrel=5
pkgdesc='Daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'metacity' 'poppler-glib'
         'rfkill' 'acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
         'mobile-broadband-provider-info' 'polkit-gnome' 'udisks2' 'upower' 'gcc-go'
         'libxkbfile' 'accountsservice')
makedepends=('deepin-dbus-generator' 'sqlite' 'git' 'mercurial')
optdepends=('networkmanager: Network Management daemon'
            'deepin-grub2-themes: deepin theme for grub menu')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/dde-daemon.git#tag=$pkgver"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        "git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver"
        "git+https://github.com/linuxdeepin/deepin-desktop-base.git#commit=$_desktopbaserev"
        'fix-i686-compile.patch' 'deepin-daemon.sysusers' 'ishuman-via-login-defs.patch' 'get-distro-info.patch')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '1dccf88c5ce480560a4a2d73134e69f05135703fe34ccd5d9e2e5d7fe852efc5'
            '4482f2c82c3652040021dd43515f131184a0417e341dc37db487117012245e25'
            '182fd299b9f222ce8f94da9137fb671f95fbd32bd28becfaf8c97b9fdd488c65'
            '4b3f743b8cffc591ab5582aa4ba6a56f464cd7c279a3594e637fffac0a14df63')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/
  
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         code.google.com/p/graphics-go/graphics code.google.com/p/jamslam-freetype-go/freetype \
         code.google.com/p/jamslam-freetype-go/freetype/truetype

  cd dde-daemon
  # TODO: a temporary solution by disable go-sqlite3 to avoid build
  #       issue for go1.4/i686
  # https://github.com/golang/go/issues/9510
  if [[ $CARCH == "i686" ]]; then
    patch -p1 -i ../fix-i686-compile.patch
  fi

  patch -p1 -i ../ishuman-via-login-defs.patch
  patch -p1 -i ../get-distro-info.patch
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/dde-daemon"
  make USE_GCCGO=1
}

package() {
  cd dde-daemon
  make USE_GCCGO=1 DESTDIR="${pkgdir}" install

  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
  install -Dm644 "${srcdir}"/deepin-desktop-base/desktop-version "${pkgdir}"/etc/deepin-version
}
