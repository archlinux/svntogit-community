# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dbus-generator
pkgver=0.6.4
_golibver=0.4.8
pkgrel=4
pkgdesc='Convert dbus interfaces to go-lang or qml wrapper code'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/go-dbus-generator"
license=('GPL3')
depends=('glibc' 'gcc-libs>=5.3.0-3')
makedepends=('qt5-declarative' 'git' 'gcc-go')
source=("git+https://github.com/linuxdeepin/go-dbus-generator.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver")
md5sums=('SKIP'
         'SKIP')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/lib
  cp -a go-lib/* build/src/pkg.deepin.io/lib
}

build() {
  cd go-dbus-generator
  go build -o out/dbus-generator -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  install -Dm 755 go-dbus-generator/out/dbus-generator "${pkgdir}"/usr/bin/dbus-generator
}
