# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dbus-generator
pkgver=0.6.6
pkgrel=1
pkgdesc='Convert dbus interfaces to go-lang or qml wrapper code'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/go-dbus-generator"
license=('GPL3')
depends=('glibc')
makedepends=('qt5-declarative' 'go' 'deepin-go-lib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-dbus-generator/archive/$pkgver.tar.gz")
sha512sums=('c3014127ecadda2c6e694e51222e4f559440c11403c3c5701836ba7a970c90b772f7dbc1aa41dbfe21c642e4a2fd60e1c8d1cc073d812c9cd2b4159844c757a6')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
}

build() {
  cd go-dbus-generator-$pkgver
  go build -o out/dbus-generator # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  install -Dm 755 go-dbus-generator-$pkgver/out/dbus-generator "$pkgdir"/usr/bin/dbus-generator
}
