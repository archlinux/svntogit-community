# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=5.1.0.11.20200622
_commit=e4acd32b24e220e66c283707b481f9757048ad7c
pkgrel=2
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory' 'deepin-network-utils' 'libdbusmenu-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$_commit.tar.gz"
         deepin-dock-qt5.15.patch
         deepin-dock-fix-build.patch)
sha512sums=('1d191dbcf23882f906eaf6f63ee1c93df33fcb792fe8d15bfbc2b64a694c2b9bbdff781ba00a02a965a23aff402959318e8b8419e8331446a1bebedf71e16a64'
            '1fb3a6490e152d081f5fdff655e503dac602cb29b231df252edd664bf8b5ffcc589efd7d96b7b61e53f620670b110dcd5e1952fe5b88fe65fbf8acf8ce8812fd'
            '38c3f683e10c70f54123d445b80831eb74830b27f265fccf7eb4037bfb0c96879bf25203e8e672ebe483e8cae291197dfac81296648f08548d61a7644c0076f9')

prepare() {
  patch -d dde-dock-$_commit -p1 -i ../deepin-dock-qt5.15.patch # Fix build with Qt 5.15
  patch -d dde-dock-$_commit -p1 -i ../deepin-dock-fix-build.patch # Fix build with new deepin-network-utils
}

build() {
  cd dde-dock-$_commit
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DDOCK_TRAY_USE_NATIVE_POPUP=YES
  ninja
}

package() {
  cd dde-dock-$_commit
  DESTDIR="$pkgdir" ninja install
}
