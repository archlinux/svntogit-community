# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=5.8.0.22
pkgrel=4
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-picker' 'deepin-qt5integration' 'freeimage' 'libexif' 'qt5-imageformats')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-draw/archive/$pkgver.tar.gz"
         deepin-draw-missing-includes.patch
         fix-build.patch)
sha512sums=('a63007d344edd8f094e73693d1d10618116139783eed3a2aaf0e43ddc32e26616b95bdcc0c6c59213e9c750289a739f67b34c8acdacf2cfa4d759fbaf795097a'
            '82016ef6a077d6887d52ff8e3f6fb6d3d2b10880cbeef76a54efa62551a62a7cbd38fb4a1a14fa5808039ab3ae1ad7c82275469386289fa42b5048c4d4eaeec7'
            'adc318a87cf0ce5aa597a7ad7680cadd6b6a9178b39eed7d3fb2ca9d0ddbfc68635bb252a0f9f8848d2b8e0e08e8ce102bb23df7015c051a20e0b210fc9d7f59')

prepare() {
  cd deepin-draw-$pkgver
  patch -p1 -i ../deepin-draw-missing-includes.patch # Fix missing includes
  sed -i '/include <DGraphicsView>/i #include <QFileDevice>' frame/cgraphicsview.h
  sed -i '/include <QPainter>/a #include <QMouseEvent>' widgets/ciconbutton.cpp
  patch -p1 -i ../fix-build.patch
}

build() {
  cd deepin-draw-$pkgver

  qmake-qt5
  make
}

package() {
  cd deepin-draw-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
