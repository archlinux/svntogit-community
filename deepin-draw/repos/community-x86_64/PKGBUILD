# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=5.10.6
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-picker' 'deepin-qt5integration')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-draw/archive/$pkgver.tar.gz")
sha512sums=('ca5730dfa38e9e61d41601689595bdd84d3a92aa9948378b6a217a94ee2a0ce217edb72f9f10eb87be993b7e0328b9a5eb8ec26aac7c7e7fd0f7bc88de081b55')

prepare() {
  cd deepin-draw-$pkgver
  sed -e '/set(CMAKE_CXX_FLAGS/d' -e '/set(CMAKE_C_FLAGS/d' -e '/SET(CMAKE_EXE_LINKER_FLAGS/d' \
      -i src/CMakeLists.txt
}

build() {
  cd deepin-draw-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-draw-$pkgver
  make DESTDIR="$pkgdir" install
}
