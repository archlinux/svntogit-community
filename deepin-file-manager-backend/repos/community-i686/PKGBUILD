# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager-backend
pkgver=0.1.12
_golibver=0.4.9
_deepinapiver=3.0.7
_dbusfactoryver=3.0.3
_girgeneratorver=0.9.3
pkgrel=1
pkgdesc='deepin file manager backend'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-file-manager-backend"
license=('GPL3')
depends=('deepin-metacity' 'libcanberra' 'poppler-glib' 'gcc-libs>=5.3.0-3')
makedepends=('git' 'deepin-dbus-generator' 'deepin-gir-generator' 'gcc-go')
provides=('deepin-file-manager')
conflicts=('deepin-file-manager')
replaces=('deepin-file-manager')
source=("git+https://cr.deepin.io/deepin-file-manager-backend.git#tag=$pkgver"
        "git+https://cr.deepin.io/go-lib.git#tag=$_golibver"
        "git+https://cr.deepin.io/dde/dde-api.git#tag=$_deepinapiver"
        "git+https://cr.deepin.io/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://cr.deepin.io/go-gir-generator.git#tag=$_girgeneratorver")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i "s|\"\${GOLDFLAGS}\"|\"\${GOLDFLAGS} $CFLAGS $LDFLAGS\"|" deepin-file-manager-backend/Makefile
  sed -i 's| generator$||' go-gir-generator/makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  go get github.com/howeyc/fsnotify github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/go-gir-generator"
  make GENERATOR=gir-generator build
  make copyfile
  cp -r out/src/gir "$GOPATH/src"/

  cd "$srcdir/$pkgname"
  make USE_GCCGO=1
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" do-install
}
