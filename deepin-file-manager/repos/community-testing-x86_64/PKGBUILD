# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=5.0.0
pkgrel=6
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-anything' 'disomaster' 'file' 'avfs' 'polkit-qt5' 'poppler'
         'ffmpegthumbnailer' 'xdg-user-dirs' 'jemalloc' 'kcodecs' 'taglib')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-movie' 'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'file-roller: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz"
         deepin-file-manager-qt-5.14.patch)
sha512sums=('045430f279417bb95ecbc55994207719f591e16ed0359ed337db797cf52ff1d7ec7d12d1c042e7837eaa9156c8dfe4f029f83b8ad567d2a9cc30552138b604ed'
            '04ee5dbd479e1fb7cc8e6371fd5c457e4f29ea373bc0058a4340db5397b0d47862c02fa1b23335ce6f2d259a935b9ebb5178f1f02dee585c92e849ec3366609d')

prepare() {
  cd dde-file-manager-$pkgver
  sed -i 's|systembusconf.path = /etc/dbus-1/system.d|systembusconf.path = /usr/share/dbus-1/system.d|' dde-file-manager-daemon/dde-file-manager-daemon.pro

  patch -p1 -i ../deepin-file-manager-qt-5.14.patch # Fix build with Qt 5.14
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
