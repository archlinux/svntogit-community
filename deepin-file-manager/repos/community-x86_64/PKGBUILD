# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.5.6.2
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs'
         'polkit-qt5' 'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual'
         'file-roller' 'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal'
         'gst-plugins-good' 'mpv' 'deepin-anything' 'deepin-movie')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz")
sha512sums=('ac0e95a04b816c3bc18d769f13fc80b4eb60e99fa388b246283d65a4b1a2c353cc955f04ac32b2f98114a34f764d33cc8db68f24133a655a7643254205bb23eb')

prepare() {
  cd dde-file-manager-$pkgver
  sed -i 's|service.path = /lib/systemd/system|service.path = /usr/lib/systemd/system|' dde-anythingmonitor/dde-anythingmonitor.pro
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
