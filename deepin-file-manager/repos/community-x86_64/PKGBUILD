# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.4.4
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('dtkwidget' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual' 'file-roller'
         'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal' 'gst-plugins-good' 'mpv')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz")
sha512sums=('f6404d63975781004aac7337dc9acaad3d7860ad991827807ee214671f9284e97589c58b35a4209f61a7e36b32f84a0cb947ade3f2d5c6e95b35b36c1606518c')

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
