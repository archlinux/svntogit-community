# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=5.5.10
pkgrel=4
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
options=('debug')
# startdde: for com.deepin.SessionManager
depends=('deepin-qt5integration' 'deepin-anything' 'disomaster' 'docparser' 'file' 'gio-qt'
         'libmediainfo' 'lucene++' 'avfs' 'polkit-qt5' 'poppler' 'ffmpegthumbnailer' 'jemalloc'
         'kcodecs' 'startdde' 'taglib' 'htmlcxx' 'libgsf' 'mimetic' 'boost-libs')
makedepends=('boost' 'qt5-tools' 'deepin-dock' 'deepin-movie' 'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'deepin-compressor: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-fix-crash-426.patch::https://github.com/linuxdeepin/dde-file-manager/pull/426.patch
        gcc12.patch)
sha512sums=('ac56de3396dc5d756b4c14011602da4996f253fb7a1f6fed920ba1a27fa35602aaab8278709feb8688e2c52b83a0e964676346e43b0d63cee5f76f30d2fc34ab'
            'aaa093d4ee601902cd4e391e51053812297e8730fbbebfb9cb7c6a7746bd2ce7bffc83753863d58f4a4eaf0e514c45a26ff1635ababd6653ff38cb4ae1aab21b'
            '4e3a35742efd43ecfc70a9bfa7e41b80e73a9197f0afa922d300ac27d655286d6fe3c8891c8a13bf08fd1e0e0b0d6a03e63f7f8af18e00673ea51db549fd5f46')

prepare() {
  cd dde-file-manager-$pkgver
  patch -p1 -i ../$pkgname-fix-crash-426.patch
  patch -p1 -i ../gcc12.patch # Fix build with GCC 12
  sed -e 's|systembusconf.path = /etc/dbus-1/system.d|systembusconf.path = /usr/share/dbus-1/system.d|' \
      -e 's|systemd_service.path = /lib/systemd/system|systemd_service.path = /usr/lib/systemd/system|' \
      -i src/dde-file-manager-daemon/dde-file-manager-daemon.pro
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr filemanager.pro
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
