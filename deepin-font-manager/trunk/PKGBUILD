# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=5.8.0.22
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'deepin-file-manager' 'gmock' 'gtest' 'ninja' 'qt5-tools')
optdepends=('deepin-file-manager: font preview in file manager')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-font-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7253fd753c6d30e2d3385c2d26655f8eb7004272c256f446c7e6f08c00e5e07101438a160a7bde1f59b561236d8dc20cf98b7d59f9cca670d12e9298a9b8ca19')

prepare() {
  cd deepin-font-manager-$pkgver

  # Fix linker flags
  sed -i 's/CMAKE_EXE_LINKER_FLAGS "-pie"/CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} -pie"/' deepin-font-manager/CMakeLists.txt

  # Fix file manager plugin install dir
  sed -i 's|lib/x86_64-linux-gnu/|lib/|' deepin-font-preview-plugin/CMakeLists.txt
}

build() {
  cd deepin-font-manager-$pkgver
  cmake -GNinja . -DPLUGIN_INSTALL_DIR=
  ninja
}

check() {
  cd deepin-font-manager-$pkgver
  ninja test || :
}

package() {
  cd deepin-font-manager-$pkgver
  DESTDIR="$pkgdir" ninja install
}
