# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-gir-generator
pkgver=0.9.6
pkgrel=4
pkgdesc='Generate static golang bindings for GObject'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/go-gir-generator"
license=('GPL3')
depends=('gobject-introspection' 'libgudev')
makedepends=('git' 'go')
source=("git+https://cr.deepin.io/go-gir-generator.git#tag=$pkgver"
        SettingsBackendLike.patch)
md5sums=('SKIP'
         'bf367edebfc344b050d35fd0fb56212d')

prepare() {
  export GOPATH="$srcdir/go-gir-generator/vender:$srcdir/go-gir-generator"

  cd go-gir-generator
  # Our gobject-introspection is too new
  # https://cr.deepin.io/#/c/16880/
  patch -p1 -i ../SettingsBackendLike.patch
}

build() {
  cd go-gir-generator
  make
}

package() {
  cd go-gir-generator
  make DESTDIR="$pkgdir" install
}
