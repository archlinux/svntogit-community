# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-gir-generator
pkgver=1.0.2
pkgrel=2
pkgdesc='Generate static golang bindings for GObject'
arch=('x86_64')
url="https://github.com/linuxdeepin/go-gir-generator"
license=('GPL3')
depends=('gobject-introspection' 'libgudev')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-gir-generator/archive/$pkgver.tar.gz"
        SettingsBackendLike.patch)
sha512sums=('58572fc6e11b8963224f80557a7ad57f7e628259024dbd110f2f5041593b14f4570003b5c069af5a3583f7b4f294a6a28ee3d2ad3f3821140f357d8bf861222b'
            'b36b10c01afb33f7bb054a928d4c5e0a017fac35e38992fe441bfcb9224b3aebd55e77a2f740a7b439221694f103a0edd102bd9bc0ef72a45512514886a90ea8')

prepare() {
  export GOPATH="$srcdir/go-gir-generator/vender:$srcdir/go-gir-generator"

  cd go-gir-generator-$pkgver
  # Our gobject-introspection is too new
  # https://cr.deepin.io/#/c/16880/
  patch -p1 -i ../SettingsBackendLike.patch
}

build() {
  cd go-gir-generator-$pkgver
  make
}

package() {
  cd go-gir-generator-$pkgver
  make DESTDIR="$pkgdir" install
}
