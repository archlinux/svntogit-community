# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2020.06.30
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver.tar.gz"
        $pkgname-fix-installation.patch::https://github.com/linuxdeepin/deepin-icon-theme/pull/23.patch)
sha512sums=('6800d244980c2c25dcfd8fe87deb878cc4c0297bc8fd1d063b605b0808d11150eceb191b76e02314bfd77907b9900f2626fae54913b8db03b9d0924e382ee988'
            'de3365adc47d824ae8f527db7b68f568d623bf5f96ab3f511146594aee57db8fd587dc6d2c5639283f620f6f19fa8dd5b789c703dfa7eaf571bd766e397cba23')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-fix-installation.patch
  sed -i 's/deepin/bloom/g' $pkgname-$pkgver/tools/hicolor.links
}

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
