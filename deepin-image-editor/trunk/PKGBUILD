# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-editor
pkgver=1.0.11
pkgrel=1
pkgdesc='Public library for deepin-image-viewer and deepin-album'
arch=('x86_64')
url="https://github.com/linuxdeepin/image-editor"
license=('GPL3')
depends=('dtkwidget' 'freeimage' 'opencv')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/image-editor/archive/$pkgver.tar.gz"
        remove-broken-flags.patch)
sha512sums=('f9f60d91822d6f1f8c53397001b0699eebde692b3f285b2651fe71745d59f1073283f87d9da077a2305f7b0099d504a881f413fd677fc8d690a1e85245b9d2ea'
            '033b26be1d5e4c5b2d4452fa1e191cd06377eb1a6ae57adfa9a2e79ead3da2433dc26301ed0cc826c3b0daffc94119b2767cf003b41beb235bcccdcdd34f7491')

prepare() {
  patch -d image-editor-$pkgver -p2 < remove-broken-flags.patch
}

build() {
  cd image-editor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd image-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
