# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.8.13
pkgrel=3
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-image-editor' 'libexif' 'libraw' 'qt5-svg')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help manual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz"
        remove-unused-deps.patch)
sha512sums=('a3963e22befc36d7c6240975555342c05a7bb349dc7c074d4f488fce49d2e36c91f5873ab03788e91023be8f4bb99a72f66f0b3ad2190548cecb28aa8c3840bc'
            '297dc6010046af99dc2407fc2779e8182cac846ccec2c1b09d7ba628ad886779efd28c020dd8104c11ba57ab0bc1d63c4d1185dadf56991a964dd52085c52fc6')

prepare() {
  patch -d deepin-image-viewer-$pkgver -p1 < remove-unused-deps.patch
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
