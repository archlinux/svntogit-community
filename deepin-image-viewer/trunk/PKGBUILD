# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.9.4
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-image-editor' 'libexif' 'libraw' 'qt5-svg')
makedepends=('cmake' 'ninja' 'qt5-tools')
optdepends=('deepin-manual: for help manual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz"
        $pkgname-ldflags.patch::https://github.com/linuxdeepin/deepin-image-viewer/pull/110.patch)
sha512sums=('d6ab0456a32e00bdaf68a4627a9539817100de67f0b3e08998281cf879d0c9b14e10496be75cf6329d69b03ab27567e635f9ecf1c237e73a7c0a07e35891fe05'
            '93bb0bc2cfb27a3edb4d1e3dac22f7f32cf5561efa83641b90d80be2f6fa944eb438b298f8418c75fb659d6ef3bb9a600cfbbdcc501c0ad7145b017f45d10f67')

prepare() {
  cd deepin-image-viewer-$pkgver
  patch -p1 -i ../$pkgname-ldflags.patch
  sed -i -e 's|qt5/plugins|qt/plugins|' -e 's|${Qt5Core_DIR}|/usr/lib/cmake/Qt5Core|' qimage-plugins/libraw/CMakeLists.txt
}

build() {
  cd deepin-image-viewer-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  ninja
}

package() {
  cd deepin-image-viewer-$pkgver
  DESTDIR="$pkgdir" ninja install
}
