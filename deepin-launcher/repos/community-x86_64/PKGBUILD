# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=5.5.19.1
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'xdg-user-dirs')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('dfb2b0d5fa9b17e35fc787d0e6fbdab7b539ae56a0f8046e5f9a4698094aa3e055544fcc3965a13487bca0003991bc3d499dbac7692c6c36960adf4208da1860')

prepare() {
  cd dde-launcher-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  ninja
}

check() {
  cd dde-launcher-$pkgver
  # ninja test
}

package() {
  cd dde-launcher-$pkgver
  DESTDIR="$pkgdir" ninja install
}
