# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=4.6.4
pkgrel=2
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'qt5-tools' 'xdg-user-dirs')
makedepends=('cmake')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz"
        disable-uninstall.patch)
sha512sums=('3546e7627f11fef7a096161cb959fb6d898661866de0a39f997e741f92b7d154601980f2603011c716c46d4c365100f63bcf116e47ea2760208e077cdccadea5'
            '3689b2605f72a4394a8cc0822671b4c174d893b6e826dd8f54a342d94001cf8191395048ac11b693c6cc45286b6a3d74dfb86af29f9e51d528e26b4afe4c60f4')

prepare() {
  cd dde-launcher-$pkgver
  patch -p1 -i ../disable-uninstall.patch
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  make
}

package() {
  cd dde-launcher-$pkgver
  make DESTDIR="$pkgdir" install
}
