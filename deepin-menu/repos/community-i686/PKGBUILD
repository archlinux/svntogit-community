# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-menu
pkgver=3.0.9
pkgrel=1
pkgdesc="Deepin menu service for building beautiful menus"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-menu"
license=('GPL3')
depends=('python2-pyqt5' 'deepin-tool-kit' 'deepin-qt-dbus-factory')
makedepends=('python2-setuptools' 'git')
groups=('deepin')
source=("git+https://cr.deepin.io/deepin-menu.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd deepin-menu

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-menu

  python2 setup.py build
  qmake-qt5 DEFINES+=QT_NO_DEBUG_OUTPUT
  make
}

package() {
  cd deepin-menu

  python2 setup.py install --root="${pkgdir}" --optimize=1

  make INSTALL_ROOT="$pkgdir" install

  install -dm 755 "${pkgdir}"/usr/share/dbus-1/services/
  install -dm 755 "${pkgdir}"/etc/xdg/autostart/

  rm -r "${pkgdir}"/usr/deepin_menu

  install -m 644 data/*.service "${pkgdir}"/usr/share/dbus-1/services/
  install -m 644 *.desktop "${pkgdir}"/etc/xdg/autostart/
}
