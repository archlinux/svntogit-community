# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-menu
pkgver=3.2.0
pkgrel=3
pkgdesc="Deepin menu service for building beautiful menus"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-menu"
license=('GPL3')
depends=('python2-pyqt5' 'deepin-qt5integration')
makedepends=('python2-setuptools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-menu/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/deepin-menu/commit/c0c06f39cdc8af1bd6e39751e243f276df325102.patch)
sha512sums=('9961e0f477261ebc3f2405f95c56d1d085ec387cf2b4dc87b4cefbd17559b442110a04c45a86f1988fbdf0c4b419b05025b0c465335b1d9d7143aeec8236a958'
            '826e545cebaa40d48d4862bfbb968bc3e8774724cb122f3a9b7d779a7f4bd2e80ac01648ae1bc7492804b4db040083791298553c90b03384343d40c1be364de6')

prepare() {
  cd deepin-menu-$pkgver
  patch -p1 -i ../c0c06f39cdc8af1bd6e39751e243f276df325102.patch

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-menu-$pkgver

  python2 setup.py build
  qmake-qt5 DEFINES+=QT_NO_DEBUG_OUTPUT
  make
}

package() {
  cd deepin-menu-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1

  make INSTALL_ROOT="$pkgdir" install

  install -dm 755 "$pkgdir"/usr/share/dbus-1/services/
  install -dm 755 "$pkgdir"/etc/xdg/autostart/

  rm -r "$pkgdir"/usr/deepin_menu

  install -m 644 data/*.service "$pkgdir"/usr/share/dbus-1/services/
  install -m 644 *.desktop "$pkgdir"/etc/xdg/autostart/
}
