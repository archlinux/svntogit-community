# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-menu
pkgver=3.1.8
pkgrel=1
pkgdesc="Deepin menu service for building beautiful menus"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-menu"
license=('GPL3')
depends=('python2-pyqt5' 'dtkwidget' 'deepin-qt-dbus-factory')
makedepends=('python2-setuptools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-menu/archive/$pkgver.tar.gz")
sha512sums=('7e05306ef68e9346c28efecc87c7eddb4742cc81fe92537eac4aecca07e18cb4c12ef634990a3dd3521f40178597087ee65dd1d584f4ecd220d9b6c10b4b90ee')

prepare() {
  cd deepin-menu-$pkgver

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-menu-$pkgver

  python2 setup.py build
  qmake-qt5 DEFINES+=QT_NO_DEBUG_OUTPUT
  make
}

package() {
  cd deepin-menu-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1

  make INSTALL_ROOT="$pkgdir" install

  install -dm 755 "$pkgdir"/usr/share/dbus-1/services/
  install -dm 755 "$pkgdir"/etc/xdg/autostart/

  rm -r "$pkgdir"/usr/deepin_menu

  install -m 644 data/*.service "$pkgdir"/usr/share/dbus-1/services/
  install -m 644 *.desktop "$pkgdir"/etc/xdg/autostart/
}
