# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=deepin-metacity
pkgver=3.22.10
pkgrel=1
pkgdesc="2D window manager for Deepin"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'deepin-desktop-schemas' 'gtk3' 'startup-notification' 'zenity' 'bamf')
makedepends=('intltool' 'itstool' 'autoconf-archive' 'yelp-tools' 'python')
url="https://github.com/linuxdeepin/deepin-metacity"
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-metacity/archive/$pkgver.tar.gz")
sha512sums=('49a0c6c0c3531c12ca0877fed3cf2b53a0dbf1ad0d0ef8699e08a0fa3e8edeef9bf60e20889b17001e8d66f8e2f3642f557a2af4bdee74c65b823073a60d5e06')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
