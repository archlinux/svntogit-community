# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=deepin-metacity
pkgver=3.22.19
pkgrel=2
pkgdesc="2D window manager for Deepin"
arch=('x86_64')
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'deepin-desktop-schemas' 'gtk3' 'startup-notification' 'zenity' 'bamf')
makedepends=('intltool' 'itstool' 'autoconf-archive' 'yelp-tools' 'python')
url="https://github.com/linuxdeepin/deepin-metacity"
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-metacity/archive/$pkgver.tar.gz")
sha512sums=('e0701affb75085aebb3da6d03793f010cd8ac5b319ad4bd73869bc44d442417386cc37d3f5e2398b75dfbffb570407a520450a3cc93ba3a8ac5f4d24abcdd949')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool  

  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/ui/*.c
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
