# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=6.2.21
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpeg' 'kcodecs' 'libcue' 'qt5-svg' 'qtdbusextended' 'qtmpris'
         'taglib' 'udisks2-qt5' 'vlc')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
options=('!lto')
source=("https://github.com/linuxdeepin/deepin-music/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d7f9938ea7678679e665029a08b16f077364e330d1484b1fb72fe5e6ad5d3998187ebd0f791346ab4fc440698ab495eb9ddcf93241931f392239653b453a43d7')

build() {
  cd deepin-music-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DVERSION=$pkgver .
  ninja
}

package() {
  cd deepin-music-$pkgver
  DESTDIR="$pkgdir" ninja install
}
