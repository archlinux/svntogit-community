# $Id: PKGBUILD 145358 2015-10-31 12:11:57Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-mutter
pkgver=3.18.2.0.20151214
_commit=319cede09f5c969c8fa9bf12d5acc80cb60c1788
pkgrel=1
pkgdesc="Base window manager for deepin, fork of gnome mutter"
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/deepin-mutter"
license=('GPL3')
depends=('clutter' 'dconf' 'gobject-introspection-runtime' 'deepin-desktop-schemas'
         'libcanberra' 'startup-notification' 'zenity' 'libsm' 'gnome-desktop' 'upower'
         'libxkbcommon-x11' 'libgudev')
makedepends=('gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gtk-doc' 'intltool' 'git')
options=('!emptydirs')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/deepin-mutter.git#commit=$_commit")
md5sums=('SKIP')

build() {
  cd "${pkgname}"
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/deepin-mutter \
    --localstatedir=/var \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-native-backend \
    --disable-static \
    --disable-schemas-compile \
    --enable-compile-warnings=minimum
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
}
