# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-utils
pkgver=5.3.0.8
pkgrel=1
pkgdesc='DDE network utils'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-utils"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'gio-qt' 'gsettings-qt')
makedepends=('qt5-tools' 'gtest')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-utils/archive/$pkgver.tar.gz")
sha512sums=('c16af4b81c9d68fb3ab1fa7251f399a7f760df0f40f121e7cefeec1c4871650bac54b138e5864491f7c6cce44963f16a4fb30eca7af907c6483d630421a0219c')

prepare() {
  cd dde-network-utils-$pkgver
  # Use our own url instead of commercial company's homepage
  sed -i '/www.uniontech.com/i \    "https://www.archlinux.org/favicon.ico",' dde-network-utils/connectivitychecker.cpp
}

build() {
  cd dde-network-utils-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

check() {
  cd dde-network-utils-$pkgver
  # make test
}

package() {
  cd dde-network-utils-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
