# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-notifications
pkgver=3.3.4
pkgrel=2
pkgdesc="System notifications for linuxdeepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-notifications"
license=('GPL3')
depends=('deepin-qt5integration' 'qt5-svg' 'qt5-declarative')
provides=('notification-daemon')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/deepin-notifications/archive/$pkgver.tar.gz"
        fix-crash.patch)
sha512sums=('324aa2109a2f2f983e0ea6f0ae5278cbfb557eab3771b89501b4ac2bf1840d3b051f8f6a046e378d6dcb30f4ab5b0e4ebe0ce6ce786cf07270b92efecdec3597'
            'a5a37e7df5d772f02c072d6519148920c450cce3cd47b6526f3870953c49f2de517b5d7add4738e20bc748e45dcfc13c64a7aa30861fb0b6c7b914ca35893318')

prepare() {
  cd deepin-notifications-$pkgver/src
  patch -p3 -i "$srcdir"/fix-crash.patch
}

build() {
  cd deepin-notifications-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-notifications-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
