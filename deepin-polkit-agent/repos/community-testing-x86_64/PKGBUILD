# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=5.4.3
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'polkit-qt5')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-polkit-agent/archive/$pkgver.tar.gz")
sha512sums=('8793a73412eb74e68d72903c5ef0a901d8c8656441fff6b6e5cafa80098a79138e25eec60ffc20ebc09769af4aa22203c36d113cea7cb5429e94210006297368')

prepare() {
  cd dde-polkit-agent-$pkgver
  # https://github.com/linuxdeepin/developer-center/issues/1721
  sed -i 's/bool is_deepin = true/bool is_deepin = false/' policykitlistener.cpp
  # https://github.com/linuxdeepin/dde-polkit-agent/issues/26
  sed -i '/setCancel/d' policykitlistener.cpp
}

build() {
  cd dde-polkit-agent-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-polkit-agent-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
