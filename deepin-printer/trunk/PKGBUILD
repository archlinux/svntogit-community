# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.6.8
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
depends=('crypto++' 'cups' 'deepin-qt5integration' 'libcups' 'smbclient')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-printer/archive/$pkgver.tar.gz"
        build-fix.patch)
sha512sums=('634608b63f83762b701dd49e318003d6af2f8c4706f0638a6ef3800f21b3f2fab8641341ac6131671f06c611f856da704acbd292fbe95c27fafd6f45d6cca13b'
            '1312037fd47a1e50385d9011269a331cd745d89313ea94a827c743938214d9cd447d74038b96b9082a0100a62314919487fe6ee65e4341e4d3f2880417896555')

prepare() {
  cd dde-printer-$pkgver
  patch -p1 -i ../build-fix.patch
  sed -i '/include <QEventLoop>/a #include <QCloseEvent>' src/Printer/ui/dprintersshowwindow.h
  sed -i '/include <QList>/a #include <QFile>' src/Printer/vendor/zdrivermanager.h
  sed -i 's/-lcrypto++/-lcryptopp/' src/Printer/Printer.pro
  sed -i '$aOnlyShowIn=Deepin' src/Printer/platform/linux/watch/dde-printer-watch.desktop
}

build() {
  cd dde-printer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
