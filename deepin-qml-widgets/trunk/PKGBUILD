# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-qml-widgets
pkgver=2.3.4
_dbusfactoryver=3.0.6
pkgrel=2
pkgdesc='Deepin QML widgets'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-qml-widgets"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-webkit' 'qt5-imageformats'
         'qt5-graphicaleffects' 'qt5-x11extras' 'gtk2')
makedepends=('git' 'deepin-dbus-generator')
conflicts=('dde-qml-gettext-plugin')
provides=('dde-qml-gettext-plugin')
replaces=('dde-qml-gettext-plugin')
source=("git+https://github.com/linuxdeepin/deepin-qml-widgets.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver")
sha256sums=('SKIP'
            'SKIP')

build(){
  cd "$srcdir"/deepin-qml-widgets
  qmake-qt5
  make

  cd "$srcdir"/dbus-factory
  make build-qml
}

package() {
  cd "$srcdir"/deepin-qml-widgets
  make INSTALL_ROOT="$pkgdir" install

  cd "$srcdir"/dbus-factory
  make DESTDIR="$pkgdir" QT5_LIBDIR="/usr/lib/qt" install-qml
}
