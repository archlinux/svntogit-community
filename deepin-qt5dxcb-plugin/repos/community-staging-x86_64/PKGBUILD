# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.1.23
pkgrel=2
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz"
        deepin-qt5dxcb-plugin-qt5.12.1.patch)
sha512sums=('d53f5635cf852ee0a271caf057e68853eb8fa9b24819bbf87ccac53e29fc255925c8d5b3fde68a6cc6ef7e5d8ba208e53d95ad1d9916e9e0b326feeaea46e8f1'
            'b6aacb3e321a53f2e6c8af43b901ff7c908346ffabbaf37d7008267b80dea79cafd65f6230fdd8b7be3a9e4b8b7d343864128543123931c0649ece9d846f385a')

prepare() {
  cd qt5dxcb-plugin-$pkgver

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri

  patch -p1 -i ../deepin-qt5dxcb-plugin-qt5.12.1.patch # Fix build with Qt 5.12.1
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
