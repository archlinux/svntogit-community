# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.0.0
pkgrel=1
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'deepin-qt5dxcb-plugin')
makedepends=('xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        deepin-qt5integration-qt5.13.patch)
sha512sums=('bfaa0cf1ca1422da1e645e86275b290c186cb2e26264b3844afd77f31cdc2d1e4ac1b54bcc58a46b336534146865a0310b802f2477f7505f666aed76a2788f00'
            '345590b7fa94c84adc0c894df8c70931a46769f7af40edf1e5d3623a5327b344be48e17db10791c8f75bf626874f2f031ef221c1218c6b0d889eca3b883c0764')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../deepin-qt5integration-qt5.13.patch # Fix build with Qt 5.13
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
