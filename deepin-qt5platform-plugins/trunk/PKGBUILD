# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.0.67
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('GPL3')
provides=('deepin-qt5dxcb-plugin')
conflicts=('deepin-qt5dxcb-plugin')
replaces=('deepin-qt5dxcb-plugin')
options=('debug')
depends=('cairo' 'deepin-wayland' 'kwayland' 'qt5-x11extras')
makedepends=('expac' 'qt5-xcb-private-headers' 'libglvnd' 'libxcb')
groups=('deepin')
source=("https://github.com/linuxdeepin/qt5platform-plugins/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1a1cb2e3ae5ce6ca06a9e4ba0bef558bfaf0421eea7e9c58955e43101f1dbdec173f10e04e67cc4dba8750d6bae1d62e341d5997adbd80f62ace1dac0bb81927')

prepare() {
  cd qt5platform-plugins-$pkgver

  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev

  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' xcb/linux.pri
  sed -i '/wayland/d' qt5platform-plugins.pro
}

build() {
  cd qt5platform-plugins-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5platform-plugins-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
