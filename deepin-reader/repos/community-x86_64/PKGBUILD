# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.7.0.13
pkgrel=1
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'djvulibre' 'karchive' 'libspectre' 'poppler-qt5')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-reader/archive/$pkgver.tar.gz")
sha512sums=('72184faf5efb0ef00e32c01fcc7c03cf9ba65de6d3d70dd7e4d45c5b060b12f87eb095d65e7b72f4c8bbaf29e2b7e5565d642ca263848582b0e09a22c0b903e2')

prepare() {
  cd deepin-reader-$pkgver/src
  sed -i '/#include <QDebug>/a #include <QPainterPath>' document/Model.h
  sed -i '/include <QPainter>/a #include <QPainterPath>' app/Utils.cpp widgets/RoundColorWidget.cpp browser/BrowserMagniFier.cpp sidebar/bookmark/BookMarkDelegate.cpp \
                                                         sidebar/thumbnail/ThumbnailDelegate.cpp document/DjVuModel.cpp sidebar/note/NoteViewWidget.cpp sidebar/note/NotesDelegate.cpp \
                                                         sidebar/search/SearchResDelegate.cpp
  sed -i '/include <QMap>/a #include <QSet>' uiframe/DocSheet.h
  sed -i '/include <QLabel>/a #include <QMouseEvent>' browser/SheetBrowser.h
  sed -i '/include <QDesktopWidget>/a #include <QMouseEvent>' widgets/SlideWidget.cpp
}

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # What the bloody hell is going on here...
  cd "$pkgdir"/usr/lib
  rm libdpoppler-qt.so{,.1} libdpoppler.so{,.100}
  ln -s libdpoppler-qt.so.* libdpoppler-qt.so.1
  ln -s libdpoppler-qt.so.1 libdpoppler-qt.so
  ln -s libdpoppler.so.* libdpoppler.so.100
  ln -s libdpoppler.so.100 libdpoppler.so
}
