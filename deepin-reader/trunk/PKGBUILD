# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.10.13
pkgrel=1
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'djvulibre' 'karchive' 'libchardet' 'libspectre' 'poppler-qt5'
         'qt5-webengine')
makedepends=('cmake' 'gtest' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-reader/archive/$pkgver/$pkgname-$pkgver.tar.gz"
         openjpeg-2.5.patch)
sha512sums=('c5304bb6828d80a7c1412e5a36a980100c9e605a82eaa8a97f7ba4a78d04ef24c2d3d493c858e0ec95f6e9f90b56d177169f0deb87c7fee94b588a2a3b5e45aa'
            '4ff8c063cc464a830cee8a5474329127c75f4298819f3384cb01d26cbcff38663eb89ff5ca69e1e800e28e509901c3671898c4dbda4b8426551b56c074af639b')

prepare() {
  cd deepin-reader-$pkgver
  patch -p1 -i ../openjpeg-2.5.patch # Fix build with openjpeg 2.5
}

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
