# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.9.3
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('byzanz' 'deepin-qt5integration' 'deepin-dock' 'deepin-turbo' 'ffmpeg' 'kconfig' 'ki18n' 'kwindowsystem')
makedepends=('qt5-tools')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('50df395dcb20fa031c174fd231889dd39b725c1ce1b344862e66ac32707dbcd147c4bade5d3bc28fad457c2810dbad8bdad5bdcd4cee26ac0108e82a3927918e')

prepare() {
  cd deepin-screen-recorder-$pkgver

  # X11 header's weirdness with GCC 10
  sed -i '/include <X11.extensions.XTest.h>/a #undef min' src/event_monitor.cpp
  sed -i '/#include <iostream>/d;1i #include <iostream>' src/screen_shot_event.cpp
  sed -i '/include <X11.extensions.shape.h>/a #undef None' src/utils.cpp
}

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
