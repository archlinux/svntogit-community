# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.8.0.14
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('byzanz' 'deepin-qt5integration' 'deepin-turbo' 'ffmpeg' 'ki18n' 'kwindowsystem')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver.tar.gz"
         deepin-screen-recorder-qt5.15.patch)
sha512sums=('88a8c9c151745601cbde1a72a2ac909d037addf8b46af50ab829416e4caef95553d1e283022a9fc1a5562e5fcc6ebc52dc84d464093a693522900dee50c2ece7'
            'aaef789b5b1e74c9b2972bf94134b639393247ea4c6459e90d0b875d692b6adca71cafc27f1cc402fc81126eb47c52ae816867d7a3d2dfa775594dd839840247')

prepare() {
  cd deepin-screen-recorder-$pkgver
  sed -i '/include <X11.extensions.XTest.h>/a #undef min' src/event_monitor.cpp

  patch -p1 -i ../deepin-screen-recorder-qt5.15.patch # Fix build with Qt 5.15
}

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
