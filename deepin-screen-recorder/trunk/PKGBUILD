# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.10.9
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-turbo' 'ffmpeg' 'libxtst' 'opencv')
makedepends=('deepin-dock')
optdepends=('deepin-shortcut-viewer: for shortcut display')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fc8578c1d41faaa8151c36e24edbf54b3325a3bef45d67fbc2feefc1789b0cdf677b84caf7f3620dc097eb49225b36e884ef327dce5bccb60bf064b9b5f9e580')

prepare() {
  cd deepin-screen-recorder-$pkgver

  # OpenCV 4 compatibility
  sed -i '/#include<opencv2/i #include <opencv2/imgproc/types_c.h>' src/utils/pixmergethread.h

  # OpenCV missing in pkg-config targets
  sed -i 's/dframeworkdbus/dframeworkdbus opencv4/' src/src.pro

  # X11 header's weirdness with GCC 10
  sed -i '/include <X11.extensions.XTest.h>/a #undef min' src/event_monitor.cpp
  sed -i '/#include <iostream>/d;1i #include <iostream>' src/screen_shot_event.cpp
}

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
