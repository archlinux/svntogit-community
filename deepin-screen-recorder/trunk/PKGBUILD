# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.11.2
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-turbo' 'ffmpeg' 'libxtst' 'opencv')
makedepends=('deepin-dock')
optdepends=('deepin-shortcut-viewer: for shortcut display')
provides=('deepin-screenshot')
conflicts=('deepin-screenshot')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d6408dba0d7cb3105c0cb7c97100d30e38c3873f133e4908167349cebc9a6a3619a7a68bbc9961d22c5d14589856def55a690ce91eae7377cd6f19fa2b9994a3')

prepare() {
  cd deepin-screen-recorder-$pkgver

  # https://github.com/linuxdeepin/developer-center/issues/3035
  sed -i "s^cat /etc/os-version | grep 'Community'^echo 'Community'^" src/src.pro

  # OpenCV 4 compatibility
  sed -i '/#include<opencv2/i #include <opencv2/imgproc/types_c.h>' src/utils/pixmergethread.h

  # OpenCV missing in pkg-config targets
  sed -i 's/dframeworkdbus/dframeworkdbus opencv4/' src/src.pro

  # X11 header's weirdness with GCC 10
  sed -i '/include <X11.extensions.XTest.h>/a #undef min' src/event_monitor.cpp
}

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
