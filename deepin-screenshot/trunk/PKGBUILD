# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screenshot
pkgver=3.1.10
pkgrel=1
pkgdesc="Easy-to-use screenshot tool for linuxdeepin desktop environment"
arch=('any')
url="https://github.com/linuxdeepin/deepin-screenshot"
license=('GPL3')
depends=('deepin-menu' 'deepin-qml-widgets' 'pygtk' 'qt5-quickcontrols'
         'qt5-svg' 'python2-pyqt5' 'python2-wnck' 'python2-xpybutil'
         'python2-opengl')
makedepends=('deepin-gettext-tools' 'git')
optdepends=('deepin-social-sharing: for "Share" support'
            'deepin-daemon: for sound effect')
groups=('deepin-extra')
source=("git+https://cr.deepin.io/deepin-screenshot.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd deepin-screenshot

  # fix python version
  find src -type f | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-screenshot
  make
}

package() {
  cd deepin-screenshot
  make DESTDIR="${pkgdir}" install
}

 # Local Variables:
 # sh-basic-offset: 2
 # End:
