# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=5.5.23
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center' 'deepin-wallpapers')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz")
sha512sums=('bc975a0bc38e0e87d8e7316e93603aa9ecfe67061220d7571456b3a0e210a7c0134850d672f27f38e0721e5f0a57adec0a355c24facfec51d733f1a1716c8e5f')

prepare() {
  mkdir -p build

  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp
}

build() {
  cd build
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ../dde-session-ui-$pkgver
  ninja
}

check() {
  cd build
  # ninja test
}

package() {
  cd build
  DESTDIR=="$pkgdir" ninja install
}
