# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=5.9.28
pkgrel=2
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-dock' 'dtkwm' 'procps-ng' 'libcap')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('fd3b7bc478411dd953ed1bcc188b5676321eead985755937a9ed17c5d15c82c74a9238bbb9c31bd8f6b62e95b30393e6beebbd5a1b362738c83e052c4ac10ef3')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../deepin-system-monitor-$pkgver -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DUSE_DEEPIN_WAYLAND=OFF
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
