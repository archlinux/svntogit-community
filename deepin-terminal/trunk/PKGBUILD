# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=3.0.0
pkgrel=2
pkgdesc='Default terminal emulation application for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL3')
depends=('vte3' 'libsecret' 'libgee' 'libwnck3' 'expect' 'deepin-shortcut-viewer' 'deepin-menu')
makedepends=('vala' 'cmake')
optdepends=('deepin-manual: view the help manual'
            'zssh: for Remote Management support')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-terminal/archive/$pkgver.tar.gz"
        remove-vendor.patch)
sha512sums=('9e5906ae5d2690841cb14e99f93901cfe122298e00c897344c7c3fbc2751d4cbec63f335da8de30cf3746f67e3c8a2a5b8e39d9faa5b210f4e0772883556c8d6'
            '1eb83fd8a8844ad7ee914afab506d0cbbc900a8239f008be8113ff8eee98006b35b6e447f40b6cce7f15864a33d3d1b385476ac7946be98012bfdc78c956a373')

prepare() {
  mkdir -p build

  cd deepin-terminal-$pkgver

  sed -i 's|/usr/lib/deepin-terminal/zssh|/usr/bin/zssh|' ssh_login.sh

  patch -p1 -i ../remove-vendor.patch
}

build() {
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DTEST_BUILD=OFF \
    ../deepin-terminal-$pkgver
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
  ln -s deepin-terminal "$pkgdir"/usr/bin/x-terminal-emulator
}
