# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=5.2.11
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL3')
depends=('at-spi2-core' 'deepin-qt5integration')
makedepends=('chrpath' 'cmake' 'expac' 'lxqt-build-tools' 'ninja' 'qt5-tools')
optdepends=('zssh: for remote management support')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-terminal/archive/$pkgver.tar.gz")
sha512sums=('7706cb12ac8379d91fe3ec3c3de96c682888a68416862db5ba6213e9ff43b6110628d6489643b3ea155626fcaba9e3c82624f089ed160da7827a862ef4adc33e')

prepare() {
  mkdir -p build
  cd deepin-terminal-$pkgver
  # Devendor qtermwidget - disabled due to the vendored copy has changes
  #rm -r terminalwidget
  #sed -i '/terminalwidget/d;/build_qtermwidget/d' deepin-terminal.pro
  #sed -i 's|terminalwidget/lib/qtermwidget.h|qtermwidget5/qtermwidget.h|' encodeplugin/encodelistmodel.cpp views/termwidget.h theme/themelistmodel.cpp

  # Much upstream weirdness
  sed -i '/<QHash>/i#include <QObject>\n#include <QMap>' 3rdparty/terminalwidget/lib/SessionManager.h
  #sed -i 's/QString("/QString::fromLatin1("/;s/message = "Session crashed.";/message = QString::fromLatin1("Session crashed.");/' 3rdparty/terminalwidget/lib/{Filter,Session}.cpp
  sed -i '/LXQtCompilerSettings/a remove_definitions(-DQT_NO_CAST_FROM_ASCII -DQT_NO_CAST_TO_ASCII)' 3rdparty/terminalwidget/CMakeLists.txt
  sed -i '/#include <QTextStream>/a #include <cwctype>' 3rdparty/terminalwidget/lib/TerminalCharacterDecoder.cpp
  sed -i '/#include <QPainter>/a #include <QPainterPath>' src/customcommand/customcommanddelegate.cpp src/remotemanage/serverconfigdelegate.cpp src/theme/themeitemdelegate.cpp
  sed -i 's|default-config.json|src/assets/other/default-config.json|' CMakeLists.txt
}

build(){
  cd build
  cmake -GNinja -DDTKCORE_TOOL_DIR=/usr/lib/libdtk-$(expac %v dtkcore | cut -d - -f 1 | cut -d : -f 2)/DCore/bin ../deepin-terminal-$pkgver
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  rm -r "$pkgdir"/build
  chrpath --delete "$pkgdir"/usr/bin/deepin-terminal
}
