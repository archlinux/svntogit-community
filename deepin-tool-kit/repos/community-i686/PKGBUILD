# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-tool-kit
pkgver=0.2.10
pkgrel=2
pkgdesc='Base development tool of all C++/Qt Developer work on Deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-tool-kit"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-x11extras' 'startup-notification' 'dtksettings')
makedepends=('qt5-tools')
replaces=('libdui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-tool-kit/archive/$pkgver.tar.gz")
sha512sums=('6c2cde429885a7354de3f1ab9e62ddc078a4b05ab46436f91c48b93f7478dd814f527cb571872db2741039e136b9255d1b8974d08081665755ad6c228cf7d095')

build() {
  cd deepin-tool-kit-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-tool-kit-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
