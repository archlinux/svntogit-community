# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-turbo
pkgver=0.0.5
pkgrel=1
pkgdesc='A daemon that helps to launch applications faster'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-turbo"
license=('GPL3')
depends=('dtkwidget')
makedepends=('cmake' 'ninja')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-turbo/archive/$pkgver.tar.gz")
sha512sums=('161526adcbcb491aadd62c88a1d4749aee635f3c609b9dc01662c156cc2f2c08ca5ce9b5bf1ec88f0f5b07916b4dcd91de27395d6d4763b9b105e3d7eef0edd3')

prepare() {
  cd deepin-turbo-$pkgver
  sed -i 's/MODULE/SHARED/' src/launcherlib/CMakeLists.txt
  sed -i '/-ldeepin-turbo/d;/Qt5::Widgets/a\    deepin-turbo' src/booster-dtkwidget/CMakeLists.txt
}

build() {
  cd deepin-turbo-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  ninja
}

package() {
  cd deepin-turbo-$pkgver
  DESTDIR="$pkgdir" ninja install
}
