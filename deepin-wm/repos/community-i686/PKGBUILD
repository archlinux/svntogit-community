# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.8
pkgrel=1
pkgdesc='Deepin Window Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'granite'
         'deepin-mutter' 'gnome-desktop' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz")
sha512sums=('1730e97e7e65f4359fc803f39e0a2a4fe6bfb0b0caee21306fe4c8012c011c872af981109f5ee6a94497c00c5675658f816d258ee8be0995b1110eb7435ef5d1')

prepare() {
  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
