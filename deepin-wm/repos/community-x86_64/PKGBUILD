# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.33
pkgrel=1
pkgdesc='Deepin Window Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'deepin-menu' 'granite' 'deepin-mutter'
         'gnome-desktop' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz")
sha512sums=('989cbb3f8db7640a9c1082c8c0d50b26e92053664b96bcc781d48a4314c57746a50cc1e7fa94feac3b263b1e7bb8ccb2ab6753b4e6db4a34f6d6393419dc0d9d')

prepare() {
  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"

  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' $pkgname-$pkgver/src/Background/BackgroundSource.vala
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
