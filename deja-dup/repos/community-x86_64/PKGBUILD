# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=43.3
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/DejaDup"
license=(GPL)
depends=(duplicity gvfs libsecret org.freedesktop.secrets python-gobject python-pydrive libadwaita)
makedepends=(appstream-glib itstool meson vala)
source=(https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('8d6ef2252bf62363d632435e64f7fe1614e736068d8b529a2ba75de484ea8862')

prepare() {
  find $pkgname-$pkgver -name meson.build | xargs sed -e 's|C.UTF-8|C|' -i
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
