# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=36.3
pkgrel=4
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('x86_64')
url="https://launchpad.net/deja-dup"
license=('GPL')
depends=('duplicity' 'gnome-online-accounts' 'gvfs' 'libpeas' 'libnotify' 'python2-gobject')
makedepends=('appstream-glib' 'gobject-introspection' 'intltool' 'itstool' 'meson' 'nautilus' 'vala')
optdepends=('gnome-keyring: save passwords'
            'nautilus: backup extension'
            'python2-boto: Amazon S3 and Google Cloud Storage backend')
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz{,.asc}
        fix-crash-on-restore.patch
        0002-don-t-use-ulimit.patch)
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('2c433a334bcead16f92a98914d36fbf6911cd11dcc75bc8163cefa73fff2fc22'
            'SKIP'
            '9b3c66d83325874d3ebe394240962e8d88bc2dc0a48d0550cb4f503f2d8d2554'
            'db584841e79069acdc2e2c910ea1229f5c16fec25fa836bf7e89996083dde722')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-crash-on-restore.patch
  patch -Np1 -i ../0002-don-t-use-ulimit.patch
}

build() {
  cd build
  meson ../$pkgname-$pkgver \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --buildtype=release
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
