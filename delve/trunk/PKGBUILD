# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=delve
pkgver=1.1.0
pkgrel=2
pkgdesc="A debugger for the Go programming language."
arch=('x86_64')
url="https://github.com/derekparker/delve"
license=('MIT')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/derekparker/delve/archive/v${pkgver}.tar.gz")
sha256sums=('de023318accf33ffe7cbb393f5a301551390111db8c0849fe5f4002b6c476583')

prepare(){
  mkdir -p gopath/src/github.com/derekparker
  ln -rTsf "${pkgname}-${pkgver}" gopath/src/github.com/derekparker/delve
}

build(){
  cd "gopath/src/github.com/derekparker/delve"
  export GOPATH="${srcdir}/gopath"
  go install -gcflags "all=-trimpath=${GOPATH}" -asmflags "all=-trimpath=${GOPATH}" ./cmd/...
}

package() {
  install -Dm755 gopath/bin/dlv "${pkgdir}/usr/bin/dlv"
  cd "${pkgname}-${pkgver}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
