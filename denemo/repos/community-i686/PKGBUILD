# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Philipp Sandhaus <philipp.sandhaus@gmx.de>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Contributor: Gnud <ach.gnud@gmail.com>

pkgname=denemo
epoch=1
pkgver=2.0.12
pkgrel=1
pkgdesc="A music score editor"
arch=('i686' 'x86_64')
url="http://www.denemo.org"
license=('GPL')
depends=('aubio' 'portaudio' 'lilypond' 'gtk3' 'libxml2' 'guile' 'fftw'
	 'librsvg' 'fluidsynth' 'libsmf' 'evince' 'gtksourceview3' 'portmidi'
	 'rubberband')
makedepends=('intltool' 'git')
install=denemo.install
noextract=(${pkgname}-${pkgver}.tar.gz)
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('7e132514661b4fb4b289407848b67854')

prepare() {
  cd $srcdir
  LC_ALL=en_US.UTF-8 tar xzf ${pkgname}-${pkgver}.tar.gz
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|<audio.h>|<portaudio.h>|' src/audio/pitchrecog.c
}

build() {
  cd $srcdir/$pkgname-$pkgver
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
