# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Philipp Sandhaus <philipp.sandhaus@gmx.de>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Contributor: Gnud <ach.gnud@gmail.com>

pkgname=denemo
epoch=1
pkgver=1.1.4
pkgrel=1
pkgdesc="A music score editor"
arch=('i686' 'x86_64')
url="http://www.denemo.org"
license=('GPL')
depends=('aubio' 'portaudio' 'lilypond' 'gtk3' 'libxml2' 'guile' 'fftw'
	 'librsvg' 'fluidsynth' 'libsmf' 'evince' 'gtksourceview3' 'portmidi'
	 'rubberband')
makedepends=('intltool' 'git')
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
#source=("$pkgname-$pkgver::git://git.sv.gnu.org/denemo.git#commit=9bdb8e75be68ed3d1ba25a1d512107ac56d2bf18")
md5sums=('095f05f1b106ca36a5300334ee7f5ca8')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|<audio.h>|<portaudio.h>|' src/audio/pitchrecog.c
}

build() {
  cd $srcdir/$pkgname-$pkgver
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
