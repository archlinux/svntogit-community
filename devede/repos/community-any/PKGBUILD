# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=devede
_pkgname=devedeng
pkgver=4.12.0
pkgrel=2
pkgdesc='Program to create VideoDVDs and CDs'
arch=('any')
url="http://www.rastersoft.com/programas/devede.html"
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-gobject' 'python-setuptools' 'ffmpeg' 'cdrtools' 'dvdauthor' 'vcdimager')
makedepends=(git)
_commit=f0893b3ff7b14723bd148db35bdfe2d284156d19  # tags/4.12.0
source=("git+https://gitlab.com/rastersoft/devedeng#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $_pkgname
  python3 setup.py build
}

package() {
  cd $_pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1
}
