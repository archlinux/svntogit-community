# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=devede
_pkgname=devedeng
pkgver=4.8.12
pkgrel=1
pkgdesc='Program to create VideoDVDs and CDs'
arch=('any')
url="http://www.rastersoft.com/programas/devede.html"
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-gobject' 'ffmpeg' 'cdrtools' 'dvdauthor' 'vcdimager')
source=($pkgname-$pkgver.tar.gz::https://github.com/rastersoft/$_pkgname/archive/$pkgver.tar.gz)
sha256sums=('4b01434e705bde59138caebde6c838017664a0e0a12ef11ec85161c03ea19a99')

prepare() {
  cd $_pkgname-$pkgver
  # Remove dependency on python-setuptools
  sed -i "/import pkg_resources/d
          s/self.version = .*$/self.version = '$pkgver' /" src/devedeng/configuration_data.py
}

build() {
  cd $_pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
