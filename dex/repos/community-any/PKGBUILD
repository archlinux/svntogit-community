# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=dex
pkgver=0.8.0
pkgrel=1

pkgdesc='Program to generate and execute DesktopEntry files of type Application'
url='https://github.com/jceb/dex'
arch=('any')
license=('GPL2')

depends=('python')
makedepends=('git' 'python-sphinx')

source=("git+https://github.com/jceb/dex.git#tag=v$pkgver"
        fix-dex-version.patch)

md5sums=('SKIP'
         'e00a4c7189bc6f097f510af4bd689998')

prepare() {
  cd dex
  patch dex <"$srcdir"/fix-dex-version.patch
}

package() {
  cd dex
  make install PREFIX=/usr MANPREFIX=/usr/share/man DESTDIR="$pkgdir"
  rm "$pkgdir/usr/share/doc/dex/LICENSE"
}

# vim:set ts=2 sw=2 et:
