# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall-json
pkgver=1.7.7
pkgrel=9
pkgdesc='Convert between Dhall and JSON or YAML'
url='https://dhall-lang.org'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-aeson-yaml' 'dhall'
         'haskell-lens-family-core' 'haskell-optparse-applicative' 'haskell-prettyprinter'
         'haskell-scientific' 'haskell-unordered-containers' 'haskell-prettyprinter-ansi-terminal')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-silver')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b7760742e59d449e278b097cd82a536c997cd46e53ee92f5019fb1ab1d4389c18a549b39053ba9bd74de598b5118aa2037dbd9a4bd33c4e2c1b2ec9d55871540')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    # https://github.com/dhall-lang/dhall-haskell/issues/2151
    runhaskell Setup test || echo "Tests failed"
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
