# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall-json
pkgver=1.6.4
pkgrel=7
pkgdesc='Convert between Dhall and JSON or YAML'
url='https://dhall-lang.org'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-aeson-yaml' 'dhall'
         'haskell-optparse-applicative' 'haskell-prettyprinter'
         'haskell-scientific' 'haskell-unordered-containers' 'haskell-prettyprinter-ansi-terminal')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0e05624e1e666030acc24a8ac593f5bb70caae5ef27b534c5d75b1f2bc3bcb145f7668afa98eeeb2de6f6860c3504d5afc0246b9fee49b3ccd77e8321be2afb6')

prepare() {
    cd $pkgname-$pkgver
    sed -i 's/< *1.3/<2/' $pkgname.cabal
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
