# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall-yaml
pkgver=1.2.7
pkgrel=9
pkgdesc="Convert between Dhall and YAML"
url="https://github.com/dhall-lang/dhall-haskell/tree/master/dhall-yaml"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hsyaml' 'haskell-hsyaml-aeson' 'haskell-aeson' 'haskell-ansi-terminal'
         'dhall' 'dhall-json' 'haskell-optparse-applicative' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-tasty' 'haskell-tasty-expected-failure' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('20549412217c74c74c9cca7793661cd60bc24df1b27ac35e0046f2f58f585e0fe6e76d39938e899196de2b88789889507dfaa4738cc4876409b0460fa97e848b')

prepare() {
  cd $pkgname-$pkgver
  gen-setup
}

build() {
  cd $pkgname-$pkgver    

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'
  
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
