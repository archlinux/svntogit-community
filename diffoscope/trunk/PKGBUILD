# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=diffoscope
pkgver=64
pkgrel=1
pkgdesc='Tool for in-depth comparison of files, archives, and directories'
url='https://diffoscope.org/'
arch=('x86_64')
license=('GPL3')
depends=('python-magic' 'python-libarchive-c' 'python-setuptools')
# TODO: python-argcomplete
optdepends=(
  'acl: access control list utilities support'
  'binutils: binary utilities support'
  'bzip2: bzip2 utilities support'
  'cdrkit: ISO utilities support'
  'colord: ICC profiles support'
  'cpio: cpio archive support'
  'diffutils: diff utilities support'
  'e2fsprogs: Ext2/3/4 filesystem utilities support'
  'enjarify: Android dex file support'
  'fpc: Free Pascal utilities support'
  'java-environment: java utilities support'
  #'libcaca: image compare support'
  'llvm: LLVM bitcode files support'
  'fontforge: bitmap font utilities support'
  'gettext: GNU internationalization utilities support'
  'ghc: haskell utilities support'
  'gnupg: GNU privacy guard support'
  'mono: mono support'
  'mono-tools: mono utilities support'
  'openssh: OpenSSH key comparing support'
  'poppler: PDF utilities support'
  'sqlite: SQLite support'
  'squashfs-tools: squashfs filesystem support'
  #'python-guestfs: guestfs filesystem support'
  'tlsh: fuzzy matching supprt'
  'unzip: zip utilities support'
  'gzip: gzip utilities support'
  'tar: tar utilities support'
  'vim: Vi IMproved (xxd) utilities support'
  'xz: XZ and LZMA utilities support'
)
checkdepends=(
  'python-pytest' 'acl' 'binutils' 'bzip2' 'cdrkit' 'cpio' 'diffutils' 'e2fsprogs' 'enjarify' 'java-environment>=8' 'fontforge' 'gettext' 'ghc' 'gnupg'
  'mono' 'mono-tools' 'poppler' 'sqlite' 'squashfs-tools' 'tlsh' 'unzip' 'gzip' 'tar' 'vim' 'xz' 'llvm' 'colord' 'fpc' 'openssh')
source=(${pkgname}-${pkgver}.tar.xz::https://anonscm.debian.org/cgit/reproducible/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.xz)
sha512sums=('200659589c7a688b878cb2d4e1a0e5ba3726e4da5cb9a1b2404d8d1c377b9ca98014d7738cc1235259619f252d9f3c063621eb0bf72b5ac06ee8985236b7d990')

prepare() {
  cd ${pkgname}-${pkgver}
  sed '/python-magic/d' -i setup.py
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  # TODO: readd fpc and dex
  # TODO: colord test fails with lcms2 >= 2.8
  PYTHONPATH=".:${PYTHONPATH}" py.test -k 'not test_icc and not test_ppu and not test_dex'
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
