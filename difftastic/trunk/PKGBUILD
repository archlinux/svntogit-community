# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=difftastic
pkgver=0.27.0
pkgrel=1
pkgdesc="An experimental diff tool that compares files based on their syntax"
arch=('x86_64')
url="https://difftastic.wilfred.me.uk"
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'rust' 'mdbook')
options=('!lto')
_commit='a91a3e5db073df6e95aec50c395ea17355d2c1c3'
source=("$pkgname::git+https://github.com/Wilfred/difftastic.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  # binary
  cargo build --frozen --release --all-features

  # documentation
  cd manual
  mdbook build
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/difft

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr manual/book "$pkgdir/usr/share/doc/$pkgname/manual"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
