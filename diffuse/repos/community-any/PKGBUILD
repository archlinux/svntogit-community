# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=diffuse
pkgver=0.7.5
pkgrel=1
pkgdesc='Graphical tool for merging and comparing text files'
arch=(any)
url='https://github.com/MightyCreak/diffuse'
license=(GPL)
depends=(gtk3 python-cairo python-gobject)
makedepends=(git meson ninja)
source=("git+$url#commit=b1567bb37c5ee3d63ae5ca4c4eb55757daf45f29") # tag: v0.7.4
b2sums=('SKIP')

build() {
  mkdir -p build
  arch-meson build $pkgname -D b_ndebug=true
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
