# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=55
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=(x86_64)
url="https://dinisnoise.org/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libglvnd sdl tcl)
makedepends=(boost glu rtaudio rtmidi)
options=(debug)
source=(
  https://archive.org/download/dinisnoise_source_code/$pkgname-$pkgver.tar.gz
  $pkgname-54-devendor_rtaudio_rtmidi.patch
)
sha512sums=('432a53e71d1cf96c9ed62694fb23d1264d2e80b34620ebf0f1f33f3d4787cc7358c42038b2a45aa9d3ba9f59828eaaebd521a046fd5a51919a13f1bffad16a22'
            '151613648d4da638835a1d5eceb3c6db78aea801e8f2bd385120a4b7bdfaadb707849a3ec9745b7e1e5f2a600ab13fe6fc3b2abe78c2783461deffb291787f1d')
b2sums=('2c6ba4a877c512a950fb7c731f20d285019da1c7cd4ab5c7ff8d75e3c25eaa4ea08ca865b37e12fa1cefc1175baaa1c7ce27a44c7fa7c0b6c59b0ccbf6ab7a39'
        'a80851871f075971139a1eb82144fa05aaad4e7892eb80c5a045d12f59dd8a25f76fabd6eaa435409c5db2ea5e40615117a9016c50160594590fd434af9a87b0')

prepare() {
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-54-devendor_rtaudio_rtmidi.patch

  cd $pkgname-$pkgver
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  export CXXFLAGS+=" -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS+=" -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS+=" $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=(librtaudio.so librtmidi.so)

  make install DESTDIR="$pkgdir" -C $pkgname-$pkgver
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
