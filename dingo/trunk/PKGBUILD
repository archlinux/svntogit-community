# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dingo
pkgver=0.13
pkgrel=5
pkgdesc="A DNS client in Go that supports Google DNS over HTTPS"
arch=('x86_64')
url="https://github.com/pforemski/dingo"
license=('GPL3')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pforemski/dingo/archive/$pkgver.tar.gz"
        dingo.service)
sha512sums=('24748a726aea598f6f8785bfe0bc349608e7106860fa73f4e3da12936035c249e8bdfee0e2d162d876beecf60b5cdfbce6cd0f09b040ce7b1db2d901bc0fd350'
            '82180643e2083963eb935b9a5a7ba0bec5dd711fd8e95ac62e46bccc094c5195b519baea07e3434b0ad34d6513959b0bc4058e11a7d8640d700b78d217292f0f')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  go get github.com/miekg/dns github.com/patrickmn/go-cache golang.org/x/net/http2
}

build() {
  cd dingo-$pkgver
  go build -o dingo # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd dingo-$pkgver
  install -Dm755 dingo "$pkgdir"/usr/bin/dingo
  install -Dm644 ../dingo.service "$pkgdir"/usr/lib/systemd/system/dingo.service
}
