# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dingo
pkgver=0.13
pkgrel=3
pkgdesc="A DNS client in Go that supports Google DNS over HTTPS"
arch=('x86_64')
url="https://github.com/pforemski/dingo"
license=('GPL3')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pforemski/dingo/archive/$pkgver.tar.gz"
        dingo.service)
sha512sums=('24748a726aea598f6f8785bfe0bc349608e7106860fa73f4e3da12936035c249e8bdfee0e2d162d876beecf60b5cdfbce6cd0f09b040ce7b1db2d901bc0fd350'
            '90bfe184b3407d32d58f69e1ee33d8b42492fc0791614fcbe06f7345052bebbc97fca897b30c9617d1e95874fc4a528d47978d25059331b720187515d50b5e07')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  go get github.com/miekg/dns github.com/patrickmn/go-cache golang.org/x/net/http2
}

build() {
  cd dingo-$pkgver
  go build -o dingo # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd dingo-$pkgver
  install -Dm755 dingo "$pkgdir"/usr/bin/dingo
  install -Dm644 ../dingo.service "$pkgdir"/usr/lib/systemd/system/dingo.service
}
