# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=dino
pkgver=0.3.1
pkgrel=1
pkgdesc="Modern XMPP (Jabber) chat client written in Vala"
arch=(x86_64)
url="https://github.com/dino/dino"
license=(GPL3)
depends=(glib2 glib-networking gtk3 gpgme libgee libgcrypt libsignal-protocol-c
         libsoup sqlite qrencode gspell libnice gstreamer gst-plugin-gtk
         gst-plugins-base gst-plugins-good libsrtp webrtc-audio-processing)
makedepends=(git cmake vala ninja)
source=(${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('aa4cf890a6353cf27f00d6cf4cd7a7a55291530138a4c60a409cc716e7c546e7'
            'SKIP')
validpgpkeys=(A32134DD19952FB12EC43E4501FABCC83FEA225E)

build() {
  cmake -G Ninja -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}
