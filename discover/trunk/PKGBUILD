# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.6.3
pkgrel=2
pkgdesc='A collection of package management tools for KDE'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/discover'
license=(LGPL)
depends=(packagekit-qt5 knewstuff kdeclarative qt5-graphicaleffects appstream-qt archlinux-appstream-data hicolor-icon-theme)
makedepends=(extra-cmake-modules python plasma-framework)
conflicts=(muon)
replaces=(muon)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('7e305965d9000f07a41d0b878bf7977f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
