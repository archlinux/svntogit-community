# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: foxbunny <bg.branko@gmail.com>

pkgname=dispcalgui
pkgver=1.5.3.1
pkgrel=2
pkgdesc="A GUI frontend for several utilities from the open source color management system Argyll CMS"
arch=('i686' 'x86_64')
url="http://hoech.net/dispcalGUI/"
license=('GPL3')
makedepends=('libx11' 'libxrandr' 'libxinerama' 'libxxf86vm' 'python2-setuptools' 'xdg-utils')
depends=('argyllcms' 'wxpython' 'hicolor-icon-theme' 'desktop-file-utils' 'python2-numpy')
optdepends=('gksu: For running as root')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/dispcalgui/release/${pkgver}/dispcalGUI-${pkgver}.tar.gz"
        dispcalGUI-1.5.3.1-wxgtk3.patch)
md5sums=('1b7542ecee8f39a49b90197613a46a73'
         'b8001e5cbbb849f9ba95e0ef9fb1d073')

prepare() {
  cd "${srcdir}"/dispcalGUI-${pkgver}
  patch -p1 -i "$srcdir"/dispcalGUI-1.5.3.1-wxgtk3.patch
  find . -name "*.py" -exec sed -i 's!/usr/bin/env python!/usr/bin/env python2!g' {} +
}

package() {
  cd "${srcdir}"/dispcalGUI-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  #udev rules are shipped with argyllcms
  rm -rf "${pkgdir}"/etc/udev/
}

# vim:set ts=2 sw=2 et:
