# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: foxbunny <bg.branko@gmail.com>

_pkgname=DisplayCAL
pkgname=displaycal
pkgver=3.7.0.0
pkgrel=1
pkgdesc="Open Source Display Calibration and Characterization powered by Argyll CMS (Formerly known as dispcalGUI)"
arch=('x86_64')
url="https://displaycal.net/"
license=('GPL3')
depends=('argyllcms' 'python2-wxpython3' 'python2-numpy')
makedepends=('libx11' 'libxrandr' 'libxinerama' 'libxxf86vm' 'python2-setuptools')
optdepends=('gnome-color-manager: profile installation for GNOME'
            'colord-kde: profile installation for Plasma')
replaces=('dispcalgui')
conflicts=('dispcalgui')
source=("https://downloads.sourceforge.net/dispcalgui/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('e2793000b8bab6eb7abd32855af51916af2bcc1f453342c3eeeea467e850c05d')

build() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py build
}

package() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    # udev rules are shipped with argyllcms
    rm -rf "${pkgdir}"/etc/udev/
}
