# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Judd Vinet <jvinet@zeroflux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=distcc
pkgver=3.2rc1
pkgrel=2
pkgdesc="A distributed C, C++, Obj C compiler"
arch=('x86_64')
url="https://github.com/distcc/distcc"
license=('GPL')
depends=('gcc' 'popt' 'avahi' 'dbus')
makedepends=('gtk2' 'pkgconfig' 'python2')
optdepends=('gtk2: for distccmon-gnome'
	    'python2')
backup=('etc/conf.d/distccd'
	'etc/distcc/hosts')
source=(https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/distcc/${pkgname}-${pkgver}.tar.bz2
	distccd.conf.d
	distccd.service)
md5sums=('18cd4f33f5cfc3e75675cafc568fb9cf'
         '239aae53250e3e35288cba566bc0bbf1'
         '09f0688da9c1840e518d2593bd5c3830')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [ -f Makefile ] || PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
	--with-gtk \
	--sysconfdir=/etc \
	--mandir=/usr/share/man \
	--enable-rfc2553
#	--without-avahi \

  make WERROR_CFLAGS= INCLUDESERVER_PYTHON=/usr/bin/python2
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
#  make TEST_PYTHON=/usr/bin/python2 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" INCLUDESERVER_PYTHON=/usr/bin/python2 install
  install -D -m644 "$srcdir"/distccd.conf.d "$pkgdir"/etc/conf.d/distccd

  install -d "$pkgdir"/usr/lib/${pkgname}/bin
  ln -sf /usr/bin/${pkgname} "$pkgdir"/usr/lib/${pkgname}/bin/cc
  ln -sf /usr/bin/${pkgname} "$pkgdir"/usr/lib/${pkgname}/bin/gcc
  ln -sf /usr/bin/${pkgname} "$pkgdir"/usr/lib/${pkgname}/bin/g++
  ln -sf /usr/bin/${pkgname} "$pkgdir"/usr/lib/${pkgname}/bin/c++
  ln -sf /usr/bin/${pkgname} "$pkgdir"/usr/lib/${pkgname}/bin/cpp

  install -Dm0644 "$srcdir"/distccd.service "$pkgdir"/usr/lib/systemd/system/distccd.service
}
