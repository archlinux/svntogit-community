# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Dmitry A. Ilyashevich <dmitry.ilyashevich@gmail.com>

pkgbase=distorm
pkgname=('distorm' 'python-distorm' 'python2-distorm')
pkgver=3.5
pkgrel=2
pkgdesc='Powerful disassembler library for x86/AMD64'
url='https://github.com/gdabah/distorm'
arch=('x86_64')
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools' 'glibc')
options=('staticlibs')
source=(https://github.com/gdabah/distorm/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e6aa36e5c70004ea759549e056d052c2f510f47c3f23175fb9f73278df96562fb0bda62421372b29ca20145a0b873c466f19e48f588a05a9d4da42da79ef1a5c')
b2sums=('4fd316620693517627f5673880714e4e105b7309e3c148f3d9958698764c29870c3f0bd6a29f9caa7d524e54a6d6c35f00ad9f5b0dd39f50baaf56170a2e5f29')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

build() {
  (cd ${pkgbase}-${pkgver}
    make -C make/linux CFLAGS="${CFLAGS} ${CPPFLAGS} -fPIC"
    python setup.py build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
  )
}

package_distorm() {
  depends=('glibc')
  provides=('libdistorm3.so')
  cd ${pkgbase}-${pkgver}
  make -C make/linux PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 include/*.h -t "${pkgdir}/usr/include"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf "/usr/lib/libdistorm3.so.$(grep 'LIB_R_VERSION =' make/linux/Makefile|cut -d' ' -f3)" "${pkgdir}/usr/lib/libdistorm3.so"
}

package_python-distorm() {
  depends=('python')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-distorm() {
  depends=('python2')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
