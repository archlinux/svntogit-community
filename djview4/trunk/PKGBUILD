# $Id$
# Contributor: Paulo Matias <matias.archlinux-br.org>
# Contributor: Leslie P. Polzer <polzer.gnu.org>
# Contributor: erm67 <erm67.yahoo.it>
# Contributor: Daniel J Griffiths
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=djview
pkgver=4.10
pkgrel=1
pkgdesc='Portable DjVu viewer and browser plugin'
url='http://djvu.sourceforge.net/djview4.html'
license=('GPL')
arch=('x86_64')
depends=('qt4' 'djvulibre')
source=("http://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz")
sha1sums=('7526d23aa501ce34468e42f094f49b6b96cce186')

conflicts=('djview4')
provides=('djview4')
replaces=('djview4')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	sed 's/netscape/mozilla/g' -i nsdejavu/Makefile
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install-djview install-nsdejavu

	cd desktopfiles
	install -Dm644 hi32-djview4.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/djvulibre-djview4.png
	install -Dm644 hi64-djview4.png "${pkgdir}"/usr/share/icons/hicolor/64x64/apps/djvulibre-djview4.png
	install -Dm644 hi64-djview4.png "${pkgdir}"/usr/share/pixmaps/djvulibre-djview4.png
	install -Dm644 djvulibre-djview4.desktop "${pkgdir}"/usr/share/applications/djvulibre-djview4.desktop
}
