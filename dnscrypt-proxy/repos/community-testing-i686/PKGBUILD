# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Techlive Zheng <techlivezheng at gmail dot com>
# Contributor: peace4all <markspost at rocketmail dot com>

pkgname=dnscrypt-proxy
pkgver=1.6.0
pkgrel=5
pkgdesc="A tool for securing communications between a client and a DNS resolver"
arch=('i686' 'x86_64')
url="http://dnscrypt.org/"
license=('custom:ISC')
depends=('libsodium' 'systemd')
install=dnscrypt-proxy.install
source=("http://download.dnscrypt.org/$pkgname/$pkgname-$pkgver.tar.gz"
        'Remove-network.target-dependency-from-dnscrypt-proxy.patch')
sha512sums=('1937a80ab0e6e5786b0d004fef1c007d9d8a4df335542ae6f96c1eac7739920f2ff4b469d30abac774c12a41d275c2a8bf31b5514d7af446028cedeeb3efcf02'
            'a3fd405cc635ec3b8564fbfc769504eeaf77b693fa521df44be7beb1d5abea971b81905a11357870a6fb5ba8d129c91bf9cc337cb730b733c3d3c047854a4021')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|/usr/local/sbin|/usr/bin|' dnscrypt-proxy.service
  patch -p1 -i ../Remove-network.target-dependency-from-dnscrypt-proxy.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sbindir=/usr/bin --with-systemd
  make -j2
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir"/{usr/share/{licenses,doc}/$pkgname,usr/lib/systemd/system}
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 AUTHORS NEWS README README.markdown "$pkgdir"/usr/share/doc/$pkgname
  install -m 644 dnscrypt-proxy.service "$pkgdir"/usr/lib/systemd/system
  install -m 644 dnscrypt-proxy.socket "$pkgdir"/usr/lib/systemd/system
}
