# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=docker-compose
pkgver=1.5.0rc3
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('any')
url="https://www.docker.com/"
depends=('python-docopt' 'python-yaml' 'python-requests' 'python-six' 'python-texttable'
         'python-websocket-client' 'python-docker-py' 'python-dockerpty' 'python-setuptools'
         'python-jsonschema' 'docker')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/compose/archive/$pkgver.tar.gz"
        "remove-requires-upper-bound.patch")
license=("BSD")
sha256sums=('78cd98ceb85fe41e25be91d9599b92729c544e2a3c6a3263137d60429cd6dc91'
            '06bd229e1d62632dc199d3b2de82571dff73631865fe6108e3a62c8441c281bf')

prepare() {
  cd "$srcdir/compose-$pkgver"

  # Remove upper bound on requires
  patch -p1 -i "$srcdir/remove-requires-upper-bound.patch"
  sed -i 's/==/>=/g' requirements.txt
}

build() {
  cd "$srcdir/compose-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/compose-$pkgver"

  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 contrib/completion/bash/docker-compose "$pkgdir/etc/bash_completion.d/docker-compose"
}
