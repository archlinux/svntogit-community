# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=docker-machine
pkgver=0.16.1
pkgrel=2
pkgdesc='Machine management for a container-centric world'
arch=('x86_64')
url='https://github.com/docker/machine'
license=('Apache')
depends=('glibc')
optdepends=('net-tools: for VirtualBox support')
makedepends=('go-pie')
optdepends=('net-tools: required for VirtualBox driver')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/machine/archive/v$pkgver.tar.gz")
sha512sums=('7f9f36190ed08071fee67edf8ff7c0744395d0f0cbd4b84befef2b471149afa25676b275b53b17e68ce7cc9aad47dcb2dfc0adbec777644264ec359a9206ad25')

prepare() {
  mkdir -p build/src
  mv machine-$pkgver/vendor/* build/src/
  mkdir -p build/src/github.com/docker
  ln -s "$srcdir"/machine-$pkgver build/src/github.com/docker/machine
}

build() {
  export GOPATH="$srcdir/build"
  cd machine-$pkgver
  go build -o bin/docker-machine cmd/docker-machine/machine.go # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd machine-$pkgver
  install -Dm755 bin/docker-machine "$pkgdir"/usr/bin/docker-machine
  install -Dm644 contrib/completion/bash/docker-machine.bash "$pkgdir"/usr/share/bash-completion/completions/docker-machine
  install -Dm644 contrib/completion/zsh/_docker-machine "$pkgdir"/usr/share/zsh/site-functions/_docker-machine
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
