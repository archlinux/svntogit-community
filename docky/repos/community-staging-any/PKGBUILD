# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: dieghen89 <dieghen89@gmail.com>
# Contributor: Alessio 'Bl@ster' Biancalana <dottorblaster@gmail.com>

pkgname=docky
pkgver=2.2.0
pkgrel=4
pkgdesc="Full fledged dock application that makes opening common applications and managing windows easier and quicker"
url="http://wiki.go-docky.com/"
arch=('any')
license=('GPL')
depends=('gconf-sharp' 'gkeyfile-sharp' 'gnome-keyring-sharp' 'librsvg' 'mono-addins' 'notify-sharp' 'wnck-sharp')
makedepends=('intltool' 'gio-sharp')
install=$pkgname.install
source=("http://launchpad.net/$pkgname/2.2/$pkgver/+download/$pkgname-$pkgver.tar.xz"
         use_dbussharp_2.patch
         fix-build.patch
         systemd-support.patch
         drop-gnome-desktop-sharp.patch
         drop-rsvg2-sharp.patch)
md5sums=('519fa5920a590a680afd8a87d0db9db5'
         '42553efbadb9dcf8c108ee9d8d568b2e'
         'a78989892dfb660600c5f63a6df90f37'
         'a9670aef62793386b058a26d78d25005'
         'c19568dde9351e258e1d2e3ef2e7dddc'
         '468fd1e7f7cffad8bc4f60c3bc711975')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  patch -Np1 -i ../fix-build.patch

  # Add systemd support for suspend and hibernate
  patch -Np0 -i ../systemd-support.patch

  # Remove gnome-desktop-sharp dependency
  patch -Np0 -i ../drop-gnome-desktop-sharp.patch

  # Remove rsvg2-sharp dependency
  patch -Np0 -i ../drop-rsvg2-sharp.patch

  # dbus-sharp 2.0
  patch -Np1 -i ../use_dbussharp_2.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Remove NetworkManager plugin, because it doesn't work
  rm "$pkgdir/usr/lib/docky/plugins/NetworkManager.dll"
}
