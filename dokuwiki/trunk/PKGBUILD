# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SÃ©bastien Luttringer

pkgname=dokuwiki
pkgver=20131028.rc
[ "x${pkgver:9:2}" == "xrc" ] && \
  _pkgver=rc${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2} || \
  _pkgver=${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}${pkgver:9:1}
pkgrel=1
pkgdesc='Simple to use and highly versatile Open Source wiki software'
arch=('any')
url='http://www.splitbrain.org/projects/dokuwiki'
license=('GPL')
depends=('php')
backup=('etc/webapps/dokuwiki/.htaccess')
options=('!strip')
source=("http://download.dokuwiki.org/src/dokuwiki/dokuwiki-${_pkgver}.tgz"
	"apache.example.conf")
md5sums=('f476b54a3679d35cdd8746db57c9c6a3'
         'c5bed40abf5bb737bad8df29bb7020dc')

package() {
  cd "$pkgdir"
  install -dm0755 usr/share/webapps var/lib/dokuwiki etc/webapps
  cp -a "$srcdir"/$pkgname-${_pkgver} usr/share/webapps/dokuwiki

  # move data in var
  mv usr/share/webapps/dokuwiki/data var/lib/dokuwiki
  ln -s {../../../../../var/lib/dokuwiki,usr/share/webapps/dokuwiki}/data

  # move config in /etc
  mv usr/share/webapps/dokuwiki/conf etc/webapps/dokuwiki
  chmod g=rwx etc/webapps/dokuwiki
  ln -s ../../../../../etc/webapps/dokuwiki usr/share/webapps/dokuwiki/conf

  # inform update on changes.log
  touch var/lib/dokuwiki/data/changes.log
  chmod 0644 var/lib/dokuwiki/data/changes.log

  # use default htaccess
  cp usr/share/webapps/dokuwiki/.htaccess.dist etc/webapps/dokuwiki/.htaccess
  ln -s /etc/webapps/dokuwiki/.htaccess usr/share/webapps/dokuwiki/.htaccess

  # fix rights and ownership
  chown -R http:http var/lib/dokuwiki/data
  chown -R http:http etc/webapps/dokuwiki

  # apache config
  install -Dm0644 $srcdir/apache.example.conf $pkgdir/etc/webapps/dokuwiki/apache.example.conf
}

# vim:set ts=2 sw=2 et:
