# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgname=dolphin-emu
pkgver=4.0.2
pkgrel=7
epoch=1
pkgdesc='A Gamecube / Wii / Triforce emulator'
arch=('i686' 'x86_64')
url='http://dolphin-emu.org'
license=('GPL2')

makedepends=('cmake' 'git' 'opencl-headers')
depends=('bluez-libs' 'ffmpeg' 'glew' 'libao' 'miniupnpc' 'openal' 'portaudio'
         'sdl2' 'soundtouch' 'wxgtk')
optdepends=('pulseaudio: PulseAudio backend')

options=('!emptydirs')

source=("${pkgname%-*}::git+https://code.google.com/p/dolphin-emu/#tag=${pkgver}"
        'dolphin-emu-gcc49.patch')
md5sums=('SKIP'
         '5fead49fc3a0ff9a9d1435368b5df1f9')

prepare() {
  cd "${srcdir}/${pkgname%-*}"

  patch -Np1 -i ../dolphin-emu-gcc49.patch
}

build() {
  cd "${srcdir}/${pkgname%-*}"

  mkdir build && cd build
  if [[ $CARCH == x86_64 ]]; then
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_CXX_FLAGS='-fno-inline-functions'
  else
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  fi
  make
}

package() {
  cd "${srcdir}/${pkgname%-*}/build"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/libpolarssl.a"
}
