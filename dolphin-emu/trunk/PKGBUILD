# $Id$
# Maintainer: Jakob Gruber <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jeremy Newton <alexjnewt@gmail.com>

pkgname=dolphin-emu
pkgver=3.5
pkgrel=4
pkgdesc="A Gamecube / Wii / Triforce Emulator"
arch=('i686' 'x86_64')
url="http://dolphin-emu.org"
license=('GPL2')
depends=('bluez' 'ffmpeg' 'glew' 'libao' 'lzo2' 'nvidia-cg-toolkit' 'openal' 'portaudio' 'wxgtk2.9')
makedepends=('cmake' 'opencl-headers')
optdepends=('pulseaudio: PulseAudio backend')
source=("http://dolphin-emu.googlecode.com/files/dolphin-${pkgver}-src.zip" 'dolphin-emu.desktop' 'dolphin-emu.xpm')
sha256sums=('b95cb6f487c1f77471c08cfb52bc9844bb2385f0ffd8fb45416b4c5095eb8d9a'
            '72091740ce202f2570d58d900a4a82230538555a5ac69f9914f5684b54d42ba6'
            '168640bb280a5dcd2946077768cefb0ac99563e724ca1b9e3399d80939f285d7')

build() {
  cd "${srcdir}"/dolphin-${pkgver}

# Build
  mkdir build && cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.9 -DwxWidgets_wxrc_EXECUTABLE=/usr/bin/wxrc-2.9
  make
}

package() {
  cd "${srcdir}"/dolphin-${pkgver}/build

# Install
  make DESTDIR="${pkgdir}" install

# Desktop file
  install -dm 755 "${pkgdir}"/usr/share/{applications,pixmaps}
  install -m 644 "${srcdir}"/dolphin-emu.desktop "${pkgdir}"/usr/share/applications/dolphin-emu.desktop
  install -m 644 "${srcdir}"/dolphin-emu.xpm "${pkgdir}"/usr/share/pixmaps/dolphin-emu.xpm
}

# vim: ts=2 sw=2 et:
