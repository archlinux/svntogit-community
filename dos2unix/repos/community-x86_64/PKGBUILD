# Contributor: Bart≈Çomiej Piotrowski
# Contributor: Renato Garcia <fgar.renatoATgmailDOTcom>
# Contributor: Gerson E. Ruotolo <gersonruotolo@globo.com>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=dos2unix
pkgver=7.4.3
pkgrel=1
pkgdesc='Text file format converter'
arch=('x86_64')
url='https://waterlan.home.xs4all.nl/dos2unix.html'
license=('BSD')
depends=('glibc')
makedepends=('perl')
conflicts=('hd2u')
source=(https://waterlan.home.xs4all.nl/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('F8F1BEA490496A09CCA328CC38C1F572B12725BE')
md5sums=('ff949538cf3c82910d5f6f486c1981dd'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  make LDFLAGS_USER="${LDFLAGS}"
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
