# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=dosbox
pkgver=0.74.2
pkgrel=1
pkgdesc='Emulator with builtin DOS for running DOS Games'
arch=('x86_64')
url='http://dosbox.sourceforge.net/'
license=('GPL')
depends=('sdl_net' 'zlib' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs' 'glu')
makedepends=('mesa-libgl' 'gendesk')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-0.74-2.tar.gz
        dosbox.png)
sha256sums=('7077303595bedd7cd0bb94227fa9a6b5609e7c90a3e6523af11bc4afcb0a57cf'
            '491c42d16fc5ef7ee2eca1b736f7801249d4ca8c0b236a001aec0d3e24504f3b')

prepare() {
  cd "${srcdir}"
  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"

  cd "${srcdir}/$pkgname-0.74-2"
  sed -i 's/png_check_sig/png_sig_cmp/' configure
}

build() {
  cd "${srcdir}/$pkgname-0.74-2"

  ./configure --prefix=/usr --sysconfdir=/etc/dosbox
  make
}

package() {
  cd "${srcdir}/$pkgname-0.74-2"

  make DESTDIR="${pkgdir}" install

# install docs, make does not install them
  install -Dm644 README "${pkgdir}"/usr/share/doc/$pkgname/README
  install -Dm644 docs/README.video "${pkgdir}"/usr/share/doc/$pkgname/README.video

  install -Dm644 "${srcdir}/$pkgname.png" \
    "${pkgdir}/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "${srcdir}/$pkgname.desktop" \
    "${pkgdir}/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
