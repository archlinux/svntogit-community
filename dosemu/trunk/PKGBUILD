# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Bob Finch <w9ya@arrl.net>

pkgname=dosemu
epoch=1
pkgver=1.4.git20151102
pkgrel=1
pkgdesc="DOS emulator"
arch=('x86_64')
url="http://www.dosemu.org/"
license=('GPL' 'custom')
depends=('gpm' 'slang' 'sdl' 'libxxf86vm' 'libsndfile')
makedepends=('git' 'xorg-mkfontdir' 'xorg-bdftopcf' 'gendesk' 'imagemagick')
backup=('etc/dosemu/dosemu.conf'
	'etc/dosemu/dosemu.users'
	'etc/dosemu/global.conf')
#source=($pkgname-$pkgver::git://git.code.sf.net/p/dosemu/code#tag=$pkgname-$pkgver
source=($pkgname-$pkgver::git://git.code.sf.net/p/dosemu/code#commit=cc971eae4738016c22b3d2e1b3473df2176c8e29
	http://downloads.sourceforge.net/sourceforge/dosemu/dosemu-freedos-1.0-bin.tgz)
md5sums=('SKIP'
         '2e09774fe91cff4372cb4a393eb467f5')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  gendesk -n --pkgname=$pkgname --pkgdesc='DOS emulator' --categories='System;Emulator'
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --with-fdtarball="$srcdir"/dosemu-freedos-1.0-bin.tgz --mandir=/usr/share/man
  make
}

package(){
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/usr/share/pixmaps
  convert etc/$pkgname.xpm "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -D -m 644 $pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
  install -D -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  install -D -m 644 COPYING.DOSEMU "$pkgdir"/usr/share/licenses/$pkgname/COPYING.DOSEMU
}
