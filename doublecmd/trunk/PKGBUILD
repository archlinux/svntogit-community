# $Id$
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: (sirocco AT ngs.ru)

pkgbase=doublecmd
pkgname=('doublecmd-gtk2' 'doublecmd-qt')
pkgver=0.5.4
_helpver=0.5.1
pkgrel=2.1
url="http://doublecmd.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
install="$pkgbase.install"
provides=("$pkgbase")
makedepends=('lazarus' 'qt4pas' 'gtk2')
optdepends=('lua: scripting' 'p7zip: support for 7zip archives' 'libunrar: support for rar archives')
source=("http://downloads.sourceforge.net/project/$pkgbase/Double%20Commander%20Source/$pkgbase-$pkgver-src.tar.gz"
	"http://downloads.sourceforge.net/project/$pkgbase/Double%20Commander%20Source/$pkgbase-help-$_helpver-src.tar.gz"
)
sha256sums=('480b83279adeba007ad5c54f455733fae3d9c9ee61762e6b5a63e2e67b1cd7b7'
            'd9f518e05e089b71aaa31c7d4af8177e77594a02be2c316e85e061e63b4c03e5')

build() {
	cp -a $pkgbase-$pkgver $pkgbase-gtk
	cp -a $pkgbase-$pkgver $pkgbase-qt

	cd "$srcdir/$pkgbase-gtk"
	./build.sh beta gtk2

	cd "$srcdir/$pkgbase-qt"
	./build.sh beta qt
}

package_doublecmd-gtk2() {
pkgdesc='Twin-panel (commander-style) file manager (GTK)'
	depends=('gtk2')
	cd "$srcdir/$pkgbase-gtk"
	sed -e 's/LIB_SUFFIX=.*/LIB_SUFFIX=/g' -i ./install/linux/install.sh
	./install/linux/install.sh --install-prefix="$pkgdir"

	# install doc
	cd "$srcdir/$pkgbase-help-$_helpver"
	cp -a * "$pkgdir/usr/share/$pkgbase/doc/"
}

package_doublecmd-qt() {
pkgdesc='Twin-panel (commander-style) file manager (QT)'
	depends=('qt4pas')
	cd "$srcdir/$pkgbase-qt"
	sed -e 's/LIB_SUFFIX=.*/LIB_SUFFIX=/g' -i ./install/linux/install.sh
	./install/linux/install.sh --install-prefix="$pkgdir"

	# install doc
	cd "$srcdir/$pkgbase-help-$_helpver"
	cp -a * "$pkgdir/usr/share/$pkgbase/doc/"
}
