# Maintainer: David Runge <dave@sleepmap.de>
pkgname=dpf-plugins
pkgver=1.5
pkgrel=1
pkgdesc="Collection of DPF-based plugins"
arch=(x86_64)
url="https://github.com/DISTRHO/DPF-Plugins"
license=(ISC GPL2 GPL3 LGPL3 MIT)
groups=(ladspa-plugins lv2-plugins pro-audio vst-plugins)
depends=(libglvnd)
makedepends=(dssi gendesk ladspa lv2 projectm)
checkdepends=(lv2lint kxstudio-lv2-extensions)
optdepends=(
  'dssi-host: for dssi-plugins'
  'jack: for standalone applications'
  'ladspa-host: for LADSPA plugins'
  'liblo: for dssi plugins'
  'lv2-host: for LV2 plugins'
  'projectm: for ProM plugin'
  'vst-host: for VST plugins'
  'vst3-host: for VST3 plugins'
)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/DISTRHO/${pkgname}/archive/v${pkgver}.tar.gz"
  "${pkgname}-1.5-install_vst3.patch::https://github.com/DISTRHO/DPF-Plugins/commit/cc3877b98d23ae571bd3a825067169ff8496e274.patch"
)
sha512sums=('14667f301e7759d3152b98f3ac4140199443f405b235ec6badfdba5c0cc27227a2f291e90e2e7e73b9837e0db60e905b62d584fe41480e77a5fbfb2f5ff293fd'
            '46fb574950d2a5ba5f51f11e7781b61c48dda4d8d627277e8a94b143c6c30a93c9696e52aab2fb7d7ff2bef9b90fdebcea07dc725027bd79f0af73140b84dc07')
b2sums=('80b773179ac65b601166007a661df9e872573ea3085fd4ffd4457deb61a2481a203d129eb210e18a0eba41de54f9119bdc5aca5e1f0987f60f04666d5b63b31c'
        '08e41e8385c8121f36f0eb926b26ff678480ffa394f3af7af43544f6342dcc8df0651f53f0d87c7d985e832bb1b547d543e4b85da8955ebf8b3204960a85406a')

_names=(3BandEQ 3BandSplitter AmplitudeImposer CycleShifter Kars MVerb
MaBitCrush MaFreeverb MaGigaverb MaPitchshift Nekobi PingPongPan SoulForce
glBars)

prepare() {
  declare -A _comments=(
    ["3BandEQ"]="A three band equalizer"
    ["3BandSplitter"]="A three band splitter"
    ["AmplitudeImposer"]="Takes 2 stereo inputs and imposes the amplitude envelope of the first one on the second one"
    ["CycleShifter"]="Reads in a cycle's-worth of the input signal, then (once the whole cycle's been read in) outputs it again, on top of the current output"
    ["Kars"]="Simple karplus-strong plucked string synth"
    ["MVerb"]="A studio quality, open-source reverb"
    ["MaBitCrush"]="A bit crusher"
    ["MaFreeverb"]="A reverb based on freeverb"
    ["MaGigaverb"]="A reverb based on gigaverb"
    ["MaPitchshift"]="A pitch shifter"
    ["Nekobi"]="A simple single-oscillator synth based on the Roland TB-303"
    ["PingPongPan"]="A Ping Pong Panner"
    ["SoulForce"]="A fairly standard waveshaping distortion plugin"
    ["glBars"]="An OpenGL bars visualization plugin"
  )
  declare -A _generic=(
    ["3BandEQ"]="Equalizer"
    ["3BandSplitter"]="Splitter"
    ["AmplitudeImposer"]="Wave Shaper"
    ["CycleShifter"]="Cyclic Input Processor"
    ["Kars"]="Plucked string synthesizer"
    ["MVerb"]="Reverb"
    ["MaBitCrush"]="Bit crusher"
    ["MaFreeverb"]="Reverb"
    ["MaGigaverb"]="Reverb"
    ["MaPitchshift"]="Pitch shifter"
    ["Nekobi"]="Synthesizer"
    ["PingPongPan"]="Ping Pong Panner"
    ["SoulForce"]="Waveshaping Distortion"
    ["glBars"]="Bars Visualization"
  )
  local _name

  mv -v "DPF-Plugins-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"

  # install vst3 plugins as well
  patch -Np1 -i ../"${pkgname}-1.5-install_vst3.patch"

  for _name in "${_names[@]}"; do
    gendesk -n \
            --pkgname "net.sf.distrho.${_name}" \
            --name "${_name}" \
            --exec "/usr/bin/${_name}" \
            --pkgdesc "${_comments[$_name]}" \
            --genericname "${_generic[$_name]}" \
            --categories "AudioVideo;Audio"
  done
}

build() {
#  make BUILD_VST=true BUILD_DSSI=true BUILD_LV2=true BUILD_JACK=true -C "$pkgname-$pkgver"
  make V=1 -C "$pkgname-$pkgver"
}

check() {
  local _name
  cd "$pkgname-$pkgver"
  # except symbol pattern from tests as it is hard to remove according to upstream
  for _name in "${_names[@]}"; do
    lv2lint -s "lv2_generate_ttl" -Mpack -I "bin/${_name}.lv2/" "http://distrho.sf.net/plugins/${_name}" || echo "Known to fail"
  done
}

package() {
  cd "$pkgname-$pkgver"
  make V=1 DESTDIR="$pkgdir/" PREFIX=/usr install
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # licenses
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 dpf/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/dpf/"
  install -vDm 644 plugins/Kars/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/Kars/"
  install -vDm 644 plugins/AmplitudeImposer/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/AmplitudeImposer/"
  install -vDm 644 plugins/CycleShifter/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/CycleShifter/"
  install -vDm 644 plugins/SoulForce/LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/SoulForce/"
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications"
}
