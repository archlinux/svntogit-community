# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=drbd-utils
pkgver=8.9.10
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Userland tools for Distributed Replicated Block Device'
url='https://www.drbd.org'
license=('GPL2')
depends=('perl' 'bash')
makedepends=('libxslt' 'systemd')
conflicts=('drbd')
replaces=('drbd')
source=("https://www.drbd.org/download/drbd/utils/drbd-utils-$pkgver.tar.gz")
backup=('etc/drbd.conf' 'etc/drbd.d/global_common.conf')
md5sums=('186a59a714084026c074ce7d8f2a9d11')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libdir=/usr/lib \
    --with-distro=generic \
    --with-bashcompletion \
    --with-initscripttype=systemd \
    --with-udev \
    --without-rgmanager \
    --without-pacemaker \
    --without-heartbeat \
    --without-83support \
    --without-xen
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  cd "$pkgdir"

  # move /lib files under /usr/lib
  mv lib/drbd/* usr/lib/drbd
  rmdir lib/drbd lib

  # move bash completion
  install -dm 755 usr/share/bash-completion
  mv etc/bash_completion.d usr/share/bash-completion/completions

  # remove /var/lock
  rmdir var/lock

  # autoload module
  install -Dm 644 /dev/null usr/lib/modules-load.d/drbd.conf
  echo 'drbd' > usr/lib/modules-load.d/drbd.conf
}

# vim:set ts=2 sw=2 et:
