# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>

pkgname=driconf
pkgver=0.9.1
pkgrel=11
pkgdesc="A graphical configuration tool for the Direct Rendering Infrastructure"
arch=('any')
url="http://dri.freedesktop.org/wiki/DriConf"
license=('GPL')
depends=('pygtk' 'expat' 'xorg-xdriinfo' 'mesa-demos')
source=("https://sources.archlinux.org/other/community/driconf/driconf-$pkgver.tar.gz"
	"driconf_simpleui.patch::https://bugs.archlinux.org/task/36934?getfile=10983")
md5sums=('76d610bcd56aa5e8a489debb5081178a'
         '66e249ff813c47811c4e8edf2e007e42')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p0 < $srcdir/driconf_simpleui.patch
  sed -i -e "s:/usr/local/lib/driconf:$pkgdir/usr/lib/driconf:g" -e "s:/usr/local:$pkgdir/usr:g" setup.cfg
  sed -i -e "s:local/::" driconf
  sed -i -e "s:/usr/local:/usr:g" setup.py
  sed -i -e "s:/usr/local:/usr:g" driconf_commonui.py
  sed -i -e "s:/usr/local:/usr:g" driconf.desktop
}

package() {
  cd $srcdir/$pkgname-$pkgver
  python2 setup.py install
  install -Dm644 $pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 drilogo.jpg $pkgdir/usr/share/pixmaps/drilogo.jpg
}
