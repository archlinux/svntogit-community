# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrei "Garoth" Thorp <my-username at gmail dot com>
# Contributor: Alexandr Nevskiy <kepkin@gmail.com>

pkgname=drivel
pkgver=3.0.5
pkgrel=2
pkgdesc="GTK client for working with online journals (blogs)"
url="http://drivel.sourceforge.net/"
arch=('x86_64')
license=('GPL')
depends=('gnome-vfs' 'gtksourceview2' 'gtkspell' 'libsoup' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils')
source=(http://downloads.sourceforge.net/drivel/$pkgname-$pkgver.tar.gz
	build-fix.patch)
md5sums=('b7922ea0779e62f43a85dd6c5df3f931'
         '492e6eef68e27e62d3f892dd5d88482f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <"$srcdir"/build-fix.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --disable-desktop-update --disable-schemas-install \
      --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
