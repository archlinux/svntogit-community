# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <`echo 'moc tod liamg ta nosaj tod ecreip' | rev`>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>

pkgname=dropbear
pkgver=2017.75
pkgrel=1
pkgdesc="Lightweight replacement for sshd"
arch=('i686' 'x86_64')
url="http://matt.ucc.asn.au/dropbear/dropbear.html"
license=('MIT')
depends=('zlib')
source=(https://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2{,.asc}
        $pkgname.service)
sha256sums=('6cbc1dcb1c9709d226dff669e5604172a18cf5dbf9a201474d5618ae4465098c'
            'SKIP'
            '8890a35b6cb14745c1b22f80d3a8532de2a853c78a30eac72b954012e286504a')
validpgpkeys=('F7347EF2EE2E07A267628CA944931494F29C6773')

prepare() {
  sed -i 's|usr/libexec/sftp|usr/lib/ssh/sftp|' $pkgname-$pkgver/options.h
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver

  make install DESTDIR="${pkgdir}/"

  # Configuration files
  install -d "${pkgdir}/etc/$pkgname"
  install -D -m644 "${srcdir}/$pkgname.service" "${pkgdir}/usr/lib/systemd/system/$pkgname.service"

  # License file
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}

