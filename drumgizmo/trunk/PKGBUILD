# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)
pkgname=drumgizmo
pkgver=0.9.18.1
pkgrel=1
pkgdesc="Multichannel, multilayered, cross-platform drum plugin and stand-alone application"
arch=('x86_64')
url="https://www.drumgizmo.org"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libjack.so' 'libsmf.so' 'libsndfile'
'libxext' 'libx11' 'libzita-resampler.so')
makedepends=('lv2')
source=("https://www.${pkgname}.org/releases/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('c7a9cdf2eae4ad1edfc4f9346ca9b23ad2e8f88f2de5792e9dfe8c0089885be976a142a1272ce202f4264e5a6cb747d41fe7450b97c181bd778486fd803be585'
            'SKIP')
validpgpkeys=('F39C94D556CCB995B1AE540E9EB89445BF071867')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-lv2
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 {AUTHORS,ChangeLog,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
