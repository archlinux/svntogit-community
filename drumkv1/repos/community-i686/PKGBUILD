# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.7.4
pkgrel=2
pkgdesc="An old-school drum-kit sampler"
arch=(i686 x86_64)
url="http://$pkgname.sourceforge.net/$pkgname-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
         'hicolor-icon-theme' 'shared-mime-info')
source=("http://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('eb954772794f6c3aa993d192ef417ade')

prepare() {
  cd $pkgname-$pkgver

  # x86_64 lib path fix
  sed -i "s/lib64/lib/" src/src_lv2{,ui}.pro
  # fix build with GCC 6 (Fedora)
  sed -e "s|^ac_with_paths=.*|ac_with_paths=|g" -i configure configure.ac
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
