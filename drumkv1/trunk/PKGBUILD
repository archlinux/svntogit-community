# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.9.17
pkgrel=1
pkgdesc="An old-school drum-kit sampler"
arch=('x86_64')
url="https://drumkv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'liblo' 'libsndfile' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'non-session-manager: for session management using the standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('148f1d112b1caaa770fb0968a048cc00aa542f263d4ca57e65f54ff85e8f5d316f4ec8e884c10eb51618c8abff6eb137529e9be0d967f0bc397add68d7a54a81')
b2sums=('185f5f652be0d463593455c5419d7973b7200781a85ba5785ee1183ed39471392b4ab53b172c2c5bd746bc798d55f443faf78956337c27d4dbb93e373dbc1755')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
