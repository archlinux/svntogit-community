# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.7.1
pkgrel=2
pkgdesc="MIDI libraries for Qt5/C++"
arch=(x86_64)
url="https://drumstick.sourceforge.io/"
license=(GPL2)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-5compat)
makedepends=(alsa-lib cmake doxygen docbook-xsl fluidsynth graphviz libpulse
qt6-tools sonivox)
provides=(libdrumstick-alsa.so libdrumstick-file.so libdrumstick-rt.so
libdrumstick-widgets.so)
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('5cd41c03732929d5633c348455d6d7b2bd842afaa275996628d8f51c5a5ae3712717ade9b65d166413d83101754632b1ce5fa57c092da687b8e5e73b3199042e')
b2sums=('4b22bc73b39e9436de217785f95acf5dc13b8c1d9cecc49873ba89cb5301814acfb559a241f1590a3d061c4bb55040267c2e3fdbe5115375a3711b0ca8c2cbe8')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=(libasound.so libfluidsynth.so libpulse.so libpulse-simple.so libsonivox.so)

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,NEWS,readme.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
