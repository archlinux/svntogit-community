# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=1.1.0
pkgrel=3
pkgdesc="MIDI libraries for Qt5/C++"
arch=('x86_64')
url="http://drumstick.sourceforge.net/"
license=('GPL2')
depends=('desktop-file-utils' 'fluidsynth' 'hicolor-icon-theme' 'shared-mime-info' 'qt5-svg')
makedepends=('cmake' 'doxygen' 'docbook-xsl')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('1099b80e1d2819a8343e877742c5f01c114419b2c63a965717bcff2c220aa218f40ac3860cf9496d3586663e73a6c2c7b62aee74c51229578a5ae3044da208da')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DLIB_SUFFIX=
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -Dm644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"
}
