# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Corrado Primier <cp663137@stud.polimi.it>

pkgname=drupal
pkgver=7.9
pkgrel=1
pkgdesc="A PHP-based content management platform"
arch=('any')
url="http://www.drupal.org/"
license=('GPL')
depends=('php')
install=('drupal.install')
options=(!strip)
backup=(etc/webapps/drupal/.htaccess)
source=(http://www.drupal.org/files/projects/$pkgname-$pkgver.tar.gz)
md5sums=('7f45f109c413ca69ebb6e3140ed47225')

build() {
  _instdir=$pkgdir/usr/share/webapps/drupal
  mkdir -p ${_instdir}
  cd ${_instdir}
  cp -r $srcdir/$pkgname-$pkgver/{*,.htaccess} ${_instdir}

  # move .htaccess
  mkdir -p $pkgdir/etc/webapps/drupal
  sed -i '1,1ideny from all' ${_instdir}/.htaccess
  mv ${_instdir}/.htaccess $pkgdir/etc/webapps/drupal/.htaccess
  ln -s /etc/webapps/drupal/.htaccess ${_instdir}/.htaccess

  # move sites/
  mkdir -p $pkgdir/var/lib/drupal/
  mv ${_instdir}/sites $pkgdir/var/lib/drupal/sites
  ln -s /var/lib/drupal/sites ${_instdir}/sites

  # apache config example
  cat >$pkgdir/etc/webapps/drupal/apache.example.conf <<EOF
	Alias /drupal "/usr/share/webapps/drupal"
	<Directory "/usr/share/webapps/drupal">
		AllowOverride All
		Options FollowSymlinks
		Order allow,deny
		Allow from all
		php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/:/var/lib/drupal"
	</Directory>
EOF
}
