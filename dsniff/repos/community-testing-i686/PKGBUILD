# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ViNS <gladiator@fastwebnet.it>

pkgname=dsniff
pkgver=2.4b1
pkgrel=26
pkgdesc="Collection of tools for network auditing and penetration testing"
url="http://www.monkey.org/~dugsong/dsniff/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libpcap' 'openssl' 'libxmu' 'glib2' 'libnet' 'libnids')
source=("https://www.monkey.org/~dugsong/${pkgname}/beta/$pkgname-$pkgver.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/d/${pkgname}/${pkgname}_2.4b1+debian-22.1.debian.tar.gz" dsniff-openssl-1.1.patch)
md5sums=('2f761fa3475682a7512b0b43568ee7d6'
         'e80551b70dc3976d4cdddd3319cca9eb'
         'd5ec0f6baa1d3fa713fa71c80f154cab')

prepare() {
  cd "$srcdir"/$pkgname-2.4
  for i in $(cat ../debian/patches/series); do
    patch -N < "../debian/patches/$i"
  done
  sed -i 's|${CC-cc} -E|${CC-cc} -O2 -E|g' configure
  patch -p1 -i ../dsniff-openssl-1.1.patch
}

build() {
  cd "$srcdir"/$pkgname-2.4
  LDFLAGS="-lresolv -lglib-2.0 -lgthread-2.0 $LDFLAGS" ./configure \
    --prefix=/usr --sbindir=/usr/bin
  make -j1
}

package() {
  cd "$srcdir"/$pkgname-2.4
  make install_prefix="$pkgdir" install
  install -D -m0644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  install -d "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/
}
