# $Id$
# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ViNS <gladiator@fastwebnet.it>

pkgname=dsniff
pkgver=2.4b1
pkgrel=26
pkgdesc="Collection of tools for network auditing and penetration testing"
url="https://www.monkey.org/~dugsong/dsniff/"
arch=('x86_64')
license=('BSD')
depends=('libpcap' 'openssl' 'libxmu' 'glib2' 'libnet' 'libnids')
source=("https://www.monkey.org/~dugsong/${pkgname}/beta/$pkgname-$pkgver.tar.gz"
        "http://ftp.de.debian.org/debian/pool/main/d/${pkgname}/${pkgname}_2.4b1+debian-22.1.debian.tar.gz" dsniff-openssl-1.1.patch)
sha256sums=('a9803a7a02ddfe5fb9704ce86f0ffc48453c321e88db85810db411ba0841152a'
            '28ddb8514c6a7a17f51f428b6fada299baf5a3efa153f77f081ac6c1e6466633'
            'cd87f75b167b2b9b6d31e158610731f1305edd7339c6456c6bcb15f0849b02eb')

prepare() {
  cd "$srcdir"/$pkgname-2.4
  for i in $(cat ../debian/patches/series); do
    patch -N < "../debian/patches/$i"
  done
  sed -i 's|${CC-cc} -E|${CC-cc} -O2 -E|g' configure
  patch -p1 -i ../dsniff-openssl-1.1.patch
}

build() {
  cd "$srcdir"/$pkgname-2.4
  LDFLAGS="-lresolv -lglib-2.0 -lgthread-2.0 $LDFLAGS" ./configure \
    --prefix=/usr --sbindir=/usr/bin
  make -j1
}

package() {
  cd "$srcdir"/$pkgname-2.4
  make install_prefix="$pkgdir" install
  install -D -m0644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  install -d "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/
}
