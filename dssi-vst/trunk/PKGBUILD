# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Ray Rashif <schivmeister@gmail.com>

pkgname=dssi-vst
pkgver=0.9.2
pkgrel=5
pkgdesc="DSSI adapter/wrapper for win32 VST plug-ins"
arch=('i686' 'x86_64')
url="http://www.breakfastquay.com/dssi-vst/"
license=('GPL')
depends=('wine' 'jack' 'liblo')
makedepends=('dssi' 'ladspa')
source=("http://code.breakfastquay.com/attachments/download/10/$pkgname-$pkgver.tar.bz2"
	"$pkgname-winexec.patch")
md5sums=('5c569200571de76dac18be4eb6fbd9c8'
         '3e445482c8c5302de43be806f0c7506f')

prepare() {
  cd $pkgname-$pkgver

  # fix executable extension issue
  patch -p1 -i ../$pkgname-winexec.patch

  # use CXXFLAGS in build
  sed -i "/m32/! s/CXXFLAGS)/& $CXXFLAGS/" Makefile

  # [x86_64] use /usr/lib32/dssi
  if [[ $CARCH = x86_64 ]]; then
    sed -i '/\/dssi-vst/s|DSSIDIR|&32|' Makefile
    sed -i '/DSSI_PATH/s|\(/usr/lib\)\(/dssi\)|&:\132\2|' remotevstclient.cpp
  fi
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make	BINDIR="$pkgdir/usr/bin" \
  	DSSIDIR="$pkgdir/usr/lib/dssi" \
  	DSSIDIR32="$pkgdir/usr/lib32/dssi" \
	LADSPADIR="$pkgdir/usr/lib/ladspa" install
}
