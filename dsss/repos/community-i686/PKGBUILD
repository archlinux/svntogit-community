# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Ustalov "eveel" Dmitry <eveeel@gmail.com>
pkgname=dsss
pkgver=0.78
pkgrel=2
pkgdesc="The D Shared Software System"
arch=(i686)
url="http://dsource.org/projects/dsss"
license=('MIT')
depends=(gcc-libs)
makedepends=('dmd' 'libphobos')
source=(http://svn.dsource.org/projects/dsss/downloads/$pkgver/dsss-$pkgver.tar.bz2)
md5sums=('e018d5174a8d1a0c873056634389a060')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # rebuild makefile is broken
  export MAKEFLAGS="-j1"

  make -f Makefile.dmd.posix || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  ./dsss install \
    --prefix="$pkgdir/usr" \
    --libdir="$pkgdir/usr/lib" \
    --includedir="$pkgdir/usr/include/d" \
    --docdir="$pkgdir/usr/share/doc" \
    --sysconfdir="$pkgdir/etc" \
    --scratchdir="$pkgdir/tmp" || return 1

  # Fix permissions.
  chmod 644 "$pkgdir/usr/share/dsss/manifest/dsss.manifest"
  install -Dm644 docs/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}
