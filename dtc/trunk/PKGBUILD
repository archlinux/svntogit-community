# Maintainer: Anatol Pomozov
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.5.0
pkgrel=1
pkgdesc='Device Tree Compiler'
url='http://www.devicetree.org/'
arch=(x86_64)
license=(GPL2)
makedepends=(swig python2)
checkdepends=(valgrind)
source=(https://www.kernel.org/pub/software/utils/dtc/dtc-$pkgver.tar.xz)
sha256sums=('c672e443c9f7e39f5a7c8e602da6777f9ad55ad70de87de300a43828c8050172')

prepare() {
  cd dtc-$pkgver
  sed -i 's/-Werror//' Makefile
  sed -i 's/python\b/python2/' tests/run_tests.sh
}

build() {
  cd dtc-$pkgver
  make
}

check() {
  cd dtc-$pkgver
  make check
}

package() {
  cd dtc-$pkgver
  DESTDIR="$pkgdir" make SETUP_PREFIX="$pkgdir/usr" PREFIX="$pkgdir/usr" install
}

