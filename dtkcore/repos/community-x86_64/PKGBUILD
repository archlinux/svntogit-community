# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=5.6.0.2
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('LGPL3')
options=('debug')
depends=('dtkcommon' 'deepin-desktop-base' 'python' 'gsettings-qt' 'lshw')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtkcore/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8a2612b7f1b72f78e18fe51cee0d1de54a9579020fe80d01861a8e9cb4bbee64f05b9aae849577ca1b17443fe3bec0ef7653842f8a95a870b0a0343bafd5c85c')

prepare() {
  cd dtkcore-$pkgver
  sed -i 's|/etc/os-version|/etc/uos-version|' src/dsysinfo.cpp
}

build() {
  cd dtkcore-$pkgver
  cmake -GNinja \
      -DLINUXNAME="archlinux"\
      -DBUILD_DOCS=ON \
      -DBUILD_EXAMPLES=OFF \
      -DQCH_INSTALL_DESTINATION=/usr/share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

check() {
  cd dtkcore-$pkgver
  ninja check || echo "Tests failed"
}

package() {
  cd dtkcore-$pkgver
  DESTDIR="$pkgdir" ninja install
}
