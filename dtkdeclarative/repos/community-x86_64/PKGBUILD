# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zccrs <zhangjide@deepin.org>

pkgname=dtkdeclarative
pkgver=5.6.0
pkgrel=3
pkgdesc='Deepin Toolkit, declarative module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkdeclarative"
license=('LGPL3')
depends=('dtkgui' 'qt5-declarative' 'qt5-quickcontrols2')
makedepends=('qt5-tools' 'gtest' 'gmock')
source=("https://github.com/linuxdeepin/dtkdeclarative/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b255563a8ba7f6daa909b7542545500f9163f51df5f4f11ca1c86bed4319e762ca6d6858d90f4c548e7b987e17b3108e17f593013f1109ba534ca0ba32cb18d3')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
