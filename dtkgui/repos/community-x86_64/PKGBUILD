# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.5.17.1
pkgrel=2
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore' 'librsvg' 'qt5-x11extras')
makedepends=('qt5-tools' 'gtest')
source=("https://github.com/linuxdeepin/dtkgui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('15830b286ab7cec5feac76bcb11b47e00afecfd0a22e5086f64393ed31d3ef78999d9e40c136f49171ee14c07e1128060ef544733c9215ef9e50211fb3008e8f')

build() {
  cd dtkgui-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd dtkgui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
