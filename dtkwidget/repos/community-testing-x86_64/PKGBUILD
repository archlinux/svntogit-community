# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.6.3
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
options=('debug')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-imageformats' 'qt5-multimedia'
         'qt5-svg' 'qt5-x11extras' 'startup-notification')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("https://github.com/linuxdeepin/dtkwidget/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('72dae5bad3748ba336b1cc26af31e7314b38e89533fc48fd4fd8fa95d77a0586482f4c318f1380b412899047b8d736686ad8c9b7431c844c0714450567902645')

build() {
  cd dtkwidget-$pkgver
  cmake -GNinja \
      -DHUNTER_ENABLED=OFF \
      -DLINUXNAME="archlinux" \
      -DNOTPACKAGE=OFF \
      -DBUILD_DOCS=ON \
      -DQCH_INSTALL_DESTINATION=/usr/share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkwidget-$pkgver
  DESTDIR="$pkgdir" ninja install
}
