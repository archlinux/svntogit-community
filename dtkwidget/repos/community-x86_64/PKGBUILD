# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.6.0.2
pkgrel=2
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
options=('debug')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("https://github.com/linuxdeepin/dtkwidget/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        dtk-translation-dir.patch::https://github.com/linuxdeepin/dtkwidget/commit/96049a6d49661c50a79f85ef81b948f2ce665176.patch)
sha512sums=('027f42047b2ad10e9c2a36c48120a56b1cda808c7d79f71d30a9c3db1639321a7a71353d44a4ea570473f0962ba9a211f876cf337cbbd0c996537dec01f243b2'
            'ad5b8e7aa7cbabf2088ab5e6dbbe2bf637bd1af6b65dc292f2c5aa2da0e8fe68fb7a15cb3c154d621ad9850f91a4cea9e325949e1ca2e2a73e4581265f91ffa6')

prepare() {
  cd dtkwidget-$pkgver
  patch -p1 -i ../dtk-translation-dir.patch
}

build() {
  cd dtkwidget-$pkgver
  cmake -GNinja \
      -DHUNTER_ENABLED=OFF \
      -DLINUXNAME="archlinux" \
      -DNOTPACKAGE=OFF \
      -DBUILD_DOCS=ON \
      -DQCH_INSTALL_DESTINATION=/usr/share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkwidget-$pkgver
  DESTDIR="$pkgdir" ninja install
}
