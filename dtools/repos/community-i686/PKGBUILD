# $Id$
# Maintainer: Mihails Strasunse <public@dicebot.lv>

pkgname=('dtools')
pkgver=2.072.2
pkgrel=1
pkgdesc="Ancilliary tools for the D programming language"
license=("custom")
groups=('dlang')
arch=('i686' 'x86_64')
url="http://www.dlang.org"
makedepends=('dmd' 'libphobos' 'git')
depends=('curl')

source=(
    "git+http://github.com/D-Programming-Language/tools#commit=3a32331303e2e62f429b8136ba0de0e9a9294722"
    "http://www.boost.org/LICENSE_1_0.txt"
    "autodetect.diff"
)
sha256sums=('SKIP'
          'c9bff75738922193e67fa726fa225535870d2aa1059f91452c411736284ad566'
          '283f6708499b62b0d2e1429f329f809dc6f2fd659c51e8d3cd0d16ce81efe70d')

build()
{
    # DMD=`$srcdir/Arch-PKGBUILDs/d-compiler.sh`
    DMD=dmd

    cd $srcdir/tools
    git apply $srcdir/autodetect.diff

    # rdmd is used to build others
    $DMD -od. -ofdtools-rdmd rdmd.d

    for tool in DustMite/dustmite ddemangle catdoc detab tolf dget; do
        ./dtools-rdmd --compiler=$DMD --build-only -od. -ofdtools-`basename $tool` $tool.d
    done
}

package()
{
    cd $srcdir/tools

    mkdir -p $pkgdir/usr/bin
    for tool in rdmd ddemangle catdoc detab tolf dget dustmite; do
        install -m755 -t $pkgdir/usr/bin $pkgname-$tool
    done

    cd $pkgdir
    ln -s dtools-rdmd usr/bin/rdmd
    ln -s dtools-dustmite usr/bin/dustmite

    install -Dm644 $srcdir/LICENSE_1_0.txt usr/share/licenses/$pkgname/LICENSE
}
