# $Id$
# Maintainer: Mihails Strasunse <public@dicebot.lv>

pkgname=('dtools')
pkgver=2.071.0
pkgrel=2
pkgdesc="Ancilliary tools for the D programming language"
license=("custom")
groups=('dlang')
arch=('i686' 'x86_64')
url="http://www.dlang.org"
makedepends=('dmd' 'libphobos' 'git')
depends=('curl')

source=(
    "git+http://github.com/D-Programming-Language/tools#tag=v$pkgver"
    "http://www.boost.org/LICENSE_1_0.txt"
    "autodetect.diff"
)
sha1sums=('SKIP'
          '3cba29011be2b9d59f6204d6fa0a386b1b2dbd90'
          '7fb5bd71d6c4e90c0ca49b292454e11169afdee8')

prepare()
{
    # rdmd is broken without https://github.com/dlang/tools/pull/185
    sed -i 's|std.syserror|std.windows.syserror|g' $srcdir/tools/rdmd.d
}

build()
{
    # DMD=`$srcdir/Arch-PKGBUILDs/d-compiler.sh`
    DMD=dmd

    cd $srcdir/tools
    git apply $srcdir/autodetect.diff

    # rdmd is used to build others
    $DMD -od. -ofdtools-rdmd rdmd.d

    for tool in DustMite/dustmite ddemangle catdoc detab tolf dget; do
        ./dtools-rdmd --compiler=$DMD --build-only -od. -ofdtools-`basename $tool` $tool.d
    done
}

package()
{
    cd $srcdir/tools

    mkdir -p $pkgdir/usr/bin
    for tool in rdmd ddemangle catdoc detab tolf dget dustmite; do
        install -m755 -t $pkgdir/usr/bin $pkgname-$tool
    done

    cd $pkgdir
    ln -s dtools-rdmd usr/bin/rdmd
    ln -s dtools-dustmite usr/bin/dustmite

    install -Dm644 $srcdir/LICENSE_1_0.txt usr/share/licenses/$pkgname/LICENSE
}
