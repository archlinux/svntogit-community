# $Id$
# Maintainer: Dan Printzell <arch@vild.io>
# Contributor: Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>

pkgname=('dub')
pkgver=1.7.0
pkgrel=1
pkgdesc="Developer package manager for D programming language"
arch=('x86_64')
url="https://github.com/D-Programming-Language/dub"
license=('MIT')
groups=('dlang')
makedepends=('dmd')
depends=('libphobos' 'curl')

source=("$pkgname-$pkgver.tar.gz::https://github.com/dlang/dub/archive/v$pkgver.tar.gz")
sha512sums=('3ce5c7a88264b7bae441a71e83f18e71ad6ca0d8ccd3409ada3873bdd1e90ce10be65fd5291a272ce6117e93ac68649898d905bc47d656efe12604296f8226dd')

build() {
	DC=dmd
	cd "$pkgname-$pkgver"

	echo Generating version file...
	echo "module dub.version_;" > source/dub/version_.d
	echo "enum dubVersion = \"$pkgver\";" >> source/dub/version_.d

	$DC -ofbin/dub -w -O -g -version=DubUseCurl -Isource -L-lcurl @build-files.txt
}

package() {
	# binaries
	install -Dm755 "$pkgname-$pkgver/bin/dub" "$pkgdir/usr/bin/dub"

	# license
	install -Dm644 "$pkgname-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# bash completion
	install -Dm644 "$pkgname-$pkgver/scripts/bash-completion/dub.bash" "$pkgdir/usr/share/bash-completion/completions/dub"
}
