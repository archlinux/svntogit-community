# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Christian Muehlhaeuser <muesli at gmail dot com>

pkgname=duf
pkgver=0.6.2
pkgrel=3
pkgdesc='Disk Usage/Free Utility'
arch=(x86_64)
url="https://github.com/muesli/$pkgname"
license=(MIT)
depends=(glibc)
makedepends=(git
             go)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('f2314d8e5e133a6ce93968b3450c1710a3e432cb4a5dfc528aa0317d968a8988')

build() {
	local commit=$(zcat $_archive.tar.gz | git get-tar-commit-id)
	local extraflags="-X main.Version=$pkgver -X main.CommitSHA=$commit"
	export CGO_CPPFLAGS="$CPPFLAGS"
	export CGO_CFLAGS="$CFLAGS"
	export CGO_CXXFLAGS="$CXXFLAGS"
	cd "$_archive"
	go build \
		-trimpath \
		-buildmode=pie \
		-mod=readonly \
		-modcacherw \
		-ldflags "-linkmode external $extraflags -extldflags \"$LDFLAGS\"" \
		-o "$pkgname" .
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
