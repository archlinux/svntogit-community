# $Id: PKGBUILD 162811 2016-02-20 15:24:54Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape
pkgver=1.4.0
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=('x86_64' 'i686')
url='http://duktape.org/'
makedepends=('setconf')
depends=('glibc')
license=('MIT')
source=("http://duktape.org/duktape-$pkgver.tar.xz"
        'duktape.pc')
md5sums=('7932d6dc99b8e11ec704a1ad8a7932ff'
         '2cf394714b5abb589ecd36a4742a8257')

prepare() {
  cd "$pkgname-$pkgver"

  mv Makefile.sharedlibrary Makefile
  setconf Makefile INSTALL_PREFIX="$pkgdir/usr"
}

build() {
  make -C "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/lib" "$pkgdir/usr/include"
  make install
  install -Dm644 ../duktape.pc "$pkgdir/usr/lib/pkgconfig/duktape.pc"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/svaarala/duktape
# vim:set ts=2 sw=2 et:
