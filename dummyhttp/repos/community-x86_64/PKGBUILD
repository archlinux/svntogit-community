# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=dummyhttp
pkgver=1.0.2
pkgrel=1
pkgdesc="Super simple HTTP server that replies a fixed body with a fixed response code"
arch=('x86_64')
url="https://github.com/svenstaro/dummyhttp"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/svenstaro/dummyhttp/archive/v${pkgver}.tar.gz")
sha512sums=('f7069139d335f744ff9416a6cb10112c78f25713dc669717eb205df1a256ee2f1fda40a697808f09d7b15a0bedd4b4ac2ab31c4523cc851abf1e5d24831e23e7')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/dummyhttp "$pkgdir"/usr/bin/dummyhttp

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  target/release/dummyhttp --print-completions bash > "$pkgdir"/usr/share/bash-completion/completions/dummyhttp
  target/release/dummyhttp --print-completions zsh > "$pkgdir"/usr/share/zsh/site-functions/_dummyhttp
  target/release/dummyhttp --print-completions fish > "$pkgdir"/usr/share/fish/vendor_completions.d/dummyhttp.fish

  mkdir -p "$pkgdir"/usr/share/man/man1
  target/release/dummyhttp --print-manpage | gzip > "$pkgdir"/usr/share/man/man1/$pkgname.1.gz

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
