# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.6.23
pkgrel=3
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm.'
arch=('i686' 'x86_64')
url='http://www.nongnu.org/duplicity/'
license=('GPL')
depends=('gnupg' 'librsync' 'ncftp' 'python2-paramiko' 'python2-lockfile')
optdepends=('lftp: FTPS backend'
            'python2-boto: Amazon S3 backend'
            'python2-gobject: GIO backend'
            'gvfs: GIO backend'
            'python2-gdata: Google Docs backend'
            'python2-httplib2: Ubuntu One backend'
            'python2-oauthlib: Ubuntu One backend'
            'rsync: rsync backend')
source=("http://savannah.nongnu.org/download/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        "gpg-encode.patch")
md5sums=('ae0e84446bcf114735de1057ed53c977'
         'SKIP'
         '366555c73aeebf7f15f6f37ec65176ce')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix gpg encrypted backup (FS#38838, upstream revision 961, will be fixed in 0.6.24).
  patch -Np0 -i ../gpg-encode.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python$_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python$_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  python2 setup.py install --root="$pkgdir" --optimize=1

  # fix broken documentation permissions until upstream does (FS#27301)
  chmod 644 "$pkgdir/usr/share/man/man1/$pkgname.1"
  chmod 644 "$pkgdir/usr/share/man/man1/rdiffdir.1"
  chmod 644 "$pkgdir/usr/share/doc/$pkgname-$pkgver"/*
}
