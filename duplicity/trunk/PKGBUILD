# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=1.0.1
pkgrel=2
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm'
arch=('x86_64')
url='https://duplicity.gitlab.io/'
license=('GPL')
depends=('gnupg' 'librsync' 'python-paramiko' 'python-fasteners' 'python-future')
makedepends=('python-setuptools-scm')
optdepends=('lftp: backend for ftp, ftps, fish'
            'ncftp: alternative backend for ftp'
            'python-boto: Amazon S3 and Google Cloud Storage backend'
            'python-dropbox: Dropbox backend'
            'python-gobject: GIO backend'
            'gvfs: GIO backend'
            'python-pydrive2: Google Drive backend'
            'python-requests-oauthlib: OneDrive backend'
            'rsync: rsync backend')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/duplicity/duplicity/-/archive/rel.$pkgver/duplicity-rel.$pkgver.tar.gz")
sha512sums=('3f92800b8c8d090e7c613bce81382659fd057f91e383faaca281af444f4237e903aefceb412078893ff5a7159a0bc73edfe50b0ef35b84f57e7b882ffcf12d12')
b2sums=('95627724b4593018dbe626c1496b7b94bf96cdc5923aea7d7f61ca267b7c16f0b6392749e8b22a593d8ec27288e4ef9353e2144b9eb19e984095de63a43727b1')

prepare() {
  cd "$pkgname-rel.$pkgver"

  # FS#76307 - fix version string
  sed -e "s/\$version/$pkgver/" -i "$pkgname/__init__.py"
}

package() {
  cd "$pkgname-rel.$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  # move documentation to the correct directory
  cd "$pkgdir/usr/share/doc"
  mv "$pkgname-$pkgver" "$pkgname"
}
