# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=dvdisaster
pkgver=0.72.4
pkgrel=2
pkgdesc="Provides a margin of safety against data loss on CD and DVD media caused by aging or scratches"
arch=('i686' 'x86_64')
url="http://www.dvdisaster.com/"
license=('GPL')
depends=('gtk2' 'hicolor-icon-theme')
options=('!makeflags')
install=dvdisaster.install
source=(http://dvdisaster.net/downloads/${pkgname}-${pkgver}.tar.bz2{,.gpg})
sha1sums=('2b47fb5a5535d90641f24a599cd8ae8ffd86b972'
          'SKIP')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/dvdisaster48/dvdisaster/' contrib/dvdisaster.desktop
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --mandir=/usr/share/man --docdir=/usr/share/doc \
    --localedir=/usr/share/locale \
    --with-nls=yes 
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make BUILDROOT="${pkgdir}" install
  rm -f "${pkgdir}/usr/bin/dvdisaster-uninstall.sh"
  install -D -m 644 contrib/dvdisaster.desktop "${pkgdir}/usr/share/applications/dvdisaster.desktop"
  for i in 16 24 32 48 64 ; do
    install -D -m 644 contrib/dvdisaster${i}.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/dvdisaster.png"
  done
}
