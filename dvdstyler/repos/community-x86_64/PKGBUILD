# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Leonidas Spyropoulos <artafinde at gmail com>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Mario J Steele <mario@ruby-im.net>

pkgname=dvdstyler
pkgver=3.1.2
pkgrel=2
pkgdesc="DVD authoring application for the creation of professional-looking DVDs"
arch=(x86_64)
url="https://www.dvdstyler.de/"
license=(GPL2)
depends=(cdrtools dvdauthor dvd+rw-tools mjpegtools wxsvg)
makedepends=(docbook-xsl xmlto zip)
source=("https://downloads.sourceforge.net/$pkgname/DVDStyler-$pkgver.tar.bz2"
        "config.patch"
        "$pkgname.appdata.xml")
sha256sums=('04ff61c278167363904f7ee366bcf98ee15ce05368fa353f6d8ab1e8305d9a0e'
            'ebcadbea33d96c955bd3811d7bb269fcb4006b8525dfafcf291e5779debba913'
            '4ecba886e6eca2018f659543fa2242d5801cd12a3b7d293897787f2c6bc9d8f4')

prepare() {
  cd DVDStyler-$pkgver
  patch -RNp1 -i ../config.patch
  sed -i 's|/pixmaps|/icons/hicolor/48x48/apps|' data/Makefile.{am,in}
}

build() {
  cd DVDStyler-$pkgver
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd DVDStyler-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
