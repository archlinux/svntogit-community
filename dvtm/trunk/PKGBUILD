# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: bender02 at gmx dot com

pkgname=dvtm
pkgver=0.13
pkgrel=1
pkgdesc='Dynamic virtual terminal manager.'
arch=('i686' 'x86_64')
url='http://www.brain-dump.org/projects/dvtm/'
license=('MIT')
depends=('ncurses')
source=("http://www.brain-dump.org/projects/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'config.h')
md5sums=('f4ab17eb6e79939182a40f66968ca5d0'
         'eed9c0af15233b449b1cd8874130f028')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	cp "${srcdir}/config.h" .
	sed -i 's/CFLAGS =/CFLAGS +=/' config.mk
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make clean
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
