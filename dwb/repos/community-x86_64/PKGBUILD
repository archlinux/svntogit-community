#$Id$ 
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: portix <portix at gmx.net>

pkgname=dwb
pkgver=2014.03.07
pkgrel=4
pkgdesc="A webkit web browser with vi-like keyboard shortcuts, stable snapshot"
url="http://portix.bitbucket.org/dwb/"
arch=('i686' 'x86_64')
install=dwb.install
license=('GPL')
depends=('webkitgtk2' 'desktop-file-utils')
makedepends=('json-c')
source=(https://bitbucket.org/portix/"${pkgname}"/downloads/"${pkgname}"-"${pkgver}".tar.gz
        0001-Set-G_APPLICATION_HANDLES_COMMANDLINE-for-glib2-2.40.patch
        isnan.patch
        Werror.patch)
sha1sums=('006e9df0c52219b9f022c1b4a3aa0518ff96295d'
          'd026b4caf83920b1319591d1f1487fc9f49035df'
          '557b50d0e8ecee1315371b5629d30959fcdf1919'
          'd5a66e8a16c25053a0693c0a3107aa1244ce3d88')

prepare(){
    patch -Np1 -d "$srcdir"/"$pkgname"-"$pkgver" -i "$srcdir"/0001-Set-G_APPLICATION_HANDLES_COMMANDLINE-for-glib2-2.40.patch
    patch -Np1 -d "$srcdir"/"$pkgname"-"$pkgver" -i "$srcdir"/isnan.patch
    patch -Np1 -d "$srcdir"/"$pkgname"-"$pkgver" -i "$srcdir"/Werror.patch

    sed -i 's|_BSD_SOURCE|_DEFAULT_SOURCE|' "$srcdir"/"$pkgname"-"$pkgver"/{config.mk,dwbem/dwbem.c,exar/exar.c}
}

build() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  make
}
package() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"
  export BASHCOMPLETION=/usr/share/bash-completion/completions
  make DESTDIR="${pkgdir}" install
}
