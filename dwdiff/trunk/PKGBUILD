# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=dwdiff
pkgver=2.1.3
pkgrel=1
pkgdesc="A front-end for the diff program that operates at the word level instead of the line level"
arch=('x86_64')
url="https://os.ghalkes.nl/dwdiff.html"
depends=('diffutils' 'icu')
license=('GPL')
source=("https://os.ghalkes.nl/dist/$pkgname-$pkgver.tar.bz2"
        icu68.patch)
sha256sums=('211ddbfaa2e6fcc85d5c88b5141c62a22a13ed0fecffc22fe6dded07e4cf2382'
            'b4248cedec5eec8ff280244e41c3450d302a47e50ec126b4dfdd91d7131dd941')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i ../icu68.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --without-dwfilter
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir"/usr install
}
