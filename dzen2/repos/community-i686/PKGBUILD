# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Mark Taylor <skymt0@gmail.com>

pkgname=dzen2
pkgver=0.9.5git
pkgrel=1
arch=(i686 x86_64)
pkgdesc="X notification utility"
url="https://github.com/robm/dzen"
license=("MIT")
depends=('libx11' 'libxpm' 'libxinerama' 'libxft')
makedepends=('gcc')
source=($pkgname-$pkgver.zip::https://github.com/robm/dzen/zipball/master)
md5sums=('bcaa565ebd8c3c7f3d06f8f20de6340a')

build()
{
  cd $srcdir/robm-dzen-*

  CFLAGS=" -Wall -Os ${INCS} -DVERSION=\"${VERSION}\" -DDZEN_XINERAMA -DDZEN_XPM -DDZEN_XFT `pkg-config --cflags xft`"
  LIBS=" -L/usr/lib -lc -lXft -lXpm -lXinerama -lX11"

  echo "CFLAGS=$CFLAGS" >>config.mk
  echo "LIBS=$LIBS" >>config.mk
  echo "LDFLAGS=$LIBS" >>config.mk

  make X11INC=/usr/include X11LIB=/usr/lib
  (cd gadgets && make X11INC=/usr/include X11LIB=/usr/lib)
}

package() {
  cd $srcdir/robm-dzen-*
  make PREFIX=/usr MANPREFIX=/usr/man DESTDIR=$pkgdir install
  (cd gadgets && make PREFIX=/usr MANPREFIX=/usr/man DESTDIR=$pkgdir install)
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/dzen2/COPYING
}
