# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=e-antic
pkgver=0.1.9
pkgrel=1
pkgdesc="Embedded algebraic number fields (on top of antic)"
arch=(x86_64)
url="https://github.com/videlec/e-antic"
license=(LGPL)
depends=(arb)
makedepends=()
source=(https://www.labri.fr/perso/vdelecro/e-antic/$pkgname-$pkgver.tar.gz)
sha256sums=('f73dad444fd81422d9a1594668615da94405fb6428ce8f2535b58f4cfe88a72d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
