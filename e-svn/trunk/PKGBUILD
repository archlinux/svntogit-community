# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=e-svn
pkgver=78876
pkgrel=1
pkgdesc="Enlightenment window manager DR17 (aka e17)"
arch=('i686' 'x86_64')
groups=('e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('alsa-lib' 'pm-utils' 'pam' 'shared-mime-info' 'hicolor-icon-theme'
  'desktop-file-utils' 'e_dbus-svn' 'edje-svn' 'efreet-svn' 'eeze-svn'
  'eio-svn' 'elementary-svn')
makedepends=('subversion')
conflicts=('e')
provides=('e' 'notification-daemon')
backup=('etc/enlightenment/sysactions.conf')
options=('!libtool' '!emptydirs')
install=e.install
source=('e-applications.menu'
  'quickstart.patch')
sha1sums=('e08cc63cb8a188a06705b42d03e032b9fcfa7ee5'
          '3f53931ae86de8fe99e386aeb097521aac0decd8')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/e"
_svnmod="e"

build() {
  cd "$srcdir"

  if [ $NOEXTRACT -eq 0 ]; then
    msg "Connecting to $_svntrunk SVN server...."
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

  fi

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  patch -p0 < $srcdir/quickstart.patch

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/enlightenment  \
    --enable-strict \
    --disable-static \
    --enable-pam \
    --disable-device-hal \
    --enable-device-udev \
    --disable-mount-hal \
    --enable-mount-udisks \
    --enable-mount-eeze \
    --enable-elementary \
    --enable-emotion \
    --enable-enotify \
    --disable-ephysics \
    --disable-wayland-clients \
    --disable-conf-wallpaper2 \
    --disable-illume2 \
    --disable-physics \
    --disable-doc
  make
}

package() {
  cd $srcdir/$_svnmod-build

  make DESTDIR=$pkgdir install

# install license files
  if [ -e $srcdir/$_svnmod-build/COPYING ]; then
    install -Dm644 $srcdir/$_svnmod-build/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
  fi

  if [ -e $srcdir/$_svnmod-build/COPYING-PLAIN ]; then
    install -Dm644 $srcdir/$_svnmod-build/COPYING-PLAIN \
        $pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN
  fi

  # install a default applications.menu file (mostly copy from gnome-menus)
  install -Dm644 $srcdir/e-applications.menu \
	$pkgdir/etc/xdg/menus/e-applications.menu

  rm -r $srcdir/$_svnmod-build
}


