# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=e_dbus-svn
pkgver=78869
pkgrel=1
pkgdesc="D-Bus integration with Ecore"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('ecore-svn' 'dbus-core')
makedepends=('subversion')
conflicts=('e_dbus')
provides=('e_dbus')
options=('!libtool' '!emptydirs')
source=()
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/e_dbus"
_svnmod="e_dbus"

build() {
  cd $srcdir

  if [ $NOEXTRACT -eq 0 ]; then
    msg "Connecting to $_svntrunk SVN server...."
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

  fi

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --disable-ebluez \
    --disable-econnman0_7x \
    --disable-ehal \
    --enable-enotify \
    --disable-eofono \
    --disable-eukit \
    --disable-edbus-test \
    --disable-edbus-test-client \
    --disable-edbus-bluez-test \
    --disable-edbus-connman0_7x-test \
    --disable-edbus-ofono-test \
    --disable-edbus-async-test \
    --disable-edbus-performance-test \
    --disable-doc
  make
}

package(){
  cd $srcdir/$_svnmod-build

  make DESTDIR=$pkgdir install

# install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING

  rm -r $srcdir/$_svnmod-build

}
