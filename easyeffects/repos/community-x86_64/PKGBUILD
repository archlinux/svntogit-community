# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Wellington <wellingtonwallace@gmail.com>
# Contributor: robertfoster

pkgname=easyeffects
pkgver=6.2.8
pkgrel=2
pkgdesc='Audio Effects for Pipewire applications'
arch=('x86_64')
url='https://github.com/wwmm/easyeffects'
license=('GPL3')
depends=('calf' 'gtk4' 'libebur128' 'libbs2b.so'
         'librubberband.so' 'libsamplerate.so' 'libsigc-3.0.so' 'libsndfile.so'
         'liblilv-0.so' 'lsp-plugins' 'pipewire-pulse' 'rnnoise' 'rubberband'
         'tbb' 'zita-convolver' 'libadwaita-1.so' 'libfmt.so')
makedepends=('appstream-glib' 'itstool' 'lv2' 'meson' 'nlohmann-json' 'zam-plugins')
optdepends=(
  'zam-plugins: maximizer'
  'mda.lv2: bass loudness plugin'
)
replaces=('pulseeffects')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('8a306308e783ed19fd5380a6b8d1e0ddba1400bca3447ddbf2954355f6d74229d0e750e5ce50b19c9ad1979ee88cda6ad4d1b2549eef7bb88cd29b6dc96dc33b')

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja install -C build
}
