# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Wellington <wellingtonwallace@gmail.com>
# Contributor: robertfoster

pkgname=easyeffects
pkgver=6.2.6
pkgrel=2
pkgdesc='Audio Effects for Pipewire applications'
arch=('x86_64')
url='https://github.com/wwmm/easyeffects'
license=('GPL3')
depends=('calf' 'gtk4' 'libebur128' 'libbs2b.so'
         'librubberband.so' 'libsamplerate.so' 'libsigc-3.0.so' 'libsndfile.so'
         'liblilv-0.so' 'lsp-plugins' 'pipewire-pulse' 'rnnoise' 'rubberband'
         'tbb' 'zita-convolver' 'libadwaita-1.so' 'libfmt.so')
makedepends=('appstream-glib' 'itstool' 'lv2' 'meson' 'nlohmann-json' 'zam-plugins')
optdepends=(
  'zam-plugins: maximizer'
  'mda.lv2: bass loudness plugin'
)
replaces=('pulseeffects')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('0c93c6afdc337bd5fe60beab00475087eece250ac7014a8b5ac0e6cad9154b3ac22cfa61a44bbbaa99aff79d4706772e903a056c3719a58d9507772684d6f767')

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja install -C build
}
