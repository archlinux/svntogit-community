# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Wellington <wellingtonwallace@gmail.com>
# Contributor: robertfoster

pkgname=easyeffects
pkgver=6.1.5
pkgrel=2
pkgdesc='Audio Effects for Pipewire applications'
arch=('x86_64')
url='https://github.com/wwmm/easyeffects'
license=('GPL3')
depends=('calf' 'libglibmm-2.68.so' 'gtk4' 'gtkmm-4.0' 'libebur128' 'libbs2b.so'
         'librubberband.so' 'libsamplerate.so' 'libsigc-3.0.so' 'libsndfile.so'
         'liblilv-0.so' 'lsp-plugins' 'libpipewire-0.3.so' 'rnnoise' 'rubberband'
         'tbb' 'zita-convolver')
makedepends=('appstream-glib' 'itstool' 'lv2' 'meson' 'nlohmann-json' 'zam-plugins')
optdepends=(
  'zam-plugins: maximizer'
  'mda.lv2: bass loudness plugin'
)
replaces=('pulseeffects')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('0f4505e429416186f3af4338c1c7b5afac0f0ec343749bd9bcc8cf222349b2cb25714de493236c57b1dd60dfcc3e1359cab15cdeabb661f4cb20591b518f70a6')

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja install -C build
}
