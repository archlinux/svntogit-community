# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Kevin Sullivan <ksullivan@archlinux.us>

pkgname=easystroke
pkgver=0.6.0
pkgrel=11
pkgdesc='Use mouse gestures to initiate commands and hotkeys.'
arch=('i686' 'x86_64')
url='http://easystroke.sourceforge.net/'
license=('custom:ISC')
depends=('gtkmm3' 'boost-libs' 'libxtst' 'dbus-glib' 'xorg-server')
makedepends=('boost' 'inputproto' 'intltool' 'gettext' 'xorg-server-devel' 'help2man')
source=("http://downloads.sourceforge.net/easystroke/${pkgname}-${pkgver}.tar.gz"
        'replace-sigc-group-with-lambda.patch')
md5sums=('3b4f25d02a6b5ac57a8b7a06545869db'
         '4a27e1336df773e2f1cd67788477cc3b')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix build with libsigc++ 2.6; also build in C++11 mode
  # (not sure if the lambda is correct, but seems to work)
  patch -Np1 -i ../replace-sigc-group-with-lambda.patch
}

build() {
  cd "${pkgname}-${pkgver}"

  make
  make man
}

package() {
  cd "${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm0644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
