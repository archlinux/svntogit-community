# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kevin Sullivan <ksullivan@archlinux.us>

pkgname=easystroke
pkgver=0.6.0
pkgrel=1
pkgdesc='Use mouse gestures to initiate commands and hotkeys.'
arch=('i686' 'x86_64')
url='http://easystroke.sourceforge.net/'
license=('custom:ISC')
depends=('gtkmm3' 'boost-libs' 'libxtst' 'dbus-glib' 'xorg-server')
makedepends=('boost' 'inputproto' 'intltool' 'gettext' 'xorg-server-devel' 'help2man')
install=easystroke.install
source=("http://downloads.sourceforge.net/easystroke/${pkgname}-${pkgver}.tar.gz")
md5sums=('3b4f25d02a6b5ac57a8b7a06545869db')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make
  make man
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm0644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
