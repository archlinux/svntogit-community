# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kevin Sullivan <ksullivan@archlinux.us>

pkgname=easystroke
pkgver=0.5.5.1
pkgrel=2
pkgdesc='Use mouse gestures to initiate commands and hotkeys.'
arch=('i686' 'x86_64')
url='http://easystroke.sourceforge.net/'
license=('custom:ISC')
depends=('gtkmm' 'boost' 'libxtst' 'dbus-glib' 'xorg-server')
makedepends=('inputproto' 'intltool' 'gettext' 'xorg-server-devel' 'help2man')
install=easystroke.install
source=("http://downloads.sourceforge.net/easystroke/${pkgname}-${pkgver}.tar.gz")
md5sums=('ba989d1745415be94afd567e33d124bb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make
  make man
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install

  install -Dm0644 "${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
