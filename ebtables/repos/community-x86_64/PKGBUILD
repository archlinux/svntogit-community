# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Michal Soltys <soltys@ziu.info>

pkgname=ebtables
pkgver=2.0.10_4
pkgrel=2
pkgdesc='Ethernet bridge filtering utilities'
arch=('i686' 'x86_64')
url='http://ebtables.sourceforge.net/'
license=('GPL2')
backup=("etc/$pkgname.conf" "etc/conf.d/$pkgname")
# ebtables segfault with --as-needed
options=('!buildflags')
install=$pkgname.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-v${pkgver/_/-}.tar.gz"
        "$pkgname.conf"
        "$pkgname.rc"
        "$pkgname.systemd"
        "$pkgname.service")
md5sums=('506742a3d44b9925955425a659c1a8d0'
         'c6f0838de4429af68d21b4d61f00db59'
         'd0e340f2cefe91b1569c0e87c2a1288f'
         '5a49ea09af202cb438c8a1b15abf9147'
         'baf923443c3b16277e50d60d2e68f534')

build() {
  cd $pkgname-v${pkgver/_/-}
  make CFLAGS='-Wunused -Wall -Werror -Wno-error=unused-but-set-variable'
}

package() {
  pushd $pkgname-v${pkgver/_/-}
  make install \
    DESTDIR="$pkgdir" \
    LIBDIR=/usr/lib \
    MANDIR=/usr/share/man \
    BINDIR=/usr/sbin \
    INITDIR=/etc/rc.d \
    SYSCONFIGDIR=/etc
  popd
  # rm package ebtables rc.d scripts
  rm "$pkgdir/etc/ebtables-config"
  # install initscripts
  install -Dm 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -Dm 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # install systemd service
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 ft=sh et:
