# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eclib
pkgver=20210503
pkgrel=3
pkgdesc='Includes mwrank (for 2-descent on elliptic curves over Q) and modular symbol code used to create the elliptic curve database'
arch=(x86_64)
url='https://github.com/JohnCremona/eclib/'
license=(GPL)
depends=(flint pari boost-libs)
makedepends=(boost)
source=(https://github.com/JohnCremona/eclib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('7ed61c7dfd64f6969aef2bfaaa192bc0e814fa6226faffef7b2ebbe253708281')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-flint=/usr --with-boost
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
