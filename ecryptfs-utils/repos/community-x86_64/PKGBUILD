# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ecryptfs-utils
pkgver=99
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Enterprise-class stacked cryptographic filesystem for Linux"
url="https://launchpad.net/ecryptfs"
license=('GPL')
makedepends=('swig' 'intltool' 'gettext')
depends=('keyutils' 'nss' 'python2')
source=("http://launchpad.net/ecryptfs/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz")
options=(!libtool)
md5sums=('17ef9190c6d078845e19d3e9a7d8ef7a')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-pamdir=/usr/lib/security PYTHON=python2
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  chmod +s "$pkgdir"/sbin/mount.ecryptfs_private
}
