# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ecryptfs-utils
pkgver=108
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="Enterprise-class stacked cryptographic filesystem for Linux"
url="https://launchpad.net/ecryptfs"
license=('GPL')
makedepends=('swig' 'intltool' 'gettext' 'python2')
depends=('nss' 'pam')
optdepends=('python2: for python module')
source=("http://launchpad.net/ecryptfs/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        "${pkgname}_${pkgver}.orig.tar.gz.sig::http://launchpad.net/ecryptfs/trunk/${pkgver}/+download/..-${pkgname}_${pkgver}.orig.tar.gz.asc"
        CVE-2016-1572.patch)
md5sums=('80f2a73e14030239fa01a2f1e5606a0e'
         'SKIP'
         '3932ca86f3f6e3c770d61d32b45ed3c0')
validpgpkeys=('E2D9E1C5F9F5D59291F4607D95E64373F1529469')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "$srcdir"/CVE-2016-1572.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-pamdir=/usr/lib/security PYTHON=python2
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" rootsbindir='/usr/bin' install
  chmod +s "$pkgdir/usr/bin/mount.ecryptfs_private"
}
