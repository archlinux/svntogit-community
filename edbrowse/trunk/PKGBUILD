# Maintainer: Alad Wenter <alad (at) mailbox.org>
# Contributor: Chris Brannon <cmbrannon (at) cox.net>
# Contributor: joyfulgirl <joyfulgirl (at) archlinux.us>
# Contributor: Chuck Boucher <jofywyxaxukyvofa (at) tempomail.fr>
# Contributor: aksr <aksr at t-com dot me>

pkgname=edbrowse
pkgver=3.7.4
pkgrel=2
_commit=7e1be6c69fd336561c89c94a549d7cd454470dee # 3.7.4
pkgdesc='Line-oriented editor, browser and mail client'
arch=('x86_64')
url='http://edbrowse.org'
license=('GPL' 'MPL')
depends=('duktape' 'tidy' 'curl' 'pcre')
makedepends=('git')
source=("$pkgname-$pkgver::git+https://github.com/CMB/edbrowse#commit=$_commit")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname-$pkgver"

    # Fix build with curl 7.62.0
    git cherry-pick -n 5d2b9e21fdf019f461ebe62738d615428d5db963
}

build() {
    cd "$pkgname-$pkgver"
    make CFLAGS="-I/usr/include/tidy" all
}

package() {
    cd "$pkgname-$pkgver"
    make -C src DESTDIR="$pkgdir" prefix=/usr install

    install -Dm644 README CHANGES -t "$pkgdir/usr/share/doc/$pkgname/README"
    install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 doc/usersguide.html doc/sample.ebrc -t "$pkgdir/usr/share/doc/edbrowse"
    install -Dm644 doc/man-edbrowse-debian.1 -T "$pkgdir/usr/share/man/man1/edbrowse.1"
}

