# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ejabberd
pkgver=2.1.11
pkgrel=9
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang' 'pam' 'iproute2')
backup=('etc/ejabberd/ejabberd.cfg'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
install=$pkgname.install
source=("http://www.process-one.net/downloads/ejabberd/${pkgver/_/-}/ejabberd-${pkgver}.tgz"
	"$pkgname.logrotate"
	"$pkgname"
	"ejabberd.service"
	"2.1.12rc.diff.gz")
md5sums=('a70b040c4e7602f47718c8afe8780d50'
         'f97c8a96160f30e0aecc9526c12e6606'
         '2338a6230ce5f9f60be6dddcfa39bc7e'
         'df32aa17e40a7bc5d9946d3ba6fe5e16'
         '688e99cdc1fc3a12501caabaec2293a4')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  patch -p2 <$srcdir/2.1.12rc.diff
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-pam --enable-odbc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make DESTDIR="$pkgdir" install
  install -D -m 0755 "$srcdir/$pkgname" "$pkgdir/etc/rc.d/$pkgname"
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/sbin/ejabberdctl"
  rm -rf "$pkgdir/var/lock"
  install -Dm0644 $srcdir/ejabberd.service $pkgdir/usr/lib/systemd/system/ejabberd.service
}

# vim:set ts=2 sw=2 et:
