# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

#
# cd deps ; ls -1 | while read g; do (test -d $g && cd $g && sh ../t.sh); done >qwe
#
# U=`grep 'url =' .git/config | cut -f2 -d= | tr -d ' '`
# C=`git rev-parse HEAD`
# N=`basename $(pwd)`
# echo -e "\t\"$N::$U#commit=$C\""
#

pkgname=ejabberd
pkgver=16.04
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
install=$pkgname.install
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"cache_tab::git://github.com/processone/cache_tab#commit=aeb255793d1ca48a147d8b5f22c3bc09ddb6ba87"
	"elixir::git://github.com/elixir-lang/elixir#commit=139d2eac5998259379862ff6ed2a5287f7de4ea6"
	"eredis::git://github.com/wooga/eredis#commit=cbc013f516e464706493c01662e5e9dd82d1db01"
	"esip::git://github.com/processone/esip#commit=9b57b40bd1195d097a9dca71c1dabc9230dfed8c"
	"ezlib::git://github.com/processone/ezlib#commit=85617df345589c0b6eca5d4100eb04ac4bffe3d5"
	"fast_tls::git://github.com/processone/fast_tls#commit=ccc6a5c52764f1a0c355ad16c4ae06b11194e4b7"
	"fast_xml::git://github.com/processone/fast_xml#commit=f6f21a56211afd9d99670ac4953a2d5c6163e4e0"
	"fast_yaml::git://github.com/processone/fast_yaml#commit=9a3b510d7cf3581d7211ad13c307e2be60abdc4e"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=64864ba7fcf40988361340e48680b49a2c2938cf"
	"hamcrest::git://github.com/hyperthunk/hamcrest-erlang#commit=908a24fda4a46776a5135db60ca071e3d783f9f6"
	"iconv::git://github.com/processone/iconv#commit=514703b3a5517a7a921ad5ce3e83b76df2bd70f0"
	"jiffy::git://github.com/davisp/jiffy#commit=6303ff98aaa3fce625038c8b7af2aa8b802f4742"
	"lager::git://github.com/basho/lager#commit=599dda6786a81200e00b3d9b4b36951e052727a3"
	"luerl::git://github.com/rvirding/luerl#commit=f7b2cc0ab6fa4245ebeda0169fc994aff0628bf9"
	"meck::git://github.com/eproxus/meck#commit=70d6a33ce7407029dc59e22a5b3c1c61c1348b23"
	"moka::git://github.com/processone/moka.git#commit=768efea96443c57125e6247dbebee687f17be149"
	"p1_mysql::git://github.com/processone/p1_mysql#commit=a13606026f3ee8862c7d7c9b31ad2faf5ee8031a"
	"p1_oauth2::git://github.com/processone/p1_oauth2#commit=34f9b20fd68134a4646130bdcf1abf320f815a00"
	"p1_pam::git://github.com/processone/epam#commit=f0d6588f4733c4d8068af44cf51c966af8bf514a"
	"p1_pgsql::git://github.com/processone/p1_pgsql#commit=1cefac417342c71a08a08043b1b743e72d4ddc12"
	"p1_utils::git://github.com/processone/p1_utils#commit=e8d35fa5accd9a748bb8ac4942edcfa45be09ec3"
	"p1_xmlrpc::git://github.com/processone/p1_xmlrpc#commit=6dface5a00da4cd0b0084a253075b5573336c0bb"
	"protobuffs::git://github.com/basho/erlang_protobuffs#commit=6e7fc924506e2dc166a6170e580ce1d95ebbd5bd"
	"rebar_elixir_plugin::git://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"riakc::git://github.com/basho/riak-erlang-client#commit=527722d12d0433b837cdb92a60900c2cb5df8942"
	"riak_pb::git://github.com/basho/riak_pb#commit=eacf4404625de69f7b3908cebd8efe416d9598b0"
	"samerlib::git://github.com/processone/samerlib#commit=9158f65d18ec63f8b409543b6fb46dd5fce46160"
	"sqlite3::git://github.com/processone/erlang-sqlite3#commit=6ee3570060fbbf793a0bc873831e8eabb6c39851"
	"stringprep::git://github.com/processone/stringprep#commit=5005ecbe503ae8b55d3ee81dc4e4db1193c216e2"
	"stun::git://github.com/processone/stun#commit=653943e6d0cc0f4803a9c3f014955f02f7b96bcd"
	"$pkgname.logrotate"
	"ejabberd.service"
	"sysuser.conf")
md5sums=('b93d6f8ddc219f3ea7802478ec52bfd4'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'f97c8a96160f30e0aecc9526c12e6606'
         '527d7e8616332320007f2c1616bc8270'
         'a3ebb00982d290284c1176c176a59b48')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in elixir eredis esip goldrush jiffy lager meck p1_oauth2 cache_tab \
	iconv ezlib fast_tls fast_xml fast_yaml hamcrest luerl p1_mysql \
	p1_pam p1_pgsql p1_utils p1_xmlrpc protobuffs rebar_elixir_plugin \
	riakc riak_pb sqlite3 stringprep stun; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv $srcdir/$i "$srcdir/$pkgname-$pkgver/deps"
  done

  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm04750 deps/p1_pam/priv/bin/epam $pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm0644 $srcdir/ejabberd.service $pkgdir/usr/lib/systemd/system/ejabberd.service
  install -Dm644 $srcdir/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf

  # workaround
  ln -s mod_configure.beam $pkgdir/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f $pkgdir/usr/bin/{elixir,iex,mix}

  # permissions
  chown -R 17:17 $pkgdir/var/{lib,log}/ejabberd
  chown root:17 \
    $pkgdir/etc/ejabberd/ejabberd.yml \
    $pkgdir/etc/ejabberd/ejabberdctl.cfg \
    $pkgdir/etc/ejabberd \
    $pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/epam
}

# vim:set ts=2 sw=2 et:
