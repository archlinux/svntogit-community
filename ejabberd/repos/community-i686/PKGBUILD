# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

#
# cd deps ; ls -1 | while read g; do (test -d $g && cd $g && sh ../t.sh); done >qwe
#
# U=`grep 'url =' .git/config | cut -f2 -d= | tr -d ' '`
# C=`git rev-parse HEAD`
# N=`basename $(pwd)`
# echo -e "\t\"$N::$U#commit=$C\""
#

pkgname=ejabberd
pkgver=15.11
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
install=$pkgname.install
replaces=('ejabberd-mod_mysql' 'ejabberd-mod_mysql-svn'
	  'ejabberd-mod_pgsql' 'ejabberd-mod_pgsql-svn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"elixir::git://github.com/elixir-lang/elixir#commit=1d9548fd285d243721b7eba71912bde2ffd1f6c3"
	"eredis::git://github.com/wooga/eredis#commit=bf12ecb30253c84a2331f4f0d93fd68856fcb9f4"
	"esip::git://github.com/processone/p1_sip#commit=d662d3fe7f6288b444ea321d854de0bd6d40e022"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=64864ba7fcf40988361340e48680b49a2c2938cf"
	"jiffy::git://github.com/davisp/jiffy#commit=cfc61a2e952dc3182e0f9b1473467563699992e2"
	"lager::git://github.com/basho/lager#commit=4d2ec8c701e1fa2d386f92f2b83b23faf8608ac3"
	"meck::git://github.com/eproxus/meck#commit=0845277398b8326f9dddddd9fc3cf73467ba6877"
	"oauth2::git://github.com/prefiks/oauth2.git#commit=e6da9912e5d8f658e7e868f41a102d085bdbef59"
	"p1_cache_tab::git://github.com/processone/cache_tab#commit=f7ea12b0ba962a3d2f9a406d2954cf7de4e27230"
	"p1_iconv::git://github.com/processone/eiconv#commit=8b7542b1aaf0a851f335e464956956985af6d9a2"
	"p1_mysql::git://github.com/processone/mysql#commit=dfa87da95f8fdb92e270741c2a53f796b682f918"
	"p1_pam::git://github.com/processone/epam#commit=d3ce290b7da75d780a03e86e7a8198a80e9826a6"
	"p1_pgsql::git://github.com/processone/pgsql#commit=e72c03c60bfcb56bbb5d259342021d9cb3581dac"
	"p1_stringprep::git://github.com/processone/stringprep#commit=3c640237a3a7831dc39de6a6d329d3a9af25c579"
	"p1_stun::git://github.com/processone/stun#commit=061bdae484268cbf0457ad4797e74b8516df3ad1"
	"p1_tls::git://github.com/processone/tls#commit=e56321afd974e9da33da913cd31beebc8e73e75f"
	"p1_utils::git://github.com/processone/p1_utils#commit=d7800881e6702723ce58b7646b60c9e4cd25d563"
	"p1_xml::git://github.com/processone/xml#commit=1c8b016b0ac7986efb823baf1682a43565449e65"
	"p1_yaml::git://github.com/processone/p1_yaml#commit=79f756ba73a235c4d3836ec07b5f7f2b55f49638"
	"p1_zlib::git://github.com/processone/zlib#commit=e3d4222b7aae616d7ef2e7e2fa0bbf451516c602"
	"protobuffs::git://github.com/basho/erlang_protobuffs.git#commit=ec7e99f57f5bbcd91c56242091485d41bc78c3d4"
	"rebar_elixir_plugin::git://github.com/yrashk/rebar_elixir_plugin#commit=7058379b7c7e017555647f6b9cecfd87cd50f884"
	"riakc::git://github.com/basho/riak-erlang-client#commit=8d33c020f4ca392200b2d0d973c77dd48164b263"
	"riak_pb::git://github.com/basho/riak_pb#commit=0353be3e04b971ac802acbd6b78d74589db2f6df"
	"sqlite3::git://github.com/alexeyr/erlang-sqlite3#commit=8350dc603804c503f99c92bfd2eab1dd6885758e"
	"xmlrpc::git://github.com/rds13/xmlrpc.git#commit=42e6e96a0fe7106830274feed915125feb1056f3"
	"$pkgname.logrotate"
	"ejabberd.service"
	"sysuser.conf"
	"deps-build-fix.patch")
md5sums=('6e24541cc1526d1eee59c238c77c19c1'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'f97c8a96160f30e0aecc9526c12e6606'
         'a437aff9d7d477fb036d527205617e18'
         '458fc41be4adb9554a53b8f9ae05fbee'
         '9ac450a7b81bbef95990aaa2365587fb')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in elixir eredis esip goldrush jiffy lager meck oauth2 p1_cache_tab \
	p1_iconv p1_mysql p1_pam p1_pgsql p1_stringprep p1_stun p1_tls p1_utils \
	p1_xml p1_yaml p1_zlib protobuffs rebar_elixir_plugin riakc riak_pb \
	sqlite3 xmlrpc; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv $srcdir/$i "$srcdir/$pkgname-$pkgver/deps"
  done

  (cd deps && patch -p1 <$srcdir/deps-build-fix.patch)
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-all
  make
#  make -C doc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # make install workaround
  rm -f deps/sqlite3/include/sqlite3.hrl

  make DESTDIR="$pkgdir" install

  install -Dm04750 deps/p1_pam/priv/bin/epam $pkgdir/usr/lib/ejabberd/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  mkdir -p $pkgdir/usr/bin
  mv $pkgdir/usr/sbin/ejabberdctl $pkgdir/usr/bin/ejabberdctl
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock" "$pkgdir/usr/sbin"
  install -Dm0644 $srcdir/ejabberd.service $pkgdir/usr/lib/systemd/system/ejabberd.service
  install -Dm644 $srcdir/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf
  # workaround
  ln -s mod_configure.beam $pkgdir/usr/lib/ejabberd/ebin/configure.beam
  rm -f $pkgdir/usr/bin/{elixir,iex,mix}
}

# vim:set ts=2 sw=2 et:
