# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ejabberd
pkgver=21.04
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64')
url="https://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite' 'gd')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
optdepends=('gsfonts: for captcha'
	    'imagemagick: for captcha')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"base64url::git://github.com/dvv/base64url#commit=bf1360685da704c8453aba2268c4698c959d2ba4"
	"cache_tab::git://github.com/processone/cache_tab#commit=b05040ba67dc4232f3bfda424519d8583ba26260"
	"eimp::git://github.com/processone/eimp#commit=b67754d5a2d56ff44f73e5025124c111ff1230e5"
	"ejabberd_po::git://github.com/processone/ejabberd-po#commit=e909d888484fa0f548aa3128dff192a9e72ba3e9"
	"elixir::git://github.com/elixir-lang/elixir#commit=867da6f876c48279823a41a33030c95ef37379b2"
	"epam::git://github.com/processone/epam#commit=a282ed4f2fb69b9f4a0457a111867a88f366ae4f"
	"eredis::git://github.com/wooga/eredis#commit=cbc013f516e464706493c01662e5e9dd82d1db01"
	"esip::git://github.com/processone/esip#commit=446dbe5cedeb7dec59ddbfbed4cc96c98cf463be"
	"ezlib::git://github.com/processone/ezlib#commit=e9ded4975b0e9fdbf0195f7ebce857031e8958d8"
	"fast_tls::git://github.com/processone/fast_tls#commit=9b88fbec57ec8dae55269fba49fcbb54e974404d"
	"fast_xml::git://github.com/processone/fast_xml#commit=da1ba636b944b0a09695ce29184da17c71886f15"
	"fast_yaml::git://github.com/processone/fast_yaml#commit=084dc925eb5cfc2d3ac475b08145cc164437fdf6"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=8f1b715d36b650ec1e1f5612c00e28af6ab0de82"
	"idna::git://github.com/benoitc/erlang-idna#commit=6cff72747821110169ecfac871b0c69e5064afff"
	"jiffy::git://github.com/davisp/jiffy#commit=6dadc0cd22b06e2b1972ee8aba5708c478418926"
	"jose::git://github.com/potatosalad/erlang-jose#commit=5ad99d03c5381ec68a9576c81ed4e9312fea73e0"
	"lager::git://github.com/erlang-lager/lager#commit=4ffdf0dbef53304bc0e958a00776bf0650de8c8b"
	"luerl::git://github.com/rvirding/luerl#commit=01fa43a1a5d68becfe07236a9a71505039474108"
	"mqtree::git://github.com/processone/mqtree#commit=d568635e6a04d9adfc01df52cb3dd41205b285a0"
	"p1_acme::git://github.com/processone/p1_acme#commit=437043eb37d1304ce125f3aa2d121373754b49c9"
	"p1_mysql::git://github.com/processone/p1_mysql#commit=5e0ccbc7f598f984f7be058188ed93d8d79eda6b"
	"p1_oauth2::git://github.com/processone/p1_oauth2#commit=94f76be16e21b924cbcf907905839887b0bd943e"
	"p1_pgsql::git://github.com/processone/p1_pgsql#commit=999ed99fed0a6a3720f2cfa154270201c827d7b8"
	"p1_utils::git://github.com/processone/p1_utils#commit=992b591e1c8b8894a7191676c7b10888e47ede92"
	"pkix::git://github.com/processone/pkix#commit=801962ef7660650f04be05321ccf10f057291050"
	"rebar_elixir_plugin::git://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"sqlite3::git://github.com/processone/erlang-sqlite3#commit=ca499cf5760ad7e0e7adeb99ec6893be1bf8712b"
	"stringprep::git://github.com/processone/stringprep#commit=1b8a3c2beb743f9d5fd4fffb58eb0cd8ea713400"
	"stun::git://github.com/processone/stun#commit=6177a7d063aa2f2c389080ea2b59510be0a236c7"
	"unicode_util_compat::git://github.com/benoitc/unicode_util_compat.git#commit=38d7bc105f51159e8ea3279c40121db9db1e652f"
	"xmpp::git://github.com/processone/xmpp#commit=88c43c3cf5843a8a0f76eac390980a3a39c972dd"
	"yconf::git://github.com/processone/yconf#commit=aba6e0bb5d274482caf5447c77d71b50c9418026"
	"$pkgname.logrotate"
	"sysuser.conf")
sha256sums=('2a5d22cd594ea2bd31adebcbcef9d9bdf46109803874fb336f8afba76fc08d8d'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '31780cac78736d285e46f445f8c8463a70f2aeb2683280c259129db11832ddd2'
            '68de8fd4df6e4f0e21a241dcd2b7075bdff495876646dd726c7054bf5780e3c6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in \
    base64url cache_tab eimp ejabberd_po elixir epam eredis esip ezlib fast_tls fast_xml fast_yaml goldrush idna jiffy jose lager luerl mqtree p1_acme p1_mysql p1_oauth2 p1_pgsql p1_utils pkix rebar_elixir_plugin sqlite3 stringprep stun unicode_util_compat xmpp yconf \
    ; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv "$srcdir"/$i "$srcdir/$pkgname-$pkgver/deps"
  done
  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  sed -i \
    -e "s|$pkgdir||g" \
    -e "s|Group=.*|Group=jabber|g" \
    -e "s|User=.*|User=jabber|g" \
    ejabberd.service
  install -Dm0644 ejabberd.service "$pkgdir"/usr/lib/systemd/system/ejabberd.service

  install -Dm04750 deps/epam/priv/bin/epam "$pkgdir"/usr/lib/ejabberd-$pkgver/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf
  install -Dm644 tools/ejabberdctl.bc "$pkgdir"/usr/share/bash-completion/completions/ejabberdctl

  # workaround
  ln -s mod_configure.beam "$pkgdir"/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f "$pkgdir"/usr/bin/{elixir,iex,mix}

  echo -e "\n\n# home dir workaround\nHOME=/var/lib/ejabberd" >>"$pkgdir"/etc/ejabberd/ejabberdctl.cfg

  # /usr/lib/ejabberd symlink follows to ejabberd version
  ln -s ejabberd-$pkgver "$pkgdir"/usr/lib/ejabberd

  # permissions
  chown -R 17:17 "$pkgdir"/var/{lib,log}/ejabberd
  chown root:17 \
    "$pkgdir"/etc/ejabberd/ejabberd.yml \
    "$pkgdir"/etc/ejabberd/ejabberdctl.cfg \
    "$pkgdir"/etc/ejabberd \
    "$pkgdir"/usr/lib/ejabberd/priv/bin/epam

  # fix eimp
  chmod a+x "$pkgdir"/usr/lib/eimp-*/priv/bin/eimp
}

# vim:set ts=2 sw=2 et:
