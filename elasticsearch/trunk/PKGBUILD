# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Marcello "mererghost" Rocha <https://github.com/mereghost>
# Refactored by Bla≈æ "Speed" Hrastnik <https://github.com/archSeer>

pkgname=elasticsearch
pkgver=6.0.0
pkgrel=1
pkgdesc="Distributed RESTful search engine built on top of Lucene"
arch=('any')
url="https://www.elastic.co/products/elasticsearch"
license=('APACHE')
depends=('java-runtime-headless=8' 'systemd')
source=(
  "https://artifacts.elastic.co/downloads/$pkgname/$pkgname-$pkgver.tar.gz"
  elasticsearch-env
  elasticsearch.service
  elasticsearch@.service
  elasticsearch-sysctl.conf
  elasticsearch-user.conf
  elasticsearch-tmpfile.conf
  elasticsearch.default
)
sha256sums=('0420e877a8b986485244f603770737e9e4e47186fdfa1093768a11e391e3d9f4'
            'ffb354d8ec3752daa52c1bea49445491c3b811a5327dd8510b7c8e9499adefd1'
            'd3e92ff8cf6058965b1407537c3e618bff67f8012a8e3125721d22a72be1523b'
            'a38cf624ed91e2dcf36ed65806bbcf5ae91809a2a84a700ec52d1eb04ef0d5bf'
            'b3feb1e9c7e7ce6b33cea6c727728ed700332aae942ca475c3bcc1d56b9f113c'
            '815f6a39db6f54bb40750c382ffbdc298d2c4c187ee8ea7e2f855923e2ff354b'
            '3173e3efa429507e6329f518699a072dfd442d9b5da7c62452a55f82334dd2b5'
            'bb74e5fb8bc28f2125e015395ab05bea117b72bfc6dadbca827694b362ee0bf8')

backup=('etc/elasticsearch/elasticsearch.yml'
        'etc/elasticsearch/log4j2.properties'
        'etc/elasticsearch/jvm.options'
        'etc/default/elasticsearch')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  sed -r 's;source .*/elasticsearch-env;source /usr/share/elasticsearch/elasticsearch-env;' \
      -i bin/elasticsearch{,-keystore,-plugin,-translog}
}

package() {
  cd "$pkgdir"
  install -dm750 etc/elasticsearch/scripts
  install -dm755 usr/share/elasticsearch
  install -dm755 var/lib/elasticsearch
  install -dm755 var/log/elasticsearch

  install -Dm644 "$srcdir"/elasticsearch-env usr/share/elasticsearch/elasticsearch-env
  install -Dm644 "$srcdir"/elasticsearch.service usr/lib/systemd/system/elasticsearch.service
  install -Dm644 "$srcdir"/elasticsearch@.service usr/lib/systemd/system/elasticsearch@.service
  install -Dm644 "$srcdir"/elasticsearch-user.conf usr/lib/sysusers.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch-tmpfile.conf usr/lib/tmpfiles.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch-sysctl.conf usr/lib/sysctl.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch.default etc/default/elasticsearch

  cd "$srcdir"/$pkgname-$pkgver
  cp -R lib modules plugins "$pkgdir"/usr/share/elasticsearch/

	cd config
	for conf in *; do
		install -Dm644 "$conf" "$pkgdir/etc/elasticsearch/$conf"
	done
	cd ..

  for script in elasticsearch{,-keystore,-plugin,-translog}; do
    install -Dm755 bin/$script "$pkgdir"/usr/bin/$script
  done

  ln -s ../../bin "$pkgdir"/usr/share/elasticsearch/bin
}
