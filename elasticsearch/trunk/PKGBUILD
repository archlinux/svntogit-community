# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Marcello "mererghost" Rocha <https://github.com/mereghost>
# Refactored by Bla≈æ "Speed" Hrastnik <https://github.com/archSeer>

pkgname=elasticsearch
pkgver=1.2.0
pkgrel=1
pkgdesc="Distributed RESTful search engine built on top of Lucene"
arch=('i686' 'x86_64')
url="http://www.elasticsearch.org/"
license=('APACHE')
depends=('java-runtime' 'bash')
install='elasticsearch.install'
source=(
  "http://download.elasticsearch.org/$pkgname/$pkgname/$pkgname-$pkgver.tar.gz"
  elasticsearch.service
  elasticsearch@.service
  elasticsearch-tmpfile.conf
  elasticsearch-sysctl.conf
)

backup=('etc/elasticsearch/elasticsearch.yml'
        'etc/elasticsearch/logging.yml')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  for script in plugin elasticsearch; do
    sed 's|^ES_HOME=.*dirname.*|ES_HOME=/usr/share/elasticsearch|' \
      -i bin/$script

    for java in jre jdk; do
      sed "/^CDPATH/i [ -f /etc/profile.d/$java.sh ] && . /etc/profile.d/$java.sh" \
        -i bin/$script
    done
  done

  sed 's|$ES_HOME/lib|/usr/lib/elasticsearch|g' -i bin/elasticsearch.in.sh bin/plugin

  echo -e '\nJAVA_OPTS="$JAVA_OPTS -Des.path.conf=/etc/elasticsearch"' >> bin/elasticsearch.in.sh

  sed -e 's/# \(path\.conf: \).*$/\1\/etc\/elasticsearch/' \
    -e '0,/# \(path\.data: \).*$/s//\1\/var\/lib\/elasticsearch/' \
    -e 's/# \(path\.work: \).*$/\1\/tmp\/elasticsearch/' \
    -e 's/# \(path\.logs: \).*$/\1\/var\/log\/elasticsearch/' \
    -i config/elasticsearch.yml
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -dm755 "$pkgdir/etc/elasticsearch"
  install -dm755 "$pkgdir/var/lib/elasticsearch"
  install -dm755 "$pkgdir/var/log/elasticsearch"

  if [ $CARCH = 'x86_64' ]; then
    install -Dm644 lib/sigar/libsigar-amd64-linux.so "$pkgdir/usr/lib/elasticsearch/sigar/libsigar-amd64-linux.so"
  else
    install -Dm644 lib/sigar/libsigar-x86-linux.so "$pkgdir/usr/lib/elasticsearch/sigar/libsigar-x86-linux.so"
  fi
  cp lib/sigar/sigar*.jar "$pkgdir/usr/lib/elasticsearch/sigar/"
  cp lib/*.jar "$pkgdir/usr/lib/elasticsearch/"

  cp config/* "$pkgdir/etc/elasticsearch/"

  install -Dm755 bin/elasticsearch "$pkgdir/usr/bin/elasticsearch"
  install -Dm755 bin/plugin "$pkgdir/usr/bin/elasticsearch-plugin"
  install -Dm644 bin/elasticsearch.in.sh "$pkgdir/usr/share/elasticsearch/elasticsearch.in.sh"

  install -Dm644 "$srcdir/elasticsearch.service" "$pkgdir/usr/lib/systemd/system/elasticsearch.service"
  install -Dm644 "$srcdir/elasticsearch@.service" "$pkgdir/usr/lib/systemd/system/elasticsearch@.service"

  install -Dm644 "$srcdir/elasticsearch-tmpfile.conf" "$pkgdir/usr/lib/tmpfiles.d/elasticsearch.conf"
  install -Dm644 "$srcdir/elasticsearch-sysctl.conf" "$pkgdir/usr/lib/sysctl.d/elasticsearch.conf"
}

sha256sums=('267168e3dc65ee82a5dd69e981342ceab799e9152387787c8af91210bf294c70'
            'ab13ccda28bf90a10b570e095afc47ddef68da7e45546b2d45e74722c9095f0a'
            'a04ac09172158611a03dda096b670fe6586ae11b6b3d4de7965ed2b90cb3319f'
            '48b5a876e14819270c85f8ca626a011e01d10a2728758ced9a4d5bcf5c1fd23f'
            'b3feb1e9c7e7ce6b33cea6c727728ed700332aae942ca475c3bcc1d56b9f113c')
