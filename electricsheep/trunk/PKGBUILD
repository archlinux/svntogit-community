# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=electricsheep
pkgver=2.7b12
pkgrel=5
pkgdesc="A screensaver that realize the collective dream of sleeping computers from all over the internet"
arch=('i686' 'x86_64')
url="http://community.electricsheep.org/"
license=('GPL')
depends=('curl' 'flam3' 'ffmpeg' 'mplayer' 'libglade')
makedepends=('xscreensaver')
optdepends=('xscreensaver: to use electricsheep with xscreensaver')
source=(ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        electricsheep-ffmpeg.patch electricsheep-pref-segfault.patch)
sha1sums=('fd1c307912905ba456189efbb546ef0fe336fa32'
          'd6816e27c2a2f6c74ae61c99ad2d7fb28b1d1825'
          'bdf0f66fdbb5741671e8bb127bd76ef236d0e44a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/electricsheep-ffmpeg.patch"
  patch -p1 -i "${srcdir}/electricsheep-pref-segfault.patch"
  sed -i -e '/TryExec=xscreensaver/d' -e 's/Fractals Screen Savers/Fractals/' electricsheep.desktop.kde
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}/usr/share/applications/screensavers"
  install -d "${pkgdir}/usr/lib/gnome-screensaver/gnome-screensaver"
  make DESTDIR="${pkgdir}" GNOME_SCREENSAVER_DATADIR="${pkgdir}/usr/share/applications/screensavers" \
    GNOME_SCREENSAVER_PRIVEXEDIR="${pkgdir}/usr/lib/gnome-screensaver/gnome-screensaver" install
  install -D -m644 electricsheep.desktop.kde "${pkgdir}/usr/share/kde4/services/ScreenSavers/electricsheep.desktop"
  install -D -m644 electricsheep.xml "${pkgdir}/usr/share/xscreensaver/config/electricsheep.xml"
  install -D -m755 electricsheep-saver "${pkgdir}/usr/bin/electricsheep-saver"
}
