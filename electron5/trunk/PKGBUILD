# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=electron5
pkgver=5.0.13
_commit=f5fab127ca0eff5336e6cd469c794fdb9e3330b4
_chromiumver=73.0.3683.121
pkgrel=3
pkgdesc='Build cross platform desktop apps with web technologies'
arch=('x86_64')
url='https://electronjs.org/'
license=('MIT' 'custom')
depends=('c-ares' 'ffmpeg' 'gtk3' 'http-parser' 'libevent' 'libnghttp2'
         'libxslt' 'libxss' 'minizip' 'nss' 're2' 'snappy')
makedepends=('clang' 'git' 'gn-m76' 'gperf' 'harfbuzz-icu' 'java-runtime-headless'
             'jsoncpp' 'libnotify' 'lld' 'llvm' 'ninja' 'npm' 'pciutils'
             'python2' 'wget' 'yasm')
optdepends=('kde-cli-tools: file deletion support (kioclient5)'
            'trash-cli: file deletion support (trash-put)'
            "xdg-utils: open URLs with desktop's default (xdg-email, xdg-open)")
source=('git+https://github.com/electron/electron.git'
        'git+https://chromium.googlesource.com/chromium/tools/depot_tools.git'
        "${pkgname}.desktop"
        'default_app-icon.patch'
        'use-system-libraries-in-node.patch'
        'chromium-avoid-log-flooding-in-GLSurfacePresentationHelper.patch'
        'chromium-color_utils-use-std-sqrt.patch'
        'chromium-media-fix-build-with-libstdc++.patch'
        'chromium-skia-harmony.patch'
        'icu65.patch'
        'chromium-system-icu.patch'
        'chromium-webrtc-fix-SIOCGSTAMP-include.patch'
       )
sha256sums=('SKIP'
            'SKIP'
            '8f2e4764848817c4a5b225db77025ca108416d56f95708114fa7add4ce503c79'
            'a9f4d2af71c4399dd01047bb1205c0ca9bb5ce6bf0eeaeb57acf4c69724b668b'
            'e38b50ef16e6fc7520b1892f3b17758db14733d207cc083cfcd899954444248f'
            'f2b12ccf83a8e0adda4a87ae5c983df5e092ccf1f9a6f2e05799ce4d451dbda1'
            'b3b6f5147d519c586cbdaf3b227dd1719676fa3a65edd6f08989087afd287afa'
            'f51fe91427d8638c5551746d2ec7de99e8059dd76889cfeaee8ca3d8fed62265'
            '5887f78b55c4ecbbcba5930f3f0bb7bc0117c2a41c2f761805fcf7f46f1ca2b3'
            '1de9bdbfed482295dda45c7d4e323cee55a34e42f66b892da1c1a778682b7a41'
            'e2d284311f49c529ea45083438a768db390bde52949995534034d2a814beab89'
            'f73a3226b60833f7cc12f2bd8e3569284e6c26ea6cdd97eb5f45797ed29a323a')

_system_libs=('ffmpeg'
              'flac'
              'fontconfig'
              'freetype'
              'harfbuzz-ng'
              'icu'
              'libdrm'
              'libevent'
              'libjpeg'
#              'libpng'
              'libvpx'
              'libwebp'
              'libxml'
              'libxslt'
#              'openh264'
              'opus'
              're2'
              'snappy'
              'yasm'
              'zlib'
             )

prepare() {
  mkdir -p "${srcdir}"/python2-path
  ln -sf /usr/bin/python2 "${srcdir}/python2-path/python"
  export PATH="${srcdir}/python2-path:${PATH}:${srcdir}/depot_tools"

  echo "Fetching chromium..."
  git clone --branch=${_chromiumver} --depth=1 \
      https://chromium.googlesource.com/chromium/src.git

  echo "solutions = [
  {
    \"name\": \"src/electron\",
    \"url\": \"file://${srcdir}/electron@${_commit}\",
    \"deps_file\": \"DEPS\",
    \"managed\": False,
    \"custom_deps\": {
      \"src\": None,
    },
    \"custom_vars\": {},
  },
]" > .gclient

  python2 "${srcdir}/depot_tools/gclient.py" sync \
      --with_branch_heads \
      --with_tags \
      --nohooks

  sed -e "s/'am'/'apply'/" -i src/electron/script/lib/git.py

  echo "Running hooks..."
  # python2 "${srcdir}/depot_tools/gclient.py" runhooks
  python2 src/build/landmines.py
  python2 src/build/util/lastchange.py -o src/build/util/LASTCHANGE
  python2 src/build/util/lastchange.py -m GPU_LISTS_VERSION \
    --revision-id-only --header src/gpu/config/gpu_lists_version.h
  python2 src/build/util/lastchange.py -m SKIA_COMMIT_HASH \
    -s src/third_party/skia --header src/skia/ext/skia_commit_hash.h
  # Create sysmlink to system Node.js
  mkdir -p src/third_party/node/linux/node-linux-x64/bin
  ln -sf /usr/bin/node src/third_party/node/linux/node-linux-x64/bin
  python2 src/third_party/depot_tools/download_from_google_storage.py \
    --no_resume --extract --no_auth --bucket chromium-nodejs \
    -s src/third_party/node/node_modules.tar.gz.sha1
  vpython src/chrome/android/profiles/update_afdo_profile.py
  python2 src/electron/script/apply_all_patches.py \
      src/electron/patches/common/config.json
  cd src/electron
  npm install
  cd ..

  echo "Patching Chromium for using system libraries..."
  sed -i 's/OFFICIAL_BUILD/GOOGLE_CHROME_BUILD/' \
      tools/generate_shim_headers/generate_shim_headers.py
  for lib in "${_system_libs[@]}" libjpeg_turbo; do
      third_party_dir="third_party/${lib}"
      if [ ! -d ${third_party_dir} ]; then
        third_party_dir="base/${third_party_dir}"
      fi
      find ${third_party_dir} -type f \
          \! -path "${third_party_dir}/chromium/*" \
          \! -path "${third_party_dir}/google/*" \
          \! -path 'third_party/yasm/run_yasm.py' \
          \! -regex '.*\.\(gn\|gni\|isolate\)' \
          -delete
  done
  python2 build/linux/unbundle/replace_gn_files.py \
      --system-libraries \
      "${_system_libs[@]}"

  echo "Applying local patches..."
  patch -Np1 -i ../chromium-avoid-log-flooding-in-GLSurfacePresentationHelper.patch
  patch -Np1 -i ../chromium-color_utils-use-std-sqrt.patch
  patch -d media -Np1 -i ../../chromium-media-fix-build-with-libstdc++.patch
  patch -Np0 -i ../chromium-skia-harmony.patch
  patch -Np1 -i ../icu65.patch
  patch -Np1 -i ../chromium-system-icu.patch
  patch -Np1 -i ../chromium-webrtc-fix-SIOCGSTAMP-include.patch
  patch -Np1 -i ../use-system-libraries-in-node.patch
  patch -Np1 -i ../default_app-icon.patch  # Icon from .desktop file
}

build() {
  export CC=clang
  export CXX=clang++
  export AR=ar
  export NM=nm

  cd src
  export CHROMIUM_BUILDTOOLS_PATH="${PWD}/buildtools"
  GN_EXTRA_ARGS='
    clang_use_chrome_plugins = false
    custom_toolchain = "//build/toolchain/linux/unbundle:default"
    host_toolchain = "//build/toolchain/linux/unbundle:default"
    icu_use_data_file = false
    is_component_ffmpeg = false
    link_pulseaudio = true
    linux_use_bundled_binutils = false
    remove_webcore_debug_symbols = true
    treat_warnings_as_errors = false
    use_custom_libcxx = false
    use_gnome_keyring = false
    use_sysroot = false
  '
  gn-m76 gen out/Release \
      --args="import(\"//electron/build/args/release.gn\") ${GN_EXTRA_ARGS}"
  ninja -C out/Release electron
  # Strip before zip to avoid
  # zipfile.LargeZipFile: Filesize would require ZIP64 extensions
  strip -s out/Release/electron
  ninja -C out/Release electron_dist_zip
  # ninja -C out/Release third_party/electron_node:headers
}

package() {
  install -dm755 "${pkgdir}"/usr/lib/${pkgname}
  bsdtar -xf src/out/Release/dist.zip -C "${pkgdir}"/usr/lib/${pkgname}

  chmod u+s "${pkgdir}"/usr/lib/${pkgname}/chrome-sandbox

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  for l in "${pkgdir}"/usr/lib/${pkgname}/{LICENSE,LICENSES.chromium.html}; do
    ln -s  \
      $(realpath --relative-to="${pkgdir}"/usr/share/licenses/${pkgname} ${l}) \
      "${pkgdir}"/usr/share/licenses/${pkgname}
  done

  install -dm755 "${pkgdir}"/usr/bin
  ln -s ../lib/${pkgname}/electron "${pkgdir}"/usr/bin/${pkgname}

  # Install .desktop and icon file (see default_app-icon.patch)
  install -Dm644 -t "${pkgdir}"/usr/share/applications ${pkgname}.desktop
  install -Dm644 src/electron/default_app/icon.png \
          "${pkgdir}"/usr/share/pixmaps/${pkgname}.png  # hicolor has no 1024x1024
}
