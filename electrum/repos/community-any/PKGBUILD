# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# ThomasV PGP key: gpg --recv-key 6694D8DE7BE8EE5631BED9502BD5824B7F9470E6
# http://bitcoin-otc.com/viewgpg.php?nick=ThomasV

pkgname=electrum
pkgver=2.8.1
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('python2-pyaes' 'python2-ecdsa' 'python2-pbkdf2' 'python2-requests' 'python2-qrcode'
         'python2-protobuf' 'python2-dnspython' 'python2-jsonrpclib' 'python2-pysocks'
         'python2-pyqt4' 'python2-pycryptodomex')
makedepends=('gettext' 'python2-setuptools' 'desktop-file-utils')
optdepends=('python2-btchip: BTChip hardware wallet support'
            'python2-matplotlib: plot transaction history in graphical mode'
            'zbar: QR code reading support')
url="https://electrum.org"
license=(MIT)
source=("https://download.electrum.org/${pkgver}/${pkgname^}-${pkgver}.tar.gz"{,.asc})
md5sums=('dc2bb6016cedb39980511bc142a7b3be'
         'SKIP')
sha256sums=('effca46550aeea4b5f0729eb1584bcc61839bb59b20b1a89d09100895ad2288d'
            'SKIP')
validpgpkeys=('6694D8DE7BE8EE5631BED9502BD5824B7F9470E6')

prepare() {
  cd ${pkgname^}-${pkgver}

  find . -type f -exec sed -i 's#/usr/bin/python$#/usr/bin/python2#g' {} +
  find . -type f -exec sed -i 's#/usr/bin/env python$#/usr/bin/env python2#g' {} +
}

build() {
  cd ${pkgname^}-${pkgver}

  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${pkgname^}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
