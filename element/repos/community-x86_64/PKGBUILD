# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=element
pkgver=0.45.0
pkgrel=1
pkgdesc="A modular LV2/VST3 audio plugin host"
arch=('x86_64')
url="https://github.com/kushview/element"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'gdk-pixbuf2' 'libx11' 'libxext')
makedepends=('alsa-lib' 'atk' 'boost' 'curl' 'fontconfig' 'freetype2' 'git'
'glib2' 'gtk2' 'harfbuzz' 'imagemagick' 'jack' 'ladspa' 'lilv' 'lua' 'lv2'
'pango' 'readline' 'serd' 'sord' 'sratom' 'suil' 'waf')
checkdepends=('xorg-server-xvfb')
optdepends=('lua: for LUA scripting')
source=("git+https://github.com/kushview/${pkgname}#tag=${pkgver}?signed"
        "git+https://github.com/kushview/kv-modules"
        "git+https://github.com/WeAreRoli/JUCE.git"
        "git+https://github.com/lvtk/jlv2")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')
validpgpkeys=('52CB000FABB9DBE345CD478980A5F4BE60360CDE') # Michael Fisher <mfisher@kushview.net>

prepare() {
  cd "$pkgname"
  git submodule init
  git config submodule.libs/kv.url "${srcdir}/kv-modules"
  git config submodule.libs/JUCE.url "${srcdir}/JUCE"
  git config submodule.libs/jlv2.url "${srcdir}/jlv2"
  git submodule update
}

build() {
  cd "$pkgname"
  waf configure --prefix=/usr \
                --enable-docking \
                --test
  waf build -vv
}

check() {
  cd "$pkgname"
  LD_LIBRARY_PATH="${PWD}/build/lib:${LD_LIBRARY_PATH}" xvfb-run waf check || \
    echo "Integration tests are mixed in: https://github.com/kushview/Element/issues/239"
}

package() {
  depends+=('libasound.so' 'libatk-1.0.so' 'libcurl.so' 'libfontconfig.so'
  'libfreetype.so' 'libgdk-x11-2.0.so' 'libgio-2.0.so' 'libgobject-2.0.so'
  'libglib-2.0.so' 'libgtk-x11-2.0.so' 'libharfbuzz.so' 'libjack.so'
  'liblilv-0.so' 'libpangoft2-1.0.so' 'libpango-1.0.so' 'libreadline.so'
  'libserd-0.so' 'libsord-0.so' 'libsratom-0.so' 'libsuil-0.so')
  cd "$pkgname"
  waf install --destdir="${pkgdir}"
  install -vDm 644 {AUTHORS,CODE_OF_CONDUCT,CONTRIBUTING,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 docs/*.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/docs"
  install -vDm 644 docs/img/*.png \
    -t "${pkgdir}/usr/share/doc/${pkgname}/docs/img"
}
