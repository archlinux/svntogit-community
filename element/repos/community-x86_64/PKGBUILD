# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=element
pkgver=0.46.0
pkgrel=3
pkgdesc="A modular LV2/VST3 audio plugin host"
arch=('x86_64')
url="https://github.com/kushview/element"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11' 'libxext')
makedepends=('alsa-lib' 'boost' 'curl' 'freetype2' 'git' 'gtk3' 'imagemagick'
'jack' 'ladspa' 'ldoc' 'libxcomposite' 'libxcursor' 'libxinerama' 'lilv' 'lua'
'lv2' 'pango' 'readline' 'serd' 'sord' 'sratom' 'suil' 'waf' 'xorg-xrandr')
checkdepends=('xorg-server-xvfb')
optdepends=('lua: for LUA scripting')
provides=('ladspa-host' 'lv2-host' 'vst3-host')
source=("git+https://github.com/kushview/${pkgname}#tag=${pkgver}?signed"
        "${pkgname}-0.46.0-gcc11.patch::https://github.com/kushview/Element/commit/6f06bd6f85f6920c2619853536934ade83ad9580.patch"
        "git+https://github.com/kushview/kv-modules"
        "git+https://github.com/WeAreRoli/JUCE.git"
        "git+https://github.com/lvtk/jlv2")
sha512sums=('SKIP'
            'ecf0c487f850dfd4e6b2d55075fab909ab5188da93337d9c6569b637b332cf552d09719f9ea5c0c886dd63bcbf178f239f1844b079027ed045ce2f018c12cd64'
            'SKIP'
            'SKIP'
            'SKIP')
b2sums=('SKIP'
        '72758391494ab6bbc0b3d856d939df90af6325c59f77de1c1945754328594daa7d53ae8c5546a26f6d88be16efabf3b2d6f6fe795fbc916fe0392c0545dfae34'
        'SKIP'
        'SKIP'
        'SKIP')
validpgpkeys=('52CB000FABB9DBE345CD478980A5F4BE60360CDE') # Michael Fisher <mfisher@kushview.net>

prepare() {
  cd "$pkgname"
  git submodule init
  git config submodule.libs/kv.url "${srcdir}/kv-modules"
  git config submodule.libs/JUCE.url "${srcdir}/JUCE"
  git config submodule.libs/jlv2.url "${srcdir}/jlv2"
  git submodule update
  patch -Np1 -i ../"${pkgname}-0.46.0-gcc11.patch"
}

build() {
  cd "$pkgname"
  export LINKFLAGS="$LDFLAGS"
  waf configure --prefix=/usr \
                --enable-docking \
                --test
  waf build -vv
}

check() {
  cd "$pkgname"
  LD_LIBRARY_PATH="${PWD}/build/lib:${LD_LIBRARY_PATH}" xvfb-run waf check
}

package() {
  depends+=('libasound.so' 'libcurl.so' 'libfreetype.so' 'libjack.so'
  'liblilv-0.so' 'libsuil-0.so')
  cd "$pkgname"
  waf install --destdir="${pkgdir}"
  install -vDm 644 {AUTHORS,CODE_OF_CONDUCT,CONTRIBUTING,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 docs/*.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/docs"
  install -vDm 644 docs/img/*.png \
    -t "${pkgdir}/usr/share/doc/${pkgname}/docs/img"
}
