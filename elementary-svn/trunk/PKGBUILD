# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=elementary-svn
pkgver=78836
pkgrel=1
pkgdesc="Enlightenment's widget set"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('edje-svn' 'e_dbus-svn' 'efreet-svn' 'ethumb-svn')
makedepends=('subversion')
conflicts=('elementary')
provides=('elementary')
options=('!libtool' '!emptydirs')
source=()
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/elementary"
_svnmod="elementary"

build() {
  cd $srcdir

  if [ $NOEXTRACT -eq 0 ]; then
    msg "Connecting to $_svntrunk SVN server...."
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

  fi

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/elementary \
    --with-internal-maximum-log-level=3 \
    --disable-static \
    --enable-quick-launch \
    --enable-emotion \
    --enable-ecore-x \
    --enable-ecore-fb \
    --disable-ecore-sdl \
    --disable-ecore-wayland \
    --enable-edbus \
    --enable-efreet \
    --disable-eweather \
    --disable-emap \
    --enable-ethumb \
    --disable-debug \
    --disable-install-examples \
    --disable-doc
  make
}

package(){
  cd $srcdir/$_svnmod-build
  make DESTDIR=$pkgdir install

# install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING

  rm -r $srcdir/$_svnmod-build

}
