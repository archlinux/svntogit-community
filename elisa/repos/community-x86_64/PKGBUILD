# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=0.4.0
pkgrel=2
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
depends=(baloo kirigami2 kcmutils) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('92abd357be5142085b966a383243e61823ab92d6ce1f45ce5022995329d608fb'
            'SKIP')
validpgpkeys=(267BF70F7905C2723B0243267D0F74F05C22F553) # Matthieu Gallien <matthieu_gallien@yahoo.fr>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
