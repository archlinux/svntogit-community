# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=0.2.0
pkgrel=1
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
depends=(baloo qt5-quickcontrols qt5-quickcontrols2 qt5-graphicaleffects kcmutils) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4cf7f8a170c706fb220c641053e5141106effdd651575fd215efb2f9e2708d2b'
            'SKIP')
validpgpkeys=(267BF70F7905C2723B0243267D0F74F05C22F553) # Matthieu Gallien <matthieu_gallien@yahoo.fr>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
