# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Gilbert Kennen <gilbert firewatcher org>

pkgname=elixir
pkgver=1.11.3
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="https://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
checkdepends=('git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elixir-lang/elixir/archive/v$pkgver.tar.gz")
sha256sums=('d961305e893f4fe1a177fa00233762c34598bc62ff88b32dcee8af27e36f0564')

build() {
  cd elixir-"$pkgver"
  make
}

check() {
  cd elixir-"$pkgver"
  ERL_EPMD_PORT=5369 make test

  # The elixir test suite starts up epmd and then doesn't kill it again afterwards.
  epmd -port 5369 -kill
}

package() {
  cd elixir-"$pkgver"
  mkdir -p "$pkgdir"/usr/share/licenses/"$pkgname"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
