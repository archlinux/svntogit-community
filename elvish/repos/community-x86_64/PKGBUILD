# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Haochen Tong <i at hexchain dot org>
# Contributor: Qi Xiao <xiaqqaix at gmail dot com>

pkgname=elvish
pkgver=0.17.0
pkgrel=1
pkgdesc="A friendly and expressive Unix shell"
arch=('x86_64')
url="https://elv.sh"
license=('BSD')
depends=('glibc')
makedepends=('git' 'go')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/elves/elvish/archive/v$pkgver.tar.gz")
sha512sums=('bbf4895a99f3c1aa1d35cc04073feba121fda72afcb5dc241c5b1ded3c3f815d81f3ad4b9a51878ead865ecee3ea5dacc855a38057aa7e49a2ada306fc950895')
b2sums=('3725787f287dff6d2e740f75a4a62a47815166c7146d32b9358720209fc9eb4660b68599f8cc05313a44a5f02d97a0b30077588d9950048ae02a66f741795096')

prepare() {
  cd "$pkgname-$pkgver"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags ${LDFLAGS} \
    -X github.com/elves/elvish/pkg/buildinfo.Reproducible=true \
    -X github.com/elves/elvish/pkg/buildinfo.Version=$pkgver" \
    -o build \
    ./cmd/...
}

check() {
  cd "$pkgname-$pkgver"
  go test -v -race ./...
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/elvish

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
