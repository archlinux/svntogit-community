# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Chris Brannon <cmbrannon@cox.net>

pkgname=emacs-nox
pkgver=26.3
pkgrel=2
pkgdesc="The extensible, customizable, self-documenting real-time display editor, without X11 support"
arch=('x86_64')
url="https://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'libxml2' 'glib2' 'gnutls')
provides=('emacs')
conflicts=('emacs')
changelog=$pkgname.changelog
source=(https://ftp.gnu.org/gnu/emacs/emacs-$pkgver.tar.xz{,.sig})
validpgpkeys=('28D3BED851FDF3AB57FEF93C233587A47C207910')
sha256sums=('4d90e6751ad8967822c6e092db07466b9d383ef1653feb2f95c93e7de66d3485'
            'SKIP')

build() {
  cd "${srcdir}"/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound --with-modules
  make
}

package() {
  cd "${srcdir}"/emacs-$pkgver

  make DESTDIR="${pkgdir}" install

# remove conflict with ctags package
  mv "${pkgdir}"/usr/bin/{ctags,ctags.emacs}
  mv "${pkgdir}"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

# fix user/root permissions on usr/share files
  find "${pkgdir}"/usr/share/emacs/$pkgver -exec chown root.root {} \;
# remove .desktop file and icons
  rm -rf "${pkgdir}"/usr/share/{applications,icons}
}
