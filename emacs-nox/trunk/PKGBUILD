# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Chris Brannon <cmbrannon@cox.net>

pkgname=emacs-nox
pkgver=24.3
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor, without X11 support"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus-core' 'ncurses' 'perl')
provides=('emacs')
conflicts=('emacs')
changelog=$pkgname.changelog
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-${pkgver}.tar.gz)
sha256sums=('0098ca3204813d69cd8412045ba33e8701fa2062f4bff56bedafc064979eef41')

build() {
  cd ${srcdir}/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound
  make
}

package() {
  cd ${srcdir}/emacs-$pkgver
  make DESTDIR=${pkgdir} install
  
  # remove conflict with ctags package
  mv ${pkgdir}/usr/bin/{ctags,ctags.emacs}
  mv ${pkgdir}/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}
  # remove conflict with texinfo
  rm "$pkgdir"/usr/share/info/info.info.gz
  # fix user/root permissions on usr/share files
  find ${pkgdir}/usr/share/emacs/$pkgver -exec chown root.root {} \;
  # remove .desktop file and icons
  rm -rf ${pkgdir}/usr/share/{applications,icons}
  # fix perms on /var/games
  chmod 775 ${pkgdir}/var/games
  chmod 775 ${pkgdir}/var/games/emacs
  chmod 664 ${pkgdir}/var/games/emacs/*
  chown -R root:games ${pkgdir}/var/games
}
