# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Chris Brannon <cmbrannon@cox.net>

pkgname=emacs-nox
pkgver=25.3
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor, without X11 support"
arch=('x86_64')
url="http://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'libxml2' 'glib2' 'gnutls')
provides=('emacs')
conflicts=('emacs')
changelog=$pkgname.changelog
source=(ftp://ftp.gnu.org/gnu/emacs/emacs-$pkgver.tar.xz{,.sig})
validpgpkeys=('28D3BED851FDF3AB57FEF93C233587A47C207910')
sha256sums=('253ac5e7075e594549b83fd9ec116a9dc37294d415e2f21f8ee109829307c00b'
            'SKIP')

build() {
  cd "${srcdir}"/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --without-x --without-sound --with-modules
  make
}

package() {
  cd "${srcdir}"/emacs-$pkgver

  make DESTDIR="${pkgdir}" install

# remove conflict with ctags package
  mv "${pkgdir}"/usr/bin/{ctags,ctags.emacs}
  mv "${pkgdir}"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

# fix user/root permissions on usr/share files
  find "${pkgdir}"/usr/share/emacs/$pkgver -exec chown root.root {} \;
# remove .desktop file and icons
  rm -rf "${pkgdir}"/usr/share/{applications,icons}
# fix perms on /var/games
  chmod 775 "${pkgdir}"/var/games
  chmod 775 "${pkgdir}"/var/games/emacs
  chmod 664 "${pkgdir}"/var/games/emacs/*
  chown -R root:games "${pkgdir}"/var/games
}
