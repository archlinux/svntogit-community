# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Chris Brannon <cmbrannon@cox.net>

pkgname=emacs-nox
pkgver=27.2
pkgrel=1
pkgdesc="The extensible, customizable, self-documenting real-time display editor, without X11 support"
arch=('x86_64')
url="https://www.gnu.org/software/emacs/emacs.html"
license=('GPL3')
depends=('dbus' 'ncurses' 'perl' 'libxml2' 'glib2' 'gnutls' 'jansson')
provides=('emacs')
conflicts=('emacs')
changelog=$pkgname.changelog
source=(https://ftp.gnu.org/gnu/emacs/emacs-$pkgver.tar.xz{,.sig})
validpgpkeys=('E6C9029C363AD41D787A8EBB91C1262F01EB8D39') #Eli Zaretskii (eliz) <eliz@gnu.org>
sha256sums=('b4a7cc4e78e63f378624e0919215b910af5bb2a0afc819fad298272e9f40c1b9'
            'SKIP')

build() {
  cd "${srcdir}"/emacs-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-libsystemd \
    --localstatedir=/var --without-x --without-sound --with-modules
  make
}

package() {
  cd "${srcdir}"/emacs-$pkgver

  make DESTDIR="${pkgdir}" install

# remove conflict with ctags package
  mv "${pkgdir}"/usr/bin/{ctags,ctags.emacs}
  mv "${pkgdir}"/usr/share/man/man1/{ctags.1.gz,ctags.emacs.1}

# fix user/root permissions on usr/share files
  find "${pkgdir}"/usr/share/emacs/$pkgver -exec chown root.root {} \;
# remove .desktop file and icons
  rm -rf "${pkgdir}"/usr/share/{applications,icons,metainfo}
}
