# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Seymour <dannyseeless@gmail.com>

pkgname=emby-server
pkgver=4.3.1.0
_ffmpeg_ver=2019_11_09
pkgrel=4
pkgdesc='Bring together your videos, music, photos, and live television'
arch=(any)
url=https://emby.media
license=(custom)
depends=(
  alsa-lib
  aom
  bzip2
  dotnet-runtime-2.2
  expat
  fontconfig
  fribidi
  glibc
  gmp
  gnutls
  lame
  libass.so
  libdrm
  libfreetype.so
  libjpeg-turbo
  libmfx
  libpng
  libtheora
  libva-drm.so
  libva.so
  libvorbisenc.so
  libvorbis.so
  libwebp
  libx264.so
  opus
  skia-sharp
  sqlite
  zlib
  zvbi
)
makedepends=(
  ffnvcodec-headers8.1
  nasm
)
optdepends=('intel-media-sdk: Intel QuickSync support')
source=(
  https://github.com/MediaBrowser/Emby.Releases/releases/download/${pkgver}/embyserver-netcore_${pkgver}.zip
  https://mediabrowser.github.io/embytools/ffmpeg-${_ffmpeg_ver}.tar.gz
  https://mediabrowser.github.io/embytools/ffdetect-${_ffmpeg_ver}-x64.tar.xz
  emby-server
  emby-server.conf
  emby-server.service
  emby-server.sysusers
  emby-server.tmpfiles
  license.docx
)
noextract=(license.docx)
backup=(etc/conf.d/emby-server)
sha256sums=('01da4cfe12ff071a59de4876535ce817109c05950dc95ed4ca5c417545ca18e7'
            '4afade7a1870d1f3a90de688b8706718e2592cbb281b24ba569982aac667266f'
            '3fbee0c712c01e37953293da6d3aa4ae7a608fb5801a3eb3d9f99c1274da44db'
            'ee6ed36a13fcd8fd0b2b818eac742cc2cc2513d7f8f6cd867fa8d1c45ef3dd22'
            'd1a805284d8a1329f6f6e736423be747d8951c5a9600fbb79c13a6f9be7149c0'
            'efdaabd3002fee13a6831e90fd700dea4d7c6c954ede00da6f28c1eada33ad8c'
            'f7fa33949757ffc587ecf82496dc35ebc8c8e5c98b882b31dc40a24263d3921a'
            'a13a844a1bdc679356698e3fd7f3946d07bdfe021ed857d944ead011ad338056'
            'a6d7ea65dcb06392479a85e1a10a7aeb872d803da6f784f6935fcd4ee63008c6')

prepare() {
  rm -rf system/{electron,runtimes/{osx,tizen*,win*}}
}

build() {
  cd ffmpeg-${_ffmpeg_ver}_public_fixed

  export PKG_CONFIG_PATH=/usr/lib/ffnvcodec8.1/pkgconfig

  ./configure \
    --disable-doc \
    --disable-ffplay \
    --disable-shared \
    --disable-vdpau \
    --disable-libxcb \
    --disable-lzma \
    --disable-sdl2 \
    --disable-xlib \
    --enable-fontconfig \
    --enable-gmp \
    --enable-gnutls \
    --enable-gpl \
    --enable-libaom \
    --enable-libass \
    --enable-libdrm \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libmfx \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libwebp \
    --enable-libx264 \
    --enable-libzvbi \
    --enable-version3 \
    --enable-nvdec \
    --enable-nvenc \
    --enable-static \
    --enable-vaapi
  make
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib
  cp -dr --no-preserve='ownership' system "${pkgdir}"/usr/lib/emby-server
  ln -s ../libSkiaSharp.so.68.0.0 "${pkgdir}"/usr/lib/emby-server/libSkiaSharp.so
  install -Dm 755 emby-server -t "${pkgdir}"/usr/bin/
  install -Dm 755 bin/ffdetect "${pkgdir}"/usr/bin/ffdetect-emby
  install -Dm 755 ffmpeg-${_ffmpeg_ver}_public_fixed/ffmpeg "${pkgdir}"/usr/bin/ffmpeg-emby
  install -Dm 755 ffmpeg-${_ffmpeg_ver}_public_fixed/ffprobe "${pkgdir}"/usr/bin/ffprobe-emby
  install -Dm 644 emby-server.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm 644 emby-server.sysusers "${pkgdir}"/usr/lib/sysusers.d/emby-server.conf
  install -Dm 644 emby-server.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/emby-server.conf
  install -Dm 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server
  install -Dm 644 license.docx -t "${pkgdir}"/usr/share/licenses/emby-server/
}

# vim: ts=2 sw=2 et:
