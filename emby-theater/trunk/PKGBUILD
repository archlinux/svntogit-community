# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=emby-theater
pkgver=3.0.15
_libmpv_ver=2022_01_12
pkgrel=5
pkgdesc='An Electron app for Emby'
arch=(x86_64)
url=https://emby.media
license=(GPL2)
depends=(
  at-spi2-atk
  at-spi2-core
  cairo
  gcc-libs
  glibc
  libasound.so
  libatk-1.0.so
  libcups
  libdbus-1.so
  libdrm
  libexpat.so
  libfontconfig.so
  libfreetype.so
  libgdk-3.so
  libgdk_pixbuf-2.0.so
  libgio-2.0.so
  libglib-2.0.so
  libgobject-2.0.so
  libgtk-3.so
  libmfx
  libpango-1.0.so
  libpangocairo-1.0.so
  libpulse.so
  libuuid.so
  libva-drm.so
  libva.so
  libx11
  libxcb
  libxcomposite
  libxcursor
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxrender
  libxss
  libxtst
  mesa
  nspr
  nss
  ocl-icd
  zlib
)
makedepends=(
  git
  npm
)
optdepends=(
  'libcec: HDMI-CEC support'
)
_commit=68ca6e4b9e3918dfd96bccef79d6a1937daf7e78
#_tag=aeaab148c71414f5b6c29cfc0f7cf4eac0f95c55
source=(
  git+https://github.com/MediaBrowser/emby-theater-electron.git#commit=${_commit}
  #git+https://github.com/MediaBrowser/emby-theater-electron.git#tag=${_tag}
  https://mediabrowser.github.io/embytools/libmpv-${_libmpv_ver}-x64.tar.xz
  emby-theater.desktop
  emby-theater.png
)
b2sums=('SKIP'
        '865a11599cc63a917fe7b6347e807693151d28d3a34e9b8f23830a1dfab459cbd4072968c9e9e87a791dd4c690bc9ed2983678a7066ebe7857affc6658c8d0a8'
        '33b1230792bfc034d28fed21c60f9ecb3d7392293289a878866a20a505acabd03848df92300c9b1fd76d40ff03a6926451308c2b525ded0a75a17b77672c1e3b'
        '130bcfad0a216d66e44e3551af14700b3745d55b89c4523543646ae0d1fc00f6787ad8ace827a47f0f671d340308e4529c91f1c018002e64c89cb0ca924aeb4b')

prepare() {
  cd emby-theater-electron
  npm install
  npm install electron-packager
}

build() {
  cd emby-theater-electron
  npx electron-packager . emby-theater --platform linux --arch x64
}

package() {
  install -dm 755 "${pkgdir}"/usr/{bin,lib}
  cp -dr --no-preserve='ownership' emby-theater-electron/emby-theater-linux-x64 "${pkgdir}"/usr/lib/emby-theater
  rm -rf "${pkgdir}"/usr/lib/emby-theater/resources/app/libmpv/{arm,arm64,ia32,mpv-win32-*}
  ln -s /usr/lib/emby-theater/emby-theater "${pkgdir}"/usr/bin/emby-theater
  cp -d lib/libmpv.so* -t "${pkgdir}"/usr/lib/emby-theater/
  install -Dm 644 emby-theater.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 emby-theater.png -t "${pkgdir}"/usr/share/pixmaps/
}

# vim: ts=2 sw=2 et:
