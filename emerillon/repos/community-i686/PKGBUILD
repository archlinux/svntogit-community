# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=emerillon
pkgver=0.1.90
pkgrel=3
pkgdesc="Map viewer for OpenStreetMap"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/emerillon/"
license=('GPL')
depends=('libchamplain' 'geoclue' 'libpeas' 'rest')
makedepends=('gobject-introspection' 'gnome-doc-utils' 'intltool' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('0648c70bc19d3bd334a5403938aba3855eb644ee337c29aeecd5ce57ea0bf085')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
