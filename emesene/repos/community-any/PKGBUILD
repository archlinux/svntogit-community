# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Claudio Riva <firetux83@gmail.com>

pkgname=emesene
pkgver=2.12.9
pkgrel=3
pkgdesc="Chat program that allows you to connect to different networks like MSN, Gtalk, Facebook chat, Jabber and others"
url="http://www.emesene.org/"
license=('GPL' 'GPL3' 'LGPL3')
arch=('any') 
depends=('hicolor-icon-theme' 'python2-crypto' 'python2-dbus' 'python2-dnspython'
         'gstreamer0.10-good' 'gstreamer0.10-python' 'python2-notify' 'python2-pyopenssl' \
         'pywebkitgtk' 'xdg-utils')
makedepends=('python2-setuptools')
optdepends=('python2-gtkspell: spell-check plugin'
            'python2-pyqt4: alternative qt4 interface (emesene -e main:qt4_main)')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/emesene/emesene/tarball/v${pkgver}"
        'fix-preferences.patch'
        'emesene.run')
install=emesene.install
sha1sums=('a1cd8aee7affae8f2637df65e2d45a06d3298f7a'
          '3f68fe23c4c5c795c6b4f0d40802af424838f8b5'
          '2bd13ada86aa0af6307a41047f0d7caf2995660d')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgname}-b89aa5e

  # python -> python2 
  find -name '*.py' -exec sed -i "s|/usr/bin/env python|/usr/bin/env python2|" {} \;
  sed -i "s|/usr/bin/env python|/usr/bin/env python2|" emesene/emesene

  # fix preferences (FS#32182)
  patch -Np1 -i ../fix-preferences.patch
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgname}-b89aa5e
  python2 setup.py install --root="$pkgdir" --optimize=1

  cd "${pkgdir}"
  # use /usr/share/emesene instead of /usr/lib/python2.7/site-packages
  mv usr/lib/python2.7/site-packages/emesene usr/share/
  rm -r usr/lib usr/share/emesene/data

  # create plugins dir
  install -dm755 usr/share/emesene/plugins

  # create executable
  install -Dm755 "${srcdir}"/emesene.run usr/bin/emesene
}
