# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Claudio Riva <firetux83@gmail.com>

pkgname=emesene
pkgver=1.6.3
pkgrel=3
pkgdesc="A pygtk MSN Messenger client"
url="http://www.emesene.org/"
license=('custom:PSF' 'GPL' 'LGPL2.1')
arch=('i686' 'x86_64') # libmimic is hardcoded, so no 'any' package
depends=('python2' 'pygtk' 'hicolor-icon-theme' 'xdg-utils')
optdepends=('gnome-python-extras: spell-check plugin'
	'gtkspell: spell-check plugin'
	'aspell: spell-check plugin (also need aspell dictionary)'
	'gstreamer0.10-python: webcam support')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-$pkgver.tar.gz \
	'emesene.run' 'setup.patch')
install=emesene.install
options=('!libtool')
sha1sums=('c4759efe180c8e1589bd966c6b84106aa7cd356e'
          '46dcc89d089c0356d121356aa2ffff45e4468138'
          '0195898a6cb609f4ad736e0687bf76641f6db537')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' Controller.py

  # enable installing in setup.py
  patch -Np0 -i ${srcdir}/setup.patch

  # install license files
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/emesene/COPYING
  install -Dm644 PSF ${pkgdir}/usr/share/licenses/emesene/PSF

  # install files
  python2 setup.py install --prefix=${pkgdir}/usr

  # use /usr/share/emesene instead of /usr/lib/python2.5/site-packages
  mv -f ${pkgdir}/usr/lib/python2.7/site-packages $pkgdir/usr/share/emesene
  rm -rf ${pkgdir}/usr/lib

  # remove old executable and use our own because we use /usr/share/emesene
  rm ${pkgdir}/usr/bin/emesene
  install -Dm755 ${srcdir}/emesene.run ${pkgdir}/usr/bin/emesene
}
