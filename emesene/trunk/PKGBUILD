# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Claudio Riva <firetux83@gmail.com>

pkgname=emesene
pkgver=1.6
pkgrel=1
pkgdesc="A pygtk MSN Messenger client"
url="http://www.emesene.org/"
license=('custom:PSF' 'GPL' 'LGPL2.1')
arch=('i686' 'x86_64') # libmimic is hardcoded, so no 'any' package
depends=('python' 'pygtk' 'hicolor-icon-theme' 'xdg-utils')
optdepends=('gnome-python-extras: spell-check plugin'
	'gtkspell: spell-check plugin'
	'aspell: spell-check plugin (also need aspell dictionary)'
	'gstreamer0.10-python: webcam support')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-$pkgver.tar.gz \
	'emesene.run' 'setup.patch')
install=emesene.install
options=('!libtool')
md5sums=('ea4d3f4097265daac6823d8288979d02'
         '9b777e7b447bbefce7c3d97841a86d4c'
         'e0f77435102ea7145f430f5856b1fd55')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # enable installing in setup.py
  patch -Np0 -i ${srcdir}/setup.patch || return 1

  # install license files
  install -Dm644 COPYING ${pkgdir}/usr/share/licenses/emesene/COPYING
  install -Dm644 PSF ${pkgdir}/usr/share/licenses/emesene/PSF

  # install files
  python setup.py install --prefix=${pkgdir}/usr

  # use /usr/share/emesene instead of /usr/lib/python2.5/site-packages
  mv -f ${pkgdir}/usr/lib/python2.6/site-packages $pkgdir/usr/share/emesene
  rm -rf ${pkgdir}/usr/lib
  
  # remove old executable and use our own because we use /usr/share/emesene
  rm ${pkgdir}/usr/bin/emesene
  install -Dm755 ${srcdir}/emesene.run ${pkgdir}/usr/bin/emesene
}
