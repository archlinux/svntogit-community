# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  Gour <gour@mail.inet.hr>

pkgname=emms
epoch=1
pkgver=4.3
pkgrel=2
pkgdesc="The Emacs Multimedia System"
url="http://www.gnu.org/software/emms/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('emacs' 'taglib')
makedepends=('git')
install=${pkgname}.install
validpgpkeys=('691BF9D0EEC472730726EB7869725A21D60EEC42')
source=(https://ftp.gnu.org/gnu/emms/emms-$pkgver.tar.gz{,.sig})
#source=(git://git.sv.gnu.org/emms.git)
sha256sums=('00112d631eee0cfee80a85c048bfbf7c2d25886067ee02152376208ae87c2c36'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i \
    -e "s|/usr/local|/usr|g" \
    -e "s|/info|/share/info|g" \
    Makefile
  make
  make emms-print-metadata
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  mkdir -p "$pkgdir"/usr/bin
  mkdir -p "$pkgdir"/usr/share/{man/man1,info}
  make DESTDIR="$pkgdir" install INSTALLINFO=/usr/bin/true
}
