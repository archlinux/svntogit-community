# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mewp <aur.archlinux.org@mewp.pl>

pkgname=emptyepsilon
pkgver=2020.08.07
pkgrel=1
pkgdesc='Open source spaceship bridge simulator'
url='https://github.com/daid/EmptyEpsilon'
arch=('x86_64')
license=('GPL2')
depends=('sfml' 'libglvnd' 'glew' 'libx11' 'libxrandr')
makedepends=('git' 'cmake' 'mesa' 'python')
source=("git+https://github.com/daid/EmptyEpsilon.git#tag=EE-${pkgver}"
        "git+https://github.com/daid/SeriousProton.git#tag=EE-${pkgver}")
sha512sums=('SKIP'
            'SKIP')

prepare() {
  # fix shader loading, should be upstreamed
  sed 's|new Engine();|chdir(RESOURCE_BASE_DIR); new Engine();|' -i EmptyEpsilon/src/main.cpp
}

build() {
  mkdir -p EmptyEpsilon/build
  cd EmptyEpsilon/build
  # add cppflags to use fortify
  export CXXFLAGS="${CXXFLAGS} ${CPPFLAGS}"
  # release type is important here to get optimisation flags
  # defined in the project's cmake files
  cmake -DSERIOUS_PROTON_DIR="${srcdir}/SeriousProton" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DCPACK_PACKAGE_VERSION_MAJOR="$(echo ${pkgver} | cut -d. -f1)" \
        -DCPACK_PACKAGE_VERSION_MINOR="$(echo ${pkgver} | cut -d. -f2)" \
        -DCPACK_PACKAGE_VERSION_PATCH="$(echo ${pkgver} | cut -d. -f3)" \
        -DOpenGL_GL_PREFERENCE=GLVND \
        ..
  make
}

package() {
  cd EmptyEpsilon
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/emptyepsilon"
  mv "${pkgdir}/usr/script_reference.html" "${pkgdir}/usr/share/doc/emptyepsilon"
}

# vim: ts=2 sw=2 et:
