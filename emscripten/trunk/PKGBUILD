# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Vlad Kolotvin <vlad.kolotvin@gmail.com>

pkgname=emscripten
pkgver=1.38.48
pkgrel=1
pkgdesc="LLVM-based project that compiles C and C++ into highly-optimizable JavaScript for the web"
arch=('x86_64')
url="http://emscripten.org"
license=('custom')
depends=(nodejs python binaryen)
makedepends=(cmake libxml2 git ninja)
optdepends=('java-environment: for using clojure'
            'ruby: for using websockify addon'
            'cmake: for emcc --show-ports')
install=emscripten.install
# Get commit SHAs from here:
# https://chromium.googlesource.com/emscripten-releases/+/refs/heads/master/DEPS
source=(emscripten-$pkgver.tar.gz::"https://github.com/kripken/emscripten/archive/$pkgver.tar.gz"
        git+https://github.com/llvm/llvm-project.git#commit=411f1885b655ea622fe124a87a6eadfd988d7a5e
        "emscripten.sh"
        arch-template.patch
        libcxxabi-include-libunwind.patch)
sha512sums=('dee1d3270d66a25f2ece58334c3fdfd61b94ab2c1677d173fa587619b3c8847b5e034254e43175c64ec73794df46803df634103a38c14189c27ef5d069c83bd4'
            'SKIP'
            'fbe9b95b8d18e7d0c6ec5fded6f11b72fbe4ddd0391e5704b281ba79c479f3563e82423b790ddf3f0554a23d659193ca898a81fe3db509f16c30c7188b790e4d'
            '73d1debf254e8712b88cfe1c8763dca26cc259118d8acdee35eadcc4ac9b8ef8e794b419f0f77ca0b2d765577cb1679c365bb19a695c8e606a223d0bbc3273fd'
            '3456c61bc8cc022f58cf2ae1ebda53057adc4f05fcddd71a6a69b172ef47ce41f1ded4cb49c49c71a68e8436d974458a06566d618a7dcbad9ded42fa5713386c')

prepare() {
  cd "$srcdir"/emscripten-$pkgver

  patch -Np1 -i "$srcdir"/arch-template.patch

  mkdir "$srcdir"/llvm-project/llvm/build
}

build() {
  cd "$srcdir"/llvm-project/llvm/build

  # Inspired from https://github.com/WebAssembly/waterfall/blob/58e343a47ea02cb6daf19eefbdf626b23c24980c/src/build.py#L790
  cmake .. \
    -GNinja \
    -DPYTHON_EXECUTABLE=/usr/bin/python \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=YES \
    -DLLVM_TARGETS_TO_BUILD="X86;WebAssembly" \
    -DLLVM_BUILD_RUNTIME=OFF \
    -DLLVM_TOOL_LTO_BUILD=ON \
    -DLLVM_INSTALL_TOOLCHAIN_ONLY=ON \
    -DLLVM_INCLUDE_EXAMPLES=OFF \
    -DLLVM_INCLUDE_TESTS=OFF \
    -DLLVM_ENABLE_PROJECTS="lld;clang" \
    -DCLANG_INCLUDE_TESTS=OFF
  ninja
}

package() {
  # Install LLVM stuff according to https://github.com/emscripten-core/emscripten/blob/incoming/docs/process.md
  install -d "$pkgdir"/usr/lib
  cp -r "$srcdir"/llvm-project/llvm/build/bin "$pkgdir"/usr/lib/emscripten-llvm

  # Install emscripten
  cd "$srcdir"/emscripten-$pkgver
  install -d "$pkgdir"/usr/lib/emscripten
  cp -rup em* cmake site src system third_party tools "$pkgdir"/usr/lib/emscripten

  patch -Np1 -i "$srcdir"/libcxxabi-include-libunwind.patch -d "$pkgdir"

  # Remove clutter
  rm "$pkgdir"/usr/lib/emscripten/*.bat

  install -d "$pkgdir"/usr/share/doc
  ln -s /usr/lib/emscripten/site/source/docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm755 "$srcdir"/emscripten.sh "$pkgdir"/etc/profile.d/emscripten.sh
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
