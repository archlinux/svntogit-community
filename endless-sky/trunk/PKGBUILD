# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Zhao <alcasa dot mz at gmail dot com>
# Contributor: Leonard KÃ¶nig <leonard.r.koenig at googlemail dot com>

pkgname=endless-sky
pkgver=0.9.14
pkgrel=1
pkgdesc="A sandbox-style space exploration and combat game"
arch=('x86_64')
url="https://endless-sky.github.io/"
# licenses obtained via following script:
# grep "^License:" copyright | sed -e "s/License: //" -e "s/ /-/" | sort | uniq
license=(
  'GPL3'
  'custom:CC-BY-3.0'
  'custom:CC-BY-4.0'
  'custom:CC-BY-NC-3.0'
  'custom:CC-BY-SA-3.0'
  'custom:CC-BY-SA-4.0'
  'custom:public-domain'
)
depends=(
  'gcc-libs'
  'openal'
  'libpng'
  'glew'
  'hicolor-icon-theme'
  'libjpeg-turbo'
  'sdl2'
  'libmad'
)
makedepends=('scons')
optdepends=(
  'endless-sky-high-dpi: high resolution graphics assets'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/endless-sky/endless-sky/archive/v$pkgver.tar.gz")
b2sums=('5845c3b595f0feba7fc5f6ce1ead88b34e9426c3db53cf013fc67f9197d2cabad8fae39056fa6f4d6da76fceb43a9709397b95daba855d1b1ebf3c25a04ba007')

prepare() {
  cd "$pkgname-$pkgver"

  # binary is installed to /usr/games instead of /usr/bin
  sed -i 's:games", sky:bin", sky:' SConstruct
}

build() {
  cd "$pkgname-$pkgver"
  scons -j "$(nproc)"
}

package() {
  cd "$pkgname-$pkgver"
  scons \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    install

  # license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" copyright
}
