# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=engrampa
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Archive manipulator for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('bzip2' 'desktop-file-utils' 'gtk2' 'gzip' 'libarchive' 'tar')
makedepends=('caja>=1.12' 'mate-common' 'yelp-tools')
optdepends=('caja: file archiver extension'
            'unrar: for RAR uncompression'
            'zip: for ZIP archives' 'unzip: for ZIP archives'
            'p7zip: 7zip compression utility' 'arj: for ARJ archives'
            'unace: extraction tool for the proprietary ace archive format'
            'yelp: for reading MATE help documents')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz"
        fr-rpm-bsdtar.patch)
sha1sums=('76e2ac2806c6dcf6744766df6f9d0c056df5e301'
          '219b05a979bf6f249aaae27964f02345fd81168d')
install=${_pkgbase}.install

prepare() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    # This patch 'depends' on libarchive
    patch -Np1 -i "${srcdir}/fr-rpm-bsdtar.patch"
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=2.0 \
        --disable-static \
        --disable-packagekit
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
