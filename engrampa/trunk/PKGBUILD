# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=engrampa
pkgver=1.22.0
pkgrel=1
pkgdesc="Archive manipulator for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('gtk3' 'gzip' 'libarchive' 'tar' 'unzip' 'zip')
makedepends=('caja' 'intltool' 'itstool' 'python')
optdepends=('caja: Caja support'
	    'p7zip: 7Z and ARJ archive support'
            'unace: ACE archive support'
            'unrar: RAR archive support'
	    'brotli: Brotli compression support')
groups=('mate-extra')
conflicts=("engrampa-gtk3")
replaces=("engrampa-gtk3")
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        fr-rpm-bsdtar.patch)
sha256sums=('f7b0af06ac305aff59b1078f03599b1dc0c937b01212408c3d0a6b411dbdd29b'
            'dc05c3b3fbc8242d1c85b58f756f998d644d920c8444d0872e9ffef1ce297f77')

prepare() {
    	cd "${pkgname}-${pkgver}"
    
	# This patch 'depends' on libarchive
    	patch -Np1 -i "${srcdir}/fr-rpm-bsdtar.patch"
}

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--disable-packagekit

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
