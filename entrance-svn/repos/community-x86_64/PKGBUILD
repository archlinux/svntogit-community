# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=entrance-svn
pkgver=45539
pkgrel=1
pkgdesc="e17 login manager"
arch=('i686' 'x86_64')
groups=('e17-extra-svn')
url="http://www.enlightenment.org"
license=('BSD')
backup=('etc/pam.d/entrance')
depends=('efreet-svn' 'esmart-svn')
makedepends=('svn' 'cvs' 'xorg-server')
conflicts=('entrance')
provides=('entrance')
replaces=('entrance-cvs')
options=('!libtool')
source=('entranced' 'entrance.pam')
md5sums=('dc90edc8ac33f8acec39e00c992b8976'
         '8eaaf658e911dcaba8b284695996eb2e')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/entrance"
_svnmod="entrance"

build() {
   cd $srcdir

  msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build
  
  ./autogen.sh --prefix=/usr --with-xbin=/usr/bin --libexecdir=/usr/lib
  make || return 1
  make DESTDIR=$pkgdir install || return 1

# install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING
      
  rm -r $srcdir/$_svnmod-build

# install daemon file
  install -Dm755 $srcdir/entranced $startdir/pkg/etc/rc.d/entranced

# remove init.d daemon file
  rm -rf $startdir/pkg/etc/init.d

# remove pam file and substitute with working one
 rm -rf ${pkgdir}/etc/pam.d/entrance
 install -Dm644 ${srcdir}/entrance.pam ${pkgdir}/etc/pam.d/entrance
}
