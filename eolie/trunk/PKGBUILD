# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Cédric Bellegarde cedric bellegarde at adishatz dot org

pkgname=eolie
pkgver=0.9.25
pkgrel=1
pkgdesc="Simple web browser for GNOME"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Eolie"
license=('GPL3')
depends=('gtkspell3' 'python-cairo' 'python-dateutil' 'python-gobject' 'webkit2gtk')
makedepends=('git' 'gobject-introspection' 'meson')
optdepends=('python-beautifulsoup4: Import html bookmarks'
            'python-crypto: Firefox Sync support' 
            'python-fxa: Firefox Sync support'
            'python-pyopenssl: Show SSL certificates'
            'python-requests-hawk: Firefox Sync support')
_commit=419b4ebd9af83e76d79c3fdd2635b14142ea5b6e  # tags/0.9.25
source=("git+https://gitlab.gnome.org/gnumdk/eolie.git#commit=$_commit"
        "git+https://gitlab.gnome.org/gnumdk/eolie-po.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/-/+/g'
}

prepare() {
	mkdir build
	cd $pkgname
	git config submodule.subprojects/po.url "$srcdir/eolie-po"
	git submodule update --init
}

build() {
	cd build
	meson --prefix=/usr --libexecdir=/usr/lib/$pkgname --buildtype=release ../$pkgname
	ninja
}

package() {
	cd build
	DESTDIR="$pkgdir" ninja install
}
