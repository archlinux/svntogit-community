# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Cédric Bellegarde cedric bellegarde at adishatz dot org

pkgname=eolie
pkgver=0.9.100
pkgrel=2
pkgdesc="Simple web browser for GNOME"
arch=('any')
url="https://wiki.gnome.org/Apps/Eolie"
license=('GPL3')
depends=('gtkspell3' 'python-cairo' 'python-dateutil' 'python-gobject' 'webkit2gtk' 'gnome-settings-daemon')
makedepends=('git' 'gobject-introspection' 'meson')
optdepends=('python-beautifulsoup4: Import html bookmarks'
            'python-crypto: Firefox Sync support' 
            'python-fxa: Firefox Sync support'
            'python-pyopenssl: Show SSL certificates'
            'python-requests-hawk: Firefox Sync support')
_commit=187b93a38a837d7cd37949cd6334904c1c22b861  # tags/0.9.100
source=("git+https://gitlab.gnome.org/World/eolie.git#commit=$_commit"
        "git+https://gitlab.gnome.org/gnumdk/eolie-po.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/-/+/g'
}

prepare() {
	cd $pkgname
	git config submodule.subprojects/po.url "$srcdir/eolie-po"
	git submodule update --init
}

build() {
	arch-meson $pkgname build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
