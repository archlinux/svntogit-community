# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=eom
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gobject-introspection-runtime' 'gtk3' 'exempi'
         'lcms2' 'libexif' 'libjpeg-turbo' 'librsvg' 'mate-desktop-gtk3>=1.12'
         'mate-icon-theme' 'startup-notification' 'zlib')
makedepends=('mate-common' 'gobject-introspection' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('721d1d418c8da0134662ab886445152869b715b6')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --with-librsvg \
        --disable-python
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
