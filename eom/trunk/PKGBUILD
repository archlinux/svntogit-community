# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=eom
pkgver=1.8.0
pkgrel=3
pkgdesc="An image viewing and cataloging program for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gtk2' 'exempi' 'lcms2' 'libexif'
         'libjpeg-turbo' 'librsvg' 'mate-desktop' 'mate-icon-theme' 'pygtk'
         'python2' 'python2-gobject2' 'startup-notification' 'zlib')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
replaces=('mate-image-viewer')
provides=('mate-image-viewer')
conflicts=('mate-image-viewer')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz")
sha1sums=('580b3c06e16e14dd3f72d545f47f295af5d67dd1')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --with-librsvg
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
