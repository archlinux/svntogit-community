# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=epdfview
pkgver=0.1.8
pkgrel=3
pkgdesc="A free lightweight PDF document viewer."
url="http://www.emma-soft.com/projects/epdfview/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('poppler-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'gtk2')
makedepends=('pkgconfig')
install='epdfview.install'
source=("http://www.emma-soft.com/projects/${pkgname}/chrome/site/releases/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop.patch"
        "glib2_headers.patch"
        "0001-When-using-Poppler-0.17.0-I-needed-to-swap-the-blue-.patch")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/${pkgname}.desktop.patch"
  patch -Np1 -i "${srcdir}/0001-When-using-Poppler-0.17.0-I-needed-to-swap-the-blue-.patch"
  patch -Np1 -i "${srcdir}/glib2_headers.patch" # FS#30116

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for size in 24 32 48; do
      install -Dm644 data/icon_${pkgname}-${size}.png \
          "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png"
  done
}

md5sums=('e50285b01612169b2594fea375f53ae4'
         'fbf22bbabdbb7544db615ac5775d57e2'
         '2fffa9c7cd4c5f0744803591c2f162a3'
         '7f9ea101a41f5b4e999fd024f423d41f')
