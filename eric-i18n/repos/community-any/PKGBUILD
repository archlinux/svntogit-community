# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=18.01
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
makedepends=("eric-common>=${pkgver}")
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'ru             "Russian"')

pkgname=()
source=()
_url="https://downloads.sourceforge.net/eric-ide"

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}

  pkgname+=(eric-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_locale}-${pkgver}.tar.gz")

  eval "package_eric-i18n-${_locale,,}() {
    _package ${_locale}
  }"
done

_package() {
  pkgdesc="${1} language pack for Eric IDE"
  depends=("eric-common>=${pkgver}")

  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/qt/translations/eric6_${1}.qm"
}

sha512sums=('6666b198f37dfd2ca265cd031d7bac7e6f6ca0ab5516e39cf6e9ccaae011396f12ae075e9c3f8427c218dbbfe048bbfe956c0298a244519daa46d540d25bb26a'
            '55ad42f25d9283eccf4f38be75e6545a3562835012e64eb1a31db4ca822fc3c06e19618794967cee7d9606f122b652ae2e3867db19054438d97394ad6dedc724'
            '393b70874e3252e063bd857ba508161f07120b346d51e36f1ccb684dbb748f2d8de8e9993b3013978c4f552f2d149f6a17b1888c6611c62c3f1ab766135d8b8d'
            '7ce90af3e8c8f18c8fc2e5eda1f3df4ff99ecfb432d5db9f51894bf9e566463b7be3a3ffc0411852f1f48ec5f5d6a65cf9b1fe2cbdcbd3511a7914ad0fe3bf9a')
