# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.0.2
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_path}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_lang} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${2} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('5b74c8222782d6aa872f893140f1df70'
         'c04b1691b318849e00ba729b1613aea0'
         '00f3945d5324eeef91aafe0cc76eb4c9'
         '0f01655256d98f6243a91e296e62a6ce'
         '5144390eb645f6a9a104c277e53f4d56'
         '8adeed27aa9928aaba01bd4c11ce1529'
         '196d58bcf353017d02be070c0d2d0a2c'
         '0977f9b47f20a816ef6604c6a6a39854'
         '00513443d7b3d5678975487f3ad9048d'
         'b8aac9cb49f5fe646b210ee6f4ae44bf')
