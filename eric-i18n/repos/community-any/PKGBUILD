# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=5.5.0
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=("eric-common>=${pkgver}")
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  _pkgname=eric-i18n-${_locale,,}
  pkgname+=(${_pkgname})
  source+=("${_url}/eric5-i18n-${_path}-${pkgver}.tar.gz")

  eval "package_${_pkgname}() {
    _package ${_lang}
  }"
done

_package() {
  pkgdesc="$2 language pack for Eric IDE"
  install -Dm644 "eric5-${pkgver}/eric/i18n/eric5_$1.qm" \
                 "${pkgdir}/usr/share/qt4/translations//eric5_$1.qm"
}

md5sums=('4b95e8b8f91692583d9cb490c32a3f7e'
         'c338c933133694d665aa39595a66d7bc'
         '9932eb5b755080ade976b4b71b6c153a'
         '8454e44cfb30f75361660fa058f914c7'
         '468f2eafc4911427557d35a4c4edaf32'
         '16549deb19f110bba73871aa8a462d99'
         '0fbc822559f7238b89267eabbdbf4597'
         '0a409d7728f30b0b36bc32d352ccb996'
         'cda00b70b6fc0bb487eabe0deea2193c'
         '15e82b749e3953a1fd99cb809f1dbea2')
