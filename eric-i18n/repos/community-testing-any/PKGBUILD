# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.0.0
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  _pkgname=eric-i18n-${_locale,,}
  pkgname+=(${_pkgname} ${_pkgname}-qt4)
  source+=("${_url}/eric6-i18n-${_path}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_${_pkgname}${_qtver}() {
      _package ${_lang} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${2} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('058f6433649d33d56ebb755aa2509b68'
         '4e769481934d8d77556f19a74a98e80e'
         '89a1a547d1a8fe707d8f0a805151856e'
         '7d3ed635deb99c24b0f4a6521910fc7c'
         'aedee132d370fd3f2e3efb040ab8732b'
         '30d08539a502e38b6feced82e7487847'
         '128d95da8963bcf2c2b1812b6532757e'
         '3a5f370751109ecc6bca895249cb97fb'
         '652a1384747837fbe0bde7ef8ad0cc03'
         '022691a3b5803de3d4ac31afa844db98')
