# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.0.11
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_path}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_lang} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${2} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('8d8bbe7bac4f68111e4a4396572abfce'
         'f87fc2ab92d7a671c776f36ee3146f77'
         '5b7fbc380f56e7b98cc6bcf768bed68a'
         '47c3a405b99ed55599d31ac91e094a9f'
         '54efeec4774c33bc4b76abb24760bfa2'
         '22764c1ce2d443d660144f328cb55e5e'
         'e91af4035bc6c1d35e2e276a1d48d069'
         '90bf473e3d2214e3531cb42823f867d2'
         '88cc75ebc4d99e5ada0a3ac28c1a9891'
         '78f4d39f9ebd59dbd1e8422d2af2c2c3')
