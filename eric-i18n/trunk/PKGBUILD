# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.0.4
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_path}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_lang} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${2} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('26963599313cf44afeeaeb5d106425d6'
         '8c0ab3644ac9d6215a6eda82fe78aa14'
         '20d202c22c37f2ed54990adf31af1e62'
         'ac968511e7decca0d035fd8f02d94c2a'
         '06603b1715efa4639efb5bf5275b179b'
         '37ffa0075d0ad4aeb3ca247b9a881358'
         '4e9d9ec5cd907b757798899be5a93e3e'
         '8616f6929d434e2a707fd9417308b770'
         'e0f5ec5482f09842099076ca9dd56863'
         '606c27b54e075fc6637d51cdb8ae2c3d')
