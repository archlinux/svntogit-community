# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=5.5.1
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=("eric-common>=${pkgver}")
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  _pkgname=eric-i18n-${_locale,,}
  pkgname+=(${_pkgname})
  source+=("${_url}/eric5-i18n-${_path}-${pkgver}.tar.gz")

  eval "package_${_pkgname}() {
    _package ${_lang}
  }"
done

_package() {
  pkgdesc="$2 language pack for Eric IDE"
  install -Dm644 "eric5-${pkgver}/eric/i18n/eric5_$1.qm" \
                 "${pkgdir}/usr/share/qt4/translations//eric5_$1.qm"
}

md5sums=('00586b5452d109d2e77e19125b1b3d85'
         'f73154c403902a788920831b5fe2c5ab'
         '30208e16f9bc95c51a4d92ad98f03c87'
         'eec4b3f6a0db1fc187614f568ab0a2a5'
         'd481b6c353f09802a197adf5a3422fe6'
         '6fee8af3bc62e70075c79e7248ff3265'
         'b881d5013cc986e0f36a1808b6cff83f'
         '1cc9ff8c5d79b62db28641a9392dc101'
         'd8142dfe04de69bc10b86f33cb948afd'
         'f11d9906b80e3820bfbb2d7ca2d05405')
