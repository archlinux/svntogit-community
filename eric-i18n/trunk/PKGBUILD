# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=6.0.9
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=()
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  pkgname+=(eric-i18n-${_locale,,} eric-qt4-i18n-${_locale,,})
  source+=("${_url}/eric6-i18n-${_path}-${pkgver}.tar.gz")

  for _qtver in '-qt4' ''; do
    eval "package_eric${_qtver}-i18n-${_locale,,}() {
      _package ${_lang} ${_qtver}
    }"
  done
done

_package() {
  pkgdesc="${2} language pack for Eric IDE"
  depends=("eric-common${3}>=${pkgver}")

  [ -z ${3} ] && _qtver="qt" || _qtver="qt4"
  install -Dm644 "eric6-${pkgver}/eric/i18n/eric6_${1}.qm" \
                 "${pkgdir}/usr/share/${_qtver}/translations/eric6_${1}.qm"
}

md5sums=('73232c56cfcb0a1fee159f31161d83f8'
         'd36f7f7934c50eec1cdb6e431b6c0f19'
         'c8ce4400b7f4f5eba6a688c6ed685dd4'
         '4be9ac4c1649dc2110af504883b09b78'
         '7f1c8b34653cd21997920969d6ab957f'
         'd54c3b15116c36f8edcf73f1f84a69f0'
         '65c4b790a6442f014e100c709f559f32'
         '6bcc015796f8ac05b8cfff104ecfa3c5'
         'ae167815dc33c79362a3541150bcd8ef'
         '3b85cb2a1398ef676a48b697d513b3b3')
