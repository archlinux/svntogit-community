# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: M0Rf30

pkgbase=eric-i18n
pkgver=5.5.2
pkgrel=1
pkgdesc="Language pack for the Eric IDE"
arch=('any')
depends=("eric-common>=${pkgver}")
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=()
changelog=ChangeLog

_languages=('cs             "Czech"'
            'de             "German"'
            'en             "English"'
            'es             "Spanish"'
            'fr             "French"'
            'it             "Italian"'
            'pt             "Portuguese"'
            'ru             "Russian"'
            'tr             "Turkish"'
            'zh_CN.GB2312   "Chinese (Simplified)"')

pkgname=()
source=()
_url=http://downloads.sourceforge.net/eric-ide/

for _lang in "${_languages[@]}"; do
  _path=${_lang%% *}
  _locale=${_path%%.*}

  _pkgname=eric-i18n-${_locale,,}
  pkgname+=(${_pkgname})
  source+=("${_url}/eric5-i18n-${_path}-${pkgver}.tar.gz")

  eval "package_${_pkgname}() {
    _package ${_lang}
  }"
done

_package() {
  pkgdesc="$2 language pack for Eric IDE"
  install -Dm644 "eric5-${pkgver}/eric/i18n/eric5_$1.qm" \
                 "${pkgdir}/usr/share/qt4/translations//eric5_$1.qm"
}

md5sums=('da3c17c8d066c9b06df0f42eace716e1'
         '030b960fc9e3c194c65699ba33059a13'
         '8a321c3b37f9493360164b8b53284441'
         '016afd5f1d95ac9b911444963d980470'
         '3c4f88e1c27061118eb6a99810dff127'
         '0e28c6b01fe722d948e17d3101d1922e'
         'd73f168f53acc5091632b14d90bcc30f'
         '0931406b4057d4576fcecfd3b2f43084'
         '33c6171eed66ac380557edcb9ddb761b'
         '7677f28983e7d8717df8571633e38d25')
