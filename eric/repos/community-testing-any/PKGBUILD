# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Ray Rashif <schiv at archlinux dot org>
# Contributor: Brad Fanella <bradfanellaat archlinux dot us>
# See .contrib for older/other contributors

pkgbase=eric
pkgname=('eric-common' 'eric')
pkgver=5.4.7
pkgrel=1
pkgdesc="A full-featured Python 3.x and Ruby IDE in PyQt4"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python-qscintilla')
install=${pkgbase}.install
source=("http://downloads.sourceforge.net/eric-ide/eric5-${pkgver}.tar.gz"
        "${pkgbase}.desktop")
md5sums=('774d2bd1eadd694feceb129d4556c644'
         '8aa446c29e6de54e85410e040e754c6e')

prepare() {
  cd "${srcdir}/eric5-${pkgver}"

  # some freedesktop.org compatibility adjustments (referenced at runtime)
  # TODO: check if this is still needed, else move to package()
  for i in $(grep -R "eric5.desktop" . | sed 's/:.*//' | sort -u); do
    sed -i "s/eric5.desktop/eric.desktop/" ${i}
  done

  # desktop file later needed during build
  rm eric/eric5.desktop
  install -Dm644 "${srcdir}/${pkgbase}.desktop" "eric/${pkgbase}.desktop"
}

package_eric-common() {
  depends=()

  cd "${srcdir}/eric5-${pkgver}"
  # pyqt APIs
  install -Dm644 "eric/APIs/Python3/PyQt4.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/PyQt4.bas"
  # qscintilla APIs
  install -Dm644 "eric/APIs/Python3/QScintilla2.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/QScintilla2.bas"
  # zope APIs
  install -Dm644 "eric/APIs/Python/zope-2.10.7.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/zope-2.10.7.api"
  install -Dm644 "eric/APIs/Python/zope-2.11.2.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/zope-2.11.2.api"
  install -Dm644 "eric/APIs/Python/zope-3.3.1.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/zope-3.3.1.api"
  # ruby APIs
  install -Dm644 "eric/APIs/Ruby/Ruby-1.8.7.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.8.7.api"
  install -Dm644 "eric/APIs/Ruby/Ruby-1.8.7.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.8.7.bas"
  install -Dm644 "eric/APIs/Ruby/Ruby-1.9.1.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.9.1.api"
  install -Dm644 "eric/APIs/Ruby/Ruby-1.9.1.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.9.1.bas"
}

package_eric() {
  depends+=('eric-common')

  cd "${srcdir}/eric5-${pkgver}"
  python install.py -c -b /usr/bin -i "${pkgdir}"
  # remove common components
  find "${pkgdir}/usr/share/qt4/qsci/api/" -type f -not -name 'eric*' -exec rm {} \;

  # some canonical symlinks
  # TODO: might we want to just package this as eric5?
  #       upstream references them as two distinct versions...
  cd "${pkgdir}/usr/bin"
  for i in *; do
    ln -s /usr/bin/${i} ${i/eric5/eric}
  done
}

# vim:set ts=2 sw=2 et:
