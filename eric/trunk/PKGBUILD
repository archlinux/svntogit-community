# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Ray Rashif <schiv at archlinux dot org>
# Contributor: Brad Fanella <bradfanellaat archlinux dot us>
# See .contrib for older/other contributors

pkgbase=eric
pkgname=('eric-common' 'eric' 'python2-eric')
pkgver=19.03
pkgrel=1
pkgdesc="A full-featured Python and Ruby IDE in PyQt"
arch=('any')
url="https://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python-qscintilla-qt5' 'python2-qscintilla-qt5'
         'python-pyqtwebengine' 'python2-pyqtwebengine'
         'qt5-svg' 'qt5-webengine')
source=("https://downloads.sourceforge.net/eric-ide/eric6-${pkgver}.tar.gz"
        "fix-noapis.patch")
sha512sums=('66d2aaf9b8c215c84fadcbfc0985445e22dd6ec10724a2a4680345295a6e9b73478be5ba2d96ca2036adf3e7704072f9ef3c1be313fe076affa59492fc86690e'
            '428b0cdc1f28b9b969805e04a3b3a00a65094d968736870e00adf2fc1b46bc17e3a329d7c63c936fab6282e1f66059abc951739aa803e7015312f909c8593b2c')
changelog=ChangeLog

prepare() {
  # fix no-apis option
  cd "${srcdir}/eric6-${pkgver}"
  patch -p1 -i "${srcdir}/fix-noapis.patch"

  cp -r "${srcdir}/eric6-${pkgver}"{,_py2}

  cd "${srcdir}/eric6-${pkgver}_py2/eric"
  find . -name '*.py' -exec \
    sed -i -r "s:^#!/usr/bin/(env |)python(3|)$:#!/usr/bin/env python2:" {} \;
}

package_eric-common() {
  depends=()

  cd "${srcdir}/eric6-${pkgver}/eric"
  # eric APIs
  install -Dm644 "APIs/Python3/eric6.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.api"
  install -Dm644 "APIs/Python3/eric6.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.bas"
  # pyqt APIs
  install -Dm644 "APIs/Python3/PyQt5.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/PyQt5.bas"
  # qss APIs
  install -Dm644 "APIs/QSS/qss.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/qss/qss.api"
  # qscintilla APIs
  install -Dm644 "APIs/Python3/QScintilla2.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/QScintilla2.bas"
  # zope APIs
  install -Dm644 "APIs/Python/zope-2.10.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.10.7.api"
  install -Dm644 "APIs/Python/zope-2.11.2.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.11.2.api"
  install -Dm644 "APIs/Python/zope-3.3.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-3.3.1.api"
  # ruby APIs
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api"
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.bas"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.api"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.bas"
}

package_eric() {
  depends=('eric-common' 'python-qscintilla-qt5'  'python-pyqtwebengine' 'qt5-svg' 'qt5-webkit')
  optdepends=('qt5-webengine: alternative browser backend')

  cd "${srcdir}/eric6-${pkgver}"
  python install.py -c -b /usr/bin -i "${pkgdir}" --no-apis

  # fix paths in desktop files
  find "${pkgdir}" -name '*.desktop' -exec sed -i "s|${pkgdir}||g" {} \;
}

package_python2-eric() {
  depends=('eric-common' 'python2-qscintilla-qt5' 'python2-pyqtwebengine' 'qt5-svg' 'qt5-webkit')
  optdepends=('qt5-webengine: alternative browser backend')

  cd "${srcdir}/eric6-${pkgver}_py2"
  python2 install.py -c -b /usr/bin -i "${pkgdir}" -y --no-apis

  # fix paths in desktop files
  find "${pkgdir}" -name '*.desktop' -exec sed -i "s|${pkgdir}||g" {} \;
}

# vim:set ts=2 sw=2 et:
