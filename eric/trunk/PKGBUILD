# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Ray Rashif <schiv at archlinux dot org>
# Contributor: Brad Fanella <bradfanellaat archlinux dot us>
# See .contrib for older/other contributors

pkgbase=eric
pkgname=('eric-common' 'eric' 'python2-eric'
         'eric-common-qt4' 'eric-qt4' 'python2-eric-qt4')
pkgver=6.1.4
pkgrel=2
pkgdesc="A full-featured Python and Ruby IDE in PyQt"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python-qscintilla' 'python2-qscintilla'
         'python-qscintilla-qt5' 'python2-qscintilla-qt5'
         'qt5-svg' 'qt5-webkit')
source=("http://downloads.sourceforge.net/eric-ide/eric6-${pkgver}.tar.gz")
md5sums=('1cddb947f1d19b7f01390392d1835c4a')
changelog=ChangeLog

prepare() {
  cp -r "${srcdir}/eric6-${pkgver}"{,_py2}

  cd "${srcdir}/eric6-${pkgver}_py2/eric"
  find . -name '*.py' -exec \
    sed -i -r "s:^#!/usr/bin/(env |)python(3|)$:#!/usr/bin/env python2:" {} \;

  cp -r "${srcdir}/eric6-${pkgver}"{,-qt4}
  cp -r "${srcdir}/eric6-${pkgver}_py2"{,-qt4}
}

package_eric-common-qt4() {
  depends=()

  cd "${srcdir}/eric6-${pkgver}/eric"
  # eric APIs
  install -Dm644 "APIs/Python3/eric6.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/eric6.api"
  install -Dm644 "APIs/Python3/eric6.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/eric6.bas"
  # pyqt APIs
  install -Dm644 "APIs/Python3/PyQt4.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/PyQt4.bas"
  # qss APIs
  install -Dm644 "APIs/QSS/qss.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/qss/qss.api"
  # qscintilla APIs
  install -Dm644 "APIs/Python3/QScintilla2.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/QScintilla2.bas"
  # zope APIs
  install -Dm644 "APIs/Python/zope-2.10.7.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/zope-2.10.7.api"
  install -Dm644 "APIs/Python/zope-2.11.2.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/zope-2.11.2.api"
  install -Dm644 "APIs/Python/zope-3.3.1.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/python/zope-3.3.1.api"
  # ruby APIs
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.8.7.api"
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.8.7.bas"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.api" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.9.1.api"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.bas" \
                 "${pkgdir}/usr/share/qt4/qsci/api/ruby/Ruby-1.9.1.bas"
}

package_eric-qt4() {
  depends=('eric-common-qt4' 'python-qscintilla')
  conflicts=('eric')

  cd "${srcdir}/eric6-${pkgver}-qt4"
  python install.py -c -b /usr/bin -i "${pkgdir}" --noapis --pyqt=4
}

package_python2-eric-qt4() {
  depends=('eric-common-qt4' 'python2-qscintilla')
  conflicts=('python2-eric-qt4')

  cd "${srcdir}/eric6-${pkgver}_py2-qt4"
  python2 install.py -c -b /usr/bin -i "${pkgdir}" -y --noapis --pyqt=4
}

package_eric-common() {
  depends=()

  cd "${srcdir}/eric6-${pkgver}/eric"
  # eric APIs
  install -Dm644 "APIs/Python3/eric6.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.api"
  install -Dm644 "APIs/Python3/eric6.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.bas"
  # pyqt APIs
  install -Dm644 "APIs/Python3/PyQt5.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/PyQt5.bas"
  # qss APIs
  install -Dm644 "APIs/QSS/qss.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/qss/qss.api"
  # qscintilla APIs
  install -Dm644 "APIs/Python3/QScintilla2.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/QScintilla2.bas"
  # zope APIs
  install -Dm644 "APIs/Python/zope-2.10.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.10.7.api"
  install -Dm644 "APIs/Python/zope-2.11.2.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.11.2.api"
  install -Dm644 "APIs/Python/zope-3.3.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-3.3.1.api"
  # ruby APIs
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api"
  install -Dm644 "APIs/Ruby/Ruby-1.8.7.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.bas"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.api"
  install -Dm644 "APIs/Ruby/Ruby-1.9.1.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.bas"
}

package_eric() {
  depends=('eric-common' 'python-qscintilla-qt5' 'qt5-svg' 'qt5-webkit')
  conflicts=('eric-qt4')

  cd "${srcdir}/eric6-${pkgver}"
  python install.py -c -b /usr/bin -i "${pkgdir}" --noapis
}

package_python2-eric() {
  depends=('eric-common' 'python2-qscintilla-qt5' 'qt5-svg' 'qt5-webkit')
  conflicts=('python2-eric-qt4')

  cd "${srcdir}/eric6-${pkgver}_py2"
  python2 install.py -c -b /usr/bin -i "${pkgdir}" -y --noapis
}

# vim:set ts=2 sw=2 et:
