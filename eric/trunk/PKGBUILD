# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Ray Rashif <schiv at archlinux dot org>
# Contributor: Brad Fanella <bradfanellaat archlinux dot us>
# See .contrib for older/other contributors

pkgbase=eric
pkgname=('eric-common' 'eric' 'python2-eric')
pkgver=19.06
_pkgver=19.6
pkgrel=1
pkgdesc="A full-featured Python and Ruby IDE in PyQt"
arch=('any')
url="https://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python-qscintilla-qt5' 'python2-qscintilla-qt5'
         'python-pyqtwebengine' 'python2-pyqtwebengine'
         'qt5-svg' 'qt5-webengine')
source=("https://downloads.sourceforge.net/eric-ide/eric6-${_pkgver}.tar.gz"
        "fix-install-dir.patch")
sha512sums=('91401f73c3f009265b240a4654cdc1ca6af636f6f12afa40ca59bebdf4955854212400d18e5eb5d926de6afdd904a27f68523bb52dff68b256b67b00142f3ac8'
            '027c79e7ec4d017e1da10f9d0256fcef28033780f3bddf29b52538b6c0128ce952adf6212607379db83bd143a8851df68c592d2c142ad02d742c9b423908fc50')
changelog=ChangeLog

prepare() {
  # fix no-apis option
  cd "${srcdir}/eric6-${_pkgver}"
  patch -p1 -i "${srcdir}/fix-install-dir.patch"
  cp -r "${srcdir}/eric6-${_pkgver}"{,_py2}

  cd "${srcdir}/eric6-${_pkgver}_py2/eric"
  find . -name '*.py' -exec \
    sed -i -r "s:^#!/usr/bin/(env |)python(3|)$:#!/usr/bin/env python2:" {} \;
}

package_eric-common() {
  depends=()

  cd "${srcdir}/eric6-${_pkgver}/eric"
  # eric APIs
  install -Dm644 "eric6/APIs/Python3/eric6.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.api"
  install -Dm644 "eric6/APIs/Python3/eric6.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/eric6.bas"
  # pyqt APIs
  install -Dm644 "eric6/APIs/Python3/PyQt5.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/PyQt5.bas"
  # qss APIs
  install -Dm644 "eric6/APIs/QSS/qss.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/qss/qss.api"
  # qscintilla APIs
  install -Dm644 "eric6/APIs/Python3/QScintilla2.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/QScintilla2.bas"
  # zope APIs
  install -Dm644 "eric6/APIs/Python/zope-2.10.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.10.7.api"
  install -Dm644 "eric6/APIs/Python/zope-2.11.2.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-2.11.2.api"
  install -Dm644 "eric6/APIs/Python/zope-3.3.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/python/zope-3.3.1.api"
  # ruby APIs
  install -Dm644 "eric6/APIs/Ruby/Ruby-1.8.7.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api"
  install -Dm644 "eric6/APIs/Ruby/Ruby-1.8.7.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.bas"
  install -Dm644 "eric6/APIs/Ruby/Ruby-1.9.1.api" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.api"
  install -Dm644 "eric6/APIs/Ruby/Ruby-1.9.1.bas" \
                 "${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.9.1.bas"
}

package_eric() {
  depends=('eric-common' 'python-qscintilla-qt5'  'python-pyqtwebengine' 'qt5-svg' 'qt5-webkit')
  optdepends=('qt5-webengine: alternative browser backend')

  cd "${srcdir}/eric6-${_pkgver}"
  python install.py -c -b "/usr/bin" -i "${pkgdir}" --no-apis

  # fix paths in desktop files
  find "${pkgdir}" -name '*.desktop' -exec sed -i "s|${pkgdir}||g" {} \;
}

package_python2-eric() {
  depends=('eric-common' 'python2-qscintilla-qt5' 'python2-pyqtwebengine' 'qt5-svg' 'qt5-webkit')
  optdepends=('qt5-webengine: alternative browser backend')

  cd "${srcdir}/eric6-${_pkgver}_py2"
  python2 install.py -c -b "/usr/bin" -i "${pkgdir}" --no-apis

  # fix paths in desktop files
  find "${pkgdir}" -name '*.desktop' -exec sed -i "s|${pkgdir}||g" {} \;
}

# vim:set ts=2 sw=2 et:
