# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Felix Rohrbach <fxrh@gmx.de>

pkgname=eric5
pkgver=5.0.5
pkgrel=5
pkgdesc="A full-featured Python IDE that is written in PyQt4 using the QScintilla2 editor widget"
arch=('any')
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=('python-qscintilla' 'python-qt')
source=(http://downloads.sourceforge.net/eric-ide/${pkgname}/stable/${pkgver}/${pkgname}-${pkgver}.tar.gz
	'eric5.desktop')
md5sums=('daac91f1982c114ac3cfae5a3509a6b6'
         '67944b5b6a963805694e82ee620fcbf3')

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	python install.py -c -b /usr/bin -d /usr/lib/python3.1/site-packages -i ${pkgdir}

	# Install .desktop file	
	install -m644 ${srcdir}/eric5.desktop \
		${pkgdir}/usr/share/applications/eric5.desktop

	# Fix icon
	mv ${pkgdir}/usr/share/pixmaps/eric.png \
		${pkgdir}/usr/share/pixmaps/eric5.png
	
	# Remove files conflicting with eric4
	rm ${pkgdir}/usr/share/qt/qsci/api/python/zope-2.10.7.api
	rm ${pkgdir}/usr/share/qt/qsci/api/python/zope-2.11.2.api
	rm ${pkgdir}/usr/share/qt/qsci/api/python/zope-3.3.1.api
	rm ${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api
}


