# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: kappa <kappacurve@gmail.com>

pkgname=erlang-sdl
pkgver=1.3.1
pkgrel=3
arch=('x86_64')
pkgdesc='SDL and OpenGL bindings for Erlang'
url='http://github.com/dgud/esdl/'
license=('custom')
depends=('erlang' 'glu' 'libgl' 'sdl')
makedepends=('git' 'mesa-libgl' 'rebar')
#tag=esdl-1.3.1 fails
source=("esdl::git+https://github.com/dgud/esdl.git#commit=dbd8ce9228aa36828091df2e8706c364094a3e22")
md5sums=('SKIP')

prepare() {
  find esdl -type f -print0 | xargs -0 chmod 644
}

build() {
  cd esdl
  CFLAGS="$CFLAGS -Wno-deprecated-declarations" rebar compile
}

package() {
  cd esdl
  install -d "$pkgdir/usr/lib/erlang/lib/esdl-$pkgver"
  cp -ruv c_src doc ebin include priv src \
    "$pkgdir/usr/lib/erlang/lib/esdl-$pkgver/"
  install -Dm644 license.terms "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
