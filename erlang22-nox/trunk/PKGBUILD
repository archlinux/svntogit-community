# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>
# Contributor: Ricardo Catalinas Jiménez <jimenezrick@gmail.com>

pkgname=erlang22-nox
pkgver=22.3.4.21
_docver=22.3
pkgrel=1
pkgdesc="General-purpose concurrent functional programming language (headless version)"
arch=(x86_64)
url="https://www.erlang.org/"
license=(Apache)
depends=(ncurses openssl)
makedepends=(fop git java-environment libxslt lksctp-tools)
conflicts=(erlang)
options=(staticlibs)
_tag=2aaaf99359ecf92a6c35f2abf3b2b622ad71bdcc # git rev-parse OTP-${pkgver}
source=(git+https://github.com/erlang/otp.git#tag=${_tag})
sha256sums=('SKIP')

prepare() {
  cd otp
  ./otp_build autoconf
}

build() {
  cd otp
  ./configure \
    --prefix=/usr \
    --enable-builtin-zlib \
    --enable-smp-support
  make
}

package() {
  cd otp

  make DESTDIR="$pkgdir" install

  # License
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
