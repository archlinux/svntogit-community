# $Id: PKGBUILD 61589 2012-01-03 23:37:49Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: kappa <kappacurve@gmail.com>

pkgname=esdl
pkgver=1.0.1
pkgrel=2
epoch=2
arch=('x86_64' 'i686')
pkgdesc="SDL and OpenGL bindings for the Erlang programming language"
url="http://esdl.sourceforge.net/"
license=('custom')
depends=('erlang' 'sdl' 'mesa')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.src.tar.gz")
md5sums=('50230ea81418cb029281a70627a5dd8e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  find -type f -print0 | xargs -0 chmod 644
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INSTALLDIR="$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver" install

  ## To make wings3d compile with esdl 1.2
  #ln -s /usr/lib/erlang/lib/wx-0.99/include/glu.hrl \
  #  "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver/include/glu.hrl"
  #ln -s /usr/lib/erlang/lib/wx-0.99/include/gl.hrl \
  #  "$pkgdir/usr/lib/erlang/lib/$pkgname-$pkgver/include/gl.hrl"

  install -Dm644 "$srcdir/$pkgname-$pkgver/license.terms" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
