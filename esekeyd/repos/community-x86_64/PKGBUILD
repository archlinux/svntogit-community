# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Evgenij Vdovin <evdovin@pochta.ru>

pkgname=esekeyd
pkgver=1.2.6
pkgrel=1
pkgdesc="Multimedia keyboard driver for Linux"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/esekeyd/"
license=('GPL')
depends=('glibc')
source=(http://www.burghardt.pl/files/$pkgname-$pkgver.tar.gz
        esekeyd.rc esekeyd.confd esekeyd.conf)
backup=('etc/conf.d/esekeyd' 'etc/esekeyd.conf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install || return 1
  install -D -m755 "$srcdir/esekeyd.rc" "$pkgdir/etc/rc.d/esekeyd" || return 1
  install -D -m644 "$srcdir/esekeyd.confd" "$pkgdir/etc/conf.d/esekeyd" || return 1
  install -D -m644 "$srcdir/esekeyd.conf" "$pkgdir/etc/esekeyd.conf" || return 1
}

md5sums=('73e2c817d0b3d21cc40ba5d94fe3abd0'
         '1c1b954265ad29de010b1dd750350ed2'
         '5edf18376549251daacd49ae136ba984'
         'f16c1aefe84214856f3a7e49a1a866f3')
