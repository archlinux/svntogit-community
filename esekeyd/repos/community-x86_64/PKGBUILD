# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Evgenij Vdovin <evdovin@pochta.ru>

pkgname=esekeyd
pkgver=1.2.7
pkgrel=1
pkgdesc="Multimedia keyboard driver for Linux"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/esekeyd/"
license=('GPL')
depends=('glibc' 'sh')
source=(http://www.burghardt.pl/files/$pkgname-$pkgver.tar.gz
        esekeyd.rc esekeyd.confd esekeyd.conf)
backup=('etc/conf.d/esekeyd' 'etc/esekeyd.conf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install 
  install -D -m755 "$srcdir/esekeyd.rc" "$pkgdir/etc/rc.d/esekeyd" 
  install -D -m644 "$srcdir/esekeyd.confd" "$pkgdir/etc/conf.d/esekeyd" 
  install -D -m644 "$srcdir/esekeyd.conf" "$pkgdir/etc/esekeyd.conf" 
}
md5sums=('5937ad6d7815dbc6ab6983411a9f37d4'
         '1c1b954265ad29de010b1dd750350ed2'
         '5edf18376549251daacd49ae136ba984'
         'f16c1aefe84214856f3a7e49a1a866f3')
