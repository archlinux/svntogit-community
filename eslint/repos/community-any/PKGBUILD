# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=eslint
pkgver=5.16.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='http://eslint.org'
license=('MIT')
depends=('nodejs' 'acorn')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('4b7473d758bb73c000e493efef1007f9d3b2abf0aefd55c78875c13ce53593f240339757a903eddea72b8691d2a2b5e6ae9bb683482420155702a0a9cdfa1922')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/eslint/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done
}
