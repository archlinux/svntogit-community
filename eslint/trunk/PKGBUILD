# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=eslint
pkgver=3.19.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='http://eslint.org'
license=('MIT')
depends=('nodejs' 'acorn')
makedepends=('npm')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('c7a2c9197582181ff860e0612f8f327a9a594e8f98414342dfb379aaa0a90b56f5924373c9d9501d002d3ee512168652c6021a76bcac4285b61eaeb4d8ffae10')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/eslint/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
    npm link $dep;
  done
}
