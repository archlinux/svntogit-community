# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=esmska
pkgver=1.8
pkgrel=1
pkgdesc="Program for sending SMS over the Internet"
arch=('any')
url="http://esmska.googlecode.com/"
license=('AGPL' 'custom')
depends=('java-runtime' 'desktop-file-utils')
makedepends=('apache-ant')
install=$pkgname.install
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/kparal/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('9a92150dfb8cbcccd8ef8e875ffbd13a61cabfee5b3f0af29c094adae35aea71')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ant clean jar
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver/dist

  install -d "${pkgdir}"/usr/share/java/$pkgname
  cp -a * "${pkgdir}"/usr/share/java/$pkgname

#.desktop + icon files
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/resources/$pkgname.desktop \
    "${pkgdir}"/usr/share/applications/$pkgname.desktop
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/include/icons/$pkgname.png \
    "${pkgdir}"/usr/share/pixmaps/$pkgname.png

#executable file
  install -d "${pkgdir}"/usr/bin
  ln -s /usr/share/java/$pkgname/$pkgname.sh \
    "${pkgdir}"/usr/bin/$pkgname

#licenses
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/include/license/license.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 "${srcdir}"/$pkgname-$pkgver/include/license/gnu-agpl.txt \
    "${pkgdir}"/usr/share/licenses/$pkgname/AGPL

#removing unneeded
  rm "${pkgdir}"/usr/share/java/$pkgname/$pkgname.exe
}
