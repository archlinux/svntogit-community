# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Kyle <kyle@gmx.ca>

pkgname=espeakup
pkgver=0.90
pkgrel=1
pkgdesc="A light weight connector for espeak-ng and speakup"
arch=('x86_64')
url="https://github.com/linux-speakup/espeakup"
license=('GPL3')
depends=('glibc' 'espeak-ng')
makedepends=('alsa-lib' 'meson' 'ruby-ronn-ng')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linux-speakup/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('0731835a04063c5eac8ed0ed37cd92cc5d2dd1c58cbd94e158b7dfbb7bc0e2f84a6484fffd9009bf2920652a36fb737044b2e1fb67fb1679ee7150855bc9a15e')
b2sums=('f91a70bad0d985d313852b9e5f72dea4330ab23050ca6904ae5895d513088031f34d878cd64d97dcf2cf02dddc3e9fdabd0826cac98634829019ed3267e27bd8')

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

package() {
  depends+=('libasound.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
}

