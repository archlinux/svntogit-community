# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mathieu.clabaut <mathieu.clabaut@gmail.com>
# Contributor: Buce <dmbuce@gmail.com>
# Contributor: Danie Roux <accounts@danieroux.com>

pkgname=etckeeper
_tag='e7fbd70e5db8e3c526e028a632406d85c2f0e37f' # git rev-parse ${pkgver}
pkgver=1.18.12
pkgrel=1
pkgdesc='Collection of tools to let /etc be stored in a git, hg or bzr repository'
arch=(any)
url='https://etckeeper.branchable.com/'
license=(GPL2)
depends=(git diffutils inetutils sed)
makedepends=(python2 mercurial bzr)
optdepends=('mercurial: use mercurial for version control'
            'bzr: use bazaar for version control'
            'which: etckeeper vcs <subcommand> support')
backup=('etc/etckeeper/etckeeper.conf')
source=("git+https://git.kitenet.net/git/etckeeper.git#tag=${_tag}?signed")
sha256sums=('SKIP')
validpgpkeys=('E85A5F63B31D24C1EBF0D81CC910D9222512E3C7') # Joey Hess <joeyh@joeyh.name>

build() {
	cd ${pkgname}

	# Fix python name
	sed -i '/^PYTHON=/c PYTHON=python2' Makefile
	sed -i '1s/python/python2/' zypper-etckeeper.py

	# We want pacman :D
	sed -i -e "/^LOWLEVEL_PACKAGE_MANAGER/c LOWLEVEL_PACKAGE_MANAGER=pacman" \
		-e "/^HIGHLEVEL_PACKAGE_MANAGER/c HIGHLEVEL_PACKAGE_MANAGER=pacman" etckeeper.conf
}

package() {
	cd ${pkgname}
	make DESTDIR="${pkgdir}" systemddir=/usr/lib/systemd/system install
}
