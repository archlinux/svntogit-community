# $Id: PKGBUILD,v 1.4 2009/03/29 20:05:05 BaSh Exp $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Adam 'battlemidget' Stokes <adam.stokes@gmail.com>

pkgname=eterm
pkgver=0.9.5
pkgrel=3
pkgdesc="A vt102 terminal emulator intended as a replacement for xterm."
arch=('i686' 'x86_64')
url="http://www.eterm.org/"
depends=('freetype2>=2.3.5' 'libast>=0.7' 'libxmu' 'libxres')
license=('custom')
md5sums=('87220a61f763d111a4f5fc88ef9e50f1')
source=(http://www.eterm.org/download/Eterm-$pkgver.tar.gz)
options=('!libtool')

build() {
  cd "$srcdir/Eterm-$pkgver"
  CONF=""
  [ "${CARCH}" = "x86_64" ] && CONF="--disable-mmx"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --enable-trans --enable-utmp \
    --enable-xim --with-theme-update \
    --with-backspace=bs --without-terminfo \
    --enable-multi-charset ${CONF}
  make || return 1
  make DESTDIR="$pkgdir" install

  install -D -m644 "$srcdir/Eterm-${pkgver}/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
