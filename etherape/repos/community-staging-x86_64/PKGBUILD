# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=etherape
pkgver=0.9.19
pkgrel=1
pkgdesc='Graphical network monitor for various OSI layers and protocols'
url='http://etherape.sourceforge.net/'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'glib2' 'libpcap' 'popt' 'goocanvas' 'gtk3')
makedepends=('yelp-tools')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3819c36a6ea5b1ec6fc675563ab0264d180824343344bf4f2da834db39547b721c1565b6edb882d55ce4e0ca93e4b34e3a544996a988584aaf7fe8b8d5ca265e')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/goocanvas-2.0/goocanvas-3.0/' configure.ac
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
