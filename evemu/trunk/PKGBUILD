# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Nicolas Quiénot < niQo at aur >
# Contributor: Damián Nohales <damiannohales at gmail.com>
 
pkgname=evemu
pkgver=2.4.0
pkgrel=1
pkgdesc='Tools and bindings for kernel input event device emulation and data capture and replay'
arch=(i686 x86_64)
url='http://www.freedesktop.org/wiki/Evemu'
license=(GPL3)
depends=(libevdev python2)
source=(http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=(0A75E35E0FAEE97EC769103E2F2670AC164DB36F) # Benjamin Tissoires <benjamin.tissoires@gmail.com>
sha1sums=('3df372e8655a3eb939b0eb08ab21fe38c1498471'
          'SKIP')
 
build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  # Tests have to be run as 'root'
  # make check
}
 
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
