# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: kpcyrd <kpcyrd@archlinux.org>
# Contributor: Lucas Raab <tuftedocelot@fastmail.fm>

pkgname=exa
pkgver=0.10.0
pkgrel=2
pkgdesc='ls replacement'
arch=(x86_64)
url='https://the.exa.website/'
license=(MIT)
depends=(libgit2)
makedepends=(cargo git pandoc)
source=("$pkgname-$pkgver.tar.gz::https://github.com/ogham/exa/archive/v$pkgver.tar.gz")
b2sums=('4633de80370872d0bece036f313a9c9fdef27ffa4676e12b741e60367ec074d0c5b9ba2a811d12df72c0dbb8304bfe08aa9fe3a791ec6aaa7ba6cca1cb7af351')

build() {
  cd $pkgname-$pkgver
  cargo build --release
  pandoc --standalone -f markdown -t man man/exa.1.md > exa.1
  pandoc --standalone -f markdown -t man man/exa_colors.5.md > exa_colors.5
}

package() {
  cd $pkgname-$pkgver
  cargo install --path . --root "$pkgdir"/usr
  rm -f "$pkgdir"/usr/.crates*
  install -Dm644 completions/completions.bash \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 completions/completions.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm644 completions/completions.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 LICEN?E \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 exa.1 "$pkgdir/usr/share/man/man1/exa.1"
  install -Dm644 exa_colors.5 "$pkgdir/usr/share/man/man5/exa_colors.5"
}
