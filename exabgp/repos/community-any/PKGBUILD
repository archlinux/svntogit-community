# $Id$ 
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=exabgp
pkgver=2.0.4
pkgrel=1
pkgdesc='BPG route injector'
arch=('any')
license=('custom')
url='http://code.google.com/p/exabgp/'
depends=('python2' 'start-stop-daemon')
backup=('etc/exabgp.conf' 'etc/conf.d/exabgp')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tgz"
        'exabgp.rc'
        'exabgp.conf')
md5sums=('71b4161e01224f0b37053845cd084650'
         'bd0e19f0ce5f3b85bb7188e7e7d94047'
         '38a35e662074e2c8a7858ca2ada36c5c')

package() {
  # install initscripts
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  
  # install empty config file
  install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"

  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --install-data=/

  # install license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # move data in a more private directory
  mv "$pkgdir/etc/$pkgname" "$pkgdir/usr/share/$pkgname"
}

# vim:set ts=2 sw=2 ft=sh et:
