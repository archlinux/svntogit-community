# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=exabgp
pkgver=3.0.4
pkgrel=1
pkgdesc='BPG route injector'
arch=('any')
license=('custom')
url='http://code.google.com/p/exabgp/'
depends=('python2')
backup=('etc/exabgp.conf' 'etc/conf.d/exabgp')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tgz"
        'exabgp.service'
        'exabgp.rc'
        'exabgp.conf')
sha1sums=('84c9920accdf1864d1a7781fc0f3ebb5d3ff2143'
          '2f12eaf9be33e4a5b19ad96aeba97d2b4b67d58b'
          '4e796ad78e58640ab7a3eca6702b750125df9407'
          '138acaf511cdd95c3b1734d0c0913a1ca4c8207c')

build() {
  # arch python2
  find $pkgname-$pkgver -type f -exec \
    sed -i '1s,^#!/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \; -print
}

package() {
  pushd $pkgname-$pkgver
  # upstream installer
  python2 setup.py install --root="$pkgdir" --install-data=/
  # install license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  popd
  # install initscripts
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # install systemd service file
  install -D -m 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # install empty config file
  install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"
  # move data in a more private directory
  mv "$pkgdir/etc/$pkgname" "$pkgdir/usr/share/$pkgname"
}

# vim:set ts=2 sw=2 et:
