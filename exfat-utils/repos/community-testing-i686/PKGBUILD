# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alex <mail.avatar@gmail.com>

pkgname=exfat-utils
pkgver=1.2.7
pkgrel=1
pkgdesc="Utilities for exFAT file system"
arch=('i686' 'x86_64')
url="https://github.com/relan/exfat"
license=('GPL2')
depends=('glibc' 'fuse2')
conflicts=('fuse-exfat')
provides=('fuse-exfat')
replaces=('fuse-exfat')
source=("$pkgname-$pkgver.zip::https://github.com/relan/exfat/archive/v$pkgver.zip")
md5sums=('b32dc1c1629e6ce9b76fabd79a1d6589')

build() {
  cd exfat-$pkgver

  autoreconf -i
  ./configure --prefix=/usr \
              --sbindir=/usr/bin

  make CCFLAGS="${CFLAGS} -std=c99" LINKFLAGS="${LDFLAGS}"
}

package() {
  cd exfat-$pkgver

  make DESTDIR="${pkgdir}" install

  # Install man pages
  install -d "${pkgdir}/usr/share/man/man8/"
  install -m444 dump/dumpexfat.8 fsck/exfatfsck.8 mkfs/mkexfatfs.8 label/exfatlabel.8 fuse/mount.exfat-fuse.8 \
      "${pkgdir}/usr/share/man/man8/"
}
