# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=exim
pkgver=4.80
pkgrel=2
pkgdesc="Message Transfer Agent"
arch=('x86_64' 'i686')
url='http://www.exim.org/'
license=('GPL')
backup=(etc/mail/aliases etc/mail/exim.conf etc/logrotate.d/exim etc/conf.d/exim)
install=exim.install
depends=('db' 'pcre' 'pam' 'openssl' 'libldap')
provides=('smtp-server')
conflicts=('smtp-server')
options=('!makeflags')
source=("http://mirror.switch.ch/ftp/mirror/exim/exim/exim4/exim-$pkgver.tar.bz2"
        aliases
        exim
        exim.logrotate
        exim.conf.d
        exim.Makefile
        exim-submission@.service
        exim.service
        exim@.service
        exim.socket
        exim-submission.socket)
sha256sums=('787b6defd37fa75311737bcfc42e9e2b2cc62c5d027eed35bb7d800b2d9a0984'
            '932c9149b6809c70e94c1256e28325d197bbf80d27322793e217d4b692d49c5a'
            '48457622b22de9721efe9f143a88e5f5ce833b792d2e757237b6cdb74a1708e6'
            '7f1408f9c5d905968e665941f5c5efcf9da53e7a0bbef6c66220343bc2ae994b'
            '0209c701f8696a9628d43c1684105eadd35d1caba199b94e3a54a4d26cecff01'
            '7d1e062c907a06293eee40a3139f70c74a055374b234fb8c431ea68362bdda55'
            '32def7387aaa040b994c0d399c6053dc42d9da4c8c8f0a34d33da653be27e324'
            '99aaf3c960396fc08ec918fdca1cf73850960f6c519982d1a653f2e673754445'
            '0436411932d6b6268db54a84e4ffc209bde0fa7567442d8e3d138557eeccbad4'
            '3e3d8b6be2741d2587a496196c08b3f2ffa05b5803b2bf9fb49359cef3a98d26'
            'd3bb58f0fbeaaa33c812a823708664bbcd828da7d24e2a098f84a15aee443fee')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir/$pkgname.Makefile" Local/Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0644 $srcdir/exim.logrotate "${pkgdir}/etc/logrotate.d/exim"
  install -Dm0644 $srcdir/exim.conf.d "${pkgdir}/etc/conf.d/exim"
  install -Dm0644 doc/exim.8 "${pkgdir}/usr/share/man/man8/exim.8"
  install -Dm0755 $srcdir/exim "${pkgdir}/etc/rc.d/exim"

  mkdir -p "${pkgdir}/var/spool/exim/db" "${pkgdir}/etc/mail" \
    "${pkgdir}/var/log/exim" "${pkgdir}/usr"/{lib,sbin}

  chmod 770 "${pkgdir}/var/spool/exim" "${pkgdir}/var/spool/exim/db" "${pkgdir}/var/log/exim"

  cd build-Linux-*
  for i in exicyclog exim_checkaccess exim_dumpdb exim_lock exim_tidydb exipick exiqsumm exigrep \
    exim_dbmbuild exim exim_fixdb eximstats exinext exiqgrep exiwhat; do
    install -m0755 "$i" "$pkgdir/usr/sbin"
  done

  cd "$srcdir/exim-$pkgver/src"
  sed -e "s|/etc/aliases|/etc/mail/aliases|g" -e "s|SYSTEM_ALIASES_FILE|/etc/mail/aliases|g" \
    configure.default > "$pkgdir/etc/mail/exim.conf"

  cp "$srcdir/aliases" "$pkgdir/etc/mail"

  cd "$pkgdir/usr/sbin"
  for i in mailq rmail rsmtp runq sendmail; do
    ln -s exim "$i"
  done
  # fhs compliancy
  ln -s ../sbin/exim ../lib/sendmail

  install -Dm0644 "$srcdir/exim-submission@.service" \
    "${pkgdir}/usr/lib/systemd/system/exim-submission@.service"
  install -Dm0644 "$srcdir/exim.service" "${pkgdir}/usr/lib/systemd/system/exim.service"
  install -Dm0644 "$srcdir/exim@.service" "${pkgdir}/usr/lib/systemd/system/exim@.service"
  install -Dm0644 "$srcdir/exim.socket" "${pkgdir}/usr/lib/systemd/system/exim.socket"
  install -Dm0644 "$srcdir/exim-submission.socket" \
    "${pkgdir}/usr/lib/systemd/system/exim-submission.socket"
}
