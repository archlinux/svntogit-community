# $Id$
# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=expac
pkgver=2
pkgrel=1
pkgdesc="pacman database extraction utility"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/expac"
license=('GPL')
depends=('pacman')
makedepends=('perl')
source=("http://code.falconindy.com/archive/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('41a65f51009e44689bd5320f5c3aa7d4'
         'SKIP')

build() {
  make -C "$pkgname-$pkgver"

  sed '/\*\//q' "$pkgname-$pkgver"/expac.c >LICENSE
}

package() {
  make -C "$pkgname-$pkgver" PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
