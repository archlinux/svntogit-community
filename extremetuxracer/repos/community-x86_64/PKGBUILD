# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Eric Belanger <eric.archlinux.org>
# Contributor: Fabio Scotoni <CCuleX.gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=extremetuxracer
pkgver=0.4
pkgrel=6
pkgdesc="Downhill racing game starring Tux"
arch=('x86_64' 'i686')
url="http://www.extremetuxracer.com"
license=('GPL')
depends=('gcc-libs' 'sdl_mixer' 'tcl>=8.5.0' 'libpng>=1.4.0' 'freetype2' 'glu')
makedepends=('mesa' 'libxi' 'libxmu')
conflicts=('ppracer' 'etracer')
replaces=('ppracer' 'etracer')
source=("http://downloads.sourceforge.net/extremetuxracer/extremetuxracer-$pkgver.tar.gz"
       "$pkgname.png"
       "$pkgname.desktop"
       "libpng15.patch")
sha256sums=('5e4057f90e6854e774f251f189b0e676cfd7e2f15779c75544c9ac5b8e30273d'
            'ec1426171fc80b07b702e373bf9528244a3b224e887ab24b52f90740691e92fb'
            '349448e8be522bbbe9fd896e875a4add7e02c35d0fafa46c5b10582562da3ca8'
            '2b693eaf421d56887615fc02d40f742e9b8bf817ae36be1f1f9e33a52719eea8')

build() {
  cd "$srcdir/extremetuxracer-$pkgver"

  patch -Np1 -i "${srcdir}/libpng15.patch"
  sed -i -e 's/libpng12/libpng15/g' configure
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/extremetuxracer-$pkgver"
  make DESTDIR="$pkgdir" install

 # Install icon and desktop file
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
