# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Eric Belanger <eric.archlinux.org>
# Contributor: Fabio Scotoni <CCuleX.gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=extremetuxracer
pkgver=0.7.5
pkgrel=1
pkgdesc="Downhill racing game starring Tux"
arch=('x86_64')
url="https://sourceforge.net/projects/extremetuxracer/"
license=('GPL')
depends=('sfml' 'glu' 'freetype2')
makedepends=('csfml')
conflicts=('ppracer' 'etracer')
replaces=('ppracer' 'etracer')
source=("https://downloads.sourceforge.net/${pkgname}/etr-${pkgver}.tar.xz")
sha256sums=('4fd39c52a426ba22deb26bcceb807d7eebe7b7d7df578cfd03f10066c218c6d3')

build() {
  cd "${srcdir}/etr-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/etr-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/etr-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
