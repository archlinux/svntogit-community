# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=fabric
pkgver=2.5.0
pkgrel=5
pkgdesc="Python library and command-line tool designed to streamline deploying applications or performing system administration tasks via the SSH protocol"
url="https://www.fabfile.org/"
license=('BSD')
arch=('any')
depends=('python-setuptools' 'python-paramiko' 'python-invoke')
optdepends=('python-patchwork: Common deployment/sysadmin operations')
source=("https://github.com/fabric/fabric/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9fb3ceaeca449a39a5f78176b79f296927c17ef1938dc345d6c8954c9ed1547e288a68e5fbfb0be6148bc78940b4efed373257a3b7cba0354b3dad633ccb552f')

build() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py build
}

package() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}"

  install -Dm644 README.rst "${pkgdir}"/usr/share/doc/${pkgname}/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
