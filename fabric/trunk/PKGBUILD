# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=fabric
pkgver=2.2.0
pkgrel=2
pkgdesc="Python library and command-line tool designed to streamline deploying applications or performing system administration tasks via the SSH protocol"
url="http://www.fabfile.org/"
license=('BSD')
arch=('any')
depends=('python-setuptools' 'python-paramiko' 'python-invoke')
optdepends=('python-patchwork: Common deployment/sysadmin operations')
source=("https://github.com/fabric/fabric/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6b23fce63ad82224e59ec63d34352bbdd3ff0a46939b027c5c9733a68015e2fc249138ba2e2d53d795c35fc8a54ea0901d82cfaf9fa0c9f6ac3ee32dab90162f')

build() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py build
}

package() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}"

  install -Dm644 README.rst "${pkgdir}"/usr/share/doc/${pkgname}/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
