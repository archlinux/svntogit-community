# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=fatrat
pkgver=1.1.3
pkgrel=5
pkgdesc="QT4 based download manager with support for HTTP, FTP, SFTP, BitTorrent, rapidshare and more"
arch=('i686' 'x86_64')
url="http://fatrat.dolezel.info/"
license=('GPL')
depends=('gloox' 'curl' 'qt' 'libtorrent-rasterbar')
optdepends=('geoip: GeoIP support')
makedepends=('asio' 'cmake')
source=(http://www.dolezel.info/download/data/fatrat/fatrat-$pkgver.tar.gz)
md5sums=('6d4a00cdd0b59a05b1521184508d4637')

build() {
  cd $pkgname-$pkgver

  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  cmake \
    -DWITH_EVERYTHING=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
