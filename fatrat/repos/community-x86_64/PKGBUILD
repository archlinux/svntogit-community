# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=fatrat
pkgver=1.2.0_beta1
pkgrel=2
pkgdesc="QT4 based download manager with support for HTTP, FTP, SFTP, BitTorrent, rapidshare and more"
arch=('i686' 'x86_64')
url="http://fatrat.dolezel.info/"
license=('GPL')
depends=('gloox' 'qt' 'libtorrent-rasterbar' 'pion-net')
optdepends=('geoip: GeoIP support')
makedepends=('boost' 'cmake' 'java-environment')
source=(http://www.dolezel.info/download/data/fatrat/fatrat-$pkgver.tar.gz)
md5sums=('7ce4d5e6318361f36ba5ac86a5755da9')

build() {
  cd $pkgname-$pkgver

  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"

  cmake \
    -DWITH_EVERYTHING=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
