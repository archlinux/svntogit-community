# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=fatrat
pkgver=1.2.0_beta2
pkgrel=15
pkgdesc="Qt4 based download manager with support for HTTP, FTP, SFTP, BitTorrent, rapidshare and more"
arch=('x86_64')
url="http://fatrat.dolezel.info/"
license=('GPL')
depends=('gloox' 'libtorrent-rasterbar' 'pion' 'qtwebkit' 'curl')
optdepends=('geoip: GeoIP support' 'java-runtime: Java plugins')
makedepends=('boost' 'cmake' 'java-environment')
source=(http://www.dolezel.info/download/data/fatrat/fatrat-$pkgver.tar.xz \
        pion-5.0.diff)
md5sums=('ad823d2a81f0583316ece815b928c71b'
         'd7d398e7b6415c0a81eb4960d8271997')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix help generation with qt4
  sed -s 's|qcollectiongenerator|qcollectiongenerator-qt4|' -i CMakeLists.txt
  # fix for the new pion
  patch -Np1 < "$srcdir/pion-5.0.diff" || true
  # workaround the moc bug (QTBUG-22829) with new boost
  # by disabling all boost headers during the MOC execution
  find . -type f -exec sed -i 's|#include <boost/.*|#ifndef Q_MOC_RUN\n&\n#endif|' '{}' ';'
  find . -type f -exec sed -i 's|#include <pion/.*|#ifndef Q_MOC_RUN\n&\n#endif|' '{}' ';'
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export JAVA_HOME=/usr/lib/jvm/default

  cmake \
    -DWITH_EVERYTHING=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
