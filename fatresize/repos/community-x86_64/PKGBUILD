# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: ValHue <vhuelamo at gmail dot com>
# Contributor: Alexander Blinne <alexander[at]blinne[dot]net>

pkgname=fatresize
pkgver=1.0.4
pkgrel=3
pkgdesc='A utility to resize FAT filesystems using libparted'
url='https://sourceforge.net/projects/fatresize/'
arch=(x86_64)
license=(GPL3)
depends=(parted)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ya-mouse/fatresize/archive/v$pkgver.tar.gz")
sha256sums=('7f3ab7a9ac92335f66d239419860f674359449d49cf67ba1af7a8b686303612b')

build() {
  cd $pkgname-$pkgver
  autoreconf -fisv
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/
}
