# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=fbgetty
pkgver=0.1.698
pkgrel=4
license=("GPL")
pkgdesc="fbgetty is a console getty like mingetty, which supports framebuffers"
arch=('i686' 'x86_64')
url="http://projects.meuh.org/fbgetty"
source=('http://projects.meuh.org/fbgetty/downloads/fbgetty-'$pkgver'.tar.gz')
md5sums=('1705bc0f8f1e03fe50d324ba84ac4e56')

build()
{
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --mandir=/usr/share/man

  patch src/options.c <<EOF
33a34
> #include <stddef.h>
EOF

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  mv ${pkgdir}/usr/info ${pkgdir}/usr/share/
}
