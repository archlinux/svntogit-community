# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contriutor: Eric Belanger <eric@archlinux.org>
# Contributor: Jo Christian Bergskås <jcberg@gmail.com>

pkgname=fceux
pkgver=2.2.3
pkgrel=4
url='http://www.fceux.com/'
pkgdesc='Fast and ultra-compatible NES/Famicom emulator'
arch=('x86_64')
license=('GPL')
depends=('sdl' 'gtk2' 'gd')
makedepends=('scons' 'mesa-libgl' 'glu')
source=("https://downloads.sourceforge.net/project/fceultra/Source%20Code/$pkgver%20src/$pkgname-$pkgver.src.tar.gz" scons-python3.patch)
sha256sums=('4be6dda9a347f941809a3c4a90d21815b502384adfdd596adaa7b2daf088823e'
            'ba1828375e00b4a2076311f5ee1802a42d52eb1ef93b358a45459d475c55e6b9')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../scons-python3.patch
}

build() {
  scons -C "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  scons install -i --prefix="$pkgdir/usr"

  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r documentation/* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 changelog.txt "$pkgdir/usr/share/doc/$pkgname/"

  # desktop shortcut
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  rm -f "$pkgdir/usr/share/$pkgname/"*.dll
}

# vim:set ts=2 sw=2 et:
