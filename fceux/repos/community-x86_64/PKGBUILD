# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contriutor: Eric Belanger <eric@archlinux.org>
# Contributor: Jo Christian Bergskås <jcberg@gmail.com>

pkgname=fceux
pkgver=2.6.4
pkgrel=2
url='https://github.com/TASEmulators/fceux'
pkgdesc='Fast and ultra-compatible NES/Famicom emulator'
arch=(x86_64)
license=(GPL)
depends=(gd lua minizip qt5-base sdl2)
makedepends=(cmake git glu mesa-libgl ninja setconf scons)
optdepends=('ffmpeg: for recording')
source=("git+https://github.com/TASVideos/fceux#commit=2b8c61802029721229a26592e4578f92efe814fb") # tag: fceux-2.6.4
b2sums=(SKIP)

prepare() {
  cd $pkgname
  sed -i 's/-interim git//g' src/version.h
  setconf scripts/genGitHdr.sh GIT_URL "'""${source:4:34}""'"
  setconf scripts/genGitHdr.sh GIT_REV "${source#*=}"
}

build() {
  cmake -B build \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -fPIC -w" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -G Ninja \
    -S $pkgname
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r $pkgname/documentation/* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 $pkgname/changelog.txt "$pkgdir/usr/share/doc/$pkgname/"
}
