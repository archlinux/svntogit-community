# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Jo Christian Bergskås <jcberg@gmail.com>
pkgname=fceux
pkgver=2.1.5
pkgrel=3
arch=('x86_64' 'i686')
url="http://fceux.com/"
pkgdesc="Fast and ultra-compatible NES/Famicom emulator"
license=('GPL')
depends=('sdl' 'gtk2' 'gd' 'desktop-file-utils')
makedepends=('scons' 'mesa' 'lua')
optdepends=('xchm: for viewing the manual')
provides=('fceu' 'gfceux')
replaces=('fceu' 'gfceux')
install=fceux.install
source=("http://downloads.sourceforge.net/fceultra/$pkgname-$pkgver.src.tar.bz2"
        "fceux.desktop"
        "fceux.png")
md5sums=('e8b20e62bbbb061b1a59d51b47c827bd'
         '3c6a259d88825ff1b9387d191a6c970e'
         '3fe753731ac46b5fee528b1f73abd3e3')

build() {
  cd "$srcdir/fceu${pkgver}"

  sed -i "s|/usr/local|$pkgdir/usr|" SConstruct
  scons OPENGL=0 install
}

package() {
  cd "$srcdir/fceu${pkgver}"

  install -Dm644 bin/$pkgname.chm \
    "$pkgdir/usr/share/doc/$pkgname/$pkgname.chm"
  install -Dm644 $srcdir/fceux.desktop \
    "$pkgdir/usr/share/applications/fceux.desktop"
  install -Dm644 $srcdir/fceux.png \
    "$pkgdir/usr/share/pixmaps/fceux.png"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
