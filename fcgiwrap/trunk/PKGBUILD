# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Aaron Bull Schaefer <aaron@elasticdog.com>
# Contributor: Ron Huang <ronhuang+aur at gmail dot com>

pkgname=fcgiwrap
pkgver=1.1.0
pkgrel=4
pkgdesc='A simple server for running CGI applications over FastCGI.'
arch=('x86_64')
url='http://nginx.localdomain.pl/wiki/FcgiWrap'
license=('MIT')
depends=('fcgi' 'systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gnosek/fcgiwrap/archive/${pkgver}.tar.gz"
        'LICENSE')
md5sums=('d14f56bda6758a6e02aa7b3fb125cbce'
         '5aee62c27b4308f25ab32f05da387366')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  autoreconf --install
  # Here --sbindir is implicitly prefixed by /usr
  ./configure --prefix=/usr --mandir=/share/man --sbindir=/bin --with-systemd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
