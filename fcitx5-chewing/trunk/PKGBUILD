# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-chewing
pkgver=5.0.12
pkgrel=1
pkgdesc="Chewing Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-chewing"
license=('GPL')
depends=('fcitx5' 'libchewing')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/fcitx5-chewing/fcitx5-chewing-$pkgver.tar.xz"{,.sig})
sha512sums=('562a44cf9742594d2215d1758ba4a5e86753da645ef92c3bb6009c6248cab8f2e250e3ef959ae4ef0d11f55a14f0ea4a5c7888693a502678b4b28267b49bc338'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
