# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-chinese-addons
pkgver=0.0.0.20201102
_commit=85181edc5ba8362b43aa6cbbcb48ca41660dd300
_pytablever=20121124
pkgrel=1
pkgdesc="Addons related to Chinese, including IME previous bundled inside fcitx4"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-chinese-addons"
license=('GPL')
conflicts=('fcitx')
depends=('curl' 'fcitx5-qt' 'libime' 'opencc' 'qt5-webkit')
makedepends=('boost' 'extra-cmake-modules' 'fcitx5-lua' 'ninja')
optdepends=('fcitx5-lua: Lua and imeapi support from pinyin')
source=("https://github.com/fcitx/fcitx5-chinese-addons/archive/$_commit/fcitx5-chinese-addons-$_commit.tar.gz"
        https://download.fcitx-im.org/data/py_table-$_pytablever.tar.gz
        https://download.fcitx-im.org/data/py_stroke-$_pytablever.tar.gz)
noextract=(py_table-$_pytablever.tar.gz)
sha512sums=('f2a0380915102ab76c24f97b62e8a5fc719369102eeea4aadb9e28e551e44a0e9fb82afa35f67fb5267f50cc1761ede9458b15d68d05cbdba0b0e34b9acb1431'
            '0fa72ce6820a316e9729bc0d7a08af5ca3468134de9ff1bf0c559a398846a7bcf7313a3609d943e6bab637d930cf45ad9acfe25a8d3df078a774b071a7296486'
            'd80ff9a3549e07c7a6935e686785f9a076c58f1a782a832ae28ee65e2a213d67f089f450ce09bed87ec2a141c32b9c1fcb83c221ddcab436858fa9009f161fb3')

prepare() {
  cd $pkgname-$_commit/modules/pinyinhelper
  ln -s "$srcdir"/py_table-$_pytablever.tar.gz
  ln -s "$srcdir"/py_stroke-$_pytablever.tar.gz
}

build() {
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$_commit
  ninja test
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
