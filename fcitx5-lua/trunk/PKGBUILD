# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-lua
pkgver=0.0.0.20200610
_commit=8ad9929af4ac7eada792a2f4a4c42f0be078c6ad
pkgrel=1
pkgdesc="Lua support for Fcitx 5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-lua"
license=('GPL')
depends=('fcitx5' 'lua53')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://github.com/fcitx/fcitx5-lua/archive/$_commit/fcitx5-lua-$_commit.tar.gz")
sha512sums=('77cfc8084b00a8e306bc52441d43c36fb2bd5456b70bb2133e3752f3fc6f8ae7fadd32dbf4e4c9ccb9b55b10db1708be01c2ecaa60332e22d58966193b9b774a')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
