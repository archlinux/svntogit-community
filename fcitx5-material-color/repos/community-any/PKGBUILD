# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-material-color
pkgver=0.0.0.20201212
_commit=c5f240591af52a041ff0fcde6fe245761c926f61
pkgrel=1
pkgdesc="Material color theme for fcitx5"
arch=('any')
url="https://github.com/hosxy/Fcitx5-Material-Color"
license=('GPL')
source=("https://github.com/hosxy/Fcitx5-Material-Color/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('b82bae08030b4953db7bab84fe5b4542d5153e23d3d70c94e97fcc8be43bbaa2acdc5552c95576d9e349f9cea2c9c74d68aaf2081428a5ad8c6e003f9f84bc2f')

package() {
  cd Fcitx5-Material-Color-$_commit
  install -Dm644 arrow.png radio.png -t "$pkgdir"/usr/share/$pkgname/
  for _variant in black blue brown deepPurple indigo orange pink red sakuraPink teal; do
    _variant_name=Material-Color-${_variant^}
    install -Dm644 panel-$_variant.png -t "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    ln -s ../../../$pkgname/arrow.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    ln -s ../../../$pkgname/radio.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    install -Dm644 theme-$_variant.conf "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/theme.conf
    sed -i "s/^Name=.*/Name=$_variant_name/" "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/theme.conf
  done
}
