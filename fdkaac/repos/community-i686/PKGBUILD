# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: PelPix <kylebloss@pelpix.info>

pkgname=fdkaac
pkgver=0.5.3
pkgrel=1
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=('i686' 'x86_64')
url="https://github.com/nu774/${pkgname}"
license=('custom')
depends=('libfdk-aac')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('285a07ff94565bb36dfa95513ec6a80fba9567100e588b8515e62585792282b4')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -if
  ./configure --prefix='/usr' --disable-rpath

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
