# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: PelPix <kylebloss@pelpix.info>

pkgname=fdkaac
pkgver=0.6.1
pkgrel=1
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=('i686' 'x86_64')
url="https://github.com/nu774/${pkgname}"
license=('custom')
depends=('libfdk-aac')
source=("$pkgname-$pkgver.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('fcc923817345a380e3716086dcc40b5e807343a23b3cf4c0717333fb05350a2c')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -if
  ./configure --prefix='/usr' --disable-rpath

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
