# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>

pkgname=feathernotes
pkgver=1.0.0
pkgrel=1
pkgdesc='Lightweight Qt hierarchical notes-manager'
arch=('x86_64')
url='https://github.com/tsujan/FeatherNotes'
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'qt6-svg')
makedepends=('cmake' 'ninja' 'qt6-tools')
source=("https://github.com/tsujan/FeatherNotes/releases/download/V$pkgver/FeatherNotes-$pkgver.tar.xz"{,.asc})
sha256sums=('6a3a19dbf65be99f9f9e5f8cf56b635a563230f0962d5c96607dc3e051cb021d'
            'SKIP')
validpgpkeys=('19DFDF3A579BD509DBB572D8BE793007AD22DF7E')

build() {
  cmake -S FeatherNotes-$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr' -DENABLE_QT5=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
