# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>

pkgname=featherpad
pkgver=0.18.0
pkgrel=1
pkgdesc='Lightweight Qt plain text editor'
arch=(x86_64)
url='https://github.com/tsujan/FeatherPad'
license=(GPL3)
depends=(hunspell qt5-svg qt5-translations qt5-x11extras hicolor-icon-theme)
makedepends=(qt5-tools)
source=("https://github.com/tsujan/FeatherPad/releases/download/V$pkgver/FeatherPad-$pkgver.tar.xz"{,.asc}
        "featherpad.appdata.xml")
sha256sums=('f95c5494451890eefa7177022b065648b0633d5096715ae7f47c2cc7d784aec8'
            'SKIP'
            'd67bc310f4c31257b795beabbce2d0887640088b0fe3040307906da0d9f760d9')
validpgpkeys=('19DFDF3A579BD509DBB572D8BE793007AD22DF7E')

build() {
  cd FeatherPad-$pkgver
  qmake PREFIX=/usr
  make
}

package() {
  cd FeatherPad-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
