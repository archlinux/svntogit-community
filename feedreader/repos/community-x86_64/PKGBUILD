# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrew O'Neill <andrew at meanjollies dot com>
# Contributor: poisonby <poisonby@tutanota.com>
# Contributor: Jason Scurtu (scujas) <jscurtu@gmail.com>
# Contributor: Marcin Tydelski <marcin.tydelski@gmail.com>
# Contributor: Jan Lukas Gernert (JeanLuc) <https://launchpad.net/~eviltwin1>

pkgname=feedreader
_pkgname=FeedReader
pkgver=2.8.2
pkgrel=1
pkgdesc="Modern desktop application designed to complement existing web-based RSS accounts"
arch=(x86_64)
url="https://jangernert.github.io/FeedReader/"
license=(GPL3)
depends=(curl gnome-keyring gnome-online-accounts gumbo-parser libgee libpeas)
makedepends=(git gobject-introspection meson vala)
_commit=1d7c7f3814bbdeecc4d12e27058941397d160ced  # tags/v2.8.2
source=("git+https://github.com/jangernert/$_pkgname#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname

  # Don't use legacy path for AppStream metainfo file
  # https://github.com/jangernert/FeedReader/pull/822
  sed -i "s/install_dir: join_paths(SHARE_DIR, 'appdata')/install_dir: join_paths(SHARE_DIR, 'metainfo')/" data/meson.build
}

build() {
  arch-meson $_pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
