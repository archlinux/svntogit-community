# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-qqmusic
pkgver=0.3.3
pkgrel=2
pkgdesc="feeluown qqmusic plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-qqmusic"
license=('GPL3')
depends=('feeluown' 'python-marshmallow' 'python-requests')
makedepends=('python-setuptools' 'python-pip')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/fuo_qqmusic/fuo_qqmusic-$pkgver.tar.gz")
sha512sums=('a3a2986e96886ffa5ca416b76d0233144eea1b456216b059c64f1d2c67bd56e953449faf418eebd65fcccb509c02989c5cff43e89a0b55f38f408a4888870bb2')

build() {
  cd fuo_qqmusic-$pkgver
  python setup.py build
}

package() {
  cd fuo_qqmusic-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
