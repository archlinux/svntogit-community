# $Id$
# Maintainer: Alexander RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=festival-voices
pkgname=('festival-us' 'festival-english')
pkgver=2.4
pkgrel=1
arch=('any')
url='http://www.cstr.ed.ac.uk/projects/festival/'
license=('custom')
depends=('festival')
source=("http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_cmu_us_awb_cg.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_cmu_us_rms_cg.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_cmu_us_slt_cg.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_kallpc16k.tar.gz"
	"http://www.cstr.ed.ac.uk/downloads/festival/$pkgver/voices/festvox_rablpc16k.tar.gz"
	'LICENSE')
md5sums=('53d93d0ee910bde62d85ca261edd67e1'
         '9dca49338804f4081ebc2e65ac1b88fe'
         'd7f992fea3000e2aa2139d72103dd17f'
         '3869af78f473b616601cac3fa83cc14e'
         '34cb2478f5b8fa1ed02f5cbb496c1dcd'
         'eb7167e849fbd166699eef79e87a7e36')

package_festival-us() {
  pkgdesc='American Male/Female and Scottish English Male speaker'
  replaces=('festival-awb-arctic')

  install -d "$pkgdir/usr/share/festival/voices/"
  cp -rf festival/lib/voices/us "$pkgdir/usr/share/festival/voices/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/festival-us/LICENSE"
}

package_festival-english() {
  pkgdesc='British and American English Male speaker'
  replaces=('festival-kallpc16k' 'festival-rablpc16k')

  install -d "$pkgdir/usr/share/festival/voices/"
  cp -rf festival/lib/voices/english "$pkgdir/usr/share/festival/voices/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/festival-english/LICENSE"
}

# vim:set ts=2 sw=2 et:
