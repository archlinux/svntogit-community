# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ffmpeg2theora
pkgver=0.29
pkgrel=6
pkgdesc="Simple converter to create Ogg Theora files"
arch=('i686' 'x86_64')
url="http://www.v2v.cc/~j/ffmpeg2theora/"
license=('GPL')
depends=('ffmpeg')
makedepends=('scons' 'libkate')
source=(http://www.v2v.cc/~j/$pkgname/downloads/$pkgname-$pkgver.tar.bz2
        ffmpeg2theora-0.29-underlinking.patch)
sha256sums=('214110e2a5afdd8ff8e0be18152e893dbff5dabc1ae1d1124e64d9f93eae946d'
            '3b937c7e3a5afe327ee75e246ef606a747b8fb5880b43a864b3b9db4a245ef7f')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  echo "#define AVCODEC_MAX_AUDIO_FRAME_SIZE 192000" >> src/ffmpeg2theora.h
  patch -p0 -i ../ffmpeg2theora-0.29-underlinking.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  scons
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  scons install destdir="${pkgdir}" prefix="/usr" mandir="PREFIX/share/man"
}
