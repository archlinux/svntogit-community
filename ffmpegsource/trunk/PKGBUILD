# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffmpegsource
pkgver=2.17
pkgrel=9
pkgdesc="A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmpegsource/"
license=('MIT')
depends=('ffmpeg')
options=('!libtool')
source=("http://ffmpegsource.googlecode.com/files/ffms-${pkgver}-src.tar.bz2" 'autoconf.patch' 'ffmpeg.patch')
sha256sums=('991f7a77982823afdcf0beb8961deddaf7dd0aea949249e51fbf51cb3314643d'
            '77e5185f91249b8d032958abb142827a5349b06ff1b4fbd4c5c87ccea811273d'
            'adc08b58dec29b04be9d9eb2f12c2fa836565d363aafcfc3e785ae4692db6845')

build() {
  cd "${srcdir}"/ffms-${pkgver}-src

# Patch
  patch -Np1 -i "${srcdir}"/autoconf.patch
  patch -Np1 -i "${srcdir}"/ffmpeg.patch
  mv configure.in configure.ac

# Build
  ./autogen.sh --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd "${srcdir}"/ffms-${pkgver}-src

# Install
  make DESTDIR="${pkgdir}" install

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/ffmpegsource
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/ffmpegsource/LICENSE
}

# vim: ts=2 sw=2 et:
