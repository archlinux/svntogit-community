# $Id$$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffms2
pkgver=2.19.1
pkgrel=1
pkgdesc='A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access'
arch=('i686' 'x86_64')
url='https://github.com/FFMS/ffms2'
license=('GPL')
depends=('ffmpeg')
makedepends=('git')
provides=('vapoursynth-plugin-ffms2')
source=("https://github.com/FFMS/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('39c8a5f6c6735d10b3b0a3830f71ef0182b42e747cbceb6bf9b8dcc0532c99db')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --enable-shared --disable-static --enable-avresample
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/vapoursynth
  ln -s ../libffms2.so "${pkgdir}"/usr/lib/vapoursynth/
}

# vim: ts=2 sw=2 et:
