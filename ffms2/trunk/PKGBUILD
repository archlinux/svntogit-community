# $Id$$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffms2
pkgver=2.19.r81.047b479
pkgrel=1
pkgdesc='A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access'
arch=('i686' 'x86_64')
url='https://github.com/FFMS/ffms2'
license=('GPL')
depends=('ffmpeg')
makedepends=('git')
provides=('vapoursynth-plugin-ffms2')
source=("git+https://github.com/FFMS/ffms2.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}

  printf "%s" "$(git describe --tags | sed 's/-/.r/; s/-g/./')"
}

build() {
  cd ${pkgname}

  ./autogen.sh --prefix='/usr' --enable-shared --disable-static --enable-avresample
  make
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/vapoursynth
  ln -s ../libffms2.so "${pkgdir}"/usr/lib/vapoursynth/
}

# vim: ts=2 sw=2 et:
