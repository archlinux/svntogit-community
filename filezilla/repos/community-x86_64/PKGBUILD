# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=filezilla
pkgver=3.34.0
pkgrel=1
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('x86_64')
url="http://filezilla-project.org/"
license=('GPL')
depends=('dbus' 'xdg-utils' 'wxgtk3' 'libidn' 'hicolor-icon-theme' 'sqlite' 'gnutls' 'libfilezilla')
source=("https://dl3.cdn.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2")

prepare() {
  cd "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin \
    --with-wx-config=/usr/bin/wx-config-gtk3

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
md5sums=('de54afdd455bc368b35640ebf47d44e5')
