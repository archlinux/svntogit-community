# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=filezilla
pkgver=3.55.1
pkgrel=2
pkgdesc='Fast and reliable FTP, FTPS and SFTP client'
url='https://filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=(
  'dbus' 'xdg-utils' 'wxgtk3' 'libidn' 'hicolor-icon-theme' 'sqlite'
  'gnutls' 'libfilezilla'
  libfilezilla.so libdbus-1.so libgtk-3.so libgdk-3.so libnettle.so
)
source=(https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2)
sha256sums=('67f8d0e11f53fd23207d3cbfc942d03f3e23ee439ee48a14a2143c9950f776a5')
sha512sums=('07f1ccc596cac64aca0e82a97725f2ade26de78116441ba5aefaa8bd1396a483be88d9820820b9767e77686abbfd0eb6190684303e1f4d9ec45ef0b1f2cf0170')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin \
    --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
