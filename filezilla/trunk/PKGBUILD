# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=filezilla
pkgver=3.60.2
pkgrel=3
pkgdesc='Fast and reliable FTP, FTPS and SFTP client'
url='https://filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=(
  'dbus' 'xdg-utils' 'wxwidgets-gtk3' 'libidn' 'hicolor-icon-theme' 'sqlite'
  'gnutls' 'libfilezilla'
  libfilezilla.so libdbus-1.so libgtk-3.so libgdk-3.so libnettle.so
)
source=(https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2
        wxwidgets-3.2.patch)
sha256sums=('e407dbad169953ff951452bcbf138d953a3a30235f8abb03d271c712cc7e5792'
            'e77564b7c21a443f52dd7cb4fa4b8d1519396b241414fe6ee95feca6e143d748')
sha512sums=('2660d25c2e8390cc34c08a8ad84349c8a5d5245a708223d6d5935579a29568f4276e6672a62ab3a8e187e649f30a436dca0aa1fdc6b4d6e59394f6cad10a299a'
            'd31eb0da71cbc740e6210435f97920caac6c26be98717b656c785df6be248fa27925495fa90b118dcbc582ac32af957472cbbe16fdb4fef0bbc6257d15d06931')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../wxwidgets-3.2.patch # Allow building with wxWidgets 3.2
  autoreconf -vif
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
