# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Eduard Tolosa <edu4rdshl@protonmail.com>

pkgname=findomain
_pkgname=Findomain
pkgver=4.3.0
pkgrel=1
pkgdesc='The fastest and cross-platform subdomain enumerator, do not waste your time'
url='https://github.com/Edu4rdSHL/findomain'
arch=('x86_64')
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
optdepends=('postgresql: for subdomain monitoring')
source=("https://github.com/${_pkgname}/${_pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('4d184a473ed02aa14980a728b2c8da8b55c80aac0b7c2c1523d408fb902e27d988eb590b6534a546191929b71f067954482e53195cf26c481f0f7b44f2439ee2')

build() {
  cd ${_pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${_pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${_pkgname}-${pkgver}

  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
