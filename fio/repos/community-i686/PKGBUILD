# $Id$
# Maintainer: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: John Williams <jwilliams4200 liamg reverse&remove moc>

pkgname=fio
pkgver=2.1.11
pkgrel=1
pkgdesc='Scriptable I/O tool for storage benchmarks and drive testing'
arch=(i686 x86_64)
url='http://freshmeat.net/projects/fio'
license=(GPL2)
depends=(bash libaio python2 numactl)
makedepends=(gtk2)
optdepends=(
  'gtk2: for gfio - fio GUI frontend' 
  'gnuplot: generating plots using fio_generate_plots'
)
source=(http://brick.kernel.dk/snaps/fio-$pkgver.tar.bz2)
sha256sums=('aba2d8bac1997277cb9bb5a0abab345248298d4c0c712d484c3d2fab0674dbb1')

prepare() {
  cd fio-$pkgver

  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i tools/plot/fio2gnuplot 
}

# TODO: enable rdma, need to move the libraries from AUR
build() {
  cd fio-$pkgver
  ./configure --enable-gfio --extra-cflags="$CFLAGS"
}

package() {
  cd fio-$pkgver
  make DESTDIR="$pkgdir" prefix=/usr mandir=/usr/share/man install

  # documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 HOWTO README REPORTING-BUGS SERVER-TODO "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # examples
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/examples"
  install -m644 examples/* "$pkgdir/usr/share/doc/$pkgname/examples"
}
