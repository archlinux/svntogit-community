# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=fira-sans
pkgname=(ttf-fira-sans otf-fira-sans)
pkgver=4.202
pkgrel=1
epoch=1
pkgdesc="Mozilla's typeface designed for Firefox OS"
arch=('any')
license=('custom:OFL')
url='https://github.com/mozilla/Fira'
makedepends=('git')
depends=('fontconfig' 'xorg-font-utils')
source=("git+https://github.com/mozilla/Fira.git#tag=$pkgver"
        ttf.install otf.install)
md5sums=('SKIP'
         '9d795e681b14231a6f234c8d2a6d9957'
         '56bf7b135ff34a40513d5a832d882bff')

function _package {
    cd "$srcdir/Fira"

    case "$1" in
        ttf-fira-sans)
            pkgdesc="Mozilla's sans-serif typeface designed for Firefox OS"
            install=ttf.install
            cd ttf
            fonts=(FiraSans*.ttf)
            installdir=TTF;;
        otf-fira-sans)
            pkgdesc="Mozilla's sans-serif typeface designed for Firefox OS"
            install=otf.install
            cd otf
            fonts=(FiraSans*.otf)
            installdir=OTF;;
    esac

    # Prepare destination directory
    install -dm755 "$pkgdir/usr/share/fonts/$installdir"

    # Install fonts
    for font in "${fonts[@]}"; do
        install -m644 "$font" "$pkgdir/usr/share/fonts/$installdir"
    done

    install -D -m644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"   
}

for _pkgname in ${pkgname[@]}; do
    eval "function package_$_pkgname() { _package $_pkgname; }"
done
