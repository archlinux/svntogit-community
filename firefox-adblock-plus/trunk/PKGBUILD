# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=firefox-adblock-plus
pkgver=2.6.7
pkgrel=1
pkgdesc="Extension for firefox which block ads and banners"
arch=('any')
url="http://adblockplus.org/"
license=('GPL')
groups=('firefox-addons')
depends=("firefox")
makedepends=('unzip')
noextract=("adblockplus-$pkgver.xpi")
source=("https://downloads.adblockplus.org/adblockplus-$pkgver.xpi")
md5sums=('e2ffa2355457397d5df135b54d65c726')

package() {
  cd $srcdir
  unzip adblockplus-$pkgver.xpi
  local emid=$(sed -n -e '/<\?em:id>\?/!d; s/.*\([\"{].*[}\"]\).*/\1/; s/\"//g; p; q' install.rdf)
  local dstdir=$pkgdir/usr/lib/firefox/browser/extensions/${emid}
  [ -n ${emid} ] || return 1
  install -d $dstdir
  cp -R * $dstdir
  rm $dstdir/*.xpi
  find $pkgdir -type d -exec chmod 0755 {} \;
  find $pkgdir -type f -exec chmod 0644 {} \;
}
