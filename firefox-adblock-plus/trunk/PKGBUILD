# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=firefox-adblock-plus
pkgver=1.3.9
pkgrel=5
pkgdesc="plugin for firefox which block ads and banners"
arch=('any')
url="http://adblockplus.org/"
license=('GPL')
depends=("firefox")
makedepends=('unzip')
_addons_file=1865
source=(https://addons.mozilla.org/downloads/latest/${_addons_file}/adblock_plus-$pkgver.xpi)
md5sums=('a834246b6fe8f91e1f81c23ca8af675b')

build() {
  _ffver=`pacman -Q firefox | cut -f2 -d\ | cut -f1 -d-`
  depends=("firefox>=${_ffver}" "firefox<=${_ffver/0/99}")

  cd $srcdir
  local emid=$(sed -n -e '/<\?em:id>\?/!d; s/.*\([\"{].*[}\"]\).*/\1/; s/\"//g; p; q' install.rdf)
  local dstdir=$pkgdir/usr/lib/firefox-$_ffver/extensions/${emid}
  install -d $dstdir
  sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>7.*</em:maxVersion>#' install.rdf
  cp -R * $dstdir
  rm $dstdir/adblock_plus-$pkgver.xpi
}
