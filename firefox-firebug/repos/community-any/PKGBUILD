# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=firefox-firebug
pkgver=2.0.2
pkgrel=1
pkgdesc="Firefox extension with tools for web development."
arch=(any)
url="http://getfirebug.com/"
license=('custom:BSD')
groups=('firefox-addons')
depends=('firefox')
provides=('firebug')
source=("${url}releases/firebug/${pkgver%.*}/firebug-$pkgver.xpi")
md5sums=('ecd878a910e38262fa70d58b5a989201')

package() {
  # extension
  _dest="$pkgdir/usr/lib/firefox/browser/extensions/firebug@software.joehewitt.com"
  find . -type d -exec install -d {} $_dest/{} \;
  find . -type f -exec install -Dm644 {} $_dest/{} \;

  # license
  install -Dm644 license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
