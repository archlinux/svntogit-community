# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Eschwartz <eschwartz93@gmail.com>

pkgname=firefox-noscript
pkgver=5.1.6
pkgrel=1
pkgdesc="Extension for firefox which disables script"
arch=('any')
url="http://noscript.net/"
license=('GPL2')
groups=('firefox-addons')
depends=('firefox')
makedepends=('unzip')
source=(https://secure.informaction.com/download/releases/noscript-${pkgver}.xpi)
noextract=("noscript-${pkgver}.xpi")
sha256sums=('7fd1702ddbba2ce57c3e3a2aa00235828c4b1785c332ef1f2e619323c102b0de')

prepare() {
  cd "${srcdir}"
  unzip -qqo "noscript-${pkgver}.xpi" -d "noscript-${pkgver}"
}

package() {
  cd "${srcdir}"

  _extension_id="$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' noscript-${pkgver}/install.rdf)"
  _extension_dest="${pkgdir}/usr/lib/firefox/browser/extensions/${_extension_id}"
  if grep '<em:unpack>true</em:unpack>' noscript-${pkgver}/install.rdf > /dev/null; then
    install -dm755 "${_extension_dest}"
    cp -R noscript-${pkgver}/* "${_extension_dest}"
    chmod -R ugo+rX "${_extension_dest}"
  else
    install -Dm644 noscript-${pkgver}.xpi "${_extension_dest}.xpi"
  fi
}
