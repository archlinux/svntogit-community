# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-stylus
pkgver=1.5.17
pkgrel=1
pkgdesc='Easily install themes and skins for many popular sites'
url=https://add0n.com/stylus.html
arch=('any')
license=('GPL3')
groups=('firefox-addons')
makedepends=('git' 'npm')
source=("https://github.com/openstyles/stylus/archive/v$pkgver/stylus-v$pkgver.tar.gz")
b2sums=('f3e884cab0fe930c47fccdfd735b59913e0339174aa2224e17e504462c603f1cb77b3a35c16b93faca5b9725803056cd3054dbb584a44c6d3a159a6bd6a0fd01')

prepare() {
  cd stylus-$pkgver
  git config --global url."https://github.com".insteadOf ssh://git@github.com
  npm ci
}

build() {
  cd stylus-$pkgver
  npm run zip
}

package() {
  cd stylus-$pkgver
  install -Dm644 stylus.zip \
    "$pkgdir"/usr/lib/firefox/browser/extensions/{7a7a4a92-a2a0-41d1-9fd7-1e92480d612d}.xpi
}

# vim:set ts=2 sw=2 et:
