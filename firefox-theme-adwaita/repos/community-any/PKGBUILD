# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgname=firefox-theme-adwaita
pkgver=38.1
pkgrel=1
pkgdesc="Adwaita theme for Firefox (Matches the default Gnome Shell theme)"
url="https://addons.mozilla.org/en-US/firefox/addon/adwaita/"
arch=('any')
license=('MPL')
depends=("firefox")
makedepends=('unzip')
source=("gnome-firefox-theme-${pkgver}.xpi"::"https://github.com/gnome-integration-team/firefox-gnome/releases/download/${pkgver}/gnome-firefox-theme.xpi")
sha256sums=('af050237a8cddee0f9123f81a9df58b0b97e94f84059cc67225183cb3243a70e')
noextract=("gnome-firefox-theme-${pkgver}.xpi")

prepare() {
  unzip -qo "gnome-firefox-theme-${pkgver}.xpi" -d "${pkgname}-${pkgver}"
  # Fix for wrong version number
  # https://github.com/gnome-integration-team/firefox-gnome/issues/337
  cd "${pkgname}-${pkgver}"
  sed -i "s_\(<em:version>\)35.1\(</em:version>\)_\1${pkgver}\2_" "install.rdf"
}

package() {
  cd "${pkgname}-${pkgver}"
  # Find extension UUID
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  # Prepare destination folder
  _dstdir="${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}"
  install -d "${_dstdir}"
  # Copy files
  cp -R * "${_dstdir}"
  # Restore standard permissions
  find "${pkgdir}" -type d -print0 | xargs --null chmod 0755
  find "${pkgdir}" -type f -print0 | xargs --null chmod 0644
}

