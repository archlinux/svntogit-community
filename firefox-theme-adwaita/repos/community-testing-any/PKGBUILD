# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgname=firefox-theme-adwaita
_pkgname=firefox-gnome
pkgver=45.1
pkgrel=1
pkgdesc="Adwaita theme for Firefox (Matches the default GNOME Shell theme)"
url="https://addons.mozilla.org/en-US/firefox/addon/adwaita/"
arch=('any')
license=('MPL')
depends=("firefox")
makedepends=('python2')
source=("${_pkgname}-${pkgver}.tar.gz"::"https://github.com/gnome-integration-team/${_pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('aa7f13d6d00e3801360b646ac4f704b96601f14741ec9a3765a89b6d41e3c1f3')

build() {
  cd "${_pkgname}-${pkgver}"
  python2 make-xpi.py --force-rebuild all
}

package() {
  cd "${_pkgname}-${pkgver}"
  # Prepare destination folder
  install -m 755 -d "${pkgdir}/usr/lib/firefox/browser/extensions"
  # Install extension
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' extension/install.rdf.in)
  install -m 644 "gnome-firefox-extension.xpi" "${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}.xpi"
  # Install theme
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' theme/install.rdf.in)
  install -m 644 "gnome-firefox-theme.xpi" "${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}.xpi"
}

