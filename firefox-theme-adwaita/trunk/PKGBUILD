# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgname=firefox-theme-adwaita
pkgver=35.1
pkgrel=2
pkgdesc="Adwaita theme for Firefox (Matches the default Gnome Shell theme)"
url="https://addons.mozilla.org/en-US/firefox/addon/adwaita/"
arch=('any')
license=('MPL')
depends=("firefox")
makedepends=('unzip')
source=("https://github.com/gnome-integration-team/firefox-gnome/releases/download/${pkgver}/gnome-firefox-theme.xpi")
sha256sums=('aaa13577b1c4f3038f719f32780a4e4e1e5ef509e7b85336172788994da81594')
noextract=("gnome-firefox-theme.xpi")

prepare() {
  unzip -qo "gnome-firefox-theme.xpi" -d "${pkgname}-${pkgver}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Find extension UUID
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  # Prepare destination folder
  _dstdir="${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}"
  install -d "${_dstdir}"
  # Copy files
  cp -R * "${_dstdir}"
  # Restore standard permissions
  find "${pkgdir}" -type d -print0 | xargs --null chmod 0755
  find "${pkgdir}" -type f -print0 | xargs --null chmod 0644
}

