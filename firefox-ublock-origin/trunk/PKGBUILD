# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=firefox-ublock-origin
pkgver=1.35.0
_commit=0f580ec20446e4fd508363d7f86bcdc43f9f0503
pkgrel=1
pkgdesc='Efficient blocker add-on for various browsers. Fast, potent, and lean'
arch=('any')
url=https://github.com/gorhill/uBlock
license=('GPL3')
groups=('firefox-addons')
makedepends=('git' 'python' 'strip-nondeterminism' 'zip')
source=("git+$url.git#commit=$_commit?signed"
        "git+https://github.com/uBlockOrigin/uAssets.git")
b2sums=('SKIP' 'SKIP')
validpgpkeys=('603B28AA5D6CD687A554347425E1490B761470C2') # Raymond Hill <rhill@raymondhill.net>

build() {
  cd uBlock
  ./tools/make-firefox.sh all
  strip-nondeterminism -t zip dist/build/uBlock0.firefox.xpi
}

package() {
  cd uBlock/dist/build
  install -Dm644 uBlock0.firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/uBlock0@raymondhill.net.xpi
}

# vim:set ts=2 sw=2 et:
