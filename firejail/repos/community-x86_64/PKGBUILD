# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: ajs124 < aur AT ajs124 DOT de >

pkgname=firejail
pkgver=0.9.64
pkgrel=1
pkgdesc="Linux namespaces sandbox program"
arch=(x86_64)
license=(GPL2)
url="https://github.com/netblue30/firejail"
backup=('etc/firejail/login.users'
	'etc/apparmor.d/local/firejail-local'
	'etc/firejail/firejail.config')
depends=('apparmor')
validpgpkeys=('F951164995F5C4006A73411E2CCB36ADFC5849A7')
install=firejail.install
source=(https://github.com/netblue30/firejail/releases/download/${pkgver}/firejail-${pkgver}.tar.xz{,.asc})
sha256sums=('e53bab074e6c97609a3486d2055e44094025e32b217f374c06dd9e3285e7f2fd'
            'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-apparmor
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
#	install -c -m 0644 etc/firejail-local ${pkgdir}/etc/apparmor.d/local/firejail-local
}
