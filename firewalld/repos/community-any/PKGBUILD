# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=0.3.11
pkgrel=1
pkgdesc='A dynamic firewall daemon with a D-BUS interface'
arch=('any')
url="http://fedorahosted.org/${pkgname}"
license=('GPL2')
depends=('ebtables' 'gtk3' 'iptables' 'libnotify' 'networkmanager' 'python-slip')
makedepends=('intltool' 'docbook-xsl')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
install="${pkgname}.install"
source=("http://fedorahosted.org/released/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}-paths.patch"
        "${pkgname}-syntax.patch")
sha256sums=('02aeea580954d62abe7ad9ec053c2d943c7575d3f30e48e518a24d420b8a994f'
            '41591c5358c5ad8d43b29d72f42c331fcfc25e3d38c0ba5b3c818fc757ceef2f'
            '42f0e4dbf574ac7b87df77117816dbc4a2ac17f709a22990eeeb973950cc6322')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../${pkgname}-paths.patch
  patch -Np1 -i ../${pkgname}-syntax.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --disable-schemas-compile
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
