# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Jan Fader <jan.fader@web.de>

pkgname=fish
pkgver=2.1.0
pkgrel=1
pkgdesc='Smart and user friendly shell intended mostly for interactive use'
arch=('i686' 'x86_64')
url='http://fishshell.com/'
license=('GPL2')
depends=('python2' 'inetutils')
makedepends=('doxygen' 'python')
install=fish.install
source=(http://fishshell.com/files/$pkgver/fish-$pkgver.tar.gz)
md5sums=('3a29aebde522b8f52d9975d7423db99e')

build() {
  cd fish-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-xsel
  make
}

package() {
  cd fish-$pkgver
  make DESTDIR="$pkgdir" install

  # use python2
  find "$pkgdir"/usr/share/fish/tools/ -type f -exec sed -e "1s|python|python2|" -i {} \;
}
