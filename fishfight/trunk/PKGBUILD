# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: kleintux <reg-archlinux AT klein DOT tuxli DOT ch>

_pkgname=FishFight
pkgname=fishfight
pkgver=0.4.2
pkgrel=1
pkgdesc="A tactical 2D shooter game"
arch=('x86_64')
url="https://fishfight.org/"
license=('MIT' 'Apache')
depends=('pkg-config' 'libx11' 'libxi' 'mesa-libgl' 'alsa-lib' 'systemd-libs' 'libudev.so')
makedepends=('cargo' 'systemd')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/fishfight/FishFight/archive/v$pkgver.tar.gz"
  "$pkgname.sh"
)
sha512sums=('238031b0ae4a9764a1af8176ba5a80729d6514fb229b0eeff13973c86eb12dc20db9cbb7933791eea9b3ee3869ce78b8a854ae51073b6b19cb1c15b1b6573618'
            '00eed20436ffe3830f28c41ad0f437ad3ebef80aec1b05b68c0500a5958701f581bfe0147e5b5bdd379fc8494ed3386d5904b300d77bff91344a58939c269401')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/lib"
  install -Dm 755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  mkdir -p "$pkgdir/opt/$pkgname"
  cp -r "assets" "mods" "$pkgdir/opt/$pkgname/"
  find "$pkgdir/opt/$pkgname/assets/sounds/" -type f -exec chmod 444 {} \;

  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 licenses/LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
