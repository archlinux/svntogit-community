# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Cuneyt Unlu <unlucu AT gmail.com>

pkgname=fityk
pkgver=0.9.4
pkgrel=1
pkgdesc='A program for nonlinear fitting of analytical functions to data.'
arch=('i686' 'x86_64')
url='http://www.unipress.waw.pl/fityk/'
license=('GPL')
depends=('boost-libs' 'xylib' 'wxgtk' 'shared-mime-info')
makedepends=('boost')
options=('!libtool')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('2247efff3890e86489b903ff0b09aeef')

build() {
  cd ${pkgname}-${pkgver}

  # disable xyconvert because wxwidgets>=2.9 is needed (dev version) and it isn't standard with arch yet
  ./configure \
    --prefix=/usr \
    --disable-xyconvert

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
