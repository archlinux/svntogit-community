# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Allen Li <darkfeline at abagofapples.com>

pkgbase=flake8
pkgname=(flake8 python2-flake8)
pkgver=3.5.0
pkgrel=3
epoch=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://flake8.pycqa.org"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyflakes' 'python2-pyflakes'
             'python-pycodestyle' 'python2-pycodestyle' 'python-mccabe' 'python2-mccabe'
             'python2-enum34' 'python2-configparser' 'python-pytest-runner' 'python2-pytest-runner')
checkdepends=('python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/PyCQA/flake8/archive/$pkgver.tar.gz"
        "https://gitlab.com/pycqa/flake8/commit/4fcbcccf381ce0987faa297173e4008b0490918f.patch"
        "flake8-pycodestyle2.4-compat.patch::https://gitlab.com/pycqa/flake8/merge_requests/230.patch"
        "flake8-pycodestyle2.4-new-checks.patch::https://gitlab.com/pycqa/flake8/merge_requests/231.patch"
        "https://gitlab.com/pycqa/flake8/commit/527af5c214ef0eccfde3dd58d7ea15e09c483bd3.patch")
sha512sums=('36c6bd0166a0e8f9b8c99cfd7d92f21a4b8d761727185bbd0dff231001dbb495ed4128e2c60ab16bf3920a8a8394efb0127d9233962940333cfc346766736639'
            '16805e72ea65a0e7de0b90e4ac011ebe90e1d4ff6ff88616c8aa13a9a30f4de873f0bd0254ef3939ba2e00240e8127904d68ae6c4e6e7e8482011a7d0dfaf7cb'
            '0d4ce24e4edd52c4d592146934ce7894c1cd96abdcab36efe16f0cb4b06de86af7ee847d4f7c7e0461b70148629c3ee4fc6adef8f30e45075bf081f012befb46'
            '7215957aa0c1e66830635cf96cc8a69dde3cd09b0b7dd4ab3bbd27e8a8c4748e8ef8c113c2ebb9a81ddaaa98979381ecfd72bb47a1c48a34529c9d40d96550f0'
            '76e017af744a0bd11ad6cfa855364e0f9c951873a4a272bb24d7f00187e5428e4dfa7bfe9f9127859fd65e79a663391668409951aa29ad9b1aecc1adeb9fb9e8')

prepare() {
  # update versions to what the next patch expects
  patch -p1 -d flake8-$pkgver < 4fcbcccf381ce0987faa297173e4008b0490918f.patch
  # fix renamed checks in newer pycodestyle versions
  patch -p1 -d flake8-$pkgver < flake8-pycodestyle2.4-compat.patch
  # add new pycodestyle 2.4.0 checks
  patch -p1 -d flake8-$pkgver < flake8-pycodestyle2.4-new-checks.patch
  # fix pyflakes 2.0 compat
  patch -p1 -d flake8-$pkgver < 527af5c214ef0eccfde3dd58d7ea15e09c483bd3.patch
  # disable overly restrictive dependency pinning
  sed -i -e 's/, *< *[0-9=.]*//' flake8-$pkgver/setup.py
  cp -a flake8-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flake8-$pkgver
  python setup.py build

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flake8-$pkgver
  python setup.py pytest

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py pytest
}

package_flake8() {
  depends=('python-pyflakes' 'python-mccabe' 'python-pycodestyle')
  provides=('flake8-python3')
  replaces=('flake8-python3')
  conflicts=('flake8-python3')

  cd "$srcdir"/flake8-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flake8() {
  depends=('python2-pyflakes' 'python2-mccabe' 'python2-pycodestyle' 'python2-enum34'
           'python2-configparser')
  provides=('flake8-python2')
  replaces=('flake8-python2')
  conflicts=('flake8-python2')

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/flake8{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
