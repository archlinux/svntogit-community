# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Allen Li <darkfeline at abagofapples.com>

pkgbase=flake8
pkgname=(flake8 python2-flake8)
pkgver=3.3.0
pkgrel=1
epoch=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://bitbucket.org/tarek/flake8"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyflakes' 'python2-pyflakes'
             'python-pycodestyle' 'python2-pycodestyle' 'python-mccabe' 'python2-mccabe'
             'python2-enum34' 'python2-configparser' 'python-pytest-runner' 'python2-pytest-runner'
             'git')
checkdepends=('python-mock' 'python2-mock')
source=("git+https://gitlab.com/pycqa/flake8.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i -e 's/, *< *[0-9=.]*//' flake8/setup.py
  cp -a flake8{,-py2}
}

build() {
  cd "$srcdir"/flake8
  python setup.py build

  cd "$srcdir"/flake8-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flake8
  python setup.py ptr

  cd "$srcdir"/flake8-py2
  python2 setup.py ptr
}

package_flake8() {
  depends=('python-pyflakes' 'python-mccabe' 'python-pycodestyle')
  provides=('flake8-python3')
  replaces=('flake8-python3')
  conflicts=('flake8-python3')

  cd "$srcdir"/flake8
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flake8() {
  depends=('python2-pyflakes' 'python2-mccabe' 'python2-pycodestyle' 'python2-enum34'
           'python2-configparser')
  provides=('flake8-python2')
  replaces=('flake8-python2')
  conflicts=('flake8-python2')

  cd "$srcdir"/flake8-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/flake8{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
