# $Id$

pkgname=flashplugin
_licensefile='Reader_Player_AIR_WWEULA-Combined-20080204_1313.pdf'
pkgver=10.2.159.1
pkgrel=2
pkgdesc='Adobe Flash Player'
url='http://get.adobe.com/flashplayer'
arch=('x86_64')
depends=('mozilla-common' 'lib32-libxt' 'lib32-gtk2' 'lib32-nss' 'lib32-curl' 'lib32-alsa-lib' 'nspluginwrapper')
provides=('flashplayer')
optdepends=('lib32-libvdpau: GPU acceleration on Nvidia card')
license=('custom')
install=flashplugin.install
backup=(etc/adobe/mms.cfg)
source=('http://fpdownload.macromedia.com/get/flashplayer/current/install_flash_player_10_linux.tar.gz'
        "http://www.adobe.com/products/eulas/pdfs/${_licensefile}"
        mms.cfg)
md5sums=('716e9e4fd746e400a998afcd12532eab'
         '1636037610ee2aa35c5fb736a697b7e0'
         'f34aae6279b40e0bd2abfb0d9963d7b8')
options=(!strip)

package() {
  install -d -m755 "${pkgdir}"/usr/lib32/mozilla/plugins/
  install -m755 "${srcdir}"/libflashplayer.so "${pkgdir}"/usr/lib32/mozilla/plugins/
  install -d -m755 "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -m644 "${_licensefile}" "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.pdf
  install -d -m755 "${pkgdir}"/etc/adobe
  install -m644 "${srcdir}"/mms.cfg "${pkgdir}"/etc/adobe/mms.cfg


  # Install npwrapper:
  mkdir -p "${pkgdir}"/usr/lib/mozilla/plugins/
  touch "${pkgdir}"/usr/lib/mozilla/plugins/npwrapper.libflashplayer.so
  chmod 755 "${pkgdir}"/usr/lib/mozilla/plugins/npwrapper.libflashplayer.so
}
