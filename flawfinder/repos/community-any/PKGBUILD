# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.17
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='https://dwheeler.com/flawfinder/'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
source=(https://dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha512sums=('8691e561e634d6562d43fca9ac8c505b62435ac368c1b038b84eabbf60628513a0410de60b53592567d07a9552b97112ff8406ddc9aba3512a3b07846d28a8c6')
b2sums=('03a3cf425eb9f2cd2a8528d574c1fd9ed1d00e4dfc5f47556f7d49db041432d311e08754220eedd8492bb6d747ed31a0da67eed5f878ede207b1118e3db5da9a')

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
