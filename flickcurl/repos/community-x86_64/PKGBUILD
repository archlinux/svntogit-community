# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=flickcurl
pkgver=1.26
pkgrel=7
pkgdesc="C library for the Flickr API"
arch=('x86_64')
url="http://librdf.org/flickcurl/"
license=('GPL')
depends=('curl' 'libxml2' 'raptor')
source=("http://download.dajobe.org/flickcurl/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "https://github.com/dajobe/flickcurl/commit/a5cc2a5d2fc7074f50fbaa772232b6e0fea7ce89.patch")
validpgpkeys=('F879F0DEDA780198DD08DC6443EC92504F71955A')
sha512sums=('5ffdef58e67cbf99da2e8eefad57e845627703d5b22c85d5635557cd2df53e47e74d8e3387523495708c5cc8952c290da5c4d5f1e86bd482312fc7e46e9af6cd'
            'SKIP'
            '1602a8f469e5c37d9482c41cc46db355223d78fef793a7d31a1a5d8e6d740844f1d647ce6da4529a0ac83439c727958adc1f942800d5c83d4f0bab89855b8ba7')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../a5cc2a5d2fc7074f50fbaa772232b6e0fea7ce89.patch
  sed -i 's|#include <curl/types.h>||' src/flickcurl_internal.h
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
