# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=flightgear
pkgver=3.4.0
pkgrel=2
pkgdesc="An open-source, multi-platform flight simulator"
arch=(i686 x86_64)
depends=('libxmu' 'libxi' 'zlib' 'openscenegraph' 'subversion' 'libxrandr' 'glu' 'openal')
makedepends=('boost' 'cmake' 'mesa' 'sharutils' 'simgear')
license=("GPL")
url="http://www.flightgear.org"
options=('!makeflags')
#source=("$pkgname-$pkgver.tar.gz::https://gitorious.org/fg/flightgear/archive-tarball/release/$pkgver")
#source=("ftp://ftp.de.flightgear.org/pub/fgfs/Source/flightgear-$pkgver.tar.bz2")
source=("http://mirrors.ibiblio.org/pub/mirrors/flightgear/ftp/Source/flightgear-$pkgver.tar.bz2"
        "explicitely-link-with-libX11.patch")
md5sums=('0a6f4c27baf64f3e7d58fd44612118df'
         'fc264184d88afdc12c669f1eabb5bbea')

prepare() {
  cd $srcdir/flightgear-$pkgver
  patch -Np1 -i ../explicitely-link-with-libX11.patch
}

build() {
  cd $srcdir/flightgear-$pkgver
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DFG_DATA_DIR:STRING="/usr/share/flightgear/data" .
  uudecode -o package/flightgear.png package/flightgear.png.uue
  make
  sed -i 's|Exec=.*|Exec=fgfs --fg-root=/usr/share/flightgear/data|' package/flightgear.desktop
}

package() {
  depends=(${depends[@]} 'flightgear-data')

  cd $srcdir/flightgear-$pkgver
  make DESTDIR=$pkgdir install

  install -Dm0644 package/flightgear.desktop $pkgdir/usr/share/applications/flightgear.desktop
  install -Dm0644 package/flightgear.ico $pkgdir/usr/share/icons/flightgear.ico
  install -Dm0644 package/flightgear.png $pkgdir/usr/share/icons/flightgear.png
  install -Dm0644 scripts/completion/fg-completion.bash $pkgdir/usr/share/bash-completion/completions/fgfs
  ln -sf flightgear $pkgdir/usr/share/FlightGear
}
