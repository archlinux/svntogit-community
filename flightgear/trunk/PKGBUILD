# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=flightgear
pkgver=2016.3.1
_pkgver=2016.3
pkgrel=1
pkgdesc="An open-source, multi-platform flight simulator"
arch=(i686 x86_64)
depends=('libxmu' 'libxi' 'zlib' 'openscenegraph' 'subversion' 'libxrandr' 'glu' 'openal')
makedepends=('boost' 'cmake' 'mesa' 'sharutils' 'simgear' 'qt5-base')
optdepends=('qt5-base: fgfs --launcher')
license=("GPL")
url="http://www.flightgear.org/"
options=('!makeflags')
source=("http://downloads.sourceforge.net/project/flightgear/release-${_pkgver}/${pkgname}-${pkgver}.tar.bz2"
	"build-fix.patch")
md5sums=('70936a91c80ee18a7dd7ed663c0273e3'
         'b3d60c225955fd4b8d80ac2087288e43')

prepare() {
  cd $srcdir/flightgear-$pkgver
  patch -p1 -i $srcdir/build-fix.patch
}

build() {
  cd $srcdir/flightgear-$pkgver
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DFG_DATA_DIR:STRING="/usr/share/flightgear/data" .
  uudecode -o package/flightgear.png package/flightgear.png.uue
  make
  sed -i 's|Exec=.*|Exec=fgfs --fg-root=/usr/share/flightgear/data|' package/flightgear.desktop
}

package() {
  depends=(${depends[@]} 'flightgear-data')

  cd $srcdir/flightgear-$pkgver
  make DESTDIR=$pkgdir install

  install -Dm0644 package/flightgear.desktop $pkgdir/usr/share/applications/flightgear.desktop
  install -Dm0644 package/flightgear.ico $pkgdir/usr/share/icons/flightgear.ico
  install -Dm0644 package/flightgear.png $pkgdir/usr/share/icons/flightgear.png
  install -Dm0644 scripts/completion/fg-completion.bash $pkgdir/usr/share/bash-completion/completions/fgfs
  ln -sf flightgear $pkgdir/usr/share/FlightGear
}
