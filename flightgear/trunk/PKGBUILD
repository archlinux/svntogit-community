# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Hans Janssen <hans@janserv.xs4all.nl>

pkgname=flightgear
pkgver=2016.4.3
_pkgver=${pkgver%.*}
pkgrel=2
pkgdesc="An open-source, multi-platform flight simulator"
arch=(i686 x86_64)
depends=('libxmu' 'libxi' 'zlib' 'openscenegraph' 'subversion' 'libxrandr' 'glu' 'openal')
makedepends=('boost' 'cmake' 'mesa' 'sharutils' 'simgear' 'qt5-base')
optdepends=('qt5-base: fgfs --launcher')
license=("GPL")
url="http://www.flightgear.org/"
options=('!makeflags')
source=("http://downloads.sourceforge.net/project/flightgear/release-${_pkgver}/${pkgname}-${pkgver}.tar.bz2"
	sec-fix.patch)
sha256sums=('35a9c439265e4a9caf794458bb42917d93280b2e561cc7b9a02aaba1226d2822'
            'c45d9596ee8269594ee600e3fe1de10422f202c2ce4b3fb733062ce92f37b11b')

prepare() {
  cd "$srcdir"/flightgear-$pkgver
  patch -p1 -i $srcdir/sec-fix.patch
}

build() {
  cd "$srcdir"/flightgear-$pkgver
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DFG_DATA_DIR:STRING="/usr/share/flightgear/data" .
  uudecode -o package/flightgear.png package/flightgear.png.uue
  make
  sed -i 's|Exec=.*|Exec=fgfs --fg-root=/usr/share/flightgear/data|' package/flightgear.desktop
}

package() {
  depends=(${depends[@]} 'flightgear-data')

  cd "$srcdir"/flightgear-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm0644 package/flightgear.desktop "$pkgdir"/usr/share/applications/flightgear.desktop
  install -Dm0644 package/flightgear.ico "$pkgdir"/usr/share/icons/flightgear.ico
  install -Dm0644 package/flightgear.png "$pkgdir"/usr/share/icons/flightgear.png
  install -Dm0644 scripts/completion/fg-completion.bash "$pkgdir"/usr/share/bash-completion/completions/fgfs
  ln -sf flightgear "$pkgdir"/usr/share/FlightGear
}
