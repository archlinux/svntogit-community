# $Id$
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=flobopuyo
pkgver=0.20
pkgrel=9
pkgdesc="Clone of the famous PuyoPuyo"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/$pkgname/"
license=('GPL')
depends=('glu' 'sdl_image' 'sdl_mixer')
makedepends=('mesa' 'patch')
changelog=$pkgname.changelog
source=(http://www.sourcefiles.org/Games/Puzzle/$pkgname-$pkgver.tgz
	$pkgname-makefile.patch $pkgname-0.20-gcc4.patch
	$pkgname-0.20-libs.patch
	$pkgname.desktop
	$pkgname.png)
md5sums=('88961898c9dd48b60464f6f10f695b8b'
         '4c6667be791681d43bb7ea1b6f9f9072'
         'bb8afd6e04bde08fd33e22c466674878'
         'b7c0fd71cd948bbc43a86742ffad6d86'
         'b18b185000c30bd3b7ff324052c95896'
         'd9abe591e6cdd654269474444df1ec9d')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  patch -Np0 < "${srcdir}"/$pkgname-makefile.patch
  patch -Np1 < "${srcdir}"/$pkgname-$pkgver-gcc4.patch
  patch -Np1 < "${srcdir}"/$pkgname-$pkgver-libs.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -D -m644 man/${pkgname}.6 "${pkgdir}"/usr/share/man/man6/$pkgname.6
  install -D "${srcdir}"/$pkgname.desktop "${pkgdir}"/usr/share/applications/$pkgname.desktop
  install -D "${srcdir}"/$pkgname.png "${pkgdir}"/usr/share/pixmaps/$pkgname.png
}
