# $Id: PKGBUILD,v 1.31 2009/07/15 21:29:07 ebelanger Exp $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Jack (nim901@gmail.com)

pkgname=flock
pkgver=2.5
pkgrel=2
pkgdesc="A free, easy-to-use web browser built on Mozilla technologies." 
url="http://www.flock.com"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
depends=('gtk2' 'dbus-glib' 'libxt' 'nss' 'startup-notification' 'libjpeg>=7')
makedepends=('cvs' 'subversion' 'clucene' "java-environment=6" 'zip' 'python' 'libidl2')
install=flock.install

source=(http://ftp.osuosl.org/pub/${pkgname}/releases/${pkgver}/${pkgname}-${pkgver}-source.tar.bz2
 	mozconfig
	100-system-hunspell-corrections.patch
	fix-mozilla-launcher.patch
	mozilla-ps-pdf-simplify-operators.patch
	fix_lucene_path.diff
	flock-gcc44.patch
        flockbrowser
	flock.desktop)
md5sums=('28a8a94f347dccbee38ef33365f502f1'         '7642ee43b17138fe5f08efbdf0c0ff11'
         '5efd6772ed0ecf8eddec5d5650191d3c'         '63eee2d1da3b43c9d604f2253f242f40'
         '13dca58c04e62a8916691c63c5c492a0'         '3293f048e42a633f6a5834bec2fb49f5'
         'b07dd0fcddb90b1ee9c5fcbcce3f45e0'         '51878eff2d60fa3d26e6c05f519f0435'
         '35702afdbc8575fdd0a4344f54db5246')

build() {

cd ${srcdir}/source-svn-export-${pkgver}/mozilla/${pkgname}/base/lucene/src
patch -p0 < ${srcdir}/fix_lucene_path.diff || return 1

cd ${srcdir}/source-svn-export-${pkgver}/mozilla
cp -f ${srcdir}/mozconfig .mozconfig

# xulrunner upstream patch. Still not applied
patch -Np1 -i ${srcdir}/mozilla-ps-pdf-simplify-operators.patch || return 1

# fix build with system hunspell - gentoo
patch -Np0 -i ${srcdir}/100-system-hunspell-corrections.patch || return 1

# gcc4.4 patch
patch -Np1 -i ${srcdir}/flock-gcc44.patch || return 1

sed "s/#CFLAGS#/${CFLAGS}/g" ${srcdir}/mozconfig >.mozconfig || return 1

./configure
make || return 1
make DESTDIR=${pkgdir} install || return 1

# moving stuff

mv ${pkgdir}/usr/lib/${pkgname}-${pkgver} ${pkgdir}/usr/lib/${pkgname} || return 1
mv ${pkgdir}/usr/bin/${pkgname} ${pkgdir}/usr/lib/${pkgname}/${pkgname} || return 1

# removing useless folders

rm -rf ${pkgdir}/usr/{bin,share,include} || return 1
rm -rf ${pkgdir}/usr/lib/{$pkgname-devel-$pkgver,pkgconfig} || return 1
rm -rf ${pkgdir}/usr/lib/${pkgname}/{init.d,install} || return 1

# installing icons and desktop files
install -Dm644 ${pkgdir}/usr/lib/flock/icons/mozicon128.png \
	${pkgdir}/usr/share/pixmaps/flock.png || return 1
install -Dm644 ${srcdir}/flock.desktop \
	${pkgdir}/usr/share/applications/flock.desktop || return 1
install -Dm755 ${srcdir}/flockbrowser \
	${pkgdir}/usr/bin/flockbrowser || return 1
}
