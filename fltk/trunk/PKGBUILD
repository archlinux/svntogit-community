# Maintainer: David Runge <dave@sleepmap.de>

pkgbase=fltk
pkgname=('fltk' 'fltk-docs')
pkgver=1.3.5
pkgrel=1
pkgdesc="Graphical user interface toolkit for X"
arch=('x86_64')
license=('custom' 'LGPL')
url="https://www.fltk.org/"
makedepends=('alsa-lib' 'cairo' 'cmake' 'desktop-file-utils' 'doxygen' 'glu' 'libjpeg'
'libxcursor' 'libxft' 'libxinerama' 'mesa' 'shared-mime-info')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fltk/fltk/archive/release-${pkgver}.tar.gz"
        "${pkgname}-fix-lib-mess.patch")
sha512sums=('35732df4d66573fdabf95f57069c5a2df63b0029d7b904d4ac02bd4c3ab3e5b287d4f06998b67129e8195c42c0b39e137e5d4f10baf4d73992077bb5ae0bef6c'
            '14592ba5616483df1b36f6e2b8309a3d1e898c5c4dcf0ddfbb374bbd1332eca6ba3490569fc8f903023a069b7771ed06baddef36140253e2e81eb409892bbba5')

prepare() {
  mv -v "${pkgbase}-release-${pkgver}" "${pkgbase}-${pkgver}"
  cd "${pkgbase}-${pkgver}"
  # fix bizarre renaming of shared libraries
  # https://github.com/fltk/fltk/issues/20
  patch -Np1 -i ../${pkgname}-fix-lib-mess.patch
  mkdir -v build
}

build() {
  cd "${pkgbase}-${pkgver}/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DFLTK_LIBDIR=/usr/lib \
        -DOPTION_CREATE_LINKS=ON \
        -DOPTION_BUILD_SHARED_LIBS=ON \
        -DOPTION_BUILD_HTML_DOCUMENTATION=ON \
        -DOPTION_INSTALL_HTML_DOCUMENTATION=ON \
        ..
  make VERBOSE=1
  make -C documentation html
}

package_fltk() {
  pkgdesc="Graphical user interface toolkit for X"
  depends=('cairo' 'desktop-file-utils' 'glu' 'hicolor-icon-theme' 'libjpeg'
  'libxcursor' 'libxft' 'libxinerama' 'shared-mime-info' 'xdg-utils')
  optdepends=('alsa-lib: for sudoku example application')
  replaces=('fltk-games')
  options=('!docs' 'staticlibs')

  cd "${pkgbase}-${pkgver}/build"
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 ../COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # example apps and integration files, that are not covered by make install
  for app in {blocks,checkers,sudoku}; do
    install -vDm 755 "bin/examples/$app" -t "${pkgdir}/usr/bin/"
    install -vDm 644 "../test/desktop/$app.desktop" \
      -t "${pkgdir}/usr/share/applications/"
    install -vDm 644 "../test/desktop/$app-32.png" \
      "${pkgdir}/usr/share/icons/hicolor/32x32/apps/$app.png"
    install -vDm 644 "../test/desktop/$app-128.png" \
      "${pkgdir}/usr/share/icons/hicolor/128x128/apps/$app.png"
  done
}

package_fltk-docs() {
  pkgdesc="Graphical user interface toolkit for X (documentation)"

  cd "${pkgbase}-${pkgver}/build"
  make DESTDIR="$pkgdir" -C documentation install
  install -vDm 644 ../COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

