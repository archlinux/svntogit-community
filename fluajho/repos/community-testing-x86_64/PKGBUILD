# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.6.2
pkgrel=3
pkgdesc="A simple sf2 soundfont host/player"
arch=('x86_64')
url="https://www.laborejo.org/fluajho/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('fluidsynth' 'glib2' 'jack' 'libsndfile' 'util-linux-libs' 'ncurses')
optdepends=('new-session-manager: Session management')
provides=('soundfont-synthesizer')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('fdddb8906d101f45da34cb0592192f5fa6d2aaa83baba1350a355add1fb61715c53d200abf753222de51e5bc492a3b37ecee7e2c64c6d8e009461cdbdd26a1de'
            'SKIP')
b2sums=('f3641018c4c71a7a548d9ab08908d500a10248c035924093379a0e666f1ebdd9111d0a8377d55569ee1f212f50dc558abf26908d025ace70eff6bfac07ed4ae0'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfluidsynth.so' 'libglib-2.0.so' 'libjack.so' 'libncursesw.so'
  'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
