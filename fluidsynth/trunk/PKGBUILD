# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
# only update to > 2.0.0 after these are resolved:
# FIXED https://github.com/calf-studio-gear/calf/issues/202
# FIXED https://github.com/falkTX/Carla/issues/758
# DISABLED https://github.com/csound/csound/issues/1036
# https://github.com/free-wheeling/freewheeling/issues/12
# FIXED https://github.com/LMMS/lmms/issues/4640
# FIXED https://sourceforge.net/p/qsynth/tickets/15/
pkgver=1.1.11
pkgrel=2
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'libpulse' 'portaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
optdepends=('pulseaudio: PulseAudio sound support')
license=('LGPL')
backup=("etc/conf.d/$pkgname")
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('ad7c634426b29e5a05d67addf32d43a9ccb11a1fe3cfc09ad617416b9df3dc99a48191377f5027232521380737dde2d9b59833516d1bffb90f410bab3bbe140c')

prepare() {
  cd "${pkgname}-${pkgver}"
  # out-of-tree build
  mkdir build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
           -Denable-ladspa=ON \
           -Denable-portaudio=ON \
           -DLIB_SUFFIX=""
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm 644 "${pkgname}.service" \
    -t "$pkgdir/usr/lib/systemd/user/"
  # system-wide configuration file
  install -vDm 644 "${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,ChangeLog,NEWS,README.md,THANKS,TODO}
}

