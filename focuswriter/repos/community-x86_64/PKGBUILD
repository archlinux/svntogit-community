# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=focuswriter
pkgver=1.8.0
pkgrel=1
pkgdesc='Simple, distraction-free word processor'
arch=('x86_64')
url='https://gottcode.org/focuswriter/'
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'qt6-multimedia' 'qt6-5compat')
makedepends=('cmake' 'qt6-tools')
source=("https://gottcode.org/$pkgname/$pkgname-$pkgver-src.tar.bz2"{,.asc})
sha256sums=('114897e2fa697e32cbf81c3ced8dc6d56ea8584956348ab607f3530c991ce7d8'
            'SKIP')
validpgpkeys=('57E4D43C10CD7EDB8360C84774FFC7F02C4F6B37')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
