# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=focuswriter
pkgver=1.7.6
pkgrel=3
pkgdesc='Simple, distraction-free word processor'
arch=('x86_64')
url='https://gottcode.org/focuswriter/'
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'qt5-multimedia')
makedepends=('qt5-tools')
source=("https://gottcode.org/$pkgname/$pkgname-$pkgver-src.tar.bz2"{,.asc})
sha256sums=('34f576a49c9fe64aedad66e02efafc29c840b6c3ddc98a42d87dfda499710541'
            'SKIP')
validpgpkeys=('57E4D43C10CD7EDB8360C84774FFC7F02C4F6B37')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/INSTALLS +=/ s/ pixmap//' focuswriter.pro
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
