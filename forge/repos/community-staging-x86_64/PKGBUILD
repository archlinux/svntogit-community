# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=1.1.0
_afver=af3.6.0
arch=('x86_64')
pkgrel=2
epoch=1
license=('BSD')
makedepends=('cmake' 'doxygen' 'graphviz' 'git' 'python' 'glm' 'boost')
depends=('glfw' 'freetype2' 'fontconfig' 'freeimage' 'glbinding')
source=(https://github.com/arrayfire/forge/archive/${_afver}.tar.gz
        glm.patch)
sha512sums=('e26ac048b7f2708c0b4bc06a99fd7bec2be870e0c73bd4a8cfa733d6a96f61a7261b0baab71bcb1cdb1969f3d133f85f5bca57ff74e1d166776cfe642fdbd16b'
            '6b6de2967dd7e0426854262be569ce9a5379ea9448c584f72cbfa08d13be269960b6a01f0ba3e64c9a543644466b62ba3bac3199312b2b035c12a2e872ff002b')
options=('!buildflags')

prepare() {
  cd forge-${_afver}

  patch -Np1 -i "$srcdir"/glm.patch
}

build() {
  cd forge-${_afver}

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_EXAMPLES=ON \
      -DBUILD_DOCUMENTATION=ON

  make
}

package() {
  cd forge-${_afver}

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd build

  make DESTDIR="${pkgdir}/" install
}
