# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=1.0.4
arch=('x86_64')
pkgrel=2
epoch=2
license=('BSD')
makedepends=('cmake' 'doxygen' 'graphviz' 'git' 'python' 'glm' 'boost')
depends=('glfw' 'freetype2' 'fontconfig' 'freeimage' 'glbinding')
source=(https://github.com/arrayfire/forge/archive/v${pkgver}.tar.gz)
sha512sums=('fd48375713c8c6646a8d4fa140e8a4b631e697c5f011656a0ca4c15c8288b47bfe1826c6cfa65c8f146a7b9dcd146058502a099e9477f92481cbc62556660ccd')
options=('!buildflags')

build() {
  cd forge-${pkgver}

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_EXAMPLES=ON \
      -DBUILD_DOCUMENTATION=ON

  make
}

package() {
  cd forge-${pkgver}

  install -Dm644 .github/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd build

  make DESTDIR="${pkgdir}/" install
}
