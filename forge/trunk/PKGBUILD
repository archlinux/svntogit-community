# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=3.2.0
arch=('i686' 'x86_64')
pkgrel=2
license=('BSD')
makedepends=('cmake' 'doxygen' 'graphviz' 'git')
depends=('glew' 'glfw' 'freetype2' 'fontconfig' 'glm')
source=(https://github.com/arrayfire/forge/archive/af${pkgver}.tar.gz
        build.patch)
md5sums=('ff192b8017bff1351cd844fde95c81bd'
         '9c223a865f8a9cc691164e97bc29d48e')

build() {
  cd "${srcdir}/forge-af${pkgver}/"

  patch -Np1 < ${srcdir}/build.patch

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DUSE_SYSTEM_GLM=ON \
      -DUSE_SYSTEM_FREETYPE=ON \
      -DBUILD_EXAMPLES=ON \
      -DBUILD_DOCUMENTATION=ON

  make VERBOSE=1
}

package() {
  cd "${srcdir}/forge-af${pkgver}/"

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd build

  make DESTDIR="${pkgdir}/" install
}
