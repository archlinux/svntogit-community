# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=1.0.7
arch=('x86_64')
pkgrel=1
epoch=2
license=('BSD')
depends=('glfw' 'freetype2' 'fontconfig' 'freeimage' 'glbinding')
makedepends=('cmake' 'graphviz' 'git' 'python' 'glm' 'boost' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/arrayfire/forge/archive/v${pkgver}.tar.gz)
sha512sums=('d341d5c6bdccf7d2f085162d1d8209b0b28d73b8a9f3173546b26f06bf8e422b3fdf1cfbaa7d122ea75cee5cf5f6bb6dbd2bffc10d8e0c336b5b8695c39f7b54')
options=('!buildflags')

build() {
  cd forge-${pkgver}

  cmake . \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DFG_BUILD_EXAMPLES=ON \
      -DFG_BUILD_DOCS=OFF

  ninja -C build
}

package() {
  cd forge-${pkgver}

  install -Dm644 .github/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  DESTDIR="${pkgdir}/" ninja -C build install
}
