# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=fortune-mod
pkgver=3.14.1
pkgrel=1
pkgdesc='The Fortune Cookie Program from BSD games'
arch=('x86_64')
url='https://www.shlomifish.org/open-source/projects/fortune-mod/'
license=('BSD')
depends=('glibc' 'recode')
makedepends=('cmake' 'rinutils')
source=("https://github.com/shlomif/fortune-mod/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz"
        'not-a-game.patch')
sha512sums=('0de0ec033ffc35a7a9ec1a549e6dac425757ec5fcc718c772f0a4e548d23c46451af4a90b0ac548f2ab60d68856ca0fa5e8f10930efe6b199e96b2cc1c568810'
            'f12bfed15648bf7e53143bfa5c626e0e614d64c64c4329119e7b1ad8a5d79c89d3e8f2c1336d489abecbf6a8ec0ad9acf3080b142d498f8b3f36cea77c82a23d')
b2sums=('a62d4056cdceef14f1d63dbc8b98bbd88098c64d0020b949ecc8e6006f1a4b1369211ac8b87a33db60b238cd7e6d3d6f3c4d2e59933a6cf0bd3fa54c439c4330'
        '3af374f348d42bf649199a79afc18a8f0dace50d2b4c8aa090a1a54ccffcb65bf069d26f0d1d417a395c5a3574f7376c5ae1ec8b97dd2385d6b4b944643b1215')


prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../not-a-game.patch
}

build() {
  cmake \
    -B build \
    -S "$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_OFFENSIVE=OFF \
    -DLOCALDIR=/usr/share/fortune \
    -DLOCALODIR=/usr/share/fortune/off \
    -DCOOKIEDIR=/usr/share/fortune \
    -DOCOOKIEDIR=/usr/share/fortune/off

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  rm -f "$pkgdir/usr/share/fortune/"{,off/}*.u8

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/COPYING.txt"
}
