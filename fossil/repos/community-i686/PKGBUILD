# $Id$
# Maintainer: Anatol Pomozov <anatol pomozov at gmail> 
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Konstantin Plotnikov <kostyapl at gmail dot com>

pkgname=fossil
pkgver=1.30
_date_pkgver=20150119112900
# NOTE: update _date_pkgver from http://www.fossil-scm.org/download.html
pkgrel=2
pkgdesc='Simple, high-reliability, distributed software configuration management'
arch=(i686 x86_64)
license=(BSD)
url='http://www.fossil-scm.org'
depends=(openssl zlib)
backup=(etc/xinetd.d/fossil)
source=(
  fossil.socket
  fossil.service
  fossil-xinetd
  fossil-autocomplete.bash
  fossil-autocomplete.zsh
  http://www.fossil-scm.org/download/fossil-src-$_date_pkgver.tar.gz
)
sha256sums=('f885e17998dc1eece1688a75e516663462fe72a7f4f132def4132055777c7ff8'
            'c4973079facf209d3757db81df08f8d0843ede242f2a0c12281720da65e3b166'
            '2f9a141fadfb7cb5225f0d65a308949bbc592bff9d567b5c472edb413c81f5aa'
            '3e80a8a8f71ff450919df16864d987e669acf31670e484721b977a3bb2960a89'
            'aeca50e6382da7c50d696cf971947a09cb14d572ee9f191b4113fd84286b5edd'
            '37969e5a09c1611199921e3518a4ed736fd637a02f2d5810cd45897a4ceb92dc')

prepare() {
  cp fossil-autocomplete.{bash,zsh} fossil-src-$_date_pkgver/tools
}

build() {
  cd fossil-src-$_date_pkgver
  ./configure --prefix=/usr
  # headers and translate targets are problematic with parallel jobs
  make -j1 bld bld/headers
  make
}

package() {
  install -Dm644 fossil-xinetd "$pkgdir"/etc/xinetd.d/fossil
  install -Dm644 fossil.socket "$pkgdir"/usr/lib/systemd/system/fossil.socket
  install -Dm644 fossil.service "$pkgdir"/usr/lib/systemd/system/fossil@.service

  cd fossil-src-$_date_pkgver
  install -Dm755 fossil "$pkgdir"/usr/bin/fossil
  install -Dm644 COPYRIGHT-BSD2.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 tools/fossil-autocomplete.bash "$pkgdir"/usr/share/bash-completion/completions/fossil
  install -Dm644 tools/fossil-autocomplete.zsh $pkgdir/usr/share/zsh/site-functions/_fossil
}
