# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Ben <ben@benmazer.net>

pkgname=fox
pkgver=1.6.44
pkgrel=1
pkgdesc="Free Objects for X: GUI Toolkit for C++"
arch=('i686' 'x86_64')
url="http://www.fox-toolkit.org/"
license=('LGPL' 'custom')
depends=('bzip2' 'libxcursor' 'libxft' 'libxrandr' 'mesa' 'libxi' 'libpng' 'libtiff')
optdepends=('perl')
options=('!libtool')
source=(http://ftp.fox-toolkit.org/pub/fox-$pkgver.tar.gz)
md5sums=('6ccc8cbcfa6e4c8b6e4deeeb39c36434')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export CPPFLAGS="$CPPFLAGS -I/usr/include/freetype2"
  ./configure   --prefix=/usr \
                --enable-release \
                --with-xft=yes \
                --with-opengl=yes \
                --with-xim \
                --with-xshm \
                --with-shape \
                --with-xcursor \
                --with-xrender \
                --with-xrandr \
                --with-xfixes \
                --with-xinput
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  cd ${srcdir}/${pkgname}-${pkgver}/tests
  make ControlPanel
  install -m755 ${srcdir}/${pkgname}-${pkgver}/tests/.libs/ControlPanel ${pkgdir}/usr/bin/
  install -Dm644 ../LICENSE_ADDENDUM ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
