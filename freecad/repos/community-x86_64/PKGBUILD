# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Gabriel Souza Franco <Z2FicmllbGZyYW5jb3NvdXphQGdtYWlsLmNvbQ==>
# Contributor: Florian Pritz
# Contributor: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jordi De Groof <jordi (dot) degroof (at) gmail (dot) com>
# Contributor: mickele
# Contributor: manwithgrenade
# Contributor: bricem13
# Contributor: gborzi

pkgname=freecad
pkgver=0.18.4
pkgrel=2
pkgdesc='General purpose 3D CAD modeler'
arch=(x86_64)
url='https://freecadweb.org/'
license=(LGPL)
depends=(boost-libs glew hicolor-icon-theme jsoncpp libspnav med netcdf opencascade openmpi pyside2-tools python-matplotlib python-pivy python-pyside2 qt5-declarative qt5-svg qt5-tools qt5-webkit qt5-x11extras shared-mime-info xerces-c)
makedepends=(boost cmake coin desktop-file-utils eigen gcc-fortran git ninja pyside2 python-shiboken2 shiboken2 swig)
optdepends=(graphviz openscad python-matplotlib)
source=("git+https://github.com/FreeCAD/FreeCAD#commit=980bf9060e28555fecd9e3462f68ca74007b70f8" # tag: 0.18.4
        'https://raw.githubusercontent.com/FreeCAD/FreeCAD/d06d5687c1498354483aff95093d7f798c2985f2/cMake/FindPySide2Tools.cmake'
        $pkgname.desktop
        $pkgname.xml)
sha256sums=('SKIP'
            '421a364ad214a25be578d81edc9960daab87d7b26f341022aa22dd15b9df621b'
            '3f076dcd15114011b7f20d5f82fbb0bc89a2652cdbf0aa57f191dd4ea6cd4bf6'
            '4ee1daf47c8371a3e17173d85a0dd4a106dacd7899d783ccd97a6f3e0dd1a21f')

prepare() {
  cd FreeCAD

  # Use the fixed version of FindPySide2Tools.cmake
  cp -f "$srcdir/FindPySide2Tools.cmake" cMake/FindPySide2Tools.cmake

  # Fix Qt5 version detection
  sed -i 's/Qt5Core_VERSION LESS/Qt5Core_VERSION VERSION_LESS/g' \
    cMake/FindPySide2Tools.cmake

  cd src

  # Update swigpyrun.in for Python 3.8
  sed -i 's/interp->modules/PyImport_GetModuleDict()/g' Base/swigpyrun.inl

  # Fix for Qt 5.15
  sed -i 's/\(.*\)include <QPainter>$/&\n\1include <QPainterPath>/' \
    Mod/Image/Gui/OpenGLImageBox.cpp

  # Compansate for the unusual Shiboken versioning scheme
  sed -i 's/SHIBOKEN_MICRO_VERSION/0/g' Gui/WidgetFactory.cpp

  # Add missing "std::" qualifiers, sledgehammer style
  find . \
    ! -wholename "*/Mod/Sketcher/*" \
    ! -wholename "*/Mod/PartDesign/*" \
    -name "*.cpp" -type f \
    -exec sed -i '1i using namespace std;' {} \;

  # Add missing include
  sed -i '1i \#include <Inventor\/SbByteBuffer.h>' Gui/Quarter/QuarterWidget.cpp
}

build() {
  # OpenCascade requires that /bin comes before /usr/bin in $PATH
  export PATH="/usr/bin:$PATH"

  # Configure with CMake and build with Ninja
  mkdir -p build
  cd build
  cmake ../FreeCAD \
    -D BUILD_QT5=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -D CMAKE_CXX_FLAGS="$CXXFLAGS -fPIC -w" \
    -D CMAKE_INSTALL_DATADIR="/usr/share/freecad" \
    -D CMAKE_INSTALL_DOCDIR="/usr/share/freecad/doc" \
    -D CMAKE_INSTALL_PREFIX="/usr/lib/freecad" \
    -D FREECAD_USE_EXTERNAL_PIVY=ON \
    -D FREECAD_USE_OCC_VARIANT="Official Version" \
    -D FREECAD_USE_QT_FILEDIALOG=ON \
    -D PYTHON_EXECUTABLE=/usr/bin/python \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # Package MIME info
  install -Dm644 freecad.xml "$pkgdir/usr/share/mime/packages/freecad.xml"

  # Package desktop shortcut
  desktop-file-install --dir="$pkgdir/usr/share/applications" $pkgname.desktop

  # Package icons
  cd FreeCAD/src/Gui/Icons
  for i in 16 32 48 64; do
    install -Dm644 "freecad-icon-$i.png" "$pkgdir/usr/share/icons/hicolor/${i}x$i/apps/freecad.png"
  done
  install -Dm644 freecad.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/freecad.svg"

  # Package symlinks in /usr/bin
  install -d "$pkgdir/usr/bin"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/freecad"
  ln -sf /usr/lib/freecad/bin/FreeCAD "$pkgdir/usr/bin/FreeCAD"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/freecadcmd"
  ln -sf /usr/lib/freecad/bin/FreeCADCmd "$pkgdir/usr/bin/FreeCADCmd"
}
