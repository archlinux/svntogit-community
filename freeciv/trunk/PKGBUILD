# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=freeciv
pkgver=2.6.1
pkgrel=1
pkgdesc="A multiuser clone of the famous Microprose game of Civilization"
arch=('x86_64')
url="http://www.freeciv.org/"
license=('GPL')
depends=('gtk3' 'sdl_mixer' 'lua' 'sqlite' 'curl')
makedepends=('python')
backup=('etc/freeciv/database.lua')
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('0a8994cf514b79897924195880f667ebfec815e9')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared --enable-sys-lua \
    --enable-client=gtk3 --enable-fcmp=gtk3 --enable-fcdb=sqlite3 --enable-aimodules
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
