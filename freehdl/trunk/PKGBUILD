# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Neil Darlow <neil@darlow.co.uk>
# Contributor: Jens Maucher <defcon@archlinux.us>

pkgname=freehdl
pkgver=0.0.8
pkgrel=8
pkgdesc='An open-source (C++ generating) VHDL simulator'
arch=('x86_64')
url='http://freehdl.seul.org'
license=('GPL')
depends=('perl')
optdepends=('gtkwave: to view output when run standalone')
options=('libtool')
source=("https://downloads.sourceforge.net/qucs/$pkgname-$pkgver.tar.gz")
sha256sums=('7f0978f8252329450de43e98c04c15fdd8a3f2bdc5ca91f75f8f5dd280c6ed84')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  sed -i 's|my $cpplibs = .*|my $cpplibs = "-lm -lfreehdl-kernel -lfreehdl-std";|g' v2cc/gvhdl-local v2cc/gvhdl.in
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
