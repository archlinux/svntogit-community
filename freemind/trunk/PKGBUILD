# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=freemind
pkgver=1.0.1
pkgrel=2
pkgdesc="A Java mindmapping tool"
arch=('any')
url="http://sourceforge.net/projects/freemind"
license=('GPL')
depends=('java-runtime' 'desktop-file-utils' 'libxtst')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-bin-max-$pkgver.zip
        $pkgname.png
        $pkgname.desktop
        $pkgname.run)
sha256sums=('005e815bf5d1867eadcaa89194df9952d986ace9eed0a3058c7ee6255799a60a'
            '39ec1dc88a1081058faef38b5ea48ca8c0b6ee6a6b397a216ce2f7b508b1ad61'
            'eac8db98257fcb8ed9c76ec340af5f3233a4783b8fe1c57ef274b9e7d2b56ed5'
            '7b02f0af26c6c9377011c0a6f0299dc6bbdbb69d17a1506cf88e7063e1ad2f6c')

package() {
  cd "${srcdir}"

  for file in $(find lib plugins -type f); do
    install -Dm644 $file "${pkgdir}"/usr/share/freemind/$file
  done

  install -Dm755 "${srcdir}"/freemind.run "${pkgdir}"/usr/bin/freemind
  install -Dm755 "${srcdir}"/freemind.sh "${pkgdir}"/usr/share/freemind/freemind.sh
  install -Dm644 "${srcdir}"/patterns.xml "${pkgdir}"/etc/freemind/patterns.xml
  
  install -d "${pkgdir}"/usr/share/freemind/accessories
  install -Dm644 "${srcdir}"/accessories/* "${pkgdir}"/usr/share/freemind/accessories/
  install -Dm644 "${srcdir}"/doc/freemind.mm "${pkgdir}"/usr/share/freemind/doc/freemind.mm

# icon and .desktop file
  install -Dm644 "${srcdir}"/freemind.desktop "${pkgdir}"/usr/share/applications/freemind.desktop
  install -Dm644 "${srcdir}"/freemind.png "${pkgdir}"/usr/share/pixmaps/freemind.png
}
