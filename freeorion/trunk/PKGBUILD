# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>
# Contributor: Christian Krause ("wookietreiber") <christian.krause@mailbox.org>

pkgname=freeorion
pkgver=0.4.10.2
pkgrel=5
pkgdesc='A turn-based space empire and galactic conquest game'
url='https://www.freeorion.org'
arch=('x86_64')
license=('GPL2')
depends=(
  'boost-libs'
  'python'
  'sdl2'
  'libvorbis'
  'glew'
  'openal'
  'hicolor-icon-theme'
  'freetype2'
)
makedepends=(
  'git'
  'cmake'
  'boost'
)
_commit='f663dad6379658b539bd28c95229218eb4333ef2'
source=(
  "$pkgname::git+https://github.com/freeorion/freeorion.git#commit=$_commit"
  'fix-version-string.patch'
)
sha512sums=('SKIP'
            'e3287063a3128f5589d44ed780ba7e3cdfb4a60bcb0aa833f9a48e51405e3f06ba11eb89b9d59313e629deaf35f78704574bf7620607b96c161037b54a63a71f')
b2sums=('SKIP'
        '38828f5cfd0ad3cba854c3457599a851b5d0af72ec00f742f5d828fffc1ef1a6163c771f69ce42f4c39ba2cff3b82330e16fdb89b63570d0b7f71810a257eade')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//'
}

prepare() {
  patch \
    --directory "$pkgname" \
    --strip=1 \
    --input=../fix-version-string.patch
}

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
