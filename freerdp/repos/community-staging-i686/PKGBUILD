# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

pkgname=freerdp
pkgver=1.0.2
pkgrel=3
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse')
makedepends=('krb5' 'cmake' 'damageproto' 'xmlto' 'docbook-xsl')
conflicts=('freerdp-git')
changelog=${pkgname}.changelog
options=('!libtool')
source=($pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/FreeRDP/archive/$pkgver.tar.gz
        ffmpeg2.0.patch)
md5sums=('c260051a94caff590def5caa4fcf59d6'
         '1260eecf01ea5212e2db8ac9c5e7fa6d')

prepare() {
  cd $srcdir/FreeRDP-$pkgver
  patch -Np1 -i ../ffmpeg2.0.patch
}

build() {
  cd $srcdir/FreeRDP-$pkgver
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSEAUDIO=ON \
	.
  make
}

package() {
  cd $srcdir/FreeRDP-$pkgver
  make DESTDIR="${pkgdir}" install
}
