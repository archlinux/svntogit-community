# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=freetalk
pkgver=3.2
pkgrel=6
pkgdesc="A console based Jabber client"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/freetalk"
license=('GPL')
depends=('loudmouth' 'guile1.8')
install=freetalk.install
source=(http://savannah.gnu.org/download/freetalk/freetalk-$pkgver.tar.gz
        freetalk-3.2-glibc210.patch)
md5sums=('ab09bd07dde158551f151d281b14642c'
         '0da1d0a0f606d2b279fe676a80a3eb32')

prepare(){
  cd $pkgname-$pkgver
  patch -Np1 -i ../freetalk-3.2-glibc210.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-rpath \
    GUILE=/usr/bin/guile1.8 \
    GUILE_CONFIG=/usr/bin/guile-config1.8
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/share/info/dir"
}
