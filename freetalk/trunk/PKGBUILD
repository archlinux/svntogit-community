# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=freetalk
pkgver=3.2
pkgrel=5
pkgdesc="A console based Jabber client"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/freetalk"
license=('GPL')
depends=('loudmouth' 'guile')
install=freetalk.install
source=(http://savannah.gnu.org/download/freetalk/freetalk-$pkgver.tar.gz)
md5sums=('ab09bd07dde158551f151d281b14642c')

build() {
  cd $srcdir/freetalk-$pkgver

  ./configure --prefix=/usr

  patch -p0 src/callbacks.cc <<EOF
156c156,159
< 	*strchr (from, '/') = '\0';
---
> 	{
> 	char *_tmp = (char*)strchr (from, '/');
> 	*_tmp = '\0';
> 	}
EOF

  make
}

package() {
  cd $srcdir/freetalk-$pkgver
  make DESTDIR=$pkgdir install
  rm $pkgdir/usr/share/info/dir
}
