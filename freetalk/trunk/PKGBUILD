# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=freetalk
pkgver=4.1
pkgrel=2
pkgdesc="A console based Jabber client"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/freetalk"
license=('GPL')
depends=('loudmouth' 'guile' 'jansson' 'curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GNUFreetalk/freetalk/archive/v$pkgver.tar.gz")
md5sums=('3141e633e83b12083e5c0e49bbc9d150')

build() {
  cd $pkgname-$pkgver
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --disable-rpath
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm "$pkgdir/usr/share/info/dir"
}
