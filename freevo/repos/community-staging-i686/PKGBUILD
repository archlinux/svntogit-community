# $Id$
# Maintainer: Ronald van Haren  <ronald.archlinux.org>

pkgname=freevo
pkgver=1.9.0
pkgrel=6
pkgdesc="Freevo is an open-source home theatre PC platform based on Linux and a number of open-source audio/video tools."
url="http://freevo.sourceforge.net/"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('lame' 'flac' 'faad2' 'cdparanoia' 'vorbis-tools' \
	 'fbset' 'smpeg' 'pil' 'python-pysqlite-legacy' \
	 'pyxml' 'lsdvd'  'twisted' 'python2-numpy' 'python-pygame'\
	 'kaa-metadata' 'kaa-imlib2' 'python-beautifulsoup' 'python-html5lib')
optdepends=('mplayer: most features' 'xine-lib: can be used instead of mplayer' 'tvtime: watching tv' 'xmltv: parsing online tv guides')
install=freevo.install
options=('docs')
source=(http://downloads.sourceforge.net/freevo/$pkgname-$pkgver.tar.gz \
	freevo.desktop
	imdb-html5lib.patch)
sha1sums=('0533a2d4ff8a7b09b3b233fdf303ff56bda22d16'
          '5f6e76dc58496f072aca29257c24be5ffa9c6c7d'
          '57e1099527603500c594fbffa2cb7233b636eed1')

build() {
  cd $srcdir/$pkgname-$pkgver

  # patch from http://sourceforge.net/tracker/index.php?func=detail&aid=2924872&group_id=46652&atid=446895
  patch -p0 < $srcdir/imdb-html5lib.patch
  python2 setup.py install --prefix=$pkgdir/usr
  
  install -Dm644 local_conf.py.example $pkgdir/usr/share/doc/freevo/local_conf.py.example

  # install .desktop file and icon
  install -Dm644 $srcdir/$pkgname-$pkgver/share/icons/misc/freevo_app.png \
  	$pkgdir/usr/share/pixmaps/freevo.png
  install -Dm644 $srcdir/freevo.desktop \
  	$pkgdir/usr/share/applications/freevo.desktop

  # fix executable for python 2.7
  sed -i "s|search = ('python', 'python2')|search = ('python2', 'python2.7')|" $pkgdir/usr/bin/freevo

  }

