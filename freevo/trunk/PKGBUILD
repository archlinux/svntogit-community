# $Id$
# Maintainer: Ronald van Haren  <ronald.archlinux.org>

pkgname=freevo
pkgver=1.9.0
pkgrel=2
pkgdesc="Freevo is an open-source home theatre PC platform based on Linux and a number of open-source audio/video tools."
url="http://freevo.sourceforge.net/"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('lame' 'flac' 'faad2' 'cdparanoia' 'vorbis-tools' \
	 'fbset' 'smpeg' 'pil' 'python-pysqlite-legacy' \
	 'pyxml' 'lsdvd'  'twisted' 'python-numeric' 'python-pygame'\
	 'kaa-metadata' 'kaa-imlib2' 'beautiful-soup')
optdepends=('mplayer: most features' 'xine-lib: can be used instead of mplayer' 'tvtime: watching tv' 'xmltv: parsing online tv guides')
install=freevo.install
options=('docs')
source=(http://downloads.sourceforge.net/freevo/$pkgname-$pkgver.tar.gz \
	freevo.desktop
	imdb-html5lib.patch)
md5sums=('aeb05e52f996258386af2951688bf6e3'
         '9c5fcea7149bf52978730229831d1a40'
	 'b11f82049f112f9964a29d37a461fee5')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -p0 < $srcdir/imdb-html5lib.patch || return 1
  python setup.py install --prefix=$pkgdir/usr
  
  install -Dm644 local_conf.py.example $pkgdir/usr/share/doc/freevo/local_conf.py.example

  # install .desktop file and icon
  install -Dm644 $srcdir/$pkgname-$pkgver/share/icons/misc/freevo_app.png \
  	$pkgdir/usr/share/pixmaps/freevo.png
  install -Dm644 $srcdir/freevo.desktop \
  	$pkgdir/usr/share/applications/freevo.desktop
  }
