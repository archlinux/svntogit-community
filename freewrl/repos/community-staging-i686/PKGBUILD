# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=freewrl
pkgver=1.22.10
pkgrel=6
pkgdesc="VRML viewer"
arch=('i686' 'x86_64')
url="http://freewrl.sourceforge.net/"
license=('GPL')
depends=('java-runtime' 'libxaw' 'glew' 'freeglut' 'curl' 'freetype2' 'imlib2' 'sox'
	 'unzip' 'imagemagick' 'libxml2' 'ttf-bitstream-vera' 'lesstif' 'js')
makedepends=('java-environment')
options=(!libtool)
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
	build-fix.patch)
md5sums=('07fd8f193d14799ffb95a59a4887fc88'
         '52e4b6aacebcaf18cbec8975e0eb7fd8')

build() {
  . /etc/profile.d/openjdk6.sh
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/build-fix.patch
  export JAVASCRIPT_ENGINE_CFLAGS="-I/usr/include/js -DXP_UNIX -DJS_THREADSAFE $(pkg-config --cflags nspr)"
  export JAVASCRIPT_ENGINE_LIBS="$(pkg-config --libs nspr) -lmozjs185"
  ./configure \
    --prefix=/usr --with-fontsdir=/usr/share/fonts/TTF --enable-libeai \
    --enable-libcurl --with-expat=/usr --with-target=x11 --disable-plugin \
    --disable-mozilla-js --disable-xulrunner-js --disable-firefox-js \
    --disable-seamonkey-js
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
