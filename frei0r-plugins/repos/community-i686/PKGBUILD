# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=1.6.1
pkgrel=3
pkgdesc="A minimalistic plugin API for video sources and filters"
arch=(i686 x86_64)
url="https://frei0r.dyne.org/"
license=('GPL')
depends=('cairo' 'gavl')
makedepends=('doxygen' 'opencv')
optdepends=('opencv: for facebl0r and facedetect plugins')
source=(https://files.dyne.org/frei0r/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('6113D89CA825C5CEDD02C87273B35DA54ACB7D10') # Denis Roio (Jaromil)
sha256sums=('e0c24630961195d9bd65aa8d43732469e8248e8918faa942cfb881769d11515e'
            'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
