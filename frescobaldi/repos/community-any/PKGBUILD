# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=frescobaldi
pkgver=3.1.3
pkgrel=6
pkgdesc="A LilyPond sheet music text editor"
arch=(any)
url="http://www.frescobaldi.org/"
_url=https://github.com/frescobaldi/frescobaldi
license=(GPL)
depends=(
  python python-pyqt5 python-pyqt5-webengine python-ly poppler
  python-poppler-qt5 qt5-base qt5-svg qt5-webkit
)
makedepends=(python-{build,installer}
             python-setuptools
             python-wheel)
optdepends=(
  'lilypond: music engraving'
  'python-pygame: MIDI input and playback'
  'python-pycups: printing to a CUPS server'
  'hyphen-lang: hyphenation patterns for desired languages'
)
# Cherry pick patches since last tagged release, enables partial Python 3.10 support
# https://github.com/frescobaldi/frescobaldi/issues/1398
_patches=(51886dba8a3cb3db6eba9a6cf1d65bd8eaf877aa.patch
          bd4dfe036b8dbc5d75552fe6b6b081c024e5beea.patch
          eb9fa1731c1ec0bb78d74c4a0d09909cea1979eb.patch
          75dd43f86f45eb06e3af287a26c18c2be7d328ac.patch
          06f0779143c67fe775c8e5fe3291f8b9c8f4f4c6.patch
          1d538efa511676ab80594273c0247e94ac3a4927.patch
          23ebdadc59d5e52c3a514814cf5858450cdcdd2c.patch
          0c3ccac837883de2110c9290e8c34e24f44dc7b1.patch
          7a0dfb5421048b010a06032b7d141456afae985b.patch
          67a2aef6a66fadf3e36bcd7a49d9964fdb38311a.patch)
source=(${_url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${_patches[@]/#/$_url/commit/})
sha256sums=('9c7f5036f367691454753f1db06ea02c33501c1e4f24bfb47485103911f3e9c1'
            '700b5ee43ff85bcedbe374415bcb0dd36f784b1d170323d32c14b32038a61bda'
            'd640b1aeafddab7f7ffd2195bb896b1c2b94d0fc51624bc782a265b48206e3e6'
            '550219c3e55b88c9dfe179f7c9ec743f7881466c3655a0e3f7fcb1148b0090af'
            '9817b922812cec024d05f353ad0d5ade227244ff510606556e5c1bfc1acc417f'
            '497d79f0ae33d956fdf493817528969f3843ff280a219186357c00c9b36c5fd8'
            'b6816e211b10dee48832bdd381d1da19c61c4355b370968a3cacd3c6de12d241'
            '596f501b007170a1d13823dee862f8a0ef14984b4ac8211b9961f9f2153c74c6'
            '37e7006075d684aabb6b53a7c6bfa553739f6b768e76154dbde7dfaad6a24c03'
            'c861ebd34f87b715f14e9154c4a80910eb3690efc4e0c9f7749225fa337cca22'
            'e767998a0af59b6b13fb8da5a8665d5adfd35eaf73e77c5bbd6fb79befa6869e')

prepare() {
  cd ${pkgname}-${pkgver}
  for p in ${_patches[@]}; do
    patch -p1 < ../$p
  done
  # Provided by hyphen-*
  rm -f frescobaldi_app/hyphdicts/hyph_*.dic
  rm -f frescobaldi_app/hyphdicts/README*
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build -wn
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer -d "${pkgdir}" dist/*.whl
}
