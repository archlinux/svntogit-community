# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=frostwire
pkgver=5.7.6
pkgrel=2
pkgdesc="BitTorrent client with integrated search"
arch=('any')
url="http://www.frostwire.com"
license=('GPL')
depends=('java-runtime' 'mplayer' 'hicolor-icon-theme' 'desktop-file-utils'
         'gtk-update-icon-cache')
install=frostwire.install
source=(http://dl.frostwire.com/$pkgname/$pkgver/$pkgname-$pkgver.noarch.tar.gz
        frostwire.sh
        icons.tar.gz
        java-version-detection.patch)
sha256sums=('b3356c2dd4ae06e6c205e58c650f3aea617c4d126ecfbd174038951456ec10b8'
            '9a05e5f1005856cd67f6e01fc499d1b64bc372bf3a390e412359196f65cc0ea6'
            '00e425501b33b7213a337314a5035d52459f743f623f9f37abbc60d6e56a2186'
            '0746842535ae17dc63ce5a8cf9e29c319b7a837d9b3aa0076de0e85c11fbe539')

package() {
  cd "$srcdir/$pkgname-$pkgver.noarch"

  # FS#15544 (Java version detection fails when _JAVA_OPTIONS is set)
  patch -Np0 -i "$srcdir/java-version-detection.patch"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -r * "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/icons" "$pkgdir/usr/share"

  install -D "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/icons/hicolor/16x16/apps/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Remove uneeded files
  rm -rf "$pkgdir"/usr/share/frostwire/{runFrostwireOSX.sh,unpack200.py,root/{.svn,magnet10/.svn}}
}

# vim:set ts=2 sw=2 et:
