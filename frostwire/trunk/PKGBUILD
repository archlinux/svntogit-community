# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=frostwire
pkgver=5.0.8
pkgrel=1
pkgdesc="BitTorrent client with integrated search"
arch=('any')
url="http://www.frostwire.com"
license=('GPL')
depends=('java-runtime' 'hicolor-icon-theme')
optdepends=('mplayer: audio playback'
            'vlc: video playback')
install=$pkgname.install
source=(http://dl.frostwire.com/frostwire/$pkgver/frostwire-$pkgver.noarch.tar.gz
        $pkgname
        icons.tar.gz
        java-version-detection.patch)
md5sums=('32119c5c8e0d701887914e2547dcadfe'
         'aec856d2e83205b7f17c1da7a3f78763'
         'dbea03ff4e35cc6c190976282682c2cf'
         'b9ffc80668dcc5e085be4476908cd3b9')

build() {
  cd "$srcdir/$pkgname-$pkgver.noarch"

  # FS#15544 (Java version detection fails when _JAVA_OPTIONS is set)
  patch -Np0 -i "$srcdir/java-version-detection.patch"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -r * "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/icons" "$pkgdir/usr/share"

  install -D "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/icons/hicolor/16x16/apps/$pkgname.png" \
                 "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Remove uneeded files
  rm -rf "$pkgdir"/usr/share/frostwire/{runFrostwireOSX.sh,unpack200.py,root/{.svn,magnet10/.svn}}
}

# vim:set ts=2 sw=2 et:
