# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=fscrypt
pkgver=0.3.2
pkgrel=1
pkgdesc='A tool for managing Linux filesystem encryption'
arch=('x86_64')
url='https://github.com/google/fscrypt'
license=('Apache')
makedepends=('go')
depends=('pam')
source=("fscrypt-$pkgver.zip::https://github.com/google/fscrypt/archive/v$pkgver.zip"
        "pam_config")
sha512sums=('0c2414b55e22b314aa771c3f205c45c2c7f45f51fed48d10006f60fb3371383d91d4c8a57f159bedfd4cf20e86ea4874531df511bafda531f5b9a25424768ecd'
            '56f443cd2fbed9c164693c800cd8dc08bb35b88bb9c912fb35eb43cc6c6bc3b966d22d9810f43580d12b9faa8d753419a06336f08c47a0e602c48a103a705299')

build() {
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd "fscrypt-$pkgver"
  BUILDDATE="`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}"`" make
}

package() {
  cd "fscrypt-$pkgver"
  make PREFIX="${pkgdir}/usr" install
  install -Dm644 ../pam_config "${pkgdir}/etc/pam.d/fscrypt"
  install -Dm644 -t "$pkgdir/usr/share/licenses/fscrypt/" LICENSE

  # Remove Ubuntu-specific PAM file
  rm -r "$pkgdir"/usr/share/pam-configs/
}
