# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fstrm
pkgver=0.5.0
pkgrel=1
pkgdesc="A C implementation of the Frame Streams data transport protocol"
arch=('x86_64')
url="https://github.com/farsightsec/fstrm"
license=('Apache')
depends=('libevent')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('ca762fb260176f35af639376cc65c3f13a5f63d1cca42eb5ce87caca8d57c450')

build() {
	cd ${pkgname}-${pkgver}
    autoreconf -vfi
	./configure --prefix=/usr
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install

	install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

