# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Philipp Robbel <robbel@gmail.com>

pkgname=fswebcam
pkgver=20110717
pkgrel=1
pkgdesc="Tiny and flexible webcam app."
arch=('i686' 'x86_64')
url="http://www.firestorm.cx/fswebcam"
license=('GPL')
depends=('gd')
backup=("etc/$pkgname.conf")
source=("$url/files/$pkgname-$pkgver.tar.gz")
md5sums=('6e09f1f31cf7cd0ac1d4d688a0acbe41')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Install configuration file
  # Commented to avoid problems with many devices, thank you SpepS
  sed "s_^_#_g" -i example.conf
  install -Dm644 example.conf "$pkgdir/etc/fswebcam.conf"
}

# vim:ts=2:sw=2:expandtab
