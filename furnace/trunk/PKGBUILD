# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=furnace
pkgver=0.6pre1.5
pkgrel=1
pkgdesc='A multi-system chiptune tracker compatible with DefleMask modules'
arch=('x86_64')
url='https://github.com/tildearrow/furnace'
license=('GPL2')
depends=(
  'alsa-lib'
  'hicolor-icon-theme'
  'sdl2'
  'libsndfile'
  'fmt'
  'rtmidi'
  'fftw'
)
makedepends=(
  'git'
  'cmake'
  'jack'
)
optdepends=('jack: JACK audio support')
_commit='d354f58a7c924567e62111999888aefca8d92d8d'
source=(
  "$pkgname::git+$url#commit=$_commit"
  'github.com-nukeykt-Nuked-OPN2::git+https://github.com/nukeykt/Nuked-OPN2'
  'github.com-superctr-adpcm::git+https://github.com/superctr/adpcm'
)
b2sums=('SKIP'
        'SKIP'
        'SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # prepare git submodules
  git submodule init \
    extern/Nuked-OPN2 \
    extern/adpcm

  git config submodule.extern/Nuked-OPN2.url "$srcdir/github.com-nukeykt-Nuked-OPN2"
  git config submodule.extern/adpcm.url "$srcdir/github.com-superctr-adpcm"

  git submodule update
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_GUI=ON \
    -DSYSTEM_RTMIDI=ON \
    -DSYSTEM_FMT=ON \
    -DSYSTEM_ZLIB=ON \
    -DSYSTEM_LIBSNDFILE=ON \
    -DSYSTEM_SDL2=ON \
    -DSYSTEM_FFTW=ON \
    -DWITH_JACK=ON

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build  
}
