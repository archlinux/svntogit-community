# $Id$
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=fuseiso
pkgver=20070708
pkgrel=3
pkgdesc="FUSE module to mount ISO filesystem images"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/fuseiso/"
license=('GPL')
depends=('fuse' 'glib2' 'zlib')
makedepends=('pkgconfig')
source=("http://ubiz.ru/dm/${pkgname}-${pkgver}.tar.bz2"
	'fuseiso-20070708-largeiso.patch')
md5sums=('4bb50412b6d01f337565e28afddca3a5'
         '5c4dce932aba735727221f4e1695f277')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Fix for isos larger than 4Go https://bugzilla.redhat.com/show_bug.cgi?id=440436
  patch -p1 -i "${srcdir}/fuseiso-20070708-largeiso.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
