# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fuzzel
pkgver=1.8.1
pkgrel=1
pkgdesc='Application launcher for wlroots based Wayland compositors'
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
makedepends=(meson
             scdoc
             tllist
             wayland-protocols)
depends=(cairo
         fcft
         libfcft.so
         libpng
         libxkbcommon
         pixman
         wayland) # nanosvg is bundled
options=(debug)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('f1dcd487d1cdb9f37be2535184503c265f5df5bd2571e9e0dfc193d00fd08aa2')

build() {
	cd "$pkgname"
	arch-meson \
		-Denable-cairo=enabled \
		-Dpng-backend=libpng \
		-Dsvg-backend=nanosvg \
		. build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
