# $Id
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fwbuilder
pkgver=5.1.0.3599
pkgrel=3
pkgdesc="Object-oriented GUI and set of compilers for various firewall platforms"
url="http://www.fwbuilder.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libxslt' 'net-snmp' 'qt4')
conflicts=('libfwbuilder')
install=fwbuilder.install
source=("http://sourceforge.net/projects/fwbuilder/files/Current_Packages/${pkgver%.*}/fwbuilder-${pkgver}.tar.gz"
		'fwbuilder-gcc47.patch')
sha256sums=('452514a1ec0be1416bfca93603e6c89deb91d1a3a19671c64b5a8868a3743daf'
			'd7c602d5b99c6e40403c0b02e34abf15de83802257ca5ce80ad53d486b692f27')

build() {
  cd "${srcdir}/fwbuilder-${pkgver}"
  patch -p1 -i "$srcdir/fwbuilder-gcc47.patch"
  export CXXFLAGS="$CXXFLAGS -fno-var-tracking-assignments"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/fwbuilder-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
}
