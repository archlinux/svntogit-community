# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupdate
pkgver=9
pkgrel=3
pkgdesc="Tools for using the ESRT and UpdateCapsule() to apply firmware updates"
arch=('i686' 'x86_64')
url="https://github.com/rhinstaller/fwupdate"
license=('GPL2')
depends=('efivar' 'libsmbios' 'bash')
makedepends=('pesign' 'gnu-efi-libs')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
	    'fix-uninitialized-variable.patch'
	    'fix-sprintf-formatting.patch'
        'create-fw-directory.patch')
sha256sums=('e926a7b33a58f5dbf029a5a687375e88b18a41f0742ba871aff7d1d82d075c87'
            '56d27c36b7f1178c818c37153e3dbdd0a26036366b3dc935cd169b0c716cb610'
            '639114a5d98c688f92c59d08aac1b182da705ad443c8b1367160c7a8c4fcd115'
            '980ee0b2f08bea482575bfd6323dae907f44ff1ce2981f743c014809fb51b8d2')
install=fwupdate.install

_efidir=arch

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../fix-uninitialized-variable.patch
    patch -p1 -i ../fix-sprintf-formatting.patch
    patch -p1 -i ../create-fw-directory.patch
}

build() {
    cd ${pkgname}-${pkgver}
    make EFIDIR="${_efidir}" GNUEFIDIR=/usr/lib
}

package() {
    cd ${pkgname}-${pkgver}

    make LIBDIR=/usr/lib EFIDIR="${_efidir}" DESTDIR="${pkgdir}" libexecdir=/usr/lib/ install

    # Do not install anything under /boot. Copy files to /usr/lib/fwupdate for manual installation.
    install -d ${pkgdir}/usr/lib/fwupdate
    mv ${pkgdir}/boot/efi/EFI ${pkgdir}/usr/lib/fwupdate/EFI
    rm -rf ${pkgdir}/boot
    rm -rf ${pkgdir}/usr/src
    rm -rf ${pkgdir}/usr/lib/debug
    rmdir  ${pkgdir}/usr/share/fwupdate
}
