# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sander Jansen <sander@knology.net>

pkgname=fxprocessview
pkgver=0.5.0
pkgrel=5
pkgdesc="Process Viewer"
arch=('i686' 'x86_64')
license=('GPL')
depends=('fox>=1.4.0')
makedepends=('glu')
url="http://code.google.com/p/fxdesktop/"
source=(http://archlinux-stuff.googlecode.com/files/$pkgname-$pkgver.tar.gz \
	build-fix.patch)
md5sums=('5d3cc8d7aec770997c281a743ddfda5a'
	 '2d2c3d54dcd2404149955f12cccb21a5')

build() {
  cd $srcdir/$pkgname-$pkgver
  # Override default flags
  export CFLAGS="-Wall -O2 -pipe"
  export CXXFLAGS="-Wall -O2 -pipe"
  # Compile
  patch -Np1 <../build-fix.patch
  ./gb
  # gb does not return valid error code
  [ -f src/fxprocessview ]
}

package() {
  cd $srcdir/$pkgname-$pkgver
  # make sure destination exists
  mkdir -p $pkgdir/usr/bin
  # Install
  ./gb install --package-root=$pkgdir/usr
}
