# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
_pkgver=1.4.5
#_fixupver=2
pkgver=${_pkgver}
#pkgver=${_pkgver}.r${_fixupver}
pkgrel=2
pkgdesc='Full featured and easy to use XMPP (Jabber) client'
url='https://gajim.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'gtksourceview4' 'python-cairo' 'python-gobject' 'python-keyring' 'python-nbxmpp'
         'python-pyasn1' 'python-pyopenssl' 'python-precis_i18n' 'python-css-parser'
         'python-distro' 'hicolor-icon-theme' 'python-pillow' 'python-gssapi' 'python-netifaces'
         'python-packaging')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
optdepends=('python-dbus: for gajim-remote and zeroconf support'
            'avahi: serverless chatting with autodetected clients in a local network'
            'farstream: start audio and video chat'
            'gst-plugins-good: for video/voice support'
            'gst-plugins-bad: for video/voice support'
            'gst-plugins-ugly: for video/voice support'
            'gst-libav: for video/voice support'
            'gst-python: for video/voice support'
            'gspell: for spell checking support'
            'geoclue: share current location'
            'gupnp-igd: request your router to forward port for file transfer'
            'libxss: measure idle time, in order to set auto status'
            'python-gnupg: encrypting chat messages with OpenPGP'
            'python-axolotl: OMEMO support'
            'python-qrcode: generate QR codes for OMEMO keys'
            'gsound: Notification Sounds')
source=(https://www.gajim.org/downloads/${_pkgver%.*}/gajim-${_pkgver}.tar.gz)
#source=(https://www.gajim.org/downloads/${_pkgver%.*}/gajim-${_pkgver}-${_fixupver}.tar.gz)
sha512sums=('bce88fc20f0c0571116e9145af222efab67b82184a87f40ddaba5f802cf5034caa1ea0feb89f518332cd5bd6a1e82ec1ca15199966e57aaf50701998c3a5edd8')
b2sums=('1c49f8eef8284b20beee933fda519729ff2007f8fb988ed900ba47531b7538046f7093aa142810f3b6686323f8b802df23c26b3065e62a980406da77928f7aee')

build() {
  cd ${pkgname}-${_pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}-${_pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
