# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=7.0.2
pkgrel=2
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libcanberra
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter43
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=c8b0fcbc4023dfecad451e630cb8c496c4525a9f
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd gala
  git describe --tags
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
