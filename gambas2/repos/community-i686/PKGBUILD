# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Toni Foerster <stonerl@skeps.de>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=gambas2
pkgver=2.14.0
pkgrel=2
pkgdesc="A free development environment based on a Basic interpreter."
arch=('i686' 'x86_64')
url="http://gambas.sourceforge.net"
depends=('librsvg' 'postgresql-libs' 'libmysqlclient' \
         'curl' 'poppler' 'sqlite3' 'sdl_mixer' 'sdl_image' \
         'unixodbc' 'kdelibs3' 'libffi' 'omniorb' 'xdg-utils' 'libxtst' 'libfbclient' 'sqlite2')
makedepends=('intltool' 'mysql' 'postgresql')
optdepends=('rpm: to create a rpm package of your Gambas Project'
	'debhelper: to create a deb package of your Gambas Project')
license=('GPL2')
options=('!makeflags' '!libtool')
source=(http://downloads.sourceforge.net/gambas/$pkgname-$pkgver.tar.bz2
	'gambas2.desktop' 'gambas2.png')
install=gambas2.install

build() {
  cd ${srcdir}/$pkgname-$pkgver
  
  ## necessary for corba fix
  cd gb.corba && ./reconf && cd ..
  ##

  . /etc/profile.d/kde3.sh
  . /etc/profile.d/qt3.sh

  ./configure -C --disable-qte --prefix=/usr
  make bindir=${pkgdir}/usr/bin || return 1
  make XDG_UTILS='' DESTDIR=${pkgdir} install || return 1

  ## needed for postinst with xdg-utils
  install -d -m755 ${pkgdir}/usr/share/gambas2/mime || return 1
  install -D -m644 main/mime/* \
    ${pkgdir}/usr/share/gambas2/mime/ || return 1
  ##

  install -d -m755 ${pkgdir}/usr/share/gambas2/icons || return 1
  install -D -m644 main/mime/application-x-gambas.png \
    ${pkgdir}/usr/share/gambas2/icons/application-x-gambas.png ||  return 1
  install -D -m644 ${srcdir}/gambas2.desktop \
    ${pkgdir}/usr/share/applications/gambas2.desktop || return 1
  install -D -m644 ${srcdir}/gambas2.png \
    ${pkgdir}/usr/share/pixmaps/gambas2.png || return 1

  #fix help
  chown root:root -R ${pkgdir}/usr/share/gambas2/help/help || return 1
}

md5sums=('bb63c4ef4925b808d502461daad7ac6e'
         'e6c2df38b29fcb6ba9ac7ba783688211'
         '32672c984d8487beb13aa5b6bc6868e2')
