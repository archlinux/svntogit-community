# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=gambas3
pkgname=('gambas3-runtime' 'gambas3-devel' 'gambas3-ide' 'gambas3-script' 'gambas3-examples' 'gambas3-gb-cairo' 'gambas3-gb-chart'
         'gambas3-gb-dbus' 'gambas3-gb-compress' 'gambas3-gb-crypt' 'gambas3-gb-db' 'gambas3-gb-db-form' 'gambas3-gb-db-mysql'
         'gambas3-gb-db-odbc' 'gambas3-gb-db-postgresql' 'gambas3-gb-db-sqlite2' 'gambas3-gb-db-sqlite3'
         'gambas3-gb-desktop' 'gambas3-gb-eval-highlight' 'gambas3-gb-form'
         'gambas3-gb-form-dialog' 'gambas3-gb-form-mdi' 'gambas3-gb-form-stock' 'gambas3-gb-gtk' 'gambas3-gb-gui' 'gambas3-gb-image'
         'gambas3-gb-image-effect' 'gambas3-gb-image-imlib' 'gambas3-gb-image-io' 'gambas3-gb-net' 'gambas3-gb-net-curl'
         'gambas3-gb-net-smtp' 'gambas3-gb-opengl' 'gambas3-gb-opengl-glu' 'gambas3-gb-opengl-glsl' 'gambas3-gb-option' 'gambas3-gb-pcre'
         'gambas3-gb-pdf' 'gambas3-gb-qt4' 'gambas3-gb-qt4-ext' 'gambas3-gb-qt4-opengl' 'gambas3-gb-qt4-webkit'
         'gambas3-gb-report' 'gambas3-gb-sdl' 'gambas3-gb-sdl-sound' 'gambas3-gb-settings' 'gambas3-gb-signal'
         'gambas3-gb-v4l' 'gambas3-gb-vb' 'gambas3-gb-xml' 'gambas3-gb-xml-rpc' 'gambas3-gb-xml-xslt' 'gambas3-gb-web') 
pkgver=2.99.5
pkgrel=1
pkgdesc="A free development environment based on a Basic interpreter."
arch=('i686' 'x86_64')
url="http://gambas.sourceforge.net/"
license=('GPL')
groups=('gambas3')
makedepends=('intltool' 'mysql' 'postgresql' 'libffi' 'bzip2' 'glib2' 'v4l-utils'
         'zlib' 'mesa' 'libgl' 'glew>=1.7.0' 'xdg-utils' 'qt' 'gtk2' 'imlib2' 'gdk-pixbuf2'
         'postgresql-libs' 'libmysqlclient' 'unixodbc' 'sqlite2' 'sqlite3' 'librsvg'
         'curl' 'poppler-glib' 'sdl_mixer' 'sdl_ttf' 'libxtst' 'pcre'
         'libxcursor' 'libsm' 'dbus-core' 'libxml2' 'libxslt' 'libgnome-keyring')
options=('!emptydirs' '!makeflags')
source=("http://downloads.sourceforge.net/gambas/${pkgbase}-${pkgver}.tar.bz2"
        'gambas3-script.install' 'gambas3-runtime.install')
md5sums=('dfa16f5208463e81ba8ca801948c353a'
         'b284be39d147ec799f1116a6abc068b4'
         'b5cc403990f31b8ea1c5cf37366d3d29')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./reconf-all
  ./configure --prefix=/usr -C

  make bindir=${pkgdir}/usr/bin
}

package_gambas3-runtime() {
  depends=('libffi' 'xdg-utils')
  pkgdesc="Gambas3 runtime environment"
  install=gambas3-runtime.install

  cd ${srcdir}/${pkgbase}-${pkgver}/main/gbc
  make DESTDIR="${pkgdir}" install

  cd ../gbx
  make DESTDIR="${pkgdir}" install
  cd ../lib/debug
  make DESTDIR="${pkgdir}" install
  cd ../eval
  make DESTDIR="${pkgdir}" install
  cd ../draw
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../gb.component \
    ${pkgdir}/usr/lib/gambas3

  cd ${srcdir}/${pkgbase}-${pkgver}/main
  ln -s gbx3 ${pkgdir}/usr/bin/gbr3
  gbc/gbi3 -r ${pkgdir}/usr gb
  rm -f ${pkgdir}/usr/lib/gambas3/gb.{so*,la}

  ## needed for postinst with xdg-utils
  install -d -m755 ${pkgdir}/usr/share/gambas3/mime
  install -D -m644 mime/* \
    ${pkgdir}/usr/share/gambas3/mime/
  install -d -m755 ${pkgdir}/usr/share/gambas3/icons
  install -D -m644 mime/application-x-gambas3.png \
    ${pkgdir}/usr/share/gambas3/icons/application-x-gambas3.png
 
  cd gbc
  make DESTDIR="${pkgdir}" uninstall
}

package_gambas3-devel() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 development environment"

  cd ${srcdir}/${pkgbase}-${pkgver}/main/gbc
  make DESTDIR="${pkgdir}" install
}

package_gambas3-ide() {
  depends=('gambas3-devel' 'gambas3-gb-db-form' 'gambas3-gb-desktop' 'gambas3-gb-eval-highlight'
           'gambas3-gb-form-dialog' 'gambas3-gb-settings' 'gambas3-gb-form-mdi' 'gambas3-gb-image-effect'
           'gambas3-gb-qt4-ext' 'gambas3-gb-qt4-webkit')
  pkgdesc="Gambas3 Integrated Development Environment"

  cd ${srcdir}/${pkgbase}-${pkgver}
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

#!! with the ide !!
  install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/app/desktop/gambas3.desktop \
    ${pkgdir}/usr/share/applications/gambas3.desktop
  install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/app/desktop/gambas3.png \
    ${pkgdir}/usr/share/pixmaps/gambas3.png

  rm -r ${pkgdir}/usr/bin/gb*
  rm -r ${pkgdir}/usr/lib
  rm -r ${pkgdir}/usr/share/gambas3
}

package_gambas3-script() {
  depends=('gambas3-devel')
  pkgdesc="Gambas3 scripter and server programs support"
  install=gambas3-script.install

  cd ${srcdir}/${pkgbase}-${pkgver}
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## needed for postinst with xdg-utils
  cd ${srcdir}/${pkgbase}-${pkgver}/app/mime
  install -d -m755 ${pkgdir}/usr/share/gambas3/mime
  install -D -m644 *.xml ${pkgdir}/usr/share/gambas3/mime/
  install -D -m644 *.png ${pkgdir}/usr/share/gambas3/mime/
  ##

  rm -r ${pkgdir}/usr/bin/{gambas*,gb{[a-r]*,x*}}
  rm -r ${pkgdir}/usr/lib
  rm -r ${pkgdir}/usr/share/gambas3/[c-i]*
}

package_gambas3-examples() {
  depends=('gambas3-gb-cairo' 'gambas3-gb-chart' 'gambas3-gb-dbus' 'gambas3-gb-compress' 'gambas3-gb-crypt' 'gambas3-gb-db'
           'gambas3-gb-db-form' 'gambas3-gb-db-mysql' 'gambas3-gb-db-odbc' 'gambas3-gb-db-postgresql' 'gambas3-gb-db-sqlite2'
           'gambas3-gb-db-sqlite3' 'gambas3-gb-desktop' 'gambas3-gb-eval-highlight' 'gambas3-gb-form' 'gambas3-gb-form-dialog'
           'gambas3-gb-form-mdi' 'gambas3-gb-gtk' 'gambas3-gb-gui' 'gambas3-gb-image' 'gambas3-gb-image-effect' 'gambas3-gb-image-imlib'
           'gambas3-gb-image-io' 'gambas3-gb-net' 'gambas3-gb-net-curl' 'gambas3-gb-net-smtp' 'gambas3-gb-opengl' 'gambas3-gb-opengl-glsl'
           'gambas3-gb-option' 'gambas3-gb-pcre' 'gambas3-gb-pdf' 'gambas3-gb-qt4' 'gambas3-gb-qt4-ext' 'gambas3-gb-qt4-opengl'
           'gambas3-gb-qt4-webkit' 'gambas3-gb-report' 'gambas3-gb-sdl' 'gambas3-gb-sdl-sound' 'gambas3-gb-settings' 'gambas3-gb-signal'
           'gambas3-gb-v4l' 'gambas3-gb-vb' 'gambas3-gb-xml' 'gambas3-gb-xml-rpc' 'gambas3-gb-xml-xslt' 'gambas3-gb-web')
  pkgdesc="Gambas3 examples"

  cd ${srcdir}/${pkgbase}-${pkgver}
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  rm -r ${pkgdir}/usr/{bin,lib}
  rm -r ${pkgdir}/usr/share/gambas3/{info,control,gb.sdl}
}

package_gambas3-gb-cairo() {
  depends=('gambas3-runtime' 'cairo')
  pkgdesc="Gambas3 cairo component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.cairo
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-chart() {
  depends=('gambas3-gb-form')
  pkgdesc="Gambas3 chart component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/share/gambas3/control
  rm -r ${pkgdir}/usr/lib/gambas3/gb.[d-w]*
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.[d-w]*
  ##
}

package_gambas3-gb-compress() {
  depends=('gambas3-runtime' 'bzip2' 'zlib')
  pkgdesc="Gambas3 compression support component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.compress.bzlib2
  make DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.compress.zlib
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/{gb.component,gb.[d-v]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.[d-v]*
  ##
}

package_gambas3-gb-crypt() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 MD5/DES crypting component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.crypt
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-db() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 database access component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  ## Workaround for splitting
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/{gb.comp*,gb.d{e,r}*,gb.[e-z]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/{gb.comp*,gb.de*,gb.[e-z]*}
  ##
}

package_gambas3-gb-db-form() {
  depends=('gambas3-gb-db' 'gambas3-gb-form')
  pkgdesc="Gambas3 database form component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/share/gambas3/control
  rm -r ${pkgdir}/usr/lib/gambas3/gb.{chart*,[e-z]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.{chart*,[e-z]*}
  ##
}

package_gambas3-gb-db-mysql() {
  depends=('gambas3-gb-db' 'libmysqlclient')
  pkgdesc="Gambas3 MySQL database access component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.db.mysql
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-db-odbc() {
  depends=('gambas3-gb-db' 'unixodbc')
  pkgdesc="Gambas3 ODBC database access component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.db.odbc
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-db-postgresql() {
  depends=('gambas3-gb-db' 'postgresql-libs')
  pkgdesc="Gambas3 PostgreSQL database access component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.db.postgresql
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-db-sqlite2() {
  depends=('gambas3-gb-db' 'sqlite2')
  pkgdesc="Gambas3 Sqlite2 database access component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.db.sqlite2
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-db-sqlite3() {
  depends=('gambas3-gb-db' 'sqlite3')
  pkgdesc="Gambas3 Sqlite3 database access component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.db.sqlite3
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-desktop() {
  depends=('gambas3-gb-image' 'libsm' 'libxtst' 'libgnome-keyring')
  pkgdesc="Gambas3 desktop component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.desktop
  make DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-dbus() {
  depends=('gambas3-runtime' 'dbus-core')
  pkgdesc="Gambas3 DBUS component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.dbus
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-eval-highlight() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 expression evaluator highlight component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/lib/gambas3/gb.[f-z]*
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.[f-z]*
  ##
}

package_gambas3-gb-form() {
  depends=('gambas3-gb-gui')
  pkgdesc="Gambas3 form component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/share/gambas3/control/{gb.db*,gb.form.*,gb.report}
  rm -r ${pkgdir}/usr/lib/gambas3/gb.{[c-e]*,[m-w]*,form.{d*,m*,s*}}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.{[c-e]*,[m-w]*,form.{d*,m*,s*}}
  ##
}

package_gambas3-gb-form-dialog() {
  depends=('gambas3-gb-form')
  pkgdesc="Gambas3 form dialog component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/share/gambas3/control
  rm -r ${pkgdir}/usr/lib/gambas3/gb.{[c-e]*,[m-w]*,form.{c*,g*,m*,s*}}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.{[c-e]*,[m-w]*,form.{i*,l*,m*,s*}}
  ##
}

package_gambas3-gb-form-mdi() {
  depends=('gambas3-gb-form')
  pkgdesc="Gambas3 form MDI component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/share/gambas3/control/{gb.db*,gb.form,gb.report}
  rm -r ${pkgdir}/usr/lib/gambas3/gb.{[c-e]*,[m-w]*,form.{c*,d*,g*,s*}}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.{[c-e]*,[m-w]*,form.{i*,l*,d*,s*}}
  ##
}

package_gambas3-gb-form-stock() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 default stock icons component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/lib/gambas3/gb.{eval*,[m-w]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.{eval*,[m-w]*}
  ##
}

package_gambas3-gb-gtk() {
  depends=('gambas3-gb-image' 'gtk2' 'librsvg')
  pkgdesc="Gambas3 graphical GTK+ toolkit component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.gtk
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-gui() {
  depends=('gambas3-gb-qt4' 'gambas3-gb-gtk')
  pkgdesc="Gambas3 automatic gui toolkit chooser"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/main/lib/gui
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/{gb.[c-e]*,gb.[i-z]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/{gb.[c-e]*,gb.[i-z]*}
  ##
}

package_gambas3-gb-image() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 image component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  ## cleanup the workaround
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/{gb.[c-g]*,gb.image.e*,gb.[j-z]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/{gb.[c-g]*,gb.info,gb.image.e*,gb.[j-z]*}
  ##
}

package_gambas3-gb-image-effect() {
  depends=('gambas3-gb-image')
  pkgdesc="Gambas3 image effect component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  ## cleanup the workaround
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/{gb.[c-g]*,gb.image.{c*,l*,s*},gb.[j-z]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/{gb.[c-g]*,gb.info,gb.image.{i*,l*},gb.[j-z]*}
  ##
}

package_gambas3-gb-image-imlib() {
  depends=('gambas3-gb-image' 'imlib2')
  pkgdesc="Gambas3 imlib component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.image.imlib
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-image-io() {
  depends=('gambas3-gb-image' 'gdk-pixbuf2')
  pkgdesc="Gambas3 input/output component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.image.io
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-net() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 network component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.net
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-net-curl() {
  depends=('gambas3-gb-net' 'curl')
  pkgdesc="Gambas3 curl component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.net.curl
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-net-smtp() {
  depends=('gambas3-runtime' 'glib2')
  pkgdesc="Gambas3 SMTP component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.net.smtp
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-opengl() {
  depends=('gambas3-runtime' 'libgl' 'glew')
  pkgdesc="Gambas3 OpenGL component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.opengl
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.opengl/src/glsl
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.opengl/src/glu
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-opengl-glu() {
  depends=('gambas3-gb-opengl')
  pkgdesc="Gambas3 GL Utility component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.opengl/src/glu
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-opengl-glsl() {
  depends=('gambas3-gb-opengl')
  pkgdesc="Gambas3 GLSL component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.opengl/src/glsl
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-option() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 getopt component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  ## cleanup the workaround
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/{gb.[a-n]*,gb.[p-z]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/{gb.[a-n]*,gb.[p-z]*}
  ##
}

package_gambas3-gb-pcre() {
  depends=('gambas3-runtime' 'pcre')
  pkgdesc="Gambas3 PCRE component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.pcre
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-pdf() {
  depends=('gambas3-runtime' 'poppler')
  pkgdesc="Gambas3 PDF component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.pdf
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-qt4() {
  depends=('gambas3-gb-image' 'qt')
  pkgdesc="Gambas3 Qt4 toolkit component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4/src/ext
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4/src/opengl
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4/src/webkit
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-qt4-ext() {
  depends=('gambas3-gb-qt4')
  pkgdesc="Gambas3 Qt4 toolkit extended component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4/src/ext
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-qt4-opengl() {
  depends=('gambas3-gb-qt4' 'libgl')
  pkgdesc="Gambas3 Qt4 toolkit OpenGL component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4/src/opengl
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-qt4-webkit() {
  depends=('gambas3-gb-qt4')
  pkgdesc="Gambas3 Qt4 toolkit webkit component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4/src/webkit
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-report() {
  depends=('gambas3-gb-form' 'gambas3-gb-image-io')
  pkgdesc="Gambas3 report component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.qt4
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/share/gambas3/control/gb.[d-f]*
  rm -r ${pkgdir}/usr/lib/gambas3/gb.{[c-m]*,[s-w]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.{[c-m]*,[s-w]*}
  ##
}

package_gambas3-gb-sdl() {
  depends=('gambas3-gb-image-io' 'sdl_ttf' 'libxcursor' 'glew' 'libgl')
  pkgdesc="Gambas3 SDL component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.sdl
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-sdl-sound() {
  depends=('gambas3-runtime' 'sdl_mixer')
  pkgdesc="Gambas3 SDL sound component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.sdl.sound
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-settings() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 setting component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/lib/gambas3/gb.{[c-r]*,[t-w]*}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.{[c-r]*,[t-w]*}
  ##
}

package_gambas3-gb-signal() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 signal component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  ## cleanup the workaround
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/{gb.[c-r]*,gb.[t-z]*,gb.so*}
  rm -r ${pkgdir}/usr/share/gambas3/info/{gb.[c-r]*,gb.[t-z]*}
  ##
}

package_gambas3-gb-v4l() {
  depends=('gambas3-runtime' 'v4l-utils' 'libjpeg' 'libpng')
  pkgdesc="Gambas3 video4linux component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.v4l
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-vb() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 VB transitional component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  ## Workaround for splitting
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/lib/gambas3/gb.[a-u]*
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.[a-u]*
  ##
}

package_gambas3-gb-xml() {
  depends=('gambas3-runtime' 'libxml2')
  pkgdesc="Gambas3 XML component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.xml
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.xml/src/xslt
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.xml/src/rpc
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-xml-rpc() {
  depends=('gambas3-gb-xml' 'gambas3-gb-net' 'gambas3-gb-net-curl')
  pkgdesc="Gambas3 XML-RPC component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.net
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.net.curl
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.xml
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.net
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/gb.net.curl
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/lib/gambas3/gb.xml.{[c-l]*,xslt*,so*}
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.xml.{[i-l]*,x*}
  ##
}

package_gambas3-gb-xml-xslt() {
  depends=('gambas3-gb-xml' 'libxslt')
  pkgdesc="Gambas3 XML-XSLT component"

  ## workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/gb.xml/src/xslt
  make XDG_UTILS='' DESTDIR="${pkgdir}" install

  ## cleanup the workaround
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  ##
}

package_gambas3-gb-web() {
  depends=('gambas3-runtime')
  pkgdesc="Gambas3 CGI component"

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" install
  ##

  cd ${srcdir}/${pkgbase}-${pkgver}/comp
  make DESTDIR="${pkgdir}" install

  ## Workaround for splitting
  cd ${srcdir}/${pkgbase}-${pkgver}/main
  make XDG_UTILS='' DESTDIR="${pkgdir}" uninstall
  rm -r ${pkgdir}/usr/lib/gambas3/gb.[c-v]*
  rm -r ${pkgdir}/usr/share/gambas3/info/gb.[c-v]*
  ##
}
