# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gammastep
pkgver=2.0.7
pkgrel=2
pkgdesc="Adjust the color temperature of your screen according to your surroundings."
arch=('x86_64')
url="https://gitlab.com/chinstrap/gammastep"
license=('GPL3')
depends=('geoclue' 'glibc' 'hicolor-icon-theme' 'libdrm' 'libx11' 'libxcb'
'libxxf86vm' 'wayland')
makedepends=('glib2' 'intltool' 'python' 'systemd')
optdepends=('python-gobject: for gammastep-indicator'
            'python-xdg: for gammastep-indicator'
            'gtk3: for gammastep-indicator')
source=("https://gitlab.com/chinstrap/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('466811636d4c4d5f3fc21272e7ab8c208e2ad8830fbabd48c8d96c2eaa04d07c29da35f409166eaf95acdd0038f926a84d199fabf08df79fb027f45441ef0d08')
b2sums=('2f98399528efc7b37f65f214c07c721d3c3e5d82272feee78d863e8d4bce6c886a6f870383fd05640847fe104a75123036e1a442d915e75d692e8048823db277')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libgio-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {CONTRIBUTING,NEWS,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
