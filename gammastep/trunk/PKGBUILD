# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gammastep
pkgver=2.0.2
pkgrel=1
pkgdesc="Adjust the color temperature of your screen according to your surroundings."
arch=('x86_64')
url="https://gitlab.com/chinstrap/gammastep"
license=('GPL3')
depends=('geoclue' 'glibc' 'hicolor-icon-theme' 'libdrm' 'libx11' 'libxcb'
'libxxf86vm' 'wayland')
optdepends=('python-gobject: for gammastep-indicator'
            'python-xdg: for gammastep-indicator'
            'gtk3: for gammastep-indicator')
makedepends=('glib2' 'intltool' 'python' 'systemd')
source=("https://gitlab.com/chinstrap/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('849d330c6fde98862428fc846f1703781eed34176f4d8bf5efaa29ffcaa0f2922ea439476f2eaf61e55e08a6942b062dc116a99fa94753616c84db2a30424ca4')
b2sums=('5009a9466ad73af7f961e56fd2f109d00c0119856dfb071798285233d388bda4b5d8dc6e9f3a6611860281bbec2c88beebfed8610dc290ea33e60902ccbd649e')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libgio-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {CONTRIBUTING,NEWS,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
