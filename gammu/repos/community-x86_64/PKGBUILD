# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gammu
pkgver=1.34.0
pkgrel=2
pkgdesc="GNU All Mobile Management Utilities"
arch=('i686' 'x86_64')
url="http://wammu.eu/"
license=('GPL')
depends=('glib2' 'bluez-libs' 'curl' 'libmariadbclient' 'postgresql-libs'
         'libusb' 'libgudev')
makedepends=('cmake' 'doxygen' 'mariadb' 'python2')
optdepends=('dialog: support for the gammu-config script'
	    'python2: for python bindings')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('5bc2508389d9b291ca0b8d4f210d0012')

build() {
  cd "$pkgname-$pkgver"

  # bash completion dir change
  sed -i 's,DESTINATION "/etc/bash_completion.d",DESTINATION "/usr/share/bash-completion/completions",' contrib/CMakeLists.txt

  cmake . -DCMAKE_INSTALL_PREFIX="/usr" -DLIB_SUFFIX=""

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
