# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gammu
pkgver=1.32.0
pkgrel=3
pkgdesc="GNU All Mobile Management Utilities"
arch=('i686' 'x86_64')
url="http://wammu.eu/"
license=('GPL')
depends=('bluez' 'curl' 'libmariadbclient'
         'postgresql-libs' 'libusb')
makedepends=('cmake' 'doxygen' 'mariadb')
optdepends=('dialog: support for the gammu-config script')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('8ea16c6b3cc48097a8e62311fe0e25b9')

build() {
  cd "$pkgname-$pkgver"

  # bash completion dir change
  sed -i 's,DESTINATION "/etc/bash_completion.d",DESTINATION "/usr/share/bash-completion/completions",' contrib/CMakeLists.txt

  cmake . -DCMAKE_INSTALL_PREFIX="/usr" -DLIB_SUFFIX=""

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
