# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgbase=gap
pkgname=(gap gap-data gap-doc gap-packages)
pkgver=4.8.3
pkgrel=1
pkgdesc="Groups, Algorithms, Programming: a system for computational discrete algebra"
arch=(i686 x86_64)
url="http://www.gap-system.org/"
license=(GPL)
source=("http://www.gap-system.org/pub/gap/gap48/tar.bz2/gap4r8p3_2016_03_19-22_17.tar.bz2")
sha256sums=('81d358109d87014eb10d1325c38ee94aa968b2a6365afd1a7411bfdd44f7a454')

build() {
  cd gap4r8
  ./configure --prefix=/usr --with-gmp=system
  make
}

package_gap() {
  depends=(gmp)
  optdepends=('gap-packages: extra packages' 'gap-data: additional databases' 'gap-doc: documentation')
  replaces=(gap-math)
  conflicts=(gap-math)
  cd gap4r8
  
  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r bin etc grp lib tst CITATION "$pkgdir"/usr/lib/gap
  
  mkdir -p "$pkgdir"/usr/lib/gap/pkg
  cp -r pkg/GAPDoc-* "$pkgdir"/usr/lib/gap/pkg

  mkdir -p "$pkgdir"/usr/bin
  ln -s /usr/lib/gap/bin/gap.sh "$pkgdir"/usr/bin/gap

# fix location
  sed -e 's|/build/gap/src/gap4r7|/usr/lib/gap|' -i "$pkgdir"/usr/lib/gap/bin/gap.sh  
}

package_gap-data() {
  depends=(gap)
  pkgdesc="Additional databases for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r prim small trans "$pkgdir"/usr/lib/gap
}

package_gap-doc() {
  depends=(gap)
  pkgdesc="Documentation for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r doc "$pkgdir"/usr/lib/gap
}

package_gap-packages() {
  depends=(gap)
  pkgdesc="Extra packages for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r pkg "$pkgdir"/usr/lib/gap

# GAPDoc is in main package
  rm -r "$pkgdir"/usr/lib/gap/pkg/GAPDoc-*
}
