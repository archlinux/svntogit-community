# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: FJ <joostef@gmail.com>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgname=gaupol
pkgver=1.10.1
pkgrel=2
pkgdesc="Editor for text-based subtitles"
arch=('any')
url="https://otsaloma.io/gaupol/"
license=('GPL')
depends=('python-gobject' 'gtk3' 'gst-plugins-'{base,good,bad,ugly} 'gst-libav'
         'gst-plugin-gtk' 'gspell' 'iso-codes' 'python-chardet' 'python-cairo'
         'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('mpv: subtitle preview'
            'vlc: subtitle preview'
            'mplayer: subtitle preview')
source=($pkgname-$pkgver.tar.gz::https://github.com/otsaloma/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('530cb55a9e8a301cff6e23d5e712e7142a3b1a7f81d61d70e4273d3655c6277f')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py --without-iso-codes install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
