# $Id$
# Maintainer :
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=gcc-avr
pkgver=4.5.1
pkgrel=1
#_snapshot=4.5-20100610
pkgdesc="The GNU avr Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'custom')
url="http://gcc.gnu.org/"
depends=('binutils-avr>=2.20.1' 'cloog-ppl' 'gcc-libs>=4.5.0' 'libmpc' 'elfutils')
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
source=(http://ftp.gnu.org/gnu/gcc/${pkgname/-avr}-${pkgver}/gcc-{core,g++}-${pkgver}.tar.bz2
        add-more-mcus.patch)
#source=(ftp://gcc.gnu.org/pub/gcc/snapshots/${_snapshot}/gcc-{core,g++}-${_snapshot}.tar.bz2)
md5sums=('dc8959e31b01a65ce10d269614815054'
         'b294953ff0bb2f20c7acb2bf005d832a'
         '2e77e4a8019b1ba515bd1c11f65d7573')

if [ -n "${_snapshot}" ]; then
  _basedir="${srcdir}/${pkgname/-avr}-${_snapshot}"
else
  _basedir="${srcdir}/${pkgname/-avr}-${pkgver}"
fi

build() {
  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  cd ${_basedir}

  # http://gcc.gnu.org/ml/gcc-patches/2010-04/msg01210.html
  patch -p0 -i ${srcdir}/add-more-mcus.patch

  mkdir build
  cd build
  ../configure --disable-libssp \
               --disable-nls \
               --enable-languages=c,c++ \
               --infodir=/usr/share/info \
               --libdir=/usr/lib \
               --libexecdir=/usr/lib \
               --mandir=/usr/share/man \
               --prefix=/usr \
               --target=avr \
               --with-gnu-as \
               --with-gnu-ld \
               --with-as=/usr/bin/avr-as \
               --with-ld=/usr/bin/avr-ld

  make
}

package() {
  cd ${_basedir}

  cd build
  make -j1 DESTDIR=${pkgdir} install

  install -Dm644 ${_basedir}/COPYING.RUNTIME \
    ${pkgdir}/usr/share/licenses/${pkgname}/RUNTIME.LIBRARY.EXCEPTION

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -rf ${pkgdir}/usr/share/man/man7
  rm -rf ${pkgdir}/usr/share/info
}

# vim:set ts=2 sw=2 et:
