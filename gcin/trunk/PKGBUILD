# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=2.9.0
pkgrel=4
pkgdesc='Input method server supporting various input methods'
url='https://hyperrate.com/dir.php?eid=67'
license=('LGPL')
arch=('x86_64')
depends=('gtk3' 'libxtst' 'libappindicator-gtk3')
makedepends=('qt5-base' 'gtk2' 'anthy')
optdepends=('qt5-base: support for qt5 input method'
            'gtk2: support for gtk2 input method'
            'anthy: support for anthy input method'
            'curl: for downloading and uploading vocabulary databases via ts-edit')
source=("https://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz"
        fix-gtk-immodules-path.patch)
sha512sums=('f8ce9407e2cfce2b3ba6e6ba6ccca8eb9dacf5c0124438df525e1a9fb692a7aea3cb685965708091b0fdedf3b00efa77fb6cdfbd33cdb50e1938e9f3fcf7a5d6'
            '54e90b04de45acf9f8821c92234b9708281907cbdf24ebb29f9820945615669d82a8443e49fd49d6c08be4a7b9f2857c8e763f170ef879c8fab1f232bb67e85d')
options=('debug')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    sed -e 's/lib64/lib/g' \
        -e 's/^OPTFLAGS=.*/OPTFLAGS="$CFLAGS $CPPFLAGS"/' \
        -e 's/echo "LDFLAGS=/echo "LDFLAGS=$LDFLAGS /' \
        -e 's/echo "SO_FLAGS=/echo "SO_FLAGS=$LDFLAGS /' \
        -i configure
    sed -e 's/QT=qt5/QT=qt/' \
        -e 's/^LDFLAGS=/LDFLAGS+=/' \
        -i qt5-im/Makefile59
    # Don't strip programs - makepkg will handle it
    sed -e 's/install -s/install/' -i Makefile
    patch -Np1 -i ../fix-gtk-immodules-path.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --use_i18n=Y
    make -j1
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
