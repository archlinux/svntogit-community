# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris-data
pkgver=12.05
pkgrel=1
pkgdesc="Data files for GCompris"
arch=('any')
url="http://gcompris.net/"
license=('GPL3')
makedepends=('texinfo' 'texi2html' 'intltool' 'gettext' 'gnuchess'
             'pygtk' 'pyxml' 'librsvg' 'gnet' 'python-pysqlite'
             'gstreamer0.10-base-plugins')
options=('!libtool')
install=gcompris-data.install
source=(http://downloads.sourceforge.net/project/gcompris/$pkgver/gcompris-$pkgver.tar.bz2)
sha256sums=('a4e9b9d04fd405c4b9ddc30f84a5f521f7ad1966c5f66c3e873bb84269ef55d0')

build() {
  cd "$srcdir/gcompris-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/gcompris-$pkgver"
  make DESTDIR="$pkgdir" install-data
}

# vim:set ts=2 sw=2 et:
