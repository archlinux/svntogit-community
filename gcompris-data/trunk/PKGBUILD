# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris-data
pkgver=14.03
pkgrel=1
pkgdesc="Data files for GCompris"
arch=('any')
url="http://gcompris.net/"
license=('GPL3')
makedepends=('texinfo' 'texi2html' 'intltool' 'pygtk' 'librsvg' 'gstreamer0.10-base-plugins')
source=(http://gcompris.net/download/gcompris-$pkgver.tar.bz2)
sha256sums=('7e0a95c67ff55f54a359c10394bddec178034543e0dddfc348a74a9e55825551')

build() {
  cd "$srcdir/gcompris-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/gcompris-$pkgver"
  make DESTDIR="$pkgdir" install-data
}

# vim:set ts=2 sw=2 et:
