# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris
pkgver=15.10
pkgrel=3
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
depends=('pygtk' 'librsvg' 'sdl_mixer')
optdepends=('gnucap: for computer simulation within the electricity activity'
            'tuxpaint: for the painting activity')
makedepends=('texinfo' 'texi2html' 'intltool')
source=(http://gcompris.net/download/gtk/src/$pkgname-$pkgver.tar.bz2)
sha256sums=('6aba73e2a1db0f50d43a40ee1a4a2b9d62a96e68542971b555c9debd8f50fc38')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's/"$GNUCHESS"/"gcompris-gnuchess"/' configure
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-sdlmixer
  make LDFLAGS+=" -lgmodule-2.0"
}

package() {
  depends+=("gcompris-data=$pkgver")

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Data files are packaged separately into gcompris-data
  rm -r "$pkgdir/usr/share/gcompris"
}

# vim:set ts=2 sw=2 et:
