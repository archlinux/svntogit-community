# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx.de>

pkgname=gcompris
pkgver=12.05
pkgrel=1
pkgdesc="Educational software suite comprising of numerous activities for children aged 2 to 10"
arch=('i686' 'x86_64')
url="http://gcompris.net/"
license=('GPL3')
depends=('pygtk' 'pyxml' 'librsvg' 'gnet' 'python-pysqlite'
         'gstreamer0.10-base-plugins')
optdepends=('gnucap: for computer simulation within the electricity activity'
            'tuxpaint: for the painting activity'
            'gnuchess: for the chess activity')
makedepends=('texinfo' 'texi2html' 'intltool' 'gettext' 'gnuchess')
options=('!libtool')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
sha256sums=('a4e9b9d04fd405c4b9ddc30f84a5f521f7ad1966c5f66c3e873bb84269ef55d0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  depends+=('gcompris-data')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-exec
}

# vim:set ts=2 sw=2 et:
