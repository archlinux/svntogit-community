# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gdal
pkgver=1.7.2
pkgrel=2
pkgdesc="A translator library for raster geospatial data formats"
arch=('i686' 'x86_64')
url="http://www.gdal.org/"
license=('custom')
depends=('curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg>=8' 'libpng' 'libtiff'
         'mysql' 'netcdf' 'postgresql' 'python' 'python-numpy' 'sqlite3')
makedepends=('cfitsio')
options=('!libtool' '!makeflags')
source=(http://download.osgeo.org/${pkgname}/${pkgname}-${pkgver}.tar.gz
	gdal-1.5.1-python-install.patch) 
md5sums=('05351f8cb61761ae579667e24a297fe6'
         '81afc1c26d29cee84aadb6924fe33861')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  cd ${srcdir}/$pkgname-$pkgver
  patch -Np0 -i ${srcdir}/gdal-1.5.1-python-install.patch || return 1

  ./configure --prefix=/usr --with-netcdf --with-libtiff --with-sqlite3 \
              --with-geotiff --with-mysql --with-python --without-libtool --with-curl

  # workaround for bug #13646
  sed -i 's/PY_HAVE_SETUPTOOLS=1/PY_HAVE_SETUPTOOLS=/g' ./GDALmake.opt || return 1
  sed -i 's/EXE_DEP_LIBS/KILL_EXE_DEP_LIBS/' apps/GNUmakefile || return 1
  install -d ${pkgdir}/usr/lib/python2.6/site-packages/ || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install license
  install -D -m644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  #FS#15477 clean up junks
  rm -f ${pkgdir}/usr/bin/gdal_sieve.dox
}
