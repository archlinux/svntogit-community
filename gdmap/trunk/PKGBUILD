# $Id$
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=gdmap
pkgver=0.8.1
pkgrel=4
pkgdesc="Tool to inspect the used space of folders."
arch=('x86_64')
url="http://gdmap.sourceforge.net"
license=('GPL')
depends=('gtk2' 'glib2' 'libxml2')
makedepends=('gettext' 'intltool')
source=(http://downloads.sourceforge.net/sourceforge/gdmap/$pkgname-$pkgver.tar.gz \
	gtk-fix.patch)
md5sums=('54b43354dbe774810104607ad1eaaed3'
         'a5488ae69fa778cd35398b99bb1d6c94')

build() {
  cd ${srcdir}/gdmap-$pkgver

  cd src
  # fix deprecated gtk macros call
  patch -Np0 -i ../../gtk-fix.patch
  # fix linking
  sed -i -e 's/@LIBXML_LIBS@/@LIBXML_LIBS@ -lm/g' Makefile.in
  cd ..

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/gdmap-$pkgver

  make DESTDIR=${pkgdir} install
}
