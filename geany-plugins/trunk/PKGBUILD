# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.32
pkgrel=4
pkgdesc='Plugins for Geany'
arch=('x86_64')
url='https://plugins.geany.org/'
license=('GPL')
depends=("geany>=$pkgver" 'ctpl' 'gpgme' 'gtkspell3' 'libgit2' 'lua51')
makedepends=('cppcheck' 'intltool')
source=("https://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2"
        enchant-2.patch)
sha256sums=('78d4b394fe22fda298e80a22616421efdd4a419eb227b99eadca73f65abac117'
            '094a613b600e42de9b23c3586abf25f3b0695eb62ca2d341e82c0a5633fefeb8')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../enchant-2.patch
  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  # Looking forward to Python 3 being supported
  export PYTHON=/usr/bin/python2

  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
