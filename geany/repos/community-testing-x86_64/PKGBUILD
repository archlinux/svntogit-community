# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=1.26
pkgrel=2
pkgdesc='Fast and lightweight IDE'
arch=('x86_64' 'i686')
url='http://www.geany.org/'
license=('GPL')
depends=('gtk2' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('perl-xml-parser' 'setconf' 'intltool')
optdepends=('geany-plugins: various extra features'
            'vte: terminal support'
            'python2')
install="$pkgname.install"
source=("http://download.geany.org/$pkgname-$pkgver.tar.bz2")
sha256sums=('e38530e87c577e1e9806be3b40e08fb9ee321eb1abc6361ddacdad89c825f90d')

prepare() {
  cd "$pkgname-$pkgver"

  # Python2 fix
	sed -i '0,/on/s//on2/' data/templates/files/main.py

  # Syntax highlighting for PKGBUILD files
  sed -i 's/Sh=/Sh=PKGBUILD;/' data/filetype_extensions.conf
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make

  # Fix for FS#10318
  setconf geany.desktop MimeType ''
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: -u 7 geany.org/Documentation/ReleaseNotes
# vim:set ts=2 sw=2 et:
