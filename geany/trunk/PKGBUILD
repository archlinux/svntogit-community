# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=1.23
pkgrel=1
pkgdesc='Fast and lightweight IDE'
arch=('x86_64' 'i686')
url="http://www.geany.org/"
license=('GPL')
depends=('gtk2' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('perlxml' 'setconf' 'intltool')
optdepends=('vte: for terminal support'
            'geany-plugins: various extra features'
            'python2')
install="$pkgname.install"
options=('!libtool')
source=("http://download.geany.org/$pkgname-$pkgver.tar.bz2")
sha256sums=('cdd4a772694803c837ae59e56f7bdc2faba174509317211f522e7d25dfcbe8b0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make

  # Python2 fix
	sed -i '0,/on/s//on2/' data/templates/files/main.py

  # Fix for FS#10318
  setconf geany.desktop MimeType ''

  # Syntax highlighting for PKGBUILD files
  sed -i 's/Sh=/Sh=PKGBUILD;/' data/filetype_extensions.conf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
