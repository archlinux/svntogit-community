# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=40.0
pkgrel=9
epoch=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(GPL3)
groups=(gnome-extra)
depends=(
  cairo
  enchant
  folks
  gcr
  gdk-pixbuf2
  glib2
  gmime3
  gnome-online-accounts
  gsound
  gspell
  gtk3
  iso-codes
  libcanberra
  libgee
  libhandy
  libnotify
  libpeas
  libsecret
  libsoup
  libstemmer
  libxml2
  libytnef
  org.freedesktop.secrets
  pango
  sqlite
  webkit2gtk
)
makedepends=(
  appstream-glib
  cmake
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
_tag=375f741026c8aa9f1ff41ce912f5c08d7d3cad15
source=(git+https://gitlab.gnome.org/GNOME/geary.git?signed#tag=${_tag})
validpgpkeys=(3A2EF7F138557A145F2866E99FAB18747A8FC649) # Michael James Gratton <mike@vee.net>
sha256sums=(SKIP)

prepare() {
  cd geary
  git cherry-pick -n b8babd4aba65c85e3e4e9f0d6b5d827d220096de # fix contact auto-completion
  git cherry-pick -n 9bd4c82952a0a2c3308c5cc86c0b85650c1fb484 # fix build with Vala 0.56
  git cherry-pick -n 0f75e7a84a39492d0748cec2ba6028e08cae3644 # fix build with Vala 0.56
}

pkgver() {
  cd geary
  git describe --tags | sed 's/^gnome-//g'
}

build() {
  arch-meson geary build \
    -Dprofile=release
  meson compile -C build
}

package() {
  meson install -C build \
    --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
