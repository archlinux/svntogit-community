# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=3.34.2
pkgrel=1
epoch=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(GPL3)
depends=(
  cairo
  enchant
  folks
  gcr
  gdk-pixbuf2
  glib2
  gmime
  gnome-keyring
  gnome-online-accounts
  gspell
  gtk3
  iso-codes
  libcanberra
  libgee
  libhandy
  libnotify
  libsecret
  libsoup
  libxml2
  libytnef
  pango
  sqlite
  webkit2gtk
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
_commit=5b5d6d9440d845e22870f253a16a9e4a797cd5ea  # tags/3.34.2^0
source=(git+https://gitlab.gnome.org/GNOME/geary.git#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd geary
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson geary build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim: ts=2 sw=2 et:
