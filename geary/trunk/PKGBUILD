# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=43.0
pkgrel=2
epoch=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(GPL3)
groups=(gnome-extra)
depends=(
  at-spi2-core
  cairo
  dconf
  enchant
  folks
  gcr
  gdk-pixbuf2
  glib2
  glibc
  gmime3
  gnome-online-accounts
  gsound
  gspell
  gtk3
  hicolor-icon-theme
  icu
  iso-codes
  json-glib
  libgee
  libhandy
  libpeas
  libsecret
  libsoup3
  libstemmer
  libunwind
  libxml2
  libytnef
  org.freedesktop.secrets
  pango
  sqlite
  webkit2gtk-4.1
)
makedepends=(
  appstream-glib
  cmake
  git
  gobject-introspection
  meson
  vala
  yelp-tools
)
_commit=94d6bec861daffb27efea85a296f347db7a5af6d  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/geary.git#commit=$_commit")
validpgpkeys=(3A2EF7F138557A145F2866E99FAB18747A8FC649) # Michael James Gratton <mike@vee.net>
sha256sums=('SKIP')

prepare() {
  cd geary
}

pkgver() {
  cd geary
  git describe --tags | sed 's/^gnome-//g;s/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson geary build \
    -Dprofile=release
  meson compile -C build
}

package() {
  meson install -C build \
    --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
