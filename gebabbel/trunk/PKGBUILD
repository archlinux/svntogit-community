# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: pfm <nl081130@yahoo.de>

pkgname=gebabbel
pkgver=0.4
pkgrel=7
pkgdesc="GUI for gpsbabel"
arch=('i686' 'x86_64')
url="http://gebabbel.sourceforge.net/"
license=('GPL2')
depends=('qt4' 'gpsbabel')
changelog=$pkgname.changelog
source=(https://sources.archlinux.org/other/$pkgname/Gebabbel-$pkgver-Src.tar.gz
        http://$pkgname.sourceforge.net/GebabbelLogo.png
        $pkgname.desktop
        $pkgname-$pkgver.patch)
sha256sums=('dcf3543412525959eaad755673fa65412077735fec400e6e62b1bd2c94ec1adf'
            'ca576fb10d493e1e7a2f96e56420477a3580bfd13961a73f2c1802de14ac7b69'
            '0b90c6443414bc85bb1abcf59d40529d29c4c1f464ef6293ebd6e3ea1aa7674f'
            '9ec383ceb462aab1d6561bcbad506b9c75a64866a13aeecf67f64b71807f9493')

build() {
  cd ${srcdir}/Gebabbel-$pkgver

  patch -Np0 -i ${srcdir}/$pkgname-$pkgver.patch

  qmake-qt4
  make
}

package() {
  cd ${srcdir}/Gebabbel-$pkgver

  install -D -m755 ${srcdir}/Gebabbel-$pkgver/bin/$pkgname \
    ${pkgdir}/usr/bin/$pkgname
  install -D -m644 ${srcdir}/GebabbelLogo.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png 
  install -D -m644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop

  #localization
  install -d ${pkgdir}/usr/share/$pkgname/locale
  install -t ${pkgdir}/usr/share/$pkgname/locale/ \
    ${srcdir}/Gebabbel-$pkgver/binincludes/translations/*.qm
}
