# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: fancris3 <fancris3 at aol.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gecko-mediaplayer
pkgver=1.0.9
pkgrel=1
pkgdesc='Browser plugin that uses gnome-mplayer to play media in a web browser.'
arch=('i686' 'x86_64')
url='http://code.google.com/p/gecko-mediaplayer/'
license=('GPL')
depends=("gnome-mplayer>=${pkgver}" 'dbus-glib' 'gmtk' 'curl')
makedepends=('xulrunner' 'pkgconfig')
replaces=('mplayer-plugin')
source=("https://${pkgname}.googlecode.com/svn/packages/${pkgname}-${pkgver}.tar.gz"
        "np_loadds.patch")
md5sums=('69b50ecbbbf34ca6d980a477345dbd40'
         '224c865f5df059ef7ac1430889d66a1f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # https://code.google.com/p/gecko-mediaplayer/issues/detail?id=191
  patch -Np1 -i ../np_loadds.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --without-gconf --enable-new-libxul
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
