# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: trya <tryagainprod@gmail.com>
# Contributor: Yvan <madridista40@orange.fr>

pkgname=gens-gs
pkgver=2.16.7
pkgrel=4
pkgdesc="An emulator of Sega Genesis, Sega CD and 32X, combining features from various forks of Gens"
arch=('i686' 'x86_64')
url="http://segaretro.org/Gens/GS"
license=('GPL')
if [[ $CARCH == "x86_64" ]]; then
  makedepends=('gcc-multilib' 'lib32-gtk2' 'lib32-mesa-libgl' 'lib32-sdl' 'nasm')
  optdepends=('lib32-alsa-plugins: Sound support for PulseAudio'
              'lib32-libpulse: Sound support for PulseAudio')
else
  depends=('gtk2' 'sdl')
  makedepends=('nasm')
fi
conflicts=('gens')
options=('!libtool')
source=('http://segaretro.org/images/6/6d/Gens-gs-r7.tar.gz'
        'gens-gtk.patch')
sha256sums=('6320cd7bee85472891e1e29b21731d2c8180d04db67ee21256491f3f9ad145c1'
            '29b1e9755b34e00ef7d19bdc527dc4f841400a0022c81ee71c331be4a76a6c1c')

prepare() {
  cd "${srcdir}"/${pkgname}-r7

  patch -Np1 -i ../gens-gtk.patch
}

build() {
  cd "${srcdir}"/${pkgname}-r7

  if [[ $CARCH == "x86_64" ]]; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  if [[ $CARCH == "x86_64" ]]; then
    LIBS='-ldl -lX11' ./configure --host=i686-unknown-linux-gnu --prefix=/usr --libdir=/usr/lib32 --with-pic --disable-static
  else
    LIBS='-ldl -lX11' ./configure --prefix=/usr --with-pic --disable-static
  fi
  make
}

package() {
  cd "${srcdir}"/${pkgname}-r7

  if [[ $CARCH == "x86_64" ]]; then
    depends=('lib32-gtk2' 'lib32-sdl')
  fi

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
