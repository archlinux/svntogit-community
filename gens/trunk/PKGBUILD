# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: trya <tryagainprod@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>

pkgname=gens
pkgver=2.15.5
pkgrel=10
pkgdesc='A Sega Genesis / Sega CD / Sega 32X emulator'
arch=('i686' 'x86_64')
url='http://gens.sourceforge.net'
license=('GPL')
if [[ $CARCH == "x86_64" ]]; then
  makedepends=('gcc-multilib' 'lib32-gtk2' 'lib32-mesa-libgl' 'lib32-sdl')
  optdepends=('lib32-alsa-plugins: Sound support for PulseAudio'
              'lib32-libpulse: Sound support for PulseAudio')
else
  depends=('gtk2' 'sdl')
fi
makedepends+=('gendesk' 'imagemagick' 'nasm')
options=('!buildflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('51910d3056da9ffbb2c01cfec064998c9a4cad0cb379d703ec1b93c83dba305a')

prepare() {
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name='Gens' --exec='gens'
}

build() {
  cd ${pkgname}-${pkgver}

  if [[ $CARCH == "x86_64" ]]; then
    export CC='gcc -m32'
    export CXX='g++ -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  fi

  export LDFLAGS='-Wl,-z,noexecstack'
  ./configure --host='i686-unknown-linux-gnu' --prefix='/usr'
  make
}

package() {
  cd ${pkgname}-${pkgver}

  if [[ $CARCH == "x86_64" ]]; then
    depends=('lib32-gtk2' 'lib32-sdl')
  fi

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/{applications,pixmaps}
  install -m 644 {..,"${pkgdir}"/usr/share/applications}/${pkgname}.desktop
  convert "${pkgdir}"/usr/share/{gens/Gens2.ico[1],pixmaps/${pkgname}.png}
}

# vim: ts=2 sw=2 et:
