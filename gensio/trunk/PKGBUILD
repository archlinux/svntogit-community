# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=gensio
pkgver=2.4.1
pkgrel=1
pkgdesc="General Stream I/O"
arch=(x86_64)
url="https://sourceforge.net/projects/ser2net/"
license=('GPL')
depends=()
source=("$pkgname-$pkgver.tar.gz::https://github.com/cminyard/gensio/archive/v$pkgver.tar.gz")
sha256sums=('d344060af79e73fdd6dd795a7be1ac3f3a898ca68faa1458b26015263008623b')

build() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  test -x configure || ./reconf
  ./configure --prefix=/usr --sbindir=/usr/bin --with-python=no
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  make DESTDIR="$pkgdir" install
}
