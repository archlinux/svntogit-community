# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Felipe Hommen <felibank at gmail dot com>
# Contributor: moostik <mooostik_at_gmail.com>

pkgname=geogebra
pkgver=4.2.19.0
pkgrel=1
pkgdesc='Dynamic mathematics software with interactive graphics, algebra and spreadsheet'
arch=('any')
url="http://www.geogebra.org/"
license=('GPL3' 'CCPL:by-sa')
depends=('java-runtime' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils' 'xdg-utils')
#optdepends=('kde-thumbnailer-geogebra: generates thumbnails of GeoGebra files in KDE'
#            'gnome-thumbnailer-geogebra: generates thumbnails of GeoGebra files in GNOME'
#            'geogebra-prim: adds a menu entry for the primary school version')
install='geogebra.install'
source=(http://$pkgname.googlecode.com/files/GeoGebra-Unixlike-Installer-$pkgver.tar.gz)
sha256sums=('d00543e918b5cf44bacae43a251af53b652e0227fba397b2b2bbee9d15caf825')

package() {
  install -dm755 $pkgdir/usr/bin \
                 $pkgdir/usr/share/applications \
                 $pkgdir/usr/share/geogebra

  cd $srcdir/$pkgname-$pkgver
  sed -i 's/\/usr/\$\{pkgdir\}\/usr/g' install.sh
  source install.sh
  install -Dm644 _license.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE

  rm -rf $pkgdir/usr/share/mime/
}
