# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname='geoip2-database'
pkgver=20191203
pkgrel=1
pkgdesc="GeoLite2 IP geolocation databases compiled by MaxMind"
arch=('any')
url="https://dev.maxmind.com/geoip/geoip2/geolite2/"
license=('custom:OPEN DATA LICENSE')
source=(https://geolite.maxmind.com/download/geoip/database/GeoLite2-City_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-ASN_$pkgver.tar.gz)
noextract=(GeoLite2-City_$pkgver.tar.gz
           GeoLite2-Country_$pkgver.tar.gz
           GeoLite2-ASN_$pkgver.tar.gz)
sha256sums=('844ae484c94e4827b9f2fcef6deb537425ea1a04ce315f08682b738290dfc2af'
            '02c70d4d0b22bca72cced86dd6eb411b18787ab9509e0a7755fd33d196292a2e'
            '269d7fbe740903fcd9856fb947840a4ddf6ed0d62b9d61076f97026aac9b8f5b')

prepare() {
  cd "$srcdir"

  for _db in GeoLite2-{City,Country,ASN}; do
    tar --strip-components=1 -xf ${_db}_$pkgver.tar.gz --wildcards "*/$_db.mmdb"
  done

  tar --strip-components=1 -xf GeoLite2-ASN_$pkgver.tar.gz --wildcards "*/COPYRIGHT.txt" "*/LICENSE.txt"
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoLite2-{City,Country,ASN}.mmdb

  install -Dm644 COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT.txt"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
