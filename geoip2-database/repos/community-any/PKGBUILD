# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname='geoip2-database'
pkgver=20190806
pkgrel=1
pkgdesc="GeoLite2 IP geolocation databases compiled by MaxMind"
arch=('any')
url="https://dev.maxmind.com/geoip/geoip2/geolite2/"
license=('custom:OPEN DATA LICENSE')
source=(https://geolite.maxmind.com/download/geoip/database/GeoLite2-City_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-ASN_$pkgver.tar.gz)
noextract=(GeoLite2-City_$pkgver.tar.gz
           GeoLite2-Country_$pkgver.tar.gz
           GeoLite2-ASN_$pkgver.tar.gz)
sha256sums=('af74328de3f54711a223ff71373d2a72517d67f0a5b8c925722a1a7683af7aa6'
            '1e4b5ac1499ccab5c3523bbee4ded9c7ad31e8604414e3b4edfc1cbad96aa85f'
            '90e70e6dc42e2e0782a6fc64933aa6614adbc90833b18d268fef022f0114139a')

prepare() {
  cd "$srcdir"

  for _db in GeoLite2-{City,Country,ASN}; do
    tar --strip-components=1 -xf ${_db}_$pkgver.tar.gz --wildcards "*/$_db.mmdb"
  done

  tar --strip-components=1 -xf GeoLite2-ASN_$pkgver.tar.gz --wildcards "*/COPYRIGHT.txt" "*/LICENSE.txt"
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoLite2-{City,Country,ASN}.mmdb

  install -Dm644 COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT.txt"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
