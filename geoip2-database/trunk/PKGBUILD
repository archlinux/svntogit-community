# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname='geoip2-database'
pkgver=20190813
pkgrel=1
pkgdesc="GeoLite2 IP geolocation databases compiled by MaxMind"
arch=('any')
url="https://dev.maxmind.com/geoip/geoip2/geolite2/"
license=('custom:OPEN DATA LICENSE')
source=(https://geolite.maxmind.com/download/geoip/database/GeoLite2-City_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-ASN_$pkgver.tar.gz)
noextract=(GeoLite2-City_$pkgver.tar.gz
           GeoLite2-Country_$pkgver.tar.gz
           GeoLite2-ASN_$pkgver.tar.gz)
sha256sums=('65b3a78efe51a6d2aad611fb7bc0fbcf53b79296ba3a44154fe1737f06098ae6'
            'fcab136442778d6188797f8f2c366e5ec7f1a9a4b3e434e1bf57c841c32dd4ca'
            '4f9b72ff6432b7245f10856370d332dd2be9785aab3275821f14b8dca1dcc1ee')

prepare() {
  cd "$srcdir"

  for _db in GeoLite2-{City,Country,ASN}; do
    tar --strip-components=1 -xf ${_db}_$pkgver.tar.gz --wildcards "*/$_db.mmdb"
  done

  tar --strip-components=1 -xf GeoLite2-ASN_$pkgver.tar.gz --wildcards "*/COPYRIGHT.txt" "*/LICENSE.txt"
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoLite2-{City,Country,ASN}.mmdb

  install -Dm644 COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT.txt"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
