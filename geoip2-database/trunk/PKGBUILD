# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname='geoip2-database'
pkgver=20190611
pkgrel=1
pkgdesc="GeoLite2 IP geolocation databases compiled by MaxMind"
arch=('any')
url="https://dev.maxmind.com/geoip/geoip2/geolite2/"
license=('custom:OPEN DATA LICENSE')
source=(https://geolite.maxmind.com/download/geoip/database/GeoLite2-City_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country_$pkgver.tar.gz
        https://geolite.maxmind.com/download/geoip/database/GeoLite2-ASN_$pkgver.tar.gz)
noextract=(GeoLite2-City_$pkgver.tar.gz
           GeoLite2-Country_$pkgver.tar.gz
           GeoLite2-ASN_$pkgver.tar.gz)
sha256sums=('85dbc340afb57c51a1154608a620b449252cf404588e3e6657a6b37a0c90d508'
            'd570dac030f6404425a8d213f414231e994d70ea6f0edc3e9f1c4dea16047448'
            'a51bcbeedaa148427f74409e08b4fe9ab21d32e3f08d83dcc7013c8b276f7f79')

prepare() {
  cd "$srcdir"

  for _db in GeoLite2-{City,Country,ASN}; do
    tar --strip-components=1 -xf ${_db}_$pkgver.tar.gz --wildcards "*/$_db.mmdb"
  done

  tar --strip-components=1 -xf GeoLite2-ASN_$pkgver.tar.gz --wildcards "*/COPYRIGHT.txt" "*/LICENSE.txt"
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoLite2-{City,Country,ASN}.mmdb

  install -Dm644 COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT.txt"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
