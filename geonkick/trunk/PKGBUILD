# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.8.0
pkgrel=2
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/free-sm/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11')
makedepends=('cmake' 'jack' 'libsndfile' 'lv2' 'rapidjson')
checkdepends=('lv2lint')
optdepends=(
  'lv2-host: for LV2 plugin'
  'jack: for standalone application'
)
source=("$pkgname-v$pkgver.tar.gz::https://github.com/free-sm/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('fff7e4390178dd667f048c731950e8e10731675aead9eedc1633ee47798cb867e81eb7d5191ee9d3ab75041bd20ae0aa6eedf0a314098c79c388349b1f23f28a')
b2sums=('6c2ae6665cb8e6fdcc3bb608fa05180c44b41cd986ac3fefdde9762eb4a12d45b41755a8eaf211759936e12ef9ee2fc34a47e6c398cf91cc9e83abac0b6a98c5')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  cp -av build/plugin/lv2/*.so plugin/lv2/${pkgname}.lv2/
  lv2lint -Mpack -I "${PWD}/plugin/lv2/${pkgname}.lv2/" \
          "http://geontime.com/geonkick"
  rm -v plugin/lv2/${pkgname}.lv2/*.so
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
