# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=geos
pkgver=3.7.2
pkgrel=1
pkgdesc="C++ port of the Java Topology Suite"
arch=('x86_64')
url="https://trac.osgeo.org/geos/"
license=('LGPL')
depends=('gcc-libs' 'bash')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://download.osgeo.org/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('2166e65be6d612317115bfec07827c11b403c3f303e0a7420a2106bc999d7707')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
