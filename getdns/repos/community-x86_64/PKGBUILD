# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Remi Gacogne <rgacogne@archlinux.org>

pkgname=getdns
pkgver=1.7.0
pkgrel=2
pkgdesc="Modern asynchronous DNS API"
arch=(x86_64)
url="https://getdnsapi.net"
license=(BSD)
depends=(libbsd libev libevent libidn2 libuv libyaml unbound check)
makedepends=(cmake)
source=("${url}/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        ${pkgname}-strengthen-LibIDN2-version-determination.patch::https://github.com/getdnsapi/getdns/commit/ed7bfa107e9e.patch)
sha256sums=('ea8713ce5e077ac76b1418ceb6afd25e6d4e39e9600f6f5e81d3a3a13a60f652'
            'SKIP'
            '2ae1d20b16da5dfd113682b7aa5585ea7cd75a88fd1dc60dedcc626af8dab0e2')
validpgpkeys=(DC34EE5DB2417BCC151E5100E5F8F8212F77A498) # Willem Toorop <willem@nlnetlabs.nl>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../${pkgname}-strengthen-LibIDN2-version-determination.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_STATIC=OFF \
    -DCURRENT_DATE=$(date -u --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" "+%Y-%m-%dT%H:%M:%SZ") \
    -DPATH_TRUST_ANCHOR_FILE=/etc/trusted-key.key
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
