# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

shopt -s extglob

pkgbase=ghc
pkgname=(ghc-libs ghc ghc-static)
pkgver=8.8.3
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('x86_64')
url='https://www.haskell.org/ghc/'
license=('custom')
makedepends=('ghc-static' 'perl' 'libxslt' 'docbook-xsl' 'python-sphinx' 'haskell-hscolour'
             'texlive-bin' 'texlive-latexextra' 'ttf-dejavu' 'alex' 'happy' 'time')
source=("https://downloads.haskell.org/~ghc/$pkgver/$pkgbase-${pkgver}-src.tar.xz"
        ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook)
noextract=("$pkgbase-${pkgver}-src.tar.xz")
sha512sums=('7db0d820a288e56bd32935cc03584295900605f6e7cac6fe6adcb3ec6c24d8d2cebf7a8efa421f2d2fa192602d7f24dfad1ddeeba252455e4cb8baa9889dcaa9'
            'd69e5222d1169c4224a2b69a13e57fdd574cb1b5932b15f4bc6c7d269a9658dd87acb1be81f52fbcf3cb64f96978b9943d10cee2c21bff0565aaa93a5d35fcae'
            '3dd22d48827a64c7b92418bb590d533e3e39ebd325a6a829f4573093476176c2107ae810108ec48973c253a643e3371483b1ae6bbb49d88fe12d3510876b009e'
            '6141310437a96e2ec485153eff3442d2d8ef9c128812fb27c592d183f8f909e6c47118c9284a080d2b683e0d9c4185909559ccc398753bd1534610c0fcd60c54')

prepare() {
  # Need to extract this tarball with a UTF-8 locale instead of a chroot's "C"
  # locale; otherwise we get:
  #   bsdtar: Pathname can't be converted from UTF-8 to current locale.
  LANG=en_US.UTF-8 bsdtar xf $pkgbase-${pkgver}-src.tar.xz

  cd ghc-$pkgver

  cp mk/build.mk{.sample,}
  sed -i '1iBuildFlavour = perf' mk/build.mk
}

build() {
  cd ghc-$pkgver

  ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/ghc \
    --with-system-libffi \
    --with-ffi-includes=$(pkg-config --variable=includedir libffi)
  make
}

package_ghc-static() {
  pkgdesc='The Glasgow Haskell Compiler - Static Libraries and Documentation'
  depends=('ghc')

  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  mv "$pkgdir"/usr/lib/ghc-$pkgver/package.conf.d "$srcdir"/static-package.conf.d
  find "$pkgdir"/usr/lib ! \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -type f -delete
  find "$pkgdir"/usr/lib -type d -empty -delete
  mv "$srcdir"/static-package.conf.d "$pkgdir"/usr/lib/ghc-$pkgver/
  rm -r "$pkgdir"/usr/bin "$pkgdir"/usr/share/man

  install -Dm644 "$srcdir"/ghc-rebuild-doc-index.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-rebuild-doc-index.hook
}

package_ghc() {
  pkgdesc='The Glasgow Haskell Compiler'
  provides=("haskell-ghc=$pkgver")
  replaces=("haskell-ghc")
  depends=('gcc' 'ghc-libs')

  cd ghc-$pkgver
  make DESTDIR="$pkgdir" -j1 install

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc-pkg is in ghc-libs
  rm "$pkgdir"/usr/lib/ghc-$pkgver/bin/ghc-pkg*
  rm "$pkgdir"/usr/bin/ghc-pkg*
  (cd "$pkgdir"/usr/lib/ghc-$pkgver; rm -r !(bin|ghc-$pkgver))

  # docs moved to ghc-static
  rm -r "$pkgdir"/usr/share/doc

  install -Dm644 utils/completion/ghc.bash \
    "$pkgdir/usr/share/bash-completion/completions/ghc"
}

package_ghc-libs() {
  pkgdesc='The Glasgow Haskell Compiler - Dynamic Libraries'
  install='ghc.install'
  depends=('gmp' 'libffi' 'perl')
  provides=('haskell-array=0.5.4.0'
            'haskell-base=4.13.0.0'
            'haskell-binary=0.8.7.0'
            'haskell-bytestring=0.10.10.0'
            'haskell-containers=0.6.2.1'
            'haskell-deepseq=1.4.4.0'
            'haskell-directory=1.3.6.0'
            'haskell-filepath=1.4.2.1'
            'haskell-ghc-boot=8.8.3'
            'haskell-ghc-boot-th=8.8.3'
            'haskell-ghc-compact=0.1.0.0'
            'haskell-ghc-heap=8.8.3'
            'haskell-ghc-prim=0.5.3'
            'haskell-haskeline=0.7.5.0'
            'haskell-hpc=0.6.0.3'
            'haskell-integer-gmp=1.0.2.0'
            'haskell-libiserv=8.8.3'
            'haskell-mtl=2.2.2'
            'haskell-parsec=3.1.14.0'
            'haskell-pretty=1.1.3.6'
            'haskell-process=1.6.8.0'
            'haskell-stm=2.5.0.0'
            'haskell-template-haskell=2.15.0.0'
            'haskell-terminfo=0.4.1.4'
            'haskell-text=1.2.4.0'
            'haskell-time=1.9.3'
            'haskell-transformers=0.5.6.2'
            'haskell-unix=2.7.2.2'
            'haskell-xhtml=3000.2.2.1'
            'haskell-cabal=3.0.1.0')
  replaces=('haskell-array'
            'haskell-base'
            'haskell-binary'
            'haskell-bytestring'
            'haskell-containers'
            'haskell-deepseq'
            'haskell-directory'
            'haskell-filepath'
            'haskell-ghc-boot'
            'haskell-ghc-boot-th'
            'haskell-ghc-compact'
            'haskell-ghc-heap'
            'haskell-ghc-prim'
            'haskell-haskeline'
            'haskell-hpc'
            'haskell-integer-gmp'
            'haskell-libiserv'
            'haskell-mtl'
            'haskell-parsec'
            'haskell-pretty'
            'haskell-process'
            'haskell-stm'
            'haskell-template-haskell'
            'haskell-terminfo'
            'haskell-text'
            'haskell-time'
            'haskell-transformers'
            'haskell-unix'
            'haskell-xhtml'
            'haskell-cabal')

  provides+=("haskell-ghci=$pkgver")
  conflicts+=('haskell-ghci')

  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc library and other exes are in the ghc package
  rm -r "$pkgdir"/usr/lib/ghc-$pkgver/ghc-$pkgver
  (cd "$pkgdir"/usr/lib/ghc-$pkgver/bin; rm !(ghc-pkg*))
  (cd "$pkgdir"/usr/bin; rm !(ghc-pkg*))

  # docs moved to ghc-static
  rm -r "$pkgdir"/usr/share/{man,doc}

  install -Dm644 "$srcdir"/ghc-register.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-register.hook
  install -Dm644 "$srcdir"/ghc-unregister.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-unregister.hook

  install -dm755 "$pkgdir"/usr/share/haskell/{register,unregister}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
