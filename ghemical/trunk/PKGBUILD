# Maintainer: Xyne <ac xunilhcra enyx, backwards>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=ghemical
pkgver=3.0.0
pkgrel=2
pkgdesc='Computational chemistry package'
license=("GPL")
arch=(i686 x86_64)
url="http://bioinformatics.org/ghemical/ghemical/"
depends=('libghemical>=3.0' 'liboglappth>=1.0' 'libglade' 'gtkglext' 'openbabel')
makedepends=('pkgconfig' 'intltool>=0.40.0' 'gettext')
source=(http://bioinformatics.org/ghemical/download/release20111012/${pkgname}-${pkgver}.tar.gz ghemical.desktop)
md5sums=('becf98626f0eba73f7f042bc92aa60ac'
         '7e8789d42d1318a427a30f3bf64cb5a6')

build() {
  cd -- "$srcdir/$pkgname-$pkgver/"
  ./configure --prefix=/usr
  # Some users have reported crashes due to the courier font.
  sed -i 's/courier 12/monospace/g' ./src/*.cpp
  make
}

package() {
  install -D -m644 "$srcdir"/ghemical.desktop "$pkgdir"/usr/share/applications/ghemical.desktop
  cd -- "$srcdir/$pkgname-$pkgver/"
  make DESTDIR="$pkgdir" install
}
