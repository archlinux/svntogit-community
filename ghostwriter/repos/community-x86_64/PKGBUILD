# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>
# Contributor: Bastien "neitsab" Traverse <firstname [at] lastname [dot] email>
# Contributor: Icaro Perseo <icaroperseo[at]protonmail[dot]com>
# Contributor: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>

pkgname=ghostwriter
pkgver=2.1.0
pkgrel=1
pkgdesc='Aesthetic, distraction-free Markdown editor'
arch=('x86_64')
url='https://wereturtle.github.io/ghostwriter/'
license=('GPL3')
depends=('hunspell' 'qt5-svg' 'qt5-webengine')
makedepends=('qt5-tools')
optdepends=('cmark: processing and rendering CommonMark'
            'mathjax: formula rendering in live preview'
            'pandoc: Pandoc supported Markdown flavors and export to various formats')
source=("https://github.com/wereturtle/ghostwriter/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('5a377e006bec14abbb935ed4ad027f489051b3ab49a0e33cc8e07c67e5b4799c')

prepare() {
  cd $pkgname-$pkgver
  # Unbundle MathJax
  sed -i 's/typeof window.MathJax !==/typeof window.MathJax.typeset !==/' resources/preview.html
  sed -i 's|qrc:3rdparty/MathJax/bin/tex-svg-full.js|file:///usr/share/mathjax/tex-svg-full.js|' resources/preview.html
  sed -i '/MathJax/d' resources.qrc
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
