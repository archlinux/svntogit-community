# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(xcas libgiac)
_pkgver=1.5.0-1
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="A free computer algebra system"
arch=(x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(mpfi gsl pari ntl lapack libpng libjpeg fltk texlive-core texlive-science hevea glpk libao)
source=("http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgbase}_${_pkgver}.tar.gz")
sha256sums=('bf627fb53d928c817184473a6389d46a41386e1d32a71dac9a10897219a74b43')

prepare() {
  cd $pkgbase-${pkgver%.*}
  sed -e '/curlbuild/d' -i src/misc.cc # Remove bogus include
}

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

package_xcas() {
  depends=(libgiac fltk)
  conflicts=(giac)
  provides=(giac)
  replaces=(giac)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari ntl lapack libpng curl glpk libao)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}
