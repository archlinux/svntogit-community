# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(xcas libgiac)
_pkgver=1.5.0-59
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="A free computer algebra system"
arch=(x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(mpfi gsl pari ntl lapack libpng libjpeg fltk texlive-core texlive-science hevea glpk libao gmp-ecm)
source=("http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgbase}_${_pkgver}.tar.gz")
sha256sums=('3f97a48e5c170dda28b26e9830a4eed7acade409e1b851dc1dd0a26fbc1e0f5e')

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

package_xcas() {
  depends=(libgiac fltk)
  conflicts=(giac)
  provides=(giac)
  replaces=(giac)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari ntl lapack libpng curl glpk libao gmp-ecm)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}
