# $Id: PKGBUILD 175037 2016-05-12 15:18:07Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(xcas libgiac)
_pkgver=1.4.9-17
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="A free computer algebra system"
arch=(i686 x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(mpfi gsl pari ntl lapack libpng libjpeg fltk texlive-core)
source=("http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgbase}_${_pkgver}.tar.gz")
sha256sums=('5d2d5856b58b882509428e00b26bee64bee468966bbe4b5423b74768c0e14b39')

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

package_xcas() {
  depends=(libgiac fltk)
  conflicts=(giac)
  provides=(giac)
  replaces=(giac)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari ntl lapack libpng)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}
