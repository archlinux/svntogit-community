# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=giada
pkgver=0.19.2
pkgrel=1
pkgdesc="A free, minimal, hardcore audio tool for DJs, live performers and electronic musicians"
arch=(x86_64)
url="https://www.giadamusic.com/"
license=(GPL3)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libx11 libxcursor libxft libxinerama
libxpm)
# upstream vendors a hacked rtaudio :(
makedepends=(alsa-lib catch2 cmake fltk imagemagick jack libpulse libsamplerate
libsndfile libxrandr nlohmann-json rtmidi)
checkdepends=(xorg-server-xvfb)
provides=(vst3-host)
source=(
  "https://www.giadamusic.com/data/${pkgname}-v${pkgver}-src.tar.gz"
  "$pkgname-0.19.0-devendor_nlohmann_json.patch"
)
sha512sums=('a929dbfbc829d1c83e2f46a109b68c410dde6dcf9724f9195e1e1228a1943746499bf603c50b0a4015d21d3c08a0705a250ae77abc340b24754af5e7e3517074'
            '4a6be28c0ba1656e4ce63504c031d6a89ae5fdf10a5652021e5a248e3202526095d097cf1337adf03efa7f618a485aadeb5b6024f15950a7b72cd23ad8cd97d6')
b2sums=('6d0da5682b30698db0be4bfcece677268d73c400cf8077ad35efa3884656b60d376a6d81b29203cb2e8c82897f61f2ac9c55103061c462228699a6cdbff72524'
        '41fce95d1df10e78c080553184bb078abe30eec7ca85b9d3883ffd2191e4b51decee9c946f0b1aa10852e054c28a4e5f8bde62d4f0093d95e6197df464f61e59')

prepare() {
  mv -v "$pkgname-$pkgver-src" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # devendor nlohmann-json
  patch -Np1 -i ../"$pkgname-0.19.0-devendor_nlohmann_json.patch"
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_VST3=ON \
        -W no-dev \
        -B build \
        -S "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DWITH_VST3=ON \
        -DWITH_TESTS=ON \
        -W no-dev \
        -B build-test \
        -S "$pkgname-$pkgver"
  make VERBOSE=1 -C build
  make VERBOSE=1 -C build-test
}

check(){
  xvfb-run ./build-test/giada --run-tests
}

package() {
  depends+=(libasound.so libfreetype.so libjack.so libpulse.so
  libpulse-simple.so librtmidi.so libsamplerate.so libsndfile.so)

  make DESTDIR="$pkgdir/" install -C build
  # docs
  install -vDm 644 "$pkgname-$pkgver"/{ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
