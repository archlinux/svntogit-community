# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Philipp Wolfer <ph.wolfer@gmail.com>

pkgname=gifski
pkgver=1.7.0
pkgrel=1
pkgdesc='GIF encoder based on libimagequant (pngquant). Squeezes maximum possible quality from the awful GIF format.'
arch=('x86_64')
url='https://gif.ski'
license=('AGPL3')
depends=('ffmpeg4.4')
makedepends=('cargo' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ImageOptim/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('67d3dbdd55acc6a550d9e96094574305ff845ea3445041409bed6654150644118f8db236a1577b4868dcb422b4deb748e624a9b0b9e62d2895df45a48e0491f9')
options=('!lto')

build() {
  cd $pkgname-$pkgver

  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  cargo build --release --features=video
}

check() {
  cd $pkgname-$pkgver

  cargo test
}

package() {
  cd $pkgname-$pkgver

  install -Dm 755 target/release/gifski "$pkgdir"/usr/bin/gifski
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
