# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=gigedit
pkgver=1.1.0
pkgrel=1
pkgdesc="Gigasampler instrument editor"
arch=('x86_64')
url="https://www.linuxsampler.org/"
license=('GPL')
depends=('linuxsampler' 'gtkmm3')
makedepends=('intltool' 'libxslt')
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('643fcb51ba6dcdb11e0c579726e4f0c715e3fa6779e7e318206af71123a1b7b1cd4cbec29230b0f76f083642b600acd13003398dd95a7fdab021fd9eb1a83d61')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # fix linking of /usr/lib/gigedit/libgigedit.so.4.0.0
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${pkgname}" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}

# vim:set ts=2 sw=2 et:
