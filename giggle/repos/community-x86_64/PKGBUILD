# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: neodreams <yanbrodeur@gmail.com>

pkgname=giggle
pkgver=0.7
pkgrel=2
pkgdesc="Git repository viewer"
arch=(i686 x86_64)
url="http://live.gnome.org/giggle"
license=(GPL)
depends=(git gtksourceview3 vte3 desktop-file-utils hicolor-icon-theme)
makedepends=(intltool itstool)
options=(!libtool)
install=giggle.install
source=(http://ftp.gnome.org/pub/GNOME/sources/giggle/0.7/$pkgname-$pkgver.tar.xz
        gtksourceview3.7.patch)
sha256sums=('ae2e2237fa0d0999d99d0a0ee9b7ec147e80bd2472d59a045b2b01eb02261f59'
            'ca0b841d11bb5072741784dbeb72705eddf8e2bde780b8712355d5b66e456b37')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../gtksourceview3.7.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
