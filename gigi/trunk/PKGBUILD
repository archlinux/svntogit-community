# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: fana-m <geminin@gmx.net>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Manuel Gaul <inkaine@hotmail.com>

pkgname=gigi
pkgver=0.8.0
pkgrel=11
_fixedrevision=1074
pkgdesc="Small, efficient and feature rich GUI for C++ and OpenGL"
url="http://gigi.sourceforge.net/"
arch=('x86_64' 'i686')
license=('LGPL')
depends=('boost' 'mesa' 'libpng' 'libjpeg' 'libtiff' 'freetype2' 'ogre' 'sdl' 'ois' 'python')
makedepends=('subversion' 'cmake' 'setconf' 'doxygen')
conflicts=('gigi-svn' 'mesa-full')

build() {
  cd "$srcdir"

  msg2 "Fetching files..."
  if [ -d gigi/.svn ]; then
    (cd gigi && svn up -r "$_fixedrevision")
  else
    svn co https://gigi.svn.sourceforge.net/svnroot/gigi/trunk/GG/ \
      --config-dir ./ -r "$_fixedrevision" gigi
  fi

  rm -rf gigi-build
  svn export gigi gigi-build
  cd gigi-build

  msg2 "Patching..."
  sed -i 's/move_backward(/adobe::move_backward(/g' GG/adobe/vector.hpp
  sed -i 's/__GNUC_MINOR__ <= 6/__GNUC_MINOR__ <= 999/g' GG/adobe/cmath.hpp
  sed -i 's/insert(adobe/this->insert(adobe/g' GG/adobe/closed_hash.hpp
  sed -i 's:TIME_UTC:TIME_UTC_:' src/GUI.cpp

  msg2 "Configuring..."
  setconf cmake/GiGiOgre.pc.in prefix /usr
  setconf cmake/GiGi.pc.in prefix /usr
  setconf cmake/GiGiSDL.pc.in prefix /usr
  cmake \
    -D CMAKE_INSTALL_PREFIX="$pkgdir"/usr \
    -D BUILD_TUTORIALS=OFF \
    -D CPACK_GENERATOR=GiGiDevel \
    -D CMAKE_C_FLAGS_RELEASE="-DNDEBUG" \
    .

  msg2 "Building documentation..."
  cd doc
  doxygen
  cd ..

  msg2 "Compiling..."
  make
}

package() {
  cd "$srcdir/gigi-build"

  msg2 "Packaging files..."
  make install

  msg2 "Fixing documentation..."
  mkdir -p "$pkgdir/usr/share/doc"
  mv "$pkgdir/usr/doc" "$pkgdir/usr/share/doc/$pkgname"

  for f in `find "$pkgdir/usr/share/doc" -name "dir_*.html"`; do
    sed -i "s:$srcdir:/:g" "$f"
  done

  msg2 "Packaging license..."
  install -Dm 644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
