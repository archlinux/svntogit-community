# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=gimagereader
pkgname=(gimagereader-gtk gimagereader-qt gimagereader-common)
pkgver=3.3.1
pkgrel=2
url="https://github.com/manisandro/gImageReader"
pkgdesc="Gtk/Qt front-end to tesseract-ocr"
arch=(x86_64)
license=(GPL3)
depends=(desktop-file-utils djvulibre gtksourceviewmm gtkspellmm libxml++ libzip podofo
         poppler-glib poppler-qt5 qt5-imageformats qtspell quazip sane tesseract)
makedepends=(cmake intltool python-gobject)
source=(https://github.com/manisandro/gImageReader/releases/download/v$pkgver/$pkgbase-$pkgver.tar.xz
        openmp.patch
        gimagereader-quazip1.patch
        gimagereader-qt5.14.patch::"https://github.com/manisandro/gImageReader/commit/6209e25d.patch"
        gimagereader-automoc.patch)
sha256sums=('7688c9c20cbf2fa4f5b6ba0a8d77bd8d2e5929882ed965c7c07ccc529673f0dd'
            'ee84de7fee390ee54152b4e332e50d8f585b0c9a3aa8b40a04f865b5250607c2'
            'c693f97984667bff435af2ba1028b44e9dffaac957d0e39bc454c097bfc3ed21'
            '5e213cde5d5078c18031dfd0922c5399e387303cfca12b421e220c1dad07a506'
            '37668af2c039d756b1661078610aed07120d88441cc38851a2563dc2f8fac4a5')

prepare() {
  [[ -d build-gtk ]] || mkdir build-gtk
  [[ -d build-qt ]] || mkdir build-qt
  [[ -d gimagereader-common ]] || mkdir gimagereader-common

  cd $pkgbase-$pkgver

  # Run OpenMP once before calling tesseract
  # https://github.com/manisandro/gImageReader/issues/285
  patch -Np1 -i ../openmp.patch

  patch -p1 -i ../gimagereader-quazip1.patch # Fix build with quazip 1.0
  patch -p1 -i ../gimagereader-qt5.14.patch # Fix build with Qt 5.14
  patch -p1 -i ../gimagereader-automoc.patch # Fix linking errors (Fedora)
}

build() {
  cd build-gtk
  cmake -G "Unix Makefiles" ../$pkgbase-$pkgver/ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINTERFACE_TYPE=gtk
  make

  cd ../build-qt
  cmake -G "Unix Makefiles" ../$pkgbase-$pkgver/ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINTERFACE_TYPE=qt5
  make
}

package_gimagereader-gtk() {
  pkgdesc="Gtk front-end to tesseract-ocr"
  depends=(gimagereader-common djvulibre gtksourceviewmm gtkspellmm libxml++ libzip podofo poppler-glib sane tesseract)

  cd build-gtk
  make DESTDIR="$pkgdir" install

  # Split gimagereader-common
  mv "$pkgdir"/usr/share/{doc,icons,locale} \
     "$srcdir/gimagereader-common/"
}

package_gimagereader-qt() {
  pkgdesc="Qt front-end to tesseract-ocr"
  depends=(gimagereader-common desktop-file-utils djvulibre podofo poppler-qt5 qt5-imageformats qtspell quazip sane tesseract)

  cd build-qt
  make DESTDIR="$pkgdir" install

  # Split gimagereader-common
  rm -r "$pkgdir"/usr/share/{doc,icons,locale}
}

package_gimagereader-common() {
  pkgdesc="Common files for gImageReader"
  depends=(hicolor-icon-theme)

  mkdir -p "$pkgdir"/usr/share
  mv gimagereader-common/* "$pkgdir/usr/share"
}
