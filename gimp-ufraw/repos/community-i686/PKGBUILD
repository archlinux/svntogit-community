# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Martin Sugioarto <nakal@web.de>

pkgname=gimp-ufraw
pkgver=0.19
pkgrel=1
pkgdesc='Converter for raw files; utility and GIMP plugin'
url='http://ufraw.sourceforge.net/'
arch=('x86_64' 'i686')
license=('GPL')
depends=('gtkimageview' 'exiv2' 'lcms' 'bzip2' 'desktop-file-utils' 'cfitsio' 'lensfun')
makedepends=('gimp' 'cinepaint')
optdepends=('gimp: to use the gimp import plugin for raw images'
            'cinepaint: to use the cinepaint import plugin for raw images')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/ufraw/ufraw-$pkgver.tar.gz")
sha256sums=('12d551418c5e69ae904cb92e9ac11b4d2e4327a857ee13ac890f34320eb58b9a')

build() {
  cd "$srcdir/ufraw-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-extras \
    --enable-mime \
    --enable-openmp \
	  --with-gimp \
	  --with-cinepaint \
	  --enable-contrast
  sed -i "s/-ffast-math -fomit-frame-pointer -W -Wall -O3/${CFLAGS}/" Makefile
  make
}

package() {
  cd "$srcdir/ufraw-$pkgver"

  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

# vim:set ts=2 sw=2 et:
