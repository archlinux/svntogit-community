# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Martin Sugioarto <nakal@web.de>

pkgname=gimp-ufraw
pkgver=0.22
pkgrel=10
pkgdesc='Converter for raw files; utility and GIMP plugin'
url='http://ufraw.sourceforge.net/'
arch=('x86_64' 'i686')
license=('GPL')
depends=('gtkimageview' 'exiv2' 'lcms' 'desktop-file-utils' 'cfitsio' 'lensfun')
makedepends=('gimp' 'cinepaint')
optdepends=('gimp: to use the gimp import plugin for raw images'
            'cinepaint: to use the cinepaint import plugin for raw images')
source=("http://downloads.sourceforge.net/ufraw/ufraw-$pkgver.tar.gz"
        '001-no-gimp-remote.patch'
        '002-CVE-2015-8366.patch'
        '003-fix-unsigned-char.patch'
        '004-gcc-7.patch')
sha256sums=('f7abd28ce587db2a74b4c54149bd8a2523a7ddc09bedf4f923246ff0ae09a25e'
            '5a3d33edc738228ea5442cef95d2914cd20fbbae911b1005a5cb0d7931fdeda1'
            '3fc1cb962d1023e6c81e2c93353faae8ee395ba31ea121265686918ad51cb219'
            'dbb0dc200867cb67f2ba7688b164e28393ce409092fb759e79b0912dceb3424a'
            '5a4be00a7548eb78d7d325646b80ceae2d54fd8bfd46f977586b5e1272af3e4c')

prepare() {
  # One of the include files that comes with cinepaint requires
  # ../lib/wire/ to exist...
  mkdir -p lib
  ln -s /usr/include/cinepaint/wire lib/wire
  
  cd "ufraw-$pkgver"

  patch -Np1 < "${srcdir}"/001-no-gimp-remote.patch
  patch -Np1 < "${srcdir}"/002-CVE-2015-8366.patch
  patch -Np1 < "${srcdir}"/003-fix-unsigned-char.patch
  patch -Np1 < "${srcdir}"/004-gcc-7.patch
}

build() {
  cd "ufraw-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-extras \
    --enable-mime \
    --enable-openmp \
    --with-gimp \
    --with-cinepaint \
    --enable-contrast
  sed -i "s/-ffast-math -fomit-frame-pointer -W -Wall -O3/${CFLAGS}/" Makefile
  make
}

package() {
  make -C "ufraw-$pkgver" DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/bin/dcraw"
}

# vim:set ts=2 sw=2 et:
