# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=ginac
pkgver=1.6.2
pkgrel=3
pkgdesc="C++ library for symbolic calculations"
arch=('i686' 'x86_64')
url='http://www.ginac.de/'
license=('GPL')
depends=('cln')
makedepends=('transfig' 'doxygen' 'texlive-bin')
install=${pkgname}.install
source=(http://www.ginac.de/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('0f34d255024b3fb258e44da84f9e82dfb3dc9bc15f0999b7a18204a730663bde')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
