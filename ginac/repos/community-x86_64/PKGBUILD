# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=ginac
pkgver=1.6.1
pkgrel=1
pkgdesc='A set of libraries that allow the creation of integrated systems that embed symbolic manipulations.'
arch=('i686' 'x86_64')
url='http://www.ginac.de'
license=('GPL')
depends=('cln')
makedepends=('transfig' 'doxygen' 'texlive-bin')
options=('!libtool')
source=("ftp://ftpthep.physik.uni-mainz.de/pub/GiNaC/ginac-${pkgver}.tar.bz2")
md5sums=('d383f3ee50f88ffa1b3966fdb11d0f12')

build() {
  cd ginac-${pkgver}

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd ginac-${pkgver}

  make check
}

package() {
  cd ginac-${pkgver}

  make DESTDIR=${pkgdir} install
}
