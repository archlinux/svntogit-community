# Maintainer: mlq <mlq@pwmt.org>

pkgname=girara-gtk$_girara_gtk_version-git
pkgver=20120306
pkgrel=1
pkgdesc="user interface library"
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/girara"
license=('custom')
depends=('gtk2>=2.18.6' 'girara-common-git')
makedepends=('git')

conflicts=(girara-gtk$_girara_gtk_version)
provides=(girara-gtk$_girara_gtk_version)

_gitroot='git://pwmt.org/girara.git'
_gitname='girara'
_branch='develop'

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git checkout $_branch && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
    cd $_gitname && git checkout --track -b $_branch origin/$_branch
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make GIRARA_GTK_VERSION=$_girara_gtk_version
}

package() {
  cd "$srcdir/$_gitname-build"

  # install files
  make GIRARA_GTK_VERSION=$_girara_gtk_version DESTDIR="$pkgdir/" install

  # remove common files
  rm -r "$pkgdir/usr/include"
  rm -r "$pkgdir/usr/share"

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
