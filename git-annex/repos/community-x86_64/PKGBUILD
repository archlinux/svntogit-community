# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=git-annex
pkgver=6.20160419
pkgrel=9
pkgdesc="Manage files with git, without checking their contents into git"
url="http://git-annex.branchable.com/"
license=("AGPL3")
arch=('i686' 'x86_64')
depends=('git' 'rsync' 'libxml2' 'gsasl' 'file')
makedepends=("ghc=7.10.3" "haskell-aeson" "haskell-async" "haskell-aws" "haskell-blaze-builder"
             "haskell-bloomfilter" "haskell-byteable" "haskell-case-insensitive"
             "haskell-clientsession" "haskell-concurrent-output" "haskell-conduit"
             "haskell-conduit-extra" "haskell-crypto-api" "haskell-cryptonite"
             "haskell-data-default" "haskell-dav" "haskell-dbus" "haskell-disk-free-space"
             "haskell-dlist" "haskell-dns" "haskell-edit-distance" "haskell-esqueleto"
             "haskell-exceptions" "haskell-fdo-notify" "haskell-feed" "haskell-gnutls"
             "haskell-hinotify" "haskell-hslogger" "haskell-http-client" "haskell-http-conduit"
             "haskell-http-types" "haskell-ifelse" "haskell-json" "haskell-magic"
             "haskell-missingh" "haskell-monad-control" "haskell-monad-logger"
             "haskell-mountpoints" "haskell-mtl" "haskell-network" "haskell-network-info"
             "haskell-network-multicast" "haskell-network-protocol-xmpp" "haskell-network-uri"
             "haskell-old-locale" "haskell-optparse-applicative" "haskell-path-pieces"
             "haskell-persistent" "haskell-persistent-sqlite" "haskell-persistent-template"
             "haskell-quickcheck" "haskell-random" "haskell-regex-tdfa" "haskell-resourcet"
             "haskell-safesemaphore" "haskell-sandi" "haskell-securemem" "haskell-shakespeare"
             "haskell-stm" "haskell-tasty" "haskell-tasty-hunit" "haskell-tasty-quickcheck"
             "haskell-tasty-rerun" "haskell-text" "haskell-torrent" "haskell-unix-compat"
             "haskell-utf8-string" "haskell-uuid" "haskell-wai" "haskell-wai-extra" "haskell-warp"
             "haskell-warp-tls" "haskell-xml-types" "haskell-yesod" "haskell-yesod-core"
             "haskell-yesod-default" "haskell-yesod-form" "haskell-yesod-static")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('1452a55809ba7367847fc4a77f2d8a5365af92e130d5ca460bf0880a26a6fb8e')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        -fcryptonite -fnetwork-uri -f-ekg -fconcurrentoutput -ftorrentparser \
        -ftestsuite -f-androidsplice -f-android -fproduction -fpairing -fwebapp \
        -fassistant -fwebdav -fs3 -f-benchmark -fdbus -fxmpp -fmagicmime
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    install -Dm644 bash-completion.bash "${pkgdir}/usr/share/bash-completion/completions/git-annex"

    rm "$pkgdir/usr/share/doc/git-annex/COPYRIGHT"
    rmdir "$pkgdir/usr/share/doc/git-annex" "$pkgdir/usr/share/doc"
}
