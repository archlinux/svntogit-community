# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Daniel Perez <tuvistavie@gmail.com>
# Contributor: AGWA
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: noirbizarre <noirbizarre@gmail.com>
# Contributor: Wael Nasreddine <wael.nasreddine@gmail.com>

pkgname=git-crypt
pkgver=0.7.0
pkgrel=1
pkgdesc='Transparent file encryption in Git'
arch=(x86_64)
url="https://www.agwa.name/projects/$pkgname"
license=(GPL3)
depends=(git
         openssl)
makedepends=(libxslt)
_archive="$pkgname-$pkgver"
# Upstream has temporarily lost access to PGP keys, see:
# https://github.com/AGWA/git-crypt/issues/245
source=("$url/downloads/$_archive.tar.gz") #{,.asc})
sha256sums=('50f100816a636a682404703b6c23a459e4d30248b2886a5cf571b0d52527c7d8')
            # 'SKIP')
# validpgpkeys=('EF5D84C1838F2EB6D8968C0410378EFC2080080C') # Andrew Ayer <agwa@andrewayer.name>

build() {
	cd "$_archive"
	make ENABLE_MAN=yes PREFIX=/usr
}

package() {
	cd "$_archive"
	make ENABLE_MAN=yes PREFIX=/usr DESTDIR="$pkgdir" install
}
