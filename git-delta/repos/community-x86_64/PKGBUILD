# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor:  Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor : ccorn

pkgname=git-delta
_pkgname=delta
pkgver=0.10.0
pkgrel=1
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=('x86_64')
url="https://github.com/dandavison/delta"
license=('MIT')
depends=('libgit2')
makedepends=('rust')
checkdepends=('git')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('8aaef22778edf1b1d3118d6a743a26f03bfb25cedbed11b4b4a783b98160cb6efbea0d0c67a7ffd37a89a46690e2bdb76096803277cede77aad905afe00cafb9')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
