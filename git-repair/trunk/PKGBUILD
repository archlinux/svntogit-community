# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=git-repair
pkgver=1.20170626
pkgrel=11
pkgdesc="Repairs a damaged git repisitory"
url="http://git-repair.branchable.com/"
license=("GPL")
arch=('x86_64')
depends=('git' 'ghc' "haskell-async" "haskell-data-default" "haskell-exceptions" "haskell-hslogger"
         "haskell-ifelse" "haskell-mtl" "haskell-network" "haskell-network-uri"
         "haskell-optparse-applicative" "haskell-quickcheck" "haskell-split" "haskell-text"
         "haskell-unix-compat" "haskell-utf8-string")
makedepends=('ghc')
source=("$pkgname::git://git-repair.branchable.com/#tag=$pkgver")
sha512sums=('SKIP')

build() {
  cd git-repair
  
  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/$pkgname" \
      -fnetwork-uri
  runhaskell Setup build
}

package() {
  cd git-repair
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 git-repair.1 "$pkgdir"/usr/share/man/man1/git-repair.1

  rm "$pkgdir"/usr/share/doc/git-repair/GPL
  rmdir "$pkgdir"/usr/share/doc/git-repair "$pkgdir"/usr/share/doc
}
