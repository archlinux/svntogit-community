# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Frederik Schwan <frederik dot schwan at linux dot com>

pkgname=gitea
pkgver=1.7.3
pkgrel=1
pkgdesc="Painless self-hosted Git service. Community managed fork of Gogs."
arch=('x86_64')
url="https://gitea.io"
license=('MIT')
depends=('git')
makedepends=('go-pie' 'go-bindata' 'dep')
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'redis: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/gitea/app.ini')
_tag=bfd52f320019c6980cc24a3e0040b5c0d2dd91d7 # git rev-parse v1.7.3
source=("git+https://github.com/go-gitea/gitea.git#tag=${_tag}?signed"
        gitea-disable-u2f.patch::"https://patch-diff.githubusercontent.com/raw/go-gitea/gitea/pull/5994.patch"
        gitea.tmpfiles
        gitea.service
        gitea.sysusers
        gitea-arch-defaults.patch
        gitea-ldflags.patch)
sha256sums=('SKIP'
            '08915e2456d0c20bf786559ba316b49c03de11bfc4b37f8f0ad4e577baf0e3df'
            '1521fd7edc3830c695698ffe9835709f1408040b5ec989f07410972c894fa8ba'
            '7789b3f6699b9e111fa080226047cdc765e55ff49a3f72aac989c11c06c3e7e0'
            '2abc51ccd0086bb996194bb7fb241a3f26d84f518417c872b66a3db8970da7e4'
            '913a28110d2fbdabcfd2d19366aad8c645813b512f4591882efb47795ad8472f'
            'c552a5adb4058530d67176713240cc435b7b974747ec51e972c37d8834e32b3f')
validpgpkeys=('8C4033A23895237CB27D52D9D9B5613BEB813F99'  # Matti Ranta <matti@mdranta.net>, retrieved from https://github.com/techknowlogick.gpg
              'ED810FD31FBE67F406ED71BDD4F1E9B6493ED946'  # Jonas Franz <info@jonasfranz.software>
              '9C5BCD799B3CDB124147A748E0DDFEC24C48784C') # Lauris Buk≈°is-Haberkorns <lauris@nix.lv> 
install=gitea.install

prepare() {
  mkdir -p gopath/src/code.gitea.io
  ln -rTsf ${pkgname} gopath/src/code.gitea.io/gitea
  export GOPATH="${srcdir}"/gopath

  cd ${pkgname}
  # Change some defaults for ArchLinux
  patch -Np1 -i ../gitea-arch-defaults.patch
  # Fix LDFLAGS not being respected by Go
  patch -Np1 -i ../gitea-ldflags.patch
  # https://github.com/go-gitea/gitea/issues/4692
  patch -Np1 -i ../gitea-disable-u2f.patch

  cd $GOPATH/src/code.gitea.io/gitea
  dep ensure
}

build() {
  export GOPATH="${srcdir}"/gopath
  cd gopath/src/code.gitea.io/gitea
  make generate
  EXTRA_GOFLAGS="-gcflags all=-trimpath=${GOPATH} -asmflags all=-trimpath=${GOPATH}" \
  make GOFLAGS="-v" TAGS="bindata sqlite pam" build
}

package() {
  install -Dm755 ${pkgname}/${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
  install -D ${pkgname}/custom/conf/app.ini.sample "${pkgdir}"/etc/gitea/app.ini
}
