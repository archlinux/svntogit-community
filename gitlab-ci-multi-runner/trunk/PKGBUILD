# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=gitlab-ci-multi-runner
pkgver=1.1.3
pkgrel=1
pkgdesc="The official GitLab CI runner written in Go"
arch=('i686' 'x86_64')
url='https://gitlab.com/gitlab-org/gitlab-ci-multi-runner'
license=('GPLv3')
depends=('ca-certificates' 'curl' 'git' 'glibc' 'tar')
makedepends=('git' 'go' 'godep' 'go-bindata' 'mercurial')
install='gitlab-runner.install'
backup=('etc/gitlab-runner/config.toml')
noextract=('prebuilt.tar.gz')
source=("${pkgname}-${pkgver}.tar.gz"::"https://gitlab.com/gitlab-org/gitlab-ci-multi-runner/repository/archive.tar.gz?ref=v${pkgver}"
        "https://gitlab-ci-multi-runner-downloads.s3.amazonaws.com/v${pkgver}/docker/prebuilt.tar.gz"
        "gitlab-runner.install"
        "gitlab-runner.service"
        "gitlab-runner.sysusers"
        "gitlab-runner.tmpfiles"
        "config.toml"
        "godeps.patch")
sha256sums=('fc05b5c0dab6251383319b6b4b7df9760c18f92073da34b49f6427d6f85f3478'
            'ad7aa6ba0f4f2be88772a82b1e52c6daf4da0b613c8b29c18b23a5abe117f7b3'
            '289f73a16df5c8823a15b67a4aaa635d32b9c49ce4e11baa5d33f12828a1e0bb'
            '87150287276b06851297d3508b33b1fd17ae95d93c811ba2ce6839dd1bc76895'
            '3f9afd6e117315a57232ada6b3637943ecd7588539a1b4686096b9cc4d65bae9'
            '8b17ed458da6926ea0d28aa4cb307b42b047bda9dfa4a96ee48e1c02051efb13'
            'b1f7d9a6a0148884528584909729cce817253759650da61d641e2225845f9330'
            'ec91955d60f3127237651050dc6cc64e5dfb8efb9485d514b301c558b16a1834')

prepare() {
    _srcdir=$(ls "${srcdir}" | grep -E "${pkgname}-v${pkgver}-[0-9a-f]{40}")

    mkdir -p "${srcdir}/src/gitlab.com/gitlab-org/"

    ln -sf "${srcdir}/${_srcdir}" "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    cd "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    # Patch invalid revisions in Godeps/Godeps.json
    patch -p0 -i "${srcdir}/godeps.patch"

    GOPATH="${srcdir}" godep restore

    ln -sf "${srcdir}/prebuilt.tar.gz" prebuilt.tar.gz
}

build() {
    _rev=$(ls "${srcdir}" | grep -Po "${pkgname}-v${pkgver}-\K([0-9a-f]{7})")

    cd "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    GOPATH="${srcdir}" go-bindata \
        -pkg docker \
        -nocompress \
        -nomemcopy \
        -prefix out/docker/ \
        -o executors/docker/bindata.go \
        prebuilt.tar.gz

    GOPATH="${srcdir}" go build \
        -ldflags "-X main.NAME=${pkgname} -X main.VERSION=${pkgver} -X main.REVISION=${_rev}"
}

package() {
    cd "${srcdir}/src/gitlab.com/gitlab-org/${pkgname}"

    install -Dm644 "${srcdir}/config.toml" "${pkgdir}/etc/gitlab-runner/config.toml"
    install -Dm644 "${srcdir}/gitlab-runner.service" "${pkgdir}/usr/lib/systemd/system/gitlab-runner.service"
    install -Dm644 "${srcdir}/gitlab-runner.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-runner.conf"
    install -Dm644 "${srcdir}/gitlab-runner.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/gitlab-runner.conf"
    install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
