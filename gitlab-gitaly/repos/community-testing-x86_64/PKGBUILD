# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-gitaly
pkgver=13.11.3
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby2.7 libxml2 libxslt libssh2)
options=(!buildflags)
makedepends=(go cmake git)
backup=("etc/gitlab-gitaly/config.toml")
_tag=v${pkgver}
source=("https://gitlab.com/gitlab-org/gitaly/-/archive/${_tag}/gitaly-${_tag}.tar.gz"
        "configs.patch"
        "gitlab-gitaly.service"
	"ruby27.patch")
sha512sums=('9823c6bc317c6fa1e30fc3eb15709508454c145ac57e95bb152d7c7a7046d08a50b0d9d0bebe6d825d49ea327b391160d31d2cae094b47eea2dd44ecb7dcbe43'
            '2988766ff7839664af2b621984eee3b52056d7c606823deb77a4d7604069b1e294a6364e0607c5e629617fc49a80ad6ac76be5673e9d5b26fa54c2b7e66bc4ca'
            '7f5cd528c873a5e43e18aa6a88bd7298422c047e0e61cf3208be7d9fcfdfdc8a844b5c439ab6afc2098c5c4c60ed9c3d167c2f87517f1e93b34f39be3d3dad09'
            'ce6a20bb4348c04ed633cd71716ef836dfd3b0207307735305c12e5b76bef9df8654a96dfba10222a5c21cb126adc0cce008b515c377a2ea81d8c1e13c976c29')

prepare() {
  cd gitaly-$_tag

  patch -p1 < ../ruby27.patch

  patch -p1 < ../configs.patch
  # At this point the config file should not contain any references to '/home/git'

  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i ruby/Gemfile.lock
}

build() {
  cd gitaly-$_tag

  bundle-2.7 config force_ruby_platform true # build from sources as some prebuilt gems are not available for newer ruby
  make BUILD_TAGS="tracer_static tracer_static_jaeger"
}

package() {
  cd gitaly-$_tag

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/etc/gitlab-gitaly
  mkdir -p "${pkgdir}"/usr/share/webapps/gitlab-gitaly
  cp -r ruby "${pkgdir}"/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}"/etc/${pkgname}/config.toml
  install -Dm644 "LICENSE" "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 "${srcdir}"/gitlab-gitaly.service "${pkgdir}"/usr/lib/systemd/system/gitlab-gitaly.service
}
