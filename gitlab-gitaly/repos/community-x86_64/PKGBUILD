# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=gitlab-gitaly
pkgver=0.93.0
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby2.3 libxml2 libxslt)
options=(!buildflags)
makedepends=(go ruby2.3-bundler cmake)
backup=("etc/gitlab-gitaly/config.toml")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitaly/repository/archive.tar.gz?ref=v${pkgver}"
        "gitlab-gitaly.service")
sha512sums=('046db8eeb6243e4a32a7497240e8b17c6e01b61d5aadf073b1dd0e388035b21cce3b06fd38ccf86a5ebde5ee544f98b8f564ba0aaf371a04a396d9c9a662eafc'
            'b330c0f42592322ad2131079ca554a13a364007182dded8c556198caff2c9ec642acf5bb7dfecb05de5a3d89bffec6588b6d05c8c7c5c771a46df3d296deed28')

_homedir="/var/lib/gitlab"

prepare() {
  cd "gitaly-v${pkgver}-"*

  sed -i "s/bundle install/bundle-2.3 install/" Makefile
  sed -i "s/bundle config/bundle-2.3 config/" Makefile
  sed -i "s/bundle/bundle-2.3/" internal/rubyserver/rubyserver.go
  sed -i "s/bundle/bundle-2.3/" internal/linguist/linguist.go
  sed -i "s/\"ruby\"/\"ruby-2.3\"/" internal/linguist/linguist.go
  sed -i "s/VERSION = .*/VERSION = ${pkgver}/" Makefile
  sed -i "s/env ruby/env ruby-2.3/" ruby/bin/gitaly-ruby
  sed -i "s/env ruby/env ruby-2.3/" ruby/bin/ruby-cd
  sed -i "s|dir = \"/home/git/gitaly/ruby\"|dir = \"/usr/share/webapps/gitlab-gitaly/ruby\"|" config.toml.example
  sed -i "s|dir = \"/home/git/gitlab-shell\"|dir = \"/usr/share/webapps/gitlab-shell\"|" config.toml.example
  sed -i "s|bin_dir = \"/home/git/gitaly\"|bin_dir = \"/usr/bin\"|" config.toml.example
}

build() {
  cd "gitaly-v${pkgver}-"*

  make build
}

package() {
  cd "gitaly-v${pkgver}-"*

  make PREFIX=/usr DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}/etc/${pkgname}"
  sed \
    -e "s|^socket_path =.*|socket_path = \"${_homedir}/sockets/gitlab-gitaly.socket\"|" \
    -e "s|^path =.*|path = \"${_homedir}/repositories\"|" \
    config.toml.example > "${pkgdir}/etc/${pkgname}/config.toml"

  mkdir -p ${pkgdir}/usr/share/webapps/gitlab-gitaly
  cp -r ruby ${pkgdir}/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}/usr/share/${pkgname}/config.toml.example"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-gitaly.service" "${pkgdir}/usr/lib/systemd/system/gitlab-gitaly.service"
}

# vim:set ts=2 sw=2 et:
