# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-gitaly
pkgver=13.2.3
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby libxml2 libxslt libssh2)
options=(!buildflags)
makedepends=(go ruby-bundler cmake)
backup=("etc/gitlab-gitaly/config.toml")
_tag=v${pkgver}
source=("https://gitlab.com/gitlab-org/gitaly/-/archive/${_tag}/gitaly-${_tag}.tar.gz"
        "configs.patch"
        "gitlab-gitaly.service")
sha512sums=('d3fbdadfd044811a30a0c734fa4482ff59e6bd71c4ce628332919e775b502f080f32331018c3ecdd3b9890d0c3f4dede014bf8bda2aafced0a5f2db80bf6ea7d'
            '6565b3dbed2653c31d5a49043dd8227812d9c8a1f6d716ed65fed5878b9c9bd6def84e8b00ff23ddb37d8bf2f484ecb229dcb1528e8276fff7fbbb1099790850'
            '7f5cd528c873a5e43e18aa6a88bd7298422c047e0e61cf3208be7d9fcfdfdc8a844b5c439ab6afc2098c5c4c60ed9c3d167c2f87517f1e93b34f39be3d3dad09')

prepare() {
  cd gitaly-$_tag

  patch -p1 < ../configs.patch
  # At this point the config file should not contain any references to '/home/git'

  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i ruby/Gemfile.lock
}

build() {
  cd gitaly-$_tag

  bundle config force_ruby_platform true # build from sources as some prebuilt gems are not available for newer ruby
  make BUILD_TAGS="tracer_static tracer_static_jaeger"
}

package() {
  cd gitaly-$_tag

  make PREFIX=/usr DESTDIR=${pkgdir} install
  mkdir -p ${pkgdir}/etc/gitlab-gitaly
  mkdir -p ${pkgdir}/usr/share/webapps/gitlab-gitaly
  cp -r ruby ${pkgdir}/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}/etc/${pkgname}/config.toml"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-gitaly.service" "${pkgdir}/usr/lib/systemd/system/gitlab-gitaly.service"
}
