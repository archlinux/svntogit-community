# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-gitaly
pkgver=15.7.5
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby2.7 libxml2 libxslt libssh2)
options=(!buildflags)
makedepends=(go cmake git)
backup=("etc/gitlab-gitaly/config.toml")
_tag=v${pkgver}
source=("https://gitlab.com/gitlab-org/gitaly/-/archive/${_tag}/gitaly-${_tag}.tar.gz"
        "configs.patch"
        "gitlab-gitaly.service"
	"ruby27.patch")
sha512sums=('061695bee937c9497f9f0588599526ae55ad857694b54c97eb40d97d5a814cedf8cde2aba2b9aaecbf4ef019a553ebf6ea9c6b3727adb8c8b9901418ee2870e3'
            'ba4343e1646e301e038d50e89509ec6fe376628506374489eb63f7ea36a1eec36ca073f69abd883bbca8079294a5bfaacea4bab2abf8a3044b79304ee09a69df'
            '7f5cd528c873a5e43e18aa6a88bd7298422c047e0e61cf3208be7d9fcfdfdc8a844b5c439ab6afc2098c5c4c60ed9c3d167c2f87517f1e93b34f39be3d3dad09'
            'efaf482c40ec74a96b324f3c2e4a4ffb5a55c3faa46fd2bead9ae5db3986d1526c92d6f2911fe2c5bf349737b345827598d598b98188c49ecc901cb935f9fcb0')

prepare() {
  cd gitaly-$_tag

  patch -p1 < ../ruby27.patch

  patch -p1 < ../configs.patch
  # At this point the config file should not contain any references to '/home/git'

  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i ruby/Gemfile.lock
}

build() {
  cd gitaly-$_tag

  pushd ruby
  bundle-2.7 config force_ruby_platform true # build from sources as some prebuilt gems are not available for newer ruby
  bundle-2.7 install --path vendor/bundle
  popd

  make V=1 BUILD_TAGS="tracer_static tracer_static_jaeger"
}

package() {
  cd gitaly-$_tag

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/etc/gitlab-gitaly
  mkdir -p "${pkgdir}"/usr/share/webapps/gitlab-gitaly
  cp -r ruby "${pkgdir}"/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}"/etc/${pkgname}/config.toml
  install -Dm644 "LICENSE" "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 "${srcdir}"/gitlab-gitaly.service "${pkgdir}"/usr/lib/systemd/system/gitlab-gitaly.service
}
