# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-gitaly
pkgver=13.0.4
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby libxml2 libxslt libssh2)
options=(!buildflags)
makedepends=(go ruby-bundler cmake)
backup=("etc/gitlab-gitaly/config.toml")
_tag=v${pkgver}
source=("https://gitlab.com/gitlab-org/gitaly/-/archive/${_tag}/gitaly-${_tag}.tar.gz"
        "ruby27.patch::https://gitlab.com/gitlab-org/gitaly/-/merge_requests/2227.patch"
        "configs.patch"
        "gitlab-gitaly.service")
sha512sums=('6b47f5873290cbe92dbd5a475e0950ccd738702d27f43d51bfd0841ad40ccd164d83c29871127f30171cee0b0aa5cad2b26ba417b79cf53615e9f3524be72e6b'
            'ffb96b3cb8360654e2bbfa5014d7bf569169ef43e0466f2553169fba8757555c4efeda07828b3491ed0397a9be6ffe5320ee7a6e882c28ca211bd13007122c93'
            '28bfb8f290d6d6206b75e2f35d8fd15b298baa005b533648550f224c6bf4fd16eeda4e2b8ff3231873ef208172f391a553956684f81867451bc08b7ad18d581f'
            'e6c31cab200424af118ee9da4def0963a52727f33025fdbdb515c6c1337a9b32f4a377bd925fdd33ffe7b91371ce1d5d70847e07c2b73eac1e85d3f38f5e1261')

prepare() {
  cd gitaly-$_tag

  patch -p1 < ../configs.patch
  # At this point the config file should not contain any references to '/home/git'

  patch -p1 < ../ruby27.patch

  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i ruby/Gemfile.lock
}

build() {
  cd gitaly-$_tag

  bundle config force_ruby_platform true # build from sources as some prebuilt gems are not available for newer ruby
  make BUILD_TAGS="tracer_static tracer_static_jaeger"
}

package() {
  cd gitaly-$_tag

  make PREFIX=/usr DESTDIR=${pkgdir} install
  mkdir -p ${pkgdir}/etc/gitlab-gitaly
  mkdir -p ${pkgdir}/usr/share/webapps/gitlab-gitaly
  cp -r ruby ${pkgdir}/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}/etc/${pkgname}/config.toml"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-gitaly.service" "${pkgdir}/usr/lib/systemd/system/gitlab-gitaly.service"
}

# vim:set ts=2 sw=2 et:
