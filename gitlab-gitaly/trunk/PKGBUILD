# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-gitaly
pkgver=1.33.0
pkgrel=2
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby2.5 libxml2 libxslt)
options=(!buildflags)
makedepends=(go ruby2.5-bundler cmake)
backup=("etc/gitlab-gitaly/config.toml")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitaly/repository/archive?sha=v${pkgver}"
        "gitlab-gitaly.service")
sha512sums=('e34073fc2d7128dcc62b37b7c97fce906c5aec4f48b13e1771b7971cb7b2f56bf39443653012672d90a8604f5b75059b29279f4d76f9be0cfdd897d8bbbaf39a'
            'b330c0f42592322ad2131079ca554a13a364007182dded8c556198caff2c9ec642acf5bb7dfecb05de5a3d89bffec6588b6d05c8c7c5c771a46df3d296deed28')

_homedir="/var/lib/gitlab"

prepare() {
  cd "gitaly-"*

  sed -i "s/bundle install/bundle-2.5 install/" Makefile
  sed -i "s/bundle config/bundle-2.5 config/" Makefile
  sed -i "s/bundle show/bundle-2.5 show/" Makefile
  sed -i "s/bundle/bundle-2.5/" internal/rubyserver/rubyserver.go
  sed -i "s/bundle/bundle-2.5/" internal/linguist/linguist.go
  sed -i "s/bundle/bundle-2.5/" _support/makegen.go
  sed -i "s/\"ruby\"/\"ruby-2.5\"/" internal/linguist/linguist.go
  sed -i "s/env ruby/env ruby-2.5/" ruby/bin/gitaly-ruby
  sed -i "s/env ruby/env ruby-2.5/" ruby/bin/ruby-cd
  sed -i "s/VERSION = .*/VERSION = ${pkgver}/" Makefile
  sed -i "s|dir = \"/home/git/gitaly/ruby\"|dir = \"/usr/share/webapps/gitlab-gitaly/ruby\"|" config.toml.example
  sed -i "s|dir = \"/home/git/gitlab-shell\"|dir = \"/usr/share/webapps/gitlab-shell\"|" config.toml.example
  sed -i "s|bin_dir = \"/home/git/gitaly\"|bin_dir = \"/usr/bin\"|" config.toml.example

  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i ruby/Gemfile.lock
}

build() {
  cd "gitaly-"*

  make build
}

package() {
  cd "gitaly-"*

  make PREFIX=/usr DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}/etc/${pkgname}"
  sed \
    -e "s|^socket_path =.*|socket_path = \"${_homedir}/sockets/gitlab-gitaly.socket\"|" \
    -e "s|^path =.*|path = \"${_homedir}/repositories\"|" \
    config.toml.example > "${pkgdir}/etc/${pkgname}/config.toml"

  mkdir -p ${pkgdir}/usr/share/webapps/gitlab-gitaly
  cp -r ruby ${pkgdir}/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}/usr/share/${pkgname}/config.toml.example"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-gitaly.service" "${pkgdir}/usr/lib/systemd/system/gitlab-gitaly.service"
}

# vim:set ts=2 sw=2 et:
