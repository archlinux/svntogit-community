# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Lee Watson <aur@revthefox.co.uk>
# Contributor: Tobias Hunger <tobias DOT hunger AT gmail DOT com>
# Contributor: Stefan Tatschner <stefan@sevenbyte.org>


# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-shell
pkgver=9.0.0
pkgrel=1
epoch=2
pkgdesc="Git management software"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby2.5' 'redis' 'git')
makedepends=('go-pie')
options=('!strip')
install=gitlab-shell.install
backup=("etc/webapps/${pkgname}/config.yml"
        "etc/webapps/${pkgname}/secret"
        "etc/webapps/${pkgname}/gitconfig"
        "etc/webapps/${pkgname}/authorized_keys"
        "etc/webapps/${pkgname}/")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitlab-shell/repository/archive?sha=v${pkgver}"
        "gitlab-shell.gitconfig"
        "gitlab-shell.sysusers"
        252.patch)
sha512sums=('81ad0ac5148d4128af05e8db9c6a3c58f11c8ba1d1323ed7bdc7a56d86690b3f918ebcfed4307d5aa0546ba46f6632020c5a95faf83c34287959c698955bb60d'
            '5caafe5300621ced58479e724b39656afc17f32a64ecde5dfa4919683ef37d9121552c78f066678472acb56e0a34b5c3cf34d9b44414d1ed5d91f725d180fa43'
            '196fd5519f1019815562da5b8bfde1d8082614cee6e63d75a7bb673db8b3c409384177dcf3dd0436c2adeec273b4ffea690058c2c457ae6a82d4f805e860495f'
            'd8f24575a3ff848c2eb1b8c32f98942377d17729a874048a3a1ab97f8bdbbca556fe0e8c1e87ef301aa3abd7b3a62fb414a46d99cd346dde51b792e4622d2a49')

_datadir="/usr/share/webapps/${pkgname}"
_etcdir="/etc/webapps/${pkgname}"
_homedir="/var/lib/gitlab"
_logdir="/var/log/gitlab"
_srcdir="gitlab-shell-v${pkgver}"

prepare() {
  cd "${srcdir}/${_srcdir}"-*

  # Do not remove: https://bugs.archlinux.org/task/60466 https://bugs.archlinux.org/task/61164
  patch -Np1 -i $srcdir/252.patch

  sed -e "s|user: git|user: gitlab|" \
      -e "s|/home/git|${_homedir}|" \
      -e "s|# log_file: .*|log_file: \"${_logdir}/gitlab-shell.log\"|" \
      -e "s|# host:|host:|" \
      -e "s|# port:|port:|" \
      -e "s|socket:|# socket:|" \
      config.yml.example > config.yml

  for file in {bin,hooks}/*; do
    sed -i s/ruby/ruby-2.5/g ${file}
  done
}

build() {
  cd "${srcdir}/${_srcdir}"-*

  ./bin/compile
}

package() {
  cd "${srcdir}/${_srcdir}"-*

  install -dm755 -o root -g root "${pkgdir}${_datadir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}/satellites"
  install -dm700 -o 105 -g 105 "${pkgdir}${_homedir}/.ssh"
  install -dm750 -o 105 -g 105 "${pkgdir}${_etcdir}"
  install -dm770 -o 105 -g root "${pkgdir}${_logdir}"

  # sysusers
  install -Dm644 "${srcdir}/gitlab-shell.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-shell.conf"

  # .gitconfig
  install -Dm644 -o 105 -g 105 "${srcdir}/gitlab-shell.gitconfig" "${pkgdir}${_etcdir}/.gitconfig"
  ln -fs "${_etcdir}/.gitconfig" "${pkgdir}${_homedir}/.gitconfig"

  # .gitlab_shell_secret
  touch "${pkgdir}${_etcdir}/secret"
  chmod 640 "${pkgdir}${_etcdir}/secret"
  chown root:105 "${pkgdir}${_etcdir}/secret"
  ln -fs "${_etcdir}/secret" "${pkgdir}${_datadir}/.gitlab_shell_secret"

  # config.yml
  mv config.yml "${pkgdir}${_etcdir}"
  ln -fs "${_etcdir}/config.yml" "${pkgdir}${_datadir}/config.yml"

  cp -r VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
  ln -fs "${_datadir}" "${pkgdir}/${_homedir}/"

  install -dm2770 -o 105 -g 105 "${pkgdir}${_homedir}/repositories"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
