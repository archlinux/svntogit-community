# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab-workhorse
pkgver=8.57.0
pkgrel=1
pkgdesc="A smart reverse proxy for GitLab that handles large HTTP requests such as file downloads, file uploads, git push/pull and git archive downloads"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitlab-workhorse"
license=('MIT')
depends=('glibc' 'perl-image-exiftool')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitlab-workhorse/repository/archive?sha=v${pkgver}"
        "gitlab-workhorse.service")
sha512sums=('2220e933543f1f5ce21276bc5729a6ec623d4218543029d9aafc169c59793b930efc24261e851240221e51c4767c4fdc2068efae2a585a79ded0fb6eda22edd2'
            '893527f270179fc78ff60e33f0b44abd984d1e9859dd5bf59fea77245ddd4c1c252d2c7882719366944d0f9341a90b5e077470b15afcc3df419b0d0a9f2e6d56')

build() {
  cd "${pkgname}-v${pkgver}-"*

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd "${pkgname}-v${pkgver}-"*

  install -Dm755 "gitlab-workhorse" "${pkgdir}/usr/bin/gitlab-workhorse"
  install -Dm755 "gitlab-zip-cat" "${pkgdir}/usr/bin/gitlab-zip-cat"
  install -Dm755 "gitlab-zip-metadata" "${pkgdir}/usr/bin/gitlab-zip-metadata"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-workhorse.service" "${pkgdir}/usr/lib/systemd/system/gitlab-workhorse.service"
}
