# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer:  Christian Hesse <mail@eworm.de>
# Contributor: Jon Eyolfson <jon@eyolfson.com>

pkgname=gitolite
pkgver=3.6.7
pkgrel=2
pkgdesc='An access control layer on top of git'
url='https://github.com/sitaramc/gitolite'
arch=('any')
license=('GPL2')
depends=('git' 'perl' 'openssh')
install=gitolite.install
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz"
        'gitolite-sysusers.conf')
sha256sums=('f75d15bb438986b77bd8aeda7839dcd3624ae9d09dbfce245c00b906de8dbdc5'
            '4113e21968b8954cb467ef89fac8485727acf31219bdb971494ca1d98efdd6af')

prepare() {
  cd $pkgname-$pkgver/src
  echo "$pkgver" > VERSION
}

package() {
  # create directories
  install -d "$pkgdir"/usr/{bin,lib/gitolite}

  # install files and create symlink
  cp -a $pkgname-$pkgver/src/* "$pkgdir"/usr/lib/gitolite
  ln -s /usr/lib/gitolite/gitolite "$pkgdir"/usr/bin/

  # sysusers file
  install -D -m 0644 "${srcdir}"/gitolite-sysusers.conf "${pkgdir}"/usr/lib/sysusers.d/gitolite.conf
}
