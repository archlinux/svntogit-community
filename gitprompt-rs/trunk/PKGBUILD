# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Streetwalrus <streetwalrus@codewalr.us>

pkgname=gitprompt-rs
pkgver=0.3.0
pkgrel=1
pkgdesc='Simple Git prompt'
arch=('x86_64')
url=https://github.com/Streetwalrus/gitprompt-rs
license=('MPL2')
depends=('git')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate")
b2sums=('7108951cc7e71ee5fb99f48f110fc5e82d1570f4e47453639d93e0783e2b67d56df3c3607eb7f8b10eae87680f743326c54e76ea5543c74410a01a8d9f3c6469')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked --offline
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
}
