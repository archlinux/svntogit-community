# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contriubutor: Jamie Magee <jamie dot magee at gmail dot com>

pkgname=gitsign
pkgver=0.2.0
pkgrel=2
pkgdesc='Keyless Git signing using Sigstore'
arch=('x86_64')
url='https://github.com/sigstore/gitsign'
license=(APACHE)
makedepends=(go)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('ac0e38a269e30a2334efb971d3ec9281ea0fe6d90a14278c3f76cdb755f57d504dd9a2779d87d33d209f889fd3589899f5a2cae7f3733d7ae114f35e4a787733')

build() {
  cd $pkgname-$pkgver
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make build-all
}

package() {
  cd $pkgname-$pkgver

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
