# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.17.1
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('x86_64')
license=('MIT')
depends=('gcc-libs' 'libgit2>=1.2.0' 'libgit2.so' 'libxcb' 'zlib')
makedepends=('cargo' 'python') # xcb crate needs python
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('36821f83d67a1233e10daa51178c03250618964ca5a5b8c001ac338bf7a355f5')
b2sums=('1db679d998efa822df84cc625cc4ac6499f6723acd32d98d86ad4c0d563eb0f3f9d4eb332a11b63d3685fb3ea06931685b90f5efc6cde3845fcb1783205142d9')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo test --frozen --release
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
