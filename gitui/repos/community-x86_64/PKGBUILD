# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.16.1
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libgit2' 'libgit2.so' 'libxcb')
makedepends=('cargo' 'python') # xcb crate needs python
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('84fd4c1c004301a12b8f96f3f9218cda5a1263e8bab8480d3857553562a057f2')
b2sums=('b85f5788837438238f69c1dca1086078d0a6badf120dedece5d52db857b1b0016f0ee8c3b6cf18bb5c2758c5db92800b0f475520dfddf5a5ee8b2e88edeace21')

build() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
