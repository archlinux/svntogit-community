# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=1.7.4
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
depends=('python2' 'python2-psutil' 'python2-setuptools')
optdepends=('python2-jinja: HTML export'
            'python2-sensors: temperature sensors support')
source=($url/archive/v$pkgver.tar.gz)
sha256sums=('20960b5f191bc86e6184d937940f00a62100eb51388bcdeb233f30c809b0f1a6')

prepare() {
  cd $pkgname-$pkgver
  sed 's|^#!/usr/bin/env python$|&2|' -i glances/glances.py
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --prefix=/usr --root=$pkgdir
}
