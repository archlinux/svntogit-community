# $Id$
# Maintainer: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=1.7.6
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
depends=('python2' 'python2-psutil' 'python2-setuptools')
optdepends=('python2-jinja: HTML export'
            'python2-sensors: temperature sensors support'
            'hddtemp: HDD temperature monitoring support')
source=("$url/archive/v$pkgver.tar.gz")
sha256sums=('7d026bd6f0289e88a9acd3be813eb5288a2af2216bab7086a58bf560dc781279')

prepare() {
  cd $pkgname-$pkgver
  sed 's|^#!/usr/bin/env python$|&2|' -i glances/glances.py
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
