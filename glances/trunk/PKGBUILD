# $Id$
# Maintainer: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=1.7.5
pkgrel=2
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
depends=('python2' 'python2-psutil' 'python2-setuptools')
optdepends=('python2-jinja: HTML export'
            'python2-sensors: temperature sensors support'
            'hddtemp: HDD temperature monitoring support')
source=("$url/archive/v$pkgver.tar.gz"
        '01-adapt-to-psutil-2.0.0-api.patch')
sha256sums=('1598d1c10f1d2b59ea8d90d5be899fd151b4af28640c6daae23258dc76f659fe'
            '5b92ad4532d40cc1d08afb0390a79b3cabf9ab7f9ca184139cbc72e5b7d6c7f5')

prepare() {
  cd $pkgname-$pkgver
  sed 's|^#!/usr/bin/env python$|&2|' -i glances/glances.py
  patch -p1 -i "$srcdir/01-adapt-to-psutil-2.0.0-api.patch"
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
