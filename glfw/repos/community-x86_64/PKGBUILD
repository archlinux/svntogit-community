# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: philefou <tuxication AT gmail DOT com>
# Contributor: lindquist <tomas@famolsen.dk>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Mihai Militaru <mihai.militaru@ephemeros.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=('glfw' 'glfw-doc')
pkgver=3.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.glfw.org/"
license=('custom:ZLIB')
depends=('libgl' 'libxrandr' 'glu' 'xorg-xinput' 'libxxf86vm' 'libxcursor')
makedepends=('mesa' 'cmake' 'doxygen')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('928079b331b7c935f63e402df4a71a1b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON
  make docs
}

package_glfw() {
  pkgdesc="A free, open source, portable framework for OpenGL application development"
  cd "$srcdir/$pkgname-$pkgver"

  cd build
  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_glfw-doc() {
  pkgdesc="Set of HTML documentation for GLFW"
  mkdir -p "${pkgdir}/usr/share/doc/glfw/"
  cp -r "${srcdir}/glfw-${pkgver}/docs/html" "${pkgdir}/usr/share/doc/glfw"
}
