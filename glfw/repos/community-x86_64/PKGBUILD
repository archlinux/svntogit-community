# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: philefou <tuxication AT gmail DOT com>
# Contributor: lindquist <tomas@famolsen.dk>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Mihai Militaru <mihai.militaru@ephemeros.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgbase=glfw
pkgname=('glfw-x11' 'glfw-wayland' 'glfw-doc')
pkgver=3.3.6
pkgrel=1
arch=('x86_64')
url="https://www.glfw.org/"
license=('custom:ZLIB')
makedepends=('mesa' 'cmake' 'doxygen' 'vulkan-headers' 'vulkan-icd-loader'
             'extra-cmake-modules' 'wayland-protocols' 'libxi' 'libxrandr'
             'libxcursor' 'libxkbcommon' 'libxinerama')
source=("$pkgname-$pkgver.tar.gz::https://github.com/glfw/glfw/archive/${pkgver}.tar.gz")
sha512sums=('4c295c5f4c02f6ede125fce67c52a97450f552f3985b664745bd8a836c1d6d69c04727c956fd26ec61f1e5fd9c074a28dcf6b1d1800f118444eef066f048b201')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  mkdir build-x11 build-wayland
}

build() {
  cd "$srcdir/$pkgbase-$pkgver/build-x11"

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON
  make docs

  cd ../build-wayland

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DBUILD_SHARED_LIBS=ON \
      -DGLFW_USE_WAYLAND=ON
}

package_glfw-x11() {
  pkgdesc="A free, open source, portable framework for graphical application development (x11)"
  depends=('libxi' 'libxrandr' 'libxinerama' 'libxcursor' 'libgl')
  replaces=('glfw')
  conflicts=('glfw')
  provides=("glfw=$pkgver")

  cd "$srcdir/$pkgbase-$pkgver"/build-x11

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_glfw-wayland() {
  pkgdesc="A free, open source, portable framework for graphical application development (wayland)"
  depends=('wayland' 'libxkbcommon' 'libgl')
  conflicts=('glfw')
  provides=("glfw=$pkgver")

  cd "$srcdir/$pkgbase-$pkgver"/build-wayland

  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_glfw-doc() {
  pkgdesc="Set of HTML documentation for GLFW"

  mkdir -p "${pkgdir}/usr/share/doc/glfw/"
  cp -r "${srcdir}/glfw-${pkgver}/build-x11/docs/html" "${pkgdir}/usr/share/doc/glfw"
}
