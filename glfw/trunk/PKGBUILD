# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: philefou <tuxication AT gmail DOT com>
# Contributor: lindquist <tomas@famolsen.dk>
# Contributor: Christoph Siegenthaler <csi@gmx.ch>
# Contributor: Mihai Militaru <mihai.militaru@ephemeros.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=glfw
pkgver=3.0
pkgrel=1
pkgdesc="A free, open source, portable framework for OpenGL application development"
arch=('i686' 'x86_64')
url="http://www.glfw.org/"
license=('custom:ZLIB')
depends=('libgl' 'libxrandr' 'glu' 'xorg-xinput')
makedepends=('mesa' 'cmake' 'doxygen')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('415aef32d70af0cc829148c1cc7b1357')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  cd build
  make DESTDIR=$pkgdir install

  cd ..
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
