# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=glide
pkgver=0.5.9
pkgrel=1
pkgdesc='Simple and minimalistic media player based on GStreamer and GTK'
arch=('x86_64')
url='https://github.com/philn/glide'
license=('MIT')
depends=('gst-plugins-bad' 'gst-plugins-base' 'gst-plugins-good' 'gtk3')
makedepends=('meson' 'rust')
optdepends=('gst-libav: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs')
replaces=('glide-player')
source=("https://github.com/philn/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('e426e8b085a89e7dc384fc241bf161732735f083ddc4099342859c289951c6a3')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 $pkgname-$pkgver/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
