# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: fanningert <thomas@fanninger.at>

pkgname=glide
pkgver=0.13.0
pkgrel=2
pkgdesc='Dependency management and vendoring for Go projects'
arch=('x86_64')
_gourl="github.com/Masterminds"
url="https://$_gourl/glide"
license=('MIT')
makedepends=('git' 'go-pie')
depends=('glibc')
provides=('glide')
options=('!strip' '!emptydirs')
source=("git+$url#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  mkdir -p build/go && cd build/go
  for f in "/usr/lib/go/"*; do ln -s "$f"; done
  rm pkg && mkdir pkg && cd pkg
  for f in "/usr/lib/go/pkg/"*; do ln -s "$f"; done

  export GOPATH="$srcdir/build"
  export GOROOT="$GOPATH/go"

  mkdir -p "$GOPATH/src/$_gourl"
  mv "$srcdir/$pkgname" "$GOPATH/src/$_gourl/$pkgname"
}

build() {
  cd "$GOPATH/src/${_gourl}/${pkgname}"

  go fix && go build -o glide -ldflags "-X main.version=$pkgver" glide.go
}

package() {
  cd "build/src/$_gourl/$pkgname"

  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/Masterminds/glide
# vim: ts=2 sw=2 et:
