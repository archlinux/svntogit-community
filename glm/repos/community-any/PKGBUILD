# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Peter Hatina <phatina AT gmail.com>
pkgname=glm
pkgver=0.9.9.5
pkgrel=1
pkgdesc="C++ mathematics library for 3D software based on the OpenGL Shading Language (GLSL) specification"
arch=('any')
license=('MIT')
url="http://glm.g-truc.net"
makedepends=('cmake')
source=("https://github.com/g-truc/glm/releases/download/${pkgver}/glm-${pkgver}.zip")
sha512sums=('ea4f03d31596e27bd8c3d8d0a82a3ae0050ec0df5a747f3d92e467eacab579bb8b191499cb63bf8574da7a1085b9fe6dd7cd0ce93b758d8d7978b2389cc58b94')

package() {
    cd $pkgname

    mkdir build
    cd build

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make DESTDIR=$pkgdir install

    cd ..

    mkdir -p $pkgdir/usr/share/doc
    cp -r doc $pkgdir/usr/share/doc/glm

    find $pkgdir -type f -exec chmod 644 {} \;
    find $pkgdir -type d -exec chmod 755 {} \;

    # Fix https://bugs.archlinux.org/task/52501
    sed -i \
        "s/PACKAGE_VERSION_UNSUITABLE TRUE/PACKAGE_VERSION_UNSUITABLE FALSE/g" \
        $pkgdir/usr/lib/cmake/glm/glmConfigVersion.cmake

    # GCC 7.3 fix
    sed -i "s/(__GNUC__ == 7) && (__GNUC_MINOR__ == 2)/(__GNUC__ == 7) \&\& (__GNUC_MINOR__ >= 2)/" $pkgdir/usr/include/glm/simd/platform.h
}
