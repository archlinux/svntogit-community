# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>
# Contributor: farseerfc <farseerfc@gmail.com>

pkgname=glmark2
pkgver=2021.12
pkgrel=1
pkgdesc="An OpenGL 2.0 and ES 2.0 benchmark"
arch=('x86_64')
url="https://github.com/glmark2/glmark2"
license=('GPL3' 'custom:SGI')
depends=('libjpeg-turbo' 'libpng' 'libgl' 'systemd-libs')
makedepends=('meson' 'systemd' 'wayland-protocols')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        $pkgname-gcc12.patch::https://github.com/glmark2/glmark2/commit/d1ca3f53c96dc8a4048b17dc16147a8fac782d4a.patch)
sha256sums=('9f111284b2ef1d3fce91928e249e6ca00796a036831b063a549a0f3b03557a95'
            '4472e8e0ec755c4e41867bdb95b5aac46be77e7e10e2519b17c77eb989b2838e')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-gcc12.patch
}

build() {
  arch-meson $pkgname-$pkgver build \
    -Dflavors=drm-gl,drm-glesv2,wayland-gl,wayland-glesv2,x11-gl,x11-glesv2
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"

  cd $pkgname-$pkgver
  install -Dm644 COPYING.SGI -t "$pkgdir"/usr/share/licenses/$pkgname/
}
