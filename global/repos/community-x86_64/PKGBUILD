# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Fredy García <frealgagu at gmail dot com>
# Contributor: Randy Ramos <rramos1295 at gmail dot com>
# Contributor: Peter Vasil <mail@petervasil.net>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>
# Contributor: Matthias Meulien <orontee@gmail.com>

pkgname=global
pkgver=6.6.7
pkgrel=1
pkgdesc='Source code tagging system'
arch=(x86_64)
url='https://www.gnu.org/software/global/'
license=(GPL)
depends=(libltdl perl python sqlite)
optdepends=(ctags emacs idutils python-pygments vim)
options=(!emptydirs !libtool)
validpgpkeys=("26F631B43D624A927E6F1C33969C3BE389DDA6EB")
source=("https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
b2sums=('040ee878f1224ee9ab8ef001e73d550d23b970ddef42e092791a904747b357a39e6b6958ee0998f0396cf1fb83e81be7d69cae22f35810843305ac54c9bcad90'
        'SKIP')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/\.la/.so/g' gtags.conf.in
  # Rename lid to lid-idutils to avoid a name collision with idutils from AUR
  sed -i 's/usable("lid")/usable("lid-idutils")/g' "$pkgname/$pkgname.c"
}

build() {
  cd $pkgname-$pkgver
  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --with-exuberant-ctags=/usr/bin/ctags \
    --with-sqlite3
  make
}

check() {
  make -C $pkgname-$pkgver -k check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # Package the emacs script
  install -d "$pkgdir/usr/share/emacs/site-lisp"
  mv "$pkgdir/usr/share/gtags/gtags.el" \
    "$pkgdir/usr/share/emacs/site-lisp/gtags.el"

  # Package the vim scripts
  install -d "$pkgdir/usr/share/vim/vimfiles/plugin"
  mv "$pkgdir/usr/share/gtags/gtags.vim" \
    "$pkgdir/usr/share/vim/vimfiles/plugin/gtags.vim"
  mv "$pkgdir/usr/share/gtags/gtags-cscope.vim" \
    "$pkgdir/usr/share/vim/vimfiles/plugin/gtags-cscope.vim"
}
