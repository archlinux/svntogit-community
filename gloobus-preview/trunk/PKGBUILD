# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gloobus-preview
pkgver=0.4.5.333
pkgrel=3
pkgdesc="GNOME application designed to enable a full screen preview of any kind of file"
arch=('i686' 'x86_64')
url="http://gloobus.net/"
license=('GPL')
depends=('gst-plugins-base' 'gtksourceview3' 'poppler-glib' 'python-dbus' 'python-gobject')
makedepends=('boost' 'bzr' 'djvulibre' 'libgxps' 'libspectre')
optdepends=('djvulibre: Preview DjVu documents'
            'gst-libav: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'imagemagick: Support for more image formats'
            'libgxps: Preview XPS documents'
            'libspectre: Preview PostScript documents'
            'unoconv: Preview LibreOffice compatible documents')
install=$pkgname.install
source=($pkgname-$pkgver::bzr+http://bazaar.launchpad.net/~gloobus-dev/gloobus-preview/last_working_branch#revision=333)
md5sums=('SKIP')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
