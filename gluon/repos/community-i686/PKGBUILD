# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=gluon
pkgver=0.71.0
_pkgver=0.71
pkgrel=5
pkgdesc="A free and open source platform for creating and distributing games"
arch=('i686' 'x86_64')
url="http://gluon.gamingfreedom.org/"
license=('LGPL')
depends=('kdelibs' 'libsndfile' 'alure')
makedepends=('cmake' 'automoc4' 'mesa' 'kdevplatform')
install=gluon.install
source=("http://download.kde.org/unstable/$pkgname/$_pkgver/src/$pkgname-$pkgver.tar.gz"
        'gcc47.patch')
md5sums=('e4f284c0ae00e5b8a58cc3e6201857fe'
         '92adbf8b23f4af2a15a005b031c81c5f')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 -i "$srcdir"/gcc47.patch

  cd "$srcdir"
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install

  if [[ "$CARCH" == "x86_64" ]]; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}
