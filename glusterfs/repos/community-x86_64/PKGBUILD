# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributors:
#   Andrei Antoukh - niwi@niwi.be - http://www.niwi.be
#   henning mueller <henning@orgizm.net>

pkgname=glusterfs
pkgver=3.5.0
_basever=`echo $pkgver | cut -f1-2 -d.`
pkgrel=1
pkgdesc='Is a cluster file-system capable of scaling to several peta-bytes.'
arch=(i686 x86_64)
url='http://www.gluster.org/'
license=(GPL2 LGPL3)
depends=(fuse python2 libxml2)
makedepends=(flex bison)
source=(http://download.gluster.org/pub/gluster/glusterfs/$_basever/$pkgver/glusterfs-$pkgver.tar.gz)
md5sums=('5c141ba3e5306d550d9092e3d1ad6d50')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  find -type f -name '*.py' -exec sed -i \
    -e 's:env\ python:env\ python2:' \
    -e 's:/usr/bin/python:/usr/bin/python2:' {} \;
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-mountutildir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --libexecdir=/usr/lib/$pkgname \
    --with-systemddir=/usr/lib/systemd/system \
    PYTHON=python2 LEXLIB=
  ./gen-headers.py
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make -j1 DESTDIR=$pkgdir install

  install -D -m 644 \
    $srcdir/$pkgname-$pkgver/{README,INSTALL,COPYING*} \
    $pkgdir/usr/share/doc/glusterfs/

  cp -rf \
    $srcdir/$pkgname-$pkgver/doc/* \
    $pkgdir/usr/share/doc/glusterfs/
}

# vim:set ts=2 sw=2 et:
