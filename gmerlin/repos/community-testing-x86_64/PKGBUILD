# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>

pkgname=gmerlin
pkgver=1.0.0
pkgrel=5
pkgdesc="Multimedia architecture for Linux"
arch=('i686' 'x86_64')
url="http://openmovieeditor.sourceforge.net/HomePage"
license=('GPL')
depends=('gavl' 'libxml2' 'gtk2' 'libxv' 'libvisual' 'libcdio>=0.82' 'musicbrainz' 'mjpegtools' 'hicolor-icon-theme' 'libgl')
makedepends=('alsa-lib' 'jack' 'libquicktime' 'pulseaudio' 'v4l-utils' 'mesa')
optdepends=('alsa-lib: for ALSA support'
	    'jack: for JACK support'
	    'libquicktime: for movie encoding'
	    'pulseaudio: for PulseAudio support'
	    'v4l-utils: for video conversion')
options=('!libtool' '!makeflags')
install=gmerlin.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz
	libpng.patch)
md5sums=('60d3c5081c8685ee7bc1afdfac8f6dc6'
         'c262023434246705296d97bc7337a212')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  rm -rf cpuinfo.sh

  patch -p1 -i "$srcdir/libpng.patch"
  ./configure --prefix=/usr --without-doxygen
  sed -i 's|LIBS = -lgavl|LIBS = -lgavl -ldl -lpthread -lgtk-x11-2.0 -lgobject-2.0 -lglib-2.0 -lxml2 -lgdk-x11-2.0 -lgdk_pixbuf-2.0 -lX11|' {lib,tests,apps/*}/Makefile
  sed -i 's|ln -sf $(DESTDIR)$(pkgdatadir)|ln -sf $(pkgdatadir)|' icons/Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
