# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgbase=gmic
pkgname=('gmic' 'gimp-plugin-gmic')
pkgver=1.5.1.7
pkgrel=1
pkgdesc="image procession framework"
arch=('i686' 'x86_64')
license=('custom:CeCILL')
url="http://gmic.sourceforge.net"
makedepends=('gimp' 'fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr' 'imagemagick' 'qt')
options=('docs' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_$pkgver.tar.gz"
	opencv-buildfix.patch)
md5sums=('da53449144f0edc9431358118e574e7c'
         '8d1cf5000239099255d25c7d3dbe5728')

build() {
  cd "$srcdir/gmic-$pkgver"
  patch -p1 <$srcdir/opencv-buildfix.patch
  make -C src all
}

package_gmic() {
  depends=('fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr')
  replaces=('greycstoration')

  cd "$srcdir/gmic-$pkgver"
  make -C src install DESTDIR="$pkgdir" USR=/usr
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gmic/LICENSE"
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the GMIC image procession framework"
  depends=('gimp' 'fftw' 'lapack')
  replaces=('gimp-plugin-greycstoration' 'gimp-plugin-gmic4gimp')

  cd "$srcdir/gmic-$pkgver"
  install -Dm755 src/gmic_gimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gimp-plugin-gmic/LICENSE"
}
