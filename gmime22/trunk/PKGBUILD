# $Id: PKGBUILD 54842 2009-10-11 19:49:14Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime22
pkgver=2.2.24
pkgrel=1
pkgdesc="Core mime parsing library"
arch=(i686 x86_64)
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2' 'zlib')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/gmime/2.2/gmime-${pkgver}.tar.bz2)
md5sums=('2d10d758de769c198efad50b086ee423')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd ${srcdir}/gmime-${pkgver}
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --disable-mono || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # These are gmime alternatives for the same shareutils tools
  rm -rf ${pkgdir}/usr/bin/uuencode ${pkgdir}/usr/bin/uudecode ${pkgdir}/usr/share && \
  mv ${pkgdir}/usr/bin/gmime-config ${pkgdir}/usr/bin/gmime22-config
}
