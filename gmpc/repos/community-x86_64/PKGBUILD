# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=gmpc
pkgver=11.8.16
pkgrel=2
pkgdesc='MPD client (Gtk2)'
arch=('x86_64' 'i686')
url='http://gmpclient.org/home'
license=('GPL')
depends=('libunique' 'libmpd' 'libsoup' 'sqlite' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('gob2' 'intltool' 'vala')
install='gmpc.install'
source=("http://download.sarine.nl/Programs/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.gz")
sha256sums=('a69414f35396846733632ca9619921d7acda537ffd6d49bd84b444945cb76b2c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's/GNOME;AudioVideo;/GTK;AudioVideo;Player;/' data/gmpc.desktop.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make CFLAGS+='-lm'
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
