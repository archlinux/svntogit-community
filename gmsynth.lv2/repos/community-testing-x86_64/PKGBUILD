# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gmsynth.lv2
pkgver=0.5.0
pkgrel=2
pkgdesc="General MIDI LV2 Synth"
arch=('x86_64')
url="https://x42-plugins.com/x42/x42-gmsynth"
license=('GPL2')
groups=('pro-audio' 'lv2-plugins')
depends=('glibc')
makedepends=('fluidsynth' 'lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v${pkgver}.tar.gz"
        "$pkgname-0.4.1-use-system-fluidsynth.patch")
sha512sums=('7059c560c514aa2fc5c342448f5437e29b84c7f391e163e7bb3312f39733270814d01e50dad18a793c3f2e7c5d09314a88dfa72a9c50d951935003741baf6256'
            '80dbc28624c2f3636c1ae8599b9b755d9b9733474254f10b2427e00c8d849ff997c5fad7ad80d08af6a67fb6c92946e71cc55d9fe6d45e5c34581d40cd9fde07')
b2sums=('70bcd32a166bcf01afbd1d6cc362bca50759af304603e84c79a9ccd40fd71e49161ce1801a130e7e4b2cfbdc711a21f702eb1e4dfc611d2ee224375f1a483bd0'
        '502b5fc39a2b1780cded2fd180d9bab1cd073a9a73654fd5a4f873031ab1575d3a3c1649ff3f636f3da81a2ab00aea87be89a3cc9815de79b2a099584f2b7007')

prepare() {
  cd "$pkgname-$pkgver"
  # use system fluidsynth
  patch -Np1 -i "../$pkgname-0.4.1-use-system-fluidsynth.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I build "http://gareus.org/oss/lv2/gmsynth"
}

package() {
  depends+=('libfluidsynth.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
