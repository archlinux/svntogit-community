# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=gmt
pkgname=(gmt gmt-doc)
pkgver=6.1.1
pkgrel=2
pkgdesc="Generic Mapping Tools for manipulating and plotting geographic and cartesian data"
arch=(x86_64)
url="https://www.generic-mapping-tools.org"
license=(LGPL)
makedepends=(cmake blas curl fftw gdal geos lapack openmp netcdf pcre zlib)
source=(https://github.com/GenericMappingTools/gmt/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.xz)
sha512sums=('14f9e7cfe0774217d7b6c593b58b054cba6324d8a9c96fd264cdafcbb9a6ec52f1e45917aeb6a166b692112a95e1459043771c11ad4e4a27dd301faa4b2122b8')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGMT_LIBDIR=lib \
    -DGMT_DATADIR=share/gmt \
    -DGMT_MANDIR=share/man \
    -DGMT_DOCDIR=share/doc/gmt \
    -DGMT_ENABLE_OPENMP=ON \
    -DGSHHG_ROOT=/usr/share/gmt/coast \
    -DDCW_ROOT=/usr/share/gmt/dcw \
    -DCOPY_GSHHG=OFF \
    -DCOPY_DCW=OFF
  make -C build
}

package_gmt() {
  depends=(blas curl fftw gdal geos lapack openmp netcdf pcre zlib)
  optdepends=(
    'gmt-doc: documentation'
    'ghostscript'
    'python-sphinx'
  )
#    'gmt-coast: coastlines'
#    'gmt-dcw: digital chart of the world polygon map'

  make -C build DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/share/doc "${srcdir}"/
}

package_gmt-doc() {
  install -d "${pkgdir}"/usr/share/
  mv doc "${pkgdir}"/usr/share/
}
