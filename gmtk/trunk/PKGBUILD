# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=gmtk
pkgver=1.0.9
pkgrel=6
pkgdesc='Common functions for gnome-mplayer and gecko-mediaplayer'
arch=('x86_64')
url='https://sites.google.com/site/kdekorte2/gmtk'
license=('GPL')
depends=('gtk3' 'mplayer')
makedepends=('intltool')
source=("https://github.com/kdekorte/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('fc23c853deb35ee35db06dd3da5069ce3dc64faf3a0053324616fadf81a415db')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
