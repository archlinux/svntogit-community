# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=gmtk
pkgver=1.0.9
pkgrel=3
pkgdesc='Common functions for gnome-mplayer and gecko-mediaplayer.'
arch=('x86_64')
url='https://sites.google.com/site/kdekorte2/gmtk'
license=('GPL')
depends=('glib2' 'gtk3' 'dconf')
makedepends=('intltool')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kdekorte/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('2c893a371e3debeadf0f37a42040f5b4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags gtk+-3.0)"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-gtk3
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
