# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.31
pkgrel=6
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb-compat' 'libxpm' 'bluez-libs' 'libical' 'sqlite')
makedepends=('gtk2' 'libmariadbclient' 'postgresql-libs' 'intltool')
optdepends=('dialog: sendsms tool'
            'gtk2: xgnokii GUI'
            'libmariadbclient: smsd mysql backend'
            'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
install=$pkgname.install
source=("http://www.gnokii.org/download/$pkgname/$pkgname-$pkgver.tar.bz2"
        'gnokii-config.patch'
        'gnokii-lock.patch')
md5sums=('d9627f4a1152d3ea7806df4532850d5f'
         'b2961b52ac1f770c4704ccc50b64fde2'
         '17b629db85a6bb2b98ca59941aa95295')

prepare() {
  cd $pkgname-$pkgver

  # Set bindir location
  patch -Np1 -i "$srcdir/gnokii-config.patch"

  # Set lock path
  patch -Np1 -i "$srcdir/gnokii-lock.patch"
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin \
              --disable-static --enable-security
  make
  pushd xgnokii
  make
  popd
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  pushd xgnokii
  make DESTDIR="$pkgdir" install
  popd

  # Install inital config file
  install -Dm644 Docs/sample/gnokiirc "$pkgdir/etc/gnokiirc"

  # Fix file permission
  chmod 755 "$pkgdir/usr/bin/sendsms"
}
