# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.26.0
pkgrel=3
pkgdesc="Small applications for the GNOME Panel"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeApplets"
license=('GPL')
depends=('gnome-panel' 'libgtop' 'tracker' 'cpupower' 'upower')
makedepends=('intltool' 'itstool' 'gnome-settings-daemon')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        0001-fix-build-with-kernel-4.7.patch)
sha256sums=('1a7a9627fa150c3efa512004d08c1eac2be59f2d9c3d6392430179c286c225ce'
            '740ce6bf12d3b4990d670eb04a24ce6bcf7b12f09e6097c4235a95222cdd67e5')

prepare() {
  cd $pkgname-$pkgver
  # Fix cpufreq applet with recent kernel
  patch -Np1 -i ../0001-fix-build-with-kernel-4.7.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --with-cpufreq-lib=cpupower --disable-maintainer-mode

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
