# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.32.1
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Boxes"
license=('LGPL')
groups=('gnome')
depends=('cdrtools' 'gtk-vnc' 'libarchive' 'libgovirt' 'libgudev' 'libosinfo' 'libsecret'
         'libvirt-glib' 'mtools' 'qemu' 'spice-gtk3' 'tracker' 'webkit2gtk' 'freerdp')
makedepends=('git' 'gobject-introspection' 'yelp-tools' 'meson' 'spice-protocol' 'vala')
_commit=5b130ef7a331074ebe8ed8dd575f19c5d7094d96  # tags/v3.32.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gtk-frdp.git"
        "git+https://gitlab.gnome.org/felipeborges/libovf-glib.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git submodule init
  git config --local submodule.subprojects/gtk-frdp.url "$srcdir/gtk-frdp"
  git config --local submodule.subprojects/libovf-glib.url "$srcdir/libovf-glib"
  git submodule update
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
