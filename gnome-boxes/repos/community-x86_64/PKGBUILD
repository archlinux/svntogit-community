# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=42.1
pkgrel=1
pkgdesc='Simple GNOME application to access virtual systems'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Boxes'
license=('LGPL')
groups=('gnome')
depends=('cdrtools' 'edk2-ovmf' 'gtksourceview4' 'gtk-vnc' 'libarchive' 'libgudev' 'libhandy'
         'libosinfo' 'libsecret' 'libvirt-glib' 'mtools' 'qemu-desktop' 'spice-gtk' 'tracker3'
         'webkit2gtk')
makedepends=('appstream-glib' 'gobject-introspection' 'itstool' 'meson' 'spice-protocol' 'vala')
options=('debug')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('96f5d011b7508db561bf0d166c0ea9e8b121805c40e5d9f36119245c15172080')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
