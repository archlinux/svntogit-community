# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-break-timer
pkgver=2.0.3
pkgrel=2
pkgdesc="Keeps track of how much you are using the computer, and it reminds you to take regular breaks"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/BreakTimer"
license=('GPL3')
depends=('gsound' 'gtk3' 'libnotify')
makedepends=('meson' 'git' 'vala')
_commit=65e7d6c536b9f1ed4c2ee21839f4ea8633ff3c50  # tags/2.0.3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-break-timer.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
