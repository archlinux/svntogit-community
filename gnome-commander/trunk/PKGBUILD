# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=gnome-commander
pkgver=1.2.8.6
pkgrel=1
pkgdesc="A graphical two-pane filemanager for Gnome"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/gcmd/"
license=('GPL')
depends=('libgnomeui' 'gnome-vfs' 'gconf' 'python' 'libsm')
makedepends=('perlxml' 'gnome-doc-utils>=0.11.2')
options=(!libtool)
install=gnome-commander.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/gnome-commander/1.2/$pkgname-$pkgver.tar.bz2)
sha256sums=('6643bc9364b4e3a3cb5d14f4d98fe86212f0a5b69bf7b37815149ac2f285acc7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc \
    --localstatedir=/var --disable-scrollkeeper --enable-python

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  mkdir -p "${pkgdir}"/usr/share/gconf/schemas
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/${pkgname}.schemas \
    --domain ${pkgname} "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
