# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=gnome-commander
pkgver=1.4.3
pkgrel=1
pkgdesc='Graphical two-pane filemanager for Gnome'
arch=('i686' 'x86_64')
url='http://gcmd.github.io/'
license=('GPL')
depends=('libgnomeui' 'gnome-vfs' 'gconf' 'python2' 'libsm' 'libunique')
makedepends=('perl-xml-parser' 'gnome-doc-utils' 'intltool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(ftp://ftp.gnome.org/pub/GNOME/sources/gnome-commander/1.4/$pkgname-$pkgver.tar.xz)
sha256sums=('8f47ac8428086406536c45b53cbf23b734322f2a557d91698cdeb2bd296f3029')

build() {
  cd "$pkgname-$pkgver"

  # Python 2 fix
  for f in doc/*/gnome-commander.xml; do
      sed -i 's:env python:env python2:' "$f"
  done

  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper --enable-python
  make
}

package() {
  cd "$pkgname-$pkgver"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" \
    --domain "$pkgname" "$pkgdir/etc/gconf/schemas/"*.schemas
  rm -rf "$pkgdir/etc/gconf/schemas/"
}

# vim:set ts=2 sw=2 et:
sha256sums=('159893bd94d2b8190026f97c1f4bee2b6dd089e482636f9454fee9ec95c7fcb7')
