# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do-plugins
pkgver=0.8.5
pkgrel=1
pkgdesc="Plugins for gnome-do"
arch=('any')
url="https://launchpad.net/do-plugins"
license=('GPL3')
depends=('gnome-do>=0.9' 'flickrnet' 'gdata-sharp' 'wnck-sharp')
makedepends=('intltool' 'libgnome-sharp')
source=(http://launchpad.net/do-plugins/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        flickrnet-3.2.patch)
md5sums=('022d184e884cabd3078716385f123c0a'
         '8222b36baf39657d8f4e4c214f124093')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Port to FlickrNet 3.2
  patch -Np1 -i "$srcdir/flickrnet-3.2.patch"

  # Disable some plugins
  sed -i '/AptURL/d
          /DiskMounter/d
          /Transmission/d' Makefile.{am,in}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-release \
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
