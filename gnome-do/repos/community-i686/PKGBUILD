# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do
pkgver=0.95.2
pkgrel=1
pkgdesc="Quickly search for many items present on your desktop or the web, and perform useful actions on those items"
arch=('i686' 'x86_64')
url="http://do.cooperteam.net/"
license=('GPL3')
depends=('gconf-sharp' 'gnome-desktop2' 'gnome-keyring-sharp' 'mono-addins' 'notify-sharp' 'gkeyfile-sharp')
makedepends=('intltool' 'gio-sharp')
install=$pkgname.install
source=(http://launchpad.net/do/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        fix-build.patch
        revert-threading-fix.patch)
md5sums=('a0631f39ce778fd679eca6a56e1b9f54'
         '4ac79a8270bc6c1539f56b8f56043894'
         'c8e19008369144fda6a0a6a06f6ac3dd')

prepare() {
  cd $pkgname-$pkgver

  # Fix build
  patch -Np1 -i ../fix-build.patch

  # Revert threading fix to avoid freeze on start
  patch -Np1 -i ../revert-threading-fix.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
