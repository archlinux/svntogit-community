# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Doehni <dohny@gmx.de>

pkgname=gnome-do
pkgver=0.95.1
pkgrel=1
pkgdesc="Quickly search for many items present on your desktop or the web, and perform useful actions on those items"
arch=('i686' 'x86_64')
url="https://launchpad.net/do"
license=('GPL3')
depends=('gconf-sharp' 'gnome-desktop2' 'gnome-keyring-sharp' 'mono-addins' 'notify-sharp' 'gkeyfile-sharp' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gio-sharp')
install=$pkgname.install
source=(http://launchpad.net/do/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('1f61376718d75d3defcdf81958754296')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
