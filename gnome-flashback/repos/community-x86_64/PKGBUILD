# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.36.3
pkgrel=2
pkgdesc="GNOME Flashback session"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
license=('GPL')
depends=('gnome-bluetooth' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'gnome-themes-extra' 'libibus' 'metacity')
makedepends=('autoconf-archive' 'git' 'intltool' 'python')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'network-manager-applet: Network management')
_commit=5036ec999d9f9ad8772e863e4cee2b679beb569f  # tags/3.36.3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-flashback.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        gnome-flashback.pam)
sha256sums=('SKIP'
            'SKIP'
            '7148b3b1773fa312d6cd14798338cc70cffb6df7504ff1787687edd0363138d2')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git submodule init
  git submodule set-url gvc/gvc "$srcdir/libgnome-volume-control"
  git submodule update

  # Remove unneeded module
  sed -i '/status_notifier_watcher/d' gnome-flashback/gf-application.c
  sed -i '/libstatus-notifier-watcher/d' gnome-flashback/Makefile.am

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --enable-compile-warnings=minimum
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # Install pam file for the screensaver
  install -Dm644 ../gnome-flashback.pam "$pkgdir/etc/pam.d/gnome-flashback"
}
