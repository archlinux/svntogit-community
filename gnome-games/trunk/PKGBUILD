# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tom Richards <tom@tomrichards.net>

pkgname=gnome-games
pkgver=3.26.1.1
pkgrel=1
pkgdesc='Simple game launcher for GNOME'
arch=('x86_64')
license=('GPL3')
url='https://wiki.gnome.org/Apps/Games'
depends=('grilo' 'libevdev' 'retro-gtk' 'tracker')
makedepends=('intltool' 'vala')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('d28415000c92edee86f9a5cd97759a81c3a4a243f37addaed8c3e227e0b478e4')

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
