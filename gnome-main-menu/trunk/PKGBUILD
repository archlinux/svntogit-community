# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=gnome-main-menu
pkgver=1.7.0
pkgrel=1
pkgdesc="A mate-panel applet similar to the traditional main-menu, but with a few additions."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja' 'libunique' 'mate-control-center' 'mate-desktop' 'mate-panel')
makedepends=('mate-common' 'yelp-tools')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('ac6ce5dcd9433d798d9de3c311127a20084fc00c')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-caja-extension \
        --without-html-dir \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
