# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=gnome-media
pkgname=$_pkgbasename-pulse
pkgver=2.30.0
pkgrel=3
pkgdesc="GNOME Media Tools, with PulseAudio support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-good-plugins>=0.10.18' 'gstreamer0.10-base-plugins>=0.10.26' 'libcanberra-pulse>=0.23' 'hicolor-icon-theme' 'pulseaudio')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.0' 'libunique>=1.1.6')
conflicts=("$_pkgbasename")
provides=("$_pkgbasename=$pkgver-2")
options=('!libtool' '!emptydirs')
install=gnome-media.install
url="http://www.gnome.org"
groups=(pulseaudio-gnome)
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.30/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('994cd9b88ff22e4815585629b14efc828a95284900809b0b4f8ebcde0c6e2926')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-media \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --enable-pulseaudio
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -Dm644 \
    gst-mixer/icons/16x16/status/audio-input-microphone-muted.png \
    "$pkgdir/usr/share/icons/hicolor/16x16/status/audio-input-microphone-muted.png"

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgbasename}.schemas" --domain gnome-media-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
