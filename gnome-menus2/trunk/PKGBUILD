# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus2
_pkgname=gnome-menus
pkgver=3.0.1
pkgrel=2
pkgdesc="Library for the Desktop Menu Specification (legacy version)"
arch=('i686' 'x86_64')
depends=('glib2' 'python2')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
license=('LGPL')
url="http://www.gnome.org/"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2)
sha256sums=('579c119c26f37781f66708e867ea45b3c37589b3b69e5b32d33e9bdb944165f0')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=/usr/bin/python2
  make
}

package(){
  cd "$srcdir/$_pkgname-$pkgver"

  make -C libmenu DESTDIR="$pkgdir" install
  make -C python DESTDIR="$pkgdir" install
}
