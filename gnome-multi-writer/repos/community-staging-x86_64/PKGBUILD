# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-multi-writer
pkgver=3.32.1
pkgrel=2
pkgdesc="Write an ISO file to multiple USB devices at once"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/MultiWriter"
license=('GPL')
groups=('gnome-extra')
depends=('gtk3' 'libcanberra' 'libgusb' 'udisks2')
makedepends=('appstream-glib' 'docbook-sgml' 'docbook-utils' 'intltool' 'meson' 'perl-sgmls')
optdepends=('gnome-icon-theme-extras: show device icons')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('bcd56cd0716a727b85e3a2dedef8e747d4567952e68c29907bea8b103d6aedaa')

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
