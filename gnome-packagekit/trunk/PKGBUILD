# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

pkgname=gnome-packagekit
pkgver=3.12.0
pkgrel=1
pkgdesc='Collection of graphical tools for PackageKit to be used in the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'hicolor-icon-theme' 'libnotify'
         'packagekit')
makedepends=('gconf' 'gtk-doc' 'intltool' 'itstool' 'libcanberra' 'upower')
optdepends=('gnome-settings-daemon-updates: update and message notifications')
options=('!emptydirs')
install="$pkgname.install"
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        "packagekit-0.8.patch")
sha256sums=('84cc4641bb174c833a2b01e2f3f5cbe24d9020ab9c2f245781be201a975a5943'
            '717ba4d281adbafc9409202538ad1c8ce9a2bb41a6a0bc127e8dd5c4c1da1d65')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -RNp1 -i "$srcdir/packagekit-0.8.patch"

  _mime='application/x-xz-compressed-tar;application/x-servicepack;'
  sed -i "s@MimeType=.*@MimeType=$_mime@" data/gpk-install-local-file.desktop.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --disable-gtk-doc \
	      --disable-schemas-compile \
	      --disable-scrollkeeper
  make -s
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -s DESTDIR="$pkgdir" install
}
