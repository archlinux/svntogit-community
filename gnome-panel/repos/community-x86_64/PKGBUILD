# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=3.42.0
pkgrel=1
pkgdesc='Panel of GNOME Flashback'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/GnomePanel'
license=('GPL')
depends=('evolution-data-server' 'gnome-desktop' 'gnome-menus' 'libgdm' 'libwnck3' 'nautilus'
         'polkit')
makedepends=('itstool')
optdepends=('alacarte: Main menu editor'
            'gnome-applets: Extra applets for the panel')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('aea8c0efc2edba20e97ad4695179cd0a2538e64c2904702cc8c594e8e0898703')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile --enable-compile-warnings=minimum 
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
