# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=3.26.0
pkgrel=2
pkgdesc="Panel of GNOME Flashback"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GnomePanel"
license=('GPL')
depends=('evolution-data-server' 'gnome-desktop' 'gnome-menus' 'libgdm' 'libwnck3' 'nautilus' 'polkit')
makedepends=('intltool' 'itstool' 'python')
optdepends=('alacarte: Main menu editor'
            'gnome-applets: Extra applets for the panel')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('ada88f8c7c55ab5f8b1879efe9276c45abedf5db9c317c1a3616877f6ed76271')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
