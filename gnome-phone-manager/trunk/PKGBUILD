# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-phone-manager
pkgver=0.69
pkgrel=3
pkgdesc="Control your mobile phone from your GNOME desktop"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/PhoneManager"
license=('GPL')
depends=('evolution-data-server' 'gconf' 'gnokii' 'gnome-bluetooth' 'libcanberra' 'telepathy-glib')
makedepends=('intltool')
install=gnome-phone-manager.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        gnome-phone-manager-0.68-eds.patch)
sha256sums=('35e038ea3afaacdf451046e87af876096cf1520efc04fc3f5b63ea22e0297175'
            '232a72108836ddd46d329993d166c38550e2923f161b4dc814d75af4ed5ccc79')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Port to evolution-data-server 3.8
  patch -p1 -i ../gnome-phone-manager-0.68-eds.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
