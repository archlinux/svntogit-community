# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-pie
_pkgname=Gnome-Pie
pkgver=0.7.3
pkgrel=1
pkgdesc="Circular application launcher for GNOME"
arch=('x86_64')
url="https://simmesimme.github.io/gnome-pie.html"
license=('MIT')
depends=('libarchive' 'libgee' 'libwnck3' 'gnome-menus')
makedepends=('cmake' 'vala')
source=($_pkgname-$pkgver.tar.gz::https://github.com/Simmesimme/$_pkgname/archive/v$pkgver.tar.gz)
sha256sums=('e5d35462d72a1418555deafd3219f62e68acb28b763a55929c89be992f9c0a85')

build() {
  cd $_pkgname-$pkgver
  ./resources/locale/compile-po.sh
  [[ -d build ]] || mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd $_pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
