# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

_pkgname=gnome-settings-daemon
pkgname=$_pkgname-updates
pkgver=3.2.0
pkgrel=2
pkgdesc="Updates plugin for the GNOME Settings daemon"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('gnome-packagekit' "$_pkgname")
makedepends=('intltool' 'gtk-doc' 'gnome-desktop')
options=('!emptydirs' '!libtool')
install=$_pkgname.install
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz"
        'arch.patch')
sha256sums=('a85242ce60b9c9bb3fbad124ee06430a408f39fa31d2d4ea29dc179db724447b'
            'e5029915c423f72256c4170f917363c385b6fcd180b87ceba0d7bb11a841d7e9')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i "$srcdir/arch.patch"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --localstatedir=/var \
	      --libexecdir=/usr/lib/gnome-settings-daemon \
	      --disable-static \
	      --enable-pulse \
	      --with-pnpids=/usr/share/libgnome-desktop-3.0/pnp.ids
  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -s
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C 'plugins/updates' DESTDIR="${pkgdir}" install

  _schema='org.gnome.settings-daemon.plugins.updates.gschema.xml'
  install -D -m644 "data/$_schema" "$pkgdir/usr/share/glib-2.0/schemas/$_schema"
}
