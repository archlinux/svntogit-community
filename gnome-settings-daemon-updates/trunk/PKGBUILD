# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

_pkgname=gnome-settings-daemon
pkgname=$_pkgname-updates
pkgver=3.8.2
pkgrel=1
pkgdesc="Updates plugin for the GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-packagekit' "$_pkgname")
makedepends=('intltool' 'gtk-doc' 'gnome-desktop' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs' '!libtool')
install=$_pkgname.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz"
	'arch.patch')
sha256sums=('e2118024549b1a3b24e338403d3451211b8cde5c68e387d9a0081e137191b0be'
	    '32f18a02698bb24aa8ae293eb7bf74a8261e3ce2c5149d259a87bd14500089ea')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  patch -Np1 -i "$srcdir/arch.patch"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --libexecdir=/usr/lib/gnome-settings-daemon \
              --disable-static

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -s
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C 'plugins/updates' DESTDIR="${pkgdir}" install
}
