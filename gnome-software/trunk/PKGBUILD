# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgname=gnome-software
pkgver=3.22.3
pkgrel=1
pkgdesc="GNOME Software Tools"
arch=(i686 x86_64)
license=(GPL2)
url="https://wiki.gnome.org/Apps/Software/"
depends=(gtk3 appstream-glib libsoup gsettings-desktop-schemas gnome-desktop flatpak
         packagekit archlinux-appstream-data gtkspell3 libgudev)
makedepends=(intltool python docbook-xsl gnome-common git)
_commit=72f13d3d46f9a5befaecb84abe8998d5eeb72eb6  # tags/GNOME_SOFTWARE_3_22_3^0
source=("git+https://git.gnome.org/browse/gnome-software#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_SOFTWARE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --disable-schemas-compile --enable-packagekit \
    --enable-flatpak --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
