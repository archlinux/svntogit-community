# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=gnome-twitch
pkgname=('gnome-twitch'
         'gnome-twitch-player-backend-gstreamer-cairo'
         'gnome-twitch-player-backend-gstreamer-clutter'
         'gnome-twitch-player-backend-gstreamer-opengl'
         'gnome-twitch-player-backend-mpv-opengl')
pkgver=0.4.2
pkgrel=3
pkgdesc='Enjoy Twitch on your GNU/Linux desktop.'
arch=('x86_64')
url='https://github.com/vinszent/gnome-twitch'
license=('GPL3')
depends=('gtk3' 'libsoup' 'json-glib' 'webkit2gtk' 'libpeas' 'gobject-introspection')
makedepends=('meson' 'gstreamer' 'gst-libav' 'gst-plugins-base' 'gst-plugins-good' 'gst-plugins-bad' 'clutter-gst' 'clutter-gtk' 'mpv')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f13806a73b1a2ca5e84104b649f845f45410a01a1516cfe7bf2b28dfab12b99ebe4db8bee65c1658410512e2df57ec440308dc5a8d4047f805d230397ef7b92d')

build() {
  mkdir -p $pkgname-$pkgver/{build,gs-cairo,gs-clutter,gs-opengl,mpv-opengl}

  # Build app
  echo 'Build the main app...'
  cd "$srcdir"/$pkgbase-$pkgver/build
  arch-meson ..
  ninja

  # Build GStreamer Cairo backend
  echo 'Building the GStreamer Cairo backend...'
  cd "$srcdir"/$pkgbase-$pkgver/gs-cairo
  arch-meson .. \
  	-Dbuild-executable=false \
  	-Dbuild-player-backends=gstreamer-cairo
  ninja

  # Build GStreamer Clutter backend
  echo 'Building the GStreamer Clutter backend...'
  cd "$srcdir"/$pkgbase-$pkgver/gs-clutter
  arch-meson .. \
  	-Dbuild-executable=false \
  	-Dbuild-player-backends=gstreamer-clutter
  ninja

  # Build GStreamer OpenGL backend
  echo 'Building the GStreamer OpenGL backend...'
  cd "$srcdir"/$pkgbase-$pkgver/gs-opengl
  arch-meson .. \
  	-Dbuild-executable=false \
  	-Dbuild-player-backends=gstreamer-opengl
  ninja

  # Build MPV OpenGL backend
  echo 'Building the MPV OpenGL backend...'
  cd "$srcdir"/$pkgbase-$pkgver/mpv-opengl
  arch-meson .. \
  	-Dbuild-executable=false \
  	-Dbuild-player-backends=mpv-opengl
  ninja
}

package_gnome-twitch() {
  depends=('gnome-twitch-player-backend')
  optdepends=('gnome-twitch-player-backend-gstreamer-cairo: GStreamer Cairo backend'
              'gnome-twitch-player-backend-gstreamer-clutter: GStreamer Clutter backend'
              'gnome-twitch-player-backend-gstreamer-opengl: GStreamer OpenGL backend'
              'gnome-twitch-player-backend-mpv-opengl: MPV OpenGL')

  cd $pkgbase-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}

package_gnome-twitch-player-backend-gstreamer-cairo() {
  depends=('gnome-twitch' 'gstreamer' 'gst-libav' 'gst-plugins-base' 'gst-plugins-good' 'gst-plugins-bad')
  provides=('gnome-twitch-player-backend')

  cd $pkgbase-$pkgver/gs-cairo

  DESTDIR="$pkgdir" ninja install
}

package_gnome-twitch-player-backend-gstreamer-clutter() {
  depends=('gnome-twitch' 'gstreamer' 'gst-libav' 'gst-plugins-base' 'gst-plugins-good' 'gst-plugins-bad' 'clutter-gst' 'clutter-gtk')
  provides=('gnome-twitch-player-backend')

  cd $pkgbase-$pkgver/gs-clutter

  DESTDIR="$pkgdir" ninja install
}

package_gnome-twitch-player-backend-gstreamer-opengl() {
  depends=('gnome-twitch' 'gstreamer' 'gst-libav' 'gst-plugins-base' 'gst-plugins-good' 'gst-plugins-bad')
  provides=('gnome-twitch-player-backend')

  cd $pkgbase-$pkgver/gs-opengl

  DESTDIR="$pkgdir" ninja install
}

package_gnome-twitch-player-backend-mpv-opengl() {
  depends=('gnome-twitch' 'mpv')
  provides=('gnome-twitch-player-backend')

  cd $pkgbase-$pkgver/mpv-opengl

  DESTDIR="$pkgdir" ninja install
}

