# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>
pkgname=gnote
pkgver=0.7.2
pkgrel=3
pkgdesc="Experimental port of Tomboy to C++ "
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gnote"
license=('GPL')
depends=('boost-libs>=1.43' 'gtkspell' 'libpanelappletmm' 'libxslt')
makedepends=('intltool' 'gnome-doc-utils' 'boost')
options=('!libtool')
install=gnote.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/0.7/$pkgname-$pkgver.tar.bz2
        gtk2.20.patch)
sha256sums=('ef46a1bade7bf684615b32e5bf35c100d646456e40ac2e9cab871e6dff0d2a4c'
            '0c1508799795d382be9e761c06d1b6519e7c4506295240323facddd262511a63')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/gtk2.20.patch" || return 1
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/gnote \
        --disable-scrollkeeper \
        --disable-schemas-install \
        --disable-static || return 1
    make || return 1
    make DESTDIR="${pkgdir}" install || return 1
    install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
    gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnote ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
    rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
