# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>
pkgname=gnote
pkgver=3.6.2
pkgrel=1
pkgdesc="A note taking application."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gnote"
license=('GPL3')
depends=('gtkmm3' 'libxslt' 'hicolor-icon-theme' 'desktop-file-utils' 'dconf' 'libsecret')
makedepends=('intltool' 'itstool' 'boost')
options=('!libtool')
install=gnote.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('38f3b069874658f281445e04b8376d946580e4a5e39620f7e30fdd8216d46c1e')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/gnote \
        --disable-scrollkeeper \
        --disable-schemas-compile \
        --disable-static
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
