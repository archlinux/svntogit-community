# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=gnu-apl
pkgver=1.7
pkgrel=5
url="http://www.gnu.org/software/apl/"
pkgdesc="An (almost) complete implementation of ISO standard 13751"
arch=('x86_64')
depends=('ncurses' 'postgresql-libs' 'gcc-libs' 'sqlite' 'libnsl')
license=('GPL3')
source=("https://ftpmirror.gnu.org/apl/apl-${pkgver}.tar.gz"{,.sig})
md5sums=('a06456f4aa9e349700da51c8efd14150'
         'SKIP')
validpgpkeys=('3EB2E2ECCB2F040A26CB7F2192B68B71531B6686')

prepare() {
    cd "$srcdir/apl-$pkgver"
    # The default color scheme is black-on-black in some terminals
    sed -i '64 s/^.*Color.*ANSI/#&/' gnu-apl.d/preferences{,.in}
    sed -i '65 s/^#.*Color.*CURSES/Color\tCURSES/' gnu-apl.d/preferences{,.in}

    # SVN 908 (remove in 1.8)
    sed -i '888 s/ || / || nc == /g' src/Symbol.cc
}

build() {
    cd "$srcdir/apl-$pkgver"
    CFLAGS='-std=gnu99' ./configure --prefix=/usr --sysconfdir=/etc
    make PREFIX=/usr -j1
}

package() {
    cd "$srcdir/apl-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir" install
}

