# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: wahnby <wahnby@yahoo.fr>

pkgname=gnunet
pkgver=0.9.5_a
pkgrel=1
pkgdesc="A framework for secure peer-to-peer networking"
arch=('i686' 'x86_64')
url="http://gnunet.org"
license=('GPL')
depends=('gmp' 'libgcrypt' 'libextractor' 'sqlite' 'curl'
	 'libmicrohttpd' 'libunistring' 'libidn')
makedepends=('gettext' 'pkgconfig')
backup=(etc/gnunetd.conf)
options=('!libtool' '!makeflags')
install=gnunet.install
source=(ftp://ftp.gnu.org/gnu/gnunet/gnunet-${pkgver/_/}.tar.gz
	gnunet.rc
	gnunet.service
	gnunet.conf.d
	defaults.conf)
md5sums=('b0e204cf9711f9799baaeb477992e66a'
         '5baf691913b007fb33e365239e3f7217'
         '54cce3d2415d95b2e5bd1bd88db3a0ea'
         'f161b46915736e4017e77ad735521453'
         '0fe23b2ca5b3fc47a0b5645e04406da0')

build() {
  cd $srcdir/gnunet-${pkgver/_/}
  sed -i 's|contrib doc|doc|' Makefile.*
  [ -f Makefile ] || ./configure --prefix=/usr --without-mysql
  make
}

package() {
  cd $srcdir/gnunet-${pkgver/_/}
  make DESTDIR=$pkgdir install
  install -D -m0755 $srcdir/gnunet.rc $pkgdir/etc/rc.d/gnunetd
  install -D -m0644 $srcdir/gnunet.conf.d $pkgdir/etc/conf.d/gnunetd
  install -D -m0644 $srcdir/defaults.conf $pkgdir/etc/gnunetd.conf
  rm -rf $pkgdir/usr/include/libltdl $pkgdir/usr/lib/libltdl.* $pkgdir/usr/include/ltdl.h
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
