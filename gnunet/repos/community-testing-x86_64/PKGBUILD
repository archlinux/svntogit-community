# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: wahnby <wahnby@yahoo.fr>

pkgname=gnunet
pkgver=0.10.1
pkgrel=12
pkgdesc="A framework for secure peer-to-peer networking"
arch=('x86_64')
url="http://gnunet.org"
license=('GPL')
depends=('gmp' 'libgcrypt' 'libextractor' 'sqlite' 'gnurl'
	 'libmicrohttpd' 'libunistring' 'libidn')
makedepends=('gettext' 'pkgconfig'
	     'bluez-libs' 'python' 'glpk' 'libpulse' 'opus')
optdepends=('bluez-libs'
	    'python'
	    'glpk'
	    'libpulse'
	    'opus')
backup=(etc/gnunetd.conf)
options=('!makeflags')
# TODO: key revoked, check for new signature on new release
# source=(https://ftp.gnu.org/gnu/gnunet/gnunet-${pkgver/_/}.tar.gz{,.sig}
source=(https://ftp.gnu.org/gnu/gnunet/gnunet-${pkgver/_/}.tar.gz
        gnunet.service
        defaults.conf)
validpgpkeys=('19647543F7861D3BF4E64FF7BF60708B48426C7E')
md5sums=('b1171f038473f8eb7fe86db138624ff7'
         '54cce3d2415d95b2e5bd1bd88db3a0ea'
         '0fe23b2ca5b3fc47a0b5645e04406da0')

build() {
  cd "$srcdir"/gnunet-${pkgver/_/}
  sed -i 's|contrib doc|doc|' Makefile.*
  [ -f Makefile ] || ./configure --prefix=/usr \
    --without-mysql \
    --with-nssdir=/usr/lib
  make
  make -C contrib
}

package() {
  cd "$srcdir"/gnunet-${pkgver/_/}
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" -C contrib install
  install -D -m0644 "$srcdir"/defaults.conf "$pkgdir"/etc/gnunetd.conf
  rm -rf "$pkgdir"/usr/include/libltdl "$pkgdir"/usr/lib/libltdl.* "$pkgdir"/usr/include/ltdl.h
  install -Dm0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
