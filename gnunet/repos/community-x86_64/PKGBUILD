# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: wahnby <wahnby@yahoo.fr>

pkgname=gnunet
pkgver=0.8.1b
pkgrel=1
pkgdesc="A framework for secure peer-to-peer networking"
arch=('i686' 'x86_64')
url="http://gnunet.org"
license=('GPL')
depends=('gmp' 'libgcrypt' 'libextractor' 'sqlite3' 'guile' 'curl' 'libglade' 'adns')
makedepends=('gettext' 'pkgconfig')
source=(ftp://ftp.gnu.org/gnu/gnunet/GNUnet-$pkgver.tar.gz
	gnunet.rc
	gnunet.conf.d)
backup=(etc/gnunetd.conf)
options=('!libtool' '!makeflags' 'force')
md5sums=('f1b625aa52389e22258bad9fb6bd7f32'
         '0d62ab7f2a28af3ac082015696ee6ef3'
         'f161b46915736e4017e77ad735521453')

build() {
  cd $srcdir/GNUnet-$pkgver

  ./configure --prefix=/usr --without-mysql

  make || return 1
  make DESTDIR=$pkgdir install || return 1

  install -D -m0755 $srcdir/gnunet.rc $pkgdir/etc/rc.d/gnunetd && \
  install -D -m0644 $srcdir/gnunet.conf.d $pkgdir/etc/conf.d/gnunetd && \
  install -D -m0644 $srcdir/GNUnet-$pkgver/contrib/gnunetd.conf $pkgdir/etc/gnunetd.conf || return 1

  rm -rf $pkgdir/usr/include/libltdl $pkgdir/usr/lib/libltdl.* $pkgdir/usr/include/ltdl.h
}

