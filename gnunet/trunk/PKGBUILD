# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: wahnby <wahnby@yahoo.fr>

pkgname=gnunet
pkgver=0.10.0
pkgrel=2
pkgdesc="A framework for secure peer-to-peer networking"
arch=('i686' 'x86_64')
url="http://gnunet.org"
license=('GPL')
depends=('gmp' 'libgcrypt' 'libextractor' 'sqlite' 'gnurl'
	 'libmicrohttpd' 'libunistring' 'libidn')
makedepends=('gettext' 'pkgconfig'
	     'bluez-libs' 'python' 'glpk' 'libpulse')
optdepends=('bluez-libs'
	    'python'
	    'glpk'
	    'libpulse')
backup=(etc/gnunetd.conf)
options=('!makeflags')
install=gnunet.install
source=(ftp://ftp.gnu.org/gnu/gnunet/gnunet-${pkgver/_/}.tar.gz
	gnunet.service
	defaults.conf)
md5sums=('8bff8280c90780b17786fd8b95f12f72'
         '54cce3d2415d95b2e5bd1bd88db3a0ea'
         '0fe23b2ca5b3fc47a0b5645e04406da0')

build() {
  cd $srcdir/gnunet-${pkgver/_/}
  sed -i 's|contrib doc|doc|' Makefile.*
  [ -f Makefile ] || ./configure --prefix=/usr --without-mysql
  make
}

package() {
  cd $srcdir/gnunet-${pkgver/_/}
  make DESTDIR=$pkgdir install
  install -D -m0644 $srcdir/defaults.conf $pkgdir/etc/gnunetd.conf
  rm -rf $pkgdir/usr/include/libltdl $pkgdir/usr/lib/libltdl.* $pkgdir/usr/include/ltdl.h
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
