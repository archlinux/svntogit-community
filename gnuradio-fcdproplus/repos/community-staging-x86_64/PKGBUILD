# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=gnuradio-fcdproplus
_pkgname=gr-fcdproplus
pkgver=3.7.5
pkgrel=11
_tag=fcdproplus-0.1.0
pkgdesc="GNU Radio source block for Funcube Dongle Pro +"
arch=('x86_64')
url="https://github.com/dl1ksv/gr-fcdproplus"
license=('GPL3')
depends=('gnuradio' 'libusb' 'alsa-lib' 'boost-libs')
makedepends=('git' 'cmake' 'boost' 'swig' 'systemd')
# systemd for FS#54735 (libudev.h)
replaces=('gr-fcdproplus')
source=("git://github.com/dl1ksv/gr-fcdproplus.git#tag=$_tag"
        "88-fcdproplus.rules")
md5sums=('SKIP'
         '465e12c454c6a22ebec9849181af7bdc')

prepare() {
  cd "$srcdir/$_pkgname"
}

build() {
  cd "$srcdir/$_pkgname"
  mkdir -p build
  cd build
  cmake \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$_pkgname/build/"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "$srcdir/88-fcdproplus.rules" "$pkgdir/etc/udev/rules.d/88-fcdproplus.rules"
}
