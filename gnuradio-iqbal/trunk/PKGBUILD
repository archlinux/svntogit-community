# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=gnuradio-iqbal
_pkgname=gr-iqbal
pkgver=0.38.2
_commit=fbee239a6fb36dd2fb564f6e6a0d393c4bc844db
pkgrel=9
pkgdesc="Gnuradio I/Q balancing"
arch=('x86_64')
url="https://cgit.osmocom.org/gr-iqbal/"
license=('GPL')
depends=('gnuradio' 'fftw' 'libunwind')
replaces=('gr-iqbal')
makedepends=('git' 'cmake' 'boost' 'spdlog')
source=("git+https://gitea.osmocom.org/sdr/gr-iqbal.git#commit=$_commit"
        "git+https://gitea.osmocom.org/sdr/libosmo-dsp.git")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $_pkgname

  git submodule init
  git submodule set-url libosmo-dsp "$srcdir/libosmo-dsp"
  git -c protocol.file.allow=always submodule update

  sed -i 's/this, _1/this, boost::placeholders::_1/' lib/fix_cc.cc
  sed -i 's/\t/        /' python/__init__.py
}

build() {
  local _pyver=$(python -c 'import sys; print(str(sys.version_info[0])+"."+str(sys.version_info[1]))')
  cmake -B build -S $_pkgname \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE=/usr/bin/python3 \
    -DPYTHON_INCLUDE_DIR="/usr/include/python${_pyver}" \
    -DPYTHON_LIBRARY="/usr/lib/libpython${_pyver}.so" \
    -DGR_PYTHON_DIR="/usr/lib/python${_pyver}/site-packages/" \
    -DCMAKE_LIBRARY_PATH=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
