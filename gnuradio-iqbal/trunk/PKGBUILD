# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=gnuradio-iqbal
_pkgname=gr-iqbal
pkgver=0.37.1
pkgrel=2
pkgdesc="gnuradio I/Q balancing"
arch=('i686' 'x86_64')
url="http://cgit.osmocom.org/gr-iqbal/"
license=('GPL')
depends=('gnuradio')
replaces=('gr-iqbal')
makedepends=('git' 'cmake' 'boost' 'swig')
source=("git://git.osmocom.org/gr-iqbal/#tag=v$pkgver"
        float-complex.patch)
md5sums=('SKIP'
         '6417f4c064b44e8875fbb783c3a2c308')

build() {
  cd "$srcdir/$_pkgname"
  git submodule init
  git submodule update
  # patch is already in git, only applies to 0.37.1
  patch -p1 -i "$srcdir/float-complex.patch"
  mkdir build
  cd build
  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python2*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython2.*.so) \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$_pkgname/build/"
  make DESTDIR="${pkgdir}" install
}
