# Maintainer: Kyle Keen <keenerd@gmail.com?>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=gnuradio-osmosdr
_pkgname=gr-osmosdr
_r=128
_commit=f3905d3510dfb3851f946f097a9e2ddaa5fb333b
pkgver=0.1.5.r$_r.${_commit:0:6}
pkgrel=4
pkgdesc='Source block for Funcube Dongle, RTL-SDR, USRP, OsmoSDR, BladeRF, HackRF and AirSpy devices'
url='https://sdr.osmocom.org/trac/'
arch=('x86_64')
license=('GPL')
depends=('gnuradio' 'gnuradio-iqbal' 'rtl-sdr' 'gnuradio-fcdproplus' 'hackrf' 'bladerf' 'airspy' 'soapysdr')
optdepends=('python-opengl: osmocom_fft')
makedepends=('git' 'cmake' 'boost' 'swig')
replaces=('gr-osmosdr')
#source=("git://git.osmocom.org/gr-osmosdr#commit=$_commit")
# use this random dude's repo, because where is osmocom?
source=("git://github.com/igorauad/gr-osmosdr/#commit=$_commit")
md5sums=('SKIP')

# add support for miri, osmo?

prepare() {
  cd $_pkgname

  #sed -i 's/python$/python2/' apps/osmocom_siggen_base.py
}

build() {
  cd $_pkgname

  mkdir -p build
  cd build

  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE="$(which python3)" \
    -DPYTHON_INCLUDE_DIR="$(echo /usr/include/python3*)" \
    -DPYTHON_LIBRARY="$(echo /usr/lib/libpython3.*.so)" \
    -DBoost_NO_BOOST_CMAKE=ON \
    -DCMAKE_INSTALL_PREFIX=/usr ../

  make
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
