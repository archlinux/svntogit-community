# Maintainer: Kyle Keen <keenerd@gmail.com?>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=gnuradio-osmosdr
_reponame=gr-osmosdr
pkgver=0.2.0
pkgrel=5
pkgdesc='Source block for Funcube Dongle, RTL-SDR, USRP, OsmoSDR, BladeRF, HackRF and AirSpy devices'
url='http://osmocom.org/projects/gr-osmosdr'
arch=('x86_64')
license=('GPL')
depends=('gnuradio' 'libvolk' 'gnuradio-iqbal' 'rtl-sdr' 'gnuradio-fcdproplus' 'hackrf' 'bladerf' 'airspy' 'soapysdr')
optdepends=('python-opengl: osmocom_fft')
makedepends=('git' 'cmake' 'boost' 'swig')
replaces=('gr-osmosdr')
source=("git://git.osmocom.org/gr-osmosdr/#tag=v$pkgver"
        "gnuradio-osmosdr.pc")
md5sums=('SKIP'
         'a62fdf732f8a9a59f667bbda5542a824')

# add support for miri, osmo?
# figure out what happened to libtool

build() {
  cd $_reponame

  mkdir -p build
  cd build

  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE="$(which python3)" \
    -DPYTHON_INCLUDE_DIR="$(echo /usr/include/python3*)" \
    -DPYTHON_LIBRARY="$(echo /usr/lib/libpython3.*.so)" \
    -DBoost_NO_BOOST_CMAKE=ON \
    -DCMAKE_INSTALL_PREFIX=/usr ../

  make
}

package() {
  cd $_reponame/build

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/gnuradio-osmosdr.pc" "$pkgdir/usr/lib/pkgconfig/gnuradio-osmosdr.pc"
}
