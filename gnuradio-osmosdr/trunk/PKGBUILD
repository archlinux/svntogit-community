# Maintainer: Kyle Keen <keenerd@gmail.com?>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=gnuradio-osmosdr
_pkgname=gr-osmosdr
pkgver=0.2.3
_commit=a100eb024c0210b95e4738b6efd836d48225bd03
pkgrel=14
pkgdesc='Source block for Funcube Dongle, RTL-SDR, USRP, OsmoSDR, BladeRF, HackRF and AirSpy devices'
url='https://osmocom.org/projects/gr-osmosdr'
arch=('x86_64')
license=('GPL')
depends=('gnuradio' 'libvolk' 'gnuradio-iqbal' 'rtl-sdr' 'hackrf' 'bladerf' 'airspy' 'soapysdr')
optdepends=('python-opengl: osmocom_fft')
makedepends=('git' 'cmake' 'boost' 'spdlog')
replaces=('gr-osmosdr')
source=("git+https://gitea.osmocom.org/sdr/gr-osmosdr.git#commit=$_commit"
        "gnuradio-osmosdr.pc")
sha256sums=('SKIP'
            '2efced5c85ef8375b8261d82c0319f94505e2b77cf30b3fa56ff73a9ddb87b05')

build() {
  local _pyver=$(python -c 'import sys; print(str(sys.version_info[0])+"."+str(sys.version_info[1]))')
  cmake -B build -S $_pkgname \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE="/usr/bin/python3" \
    -DPYTHON_INCLUDE_DIR="/usr/include/python${_pyver}" \
    -DPYTHON_LIBRARY="/usr/lib/libpython${_pyver}.so" \
    -DBoost_NO_BOOST_CMAKE=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 gnuradio-osmosdr.pc "$pkgdir/usr/lib/pkgconfig/gnuradio-osmosdr.pc"
}
