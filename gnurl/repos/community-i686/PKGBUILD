# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=gnurl
pkgver=7.54.0
pkgrel=1
pkgdesc="fork of libcurl, which is mostly for GNUnet"
arch=(i686 x86_64)
url="https://gnunet.org/gnurl"
license=('MIT')
depends=('gnutls')
source=(https://gnunet.org/sites/default/files/gnurl-${pkgver}.tar.bz2)
sha256sums=('407d2844a72f386cdf404cb4ab79792cd68546e544a11270349676dd982183f3')

build() {
  cd "$srcdir/$pkgname-${pkgver}"
  ./buildconf
  ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --with-gssapi \
        --with-gnutls \
        --without-ssl \
        --without-libidn \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -rf "$pkgdir"/usr/share/man \
    "$pkgdir"/usr/bin/curl \
    "$pkgdir"/usr/share/aclocal
  mkdir -p "$pkgdir"/usr/include/gnurl
  mv "$pkgdir"/usr/include/curl "$pkgdir"/usr/include/gnurl/curl
  sed -i 's|includedir=.*|includedir=/usr/include/gnurl|g' "$pkgdir"/usr/lib/pkgconfig/libgnurl.pc
}
