# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=gnurl
pkgver=7.61.0
pkgrel=1
pkgdesc="fork of libcurl, which is mostly for GNUnet"
arch=(x86_64)
url="https://gnunet.org/gnurl"
license=('MIT')
depends=('gnutls' 'libpsl' 'krb5' 'libnghttp2')
source=("https://ftp.gnu.org/gnu/gnunet/gnurl-$pkgver.tar.Z"{,.sig})
validpgpkeys=('A88C8ADD129828D7EAC02E52E22F9BBFEE348588')  # Nils Gillmann
sha512sums=('fb8e5b7c25671d44dd7a3415443ea60d5d28a21575ee2979b3b87210e5905cfc99143e92ffa7e103c1fd1b542f1de95d77c2281ccdbae007bd1b0341e44f58f4'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./buildconf
  ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --with-gssapi \
        --with-gnutls \
        --without-ssl \
        --without-libidn \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

#  rm -rf "$pkgdir"/usr/share/man \
#    "$pkgdir"/usr/bin/curl \
#    "$pkgdir"/usr/share/aclocal
#  mkdir -p "$pkgdir"/usr/include/gnurl
#  mv "$pkgdir"/usr/include/curl "$pkgdir"/usr/include/gnurl/curl
#  sed -i 's|includedir=.*|includedir=/usr/include/gnurl|g' "$pkgdir"/usr/lib/pkgconfig/libgnurl.pc
}
