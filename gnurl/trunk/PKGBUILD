# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=gnurl
pkgver=7.50.3
pkgrel=1
pkgdesc="fork of libcurl, which is mostly for GNUnet"
arch=(i686 x86_64)
url="https://gnunet.org/gnurl"
license=('MIT')
depends=('gnutls')
source=(https://gnunet.org/sites/default/files/gnurl-${pkgver//./_}.tar.bz2)
md5sums=('757a402444aeb75e2bdbf09383f20d02')

build() {
  cd "$srcdir/$pkgname-${pkgver//./_}"
  ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --with-gssapi \
        --with-gnutls \
        --without-ssl \
        --without-libidn \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver//./_}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -rf $pkgdir/usr/share/man \
    $pkgdir/usr/bin/curl \
    $pkgdir/usr/share/aclocal
  mkdir -p $pkgdir/usr/include/gnurl
  mv $pkgdir/usr/include/curl $pkgdir/usr/include/gnurl/curl
  sed -i 's|includedir=.*|includedir=/usr/include/gnurl|g' $pkgdir/usr/lib/pkgconfig/libgnurl.pc
}
