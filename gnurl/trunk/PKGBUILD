# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=gnurl
pkgver=7.56.1
pkgrel=1
pkgdesc="fork of libcurl, which is mostly for GNUnet"
arch=(x86_64)
url="https://gnunet.org/gnurl"
license=('MIT')
depends=('gnutls')
source=(https://gnunet.org/sites/default/files/gnurl-${pkgver}.tar.bz2)
sha256sums=('c8069972327e47fa7e4f36754cbaa5d27e75ebb3de11a9b10ba628c0e9d00b5e')

build() {
  cd "$srcdir/$pkgname-${pkgver}"
  ./buildconf
  ./configure \
        --prefix=/usr \
        --mandir=/usr/share/man \
        --disable-ldap \
        --disable-ldaps \
        --enable-ipv6 \
        --enable-manual \
        --enable-versioned-symbols \
        --enable-threaded-resolver \
        --with-gssapi \
        --with-gnutls \
        --without-ssl \
        --without-libidn \
        --with-random=/dev/urandom \
        --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

#  rm -rf "$pkgdir"/usr/share/man \
#    "$pkgdir"/usr/bin/curl \
#    "$pkgdir"/usr/share/aclocal
#  mkdir -p "$pkgdir"/usr/include/gnurl
#  mv "$pkgdir"/usr/include/curl "$pkgdir"/usr/include/gnurl/curl
#  sed -i 's|includedir=.*|includedir=/usr/include/gnurl|g' "$pkgdir"/usr/lib/pkgconfig/libgnurl.pc
}
