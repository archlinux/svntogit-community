# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=gnustep-back
pkgver=0.20.0
pkgrel=2
pkgdesc="The GNUstep GUI Backend"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('LGPL')
depends=(libgl libxmu gcc-libs freetype2 libart-lgpl)
makedepends=('gnustep-gui' 'libffi' 'gcc-objc')
conflicts=('gnustep-back-svn')
groups=('gnustep-core')
source=(ftp://ftp.gnustep.org/pub/gnustep/core/$pkgname-$pkgver.tar.gz)
md5sums=('a28ba696419eeab17f14c2a75b21c3a7')

build() {
  cd $srcdir/$pkgname-$pkgver
  . /etc/profile.d/GNUstep.sh
  ./configure --prefix=/usr --sysconfdir=/etc/GNUstep
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  . /etc/profile.d/GNUstep.sh
  make DESTDIR=$pkgdir install
  mkdir -p $pkgdir/etc/ld.so.conf.d
  cat >$pkgdir/etc/ld.so.conf.d/gnustep.conf <<EOF
/opt/GNUstep/System/Library/Libraries
/usr/lib/GNUstep/Libraries
EOF
}
