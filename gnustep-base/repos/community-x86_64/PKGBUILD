# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=gnustep-base
pkgver=1.20.0
pkgrel=1
pkgdesc="The GNUstep base package"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=("GPL" "LGPL")
depends=(libxslt avahi gmp gcc-libs openssl ffcall gnustep-make gnutls)
makedepends=(gcc-objc)
conflicts=('gnustep-base-svn')
groups=('gnustep-core')
options=('!emptydirs' '!makeflags')
source=(ftp://ftp.gnustep.org/pub/gnustep/core/$pkgname-$pkgver.tar.gz)
md5sums=('cb099fb8cf960a8dde78d9de7d1b3640')

build() {
  cd $startdir/src/$pkgname-$pkgver
  source /etc/profile.d/GNUstep.sh
  # need this stupid hack
  mkdir -p $startdir/pkg/opt/GNUstep/System/Library/Libraries/Resources/gnustep-base/NSTimeZones
  ./configure --prefix=/opt/GNUstep --enable-ffcall --disable-libffi
  # fix file ownership
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' NSTimeZones/Makefile.postamble
  make VERBOSE=1 || return 1
  make DESTDIR=$startdir/pkg install || return 1
  chown -R root.root $startdir/pkg/
}
