# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=gnustep-gui
pkgver=0.22.0
pkgrel=3
pkgdesc="The GNUstep GUI class library"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('LGPL')
depends=('gnustep-back' 'libungif' 'aspell' 'gcc-libs' 'libcups' 'audiofile' 'libsndfile' 'giflib' 'libao')
makedepends=('gcc-objc' 'gnustep-base' 'gnustep-make')
conflicts=('gnustep-gui-svn')
groups=('gnustep-core')
options=('!makeflags')
source=(ftp://ftp.gnustep.org/pub/gnustep/core/$pkgname-$pkgver.tar.gz)
md5sums=('3aa3346071736206ca144ce1ce8f063f')

build() {
  cd $srcdir/$pkgname-$pkgver
  . /etc/profile.d/GNUstep.sh
  ./configure --prefix=/usr --sysconfdir=/etc/GNUstep
  sed -i 's|#include "GNUstepBase/preface.h"|//#include "GNUstepBase/preface.h" |' Source/GSGuiPrivate.h
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
