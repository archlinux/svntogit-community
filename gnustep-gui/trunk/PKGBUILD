# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=gnustep-gui
pkgver=0.23.1
pkgrel=5
pkgdesc="The GNUstep GUI class library"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=('LGPL')
depends=('gnustep-back' 'aspell' 'gcc-libs' 'libcups' 'audiofile' 'libsndfile' 'giflib' 'libao')
makedepends=('gcc-objc' 'gnustep-base' 'gnustep-make')
conflicts=('gnustep-gui-svn')
groups=('gnustep-core')
options=('!makeflags')
source=(ftp://ftp.gnustep.org/pub/gnustep/core/$pkgname-$pkgver.tar.gz
        giflib-5.0.patch)
md5sums=('1771bdb42f27ee946b17bf60fef5eb2e'
         'b998c0bc3e2bc260c2779d7a50ce6407')

build() {
  cd $srcdir/$pkgname-$pkgver
  . /etc/profile.d/GNUstep.sh
  ./configure --prefix=/usr --sysconfdir=/etc/GNUstep
  sed -i 's|#include "GNUstepBase/preface.h"|//#include "GNUstepBase/preface.h" |' Source/GSGuiPrivate.h
  sed -i 's|png_sizeof|sizeof|g' Source/NSBitmapImageRep+PNG.m
  patch -Np1 -i ../giflib-5.0.patch
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
