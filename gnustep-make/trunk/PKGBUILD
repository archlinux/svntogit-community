# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=gnustep-make
pkgver=2.6.8
pkgrel=3
pkgdesc="The GNUstep make package"
arch=('i686' 'x86_64') # See FS#29546
url="http://www.gnustep.org/"
license=('GPL')
depends=()
conflicts=('gnustep-make-svn')
groups=('gnustep-core')
backup=('etc/GNUstep/GNUstep.conf')
source=(ftp://ftp.gnustep.org/pub/gnustep/core/$pkgname-$pkgver.tar.gz)
md5sums=('7faa60b737e9bbb7fab69ecb14af4e61')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  for i in GNUSTEP_SYSTEM_ADMIN_TOOLS GNUSTEP_NETWORK_ADMIN_TOOLS GNUSTEP_LOCAL_ADMIN_TOOLS; do
    sed -i "s|@$i@|/usr/bin|g" GNUstep{,-strict-v2}.conf.in filesystem.{sh,csh,make}.in
  done
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/GNUstep \
    --enable-native-objc-exceptions
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m755 \
    $pkgdir/usr/share/GNUstep/Makefiles/GNUstep.sh \
    $pkgdir/etc/profile.d/GNUstep.sh
}
