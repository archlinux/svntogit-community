# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=go-ethereum
pkgver=1.8.6
_commit=12683feca7483f0b0bf425c3c520e2724f69f2aa
pkgrel=1
pkgdesc='Official golang implementation of the Ethereum protocol'
arch=('x86_64')
url='http://geth.ethereum.org/'
license=('GPL3')
depends=('glibc')
makedepends=('git' 'go-pie')
provides=('geth')
conflicts=('geth')
replaces=('geth')
source=("git+https://github.com/ethereum/go-ethereum.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
    cd ${pkgname}

    make all
}

package() {
    cd ${pkgname}

    install -Dm755 -t "${pkgdir}"/usr/bin build/bin/*
}
