# Maintainer: Morten Linderud <foxboron@arhlinux.org>
# Contributor: SÃ©bastien "Seblu" Luttringer <seblu@arhlinux.org>

pkgname=go-md2man
pkgver=2.0.0
pkgrel=2
pkgdesc='A markdown to manpage generator'
arch=('x86_64')
url='https://github.com/cpuguy83/go-md2man'
license=('MIT')
makedepends=('go-pie')
source=($pkgname-$pkgver.tar.gz::"https://github.com/cpuguy83/go-md2man/archive/v$pkgver.tar.gz")
sha256sums=('50537880d42fc28b9c2e9aaa36b137349d43cc73d46436a499f8c928cd2fc576')

build() {
  export GOPATH=$(readlink -f .)
  local repo=src/github.com/cpuguy83
  mkdir -p "$repo"
  ln -sf "../../../go-md2man-$pkgver" "$repo/go-md2man"
  cd "$repo/go-md2man"
  go get -v ./...
  "$srcdir/bin/go-md2man" -in=go-md2man.1.md -out=go-md2man.1
}

package() {
  install -Dm755 "$srcdir/bin/go-md2man" "$pkgdir/usr/bin/go-md2man"
  install -Dm755 "$srcdir/$pkgname-$pkgver/go-md2man.1" "$pkgdir/usr/share/man/man1/go-md2man.1"
  install -Dm755 "$srcdir/$pkgname-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
