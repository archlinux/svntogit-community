# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Mario Finelli <mario at finel dot li>

pkgname=go-yq
pkgver=4.27.5
pkgrel=1
pkgdesc='Portable command-line YAML processor'
arch=('x86_64')
url=https://github.com/mikefarah/yq
license=('MIT')
depends=('glibc')
makedepends=('go')
conflicts=('yq')
source=("$url/archive/v$pkgver/yq-$pkgver.tar.gz")
b2sums=('fd69367f7c8a9ba0a22050eba92c7e90b1e3ff1b1c428f550c104775e85c3e079931b710e684d37ba4690e2489060181c8ec1cefcbb5982b640a1ce9edb17a6c')

prepare() {
  cd yq-$pkgver
  go mod vendor
}

build() {
  cd yq-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"
  go build .
}

check() {
  cd yq-$pkgver
  go test ./...
}

package() {
  cd yq-$pkgver
  install -Dt "$pkgdir"/usr/bin yq
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md

  ./yq shell-completion bash | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/bash-completion/completions/yq
  ./yq shell-completion zsh | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/zsh/site-functions/_yq
  ./yq shell-completion fish | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/fish/vendor_completions.d/yq.fish
}

# vim:set ts=2 sw=2 et:
