# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=0.9.3
pkgrel=2
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('i686' 'x86_64')
url='http://goaccess.io'
license=('GPL')
depends=('ncurses' 'geoip' 'glib2')
backup=('etc/goaccess.conf')
source=("http://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('624e6ae50d08550440b7f76573003f50a01fe7d0b74302ac89e3281cd7c8b1e541a4e01211dfa41c687c9485b6b1d72ac8b2ed9a9e0c7dcddf1b1c0c7dee33f8')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip --enable-utf8
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}

