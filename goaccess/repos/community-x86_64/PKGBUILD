# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.4.6
pkgrel=2
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('x86_64')
url='https://goaccess.io'
license=('GPL')
depends=('ncurses' 'libmaxminddb' 'openssl')
optdepends=('geoip2-database: for geoip support')
backup=('etc/goaccess/goaccess.conf')
source=("https://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('4470809e7ebe59522824b3300e31b93294cfd964e25b1a3c50c77e1c10d6f23b3b014c86518cd8a28f314ce875f15627aba0f0e9f4636ef4181d1169c018f1ff')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip=mmdb --enable-utf8 --with-openssl
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}
