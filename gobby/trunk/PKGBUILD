# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=gobby
pkgver=0.6.0
pkgrel=1
epoch=1
pkgdesc="A collaborative text editor"
arch=('x86_64')
url="https://gobby.github.io/"
license=('GPL')
depends=('gtkmm3' 'gtksourceview3' 'libinfinity' 'libxml++2.6')
makedepends=('git' 'intltool' 'yelp-tools')
_commit=6ce877d00e29508acb4c93484feaba99dd9e2918  # master
source=("git+https://github.com/gobby/gobby.git#commit=$_commit")
sha512sums=('SKIP')
validpgpkeys=('B63A7560B25B619CA11E219225A941E879B57591') # Armin Burgmeier <armin@arbur.net>

pkgver() {
  cd "${srcdir}/${pkgname}"
  git describe | sed 's/^v//;s/-/+/g'
}

build() {
  cd "${srcdir}/${pkgname}"
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="$pkgdir" install
}
