# $Id$
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Sauyon Lee <arch@sauyon.com>

pkgname=godep
pkgver=79
pkgrel=1
epoch=2
pkgdesc="Dependency tool for go"
arch=('i686' 'x86_64')
url="http://github.com/tools/godep"
license=('BSD')
depends=('glibc')
makedepends=('git' 'go' 'mercurial')
source=($pkgname-$pkgver.tar.gz::https://github.com/tools/godep/archive/v$pkgver.tar.gz)
sha256sums=('3dd2e6c4863077762498af98fa0c8dc5fedffbca6a5c0c4bb42b452c8268383d')

prepare() {
  export GOPATH="${srcdir}"
  go get golang.org/x/tools/go/vcs
  go get github.com/pmezard/go-difflib/difflib
  go get github.com/kr/fs
  go get github.com/kr/pretty
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  GOPATH="${srcdir}" go build
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  install -Dm755 $pkgname-$pkgver "${pkgdir}"/usr/bin/$pkgname
  install -Dm644 License "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
