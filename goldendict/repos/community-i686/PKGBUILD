# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: m0rph <m0rph.mailbox@gmail.com>

pkgname=goldendict
pkgver=1.0.1
pkgrel=6
pkgdesc="Feature-rich dictionary lookup program"
arch=('i686' 'x86_64')
url="http://goldendict.berlios.de/"
license=('GPL3')
depends=('hunspell' 'libvorbis' 'libxtst' 'libzip' 'qtwebkit' 'phonon')
makedepends=('patch')
provides=('stardict')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.tar.bz2
        $pkgname-paths.diff
        gcc47.patch)
sha256sums=('b3fb0405a5edb38f02ef881b48c36e46e2eacf641b0caf8d99403f595a4be9a6'
            '1dacae7b7a7d7c20a22b4595c07dbd65fdad134a0d4cf283312ea3bdb5f42589'
            '2e13857d047186e781c3bd9de3cfdf794f4d44e8e28f9d18dda8df50cfe13c6e')

build(){
  cd ${srcdir}

  patch -Np0 -i ${srcdir}/$pkgname-paths.diff
  patch -p1 -i ${srcdir}/gcc47.patch

  qmake-qt4
  make
}

package() {
  cd ${srcdir}

  make INSTALL_ROOT=${pkgdir} install

  #removing unneeded
  rm -rf ${pkgdir}/usr/share/app-install
}
