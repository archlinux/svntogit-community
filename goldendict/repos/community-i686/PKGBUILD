# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: m0rph <m0rph.mailbox@gmail.com>

pkgname=goldendict
pkgver=1.5.0RC
pkgrel=2
pkgdesc="Feature-rich dictionary lookup program"
arch=('i686' 'x86_64')
url="http://goldendict.org"
license=('GPL3')
depends=('hunspell' 'libxtst' 'libzip' 'libao' 'qtwebkit' 'phonon-qt4' 'ffmpeg')
provides=('stardict')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/goldendict/goldendict/archive/1.5.0-RC.tar.gz
        $pkgname-paths.patch
        gcc47.patch
        fix-wikipedia-images.patch)
sha256sums=('5705d5197509596221faf16a806965c799de7371a41fe2d2c4dc00b737ffe22e'
            '5281dc2f57a93e2aa416de35bd702926bd90b4746d62f81d8dc434f6a689d55e'
            '2e13857d047186e781c3bd9de3cfdf794f4d44e8e28f9d18dda8df50cfe13c6e'
            'ab669d96db0cb750d4c865b123474d655b6caca9242457c044a60f1fee5b7dc7')

prepare(){
  cd "${srcdir}"/${pkgname}-1.5.0-RC

  patch -Np0 -i "${srcdir}"/$pkgname-paths.patch
#  patch -p1 -i "${srcdir}"/gcc47.patch
#  patch -Np1 -i "${srcdir}"/fix-wikipedia-images.patch
}

build(){
  cd "${srcdir}"/${pkgname}-1.5.0-RC

  qmake-qt4 PREFIX="/usr"
  make
}

package() {
  cd "${srcdir}"/${pkgname}-1.5.0-RC

  make INSTALL_ROOT="${pkgdir}" install

# languages
  install -d "${pkgdir}"/usr/share/$pkgname/locale
  install -m644 locale/*.qm -t "${pkgdir}"/usr/share/$pkgname/locale

# removing unneeded
  rm -rf "${pkgdir}"/usr/share/app-install
}
