# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=goocanvas
pkgver=2.0.4
pkgrel=2
pkgdesc="A cairo canvas widget for GTK+"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/GooCanvas"
license=('LGPL')
depends=('gtk3')
makedepends=('gobject-introspection' 'python-gobject' 'python2-gobject')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('c728e2b7d4425ae81b54e1e07a3d3c8a4bd6377a63cffa43006045bceaa92e90')

build() {
  cp -r $pkgname-$pkgver python2

  cd $pkgname-$pkgver
  ./configure --prefix=/usr PYTHON=python3
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  cd "$srcdir/python2"
  ./configure --prefix=/usr PYTHON=python2
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd "$srcdir/python2"
  make -C bindings/python DESTDIR="$pkgdir" install
}
