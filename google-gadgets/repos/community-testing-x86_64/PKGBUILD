# $Id$
# Maintainer: Ray Rashif <schivmeister@gmail.com>

pkgbase="google-gadgets"
pkgname=('google-gadgets-common' 'google-gadgets-gtk' 'google-gadgets-qt')
_pkgbasename=$pkgbase-for-linux
_pkgbasedir=$startdir/pkg
pkgver=0.11.2
_sover=1.0
_sobump=0.0.0
pkgrel=2
arch=(i686 x86_64)
url="http://code.google.com/p/google-gadgets-for-linux/"
license=('APACHE')
makedepends=('gstreamer0.10-base-plugins' 'libwebkit'
             'desktop-file-utils' 'xdg-utils'
             'xulrunner' 'librsvg' 'curl' 'libsoup-gnome'
             'qt'
             'networkmanager'
             'zip' 'flex')
options=('!libtool')
source=(http://google-gadgets-for-linux.googlecode.com/files/$_pkgbasename-$pkgver.tar.bz2)

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  ./configure	--prefix=/usr \
                --disable-werror \
                --disable-update-desktop-database \
                --disable-update-mime-database \
                --with-oem-brand=Arch \
                --with-ssl-ca-file=/etc/ssl/certs/ca-certificates.crt \
                --with-browser-plugins-dir=/usr/lib/mozilla/plugins

  make || return 1
  make -j1 DESTDIR="$pkgdir/" install
}

package_google-gadgets-common() {
  pkgdesc="Desktop widgets from Google (common files)"
  install=$pkgbase-common.install
  depends=('hicolor-icon-theme' 'shared-mime-info')
  optdepends=('google-gadgets-gtk: GTK+ UI'
              'google-gadgets-qt: Qt UI')

  # we need to remove stuff in this package
  cp -a "$_pkgbasedir/usr" "$pkgdir/"

  # do not force any removal; we want to know in case files change
  cd "$pkgdir/usr"
  rm -r bin
  rm -r share/applications

  cd "$pkgdir/usr/lib/$pkgbase"

  # gtk-related
  rm gtkmoz-browser-child
  rm ../lib*gtk*
  rm modules/gtk*.so

  # qt-related
  rm ../lib*qt*
  rm modules/qt*.so
}

package_google-gadgets-gtk() {
  pkgdesc="Desktop widgets from Google (GTK+ front-end)"
  groups=('google-gadgets')
  install=$pkgbase.install
  depends=('google-gadgets-common' 'xulrunner'
           'desktop-file-utils' 'xdg-utils'
           'gstreamer0.10-base-plugins' 'libwebkit' 'libsoup-gnome'
           'librsvg' 'curl')
  optdepends=('networkmanager: desktop integration')

  # install the gtk binary
  cd "$_pkgbasedir"
  install -Dm755 "usr/bin/ggl-gtk" "$pkgdir/usr/bin/ggl-gtk"

  # gtk-only browser stuff
  install -Dm755 usr/lib/$pkgbase/gtkmoz-browser-child \
    "$pkgdir/usr/lib/$pkgbase/gtkmoz-browser-child"

  # now the gtk libraries
  mkdir "$pkgdir/usr/lib/$pkgbase/modules"

  install -m755 usr/lib/$pkgbase/modules/gtk*.so \
    "$pkgdir/usr/lib/$pkgbase/modules/"

  install -m755 usr/lib/lib*gtk* "$pkgdir/usr/lib"

  # fix permissions and symlinks
  find "$pkgdir/usr/lib" -name *.a -exec chmod 644 {} \;

  ln -sf /usr/lib/libggadget-gtk-$_sover.so.$_sobump \
    "$pkgdir/usr/lib/libggadget-gtk-$_sover.so"

  ln -sf /usr/lib/libggadget-gtk-$_sover.so.$_sobump \
    "$pkgdir/usr/lib/libggadget-gtk-$_sover.so.0"

  # freedesktop.org for gtk
  install -Dm644 usr/share/applications/ggl-gtk.desktop \
    "$pkgdir/usr/share/applications/ggl-gtk.desktop"

  install -m644 usr/share/applications/ggl-designer.desktop \
    "$pkgdir/usr/share/applications/"
}

package_google-gadgets-qt() {
  pkgdesc="Desktop widgets from Google (Qt front-end)"
  groups=('google-gadgets')
  install=$pkgbase.install
  depends=('google-gadgets-common' 'qt'
           'desktop-file-utils' 'xdg-utils'
           'gstreamer0.10-base-plugins' 'libwebkit')
  optdepends=('networkmanager: desktop integration')

  # install the qt binary
  cd "$_pkgbasedir"
  install -Dm755 usr/bin/ggl-qt "$pkgdir/usr/bin/ggl-qt"

  # the qt libraries
  mkdir -p "$pkgdir/usr/lib/$pkgbase/modules"

  install -m755 usr/lib/$pkgbase/modules/qt*.so \
    "$pkgdir/usr/lib/$pkgbase/modules/"

  install -m755 usr/lib/lib*qt* "$pkgdir/usr/lib/"

  # fix permissions and symlinks
  find "$pkgdir/usr/lib" -name *.a -exec chmod 644 {} \;

  ln -sf /usr/lib/libggadget-qt-$_sover.so.$_sobump \
    "$pkgdir/usr/lib/libggadget-qt-$_sover.so"

  ln -sf /usr/lib/libggadget-qt-$_sover.so.$_sobump \
    "$pkgdir/usr/lib/libggadget-qt-$_sover.so.0"

  # freedesktop.org for qt
  install -Dm644 usr/share/applications/ggl-qt.desktop \
    "$pkgdir/usr/share/applications/ggl-qt.desktop"
}
md5sums=('279305553bcbf74f3e3615f6abc36a5e')
