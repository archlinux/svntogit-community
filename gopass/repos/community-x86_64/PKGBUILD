# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=gopass
pkgver=1.8.4
pkgrel=2
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/gopasspw/gopass"
license=('MIT')
makedepends=('go-pie')
optdepends=('xdotool: for typing passwords selected by dmenu'
            'xsel: clipboard support'
            'xclip: clipboard support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass/archive/v${pkgver}.tar.gz")
sha256sums=('314b317c49ee7f8c59758ab82cf5f6942ce9179c97e53f0f952741dbd0e5471a')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src
  mv "${pkgname}-${pkgver}"/vendor/* src/
  mkdir -p src/github.com/gopasspw
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/gopasspw/gopass
}

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  make build
  make completion
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
