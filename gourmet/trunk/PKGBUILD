# $Id$
# Maintainer:
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: see .contrib

pkgname=gourmet
pkgver=0.16.1
pkgrel=1
pkgdesc="A simple but powerful recipe-managing application"
arch=('any')
url="http://thinkle.github.io/gourmet/"
license=('GPL')
depends=('pygtk' 'python2-pillow' 'python2-sqlalchemy' 'hicolor-icon-theme')
makedepends=('python2-distutils-extra')
optdepends=('gstreamer0.10-python: play timer sounds'
            'ipython2: Python Shell plugin'
            'pyrtf: RTF support'
            'python2-beautifulsoup3: Webpage import plugin'
            'python2-gtkspell: Spell Checking plugin'
            'python2-poppler: Printing and PDF Export plugin'
            'python2-reportlab: Printing and PDF Export plugin')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/thinkle/$pkgname/archive/$pkgver.tar.gz")
md5sums=('322814a7394d6bace7e0ef887efbe0f8')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}
