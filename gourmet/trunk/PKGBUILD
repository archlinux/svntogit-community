# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=gourmet
pkgver=0.17.1
pkgrel=1
pkgdesc="A simple but powerful recipe-managing application"
arch=('any')
url="http://thinkle.github.io/gourmet/"
license=('GPL')
depends=('pygtk' 'python2-pillow' 'python2-sqlalchemy' 'hicolor-icon-theme')
makedepends=('python2-distutils-extra')
optdepends=('gstreamer0.10-python: play timer sounds'
            'ipython2: Python Shell plugin'
            'pyrtf: RTF support'
            'python2-beautifulsoup3: Webpage import plugin'
            'python2-gtkspell: Spell Checking plugin'
            'python2-poppler: Printing and PDF Export plugin'
            'python2-reportlab: Printing and PDF Export plugin')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/thinkle/$pkgname/archive/$pkgver.tar.gz"
        "https://github.com/thinkle/gourmet/commit/1cd11c485f992593bf5b33ba15c941861ca67ccf.patch")
md5sums=('97743c102776d1eff0ba8d48d47cf9c6'
         '2a060fd6be62575003bd6e01c0b95b3b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Fix build
  patch -Np1 -i ../1cd11c485f992593bf5b33ba15c941861ca67ccf.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
