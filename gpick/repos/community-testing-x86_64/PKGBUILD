# Maintainer: Robin Broda <coderobe@archlinux.org>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=gpick
pkgver=0.2.6
pkgrel=5
pkgdesc="Advanced color picker written in C++ using GTK+ toolkit"
arch=('x86_64')
url="http://www.gpick.org/"
license=('BSD')
depends=('boost-libs' 'gtk3' 'lua')
makedepends=('boost' 'scons' 'ragel')
source=("https://github.com/thezbyg/$pkgname/archive/$pkgname-$pkgver.tar.gz"
        "https://github.com/thezbyg/$pkgname/commit/d4b7e77e895f57bf5dfee3cf4f0ad0258ec303bf.patch"
        "https://github.com/thezbyg/$pkgname/commit/0f11efed811b8d986046984c1e7099d6a49c2650.patch"
        "https://github.com/thezbyg/$pkgname/commit/37b14bbb149dfd0944c20b9da6ea472eb2f669cd.patch"
        "https://github.com/thezbyg/$pkgname/commit/decbb45ac3bda3f7a29d5a4a3471ccf52fedc707.patch")
sha256sums=('7d02be171cc88c44ad3e3572c0922bbabedc9d542b989f324fca9d5db3161e9c'
            '34ff9ef1ae6f2d9370c630baa07e9c2cbc2345f0aa865647eb53bbb5336b2578'
            '16968ffdc5b21d2ec189e3c3b2bd0796b9d11cb6de69c716cec18ef16f850c93'
            '341999b461f8e4c20b1f7111423403099d9dae45ce3ce0080590c7d0a6da05f7'
            'fab1c6eb40f250274ad9576d073126e1fbddd78761e5cb15290732e7dca44044')

prepare() {
    cd "$pkgname-$pkgname-$pkgver"
    patch -p1 <../d4b7e77e895f57bf5dfee3cf4f0ad0258ec303bf.patch
    patch -p1 <../0f11efed811b8d986046984c1e7099d6a49c2650.patch
    patch -p1 <../37b14bbb149dfd0944c20b9da6ea472eb2f669cd.patch
    patch -p1 <../decbb45ac3bda3f7a29d5a4a3471ccf52fedc707.patch
}

build() {
    cd "$pkgname-$pkgname-$pkgver"
    scons USE_GTK3=true
}

package() {
    cd "$pkgname-$pkgname-$pkgver"
    scons USE_GTK3=true DESTDIR="$pkgdir/usr" install
    install -Dm644 installer/License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
