# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=gpick
pkgver=0.2.6rc1
pkgrel=8
pkgdesc="Advanced color picker written in C++ using GTK+ toolkit"
arch=('x86_64')
url="http://www.gpick.org/"
license=('BSD')
depends=('boost-libs' 'gtk2' 'lua')
makedepends=('boost' 'scons' 'ragel')
source=("https://github.com/thezbyg/$pkgname/archive/$pkgname-$pkgver.tar.gz" scons-python3.patch
         gpick-link-expat.patch)
sha256sums=('4e55600a6894fb52ab3d9d4a2fc42580cce289da6912bf916060a5a6301333d8'
            '1a5f4f7c8846ba3af0247495391ffa09f0425a39a1911cd88dffcbdf1487082c'
            '6091496e3b300c4edc986e9c0938539ffa2e62067f3e578286ca34bbbd5dc712')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  patch -p1 -i ../scons-python3.patch
  patch -p1 -i ../gpick-link-expat.patch # https://github.com/thezbyg/gpick/issues/112
}

build() {
    cd "$pkgname-$pkgname-$pkgver"
    scons
}

package() {
    cd "$pkgname-$pkgname-$pkgver"
    scons DESTDIR="$pkgdir/usr" install
    install -Dm644 installer/License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
