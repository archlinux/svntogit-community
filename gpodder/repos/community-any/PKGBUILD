# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: enderst <enderst@gmail.com>

pkgname=gpodder
pkgver=2.18
pkgrel=1
pkgdesc='A podcast receiver/catcher'
license=('GPL3')
arch=('any')
url='http://gpodder.berlios.de/'
depends=('pygtk' 'python2-feedparser' 'dbus-python' 'python2-mygpoclient')
makedepends=('intltool' 'imagemagick' 'help2man')
optdepends=('libgpod: for ipod support'
            'pymad: for ipod support'
            'mplayer: for ipod video support'
            'python-imaging: rockbox cover art support'
            'vorbis-tools: for ogg tag updating and converting to mp3'
            'lame: for converting ogg to mp3'
            'gnome-bluetooth: transfer podcast via bluethooth'
            'pywebkitgtk: render shownotes'
            'python-eyed3: for ipod support')
            # pymtp: for mtp support (AUR only)
source=(http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('69b2667c89996c39cda70b20b6b518db')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  PYTHON=python2 make DESTDIR=${pkgdir}/ install

  sed -i -e 's|#!/usr/bin/python$|#!/usr/bin/python2|' \
         -e 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    $(find $pkgdir/usr/lib/python2.7/site-packages/gpodder/ -name '*.py')
}
