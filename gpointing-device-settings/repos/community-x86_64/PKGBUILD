# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Thomas Mudrunka <harvie@@email..cz>

pkgname=gpointing-device-settings
pkgver=1.5.1
pkgrel=4
pkgdesc="GUI tool for setting pointing device such as TrackPoint or Touchpad"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GPointingDeviceSettings"
depends=('gtk2' 'gconf')
makedepends=('gnome-settings-daemon' 'intltool')
options=('!libtool')
source=("http://keihanna.dl.sourceforge.jp/gsynaptics/45812/${pkgname}-${pkgver}.tar.gz")
md5sums=('1d1491473df8eabca3c15c997a975d7f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Enable deprecations
  sed -i -e '/DISABLE_DEPRECATED/d' -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac

  # Disable GSD plugin (won't build with GSD 3.8)
  sed -i 's/ gnome-settings-daemon-plugins//' modules/Makefile.am

  autoreconf -fi
  ./configure  --prefix=/usr --sysconfdir=/etc \
               --disable-static --disable-schemas-install \
               --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  # Remove GConf schema (used by GSD plugin)
  rm -r "${pkgdir}/usr/share/gconf"
}
