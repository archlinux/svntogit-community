# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributer: Jason Chu <jchu@xentac.net>

pkgname=gprolog
pkgver=1.4.5
pkgrel=1
pkgdesc="GNU Prolog"
arch=('x86_64')
url="http://www.gprolog.org/"
depends=('glibc')
license=('GPL')
options=('staticlibs')
# "ftp://ftp.gnu.org/gnu/gprolog/gprolog-$pkgver.tar.gz"
source=("http://www.gprolog.org/gprolog-$pkgver.tar.gz")
md5sums=('7757de718e47e284544be96994300408')

build() {
   cd "$srcdir/gprolog-$pkgver/src"
   CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
   ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --with-c-flags="$CFLAGS -fPIC"
   make
}

package() {
   cd "$srcdir/gprolog-$pkgver/src"
   make install \
	INSTALL_DIR="$pkgdir"/usr/share/gprolog \
	LINKS_DIR="$pkgdir"/usr/bin \
	DOC_DIR="$pkgdir"/usr/share/gprolog/doc \
	HTML_DIR="$pkgdir"/usr/share/gprolog/doc/Html \
	EXAMPLES_DIR="$pkgdir"/usr/share/gprolog \
	sysconfdir="$pkgdir"/etc \
	localstatedir="$pkgdir"/var
   cd "$pkgdir"/usr/bin
   rm *
   ln -s /usr/share/gprolog/bin/fd2c
   ln -s /usr/share/gprolog/bin/gplc
   ln -s /usr/share/gprolog/bin/gprolog
   ln -s /usr/share/gprolog/bin/hexgplc
   ln -s /usr/share/gprolog/bin/ma2asm
   ln -s /usr/share/gprolog/bin/pl2wam
   ln -s /usr/share/gprolog/bin/wam2ma
}
