# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributer: Jason Chu <jchu@xentac.net>

pkgname=gprolog
pkgver=1.3.1
pkgrel=2
pkgdesc="GNU Prolog"
arch=('i686' 'x86_64')
url="http://www.gprolog.org/"
depends=(glibc)
license=('GPL')
source=(ftp://ftp.gnu.org/gnu/gprolog/gprolog-$pkgver.tar.gz)
md5sums=('cbae19c31e17bcfca4b57fe35ec4aba2')

build() {
   cd "$srcdir/gprolog-$pkgver/src"
   ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
   make || return 1
   make install \
	INSTALL_DIR="$pkgdir"/usr/share/gprolog \
	LINKS_DIR="$pkgdir"/usr/bin \
	DOC_DIR="$pkgdir"/usr/share/gprolog/doc \
	HTML_DIR="$pkgdir"/usr/share/gprolog/doc/Html \
	EXAMPLES_DIR="$pkgdir"/usr/share/gprolog \
	sysconfdir="$pkgdir"/etc \
	localstatedir="$pkgdir"/var || return 1
   cd "$pkgdir"/usr/bin
   rm *
   ln -s /usr/share/gprolog/bin/fd2c && \
   ln -s /usr/share/gprolog/bin/gplc && \
   ln -s /usr/share/gprolog/bin/gprolog && \
   ln -s /usr/share/gprolog/bin/hexgplc && \
   ln -s /usr/share/gprolog/bin/ma2asm && \
   ln -s /usr/share/gprolog/bin/pl2wam && \
   ln -s /usr/share/gprolog/bin/wam2ma
}
