# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gpsbabel
pkgver=1.4.2
pkgrel=3
pkgdesc="Reads, writes, and manipulates GPS waypoints in a variety of formats"
arch=('i686' 'x86_64')
url="http://www.gpsbabel.org"
license=('GPL')
depends=('libusb-compat' 'expat')
makedepends=('fop' 'qt' 'libxml2' 'libxslt')
optdepends=('qt')
options=('docs')
source=("gpsbabel-$pkgver.tar.gz::http://archlinux-stuff.googlecode.com/files/gpsbabel-$pkgver.tar.gz"
	"gpsbabel.desktop::http://pkgs.fedoraproject.org/gitweb/?p=gpsbabel.git;a=blob_plain;f=gpsbabel.desktop;hb=HEAD")
md5sums=('76ea9f7852be2e98aa18976c4697ca93'
         '7bdbd243e29a8831842d31c079f04c45')

build() {
  cd $srcdir/gpsbabel-$pkgver
  ./configure --prefix=/usr
  make
  make doc
  (cd gui && qmake && make)
}

package() {
  cd $srcdir/gpsbabel-$pkgver
  make DESTDIR=$pkgdir install

  # GUI
  install -Dm755 gui/objects/gpsbabelfe-bin ${pkgdir}/usr/bin/gpsbabelfe-bin
  install -Dm644 gui/images/appicon.png $pkgdir/usr/share/pixmaps/gpsbabel.png
  install -Dm644 $srcdir/gpsbabel.desktop $pkgdir/usr/share/applications/gpsbabel.desktop

  # Header install
  mkdir -p $pkgdir/usr/include/gpsbabel
  cp {defs.h,queue.h,gbtypes.h,filterdefs.h,cet.h,cet_util.h,garmin_tables.h} \
     $pkgdir/usr/include/gpsbabel

  # Doc install
  mkdir -p $pkgdir/usr/share/doc/$pkgname
  cp -r $srcdir/babelweb/* $pkgdir/usr/share/doc/$pkgname/
  cp README* $pkgdir/usr/share/doc/$pkgname/
}
