# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gpsbabel
pkgver=1.5.4
_pkgver=1_5_4
pkgrel=4
pkgdesc="Read, write and manipulate GPS waypoints in a variety of formats"
arch=('x86_64')
url="https://www.gpsbabel.org/"
license=('GPL')
depends=('libusb-compat' 'qt5-base') # 'qt5-webengine'
makedepends=('qt5-tools')
source=(https://github.com/gpsbabel/$pkgname/archive/${pkgname}_$_pkgver.tar.gz
        build-fix.patch
        drop-map.patch
        fix-map.patch
        0008-Correctly-read-diff-and-terr-from-geo-format.patch)
sha256sums=('8cd740db0b92610abff71e942e8a987df58cd6ca5f25cca86e15f2b00e190704'
            '537752bd5d097d836d4d5a77b4f7a5ea68d408d6a648511e4a98865d2742fb46'
            '69b4fad0bc5b33e332dc79c1c65482f942bf5a596b2d82b3d6a565cdd444b6ee'
            'e00530f3b40a6c516cb45c59bbdfd5f4a801f7bb55e02205e7fc975c262db3a1'
            '384e1603179be2d6cc0b2837da7eb11002e5aefd72c1dbc2ed2697ef4058289f')

prepare() {
  cd $pkgname-${pkgname}_$_pkgver

  # Fix build
  patch -Np1 -i ../build-fix.patch

  # Drop map
  patch -Np1 -i ../drop-map.patch

  # Fix map
  # patch -Np1 -i ../fix-map.patch

  # Correctly read diff and terr from geo format
  patch -Np1 -i ../0008-Correctly-read-diff-and-terr-from-geo-format.patch

  # Fix path
  sed -i 's|langPath_ = QApplication::applicationDirPath();|langPath_ = "/usr/share/gpsbabel";|' gui/mainwindow.cc
  # sed -i 's|baseFile =  QApplication::applicationDirPath() + "/gmapbase.html";|baseFile = "/usr/share/gpsbabel/gmapbase.html";|' gui/map.cc
}

build() {
  cd $pkgname-${pkgname}_$_pkgver
  ./configure --prefix=/usr
  make

  # GUI
  cd gui
  qmake-qt5
  lrelease-qt5 *.ts
  make
}

package() {
  cd $pkgname-${pkgname}_$_pkgver
  make DESTDIR="$pkgdir" install

  # GUI
  install -Dm755 gui/objects/gpsbabelfe-bin "$pkgdir"/usr/bin/gpsbabelfe-bin
  install -Dm644 gui/images/appicon.png "$pkgdir"/usr/share/pixmaps/gpsbabel.png
  install -Dm644 gui/gpsbabel.desktop "$pkgdir"/usr/share/applications/gpsbabel.desktop
  # install -Dm644 gui/gmapbase.html "$pkgdir"/usr/share/gpsbabel/gmapbase.html
  install -dm755 "$pkgdir"/usr/share/gpsbabel/translations/
  install -Dm644 gui/*.qm "$pkgdir"/usr/share/gpsbabel/translations/
}
