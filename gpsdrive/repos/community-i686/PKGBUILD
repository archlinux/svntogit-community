# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: damir <damir@archlinux.org>

#
# FIXME:
# Don't know how to make this build system search in /usr/include/gtk-2.0
# I just make symlinks to gtk, gdk, gdk-pixbuf
#
# As of 2.10pre7 I have not had any problems with gtk, but if needed the GTK2
# search paths can be changed in "cmake/Modules/FindGTK2.cmake"

pkgname=gpsdrive
pkgver=2.10pre7
pkgrel=8
pkgdesc="A car (bike, ship, plane) navigation system"
arch=(i686 x86_64)
depends=('gtk2' 'libgda3' 'gpsd' 'perl-file-slurp' 'perl-www-mechanize' 'perl-text-query'
         'perl-dbi' 'perl-xml-twig' 'perl-date-manip' 'libxml2' 'curl' 'python' 'boost>=1.33.1' 'gdal')
makedepends=('cmake>=2.4.4')
url="http://www.gpsdrive.de/"
license=('GPL')
options=('!makeflags')
install=gpsdrive.install
source=(http://www.gpsdrive.de/packages/$pkgname-$pkgver.tar.gz
	http://downloads.sourceforge.net/project/gpsdrive/additional%20data/minimal%20icon%20set/openstreetmap-map-icons-minimal.tar.gz
	gsdrive-gpsd-2.94.patch)
md5sums=('dd5e1382bae11a5514e4fd6853eb062e'
         'f369ecdbb73d4ff5ae10da6fd2ee5f5e'
         '70a81ae68861ec113d3a319d7a43b691')

build() {
  cd $srcdir

  (cd gpsdrive-$pkgver && patch -p1 <$srcdir/gsdrive-gpsd-2.94.patch)

  rm -rf build
  mkdir build
  cd build

  # see DefineOptions.cmake for a list of common options and defaults
  # cmake -L for a more in-depth listing
  cmake -DCMAKE_BUILD_TYPE=debug -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_MAPNIK=OFF -DWITH_SPEECH=OFF \
        -DWITH_GDAL=ON -DWITH_DBUS=ON -DWITH_NAVIGATION=ON "$srcdir/gpsdrive-$pkgver"
  make || return 1
  make DESTDIR=$pkgdir install

  # copy the openstreetmap icon-set
  cp -R ${srcdir}/usr ${pkgdir}
  touch ${pkgdir}/usr/share/gpsdrive.db
}
