# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Gergely Tamas <dice@mfa.kfki.hu>

pkgname=gq
pkgver=1.3.4
pkgrel=5
pkgdesc="interactive graphical LDAP browser"
url="http://sourceforge.net/projects/gqclient"
license=("GPL")
depends=('libxml2' 'gtk2' 'libgnome-keyring' 'libglade' 'libldap>=2.4.18' 'libgpg-error' 'libgcrypt')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils')
arch=('i686' 'x86_64')
source=(http://downloads.sourceforge.net/sourceforge/gqclient/$pkgname-$pkgver.tar.gz
	glib-headers-fix.patch)
md5sums=('d7b1e183a3ecbd77aceea02d27f36b3e'
         '4a98232c5a908c86216c7d9e235d4c0b')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/glib-headers-fix.patch
}

build() {
  cd $srcdir/$pkgname-$pkgver
  LDFLAGS=-lcrypto ./configure --prefix=/usr --disable-debugging --disable-update-mimedb
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
