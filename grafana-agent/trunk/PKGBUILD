# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=('grafana-agent' 'grafana-agentctl' 'grafana-agent-crow')
pkgver=0.24.2
pkgrel=1
pkgdesc='Grafana Agent is a telemetry collector for sending metrics, logs, and trace data to the opinionated Grafana observability stack'
url='https://github.com/grafana/agent'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go')
options=('!lto')
source=($pkgname-$pkgver.tar.gz::https://github.com/grafana/agent/archive/v$pkgver.tar.gz
        "https://github.com/grafana/agent/commit/9e373f7b128c5045618371837dcdb0fc86dcbe76.patch"
        grafana-agent.sysusers grafana-agent.service grafana-agent.tmpfiles)
sha512sums=('356d63117a166ed497b3e1c6da0f4065122254c23cc7af5e1862cc089bf84e2284abc89fb0d0d07becc68641a21b392ba9f6af9a07a24abf1c4cf4a938b9e2a0'
            '237048bb6c02b2ce35fc7f4f70e2095da1fbd1e2fbbe7251f3c3d38dd79332320dd47b1a05564cd6ccea547fd23f4733b33e01cae0f58a46292336c25c92dee3'
            '1e58f6273562fd6ddeae41bb6d223230ed301199af6bd2f85fa3d2c1e6352952c600cd4488ad769069519c42b6863be84aaa2c93c88f696e5a0bedbb93758d0e'
            '3f8debbc4732009f54c063d70c2dce4846b81b829f495d25a431fef857077c83949d777f763de8c106fb8d1cf0bd7e874680db8387b4811704b1a303932fa090'
            '3a4c4896b2454272b5a2d53ba5aa7009fa5b42a573fef521afb5d9712e53119539c4195d2b149d04d086c5fa197ccf7dc9bbdc407efb55514e3c92b994121fa8')

prepare() {
  cd "agent-$pkgver"

  patch -p1 -i ../9e373f7b128c5045618371837dcdb0fc86dcbe76.patch

  go mod download
}

build() {
  cd "agent-$pkgver"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPROXY=off
  for path in cmd/agent cmd/agentctl tools/crow; do
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags " \
         -X github.com/grafana/agent/pkg/build.Version=$pkgver
         -X github.com/grafana/agent/pkg/build.BuildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH})
         -linkmode external
         -extldflags \"${LDFLAGS}\"" \
      "./$path"
  done
}

check() {
  cd "agent-$pkgver"

  ./agent -version

  go test -v ./cmd/agent/... ./cmd/agentctl/... ./tools/crow/...
}

package_grafana-agent() {
  backup=('etc/grafana-agent.yaml')

  cd "agent-$pkgver"

  install -Dm755 agent "$pkgdir/usr/bin/grafana-agent"
  install -Dm644 packaging/grafana-agent.yaml "$pkgdir/etc/grafana-agent.yaml"

  install -Dm644 "$srcdir/grafana-agent.sysusers" "$pkgdir/usr/lib/sysusers.d/grafana-agent.conf"
  install -Dm644 "$srcdir/grafana-agent.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/grafana-agent.conf"
  install -Dm644 "$srcdir/grafana-agent.service" "$pkgdir/usr/lib/systemd/system/grafana-agent.service"
}

package_grafana-agentctl() {
  pkgdesc="provides utilities for interacting with Grafana Agent"

  cd "agent-$pkgver"

  install -Dm755 -t "$pkgdir"/usr/bin agentctl
}

package_grafana-agent-crow() {
  pkgdesc="A correctness checker tool that validates that scraped metrics are delivered to a remote_write endpoint."

  cd "agent-$pkgver"

  install -Dm755 crow "$pkgdir"/usr/bin/grafana-agent-crow
}
