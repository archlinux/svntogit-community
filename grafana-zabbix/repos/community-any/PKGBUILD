# Maintainer: Florian Pritz <bluewind@xinu.at>
pkgname=grafana-zabbix
pkgver=4.1.3
pkgrel=1
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('any')
url="https://github.com/alexanderzobnin/grafana-zabbix"
license=('APACHE')
depends=('grafana')
makedepends=(yarn python2 libfaketime go git) # node-sass depends on node-gyp 3.8.0 which is still python2 https://github.com/sass/node-sass/issues/2877
source=("$pkgname-$pkgver.tar.gz::https://github.com/alexanderzobnin/grafana-zabbix/archive/v$pkgver.tar.gz")
sha256sums=('79f464ca181004b44be4a6bdc6a48efa20556406634c8c131acc3abdcb88a306')

build() {
	cd "$pkgname-$pkgver"
	make install
	make build
	make dist
}

check() {
	cd "$pkgname-$pkgver"
	# Force UTC timezone so that tests pass, even after a DST change
	PATH="$PATH:/build/go/bin" TZ=UTC make test lint
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -r dist/* "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
