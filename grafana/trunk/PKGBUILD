# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>
# Contributor: Gilles Hamel <hamelg at laposte dot net>

pkgname=grafana
pkgver=4.2.0
pkgrel=1
pkgdesc='Gorgeous metric viz, dashboards & editors for Graphite, InfluxDB & OpenTSDB'
url='http://grafana.org'
arch=('x86_64' 'i686')
license=('Apache')
depends=('glibc' 'freetype2' 'fontconfig')
makedepends=('git' 'go' 'npm' 'grunt-cli')
backup=("etc/grafana.ini")
source=("git+https://github.com/grafana/grafana.git#tag=v$pkgver"
        "grafana.service"
        "grafana.sysusers"
        "grafana.tmpfiles"
       )
install=$pkgname.install
md5sums=('SKIP'
         '2f1d73689923ecee92a102a65b91003a'
         '49688ed04bb3fac45fd48d0ac99e52d5'
         'a23177c9e555a7d8ede65c86cf21add2')

prepare() {
  cd $pkgname
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
}

build() {
  msg2 'GOPATH setup'
  export GOPATH="$srcdir/gopath"
  export PATH+=":$GOPATH/bin"
  mkdir -p "$GOPATH/src/github.com/grafana/"
  ln -fsrT "$srcdir/grafana/" "$GOPATH/src/github.com/grafana/grafana"
  cd "$GOPATH/src/github.com/grafana/grafana"

  msg2 'building the backend'
  go run build.go setup build

  msg2 'building the frontend'
  export NPM_CONFIG_PREFIX="$srcdir/npm"
  export PATH+=":$NPM_CONFIG_PREFIX/bin"
  npm install -g yarn
  yarn install --pure-lockfile
  grunt release
}

package() {
  install -Dm644 grafana.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/grafana.conf"
  install -Dm644 grafana.sysusers "$pkgdir/usr/lib/sysusers.d/grafana.conf"
  install -Dm644 grafana.service "$pkgdir/usr/lib/systemd/system/grafana.service"
  cd $pkgname
  install -Dsm755 bin/grafana-server "$pkgdir/usr/bin/grafana-server"
  install -Dsm755 bin/grafana-cli "$pkgdir/usr/bin/grafana-cli"
  install -Dm644 conf/sample.ini "$pkgdir/etc/$pkgname.ini"
  install -Dm644 conf/defaults.ini "$pkgdir/usr/share/$pkgname/conf/defaults.ini"
  install -dm755 "$pkgdir/usr/share/grafana/"
  cp -r vendor "$pkgdir/usr/share/grafana/"
  cp -r public_gen "$pkgdir/usr/share/grafana/public"
  ln -s ../../../var/lib/grafana "$pkgdir/usr/share/grafana/data"
}

# vim:set ts=2 sw=2 et:
