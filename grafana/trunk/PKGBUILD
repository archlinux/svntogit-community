# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>
# Contributor: Gilles Hamel <hamelg at laposte dot net>

pkgname=grafana
pkgver=5.2.2
pkgrel=1
pkgdesc='Gorgeous metric viz, dashboards & editors for Graphite, InfluxDB & OpenTSDB'
url='http://grafana.org'
arch=('x86_64')
license=('Apache')
depends=('glibc' 'freetype2' 'fontconfig')
makedepends=('git' 'go' 'npm' 'grunt-cli' 'python2')
backup=("etc/grafana.ini")
source=("git+https://github.com/grafana/grafana.git#tag=v$pkgver"
        "grafana.service"
        "grafana.sysusers"
        "grafana.tmpfiles"
       )
install=$pkgname.install
md5sums=('SKIP'
         '2f1d73689923ecee92a102a65b91003a'
         'e991d8cdf91c1e25652cdbc51fc726c5'
         'b9b576d1959fc063b3399892e201488c')

prepare() {
  cd $pkgname
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
}

build() {
  msg2 'GOPATH setup'
  export GOPATH="$srcdir/gopath"
  export PATH+=":$GOPATH/bin"
  mkdir -p "$GOPATH/src/github.com/grafana/"
  ln -fsrT "$srcdir/grafana/" "$GOPATH/src/github.com/grafana/grafana"
  cd "$GOPATH/src/github.com/grafana/grafana"

  msg2 'building the backend'
  go run build.go setup
  go run build.go build

  msg2 'building the frontend'
  export NPM_CONFIG_PREFIX="$srcdir/npm"
  export PATH+=":$NPM_CONFIG_PREFIX/bin"
  npm install -g yarn
  yarn install --pure-lockfile --no-progress
  npm run build release
}

package() {
  install -Dm644 grafana.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/grafana.conf"
  install -Dm644 grafana.sysusers "$pkgdir/usr/lib/sysusers.d/grafana.conf"
  install -Dm644 grafana.service "$pkgdir/usr/lib/systemd/system/grafana.service"
  cd $pkgname
  install -Dsm755 bin/linux-amd64/grafana-server "$pkgdir/usr/bin/grafana-server"
  install -Dsm755 bin/linux-amd64/grafana-cli "$pkgdir/usr/bin/grafana-cli"
  install -Dm640 -o207 -g207 conf/sample.ini "$pkgdir/etc/$pkgname.ini"
  install -Dm644 conf/defaults.ini "$pkgdir/usr/share/$pkgname/conf/defaults.ini"
  install -dm755 "$pkgdir/usr/share/grafana/"
  for i in vendor public tools; do
    cp -r "$i" "$pkgdir/usr/share/grafana/$i"
  done
  ln -s ../../../var/lib/grafana "$pkgdir/usr/share/grafana/data"
}

# vim:set ts=2 sw=2 et:
