# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Simon Parzer <simon.parzer@gmail.com>
pkgname=grafx2
pkgver=2.3
pkgrel=1
pkgdesc="Pixelart-oriented painting program (for pixling)"
arch=('x86_64' 'i686')
url="http://code.google.com/p/grafx2/"
license=('GPL2')
install=grafx2.install
depends=('sdl_ttf' 'sdl_image' 'lua' 'desktop-file-utils')
source=("http://grafx2.googlecode.com/files/${pkgname}-${pkgver}.1781-src.tgz"
        "${pkgname}.desktop")
md5sums=('916a35c4762e85b4210a1041fbbfd830'
         '73d1cb00b7529de5cbab5c8f00640fbc')

build() {
  cd "${srcdir}/${pkgname}/src"

  msg2 "Compiling..."
  make
}

package() {
  cd "${srcdir}/${pkgname}/src"

  msg2 "Packaging files..."
  make prefix="/usr" DESTDIR="${pkgdir}" install
  msg2 "Packaging shortcut..."
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  msg2 "Packaging documentation..."
  install -Dm644 "${srcdir}/${pkgname}/doc/README.txt" \
    "${pkgdir}/usr/share/doc/${pkgname}/readme.txt"
  msg2 "Packaging license..."
  install -Dm644 "${srcdir}/${pkgname}/doc/gpl-2.0.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  msg2 "Cleaning up..."
  rmdir ${pkgdir}/usr/share/grafx2/scripts/libs    
}

# vim:set ts=2 sw=2 et:
