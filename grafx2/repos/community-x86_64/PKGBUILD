# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.4
pkgrel=7
_pkgrev=2035
pkgdesc='Pixelart-oriented painting program'
arch=('x86_64' 'i686')
url='http://pulkomandy.tk/projects/GrafX2'
license=('GPL2')
install="$pkgname.install"
depends=('sdl_ttf' 'sdl_image' 'lua' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('gendesk')
options=('!emptydirs')
source=("http://pulkomandy.tk/projects/GrafX2/downloads/$pkgname-$pkgver.$_pkgrev-src.tgz")
sha256sums=('5818caea63cd3cf3318baf01dd20f81ec61e4514443000c80a41b68ab3f17a6b')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name 'GrafX2' \
    --genericname 'Drawing program' --exec 'grafx2 %U' \
    --mimetypes 'image/bmp;image/gif;image/png;image/x-tga;image/tiff' \
    --categories 'Graphics;2DGraphics;RasterGraphics'
}

build() {
  make -C "$pkgname/src"
}

package() {
  make -C "$pkgname/src" prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname/doc/README.txt" \
    "$pkgdir/usr/share/doc/$pkgname/readme.txt"
}

# vim:set ts=2 sw=2 et:
