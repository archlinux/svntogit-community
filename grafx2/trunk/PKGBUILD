# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.4
pkgrel=9
pkgdesc='Pixelart-oriented painting program'
arch=('x86_64' 'i686')
url='http://pulkomandy.tk/projects/GrafX2'
license=('GPL2')
depends=('sdl_ttf' 'sdl_image' 'lua' 'fontconfig')
makedepends=('gendesk' 'fontconfig')
options=('!emptydirs')
# Upstream is down right now
#source=("http://pulkomandy.tk/projects/GrafX2/downloads/$pkgname-$pkgver.2035-src.tgz")
source=("https://github.com/xyproto/grafx2/archive/master.zip")
sha256sums=('7da73307dbce425af842cdeb774335474acc51ac7c05ec85eaaef17728d9a8a5')

prepare() {
  mv "${pkgname}-master" "$pkgname"
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --name 'GrafX2' \
    --genericname 'Drawing program' --exec 'grafx2 %f' \
    --mimetypes 'image/bmp;image/gif;image/png;image/x-tga;image/tiff' \
    --categories 'Graphics;2DGraphics;RasterGraphics'
}

build() {
  make -C "$pkgname/src"
}

package() {
  make -C "$pkgname/src" prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname/doc/README.txt" \
    "$pkgdir/usr/share/doc/$pkgname/readme.txt"
}

# vim:set ts=2 sw=2 et:
