# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=granite
pkgver=0.3.0
pkgrel=1
pkgdesc='An extension of GTK+ libraries'
arch=('i686' 'x86_64')
url='https://launchpad.net/granite'
license=('LGPL3')
depends=('gtk3' 'libgee')
makedepends=('cmake' 'gobject-introspection' 'vala')
install="${pkgname}.install"
source=("${url}/${pkgver%.?}/${pkgver%.?}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('df950dd79749213971d50316c68edadf1fb3c2151cb7d63f6a6b9edf12084ad1')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
