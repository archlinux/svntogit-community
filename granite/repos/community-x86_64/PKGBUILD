# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=granite
pkgver=0.2.2
pkgrel=3
pkgdesc="An extension of GTK+ libraries"
arch=('i686' 'x86_64')
url="https://launchpad.net/granite"
license=('LGPL3')
depends=('gtk3' 'libgee06')
makedepends=('cmake' 'gobject-introspection' 'vala')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname}/${pkgver%\.?}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('341f94cd8c2af60fc072fb8602e1406bccd77189aad76424b172c3ce08c173cc')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
