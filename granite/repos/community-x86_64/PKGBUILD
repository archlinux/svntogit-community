# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=granite
pkgver=0.2.3.1
pkgrel=1
pkgdesc='An extension of GTK+ libraries'
arch=('i686' 'x86_64')
url='https://launchpad.net/granite'
license=('LGPL3')
depends=('gtk3' 'libgee06')
makedepends=('cmake' 'gobject-introspection' 'vala')
install="${pkgname}.install"
source=("${url}/${pkgver%.?.?}/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('56f05338bb7967f0b912d35bf780a3ddd68b517e1939371a14a5f79581a8f80d')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
