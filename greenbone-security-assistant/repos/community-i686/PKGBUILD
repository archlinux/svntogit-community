# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=greenbone-security-assistant
pkgver=5.0.2
pkgrel=2
pkgdesc='Greenbone Security Assistant (gsa) - OpenVAS web frontend'
url='http://www.openvas.org/'
license=(GPL)
arch=('i686' 'x86_64')
depends=('libmicrohttpd' 'openvas-libraries')
makedepends=('cmake' 'doxygen' 'libxslt' 'xmltoman')
groups=(openvas)
backup=(etc/openvas/gsad_log.conf)
# This URL needs to be manually updated on every pkgver upgrade.
# http://www.openvas.org/install-source.html
source=("http://wald.intevation.org/frs/download.php/1694/$pkgname-$pkgver.tar.gz")
md5sums=('d05d6cdb4555d9aea12abbd67e651e0d')
conflicts=(gsa)
provides=(gsa)
replaces=(gsa)

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
