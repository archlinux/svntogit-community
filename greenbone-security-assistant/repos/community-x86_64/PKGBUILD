# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=greenbone-security-assistant
pkgver=6.0.4
pkgrel=1
pkgdesc='Greenbone Security Assistant (gsa) - OpenVAS web frontend'
url='http://www.openvas.org/'
license=(GPL)
arch=('i686' 'x86_64')
depends=('libmicrohttpd' 'openvas-libraries' 'libxslt')
makedepends=('cmake' 'doxygen' 'xmltoman')
groups=(openvas)
backup=(etc/openvas/gsad_log.conf)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2137/$pkgname-$pkgver.tar.gz"
        "http://wald.intevation.org/frs/download.php/2138/$pkgname-$pkgver.tar.gz.asc"
        glib-include.patch)
md5sums=('fb3577daad10abad36c957fcf72ff87d'
         'SKIP'
         '75b6fa16ba92cc92546f1947831a1706')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)
conflicts=(gsa)
provides=(gsa)
replaces=(gsa)

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../glib-include.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var . -DSBINDIR=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
