# $Id$
# Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Daniel Micay <danielmicay@gmail.com>
pkgname=greenbone-security-assistant
pkgver=7.0.2
pkgrel=1
pkgdesc='Greenbone Security Assistant (gsa) - OpenVAS web frontend'
url='http://www.openvas.org/'
license=(GPL)
arch=('x86_64')
depends=('libmicrohttpd' 'openvas-libraries' 'libxslt')
makedepends=('cmake' 'doxygen' 'xmltoman')
groups=(openvas)
backup=(etc/openvas/gsad_log.conf)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2429/greenbone-security-assistant-7.0.2.tar.gz"
        "http://wald.intevation.org/frs/download.php/2430/greenbone-security-assistant-7.0.2.tar.gz.sig")
sha512sums=('e3ccc8704845d8271a8628a19f7a79aea6c19f2cd52f4c69ee698b314c3b8de1099831394c8b474fa79fb1bba4dc5c42e88351edeca82e1c1b4f46f625f746ae'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)
conflicts=(gsa)
provides=(gsa)
replaces=(gsa)

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var . -DSBINDIR=/usr/bin
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
