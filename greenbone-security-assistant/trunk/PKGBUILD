# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=greenbone-security-assistant
pkgver=5.0.3
pkgrel=1
pkgdesc='Greenbone Security Assistant (gsa) - OpenVAS web frontend'
url='http://www.openvas.org/'
license=(GPL)
arch=('i686' 'x86_64')
depends=('libmicrohttpd' 'openvas-libraries')
makedepends=('cmake' 'doxygen' 'libxslt' 'xmltoman')
groups=(openvas)
backup=(etc/openvas/gsad_log.conf)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1734/$pkgname-$pkgver.tar.gz"
        "http://wald.intevation.org/frs/download.php/1735/$pkgname-$pkgver.tar.gz.asc")
md5sums=('d1148f21265346ee6257895918d8dedb'
         'SKIP')
conflicts=(gsa)
provides=(gsa)
replaces=(gsa)

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
