# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Pawel Rusinek <p.rusinek@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=griffith
pkgver=0.11
pkgrel=4
pkgdesc="Movie collection manager application"
arch=('any')
url="http://griffith.berlios.de"
license=('GPL2')
depends=('pygtk>=2.14.1' 'sqlite3' 'python-pysqlite' \
         'python-reportlab>=2.3' 'pil' 'pyxml' 'python-sqlalchemy')
optdepends=('python-psycopg2: postgreSQL support'
	    'mysql-python: MySQL support'
	    'python-chardet: encoding detection of imported CSV files'
	    'gnome-python-extras: spell checking support'
	    'griffith-extra-artwork: additional icons')
source=(http://launchpad.net/griffith/trunk/${pkgver}/+download/griffith-${pkgver}.tar.gz)
md5sums=('f7ba19baf9cf1a553b7a4f0692f79430')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

    # python2 fix
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' griffith
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	# The program creates a wrong symlink so make a new one
	rm ${pkgdir}/usr/bin/griffith
	ln -s /usr/share/griffith/lib/griffith ${pkgdir}/usr/bin/griffith 
}
