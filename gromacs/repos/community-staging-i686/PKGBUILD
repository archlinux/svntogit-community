# $Id$
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Ricardo <rikardo.horo@gmail.com>

pkgname=gromacs
pkgver=4.0.7
pkgrel=2
pkgdesc='A fast Molecular Dynamics program based on the GROMOS force field'
url='http://www.gromacs.org/'
license=("GPL")
arch=('i686' 'x86_64')
depends=('fftw' 'lesstif' 'perl' 'libxml2' 'libsm' 'libx11')
options=(!libtool)
install=${pkgname}.install
source=("ftp://ftp.gromacs.org/pub/gromacs/${pkgname}-${pkgver}.tar.gz")
md5sums=('51369bd553e6bba4085a58bcf23d337d')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --enable-shared

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/gromacs/shell-specific
  mv ${pkgdir}/usr/bin/GMXRC* ${pkgdir}/usr/share/gromacs/shell-specific
  mv ${pkgdir}/usr/bin/completion* ${pkgdir}/usr/share/gromacs/shell-specific
}
