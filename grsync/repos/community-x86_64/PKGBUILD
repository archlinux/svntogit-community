# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=grsync
pkgver=1.2.8
pkgrel=1
pkgdesc="GTK+ GUI for rsync to synchronize folders, files and make backups"
arch=('x86_64')
url="http://www.opbyte.it/grsync/"
license=('GPL')
depends=('gtk3' 'rsync')
makedepends=('intltool')
source=(http://www.opbyte.it/release/$pkgname-$pkgver.tar.gz
        grsync.appdata.xml
        grsync-1.2.6-gcc-10.patch
        gtk3-port.patch
        use-themed-icon.patch)
sha256sums=('94ea5faca67e3df467b5283377af3cb32b2b47631b6a32d38bc7b371209306b1'
            '5dee994cafbcf8adfda34767cbd60f7eb2039bdeccab2dfd879f045bd11fb9fd'
            '898d3a4d50158409f55cf87ff0977edfff6bc3e69868f3d4d5f1037d9923fc03'
            '90c721af2039590e8caaa3ae31bc81981485936bf2452e3e17101772ca8705ef'
            '490108a9b65750162786f9aa230668736502477ce260ad1948f9d0124d5fb207')

prepare() {
  cd $pkgname-$pkgver
  
  # Fix build with GCC10
  patch -Np1 -i ../grsync-1.2.6-gcc-10.patch

  # Port to GTK+ 3
  patch -Np1 -i ../gtk3-port.patch

  # Use themed icon
  patch -Np1 -i ../use-themed-icon.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-unity
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
}
