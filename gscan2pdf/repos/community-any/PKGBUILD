# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Tristelune  <tristelune at archlinux dot info>
# Contributor: LeCrayonVert <greenarrow at archlinux dot us>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Sven Salzwedel <sven_salzwedel@web.de>
# Contributor: Olaf Leidinger <leidola@newcon.de>

pkgname=gscan2pdf
pkgver=2.5.1
pkgrel=1
pkgdesc='A GUI with OCR capability to produce PDFs or DjVus from scanned documents'
arch=('any')
url='http://gscan2pdf.sourceforge.net/'
license=('GPL')
depends=('imagemagick' 'perl-config-general' 'perl-date-calc' 'perl-goocanvas2'
         'perl-gtk3-simplelist' 'perl-data-uuid'
         'perl-filesys-df' 'perl-html-parser' 'perl-list-moreutils'
         'perl-locale-gettext' 'perl-log-log4perl' 'perl-pdf-api2'
         'perl-proc-processtable' 'perl-readonly' 'perl-sane' 'perl-set-intspan'
         'perl-try-tiny' 'unpaper')
optdepends=('djvulibre: DjVu image format'
            'gocr: OCR support'
            'ocropy: OCR support'
            'xdg-utils: email as PDF'
            'tesseract: OCR support'
            'cuneiform: OCR support'
            'perl-gtk2-ex-podviewer: view inline documentation')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e5f1e20d1dd1db062dee6ee30b99d4ce53d7496f231bf5d15aa4c0625e39245f')
options=('!emptydirs')

build() {
  cd "${pkgname}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  mv "${pkgdir}/usr/bin/vendor_perl/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
