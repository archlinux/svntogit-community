# $Id$
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Achraf cherti <achrafcherti@gmail.com>

pkgname=gshutdown
pkgver=0.2
pkgrel=5
pkgdesc="An advanced shutdown utility"
arch=('i686' 'x86_64')
url="http://gshutdown.tuxfamily.org"
license=('GPL')
depends=('libglade' 'libnotify')
makedepends=('pkg-config')
source=(http://gshutdown.tuxfamily.org/release/$pkgname-$pkgver.tar.gz
	build-fix.patch)
md5sums=('c0662f6b74fa1be10e4d34bfc5731d9b'
         '0b3ac3c62c5899814e136cc62f7636f7')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <$srcdir/build-fix.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LDFLAGS="-lX11 -lXau" ./configure --prefix=/usr --mandir=/usr/share
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
