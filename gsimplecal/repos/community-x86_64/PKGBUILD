# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: DrZaius <lou at fakeoutdoorsman dot com>
# Contributor: Victor Feight <vrfeight3 at gmail dot com>

pkgname=gsimplecal
pkgver=2.4.1
pkgrel=1
pkgdesc="Simple and lightweight GTK calendar"
arch=('x86_64')
url="https://dmedvinsky.github.io/gsimplecal/"
license=('BSD')
depends=('gtk3')
source=($pkgname-$pkgver.tar.gz::https://github.com/dmedvinsky/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('7d9fd4730982b7d2762b55815523cab1')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
