# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Lee.MaRS <leemars@gmail.com>

pkgname=gsoap
pkgver=2.8.5
pkgrel=1
pkgdesc="Offers an XML language binding to ease the development of SOAP/XML Web services in C and C/C++"
url="http://www.cs.fsu.edu/~engelen/soap.html"
arch=('i686' 'x86_64')
license=('GPL' 'custom')
depends=('openssl' 'zlib' 'gcc-libs')
makedepends=('autoconf' 'automake')
source=(http://prdownloads.sourceforge.net/gsoap2/${pkgname}_${pkgver}.zip
        LICENSE)
md5sums=('541dcff86d1c6171cb8540cf51a4cfa8'
         '27aaa3f5166db94d44044c11a7b2c37b')

build() {
  cd $srcdir/gsoap-2.8
  [ -f Makefile ] || CXXFLAGS=-fPIC CFLAGS=-fPIC LDFLAGS=-fPIC ./configure --prefix=/usr
  make -j1
}

package() {
  cd $srcdir/gsoap-2.8
  make DESTDIR=$pkgdir install
  mkdir -p $pkgdir/usr/share/gsoap/import
  mkdir -p $pkgdir/usr/share/gsoap/WS
  cp -R gsoap/import $pkgdir/usr/share/gsoap/
  cp -R gsoap/WS $pkgdir/usr/share/gsoap/
  install -D ../LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
