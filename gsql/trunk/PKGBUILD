# $Id$
# Contributor: Michael Kanis <mkanis@gmx.de>

pkgname=gsql
pkgver=0.2.2
pkgrel=3
pkgdesc="Integrated database development tool for GNOME"
url="http://gsql.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gtk2>=2.12' 'gtksourceview2' 'vte' 'libgnomeui' 'gconf' 'desktop-file-utils')
makedepends=('libmysqlclient' 'postgresql-libs' 'libssh')
optdepends=('libmysqlclient' 'postgresql-libs' 'libssh')
install=gsql.install
options=(!libtool)
source=(http://gsql.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2)
md5sums=('8f3322a75390584729e84a8570e08bea')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/notify_notification_new (subj, message, NULL, NULL);/notify_notification_new (subj, message, NULL);/' libgsql/notify.c
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
