# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lubosz Sarnecki <lubosz@gmail.com>
# Contributor: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=gst-editing-services
_pkgname=gstreamer-editing-services
pkgver=1.14.3
pkgrel=1
pkgdesc='GStreamer library for creating audio/video editors'
arch=('x86_64')
license=('LGPL')
url='https://gstreamer.freedesktop.org/modules/gst-editing-services.html'
depends=('gst-plugins-base')
makedepends=('bash-completion' 'gobject-introspection' 'gtk-doc' 'python-gobject')
checkdepends=('gst-plugins-bad' 'gst-plugins-good')
source=("https://gstreamer.freedesktop.org/src/$_pkgname/$_pkgname-$pkgver.tar.xz"{,.asc})
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5' # Sebastian Dröge
              'D637032E45B8C6585B9456565D2EEE6F6F349D7C') # Tim-Philipp Müller
sha256sums=('bdb6be75acfaaac3528d30ad47e77eb443d6c55ddabd597c154cd9e88c9a37de'
            'SKIP')

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --enable-gtk-doc \
    --with-package-name="GStreamer (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
}

check() {
  cd $_pkgname-$pkgver
  # Broken on i686
  make check || :
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
