# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lubosz Sarnecki <lubosz@gmail.com>
# Contributor: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=gst-editing-services
pkgver=1.18.1
pkgrel=1
pkgdesc='GStreamer library for creating audio/video editors'
arch=('x86_64')
license=('LGPL')
url='https://gstreamer.freedesktop.org/modules/gst-editing-services.html'
depends=('gst-plugins-base')
makedepends=('bash-completion' 'gobject-introspection' 'gtk-doc' 'meson' 'python-gobject')
checkdepends=('gst-plugins-bad' 'gst-plugins-good')
source=("https://gstreamer.freedesktop.org/src/$pkgname/$pkgname-$pkgver.tar.xz"{,.asc})
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5' # Sebastian Dröge
              'D637032E45B8C6585B9456565D2EEE6F6F349D7C') # Tim-Philipp Müller
sha256sums=('e93cb3f1896565c57defa72758e49b6eb6685e0f804abd184f395c94822a3927'
            'SKIP')

build() {
  arch-meson $pkgname-$pkgver build \
    -D doc=disabled \
    -D validate=disabled \
    -D package-name="GStreamer Editing Services (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
