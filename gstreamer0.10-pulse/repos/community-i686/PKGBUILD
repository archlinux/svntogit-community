# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Thar <mailbox.tec@gmail.com>

pkgname=gstreamer0.10-pulse
pkgver=0.10.22
pkgrel=2
pkgdesc="A GStreamer 0.10 plugin for the PulseAudio sound server"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig')
url="http://gstreamer.freedesktop.org/"
depends=('gstreamer0.10-base>=0.10.29' 'pulseaudio>=0.9.8')
options=(!libtool !emptydirs)
groups=(pulseaudio-gnome)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2)
sha256sums=('89ba50e0e3b7355bd75c9b0a60866a67b9bd1de38458e540a57853998103d69f')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Good Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  cd ext/pulse && make
}

package() {
  cd "${srcdir}/gst-plugins-good-${pkgver}/ext/pulse"
  make DESTDIR="${pkgdir}" install
}
