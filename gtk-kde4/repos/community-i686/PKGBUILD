# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Viliam Pucik <viliam dot pucik at gmail dot com>

pkgname=gtk-kde4
pkgver=0.9.4
pkgrel=1
pkgdesc='Allows you to change style, icons, font of GTK applications in KDE4.'
arch=('i686' 'x86_64')
url='http://kde-look.org/content/show.php?content=74689'
license=('GPL')
depends=('kdebase-workspace' 'gtk-engines')
makedepends=('cmake' 'automoc4')
provides=('gtk-qt-engine')
source=("http://betta.houa.org/no-site/${pkgname}(src)${pkgver}.tar.gz"
        'fix-permission.patch')
md5sums=('ff16f21fe6c673fb1858808681c97350'
         '1e5e0f2b057e9288a88834442ea56e5a')

build() {
  # fix error with parens in folder name (unquoted) during build
  mv "${pkgname}(src)" ${pkgname}

  cd ${pkgname}

  patch -p1 -i ${srcdir}/fix-permission.patch
  
  cmake -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd ${pkgname}

  make DESTDIR=${pkgdir} install
}
