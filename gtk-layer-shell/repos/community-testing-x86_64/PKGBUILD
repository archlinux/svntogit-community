# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='Library to create Wayland desktop components using the Layer Shell protocol'
pkgname=gtk-layer-shell
pkgver=0.8.0
pkgrel=1
arch=(x86_64)
license=(LGPL3)
url="https://github.com/wmww/gtk-layer-shell"
depends=(
    "gtk3"
    "wayland"
)
makedepends=(
    "gtk-doc"
    "gobject-introspection"
    "meson"
    "ninja"
    "valabind"
)
options=(debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/wmww/gtk-layer-shell/archive/v$pkgver.tar.gz")
sha512sums=('710019b05785abc3cc8bc639a110f141dd2f37ce1d78e03ed361f1c21caee6c43817499fa05c8228f233bff3ac0b6f6123fa9014a87ae99edb6b10e4ff8f3dde')

build() {
    meson --prefix=/usr \
          --wrap-mode=nofallback \
          --buildtype=plain \
          -Dtests=true \
          -Ddocs=true \
          -Dintrospection=true \
          "$pkgname-$pkgver" \
          build
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
}
