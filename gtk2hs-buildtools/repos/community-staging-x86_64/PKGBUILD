# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=gtk2hs-buildtools
pkgver=0.12.3.1
pkgrel=4
pkgdesc="Tools to build the Gtk2Hs suite of User Interface libraries."
url="http://hackage.haskell.org/package/gtk2hs-buildtools"
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('ghc' 'alex' 'happy' 'haskell-random')
depends=('gmp')
options=('strip')
provides=('haskell-gtk2hs-buildtools')
source=(http://hackage.haskell.org/packages/archive/gtk2hs-buildtools/$pkgver/gtk2hs-buildtools-$pkgver.tar.gz
        '0001-fix-for-ghc-7.6.1-changes.patch')
md5sums=('612611d831635024c953d2817b124e29'
         'b6551d8085c97bc2c6600ab397833101')

build() {
    cd ${srcdir}/gtk2hs-buildtools-$pkgver

    patch -Np1 -i ${srcdir}/0001-fix-for-ghc-7.6.1-changes.patch

    runhaskell Setup configure --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
}

package() {
    cd ${srcdir}/gtk2hs-buildtools-$pkgver
    runhaskell Setup copy --destdir=${pkgdir}
}
