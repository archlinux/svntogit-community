# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

_hkgname=pango
pkgname=gtk2hs-pango
pkgver=0.12.0
pkgrel=4
pkgdesc="Binding to the pango library for Gtk2Hs."
url="http://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'pango' 'gtk2hs-glib' 'gtk2hs-cairo')
makedepends=('gtk2hs-buildtools=0.12.0')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
md5sums=('0260ed21d6ca2a52ebaa0aa212390cf7')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
    depends=("ghc=${_ghcver}" 'pango' 'gtk2hs-glib' 'gtk2hs-cairo')

    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
