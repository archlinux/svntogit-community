# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gtkspell3
pkgver=3.0.6
pkgrel=1
url="http://gtkspell.sourceforge.net/"
pkgdesc="Provides word-processor-style highlighting and replacement of misspelled words in a GtkTextView widget"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'enchant')
makedepends=('intltool' 'gobject-introspection' 'vala' 'python2')
source=(http://gtkspell.sourceforge.net/download/$pkgname-$pkgver.tar.gz)
md5sums=('cdc550a06dc424c8c497672bc54649db')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
