# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jared Casper <jaredcasper@gmail.com>

pkgname=gtkwave
pkgver=3.3.28
pkgrel=1
pkgdesc='A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files'
arch=('i686' 'x86_64')
url='http://gtkwave.sourceforge.net'
license=('GPL' 'MIT')
depends=('bzip2' 'xz' 'gtk2')
makedepends=('gperf')
install='gtkwave.install'
source=("http://gtkwave.sourceforge.net/gtkwave-${pkgver}.tar.gz")
md5sums=('396cc15cfc12801c9ad6af507ee4a0f1')

build() {
  cd "${srcdir}/gtkwave-${pkgver}"

  ./configure \
    --prefix=/usr \
    --disable-tcl

  make CFLAGS=-D_LARGEFILE64_SOURCE
}

package() {
  cd "${srcdir}/gtkwave-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/gtkwave-${pkgver}/.gtkwaverc" \
    "${pkgdir}/usr/share/gtkwave/sample.gtkwaverc"
  install -D -m644 "${srcdir}/gtkwave-${pkgver}/LICENSE.TXT" \
    "${pkgdir}/usr/share/licenses/gtkwave/LICENSE.TXT"
  install -D -m644 "${srcdir}/gtkwave-${pkgver}/MIT.TXT" \
    "${pkgdir}/usr/share/licenses/gtkwave/MIT.TXT"
}
