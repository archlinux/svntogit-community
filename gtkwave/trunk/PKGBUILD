# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jared Casper <jaredcasper@gmail.com>

pkgname=gtkwave
pkgver=3.3.21
pkgrel=1
pkgdesc='A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files'
arch=('i686' 'x86_64')
url='http://gtkwave.sourceforge.net'
license=('GPL' 'MIT')
depends=('gtk2')
makedepends=('gperf')
install="${pkgname}.install"
source=("http://gtkwave.sourceforge.net/${pkgname}-${pkgver}.tar.gz")
md5sums=('ae3a99a2304f45703471629c7b614ef6')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-tcl

  make CFLAGS=-D_LARGEFILE64_SOURCE
}

package() {
  cd ${pkgname}-${pkgver}

  make -j1 prefix=${pkgdir}/usr mandir=${pkgdir}/usr/share/man install

  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/.gtkwaverc \
    ${pkgdir}/usr/share/${pkgname}/sample.gtkwaverc
  install -D -m644 ${srcdir}/${pkgname}-${pkgver}/MIT.TXT \
    ${pkgdir}/usr/share/licenses/${pkgname}/MIT.TXT
}
