# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=2.91.7
pkgrel=3
pkgdesc='Enhanced gettext po file editor for the GNOME'
arch=('x86_64' 'i686')
url='http://projects.gnome.org/gtranslator/'
license=('GPL')
depends=('desktop-file-utils' 'gdl' 'gettext' 'gtksourceview3' 'gtkspell3' 'iso-codes'
         'libgda' 'libpeas' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'dconf')
makedepends=('intltool' 'itstool' 'gucharmap' 'json-glib' 'gnome-common' 'yelp-tools' 'gobject-introspection')
optdepends=('gucharmap: for charmap plugin'
            'json-glib: for open-tran plugin')
source=(http://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/2.91/$pkgname-$pkgver.tar.xz)
sha256sums=('2b25b45b33e4aa856953b4a5da1d98bd51a343ddb479aeae3934bfe0c3651f9d')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
