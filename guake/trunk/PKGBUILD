# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>

pkgname=guake
pkgver=0.5.0
pkgrel=1
pkgdesc='Drop-down terminal for GNOME'
arch=('i686' 'x86_64')
url="http://guake.org/"
license=('GPL')
depends=('python2-notify' 'vte' 'python2-gconf' 'python2-dbus' 'python2-xdg')
makedepends=('intltool' 'gnome-common')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/Guake/$pkgname/archive/$pkgver.tar.gz
        fix-crasher.patch
        fix-hotkey.patch)
md5sums=('4383b7fcf85a355b4b69dd5cf66ff78f'
         '12933c13aa1e7beb9431e3367cfaabd0'
         '77050aeecb837d8c8109ab272f7277e2')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix crasher - https://github.com/Guake/guake/issues/373
  patch -Np1 -i ../fix-crasher.patch

  # Fix frozen hotkey - https://github.com/Guake/guake/issues/378
  patch -Np1 -i ../fix-hotkey.patch

  # Fix GConf schema dir
  sed -i 's|schemadir = $(sysconfdir)/gconf/schemas|schemadir = $(GCONF_SCHEMA_FILE_DIR)|' data/Makefile.am

  # Python2 fix
  sed -i 's|/usr/bin/env python|/usr/bin/python2|' src/{guake-prefs,prefs.py}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --disable-schemas-install \
               --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
               PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
