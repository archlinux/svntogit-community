# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sial <sial@cpan.org>
# Contributor: Martin Herndl <martin.herndl@gmail.com>

pkgname=guayadeque
pkgver=0.3.7
pkgrel=2
pkgdesc='Lightweight music player'
arch=('i686' 'x86_64')
url='http://guayadeque.org/'
license=('GPL3')
depends=('curl' 'desktop-file-utils' 'libgpod' 'taglib' 'wxgtk2.8')
makedepends=('cmake')
optdepends=('gstreamer0.10-good-plugins: Support for additional file formats'
            'gvfs: Support for external devices')
install='guayadeque.install'
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'guayadeque-link.patch')
sha256sums=('f42f20cb8650b023a6b16f7953d3be9244004b2ce77b6a5101e8f8d9eab3b5fa'
            '9dea639f2464d2aceb0dd00786ba04a6c46e539d6eccc6468ab312f6de9321d4')

prepare() {
  cd guayadeque-${pkgver}

  patch -Np1 -i ../guayadeque-link.patch

  cd src

  xgettext -d guayadeque -o guayadeque.pot -C --keyword=_ *.cpp
  mv guayadeque.pot ../po
}

build() {
  cd guayadeque-${pkgver}

  if [[ -d build || -f build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8' \
    -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8'
  make
}

package() {
  cd guayadeque-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
