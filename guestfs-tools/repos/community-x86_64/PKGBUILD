# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=guestfs-tools
pkgver=1.48.2
pkgrel=2
pkgdesc="Tools for accessing and modifying guest disk images"
arch=(x86_64)
url="http://libguestfs.org/"
license=(GPL2 LGPL2.1)
depends=(gcc-libs glibc jansson openssl perl-libintl-perl)
makedepends=(bash-completion cdrtools libguestfs libisoburn libvirt libxml2 ncurses ocaml-findlib perl-module-build xz)
checkdepends=(linux rpm-tools)
backup=(
  etc/virt-builder/repos.d/libguestfs.conf
  etc/virt-builder/repos.d/libguestfs.gpg
  etc/virt-builder/repos.d/opensuse.conf
  etc/virt-builder/repos.d/opensuse.gpg
)
options=(debug)
source=(http://download.libguestfs.org/$pkgname/${pkgver%.*}-stable/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('e11cd5a38a1681e8b2fd56419d38ca0aaab659cb59d409b3572cadcd04fd4da3078fa33e7f21c99d11854ad08797ae24a8d71c041b1b7769fc36c1391b6eb162'
            'SKIP')
b2sums=('305f281c19975f160c435b995d71ef2e18d09fc95f31e92efe8f79d811fc0a7efab27d14f766d5f6ee477fd43977167a3aabe5cc40498f125f925507fd2574d5'
        'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0')  # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

check() {
  export LIBGUESTFS_DEBUG=1 LIBGUESTFS_TRACE=1  # verbose libguestfs output
  export SKIP_TEST_COMPLETE_IN_SCRIPT_SH=1  # can not find virt-builder
  export SKIP_TEST_VIRT_FILESYSTEMS_SH=1  # mismatch in test 2
  export SKIP_TEST_VIRT_INSPECTOR_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LUKS_ON_LVM_SH=1  # requires KVM
  export SKIP_TEST_VIRT_INSPECTOR_LVM_ON_LUKS_SH=1  # requires KVM
  export SKIP_TEST_VIRT_BUILDER_SH=1  # virt-resize: error: /dev/sda2: physical volume not returned by pvs_full

  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(
    libguestfs libguestfs.so
    libvirt libvirt.so
    libxml2 libxml2.so
    ncurses libncursesw.so
    pcre2 libpcre2-8.so
    xz liblzma.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
