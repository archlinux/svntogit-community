# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=gufw
pkgver=15.04.0
pkgrel=1
pkgdesc="An uncomplicated way to manage your Linux firewall."
arch=('any')
url="http://gufw.org"
license=('GPL3')
depends=('gtk3' 'polkit' 'python2-gobject' 'python2-netifaces' 'ufw' 'webkitgtk')
makedepends=('intltool' 'python2-distutils-extra')
source=("https://launchpad.net/gui-ufw/gufw-15.04/15.04/+download/gufw_${pkgver}.tar.gz")
md5sums=('0d794d7729b3effbb4d1549cbfb93f46')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/gui-ufw-${pkgver}"
    sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/env python2|" \
           -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
              $(find . -name '*.py')
    sed -i "s|python ${LOCATIONS[${i}]}|python2 ${LOCATIONS[${i}]}|" bin/gufw
    sed -i "s|python ${LOCATIONS[${i}]}|python2 ${LOCATIONS[${i}]}|" bin/gufw-pkexec
}

package(){
    cd "${srcdir}/gui-ufw-${pkgver}"
    export XDG_RUNTIME_DIR="/tmp/${pkgname}"
    python2 ./setup.py install --root="${pkgdir}"
}
