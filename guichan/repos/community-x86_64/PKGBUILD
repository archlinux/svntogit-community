# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Bj√∏rn Lindeijer <bjorn@lindeijer.nl>

pkgname=guichan
pkgver=0.8.2
pkgrel=2
pkgdesc="A portable C++ GUI library designed for games using Allegro, SDL and/or OpenGL."
arch=('i686' 'x86_64')
url="http://guichan.sourceforge.net/"
license=('BSD')
makedepends=('sdl_image' 'allegro' 'libgl' 'glut')
conflicts=('guichan-sdl' 'guichan-allegro' 'guichan-opengl' 'guichan-glut')
provides=('guichan-sdl' 'guichan-allegro' 'guichan-opengl' 'guichan-glut')
options=(!libtool !strip)
source=(http://guichan.googlecode.com/files/$pkgname-$pkgver.tar.gz)
md5sums=('af535d7f387e774e3197cef8023ea105')

build() {
    cd $srcdir/$pkgname-$pkgver
    CXXFLAGS="-g" ./configure --prefix=/usr
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make prefix=$pkgdir/usr install

    # Install the license, as required for BSD
    install -m644 -D COPYING $startdir/pkg/usr/share/licenses/$pkgname/LICENSE
}
