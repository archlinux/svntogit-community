# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=guitarix2
pkgver=0.28.3
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('i686' 'x86_64')
url="http://guitarix.sourceforge.net/"
license=('GPL')
depends=('gtkmm' 'jack' 'librsvg' 'liblrdf' 'bluez-libs' 'boost-libs'
         'zita-convolver' 'zita-resampler' 'desktop-file-utils')
makedepends=('python2' 'boost' 'intltool' 'lv2')
optdepends=('meterbridge: sound meters')
provides=('guitarix' 'gx_head')
conflicts=('guitarix' 'gx_head')
replaces=('guitarix' 'gx_head')
install="$pkgname.install"
source=("http://download.sourceforge.net/project/guitarix/guitarix/$pkgname-$pkgver.tar.bz2")
md5sums=('9b535512a07d5a48e67a5cec01ee6944')

build() {
  cd "$srcdir/guitarix-$pkgver"
  python2 waf configure --prefix=/usr \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --shared-lib \
                        --lib-dev
  python2 waf build
}

package() {
  cd "$srcdir/guitarix-$pkgver"
  python2 waf install --destdir="$pkgdir"
}
