# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

_name=guitarix
pkgname=guitarix2
pkgver=0.38.1
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('x86_64')
url="https://guitarix.org"
license=('GPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio')
depends=('bluez-libs' 'boost-libs' 'gtkmm' 'liblrdf' 'lilv' 'ttf-roboto' 'zita-convolver' 'zita-resampler')
makedepends=('python2' 'boost' 'eigen' 'gperf' 'intltool' 'lv2')
provides=('guitarix')
source=("https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}-${pkgver}.tar.xz")
sha512sums=('54d08b4ed20b9df4c69d0c86e4d668c53fffde8919f5570f376cee903926bd01945169d177d0d45976569ea55b28f8dca7884ac6dfe2f063df6583467559c5b7')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fixing all (hopefully) relevant scripts: https://sourceforge.net/p/guitarix/bugs/43/
  sed -e 's/python/&2/g' \
    -i src/{faust,LV2/*,plugins,ladspa,gx_head}/wscript \
    -i src/gx_head/builder/make \
    -i src/gx_head/engine/gen_tube_tables \
    -i tools/{check_rpc,gcov.py,convert-0.06-format,ampsim/DK/quik.py,undiag,make_jsonrpc_methods} \
    -i tools/{coverage,dsp2cc,dsp2insert}
}

build() {
  cd "${pkgname}-${pkgver}"
  # build without faust support: https://sourceforge.net/p/guitarix/bugs/44/
  python2 waf configure --prefix=/usr \
                        --enable-nls \
                        --ladspa \
                        --new-ladspa \
                        --faust \
                        --shared-lib \
                        --lib-dev \
                        --ldflags="${LDFLAGS}"
  python2 waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
