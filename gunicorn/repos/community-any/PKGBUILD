# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgname=gunicorn
pkgver=19.9.0
pkgrel=3
pkgdesc='WSGI HTTP Server for UNIX'
arch=('any')
url='https://gunicorn.org/'
license=('MIT')
depends=('python-setuptools')
optdepends=('python-eventlet: For asynchronous request handling with eventlet.'
            'python-gevent: For asynchronous request handling with gevent.')
checkdepends=('python-coverage' 'python-pytest' 'python-pytest-cov' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benoitc/$pkgbase/archive/$pkgver.tar.gz"
         gunicorn-pytest5.patch)
sha512sums=('8d1ff775dd5c1abb8bb4c80aecacb544382e38d101f105fd0fd7eb6bf98ecb663dd533d16e7b1741d6e5ef0eb12a707ab90e0a7751356e625725f076b4dc1a30'
            'c14fe17b247567ae096ffb1522b9dd424a2e29f3cabf2b4d8ae3b84bf17d9b1ad4abf25df2f2eed884fc931087879c6ce68c160f4f7f7074afb913cbe56546dc')

prepare() {
  (cd gunicorn-$pkgver
   patch -p1 -i ../gunicorn-pytest5.patch # Fix test failures with pytest 5
  )
  sed -e 's/==/>=/' -e 's/,<.*$//' -i gunicorn-$pkgver/requirements_test.txt
}

build() {
  cd "$srcdir"/gunicorn-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/gunicorn-$pkgver
  python setup.py pytest
}

package() {
  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  rm -r "$pkgdir"/usr/bin/gunicorn_paster
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
