# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgname=gunicorn
pkgver=20.0.2
pkgrel=1
pkgdesc='WSGI HTTP Server for UNIX'
arch=('any')
url='https://gunicorn.org/'
license=('MIT')
depends=('python-setuptools')
optdepends=('python-eventlet: For asynchronous request handling with eventlet.'
            'python-gevent: For asynchronous request handling with gevent.')
checkdepends=('python-aiohttp' 'python-coverage' 'python-pytest' 'python-pytest-cov'
              'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benoitc/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('d666d049c41494bf413bc53d61f5a557c2bb68334a7d142a129968b1b7c0ea6d1495a4cdd4aa0997e3bde05c62dfa1e339e9fe66bff9ba3eae95426b53c58d34')

prepare() {
  sed -e 's/==/>=/' -e 's/,<.*$//' -i gunicorn-$pkgver/requirements_test.txt
}

build() {
  cd gunicorn-$pkgver
  python setup.py build
}

check() {
  cd gunicorn-$pkgver
  python setup.py pytest
}

package() {
  cd gunicorn-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
