# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgbase=gunicorn
pkgname=(gunicorn python2-gunicorn)
pkgver=19.8.1
pkgrel=1
pkgdesc='WSGI HTTP Server for UNIX'
arch=('any')
url='http://gunicorn.org/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-mock' 'python-coverage' 'python2-coverage' 'python-pytest' 'python2-pytest'
              'python-pytest-cov' 'python2-pytest-cov' 'python-pytest-runner'
              'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/benoitc/$pkgbase/archive/$pkgver.tar.gz"
        pr1796-python37.patch)
sha512sums=('2fbdbc939c08fde2035d88780f97afdcdd9aada9267fefdd9c39e2c8e48f790077ea9e45e7149e93d2f97041d176af8470e0be400775ee775e50573f7de03148'
            'cae6af688ca47a3b56196e7894f320a360ed60a9542b499dd072218c79c6210852510e7698dd6e3cf7cea7220cba460e3c7c0c48faa3e36366246f2baf90c577')

prepare() {
  # https://github.com/benoitc/gunicorn/issues/1795
  patch -d gunicorn-$pkgver -Np1 < pr1796-python37.patch
  sed -e 's/==/>=/' -e 's/,<.*$//' -i gunicorn-$pkgver/requirements_test.txt
  cp -a gunicorn-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/gunicorn-$pkgver
  python setup.py build

  cd "$srcdir"/gunicorn-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/gunicorn-$pkgver
  python setup.py pytest

  cd "$srcdir"/gunicorn-$pkgver-py2
  python2 setup.py pytest
}

package_gunicorn() {
  depends=('python-setuptools')
  optdepends=('python-eventlet: For asynchronous request handling with eventlet.'
              'python-gevent: For asynchronous request handling with gevent.')

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  rm -r "$pkgdir"/usr/bin/gunicorn_paster
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gunicorn() {
  depends=('python2-setuptools')
  provides=('gunicorn-python2')
  conflicts=('gunicorn-python2')
  replaces=('gunicorn-python2')
  optdepends=('python2-eventlet: For asynchronous request handling with eventlet.'
              'python2-gevent: For asynchronous request handling with gevent.')

  cd $pkgbase-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  rm -r "$pkgdir"/usr/bin/gunicorn_paster
  mv "$pkgdir"/usr/bin/gunicorn "$pkgdir"/usr/bin/gunicorn-python2
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
