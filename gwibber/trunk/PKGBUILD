# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gwibber
pkgver=3.6.0
pkgrel=4
pkgdesc="Microblogging client for GNOME, which supports Facebook, Identi.ca, Twitter, Flickr, Foursquare, Sina and Sohu"
arch=('i686' 'x86_64')
url="http://gwibber.com/"
license=('GPL')
depends=('libgee06' 'account-plugins' 'libnotify' 'libsoup' 'dee' 'json-glib' 'gtkspell3' 'python2-dbus' 'python2-gobject' 'python2-gobject2' 'python2-httplib2' 'python2-oauth' 'python2-imaging' 'python2-xdg' 'dconf')
makedepends=('intltool' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        lp_934530.patch
        lp_1051801.patch
        lp_1088775.patch
        gtkspell3-port.patch)
md5sums=('5988e36e9f592c6eca6537ba6878307a'
         '8c43ae0ed96c52602befeff640a8672a'
         '0eb53f4f248a6c600b6c6dfaac12341f'
         '5698a3b1699c018719b193362486c9b5'
         'e8c657543542d8141fdfe4b87a24d7fe')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Apply some fixes from Ubuntu
  patch -Np1 -i "$srcdir/lp_934530.patch"
  patch -Np1 -i "$srcdir/lp_1051801.patch"
  patch -Np1 -i "$srcdir/lp_1088775.patch"

  # Port to the new gtkspell3
  patch -Np1 -i "$srcdir/gtkspell3-port.patch"

  # Use the standalone accounts configuration panel
  sed -i 's/"gnome-control-center", "credentials", "application=gwibber"/"credentials-preferences"/' client/gwibber-client.vala
  sed -i 's/"gnome-control-center", "credentials"/"credentials-preferences"/' gwibber/microblog/dispatcher.py

  # Remove unneeded dependency on libdbusmenu
  sed -i '/Dbusmenu-0.4/ d' client/Makefile.{am,in}

  autoreconf -fi
  intltoolize --force
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --disable-unity
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
