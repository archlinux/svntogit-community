# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Jaroslav Lichtblau <tu@dragonlord.cz>

pkgname=gxmessage
pkgver=3.4.3
pkgrel=2
pkgdesc="A GTK3 based xmessage clone."
arch=('i686' 'x86_64')
url="http://homepages.ihug.co.nz/~trmusson/programs.html#gxmessage"
license=('GPL')
depends=('gtk3' 'desktop-file-utils')
makedepends=('pkgconfig' 'intltool')
source=(http://homepages.ihug.co.nz/~trmusson/stuff/$pkgname-$pkgver.tar.gz
        $pkgname.desktop)
md5sums=('0e6b5b23ffccc785ff8627485f6b7ce4'
         '977bbafe46ff20082cbfb96175bfebec')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir/$pkgname.desktop" \
    "${pkgdir}/usr/share/applications/$pkgname.desktop"
}
