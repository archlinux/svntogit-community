# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=hackrf
pkgver=2014.04.1
pkgrel=1
pkgdesc="Driver for HackRF, allowing general purpose software defined radio (SDR)."
arch=('i686' 'x86_64')
url="https://github.com/mossmann/hackrf"
license=('GPLv2')
depends=('libusb')
makedepends=('cmake')

source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('bcdd6a9a99dfc5b1a277825d1be9e2ec')

build() {
  cd "$srcdir/$pkgname-$pkgver/host"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  pushd host/build
  make DESTDIR="$pkgdir" install
  popd
  pushd host/hackrf-tools
  install -vD -m644 52-hackrf.rules "$pkgdir/usr/lib/udev/rules.d/52-hackrf.rules"
  popd
  pushd firmware-bin/hackrf-one
  install -vD -m644 hackrf_usb_rom_to_ram.bin "$pkgdir/usr/share/hackrf/hackrf_usb_rom_to_ram.bin"
}
