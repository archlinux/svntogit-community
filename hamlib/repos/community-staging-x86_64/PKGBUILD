# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=hamlib
pkgver=3.3
pkgrel=13
pkgdesc='Ham radio equipment control libraries'
arch=('x86_64')
url='https://hamlib.github.io'
license=('GPL' 'LGPL')
depends=('perl' 'python' 'lua' 'tcl' 'libxml2' 'libusb-compat' 'libtool' 'libuhd' 'boost-libs')
makedepends=('swig' 'boost')
source=("https://github.com/Hamlib/Hamlib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        'hamlib-3.3-format-security.patch'
        'fix-lua-5.4.patch::https://github.com/Hamlib/Hamlib/commit/038b87b29913b5264c00eafaf06a3cc13dc16db7.patch')
sha512sums=('4cf6c94d0238c8a13aed09413b3f4a027c8ded07f8840cdb2b9d38b39b6395a4a88a8105257015345f6de0658ab8c60292d11a9de3e16a493e153637af630a80'
            'SKIP'
            'fea0ed27228de5177f1f849e237a517b32f3efd74efab4dfc858c75ae7339dacf942097016229faaa790396f6627160e0e4716dfb16eb496e91c5d8301134098'
            '364b443c49aa40f5c30015f8a020545f23f05ddd0b8c25c3ff22516068a1cc92259ddd4063c7de37c31dde95012202f688115720c1c4f3f0b9bbfc4f3165653e')
validpgpkeys=('82D64F6B0E67CD41F689BBA6FB2C5130D55A8819') # Nate Bargmann <n0nb@n0nb.us>

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|usrp|uhd|g' configure.ac
  sed -i 's|#include <usrp_standard.h>|#include <uhd.h>|' kit/usrp_impl.cc

  patch -p1 -i ../hamlib-3.3-format-security.patch
  patch -p1 -i ../fix-lua-5.4.patch

  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver

  ./configure \
  	--prefix=/usr \
  	--sbindir=/usr/bin \
  	--with-perl-binding \
  	--with-python-binding \
  	--with-tcl-binding \
  	--with-lua-binding \
  	--with-xml-support \
  	--disable-uhd  # https://github.com/Hamlib/Hamlib/issues/355

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
