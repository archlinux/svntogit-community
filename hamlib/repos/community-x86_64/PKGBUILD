# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=hamlib
pkgver=3.3
pkgrel=8
pkgdesc='Ham radio equipment control libraries'
arch=('x86_64')
url='https://hamlib.github.io'
license=('GPL' 'LGPL')
depends=('perl' 'python' 'lua' 'tcl' 'libxml2' 'libusb-compat' 'libtool' 'libuhd' 'boost-libs')
makedepends=('swig' 'boost')
source=("https://github.com/Hamlib/Hamlib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        'fix-lua-5.4.patch::https://github.com/Hamlib/Hamlib/commit/038b87b29913b5264c00eafaf06a3cc13dc16db7.patch')
sha512sums=('4cf6c94d0238c8a13aed09413b3f4a027c8ded07f8840cdb2b9d38b39b6395a4a88a8105257015345f6de0658ab8c60292d11a9de3e16a493e153637af630a80'
            'SKIP'
            'e16a83f0892084c3b8a56fdcff9bed6f471d242dd820dfa4daeefab262ecb18c2b487be55851e9337d2fe6ccf47ee2b64696d4aa7948fcf1d4ea80d1f2a40deb')
validpgpkeys=('82D64F6B0E67CD41F689BBA6FB2C5130D55A8819') # Nate Bargmann <n0nb@n0nb.us>

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|usrp|uhd|g' configure.ac
  sed -i 's|#include <usrp_standard.h>|#include <uhd.h>|' kit/usrp_impl.cc

  patch -p1 -i ../fix-lua-5.4.patch

  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver

  ./configure \
  	--prefix=/usr \
  	--sbindir=/usr/bin \
  	--with-perl-binding \
  	--with-python-binding \
  	--with-tcl-binding \
  	--with-lua-binding \
  	--with-xml-support \
  	--disable-uhd  # https://github.com/Hamlib/Hamlib/issues/355

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
