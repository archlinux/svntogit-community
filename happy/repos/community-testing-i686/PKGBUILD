# $Id$
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
 
pkgname=happy
pkgver=1.18.10
pkgrel=2
pkgdesc="The Parser Generator for Haskell"
url="http://hackage.haskell.org/package/happy"
arch=('i686' 'x86_64')
license=("custom:BSD3")
depends=('gmp')
makedepends=('ghc=7.6.2-1' 'haskell-mtl=2.1.2-2')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a1638e89434427e57f68ef311761dc30')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  runhaskell Setup.lhs configure -O --prefix=/usr
  runhaskell Setup.lhs build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  runhaskell Setup.lhs copy --destdir="${pkgdir}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
