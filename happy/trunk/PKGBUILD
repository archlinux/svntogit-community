# $Id$
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=happy
pkgver=1.19.5
pkgrel=4
pkgdesc="The Parser Generator for Haskell"
url="http://hackage.haskell.org/package/happy"
arch=('i686' 'x86_64')
license=("custom:BSD3")
depends=('gmp' 'libffi')
makedepends=('ghc=7.10.3' 'haskell-mtl=2.2.1')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('ffa56a1c312a60dfb1110afd25b28785')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  runhaskell Setup.lhs configure -O --prefix=/usr
  runhaskell Setup.lhs build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  runhaskell Setup.lhs copy --destdir="${pkgdir}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
