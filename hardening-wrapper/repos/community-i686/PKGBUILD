# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=hardening-wrapper
pkgver=6
pkgrel=1
pkgdesc='Wrapper scripts for building hardened executables by default'
arch=(i686 x86_64)
url='https://archlinux.org/'
license=('GPL')
depends=(bash)
backup=(etc/hardening-wrapper.conf)
source=(cc-wrapper.sh ld-wrapper.sh common.sh path.sh
        hardening-wrapper-i686.conf hardening-wrapper-x86_64.conf)
sha1sums=('793f885b61b96a23791d786e99a56c7b67c74fee'
          '1091ff9c65a60fa785ecb5b825db1ab6dfd310ff'
          '0c420f5323c0573d5c23c0ff8981025e2ba347ff'
          '1e5f6d9931f01b26bb4b6fbb839e21d34d534cdc'
          '4d7a8f4818c531ce7002e860e0654b42b6147037'
          '50db33c08439393b673c23d542e274beef44fbdd')

package() {
  install -Dm644 hardening-wrapper-${CARCH}.conf "$pkgdir/etc/hardening-wrapper.conf"
  install -Dm644 path.sh "$pkgdir/etc/profile.d/hardening-wrapper.sh"

  mkdir -p "$pkgdir/usr/lib/hardening-wrapper/bin"
  install -m755 {cc,ld}-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper"
  install -m644 common.sh "$pkgdir/usr/lib/hardening-wrapper"

  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/c89"
  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/c99"
  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/cc"
  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/c++"
  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/clang"
  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/clang++"
  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/gcc"
  ln -s ../cc-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/g++"

  ln -s ../ld-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/ld"
  ln -s ../ld-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/ld.bfd"
  ln -s ../ld-wrapper.sh "$pkgdir/usr/lib/hardening-wrapper/bin/ld.gold"
}
