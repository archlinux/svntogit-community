# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: prg <prg _at_ xannode _dot_ com>
# Contributor: Nico <d3sox at protonmail dot com>
# Contributor: Caltlgin Stsodaat <contact@fossdaily.xyz>

pkgname=haruna
pkgver=0.9.1
pkgrel=1
pkgdesc='Video player built with Qt/QML on top of libmpv'
arch=(x86_64)
url='https://apps.kde.org/haruna/'
license=(GPL3)
depends=(kfilemetadata kio mpv kirigami2)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('yt-dlp: YouTube support')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9131f2d3a44332ff65c15c77b243a9897cd7acc1d7fe77d75180bb519344840d'
            'SKIP')
validpgpkeys=(4E421C6554B89766DF9B7A37E12AB207C8755905) # George Florea Bănuș <georgefb899@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
