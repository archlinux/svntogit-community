# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps dot aur dot archlinux dot org>

pkgname=harvid
pkgver=0.8.3
pkgrel=2
pkgdesc="HTTP Ardour Video Daemon"
arch=('x86_64')
url="https://x42.github.io/harvid/"
license=('GPL2')
depends=('glibc' 'libpng')
makedepends=('ffmpeg' 'libjpeg-turbo' 'xxd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v$pkgver.tar.gz"
        "${pkgname}-0.8.3-fix_dep_detection.patch")
sha512sums=('748475602c7279f10790523cbd5cbf34c9cd283ca9e959ac0535fbb5b4ee6d2fcab976c5ddba3fa40862552b49d73df7145f36d26c9fe708063505812537a8ac'
            '3da826ec4cd9790c58535c85db0835221d8f61a92a42632262a109f0d3d8f794b54827ecfed17b52df86c8669d914e0aab54a46d9c043fb4bdd2b49b4d54cfe2')
b2sums=('b245166ab2244a7afdddabb463296974c0d995d70ad8af6e93f549f045245597cb4ca3abf47c608eea15089ac5e6f82972b789d9a3f268b8be98f3080fd6e5e7'
        'd5aa761ab59c5ad8d87a3721f96c02826572a697ae320c1b6fee53525d7bfbb722bf40bde46fb16afba0a09e4899a51bfd176d58568d0d7268c534f9bea82fde')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix issues with newer versions of make:
  # https://github.com/x42/harvid/issues/7
  patch -Np1 -i "../${pkgname}-0.8.3-fix_dep_detection.patch"
}
build() {
  cd "${pkgname}-${pkgver}"
  # different job setting needed to not crash build
  make -j1
}

package() {
  depends+=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'libjpeg.so'
  'libswscale.so')

  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}/" install

  # ffmpeg and ffprobe symlinks (for ardour)
  ln -sv ffmpeg "${pkgdir}/usr/bin/ffmpeg_${pkgname}"
  ln -sv ffprobe "${pkgdir}/usr/bin/ffprobe_${pkgname}"

  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
