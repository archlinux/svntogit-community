# $Id$
# Maintainer: speps <speps dot aur dot archlinux dot org>

pkgname=harvid
pkgver=0.8.1
pkgrel=2
pkgdesc="HTTP Ardour Video Daemon"
arch=('x86_64')
url="http://x42.github.io/harvid/"
license=('GPL')
depends=('ffmpeg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/harvid/archive/v$pkgver.tar.gz")
md5sums=('a4ba437cc1895a5a47702f0188981ea2')

build() {
  cd $pkgname-$pkgver

  # prevent GNU executable stack
  CFLAGS+=" -Wl,-znoexecstack"
  make PREFIX=/usr -j1
}

package() {
  cd $pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir/" install

  # ffmpeg and ffprobe symlinks (for ardour)
  ln -s ffmpeg "$pkgdir/usr/bin/ffmpeg_harvid"
  ln -s ffprobe "$pkgdir/usr/bin/ffprobe_harvid"

  # doc
  install -Dm644 README.md \
    "$pkgdir/usr/share/doc/$pkgname/README"
}
