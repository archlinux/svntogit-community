# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

pkgname=hashcat
pkgver=3.6.0
pkgrel=1
epoch=1
pkgdesc='Multithreaded advanced password recovery utility'
url='https://hashcat.net/hashcat'
arch=('i686' 'x86_64')
depends=('opencl-icd-loader')
optdepends=('libxnvctrl: NVIDIA X driver configuration support')
makedepends=('opencl-headers')
license=('MIT')
source=(https://hashcat.net/files/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('7842b6d742cd1c83f136b0ae13cdd4c6991618654360b5f67376c6d08e93888f4d958360aac8b0c8bbe96a6cbe032971fbc2ec7fa6d58794aa50381e0191a636'
            'SKIP')
validpgpkeys=('A70833229D040B4199CC00523C17DA8B8A16544F') # Hashcat signing key <signing@hashcat.net>

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr SHARED=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr SHARED=1 install
  install -Dm 644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
