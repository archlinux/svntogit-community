# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=aeson-compat
pkgname=haskell-aeson-compat
pkgver=0.3.6
pkgrel=18
pkgdesc="Compatibility layer for aeson"
url="https://hackage.haskell.org/package/${_hkgname}"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-attoparsec" "haskell-base-compat" "haskell-exceptions"
         "haskell-hashable" "haskell-scientific" "haskell-semigroups" "haskell-text"
         "haskell-tagged" "haskell-time-locale-compat" "haskell-unordered-containers"
         "haskell-vector")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('7aa365d9f44f708f25c939489528836aa10b411e0a3e630c8c2888670874d142')

prepare() {
    # nats is an empty package in new GHC
    sed -e '/nats/d' \
        -e 's/, vector                   >=0.10 && <0.12/, vector                   >=0.10 \&\& <0.13/' \
        -e 's/, aeson                    >=0.7.0.6 && <1.1/, aeson                    >=0.7.0.6 \&\& <1.3/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
