# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=aeson-yaml
pkgname=haskell-aeson-yaml
pkgver=1.1.0.0
pkgrel=196
pkgdesc="Output any Aeson value as YAML (pure Haskell library)"
url="https://github.com/clovyr/aeson-yaml"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-string-qq' 'haskell-tasty' 'haskell-tasty-discover'
             'haskell-tasty-hunit' 'haskell-yaml')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ab7c48371906221fb4c99bbe230a6ada8e912744c012a2e90cf9187b3bf5083ee371664bb56861813d0a454eb6ed6344cb0df29ed5797cc888a2728ac4b1f8ac')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
