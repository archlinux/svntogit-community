# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_hkgname=alsa-core
pkgname=haskell-${_hkgname}
pkgver=0.5.0.1
pkgrel=13
pkgdesc='Binding to the ALSA Library API (Exceptions)'
url='http://www.haskell.org/haskellwiki/ALSA'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'glibc' 'gmp' 'alsa-lib' 'haskell-base' 'haskell-extensible-exceptions')
makedepends=('ghc')
source=(https://hackage.haskell.org/package/${_hkgname}-${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha256sums=('eb50495ef05ecc7c06a0147da7f0d3efde832a44d23caaf5172dc114882270ab')
sha512sums=('e02c8353911bf2c0c703400701ae72be6f510f771ff56dfc09322cbcf3a006f4cd0903f71f7ed2268886b3468113d5ce76933eedf5fad47aa71cb052d07fc4b4')

build() {
  cd ${_hkgname}-${pkgver}
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
        --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd ${_hkgname}-${pkgver}
  runhaskell Setup test
}

package() {
  cd ${_hkgname}-${pkgver}
  install -Dm 744 register.sh "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm 755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
  runhaskell Setup copy --destdir="$pkgdir"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
}

# vim: ts=2 sw=2 et:
