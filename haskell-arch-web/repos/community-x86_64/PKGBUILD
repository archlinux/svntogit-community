# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=arch-web
pkgname=haskell-arch-web
pkgver=0.1.0
pkgrel=161
pkgdesc="Arch Linux official and AUR web interface binding"
url="https://github.com/berberman/arch-web"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-deriving-aeson' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-lens' 'haskell-servant'
         'haskell-servant-client' 'haskell-servant-client-core')
makedepends=('ghc' 'uusi' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('ffb66a4bee5432799e75f4c637856ae49868b2633e3711b3468a8d78b9263ef2')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u http-client -u lens $_hkgname.cabal
  gen-setup

  sed -i '/CommunityTesting/a \  | KDEUnstable' src/Web/ArchLinux/Types.hs
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
