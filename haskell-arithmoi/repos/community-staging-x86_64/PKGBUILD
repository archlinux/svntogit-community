# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=arithmoi
pkgname=haskell-arithmoi
pkgver=0.12.0.2
pkgrel=43
pkgdesc="Efficient basic number-theoretic functions."
url="https://github.com/Bodigrim/arithmoi"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-chimera' 'haskell-constraints' 'haskell-exact-pi'
         'haskell-integer-logarithms' 'haskell-integer-roots' 'haskell-mod' 'haskell-random'
         'haskell-semirings' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-quickcheck-classes' 'haskell-smallcheck'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-tasty-rerun'
             'haskell-tasty-smallcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('bc8c9a3bfb1a543e70db112ef61942c71e41692f6b81cbf6f48a2fc4f4555c836bed0f163564c2c34efc9eba2e88727bad9fceae5a3bf39f2240e6ec63b783ff')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
