# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=atomic-write
pkgname=haskell-atomic-write
pkgver=0.2.0.7
pkgrel=42
pkgdesc="Atomically write to a file"
url="https://github.com/stackbuilders/atomic-write"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-temporary' 'haskell-unix-compat')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('3405437b801fbdd542c5c8a403f603ccb65ceaa63beca15062c429eaea412d7006342eb237982c9fe6751ffd160f810197b30fec3e49528e51bae1fc7f47af2e')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
