# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=attoparsec-iso8601
pkgname=haskell-attoparsec-iso8601
pkgver=1.0.0.0
pkgrel=43
pkgdesc="Parsing of ISO 8601 dates, originally from aeson"
url="https://github.com/bos/aeson"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-compat')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d4b545204429a73516eefa4ff1f174fb238544ca76258950275f146865873d2ebf00705dfdd16c17897ad3b10516aa2111eaaa27290e33009d033d227fbe6699')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<.*0.10/<1/' $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-developer -f-fast
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
