# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=aws
pkgname=haskell-aws
pkgver=0.18
pkgrel=9
pkgdesc="Amazon Web Services (AWS) for Haskell"
url="https://github.com/aristidb/aws"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-base64-bytestring' 'haskell-blaze-builder' 'haskell-byteable'
         'haskell-case-insensitive' 'haskell-cereal' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-cryptonite' 'haskell-data-default' 'haskell-http-conduit' 'haskell-http-types'
         'haskell-lifted-base' 'haskell-memory' 'haskell-monad-control' 'haskell-mtl'
         'haskell-network' 'haskell-old-locale' 'haskell-resourcet' 'haskell-safe'
         'haskell-scientific' 'haskell-tagged' 'haskell-text' 'haskell-unordered-containers'
         'haskell-utf8-string' 'haskell-vector' 'haskell-xml-conduit')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-conduit-combinators' 'haskell-errors'
             'haskell-http-client' 'haskell-http-client-tls' 'haskell-quickcheck-instances'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-transformers-base')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('11cf6675e4caf818e39efe697704acff7c5e0a74641482b791de17b18f6e0224be1bd24fb734a419685ca4b2cbf5913bea0012f4f79e4b988ee4cbc9252d0f52')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-examples
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Requires AWS credentials to test"
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
