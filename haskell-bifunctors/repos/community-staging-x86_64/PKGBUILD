# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=bifunctors
pkgname=haskell-bifunctors
pkgver=5.5.2
pkgrel=25
pkgdesc="Bifunctors"
url="https://github.com/ekmett/bifunctors/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-comonad' 'haskell-tagged'
         'haskell-th-abstraction' 'haskell-transformers-compat')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1e9eee61a25b6e0dafb3de200dfbc11b6b82a9691065aa8bfa4ad8a04e040cd395220724f209f0adca018fdc6d0b3fad7232c2f490a9a6e51c6181c0802a9de2')

prepare() {
    cd $_hkgname-$pkgver
    sed -e 's/<.*2.13/<3/' \
        -e 's/<.*0.6/<1/' \
        -i $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -ftagged -f-semigroups
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
