# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=binary-instances
pkgname=haskell-binary-instances
pkgver=1.0.3
pkgrel=33
pkgdesc="Orphan instances for binary"
url="https://github.com/phadej/binary-instances"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-binary-orphans' 'haskell-case-insensitive'
         'haskell-hashable' 'haskell-primitive' 'haskell-scientific' 'haskell-tagged'
         'haskell-text-binary' 'haskell-time-compat' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-vector-binary-instances')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('edb74f0382c36b2b7b1cdd03472ec8ce370dfa073eaae03a17975d347cc20c242115164d1865d7fcf031fdd646583386ec4f9688946e990ea1762a11518c943a')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u tagged $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/phadej/binary-instances/issues/7
    runhaskell Setup test || echo "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
