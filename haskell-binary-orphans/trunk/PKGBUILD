# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=binary-orphans
pkgname=haskell-binary-orphans
pkgver=0.1.8.0
pkgrel=101
pkgdesc="Orphan instances for binary"
url="https://github.com/phadej/binary-orphans"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-case-insensitive" "haskell-hashable" "haskell-scientific"
         "haskell-tagged" "haskell-unordered-containers" "haskell-vector"
         "haskell-text-binary" "haskell-vector-binary-instances")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('78bfef3338cbb8f30e72d7b70695240fd70e812e4f4c006af0bf84674320a055262c7b516dece417e84ac46d920eb68f10665d7d136f819e6e202de32124c6a2')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*4.11/<5/' -e 's/<.*1.3/<2/' -e 's/<.*0.8/<1/' -e 's/< *1.2.1/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
