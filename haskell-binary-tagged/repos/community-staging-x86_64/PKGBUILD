# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=binary-tagged
pkgname=haskell-binary-tagged
pkgver=0.3
pkgrel=55
pkgdesc="Tagged binary serialisation."
url="https://github.com/phadej/binary-tagged#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base16-bytestring' 'haskell-structured' 'haskell-tagged')
makedepends=('ghc' 'uusi' 'haskell-binary-instances' 'haskell-quickcheck' 'haskell-quickcheck-instances'
             'haskell-singleton-bool' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('731f94d53eabd0a00c1c116836a5e2eb9543911561b99f91c85df32534b73d7a441f25fdb5e98d2b6cba5523ceeaa112b5671ca8196535dc35c391943cb58b5e')

prepare() {
    uusi -u base16-bytestring $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
