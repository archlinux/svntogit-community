# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=blaze-html
pkgname=haskell-blaze-html
pkgver=0.9.1.2
pkgrel=44
pkgdesc="A blazingly fast HTML combinator library for Haskell"
url="https://jaspervdj.be/blaze"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-blaze-markup')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('26e5294285033fa453ab9914a5bdb37c42f5b7c3a10400be1ff6dd125a70499d556c106cc8128154dc0c455c8b80b489a883265fcb626e2d4afcd8e4d94374bc')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *2.14/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runghc -isrc src/Util/GenerateHtmlCombinators.hs
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
