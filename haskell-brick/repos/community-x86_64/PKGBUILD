# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=brick
pkgname=haskell-brick
pkgver=0.41.2
pkgrel=10
pkgdesc="A declarative terminal user interface library"
url="https://github.com/jtdaugherty/brick"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-vty' 'haskell-data-clist' 'haskell-dlist' 'haskell-microlens'
         'haskell-microlens-th' 'haskell-microlens-mtl' 'haskell-config-ini' 'haskell-vector'
         'haskell-contravariant' 'haskell-text-zipper'
         'haskell-word-wrap')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a96a4a71d1654966e49265d34065caaaf09c9fc53088d9c0c2430659c57765a2ff5e1595e48d4df023ef1d90892228fbed8217740ae6cfd016900b36fa2284c3')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/< *0.6/<1/' -e 's/<= *4.11/<5/' $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-demos
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
