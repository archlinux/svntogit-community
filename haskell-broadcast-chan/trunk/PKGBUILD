# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=broadcast-chan
pkgname=haskell-broadcast-chan
pkgver=0.2.1
pkgrel=7
pkgdesc="Closable, fair, single-wakeup channel type that avoids 0 reader space leaks"
url="https://github.com/merijn/broadcast-chan"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-unliftio-core')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('25e5f8bcc2c27d0cf21ca205f234048df64d6d881f24a48169b88ce198db525f12ea727962f7bf14e4ee6546e23b49c6b47aa1a44a3f3e7d6028d2bf12a01d1c')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *0.2/<1/;s/< *4.14/<5/' $_hkgname.cabal
    sed -i 's/, UnliftIO/, askUnliftIO, UnliftIO/' BroadcastChan.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
