# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-cairo
pkgver=0.12.4
pkgrel=2
pkgdesc="Binding to the cairo library for Gtk2Hs."
url="http://hackage.haskell.org/package/cairo"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'cairo' 'haskell-mtl')
makedepends=("gtk2hs-buildtools")
options=('strip')
conflicts=('gtk2hs-cairo')
provides=('gtk2hs-cairo')
replaces=('gtk2hs-cairo')
install=gtk2hs-cairo.install
source=(http://hackage.haskell.org/packages/archive/cairo/$pkgver/cairo-$pkgver.tar.gz)
md5sums=('b9d7e8de6ba4db680c222958622d6ee6')

build() {
    cd ${srcdir}/cairo-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-cairo \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    depends=("ghc=`pacman -Q ghc | cut -f2 -d\ |cut -f1 -d-`" 'cairo' "haskell-mtl=`pacman -Q haskell-mtl | cut -f2 -d\ |cut -f1 -d-`")

    cd ${srcdir}/cairo-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-cairo/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-cairo/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-cairo/html ${pkgdir}/usr/share/doc/ghc/html/libraries/cairo
    runhaskell Setup copy --destdir=${pkgdir}
}
