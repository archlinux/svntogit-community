# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-cairo
pkgver=0.13.3.0
pkgrel=1
pkgdesc="Binding to the cairo library for Gtk2Hs."
url="http://hackage.haskell.org/package/cairo"
license=('LGPL2.1')
arch=('i686' 'x86_64')
# deps adjusted to specific versions in package()
depends=('ghc' 'cairo' 'haskell-mtl' 'haskell-utf8-string' 'haskell-text')
makedepends=("haskell-gtk2hs-buildtools")
conflicts=('gtk2hs-cairo')
provides=('gtk2hs-cairo')
replaces=('gtk2hs-cairo')
source=(http://hackage.haskell.org/packages/archive/cairo/$pkgver/cairo-$pkgver.tar.gz)
md5sums=('54d2490e0adb1ae45d6c798a59c120a0')

build() {
    cd "$srcdir"/cairo-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-cairo \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

_update_deps() {
    _ver=`pacman -Q $1 | cut -f2 -d\  | cut -f1 -d-`
    for i in `seq 0 $(expr ${#depends[@]} - 1)`; do
        if [ ${depends[$i]} == $1 ]; then
            depends[$i]="$1=${_ver}"
        fi
    done
}

package() {
    _update_deps ghc
    _update_deps haskell-mtl

    cd "$srcdir"/cairo-${pkgver}
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "$pkgdir"/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-cairo/html "$pkgdir"/usr/share/doc/ghc/html/libraries/cairo
    runhaskell Setup copy --destdir="$pkgdir"
}
