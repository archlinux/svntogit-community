# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cassava-megaparsec
pkgname=haskell-cassava-megaparsec
pkgver=2.0.3
pkgrel=6
pkgdesc="Megaparsec parser of CSV files that plays nicely with Cassava"
url="https://github.com/stackbuilders/cassava-megaparsec"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-cassava' 'haskell-megaparsec' 'haskell-unordered-containers'
         'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-megaparsec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e5d3a278afc230a1feabcaaea1162f8ff1ebdf4c1e6c604a09021da8cf799f754cf4f2cfd49a0e3ebcf363ae41541d9fb168f080aad6d20970998dbb9da6d6c5')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
