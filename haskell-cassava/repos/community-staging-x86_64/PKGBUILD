# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=cassava
pkgname=haskell-cassava
pkgver=0.5.2.0
pkgrel=51
pkgdesc="A CSV parsing and encoding library"
url="https://github.com/tibbe/cassava"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-attoparsec" "haskell-hashable" "haskell-only" "haskell-scientific"
         "haskell-text-short" "haskell-unordered-containers" "haskell-vector")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f7f3fc2ebf58491847fb20b21ecf18595933d296bfd4c48249d35c52e72988c4e2758d7a6d2095a9edb3bed8e154a11170920a7c08262270c9f60cb853085383')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *4.14/<5/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-bytestring--LT-0_10_4
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
