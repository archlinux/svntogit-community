# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cborg
pkgname=haskell-cborg
pkgver=0.2.2.1
pkgrel=14
pkgdesc="Concise Binary Object Representation (CBOR)"
url="https://github.com/well-typed/cborg"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-half' 'haskell-primitive')
makedepends=('ghc' 'haskell-aeson' 'haskell-base-orphans' 'haskell-base16-bytestring'
             'haskell-base64-bytestring' 'haskell-quickcheck' 'haskell-scientific' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('afa2eead3255769f6bc487182482c79cb9d1f264484112035aa5942255f6a381801e3bbb73d5414951ba561c2d1a844e3a692c063fead41eef11e2b6ea1180ab')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *2.14/<3/;s/< *0.6/<1/;s/< *1.1/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.txt
}
