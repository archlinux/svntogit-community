# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=cheapskate
pkgname=haskell-cheapskate
pkgver=0.1.1.1
pkgrel=113
pkgdesc="Experimental markdown processor."
url="https://github.com/jgm/cheapskate"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-blaze-html" "haskell-data-default" "haskell-http-types"
         "haskell-syb" "haskell-uniplate" "haskell-wai"
         "haskell-wai-extra" "haskell-xss-sanitize")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        cheapskate-base-4.13.patch::https://github.com/jgm/cheapskate/commit/74c308a9f145c00c7d0f41dcf90aac56d8614c65.patch)
sha512sums=('551fdf43f8ed8e15ed4a5ac1649f319477cc80762a34499eed58c7c77e1f3e2bc0c4f9887aac7632f341e825c202850617d0572fa6cb748104bbf2874dfba353'
            '051b925e45648545f05c86717efcc52f0565f21f8a4fd653b2d177b681daf3525c460da9733c3cd054cbd3f7ff0f2ff5525dcbe3621306d5335eaa7cdabe3cc7')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../cheapskate-base-4.13.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fdingus
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
