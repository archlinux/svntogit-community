# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=checkers
pkgname=haskell-checkers
pkgver=0.4.10
pkgrel=19
pkgdesc="Check properties on standard classes and data structures"
url="https://github.com/conal/checkers"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-random' 'haskell-quickcheck' 'haskell-semigroupoids')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b2884143505e8025d436d6792dded2df74b81070d9313f9f5f864a2b630479cd344fa00181a38bcce100b970d5a561b02509728bf26c73ad9ba0222c4be429be')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
