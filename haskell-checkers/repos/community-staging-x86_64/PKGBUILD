# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=checkers
pkgname=haskell-checkers
pkgver=0.4.14
pkgrel=5
pkgdesc="Check properties on standard classes and data structures"
url="https://github.com/conal/checkers"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-random' 'haskell-quickcheck' 'haskell-semigroupoids')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('cdbbeecd4d88f7d4b1b3ebffe1c521e7342c76b5710aebd341d51672a8621609428a51410b8d7183e5905cce32b9eb9db45ff9d4a86da0e3cc9fed50297bfa9f')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
