# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=checkers
pkgname=haskell-checkers
pkgver=0.5.2
pkgrel=17
pkgdesc="Check properties on standard classes and data structures"
url="https://github.com/conal/checkers"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-random' 'haskell-quickcheck' 'haskell-semigroupoids')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('f642401f4c60cc142d8b3213113f8ea5de07fdb158affe4fa6a54021b3c9a63581ff113c27d47a174a9e70d05594399d33597a990096d024880cc8999d54f776')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
