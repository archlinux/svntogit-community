# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=checkers
pkgname=haskell-checkers
pkgver=0.5.5
pkgrel=6
pkgdesc="Check properties on standard classes and data structures"
url="https://github.com/conal/checkers"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-random' 'haskell-quickcheck' 'haskell-semigroupoids')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        checkers-ghc-8.10.patch::https://github.com/conal/checkers/pull/45.patch)
sha512sums=('162d40396e1cdc58e2c9c12174289141d7cbc4fa3d9a67273e8a6aa72cf0273023d33fa915ca33d2a0324016defbd4530cddfaf495e8ded66c50d6faf1c92e23'
            'f1969cfc598ed712de3326bba53f33a08df017b8eae3eb8dc4eb31f5834340db8d60b70e46278823b55817c8ddc246afcc05ea9e2a6cf75d072f3c378eb10124')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../checkers-ghc-8.10.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
