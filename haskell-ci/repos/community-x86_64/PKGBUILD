# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_hkgname=haskell-ci
pkgname=haskell-ci
pkgver=0.12.1
pkgrel=356
pkgdesc="Cabal package script generator for Travis-CI"
url="https://haskell-ci.rtfd.org/"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base-compat'
         'haskell-base16-bytestring' 'haskell-cabal-install-parsers' 'haskell-cryptohash-sha256'
         'haskell-generic-lens-lite' 'haskell-hsyaml' 'haskell-indexed-traversable'
         'haskell-indexed-traversable-instances' 'haskell-ini' 'haskell-lattices'
         'haskell-network-uri' 'haskell-optparse-applicative' 'shellcheck' 'haskell-temporary'
         'haskell-unordered-containers' 'haskell-zinza')
makedepends=('ghc' 'uusi' 'haskell-diff' 'haskell-ansi-terminal' 'haskell-tasty' 'haskell-tasty-golden')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        haskell-ci-aeson-2.patch)
sha512sums=('d93a6b8e880255469b34b568320659802252379f0c5486ed315b366fd07dae935a480047468912bc5a397b9e55b00ea964451790f963d05b8df1d426d871da1c'
            '6f96010eec7add9740b5c760c76a9dad01e391ebd503c52c93b0126fee7d48e29e3ffd30c387908b0341ebaf20b34caafbd2d291b2f5e0a22b086f0402784497')

prepare(){
  cd $_hkgname-$pkgver
  patch -p1 -i ../haskell-ci-aeson-2.patch
  uusi -u aeson -u attoparsec -u base-compat -u optparse-applicative -u ShellCheck $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
