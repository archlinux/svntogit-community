# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-prelude
pkgname=haskell-clash-prelude
pkgver=1.2.0
pkgrel=15
pkgdesc="CAES Language for Synchronous Hardware - Prelude library"
url="https://github.com/clash-lang/clash-prelude"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-constraints' 'haskell-data-binary-ieee754'
         'haskell-data-default-class' 'haskell-ghc-typelits-extra' 'haskell-ghc-typelits-knownnat'
         'haskell-ghc-typelits-natnormalise' 'haskell-half' 'haskell-hashable' 'haskell-lens'
         'haskell-quickcheck' 'haskell-recursion-schemes' 'haskell-reflection' 'haskell-singletons'
         'haskell-th-abstraction' 'haskell-th-lift' 'haskell-text-show' 'haskell-th-orphans'
         'haskell-type-errors' 'haskell-vector')
makedepends=('ghc' 'haskell-doctest' 'haskell-hint' 'haskell-quickcheck-classes-base'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('11b3686c8ca25d4ab65ae72622de9bfd2240900db88059a6cfa442667dc0c3619bab2f9cdd61b25ac58b11cdc8c0424620de4e3337fc0b24b5c4406b5b56c6c9')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/< *0.4/<1/' -e 's/< *2.14/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # TODO
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
