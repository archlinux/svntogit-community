# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_hkgname=classy-prelude
pkgname=haskell-classy-prelude
pkgver=1.5.0
pkgrel=186
pkgdesc='A typeclass-based Prelude'
arch=(x86_64)
url="https://hackage.haskell.org/package/$_hkgname"
license=(MIT)
depends=(ghc-libs haskell-async haskell-chunked-data haskell-dlist
         haskell-hashable haskell-mono-traversable haskell-mtl haskell-primitive
         haskell-unliftio haskell-stm-chans
         haskell-unordered-containers haskell-vector-instances
         haskell-say haskell-mutable-containers haskell-basic-prelude
         haskell-mono-traversable-instances)
checkdepends=(haskell-hspec)
makedepends=(ghc)
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('eb8afb8a8988d0b251f6b3c2046f3ad943201d35379bfcd21f024afa67031c1b43677f43b22a0dd88e5e586a669bf5cb0b7348e13f40bbef624d09a3c6c62dfd')

prepare() {
  cd $_hkgname-$pkgver
  sed -i '/semigroups/d' classy-prelude.cabal
}

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
