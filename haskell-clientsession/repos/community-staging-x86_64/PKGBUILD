# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=clientsession
pkgname=haskell-clientsession
pkgver=0.9.1.2
pkgrel=164
pkgdesc="Securely store session data in a client-side cookie."
url="https://github.com/yesodweb/clientsession/tree/master"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-base64-bytestring" "haskell-cereal"
         "haskell-cipher-aes" "haskell-cprng-aes" "haskell-crypto-api"
         "haskell-crypto-random" "haskell-entropy" "haskell-setenv"
         "haskell-skein" "haskell-tagged")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('27b44924843363f86b7c27aec1a73e8d74fcf42075cbe45ded4f26d690eb086bac0a4091e66bbdcb431c090aed0ddb64384ca6c555b8ba4c7372f1c42117587a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-test
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
