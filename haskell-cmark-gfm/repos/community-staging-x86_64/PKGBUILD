# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=cmark-gfm
pkgname=haskell-cmark-gfm
pkgver=0.1.3
pkgrel=3
pkgdesc="Fast, accurate GitHub Flavored Markdown parser and renderer"
url="https://github.com/kivikakk/cmark-gfm-hs"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-text') # 'cmark-gfm'
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c12b533c377a7433e78acb39d0ec743e6c2b341f25e6c6722cfb543297b2a4c936c8d2dd5ef878e931bdb829b4a556ef630854c3dad442b5f464e91fbe6ed9c0')

prepare() {
    # Disabled system cmark-gfm completely because the header files are unusable: https://github.com/github/cmark/issues/62
    # Cabal errors due to the strange version https://github.com/haskell/cabal/issues/4860
    # sed -i 's/pkgconfig-depends: libcmark-gfm/Extra-Libraries: cmark-gfm/' $_hkgname-$pkgver/$_hkgname.cabal
    :
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-pkgconfig
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
