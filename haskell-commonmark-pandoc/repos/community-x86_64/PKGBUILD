# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=commonmark-pandoc
pkgname=haskell-commonmark-pandoc
pkgver=0.2.1.3
pkgrel=8
pkgdesc="Bridge between commonmark and pandoc AST"
url="https://github.com/jgm/commonmark-hs"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-commonmark' 'haskell-commonmark-extensions' 'haskell-pandoc-types')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d5d91748501cdb4fcb738f89ee32135eed83ff1a41a3665f86ad36286a346b4751a355cd81d29e43a47500913b94796330df9b35201586d2610dbdb4ef4fa544')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
