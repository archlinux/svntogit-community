# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=conduit-combinators
pkgname=haskell-conduit-combinators
pkgver=1.1.2
pkgrel=5
pkgdesc="Commonly used conduit functions, for both chunked and unchunked data"
url="https://github.com/snoyberg/mono-traversable"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base16-bytestring' 'haskell-base64-bytestring' 'haskell-chunked-data'
         'haskell-conduit' 'haskell-conduit-extra' 'haskell-monad-control'
         'haskell-mono-traversable' 'haskell-mwc-random' 'haskell-primitive' 'haskell-resourcet'
         'haskell-text' 'haskell-transformers-base' 'haskell-unix-compat' 'haskell-vector'
         'haskell-void')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-mtl' 'haskell-safe'
             'haskell-silently')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7ece12d0af700f2fe903f044857d0ae754bd205b427dc63f2e8749b18e2fbdbd3c1727df742618ef5019abb25ffbf2dec9ba59a6c2f996e0e502eb2f7a6499ba')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fmonotrav1
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
