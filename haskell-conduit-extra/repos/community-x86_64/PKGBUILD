# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=conduit-extra
pkgname=haskell-conduit-extra
pkgver=1.1.17
pkgrel=1
pkgdesc="Batteries included conduit: adapters for common libraries."
url="https://github.com/snoyberg/conduit"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-async" "haskell-attoparsec" "haskell-blaze-builder" "haskell-conduit"
         "haskell-exceptions" "haskell-monad-control" "haskell-network" "haskell-primitive"
         "haskell-resourcet" "haskell-stm" "haskell-streaming-commons" "haskell-text"
         "haskell-transformers-base")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('60fd7d6437fdfc8bc33e1daa1fd58f14930156c750bd6baaec5f33c37a2a053c3aac8a7dc9054bf18cdff7ff514f9572eac941dc2d11741edb9f02a9ce17cc17')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
