# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=conduit-parse
pkgname=haskell-conduit-parse
pkgver=0.2.1.0
pkgrel=1
pkgdesc="Parsing framework based on conduit"
url="https://github.com/k0ral/conduit-parse"
license=("custom:PublicDomain")
arch=('x86_64')
depends=('ghc-libs' 'haskell-conduit' 'haskell-dlist' 'haskell-parsers' 'haskell-safe'
         'haskell-safe-exceptions')
makedepends=('ghc' 'hlint' 'haskell-resourcet' 'haskell-tasty' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('b585dbdc0c1e3a844a9cd97cd1e72d7a73521b66b856001960afe4057130dae1')

build() {
  cd $_hkgname-$pkgver    

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fenable-hlint-test

  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
