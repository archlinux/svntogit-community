# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=connection
pkgname=haskell-connection
pkgver=0.3.0
pkgrel=12
pkgdesc="Simple and easy network connections API"
url="https://github.com/vincenthz/hs-connection"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-basement" "haskell-data-default-class" "haskell-network" "haskell-socks"
         "haskell-tls" "haskell-x509" "haskell-x509-store" "haskell-x509-system"
         "haskell-x509-validation")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2f59f2cdb97c9f8815230dbade0b4b07ce7f7543cd71cd63fadad843dd1856870a6402021778f456e8f8d8263688afe365cc67ba3ee3b0ceb27809f682e1f50a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
