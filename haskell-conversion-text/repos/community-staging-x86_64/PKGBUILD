# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=conversion-text
pkgname=haskell-conversion-text
pkgver=1.0.1.1
pkgrel=1
pkgdesc="Conversion instances for the text library"
url="https://github.com/nikita-volkov/conversion-text"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-prelude' 'haskell-conversion' 'haskell-conversion-bytestring')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ee5b0285516bc8af9dec104150ccda82e69901e87ebf9cfa9ade5aaa548a37b727a464176907eff8bcd8e1f3eb2de0e3140c2e244b4259a1a7aedb7946795ea5')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
