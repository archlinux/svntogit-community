# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=criterion
pkgname=haskell-criterion
pkgver=1.2.0.0
pkgrel=3
pkgdesc="Robust, reliable performance measurement and analysis"
url="http://www.serpentine.com/criterion"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-aeson" "haskell-ansi-wl-pprint" "haskell-cassava" "haskell-code-page"
         "haskell-exceptions" "haskell-glob" "haskell-js-flot" "haskell-js-jquery"
         "haskell-microstache" "haskell-mtl" "haskell-mwc-random" "haskell-optparse-applicative"
         "haskell-parsec" "haskell-statistics" "haskell-text" "haskell-transformers-compat"
         "haskell-vector" "haskell-vector-algorithms")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('29b5a4262b3431a6af2e726a3d867cbffde612d05c92011e977b30759f890bc190147e6170598a8ce45a3a1b761deaeed477e3cb0d51d5b83dd36dc68c18fe95')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-fast
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
