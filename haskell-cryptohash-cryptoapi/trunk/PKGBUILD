# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cryptohash-cryptoapi
pkgname=haskell-cryptohash-cryptoapi
pkgver=0.1.4
pkgrel=202
pkgdesc="Crypto-api interfaces for cryptohash"
url="https://github.com/vincenthz/hs-cryptohash-cryptoapi"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-memory' 'haskell-cryptonite' 'haskell-crypto-api' 'haskell-tagged'
         'haskell-cereal')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('f5add1c8922e682d5d08073d0a3d03c6d5545f38edd90821bad7a92f354203ca380ca7e302b4b259bb9e2ce6ae09b1a2428e981e2d745a4a1b1a5cff696621e2')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
