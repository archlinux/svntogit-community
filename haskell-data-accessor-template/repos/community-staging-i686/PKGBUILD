# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=data-accessor-template
pkgname=haskell-data-accessor-template
pkgver=0.2.1.12
pkgrel=2
pkgdesc="Automate generation of Accessor's of the data-accessor package by Template Haskell functions."
url="http://www.haskell.org/haskellwiki/Record_access"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-data-accessor" "haskell-utility-ht")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('07042d79a2f26892ef0b847a36a78f8fc217843a47dd8684d24ffbb7174e795e')

prepare() {
    sed -i 's/template-haskell >=2.4 && <2.11/template-haskell >=2.4 \&\& <2.12/' ${_hkgname}-${pkgver}/${_hkgname}.cabal
    sed -e 's/DataD _ _ params cons/DataD _ _ params Nothing cons/' \
        -e 's/NewtypeD _ _ params con/NewtypeD _ _ params Nothing con/' \
        -i ${_hkgname}-${pkgver}/src-5/Data/Accessor/Template.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
