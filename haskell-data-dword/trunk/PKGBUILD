# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=data-dword
pkgname=haskell-data-dword
pkgver=0.3.1.3
pkgrel=16
pkgdesc="Stick two binary words together to get a bigger one"
url="https://github.com/mvv/data-dword"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-bword' 'haskell-hashable')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('12651bc8d086e1873e05261c1a7ea807863f05ea35dd1d6b80a48d5f4750a4beee4f6eeedf1a79b290899174b93c8c6b2f32f4a02941e1de6dae236fea55239d')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
