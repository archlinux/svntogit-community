# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=data-textual
pkgname=haskell-data-textual
pkgver=0.3.0.2
pkgrel=69
pkgdesc="Human-friendly textual representations."
url="https://github.com/mvv/data-textual"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-parsers' 'haskell-text-latin1' 'haskell-text-printer')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2a3bda491f27b1a279d833182bb3c3dfe76921321a9bb4746d53eda3fbab24976a28267d5d84762ac21d61be8b6ae24beeb8861160e51b412afb5180848be9ed')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
