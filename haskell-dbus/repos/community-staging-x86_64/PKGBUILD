# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=dbus
pkgname=haskell-dbus
pkgver=1.2.13
pkgrel=7
pkgdesc="A client library for the D-Bus IPC system"
url="https://john-millikin.com/software/haskell-dbus/"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-conduit' 'haskell-lens'
         'haskell-network' 'haskell-random' 'haskell-split' 'haskell-th-lift'
         'haskell-vector' 'haskell-xml-conduit' 'haskell-xml-types')
makedepends=('ghc' 'haskell-extra' 'haskell-quickcheck' 'haskell-resourcet' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('93f0ba057e9e7753aa11c201283c4e0b6fb8ef76dc063de772c0772616eadcc207f0aef6d059d17fc7412cfb6a9f7011608038bb76ac3ff2a398a678ec48d2c7')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *2.16/<3/' $_hkgname.cabal
    sed -i 's/TupE/mkTupE/' lib/DBus/Generation.hs
    sed -i '/type ClientBusPathR/i mkTupE :: [Exp] -> Exp\nmkTupE = TupE . map Just\n' lib/DBus/Generation.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
