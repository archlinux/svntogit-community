# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=deferred-folds
pkgname=haskell-deferred-folds
pkgver=0.9.7.1
pkgrel=2
pkgdesc="Abstractions over deferred folds"
url="https://github.com/metrix-ai/deferred-folds"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-foldl' 'haskell-hashable' 'haskell-primitive'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('678386fdcd08b5cd027b2533a460eb36cb55d3debd684bfd58447cd9fea1e11cb2db0038b7bdf6f7377afd2858db04607177444ee0ae23c7a98ef5d4dd963ff1')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
