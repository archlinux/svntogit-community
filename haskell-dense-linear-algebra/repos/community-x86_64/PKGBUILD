# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=dense-linear-algebra
pkgname=haskell-dense-linear-algebra
pkgver=0.1.0.0
pkgrel=236
pkgdesc="Simple and incomplete pure haskell implementation of linear algebra"
url="https://github.com/bos/statistics"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-math-functions' 'haskell-primitive' 'haskell-vector'
         'haskell-vector-algorithms' 'haskell-vector-th-unbox' 'haskell-vector-binary-instances')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('bcc70b42c3ea59aa3a739e2b189b9a1018d3dc27e65f5016502a2068005f480c296960ba7758a810cb22bfde53b5a597a66a3e1dd7bb732e54fe5daa1be08aa0')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
