# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=doctemplates
pkgname=haskell-doctemplates
pkgver=0.2.2.1
pkgrel=94
pkgdesc="Pandoc-style document templates"
url="https://github.com/jgm/doctemplates"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-blaze-markup" "haskell-blaze-html"
         "haskell-scientific" "haskell-unordered-containers" "haskell-vector")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e1ad0d61dc97a69febf55ad76ee05e6696b76b0df5ee925072e934b977d1ca4c8d9ce0bc9acb311ec1992ce783e86fcaec93a93fc47fecffbd0071f002013485')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
