# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=either
pkgname=haskell-either
pkgver=4.5
pkgrel=1
pkgdesc="An either monad transformer"
url="https://github.com/ekmett/either/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-bifunctors" "haskell-exceptions" "haskell-free"
         "haskell-mmorph" "haskell-monad-control" "haskell-monadrandom"
         "haskell-mtl" "haskell-profunctors" "haskell-semigroupoids"
         "haskell-semigroups" "haskell-transformers-base")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0a55a31f8a4c11c48715494615db5c43f683d4bd56d4904f3872d706aabf792744cb58cef3de9804ce05ead223323947306aaeac7ab26a2f61ee9d337bf6f498')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
