# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=email-validate
pkgname=haskell-email-validate
pkgver=2.3.2.5
pkgrel=3
pkgdesc="Validating an email address string against RFC 5322"
url="http://porg.es/blog/email-address-validation-simpler-faster-more-correct"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0ea3457e31e16948650ba810302d42f07e605cbbbfa3b7474d596f56de02ea3bbf995bacdb49d2dc37c6360062267ac0153e9275bfa5a513e0c5e2764f58399c')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/]/i \    , "-dynamic"' tests/doctests.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
