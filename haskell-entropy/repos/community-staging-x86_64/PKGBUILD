# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Daniel Nagy <danielnagy at gmx de>

pkgname=haskell-entropy
pkgver=0.4.1.4
pkgrel=3
pkgdesc="A mostly platform independent (not GHCJS) method to obtain cryptographically strong entropy"
url="https://hackage.haskell.org/package/entropy"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/entropy/${pkgver}/entropy-${pkgver}.tar.gz)
sha512sums=('714779479b4641c3af1102469db60a8dc95d311efab6c40c294391d88d1bab623c837224f792bd68f8d11ff69636042d9facbae145415cf00cc4d1bdc8fc08d5')

build() {
    cd "${srcdir}/entropy-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/entropy-${pkgver}"
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
