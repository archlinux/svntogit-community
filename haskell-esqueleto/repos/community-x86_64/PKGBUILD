# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=esqueleto
pkgname=haskell-esqueleto
pkgver=3.1.0
pkgrel=20
pkgdesc="Type-safe EDSL for SQL queries on persistent backends."
url="https://github.com/bitemyapp/esqueleto"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-blaze-html' 'haskell-conduit' 'haskell-monad-logger'
         'haskell-persistent' 'haskell-resourcet' 'haskell-tagged' 'haskell-unliftio'
         'haskell-unordered-containers')
makedepends=('ghc')  # 'haskell-exceptions' 'haskell-vector')  # Not enabled yet
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('de09f1c1073d5576f1ad625eb63183213d2ae9c38378c92f42edd2dd8f4e29ec8e65ad106818c7517744e279120dd6b35f69493d5cc054a7da812c499f636c43')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-mysql -f-postgresql
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
