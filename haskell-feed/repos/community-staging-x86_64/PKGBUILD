# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=feed
pkgname=haskell-feed
pkgver=1.3.2.0
pkgrel=63
pkgdesc="Interfacing with RSS (v 0.9x, 2.x, 1.0) + Atom feeds."
url="https://github.com/bergmark/feed"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-old-locale' 'haskell-old-time' 'haskell-safe'
         'haskell-time-locale-compat' 'haskell-utf8-string' 'haskell-xml-types'
         'haskell-xml-conduit')
makedepends=('ghc' 'uusi' 'haskell-doctest' 'haskell-doctest-driver-gen' 'haskell-hunit'
             'haskell-test-framework' 'haskell-test-framework-hunit' 'haskell-markdown-unlit'
             'haskell-syb')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('17d182c3ebf07b3f9f72f90ece7dcbfaa91931410663b513fda4e85c19b4d3118eb7e8ac685cf0f868d00fa9d05153409b2b4ce40ccdcdf3cf13bfed1929ebdd')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u base $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || echo "doctests are broken for now"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
