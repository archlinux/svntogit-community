# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: William J. Bowman <aur@williamjbowman.com>

_hkgname=filemanip
pkgname=haskell-filemanip
pkgver=0.3.6.3
pkgrel=8
pkgdesc="Expressive file and directory manipulation for Haskell."
url="https://hackage.haskell.org/package/filemanip"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc" "sh" "haskell-mtl" "haskell-unix-compat")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('8836da17baaaf02ca080c9990ece4e0b0c8d6a128f458f8b64fd07b225ca9846')
sha512sums=('5cb6ad4e310c1ae3b038ccfa3154536db73145e4dea4a7be1ae0563ba44565494e378fa8e459f449feac2bcc140f1128b0b3ec915acd2300e73cf012ada85160')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
