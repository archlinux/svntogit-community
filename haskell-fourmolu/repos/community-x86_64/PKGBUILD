# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=fourmolu
pkgname=haskell-fourmolu
pkgver=0.3.0.0
pkgrel=103
pkgdesc="A formatter for Haskell source code"
url="https://github.com/parsonsmatt/fourmolu"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hsyaml' 'haskell-hsyaml-aeson' 'haskell-aeson' 'haskell-dlist'
         'haskell-ghc-lib-parser' 'haskell-gitrev' 'haskell-optparse-applicative' 'haskell-syb')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-hspec-discover' 'haskell-path' 'haskell-path-io')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('ba7201c78ee61665eaf1fce10cd297dd5383d36053cd3984e41a5094d96e096d')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u path $_hkgname.cabal
  # uusi -rghc-lib-parser:ghc,ghc-boot,ghc-boot-th $_hkgname.cabal
  # sed -i 's/using ghc-lib-parser/using ghc/;s/VERSION_ghc_lib_parser/VERSION_ghc/' app/Main.hs

  sed -i 's/matchIgnoringSrcSpans = genericQuery/matchIgnoringSrcSpans x = genericQuery x/;1i{-# LANGUAGE ImpredicativeTypes #-}' src/Ormolu/Diff.hs
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
