# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-lens-core
pkgname=haskell-generic-lens-core
pkgver=2.1.0.0
pkgrel=5
pkgdesc="Generically derive traversals, lenses and prisms"
url="https://github.com/kcsongor/generic-lens"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-indexed-profunctors')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        ghc9.patch)
sha512sums=('9a64ab9a2f7c0f485088ef0e5a6bb75668a8a7a9c059f566b3ec4145f38edc35f49331e7d2553f6f7753f2a02fb9bad461c978e90c44f65b878b3d70bddda291'
            'ce5d9531e8a0755bde320c98fc06e49355220d731d98dd94b657155e7aa769c8617f2c1486fbf622fa449a9a07a97aec0b986ad8e3dbc270da65c0d2c0d97d79')

prepare() {
    # https://github.com/kcsongor/generic-lens/pull/130
    patch -d $_hkgname-$pkgver -p2 < ghc9.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
