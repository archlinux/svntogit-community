# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-lens
pkgname=haskell-generic-lens
pkgver=2.1.0.0
pkgrel=49
pkgdesc="Generically derive traversals, lenses and prisms"
url="https://github.com/kcsongor/generic-lens"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-generic-lens-core' 'haskell-profunctors')
makedepends=('ghc' 'haskell-doctest' 'haskell-hunit' 'haskell-inspection-testing' 'haskell-lens')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        ghc9.patch)
sha512sums=('d62f07d3133c4513478d868493d31350a6378cc6c31a7da88ca09b8e8d4032a82c7ae42cbbb0770167aeb15b289cb1401ae3f4576ff10bc39af6f5dca9d80640'
            '4e690ab5db428a44ef8b016e18500e4a57b9eaf31e0ebc56cc0b5afbaf1a07990c2f6c3bf72e4058ba3cca10cea35888e4d9840343f1750714abfdf4298539b5')

prepare() {
    patch -d $_hkgname-$pkgver -p2 < ghc9.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
