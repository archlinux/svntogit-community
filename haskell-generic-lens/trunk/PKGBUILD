# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-lens
pkgname=haskell-generic-lens
pkgver=1.2.0.1
pkgrel=8
pkgdesc="Generically derive traversals, lenses and prisms"
url="https://github.com/kcsongor/generic-lens"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-profunctors' 'haskell-tagged')
makedepends=('ghc' 'haskell-doctest' 'haskell-hunit' 'haskell-inspection-testing' 'haskell-lens')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('c3ca1d6b532893f1f6cd2a42ee97c3625cd4a3c1a748f85a891fa9ea6f6d4354fa8a87908c45ae671761e8b99b761a2ddf6d628602d3d7783e9c7c6418f80bed')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/kcsongor/generic-lens/issues/83
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
