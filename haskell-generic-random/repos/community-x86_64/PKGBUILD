# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-random
pkgname=haskell-generic-random
pkgver=1.3.0.1
pkgrel=41
pkgdesc="Generic random generators for QuickCheck"
url="https://github.com/lysxia/generic-random"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck')
makedepends=('ghc' 'haskell-inspection-testing')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d936f6b2cab03275e37f07664602a2ab0037efa1a0b76682bd032141390f14ad70c263fed64bf9105a71b098c67d7ce06dccb78ec1d8cc59fc9ffd1e05fde8b8')

build() {
    cd $_hkgname-$pkgver

    # Tests disabled: https://github.com/Lysxia/generic-random/issues/22
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --disable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    #runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
