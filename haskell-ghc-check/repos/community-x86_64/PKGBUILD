# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-check
pkgname=haskell-ghc-check
pkgver=0.5.0.6
pkgrel=10
pkgdesc="detect mismatches between compile-time and run-time versions of the ghc api"
url="https://hackage.haskell.org/package/ghc-check"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc' 'haskell-ghc-paths' 'haskell-safe-exceptions' 'haskell-th-compat')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('bfae0b338f94a4de6c2b57c56e07b630b066d26fa540bdc3ecfbf11c77da052d8ff9f337fa35329a57b53fdd5a0ffe2a7b3ae20632a248a36da823654a022e4d')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
