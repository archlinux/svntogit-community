# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-lib-parser-ex
pkgname=haskell-ghc-lib-parser-ex
pkgver=8.10.0.19
pkgrel=15
pkgdesc="Algorithms on GHC parse trees"
url="https://github.com/shayne-fletcher/ghc-lib-parser-ex"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-uniplate' 'haskell-ghc')
makedepends=('ghc' 'haskell-extra' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-uniplate')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('a286a0874191e7e10484279e9a70e1dc1add602ee6180718e7936f9ac0714ad0997a0398d1879ae0b1d1864f5976c26eb05d8db621343885630e37739e6cc17f')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
