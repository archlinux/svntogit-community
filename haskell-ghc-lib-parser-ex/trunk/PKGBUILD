# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-lib-parser-ex
pkgname=haskell-ghc-lib-parser-ex
pkgver=9.0.0.4
pkgrel=23
pkgdesc="Algorithms on GHC parse trees"
url="https://github.com/shayne-fletcher/ghc-lib-parser-ex"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-uniplate' 'haskell-ghc')
makedepends=('ghc' 'haskell-extra' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-uniplate')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ba02c5606113ce5b8782c55669ec657cd39462d3e6296cd4c82c21f74c9754b2882fdf77f4c239621656469f6cb1609a6ade2c18083c3d403a834744918f634a')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
