# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-tcplugins-extra
pkgname=haskell-ghc-tcplugins-extra
pkgver=0.4.2
pkgrel=2
pkgdesc="Utilities for writing GHC type-checker plugins"
url="https://github.com/clash-lang/ghc-tcplugins-extra"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('bbacc0be7e0688cef3ee722611597139b9be3036002337a85538ff0b1fdf4c5d5741d00096789d0da65b6899e84e99c5603da2847d8ea17d4e706f5fd9109894')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
