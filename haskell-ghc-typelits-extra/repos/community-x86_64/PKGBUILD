# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-typelits-extra
pkgname=haskell-ghc-typelits-extra
pkgver=0.4
pkgrel=23
pkgdesc="Additional type-level operations on GHC.TypeLits.Nat"
url="https://github.com/clash-lang/ghc-typelits-extra"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc' 'haskell-ghc-tcplugins-extra' 'haskell-ghc-typelits-knownnat'
         'haskell-ghc-typelits-natnormalise')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('215cf547468856f5f7309928dce8fbecc23bcb1e5e86b3614483b2e08144801c063d8c61827b13aa92d8540a0f06f5fc10ba487d5283f5cb15b2f6ce5c5eb5c6')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
