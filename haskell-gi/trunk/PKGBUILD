# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=haskell-gi
pkgname=haskell-gi
pkgver=0.23.0
pkgrel=12
pkgdesc="Generate Haskell bindings for GObject Introspection capable libraries"
url="https://github.com/haskell-gi/haskell-gi"
license=("LGPL2")
arch=('x86_64')
depends=('ghc-libs' 'glib2' 'haskell-gi-base' 'haskell-attoparsec' 'haskell-pretty-show'
         'haskell-safe' 'haskell-xdg-basedir' 'haskell-xml-conduit' 'haskell-regex-tdfa')
makedepends=('ghc' 'gobject-introspection' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('594180257709b7b058b78fe4acafd39c9d51e3a0767d625ddecf0efbd4d0c84f6ac5a0826044311cf35b4d5701ebadcc5ae97130685a77b4754580703f50b066')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
