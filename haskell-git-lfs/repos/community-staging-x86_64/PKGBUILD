# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=git-lfs
pkgname=haskell-git-lfs
pkgver=1.1.0
pkgrel=12
pkgdesc="An implementation of the git-lfs protocol"
url="https://hackage.haskell.org/package/git-lfs"
license=('AGPL3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-case-insensitive' 'haskell-http-client'
         'haskell-http-types' 'haskell-network-uri')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('c662e7752ba9346b8adec5af225806f95702aaded3ea6b3d8e8871e923db24f8bd49a275fe7477cc44b65b35edab06fb7ab92f0e57d9bdd8af7b209c4adfa5c5')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<= 0.7/<1/;s/<= 1.5/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
