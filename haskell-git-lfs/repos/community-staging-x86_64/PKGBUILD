# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=git-lfs
pkgname=haskell-git-lfs
pkgver=1.1.1
pkgrel=17
pkgdesc="An implementation of the git-lfs protocol"
url="https://hackage.haskell.org/package/git-lfs"
license=('AGPL3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-case-insensitive' 'haskell-http-client'
         'haskell-http-types' 'haskell-network-uri')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('4c45441038d220738b154ee93ce9a108044d85ac1c7d892f9001ed3781264ab0a3854f7ecbdef517b05604ea57f96761ca44b026a39e9bd7fccab84b5cd3c779')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
