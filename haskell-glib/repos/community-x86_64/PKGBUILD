# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-glib
pkgver=0.12.1
pkgrel=1
pkgdesc="Binding to the GLIB library for Gtk2Hs."
url="http://hackage.haskell.org/package/glib"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'glib2')
makedepends=("haskell-gtk2hs-buildtools=$pkgver")
options=('strip')
install=gtk2hs-glib.install
provides=('gtk2hs-glib')
replaces=('gtk2hs-glib')
source=(http://hackage.haskell.org/packages/archive/glib/$pkgver/glib-$pkgver.tar.gz)
md5sums=('1ec61fc88876c127949adea6ed528328')

build() {
    cd ${srcdir}/glib-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-glib \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
    depends=("ghc=${_ghcver}" "glib2")

    cd ${srcdir}/glib-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-glib/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-glib/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-glib/html ${pkgdir}/usr/share/doc/ghc/html/libraries/glib
    runhaskell Setup copy --destdir=${pkgdir}
}
