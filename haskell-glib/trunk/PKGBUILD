# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-glib
pkgver=0.13.4.1
pkgrel=5
pkgdesc="Binding to the GLIB library for Gtk2Hs."
url="https://hackage.haskell.org/package/glib"
license=('LGPL2.1')
arch=('x86_64')
# deps adjusted to specific versions in package()
depends=('ghc-libs' 'glib2' 'haskell-utf8-string' 'haskell-text')
makedepends=('ghc' "haskell-gtk2hs-buildtools")
provides=('gtk2hs-glib')
replaces=('gtk2hs-glib')
conflicts=('gtk2hs-glib')
source=(https://hackage.haskell.org/packages/archive/glib/$pkgver/glib-$pkgver.tar.gz)
sha512sums=('537f70688e398b8c68727b8260080790569e7f1b87633912a1438c5b1ed590221ac633168a83e7bc49728292ae53f8eaaf268d78c0be0df81527c5a3ce810869')

build() {
    cd "${srcdir}/glib-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/glib-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
}
