# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-glib
pkgver=0.13.4.1
pkgrel=1
pkgdesc="Binding to the GLIB library for Gtk2Hs."
url="http://hackage.haskell.org/package/glib"
license=('LGPL2.1')
arch=('i686' 'x86_64')
# deps adjusted to specific versions in package()
depends=('ghc' 'glib2' 'haskell-utf8-string' 'haskell-text')
makedepends=("haskell-gtk2hs-buildtools")
provides=('gtk2hs-glib')
replaces=('gtk2hs-glib')
conflicts=('gtk2hs-glib')
source=(https://hackage.haskell.org/packages/archive/glib/$pkgver/glib-$pkgver.tar.gz)
sha256sums=('f57202ed4094cc50caa8b390c8b78a1620b3c43b913edb1e5bda0f3c5be32630')

build() {
    cd glib-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-glib \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

_update_deps() {
    _ver=`pacman -Q $1 | cut -f2 -d\  | cut -f1 -d-`
    for i in `seq 0 $(expr ${#depends[@]} - 1)`; do
        if [ ${depends[$i]} == $1 ]; then
            depends[$i]="$1=${_ver}"
        fi
    done
}

package() {
    _update_deps ghc

    cd "$srcdir"/glib-${pkgver}
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "$pkgdir"/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-glib/html "$pkgdir"/usr/share/doc/ghc/html/libraries/glib
    runhaskell Setup copy --destdir="$pkgdir"
}
