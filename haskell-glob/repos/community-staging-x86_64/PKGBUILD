# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Glob
pkgname=haskell-glob
pkgver=0.9.3
pkgrel=2
pkgdesc="Globbing library"
url="http://iki.fi/matti.niemenmaa/glob/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-transformers-compat')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4b677745881b318a5a82668d56c4547f8d1173be55cb1589ad0b9e62622151d771d3a40479126749135cbc98f56d8cfea3619358db5929c890838d3e0b85a536')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.txt"
}
