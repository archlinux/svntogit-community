# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=graphviz
pkgname=haskell-graphviz
pkgver=2999.20.0.3
pkgrel=8
pkgdesc="Bindings to Graphviz for graph visualisation."
url="https://hackage.haskell.org/package/${_hkgname}"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-colour' 'haskell-dlist' 'haskell-fgl' 'haskell-polyparse'
         'haskell-temporary' 'haskell-wl-pprint-text')
makedepends=('ghc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ivan-m/graphviz/archive/$pkgver.tar.gz")
sha512sums=('3542ce3703277827937f354765fc82263383bdbf7c2ea7297368fc0713f442ad3f0608c740c0d87c6c3696b819abd3711b3740c7fa5001943654b2757c7749c5')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-test-parsing
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
