# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk
pkgver=0.12.4
pkgrel=3
pkgdesc="Binding to the gtk library for Gtk2Hs."
url="http://hackage.haskell.org/package/gtk"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'gtk2' "haskell-pango" 'haskell-mtl')
makedepends=("gtk2hs-buildtools")
provides=('gtk2hs-gtk')
replaces=('gtk2hs-gtk')
conflicts=('gtk2hs-gtk')
options=('strip')
install=gtk2hs-gtk.install
source=(http://hackage.haskell.org/packages/archive/gtk/$pkgver/gtk-$pkgver.tar.gz
	ghc-7.6.1.patch)
md5sums=('165a04134cdc3aec9a9aa149c421b1de'
         '5fb93f37fe3f494b30ee682a36b8f1b6')

build() {
  cd ${srcdir}/gtk-${pkgver}
#  patch -p1 <$srcdir/ghc-7.6.1.patch
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/haskell-gtk \
    --libsubdir=\$compiler/site-local/\$pkgid
  sed -i 's|import Foreign.C.Types.*(CULong, CUInt)|import Foreign.C.Types (CULong(..), CUInt(..))|' Graphics/UI/Gtk/Types.chs
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  _hmtlver=`pacman -Q haskell-mtl | cut -f2 -d\  | cut -f1 -d-`
  _hpangover=`pacman -Q haskell-pango | cut -f2 -d\  | cut -f1 -d-`
  depends=("ghc" "gtk2" "haskell-pango=${_hpangover}" "haskell-mtl=${_hmtlver}")

  cd ${srcdir}/gtk-${pkgver}
  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-gtk/register.sh
  install -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-gtk/unregister.sh
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/haskell-gtk/html ${pkgdir}/usr/share/doc/ghc/html/libraries/gtk2hs-gtk
  runhaskell Setup copy --destdir=${pkgdir}
}
