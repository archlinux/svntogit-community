# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk
pkgver=0.15.3
pkgrel=5
pkgdesc="Binding to the gtk library for Gtk2Hs."
url="https://hackage.haskell.org/package/gtk3"
license=('LGPL2.1')
arch=('x86_64')
depends=('ghc-libs' 'gtk3' 'haskell-pango')
makedepends=('ghc' 'haskell-gtk2hs-buildtools')
provides=('gtk2hs-gtk')
replaces=('gtk2hs-gtk')
conflicts=('gtk2hs-gtk')
source=("https://hackage.haskell.org/packages/archive/gtk3/${pkgver}/gtk3-${pkgver}.tar.gz")
sha512sums=('ce45659c04143c0aec6e3c62f06c9d9e8756ae49382bc89a6119ba290c22c60a86d8c279ed7f26d9ad1e9ba261e1603d3862e5374825d7052a3ef1f81d2d0ed1')

build() {
    cd "${srcdir}/gtk3-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/gtk3-${pkgver}"

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
}
