# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=haskell-haddock-api
pkgver=2.16.0
pkgrel=2
pkgdesc="A documentation-generation tool for Haskell libraries"
url="http://www.haskell.org/haddock/"
license=("BSD3")
arch=('i686' 'x86_64')
makedepends=()
depends=("ghc=7.10.1"
         "haskell-ghc-paths"
         "haskell-haddock-library")
options=('strip' 'staticlibs')
source=("https://hackage.haskell.org/package/haddock-api-${pkgver}/haddock-api-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('1286dcb12bf3b4d657df64dc595e8ce5e414e10260cbcb3abadde4e50c176442')

build() {
    cd "${srcdir}/haddock-api-${pkgver}"

    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/haddock-api-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/haddock-api"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
