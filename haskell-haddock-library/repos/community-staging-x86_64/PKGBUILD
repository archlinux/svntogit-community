# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

_hkgname=haddock-library
pkgname=haskell-haddock-library
pkgver=1.7.0
pkgrel=55
pkgdesc="A documentation-generation tool for Haskell libraries"
url="https://www.haskell.org/haddock/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-base-compat' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-quickcheck' 'haskell-optparse-applicative' 'haskell-tree-diff')
source=("https://hackage.haskell.org/packages/archive/haddock-library/${pkgver}/haddock-library-${pkgver}.tar.gz")
sha512sums=('ae4a3e2e02a866399623349c69b19738b371c574a72b2616f74c9b8fa2acdc04dfb20b7ef6cdd32709b474c0c99d27a49a810b7c58f68d8eb6bea7244323c30c')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/>= 2.11/>= 2.13/' -e 's/< *2.6/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
