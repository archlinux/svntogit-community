# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hakyll
pkgname=haskell-hakyll
pkgver=4.8.3.1
pkgrel=8
pkgdesc="A static website compiler library"
url="http://jaspervdj.be/hakyll"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-blaze-html" "haskell-blaze-markup" "haskell-cmdargs"
         "haskell-cryptohash" "haskell-data-default" "haskell-http-conduit"
         "haskell-http-types" "haskell-lrucache" "haskell-mtl" "haskell-network"
         "haskell-network-uri" "haskell-pandoc" "haskell-pandoc-citeproc"
         "haskell-parsec" "haskell-random" "haskell-regex-base" "haskell-regex-tdfa"
         "haskell-resourcet" "haskell-scientific" "haskell-tagsoup" "haskell-text"
         "haskell-time-locale-compat" "haskell-unordered-containers" "haskell-vector"
         "haskell-yaml")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('e56b4a1b73b5d1b394af9c9730a2baf6df93a8cfd72b0429af36253083fdfaa4')

prepare() {
    sed -e 's/binary               >= 0.5    && < 0.8,/binary               >= 0.5    \&\& < 0.9,/' \
        -e 's/data-default         >= 0.4    && < 0.7,/data-default         >= 0.4    \&\& < 0.8,/' \
        -e 's/process              >= 1.0    && < 1.3,/process              >= 1.0    \&\& < 1.5,/' \
        -e 's/time                 >= 1.4    && < 1.6,/time                 >= 1.4    \&\& < 1.7,/' \
        -e 's/pandoc-citeproc      >= 0.4    && < 0.10,/pandoc-citeproc      >= 0.4    \&\& < 0.11,/' \
        -e 's/tagsoup              >= 0.13.1 && < 0.14,/tagsoup              >= 0.13.1 \&\& < 0.16,/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fcheckexternal -f-watchserver -f-previewserver
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
