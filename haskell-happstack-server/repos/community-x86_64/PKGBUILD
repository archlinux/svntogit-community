# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=happstack-server
pkgname=haskell-happstack-server
pkgver=7.8.0.2
pkgrel=4
pkgdesc="Web related tools and services."
url="http://happstack.com"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-extensible-exceptions'
         'haskell-hslogger' 'haskell-html' 'haskell-monad-control' 'haskell-network'
         'haskell-network-uri' 'haskell-old-locale' 'haskell-sendfile' 'haskell-syb'
         'haskell-system-filepath' 'haskell-threads' 'haskell-transformers-base'
         'haskell-transformers-compat' 'haskell-utf8-string' 'haskell-zlib')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('3a5653b2e2fea383eb3c3bfecbd60348a0913c682eec9c9e133b17d80b379795')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fnetwork-uri

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/COPYING
}
