# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hasql-transaction
pkgname=haskell-hasql-transaction
pkgver=1.0.1.1
pkgrel=151
pkgdesc="A composable abstraction over the retryable transactions for Hasql"
url="https://github.com/nikita-volkov/hasql-transaction"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bytestring-tree-builder' 'haskell-contravariant'
         'haskell-contravariant-extras' 'haskell-hasql')
makedepends=('ghc' 'uusi' 'haskell-async' 'haskell-rerebase')
checkdepends=('postgresql' 'pifpaf')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c37c463c3cce859cb3afb35ab3ac6a55db27209bbed4209f77b027ace2c21888b544223ceabf86cde92d60c26e65d03df26c2974bcbca99b466f3ccc683f0583')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/5432/9824/' conflicts-test/Main.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver

    eval $(pifpaf run postgresql --host 127.0.0.1)
    createuser -s postgres
    runhaskell Setup test --show-details=direct
    pifpaf_stop
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
