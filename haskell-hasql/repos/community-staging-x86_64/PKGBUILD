# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=hasql
pkgname=haskell-hasql
pkgver=1.1.1
pkgrel=6
pkgdesc="An efficient PostgreSQL driver and a flexible mapping API"
url="https://github.com/nikita-volkov/hasql"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-prelude' 'haskell-bytestring-strict-builder'
         'haskell-contravariant' 'haskell-contravariant-extras' 'haskell-data-default-class'
         'haskell-dlist' 'haskell-hashable' 'haskell-hashtables' 'haskell-loch-th'
         'haskell-mtl' 'haskell-placeholders' 'haskell-postgresql-binary' 'haskell-postgresql-libpq'
         'haskell-profunctors' 'haskell-semigroups' 'haskell-text' 'haskell-vector')
makedepends=('ghc' 'haskell-bug' 'haskell-tasty' 'haskell-tasty-quickcheck' 'haskell-tasty-hunit'
             'haskell-quickcheck-instances' 'haskell-quickcheck' 'haskell-rerebase')
checkdepends=('postgresql' 'pifpaf')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f72097118dbecd87c31394be882aa81a591d5e6d429bbee974cc4d6acaf1a86b9cb1c623666f3ffcc4f258992a3522d1780fbcc5a2edaded18c106bbe1fd28be')

prepare() {
    sed -i 's/tasty-hunit >= 0.9 && < 0.10,/tasty-hunit >= 0.9,/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver

    eval $(pifpaf run postgresql --host 127.0.0.1 --port 5432)
    createuser -s postgres
    runhaskell Setup test
    pifpaf_stop
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
