# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hit
pkgname=haskell-hit
pkgver=0.6.3
pkgrel=58
pkgdesc="Git operations in haskell"
url="https://github.com/vincenthz/hit"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-attoparsec" "haskell-byteable" "haskell-cryptohash" "haskell-hashable"
         "haskell-hashtables" "haskell-hourglass" "haskell-mtl" "haskell-parsec" "haskell-patience"
         "haskell-random" "haskell-system-fileio" "haskell-system-filepath" "haskell-unix-compat"
         "haskell-utf8-string" "haskell-vector" "haskell-zlib" "haskell-zlib-bindings")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('82ccfeb77a439caa1792f1ae29535636764be9e6e94f75973f37a86db6627a492e57da835954384ef8468799de9f0897e20905f65dad256fe3547a4248d5fffc')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fexecutable -f-debug
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
