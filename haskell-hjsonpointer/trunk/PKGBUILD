# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hjsonpointer
pkgname=haskell-hjsonpointer
pkgver=1.5.0
pkgrel=124
pkgdesc="JSON Pointer library"
url="https://github.com/haskell-foundation/foundation"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-hashable' 'haskell-unordered-containers'
         'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-http-types' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('dd47b98884982d06892c82f76385650ab76ae4ade31ef9ead1e1dcdb4f0d24d9c2268023cc0f232e7febd7ea5f7058b2561922b8e99a04eb8afd9e96935d7336')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "MIT-LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/MIT-LICENSE.txt"
}
