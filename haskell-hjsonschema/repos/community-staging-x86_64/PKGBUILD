# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hjsonschema
pkgname=haskell-hjsonschema
pkgver=1.9.0
pkgrel=127
pkgdesc="JSON Schema library"
url="https://github.com/seagreen/hjsonschema"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-file-embed' 'haskell-hashable' 'haskell-hjsonpointer'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types' 'haskell-pcre-heavy'
         'haskell-profunctors' 'haskell-protolude' 'haskell-quickcheck' 'haskell-safe-exceptions'
         'haskell-scientific' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-wai-app-static' 'haskell-warp')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c71f4f23453abbb7e5c3b8a04e9fd2bea560e03ccf590fbad3739f609fe0e4e5292acfe94204b2d52d30f8fda8cc1c94edac41d5095f6be1c4cf8c05ec08e273')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "json-schema.org is NXDOMAIN as of 2018-06-03"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "MIT-LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/MIT-LICENSE.txt"
}
