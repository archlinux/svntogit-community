# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hledger-lib
pkgname=haskell-hledger-lib
pkgver=1.11.1
pkgrel=5
pkgdesc="Core data types, parsers and functionality for the hledger accounting tools"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-base-compat-batteries' 'haskell-blaze-markup'
         'haskell-call-stack' 'haskell-cassava' 'haskell-cassava-megaparsec' 'haskell-cmdargs'
         'haskell-data-default' 'haskell-decimal' 'haskell-easytest' 'haskell-extra'
         'haskell-glob' 'haskell-hashtables' 'haskell-megaparsec' 'haskell-old-time'
         'haskell-parser-combinators' 'haskell-pretty-show' 'haskell-regex-tdfa' 'haskell-safe'
         'haskell-split' 'haskell-tabular' 'haskell-uglymemo' 'haskell-utf8-string')
makedepends=('ghc' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e9ff28efe6b1633d307006fbf705fbbb00dd123d31939812c4c0ccf7cd90a63a738e3077ce4b329098af30b980142a11fdf37bf56ab378ffd3cab13e31783fbc')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
	-e 's/< *4.12/<5/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
