# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hledger-lib
pkgname=haskell-hledger-lib
pkgver=1.3.2
pkgrel=2
pkgdesc="Core data types, parsers and functionality for the hledger accounting tools"
url="http://hledger.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-base-compat' 'haskell-blaze-markup'
         'haskell-cmdargs' 'haskell-csv' 'haskell-data-default' 'haskell-decimal'
         'haskell-hashtables' 'haskell-megaparsec' 'haskell-mtl' 'haskell-old-time'
         'haskell-pretty-show' 'haskell-regex-tdfa' 'haskell-safe' 'haskell-split' 'haskell-text'
         'haskell-uglymemo' 'haskell-utf8-string' 'haskell-hunit' 'haskell-parsec'
         'haskell-semigroups')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f03a39f183263a9e2a4f46c148ed8fe8357c5731b6bde385655b0f7f252e4a1422c2ac547d9f8ffd0f046fd12f6195004d8dea3de248c565561bd02a07f005e7')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-oldtime
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
