# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hledger-lib
pkgname=haskell-hledger-lib
pkgver=1.9
pkgrel=6
pkgdesc="Core data types, parsers and functionality for the hledger accounting tools"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-base-compat' 'haskell-blaze-markup'
         'haskell-cmdargs' 'haskell-csv' 'haskell-data-default' 'haskell-decimal' 'haskell-extra'
         'haskell-hashtables' 'haskell-megaparsec' 'haskell-mtl' 'haskell-old-time'
         'haskell-pretty-show' 'haskell-regex-tdfa' 'haskell-safe' 'haskell-split' 'haskell-text'
         'haskell-uglymemo' 'haskell-utf8-string' 'haskell-hunit' 'haskell-parsec')
makedepends=('ghc' 'haskell-doctest' 'haskell-easytest' 'haskell-glob' 'haskell-test-framework'
             'haskell-test-framework-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ccb78452a2048378fc37c446a998777b8fdd66784552d77805e033a735944fdc8d466851e1aec685abacd32af8545bc44695e9eb7cf8a9d67d69a83ecd8b0745')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal

    sed -i 's/++ fs2$/++ fs2 ++ ["-dynamic"]/' ${_hkgname}-${pkgver}/tests/doctests.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
