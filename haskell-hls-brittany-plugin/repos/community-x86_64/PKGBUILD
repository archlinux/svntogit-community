# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-brittany-plugin
pkgname=haskell-hls-brittany-plugin
pkgver=1.0.0.0
pkgrel=23
pkgdesc="Integration with the Brittany code formatter"
url="https://haskellwingman.dev"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-brittany' 'haskell-ghc' 'haskell-ghcide' 'haskell-lsp-types'
         'haskell-hls-plugin-api' 'haskell-lens')
makedepends=('ghc' 'uusi' 'haskell-data-default' 'haskell-shake' 'haskell-aeson'
             'haskell-hspec-expectations' 'haskell-megaparsec' 'haskell-lsp-test' 'haskell-tasty'
             'haskell-tasty-ant-xml' 'haskell-tasty-hunit' 'haskell-tasty-golden'
             'haskell-tasty-rerun')
checkdepends=('git')
#source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
source=("https://github.com/haskell/haskell-language-server/archive/ghcide-v1.1.0/haskell-language-server-ghcide-v1.1.0.tar.gz")
sha256sums=('ec611943b46027488d85135eb61245ea7cbd892ed9825b1456a4b74e0eb5a713')

prepare() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname
  gen-setup
}

build() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname
  PATH="$PWD/dist/build/test-server:$PATH" LD_LIBRARY_PATH="$PWD/dist/build" runhaskell Setup test --show-details=direct
}

package() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  rm "$pkgdir"/usr/bin/test-server
}
