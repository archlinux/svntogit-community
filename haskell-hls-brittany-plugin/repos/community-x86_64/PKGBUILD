# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-brittany-plugin
pkgname=haskell-hls-brittany-plugin
pkgver=1.0.0.1
pkgrel=28
pkgdesc="Integration with the Brittany code formatter"
url="https://haskellwingman.dev"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-brittany' 'haskell-ghc' 'haskell-ghcide' 'haskell-hls-plugin-api'
         'haskell-lens' 'haskell-lsp-types')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('d74039f04e07810ead5a25466f43df4c11d96d120df597faecc9ac8bce7652b1a139e3aad69198529c14420fe687a55a0048f1bef4d0e6ed2b39a3dd77b7a082')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u ghcide $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
