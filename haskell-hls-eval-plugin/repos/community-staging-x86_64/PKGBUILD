# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-eval-plugin
pkgname=haskell-hls-eval-plugin
pkgver=1.2.1.0
pkgrel=9
pkgdesc="Eval plugin for Haskell Language Server"
url="https://github.com/haskell/haskell-language-server"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-quickcheck' 'haskell-aeson' 'haskell-data-default'
         'haskell-dlist' 'haskell-extra' 'haskell-ghc' 'haskell-ghc-paths' 'haskell-ghcide'
         'haskell-hashable' 'haskell-hls-graph' 'haskell-hls-plugin-api' 'haskell-lens'
         'haskell-lsp' 'haskell-lsp-types' 'haskell-megaparsec' 'haskell-parser-combinators'
         'haskell-pretty-simple' 'haskell-safe-exceptions' 'haskell-temporary' 'haskell-unliftio'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('7e2e7453393317a5a6915ad16e9c0aac256e737a93b0bf77794d608d3cfa1607fab6f5dad501b2acddd67fc975e1b3b65716d1c9a53a282995152b140e6117d9')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # https://github.com/haskell/haskell-language-server/issues/1809
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
