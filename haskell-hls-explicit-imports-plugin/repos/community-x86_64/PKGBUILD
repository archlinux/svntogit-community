# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-explicit-imports-plugin
pkgname=haskell-hls-explicit-imports-plugin
pkgver=1.0.1.1
pkgrel=17
pkgdesc="Explicit imports plugin for Haskell Language Server"
url="https://hackage.haskell.org/package/hls-explicit-imports-plugin"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ghc' 'haskell-ghc-api-compat' 'haskell-ghcide'
         'haskell-hls-graph' 'haskell-hls-plugin-api' 'haskell-lsp' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('2bf0ab3e9f48a1e3dd95bcbeecee814029c647bb020611732defb3e3ccd8f5989b14350628bf511237e9354cb16d4b7be4c5a875a4e4eae7ffa40a610e390be5')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -r ghc:ghc,ghc-api-compat $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # TODO
  runhaskell Setup test || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
