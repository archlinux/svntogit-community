# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-explicit-imports-plugin
pkgname=haskell-hls-explicit-imports-plugin
pkgver=1.0.0.1
pkgrel=4
pkgdesc="Explicit imports plugin for Haskell Language Server"
url="https://hackage.haskell.org/package/hls-explicit-imports-plugin"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ghc' 'haskell-ghcide' 'haskell-hls-plugin-api'
         'haskell-lsp' 'haskell-lsp-types' 'haskell-shake' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('94bda4eab1b5638f78ea23f69d1aee105769f91e9eee627465b04f0903d41d4e93beb88f94c6d47ef586f0684130991882e677852f0bd88adac6098969ee9dfe')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
