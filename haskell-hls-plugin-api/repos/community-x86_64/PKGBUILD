# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-plugin-api
pkgname=haskell-hls-plugin-api
pkgver=1.2.0.2
pkgrel=12
pkgdesc="Haskell Language Server API for plugin communication"
url="https://github.com/haskell/haskell-language-server/hls-plugin-api"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-aeson' 'haskell-data-default' 'haskell-dependent-map'
         'haskell-dependent-sum' 'haskell-dlist' 'haskell-ghc' 'haskell-ghc-api-compat'
         'haskell-hashable' 'haskell-hls-graph' 'haskell-hslogger' 'haskell-lens'
         'haskell-lens-aeson' 'haskell-lsp' 'haskell-optparse-applicative' 'haskell-opentelemetry'
         'haskell-regex-tdfa' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        hls-plugin-api-aeson-2.patch)
sha512sums=('8654b38529ad8804dfb3296fe789ceafe111f1d7807ffa49a92af8a8cae9b5331167d64f01263efec37b280b1e029e4513455c8a9329349cb0aeba9b85233232'
            '330d5a02d13df720252a0de3da311c7d3e549b6d31d7dc936b8a93b3dc5fc555d16f911ba1b59ba6388f93205757b2ecba1bcf3fe4e3558c9767d04df82c18c3')

prepare() {
  cd $_hkgname-$pkgver
  patch -p2 -i ../hls-plugin-api-aeson-2.patch
  gen-setup
  uusi -r ghc:ghc,ghc-api-compat $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
