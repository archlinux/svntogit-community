# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-refactor-plugin
pkgname=haskell-hls-refactor-plugin
pkgver=1.0.0.0
pkgrel=10
pkgdesc="Exactprint refactorings for Haskell Language Server"
url="https://github.com/haskell/haskell-language-server.git"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-data-default' 'haskell-dlist' 'haskell-extra'
         'haskell-ghc' 'haskell-ghc-exactprint' 'haskell-ghcide' 'haskell-hls-graph'
         'haskell-hls-plugin-api' 'haskell-lens' 'haskell-lsp' 'haskell-regex-tdfa'
         'haskell-retrie' 'haskell-syb' 'haskell-text-rope' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-async' 'haskell-ghcide-test-utils' 'haskell-hls-test-utils'
             'haskell-lsp-test' 'haskell-lsp-types' 'haskell-network-uri'
             'haskell-parser-combinators' 'haskell-shake' 'haskell-tasty'
             'haskell-tasty-expected-failure' 'haskell-tasty-hunit' 'haskell-tasty-rerun')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('a1111065d7e351c624eb48cdcd83ee543899ce051db165dbc1aa398f9c2ac33e')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
