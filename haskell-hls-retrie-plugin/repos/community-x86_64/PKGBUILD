# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-retrie-plugin
pkgname=haskell-hls-retrie-plugin
pkgver=1.0.1.3
pkgrel=16
pkgdesc="Retrie integration plugin for Haskell Language Server"
url="https://hackage.haskell.org/package/hls-retrie-plugin"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-extra' 'haskell-ghc' 'haskell-ghc-api-compat'
         'haskell-ghcide' 'haskell-hashable' 'haskell-hls-plugin-api' 'haskell-lsp'
         'haskell-lsp-types' 'haskell-retrie' 'haskell-safe-exceptions'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('1afa8c1493c11ea5e15a7a82961176fb61ea13c5b4657b7aaa5de07db158da4d0a924e15a704320a551e4123580c945b34762f46cdbf1b5385fcb290a4b1ce8c')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -r ghc:ghc,ghc-api-compat $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
