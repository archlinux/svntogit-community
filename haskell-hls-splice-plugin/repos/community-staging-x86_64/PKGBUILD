# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-splice-plugin
pkgname=haskell-hls-splice-plugin
pkgver=1.0.0.1
pkgrel=4
pkgdesc="HLS Plugin to expand TemplateHaskell Splices and QuasiQuotes"
url="https://hackage.haskell.org/package/hls-splice-plugin"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-dlist' 'haskell-extra' 'haskell-foldl' 'haskell-ghc'
         'haskell-ghc-exactprint' 'haskell-ghcide' 'haskell-hls-plugin-api' 'haskell-lens'
         'haskell-lsp' 'haskell-retrie' 'haskell-shake' 'haskell-syb' 'haskell-unliftio-core'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('6efd1df111398273aa4cfbc269381d84f0844d06e881f8491e1aaf1b40b16eb8aec30ac336ba0b673e00e6ed23a391774c0da6073181795577c5e4f9004ff2f5')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # https://github.com/haskell/haskell-language-server/issues/1808
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
