# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hOpenPGP
pkgname=haskell-hopenpgp
pkgver=2.9.6
pkgrel=14
pkgdesc="Native Haskell implementation of OpenPGP (RFC4880)"
url="http://floss.scru.org/hOpenPGP/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-asn1-encoding' 'haskell-attoparsec'
         'haskell-base16-bytestring' 'haskell-bifunctors' 'haskell-bz2' 'haskell-binary-conduit'
         'haskell-conduit' 'haskell-conduit-extra' 'haskell-cryptonite'
         'haskell-crypto-cipher-types' 'haskell-errors' 'haskell-hashable'
         'haskell-incremental-parser' 'haskell-ixset-typed' 'haskell-lens' 'haskell-memory'
         'haskell-monad-loops' 'haskell-nettle' 'haskell-network-uri'
         'haskell-openpgp-asciiarmor' 'haskell-prettyprinter' 'haskell-resourcet' 'haskell-split'
         'haskell-time-locale-compat' 'haskell-unliftio-core' 'haskell-unordered-containers'
         'haskell-zlib')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('c9a833e9d56fa0d1ab87be54e3a7b23f4bb93f737ff74d4f99356fcd14ef54afea7db58d3a4b1fd4b382a23ab9153f95ceb4397f03a5c32e4e71355a9e46541c')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -fnetwork-uri
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
