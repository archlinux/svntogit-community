# Maintainer: David Runge <dave@sleepmap.de>

_name=hosc
pkgname=haskell-hosc
pkgver=0.17
pkgrel=26
pkgdesc="Haskell Open Sound Control"
arch=('x86_64')
url="https://github.com/rd--/hosc"
license=('GPL')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-data-binary-ieee754' 'haskell-network')
makedepends=('ghc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rd--/hosc/archive/${pkgver}.tar.gz")
sha512sums=('4f11fef8fd06114d28cd8d84b6c2ff2dcb863d96c8b83f9f458404a6fc7a3c550919c18c4dcfde912587c9eb077ba778e40425b9356d051c4bc20979607664a9')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir="/usr/share/doc/${pkgname}" \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd "$pkgname-$pkgver"
  runhaskell Setup test
}

package() {
  cd "$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
