# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=haskell-hpack
_hkgname=hpack
pkgver=0.18.1
pkgrel=7
pkgdesc="An alternative format for Haskell packages"
url="https://github.com/sol/hpack#readme"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-compat' 'haskell-glob' 'haskell-text'
         'haskell-unordered-containers' 'haskell-yaml')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('679fbb1f921b808828a287f2314d548edd230afa2ca814d7c904fcb09563bdcb88c702077fedc539a5627659b8627d231d7421668dcb3e5c195aa71867153d3d')

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "${srcdir}/${_hkgname}-${pkgver}"
  
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
