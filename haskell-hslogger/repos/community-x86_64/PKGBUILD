# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hslogger
pkgname=haskell-hslogger
pkgver=1.1.0
pkgrel=6
pkgdesc="Versatile logging framework"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('ghc=7.0.3-2' 'haskell-mtl=2.0.1.0' 'haskell-network=2.3.0.2')
options=('strip')
install=${pkgname}.install
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
md5sums=('8039e079338dae19e1273bbd73332014')

build() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    runhaskell Setup configure -p --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O \
	--enable-split-objs --enable-shared --libsubdir=\$compiler/site-local/\$pkgid \
	--enable-library-profiling
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
    depends=("ghc=${_ghcver}" 'haskell-mtl=2.0.1.0' 'haskell-network=2.3.0.2')

    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
