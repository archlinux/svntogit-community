# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=HsOpenSSL
pkgname=haskell-hsopenssl
pkgver=0.11.4.14
pkgrel=4
pkgdesc="Partial OpenSSL binding for Haskell"
url="https://github.com/vshabanov/HsOpenSSL"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-network' 'openssl')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('bc1ed25df06d7ee1267f6880e02a6a194ee8a0c7f041b3e62999f2ea0ecd90734f6cb9c8348eb9e82ccf0c437035c732941c5a6e638c7d2e86a27a1a359561c6')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
