# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-core
pkgname=haskell-hspec-core
pkgver=2.4.7
pkgrel=3
pkgdesc="A Testing Framework for Haskell"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-quickcheck' 'haskell-ansi-terminal' 'haskell-async'
         'haskell-call-stack' 'haskell-hspec-expectations' 'haskell-quickcheck-io'
         'haskell-random' 'haskell-setenv' 'haskell-tf-random')
makedepends=('ghc' 'haskell-hspec-meta' 'haskell-silently' 'haskell-temporary')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d3f4dc6e109443839a7c4f38200a846ea3302344cac954b1fb74f4d64bf641a3f3cd8a39ffd347291ca4260b6bff074887ba77e248373d1079df3dbe98632b74')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
