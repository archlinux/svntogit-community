# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-meta
pkgname=haskell-hspec-meta
pkgver=2.9.3
pkgrel=53
pkgdesc="A version of Hspec which is used to test Hspec itself"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-call-stack' 'haskell-clock' 'haskell-ghc'
         'haskell-quickcheck' 'haskell-quickcheck-io' 'haskell-random' 'haskell-setenv')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d6205a4aead9c8824a89b18f41d2513a0115c76f9130b7100e349f2b8c91dfef34c704ba169f5b103895d0144a8fdf79eaf46abf1c7447da7d634eb3b7c8e80b')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
