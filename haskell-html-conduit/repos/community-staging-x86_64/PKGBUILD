# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=html-conduit
pkgname=haskell-html-conduit
pkgver=1.2.1.2
pkgrel=35
pkgdesc="Parse HTML documents using xml-conduit datatypes"
url="https://github.com/snoyberg/xml"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-text' 'haskell-resourcet' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-xml-conduit' 'haskell-tagstream-conduit' 'haskell-xml-types')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('73d66bc08dae94940485c1da27cb75ef008cde4832203bc3d10c7307af597bbad5e717d19a0ab328a6e7b716275d3f4f2e8a17dec8b5c84016921e1c2f3ce8a2')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
