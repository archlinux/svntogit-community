# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=html-conduit
pkgname=haskell-html-conduit
pkgver=1.3.0
pkgrel=19
pkgdesc="Parse HTML documents using xml-conduit datatypes"
url="https://github.com/snoyberg/xml"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-resourcet' 'haskell-conduit' 'haskell-xml-conduit'
         'haskell-tagstream-conduit' 'haskell-xml-types')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('2023e8b58cf69f3431cd154010ce44664f253ae4d9bb84bd4bf6b2b88b69a77591c190b67df62bd9775b99334fc765b2b69d5f3b181a035031f79950f632bdd6')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
