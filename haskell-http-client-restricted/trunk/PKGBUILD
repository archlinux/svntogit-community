# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=http-client-restricted
pkgname=haskell-http-client-restricted
pkgver=0.0.3
pkgrel=8
pkgdesc="Restricting the servers that http-client will use"
url="https://hackage.haskell.org/package/http-client-restricted"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-connection' 'haskell-data-default' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-network' 'haskell-network-bsd' 'haskell-utf8-string')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('6734f674b6a026f1cbba495b892cd98892584817db69bc36db07f4179e6ae4841055afa37a4841edff7c30270f9977b5700ecd9640ca812d213c575fb4ffbf86')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< 0.7/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
