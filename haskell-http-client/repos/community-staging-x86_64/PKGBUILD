# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-client
pkgname=haskell-http-client
pkgver=0.7.1
pkgrel=28
pkgdesc="An HTTP client engine"
url="https://github.com/snoyberg/http-client"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-case-insensitive' 'haskell-cookie'
         'haskell-http-types' 'haskell-memory' 'haskell-mime-types'
         'haskell-network' 'haskell-network-uri' 'haskell-random' 'haskell-streaming-commons')
makedepends=('ghc' 'haskell-async' 'haskell-hspec' 'haskell-monad-control' 'haskell-zlib')
checkdepends=('httpbin' 'pifpaf')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('82f9b85a5da976f8bc965a625dca2e1795bb53f88babf830aa82b511046da223fc1966d139c2d1c3d1f55103d7130715bab38a9280ec7dd9b579ead47fc068eb')

prepare() {
    cd $_hkgname-$pkgver
    # https://github.com/postmanlabs/httpbin/issues/617
    sed -i 's|http://httpbin.org/redirect-to|http://127.0.0.1:5000/redirect-to|' test/Network/HTTP/ClientSpec.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid -fnetwork-uri
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    pifpaf run httpbin -- runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
