# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-client
pkgname=haskell-http-client
pkgver=0.7.13
pkgrel=1
pkgdesc="An HTTP client engine"
url="https://github.com/snoyberg/http-client"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base64-bytestring' 'haskell-blaze-builder'
         'haskell-case-insensitive' 'haskell-cookie' 'haskell-http-types' 'haskell-iproute'
         'haskell-mime-types' 'haskell-network' 'haskell-network-uri' 'haskell-random'
         'haskell-streaming-commons')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-discover' 'haskell-monad-control' 'haskell-zlib')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('cf78d47f64dbc69192919201ce9712b43ed9da00498281dc51b76e3753f521e4908c7676c3500589e8a3aa2c8ca013d3e16e3a600f5e27e362429acd5699e361')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid -fnetwork-uri
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
