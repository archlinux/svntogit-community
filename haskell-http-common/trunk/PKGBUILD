# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=http-common
pkgname=haskell-http-common
pkgver=0.8.3.4
pkgrel=107
pkgdesc="Common types for HTTP clients and servers"
url="https://github.com/aesiniath/http-common"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base64-bytestring' 'haskell-blaze-builder' 'haskell-case-insensitive'
         'haskell-network' 'haskell-random' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8b344d80ecd5cf54eed44e281dadda0be401f88af7def443d172c3bde9c702e5137f09ab4bf3695e43ef6598508e94e80c12989568ca0c876d68479d553ee876')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
