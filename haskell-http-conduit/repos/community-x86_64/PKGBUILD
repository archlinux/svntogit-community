# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-conduit
pkgname=haskell-http-conduit
pkgver=2.3.1
pkgrel=32
pkgdesc="HTTP client package with conduit interface and HTTPS support"
url="http://www.yesodweb.com/book/http-conduit"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-resourcet' 'haskell-unliftio-core')
makedepends=('ghc' 'haskell-blaze-builder' 'haskell-case-insensitive' 'haskell-connection'
             'haskell-cookie' 'haskell-data-default-class' 'haskell-hunit' 'haskell-hspec'
             'haskell-lifted-base' 'haskell-network' 'haskell-streaming-commons' 'haskell-temporary'
             'haskell-unliftio' 'haskell-utf8-string' 'haskell-wai' 'haskell-wai-conduit'
             'haskell-warp' 'haskell-warp-tls')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d63fc7e8a2ee7eab305205a633829d17433c7fb6db1f32bd32bdb260abdec3b79ac86f54effe98c5c7c904d5c4b426b887ef8bc1c4cbc33cbb3a5813b24efc28')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
