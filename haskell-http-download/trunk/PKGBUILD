# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=http-download
pkgname=haskell-http-download
pkgver=0.1.0.0
pkgrel=26
pkgdesc="Verified downloads with retries"
url="https://github.com/commercialhaskell/stack"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base64-bytestring' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-cryptonite' 'haskell-cryptonite-conduit' 'haskell-exceptions'
         'haskell-http-client' 'haskell-http-conduit' 'haskell-http-types' 'haskell-memory'
         'haskell-path' 'haskell-path-io' 'haskell-retry' 'haskell-rio' 'haskell-rio-prettyprint')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-discover')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0d8c768ac87c9c2fdfe7503e88bbc6135e6edd0938790b46849346f86c62f5416abd18dcb14349c2624a956990cac8b66e93c3ef5043ffc60e1df7f6dab950e6')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
