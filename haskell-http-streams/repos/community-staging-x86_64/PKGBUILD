# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=http-streams
pkgname=haskell-http-streams
pkgver=0.8.9.4
pkgrel=24
pkgdesc="An HTTP client using io-streams"
url="https://github.com/aesiniath/http-streams"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base64-bytestring' 'haskell-blaze-builder'
         'haskell-case-insensitive' 'haskell-io-streams' 'haskell-hsopenssl'
         'haskell-openssl-streams' 'haskell-unordered-containers' 'haskell-aeson'
         'haskell-http-common' 'haskell-network' 'haskell-network-uri')
makedepends=('ghc')
checkdepends=('haskell-hunit' 'haskell-aeson-pretty' 'haskell-hspec'
             'haskell-hspec-expectations' 'haskell-random' 'haskell-snap-core'
             'haskell-snap-server' 'haskell-system-fileio' 'haskell-system-filepath')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8448fba398ae323f1bdd44c8cbb02ba678ffc9d06f26d4e46c8c1f1de849a83a9fef697ffa5650ef58730ad81714775046a9b0857e760112ef25b418e2b5d0fe')

build() {
    cd $_hkgname-$pkgver
    
    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
