# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hyphenation
pkgname=haskell-hyphenation
pkgver=0.8.2
pkgrel=97
pkgdesc="Configurable Knuth-Liang hyphenation"
url="https://github.com/ekmett/hyphenation"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-file-embed' 'haskell-unordered-containers' 'haskell-zlib')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('35e96a966d1eaa5e3e66e95e1a3fff5a2522adb9a1bc6c34c823bb34b936e1f6891fe987a808bb683d4c832f33f2bbb22bc8d259ffc4380fc64e889ccb2f23c0')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
