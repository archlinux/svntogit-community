# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=incremental-parser
pkgname=haskell-incremental-parser
pkgver=0.3.3
pkgrel=2
pkgdesc="Generic parser library capable of providing partial results from partial input."
url="https://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-monoid-subclasses')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-checkers' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2a1b1c0831a45c242537c880e955290bf32d8422375a96675fb1028ac304fdf5cc4cf9349076202da2573ef2adf7e4d3f3135fdf63aae2656cbec37cf7c9bf28')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.txt"
}
