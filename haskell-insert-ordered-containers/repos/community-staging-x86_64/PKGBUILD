# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=insert-ordered-containers
pkgname=haskell-insert-ordered-containers
pkgver=0.2.1.0
pkgrel=143
pkgdesc="Associative containers retating insertion order for traversals."
url="https://github.com/phadej/insert-ordered-containers"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-compat' 'haskell-hashable' 'haskell-lens'
         'haskell-semigroupoids' 'haskell-unordered-containers')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('baff8b23700a7d0209810208183d9856c0a5219018c5f09e82f8820ac87630073543da19e1198fc42f3feec143895707e5e33fdf1edbf8f4e6c441271cc3243b')

prepare() {
    sed -e 's/, semigroupoids         >=4.3      && <5.2/, semigroupoids         >=4.3/' \
        -e 's/, aeson                 >=1.0.0.0  && <1.2/, aeson                 >=1.0.0.0/' \
        -e 's/base                  >=4.6      && <4.10/base                  >=4.6/' \
        -e 's/<.*4.16/<5/' \
        -e '/semigroups/d' \
        -e 's/<.*0.10/<1/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
