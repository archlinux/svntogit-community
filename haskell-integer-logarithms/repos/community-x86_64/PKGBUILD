# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=integer-logarithms
pkgname=haskell-integer-logarithms 
pkgver=1.0.3
pkgrel=6
pkgdesc="Integer logarithms"
url="https://github.com/Bodigrim/integer-logarithms"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b211127620bb52398ae7e64281f991d9dd016d5f74ff5d939275afea15689b01f82577efeaa2dfcdf4230cfea841f6c14f731ba16d38192f48e2e7ae90e704f3')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *4.13/<5/;s/< *0.6/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -finteger-gmp -f-check-bounds
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
