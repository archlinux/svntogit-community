# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=interpolatedstring-perl6
pkgname=haskell-interpolatedstring-perl6
pkgver=1.0.2
pkgrel=167
pkgdesc="QuasiQuoter for Perl6-style multi-line interpolated strings"
url="https://github.com/audreyt/interpolatedstring-perl6"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-src-meta')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('660e2fafe2f5a03baf16d56fa298e9fe0df677023fff7b3e5d8879910a76066b95a753fffc0c6374fd405de67f54c1410d4542f6d85823e8e87277bde28624a7')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
