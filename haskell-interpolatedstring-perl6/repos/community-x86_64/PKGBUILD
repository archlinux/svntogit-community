# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=interpolatedstring-perl6
pkgname=haskell-interpolatedstring-perl6
pkgver=1.0.1
pkgrel=20
pkgdesc="QuasiQuoter for Perl6-style multi-line interpolated strings"
url="https://github.com/audreyt/interpolatedstring-perl6"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-src-meta')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e3b793515bda8c04f9b6ff0bc61614fb1e9e2cbad79592333c09d7d134e3d83ec341049b68debf391fac0760b6f01aa8c6817d69207e0db42e239e9eb3f2385d')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
