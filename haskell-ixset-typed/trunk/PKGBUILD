# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ixset-typed
pkgname=haskell-ixset-typed
pkgver=0.4.0.1
pkgrel=57
pkgdesc="Efficient relational queries on Haskell sets"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-safecopy' 'haskell-syb')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        ghc-8.8.patch::https://github.com/well-typed/ixset-typed/pull/13.patch)
sha512sums=('48ed1cc65b7bc3522eab71727f3e11052c29cbb1eef4f7c043fd6c3fc43547909390cb36f823e109a15a136242f3e780f66f76ede29c2a7bc39f9bdf6c51ecd6'
            'a4eb22e9abcf6b9c5b0d82d1826d861910e19c504af8e3e1ec0ab94b8c259df851ff56dcf8a41539e2df1fa9e97cf272df65e1bca354966e55484b3043a65818')

prepare() {
    cd $_hkgname-$pkgver
    # Ignore change in .travis.yml
    patch -p1 -i ../ghc-8.8.patch || :
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
