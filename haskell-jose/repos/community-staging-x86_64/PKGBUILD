# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=jose
pkgname=haskell-jose
pkgver=0.8.2.0
pkgrel=35
pkgdesc="Javascript Object Signing and Encryption and JSON Web Token library"
url="https://github.com/frasertweedale/hs-jose"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base64-bytestring' 'haskell-concise'
         'haskell-cryptonite' 'haskell-lens' 'haskell-memory' 'haskell-monad-time'
         'haskell-safe' 'haskell-aeson' 'haskell-unordered-containers' 'haskell-network-uri'
         'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-x509' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1af3b5231165296344495d2c8e501c1816df3976b3f298c918a50a0f8f525809d20d60cb34b9019747639e98c197aa828b1f512c409ab71fc2a53aed1170db5b')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
