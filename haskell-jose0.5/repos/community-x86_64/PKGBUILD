# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=jose
pkgname=haskell-jose0.5
pkgver=0.5.0.5
pkgrel=7
pkgdesc="Javascript Object Signing and Encryption and JSON Web Token library (legacy 0.5.x version)"
url="https://github.com/frasertweedale/hs-jose"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base64-bytestring' 'haskell-byteable'
         'haskell-cryptonite' 'haskell-lens' 'haskell-memory' 'haskell-monad-time' 'haskell-mtl'
         'haskell-semigroups' 'haskell-safe' 'haskell-aeson' 'haskell-unordered-containers'
         'haskell-text' 'haskell-network-uri' 'haskell-quickcheck' 'haskell-quickcheck-instances'
         'haskell-x509' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('06051e8a73e7f6d0c41fa03f2e384566531b0a43fa0f77bc620b31e5ec117d41b20bcce9b518f30da878ffdf804a047953484837f698500309fb43adafea4db6')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    mv "$pkgdir"/usr/bin/{,jose05-}example
}
