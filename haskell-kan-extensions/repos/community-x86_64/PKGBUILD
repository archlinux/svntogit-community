# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=kan-extensions
pkgname=haskell-kan-extensions
pkgver=5.1
pkgrel=33
pkgdesc="Kan extensions, Kan lifts, the Yoneda lemma, and (co)density (co)monads"
url="https://github.com/ekmett/kan-extensions/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-adjunctions' 'haskell-comonad' 'haskell-contravariant'
         'haskell-distributive' 'haskell-free' 'haskell-profunctors'
         'haskell-semigroupoids' 'haskell-tagged' 'haskell-transformers-compat')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('9a9390cc099c512ac2a4c1a0d9b4ee33690a16ef2cec32877f24778239a60cdf6b0ba3464d15e8a23a6112e95fe124d6517934aba5e5c2b3ec70714c0a18b8bb')

prepare() {
    sed -i -e '/ fail /d' -e 's/<.*0.6/<1/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
