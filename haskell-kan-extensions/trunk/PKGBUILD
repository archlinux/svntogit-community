# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=kan-extensions
pkgname=haskell-kan-extensions
pkgver=5.0.2
pkgrel=11
pkgdesc="Kan extensions, Kan lifts, various forms of the Yoneda lemma, and (co)density (co)monads"
url="https://github.com/ekmett/kan-extensions/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-adjunctions" "haskell-comonad" "haskell-contravariant"
         "haskell-distributive" "haskell-free" "haskell-mtl" "haskell-semigroupoids"
         "haskell-tagged")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bce02cb4ce3a797682fd369962ceee54b8c561a2a637942a678e7ef6d0757a5074cfd25d22f6d64b5475729fa00f464b635026ee7765be04a4fd0289eb17583d')

prepare() {
    sed -i '/fail/d' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
