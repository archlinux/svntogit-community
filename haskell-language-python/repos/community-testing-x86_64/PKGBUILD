# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=language-python
pkgname=haskell-language-python
pkgver=0.5.4
pkgrel=8
pkgdesc="Parsing and pretty printing of Python code."
url="https://hackage.haskell.org/package/${_hkgname}"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-monads-tf' 'haskell-utf8-string')
makedepends=('happy' 'alex' 'ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        ghc-8.2.1.patch)
sha512sums=('adbe81eeb4a161c8e685c3dacdee0924f1193fa291a753e1608f0c94effc90b77df60fee1e46e791516d01ea0153f243c13fb2fdd24d688cfe9ce8ac9ed44059'
            'ce812b6937158977a85c2b5c1118a79779570bd4023ac58b22d9e2960e24be98b107e733c3ed90ed9977d20e757d56c6e2e3ea48d473f3e6d42589fa2c8281ea')

prepare() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    patch -p1 -i ../ghc-8.2.1.patch

    sed -i '/import Data.Data/i import Prelude hiding ((<>))' src/Language/Python/Common/SrcLocation.hs
    sed -i '/import Language.Python.Common.AST/i import Prelude hiding ((<>))' src/Language/Python/Common/PrettyAST.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsplit-base
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
