# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=haskell-language-server
pkgver=1.6.1.1
pkgrel=9
pkgdesc="LSP server for GHC"
url="https://github.com/haskell/haskell-language-server#readme"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-async'
         'haskell-base16-bytestring' 'haskell-cryptohash-sha1' 'haskell-data-default'
         'haskell-extra' 'haskell-ghc' 'haskell-ghc-api-compat' 'haskell-ghc-paths' 'haskell-ghcide'
         'haskell-githash' 'haskell-gitrev' 'haskell-hashable' 'haskell-hie-bios' 'haskell-hiedb' 
         'haskell-hls-alternate-number-format-plugin' 'haskell-hls-brittany-plugin'
         'haskell-hls-call-hierarchy-plugin' 'haskell-hls-class-plugin' 'haskell-hls-eval-plugin'
         'haskell-hls-explicit-imports-plugin' 'haskell-hls-floskell-plugin'
         'haskell-hls-fourmolu-plugin' 'haskell-hls-ormolu-plugin' 'haskell-hls-graph'
         'haskell-hls-haddock-comments-plugin' 'haskell-hls-hlint-plugin'
         'haskell-hls-module-name-plugin' 'haskell-hls-ormolu-plugin' 'haskell-hls-plugin-api'
         'haskell-hls-pragmas-plugin' 'haskell-hls-qualify-imported-names-plugin'
         'haskell-hls-refine-imports-plugin' 'haskell-hls-rename-plugin' 'haskell-hls-retrie-plugin'
         'haskell-hls-selection-range-plugin' 'haskell-hls-splice-plugin'
         'haskell-hls-stylish-haskell-plugin' 'haskell-hls-tactics-plugin' 'haskell-hslogger'
         'haskell-lens' 'haskell-lens-aeson' 'haskell-lsp' 'haskell-optparse-applicative'
         'haskell-optparse-simple' 'haskell-regex-tdfa' 'haskell-safe-exceptions'
         'haskell-sqlite-simple' 'haskell-temporary' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-hspec-expectations' 'haskell-lsp-test'
             'haskell-lsp-types')
checkdepends=('cabal-install')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('da072693c6103d9a54c6360e8fe58970aa6da7b3d413c7cbd53bb86d52ed0cc100ef058bf75e417a9fbc0599f24b3e8e8d5b7c7937403cd86ed5997f7283595f')

prepare() {
  cd $pkgname-$pkgver
  find test/testdata -name \*.cabal -exec uusi --add-options-all=-dynamic {} \;
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fignore-plugins-ghc-bounds -fclass -fcallHierarchy -fhaddockComments -feval -fimportLens \
    -frefineImports -frename -fretrie -ftactic -fhlint -fmoduleName -fpragmas -fsplice \
    -falternateNumberFormat -fqualifyImportedNames -fselectionRange \
    -ffloskell -ffourmolu -formolu -fstylishHaskell -fbrittany \
    -fdynamic

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  PATH="$PWD/dist/build/haskell-language-server:$PWD/dist/build/haskell-language-server-wrapper:$PATH" \
  LD_LIBRARY_PATH="$PWD/dist/build" \
  runhaskell Setup test || echo "Tests failed"
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
