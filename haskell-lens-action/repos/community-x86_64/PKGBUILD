# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lens-action
pkgname=haskell-lens-action
pkgver=0.2.4
pkgrel=81
pkgdesc="Monadic Getters and Folds"
url="https://github.com/ekmett/lens-action"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-comonad' 'haskell-contravariant' 'haskell-lens'
         'haskell-profunctors' 'haskell-semigroupoids')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9fde7be03c4cc289e19cee5dd5695c247a71c2585f8e085033e862b158ddc062a138f500b7dd71e3eb9f51869759b83fb0f88309db53f24300aaa0076322b883')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
