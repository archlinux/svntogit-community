# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lens-aeson
pkgname=haskell-lens-aeson
pkgver=1.0.0.5
pkgrel=5
pkgdesc="Law-abiding lenses for aeson"
url="http://github.com/lens/lens-aeson/"
license=('MIT')
arch=('i686' 'x86_64')
depends=('ghc=8.0.1' 'haskell-aeson' 'haskell-attoparsec' 'haskell-lens' 'haskell-scientific'
         'haskell-text' 'haskell-unordered-containers' 'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
md5sums=('e3c948acadb91971443852ebaa13d0c5')

prepare() {
    sed -i 's/aeson                >= 0.7.0.5   && < 0.10,/aeson                >= 0.7.0.5   \&\& < 1.2,/' \
        ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-test-doctests
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
