# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=lens
pkgname=haskell-lens
pkgver=4.19.2
pkgrel=52
pkgdesc="Lenses, Folds and Traversals"
url="https://github.com/ekmett/lens/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-bifunctors' 'haskell-call-stack' 'haskell-comonad'
         'haskell-contravariant' 'haskell-distributive' 'haskell-free'
         'haskell-hashable' 'haskell-kan-extensions' 'haskell-parallel' 'haskell-profunctors'
         'haskell-reflection' 'haskell-semigroupoids' 'haskell-tagged' 'haskell-th-abstraction'
         'haskell-transformers-compat' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-generic-deriving' 'haskell-hunit'
             'haskell-quickcheck' 'haskell-simple-reflect' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f1627c950457c38b0363a71717b76c0c84eafdcebaf14904da632ea295195dee1fd11db07ca524988729254b0e018da4417e40c0cb4f8ff086fbe412ce89f4c6')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/nats/d;/semigroups *>= 0.9/d' $_hkgname.cabal
    uusi $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-j -ftrustworthy -f-safe -ftest-templates -ftest-properties -ftest-hunit \
            -ftest-doctests -f-dump-splices -f-old-inline-pragmas -finlining \
            -f-benchmark-uniplate
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
