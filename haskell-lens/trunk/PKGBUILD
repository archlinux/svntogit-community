# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=lens
pkgname=haskell-lens
pkgver=5.0.1
pkgrel=47
pkgdesc="Lenses, Folds and Traversals"
url="https://github.com/ekmett/lens/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-assoc' 'haskell-base-orphans' 'haskell-bifunctors'
         'haskell-call-stack' 'haskell-comonad' 'haskell-contravariant' 'haskell-distributive'
         'haskell-free' 'haskell-hashable' 'haskell-indexed-traversable'
         'haskell-indexed-traversable-instances' 'haskell-kan-extensions' 'haskell-parallel'
         'haskell-profunctors' 'haskell-reflection' 'haskell-semigroupoids' 'haskell-strict'
         'haskell-tagged' 'haskell-th-abstraction' 'haskell-these'
         'haskell-transformers-compat' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hunit' 'haskell-quickcheck' 'haskell-simple-reflect'
             'haskell-test-framework' 'haskell-test-framework-hunit'
             'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('03b5407003e19ead5778938a37ad25c50d2a7e5acae2e9b644bf07267357a994df11328a4d0ee0f4d3b9b8671071773e9a520ac14125f6b0a22a28d9efd988fa')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u call-stack $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-j -ftrustworthy -f-safe -ftest-templates -ftest-properties -ftest-hunit \
            -ftest-doctests -f-dump-splices -f-old-inline-pragmas -finlining \
            -f-benchmark-uniplate
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
