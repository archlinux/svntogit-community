# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributer: Colin Woodbury <colingw@gmail.com>
# Contributer: Leif Warner <abimelech@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=libmpd
pkgname=haskell-libmpd
pkgver=0.10.0.0
pkgrel=126
pkgdesc='Client library for MPD, the Music Player Daemon'
url='https://hackage.haskell.org/package/libmpd'
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-data-default-class' 'haskell-network'
         'haskell-safe-exceptions' 'haskell-utf8-string')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-discover')
#source=(${pkgname}-${pkgver}.tar.gz::https://github.com/vimus/libmpd-haskell/archive/v${pkgver}.tar.gz)
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('33b2af4cbbcc2c6bb2f05ada685d8870ca5132559f8f0505da39f0deb290f1e15028d783c9b28fc51f785ed86878fbf40bb0be0487612cc2b600eaf619114c11')

build() {
  cd ${_hkgname}-${pkgver}
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd ${_hkgname}-${pkgver}
  runhaskell Setup test --show-details=direct
}

package() {
  cd ${_hkgname}-${pkgver}
  install -Dm 744 register.sh "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm 755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
  runhaskell Setup copy --destdir="$pkgdir"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md changelog.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
}

# vim: ts=2 sw=2 et:
