# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributer: Colin Woodbury <colingw@gmail.com>
# Contributer: Leif Warner <abimelech@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=libmpd
pkgname=haskell-libmpd
pkgver=0.9.0.7
pkgrel=3
pkgdesc='Client library for MPD, the Music Player Daemon'
url='https://hackage.haskell.org/package/libmpd'
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'gmp' 'haskell-attoparsec' 'haskell-bytestring' 'haskell-containers' 'haskell-data-default'
         'haskell-filepath' 'haskell-mtl' 'haskell-network' 'haskell-text' 'haskell-time' 'haskell-utf8-string'
         'haskell-primitive')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('bdac04905cefd9035769c66c5726cc375697afd5e29a8c59e5bfe7b2e3d4ba4bd44f65962f879377f6dd9587771d505da5f47f8e6a36f389ebbcdf5188b3de33')

build() {
  cd ${_hkgname}-${pkgver}
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}
package() {
  cd ${_hkgname}-${pkgver}
  install -Dm 744 register.sh "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm 755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md changelog.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
}

# vim: ts=2 sw=2 et:
