# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=libyaml
pkgname=haskell-libyaml
pkgver=0.1.0.0
pkgrel=16
pkgdesc="Low-level, streaming YAML interface."
url="https://github.com/snoyberg/libyaml"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'libyaml' 'haskell-conduit' 'haskell-resourcet')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('49d5cac28ac0bed9bb9964b9f0a8addbfc3847589947ca628c16e07018ea665561f8ebff68ed2c7ee0af94b3fb4593b0aa053b8eaa010349752284cc738ac808')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsystem-libyaml
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
