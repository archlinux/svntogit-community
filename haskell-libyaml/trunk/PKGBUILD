# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=libyaml
pkgname=haskell-libyaml
pkgver=0.1.2
pkgrel=170
pkgdesc="Low-level, streaming YAML interface."
url="https://github.com/snoyberg/libyaml"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'libyaml' 'haskell-conduit' 'haskell-resourcet')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0423fb36f1fdeeb89244a81930558ad272f8ffe8161ced3cc542e180937a1e5f264f188972786830b0bace6851adca796756abe100dffa8cb969eff4cb0e3121')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -fsystem-libyaml
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
