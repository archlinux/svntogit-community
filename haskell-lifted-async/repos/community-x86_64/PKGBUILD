# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lifted-async
pkgname=haskell-lifted-async
pkgver=0.9.3.2
pkgrel=2
pkgdesc="Run lifted IO operations asynchronously and wait for their results"
url="https://github.com/maoe/lifted-async"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-constraints' 'haskell-lifted-base'
         'haskell-monad-control' 'haskell-transformers-base')
makedepends=('ghc' 'haskell-hunit' 'haskell-mtl' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-th')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('add754886084d28b37ee04f586506e4a01f10d505ad4252690e97d448f38e076d702d49b5cd11f11dd58fcf16040e57bbe51575e9592078a3585ff61677afeea')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fmonad-control-1
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
