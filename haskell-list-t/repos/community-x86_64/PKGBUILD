# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=list-t
pkgname=haskell-list-t
pkgver=1.0.5.3
pkgrel=22
pkgdesc="ListT done right"
url="https://github.com/nikita-volkov/list-t"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-foldl' 'haskell-logict' 'haskell-mmorph' 'haskell-monad-control'
         'haskell-transformers-base')
makedepends=('ghc' 'uusi' 'haskell-htf' 'haskell-base-prelude' 'haskell-mtl-prelude')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('adf3d36457d4c505bd7c9d5f81760145102d10d66503240fe24c3882947b6e48')

prepare() {
  cd $_hkgname-$pkgver
  uusi -d semigroups $_hkgname.cabal
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
