# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ListLike
pkgname=haskell-listlike
pkgver=4.7.6
pkgrel=8
pkgdesc="Generic support for list-like structures"
url="https://github.com/ddssff/listlike"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-fmlist' 'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-random')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('40f3c9628c6ca2a1b3b158adb5a30b368d806890229fcd1e42a9162dba5fec50483a4bc82eca942aeaf07bb6054d8f0355f2af0c5aadf8eabc1a327c918369f8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYRIGHT"
}
