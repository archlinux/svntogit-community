# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=loop
pkgname=haskell-loop
pkgver=0.3.0
pkgrel=69
pkgdesc="Fast loops (for when GHC can't optimize forM_)"
url="https://github.com/nh2/loop"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('c4f57547dc80a0afdb20f590564f36425b41263b420f64877b26650b8f15479c29c0d87a8bfeb751bf2abb85c019f3b3459ecda8b50ca35627e26599d284f8f8')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
}
