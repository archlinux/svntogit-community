# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lsp-test
pkgname=haskell-lsp-test
pkgver=0.13.0.0
pkgrel=17
pkgdesc="Functional test framework for LSP servers."
url="https://github.com/bubba/lsp-test#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-glob' 'haskell-aeson' 'haskell-aeson-pretty'
         'haskell-ansi-terminal' 'haskell-async' 'haskell-conduit' 'haskell-conduit-parse'
         'haskell-data-default' 'haskell-lens' 'haskell-lsp' 'haskell-lsp-types'
         'haskell-parser-combinators' 'haskell-some' 'haskell-unliftio'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hspec')
# https://github.com/bubba/lsp-test/issues/92
source=("https://github.com/bubba/lsp-test/archive/$pkgver/$pkgname-$pkgver.tar.gz")
#source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('4b9888d25aa749e1be37ac22c8a7c90e4dbb34983797f05a9be07ed775cf9a38')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  sed -i 's|dist-newstyle|dist/build/dummy-server|' test/Test.hs
}

build() {
  cd $_hkgname-$pkgver

  # We need to enable dummy-server here for tests, but it shouldn't be installed...
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fdummyserver

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # https://github.com/bubba/lsp-test/issues/93
  runhaskell Setup test || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  # Remove dummy-server
  rm -r "$pkgdir"/usr/libexec
}
