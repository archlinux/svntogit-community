# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lsp-test
pkgname=haskell-lsp-test
pkgver=0.14.1.0
pkgrel=3
pkgdesc="Functional test framework for LSP servers."
url="https://github.com/haskell/lsp/blob/master/lsp-test/README.md"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-diff' 'haskell-glob' 'haskell-aeson' 'haskell-aeson-pretty'
         'haskell-ansi-terminal' 'haskell-async' 'haskell-co-log-core' 'haskell-conduit'
         'haskell-conduit-parse' 'haskell-data-default' 'haskell-lens' 'haskell-lsp'
         'haskell-lsp-types' 'haskell-parser-combinators' 'haskell-some' 'haskell-unliftio'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('2b4ba5c8d8cfcad3f1edb8fc410078cb483e6a837023fe2746b1188f485b849d24840d9ad1cfaa06a3cee1fa41e53516d8f7fd867494cf5f1f8d3464c784c2fe')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
