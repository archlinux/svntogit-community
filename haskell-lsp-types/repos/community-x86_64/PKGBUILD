# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lsp-types
pkgname=haskell-lsp-types
pkgver=1.3.0.1
pkgrel=128
pkgdesc="Haskell library for the Microsoft Language Server Protocol, data types"
url="https://github.com/haskell/lsp"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-data-default' 'haskell-dependent-sum'
         'haskell-dependent-sum-template' 'haskell-diff' 'haskell-dlist' 'haskell-hashable'
         'haskell-hslogger' 'haskell-lens' 'haskell-network-uri' 'haskell-rope-utf16-splay'
         'haskell-scientific' 'haskell-some' 'haskell-temporary' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        lsp-types-aeson-2.patch)
sha512sums=('074a2a383efefd3754ed1c835af913e1ab5fee9f634cb343f392bc8148f1a62030777dbb1104cd0e60cec1405d2cc5067c8fb00f2e902a3ffa52601ff7b02c2b'
            '491c235382551e6c5e7969c307e275f95ca3d427eeda3143217914dba43ec498c29f83246ed42787c0700b891f4aec5759e46867986224d6304f3bd0e7460763')

prepare() {
  cd $_hkgname-$pkgver
  patch -p2 -i ../lsp-types-aeson-2.patch
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
