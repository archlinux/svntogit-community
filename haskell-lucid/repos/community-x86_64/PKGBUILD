# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lucid
pkgname=haskell-lucid
pkgver=2.9.12.1
pkgrel=48
pkgdesc="Clear to write, read and edit DSL for HTML"
url="https://github.com/chrisdone/lucid"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-hashable' 'haskell-mmorph'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-bifunctors' 'haskell-hspec' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('2572047a2711ae26b3e736ac73acb5c154d7493d47323853fda0503fa75ded35df9d7819f43879583b097718ee90235c71d8c9839c4a89ceecaa8a830b077651')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
