# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lumberjack
pkgname=haskell-lumberjack
pkgver=1.0.1.0
pkgrel=59
pkgdesc="Trek through your code forest and make logs"
url="https://github.com/GaloisInc/lumberjack"
license=("custom:ISC")
arch=('x86_64')
depends=('ghc-libs' 'haskell-contravariant' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('9d9877ef4c04f0e2530d91b105a30cda7292c22b797a86d67f7c2465474eea5ebc91d394dc17c2f7ec9d4eab6afcc6d76082adf05b93d103c05d57ffa0166453')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
