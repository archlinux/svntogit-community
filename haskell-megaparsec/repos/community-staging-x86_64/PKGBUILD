# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=megaparsec
pkgname=haskell-megaparsec
pkgver=6.4.1
pkgrel=6
pkgdesc="Monadic parser combinators"
url="https://github.com/mrkkrp/megaparsec"
license=("custom:BSD2")
arch=('x86_64')
depends=('ghc-libs' 'haskell-case-insensitive' 'haskell-mtl' 'haskell-parser-combinators'
         'haskell-scientific' 'haskell-text')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-expectations')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('24b02c5ec5ef27c923ea034b6d6dcaf49cc16e288d5b22b0befb399fbc7489d4884e4239888dd5fb70d9cb9bf99f41246fbaa7705a622874f4183fcb611df6a0')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
