# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=megaparsec
pkgname=haskell-megaparsec6
pkgver=6.5.0
pkgrel=8
pkgdesc="Monadic parser combinators (Legacy 6.x branch)"
url="https://github.com/mrkkrp/megaparsec"
license=("custom:BSD2")
arch=('x86_64')
depends=('ghc-libs' 'haskell-case-insensitive' 'haskell-parser-combinators'
         'haskell-scientific')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-hspec-expectations')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4a8194909f2779bd82226a1f8fb6cce089820fa960249fdd723a4f54aa0c0d52436b4835431327be1164a7698d59059e866b694477407fae4d93c1ae42664011')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/<.*1.0/<2/' -e 's/< *0.6/<1/' -e 's/< *2.12/<3/' $_hkgname.cabal
    sed -i '1i{-# language NoMonadFailDesugaring #-}' tests/Text/MegaparsecSpec.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
