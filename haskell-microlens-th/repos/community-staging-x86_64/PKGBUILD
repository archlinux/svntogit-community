# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=microlens-th
pkgname=haskell-microlens-th
pkgver=0.4.3.7
pkgrel=5
pkgdesc="Automatic generation of record lenses for microlens"
url="https://github.com/monadfix/microlens"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-microlens' 'haskell-th-abstraction')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a2f6c5bf7c5c4986b09c9d6f23361bf39a4e5e7bee935a2bc16a4a5f859211a57e5a9675574fd232b7b38b661da22257b8b7c6e3731988bc6e9fc952d19ab7cf')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -finlining
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
