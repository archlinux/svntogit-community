# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=mono-traversable
pkgname=haskell-mono-traversable
pkgver=1.0.6.0
pkgrel=2
pkgdesc="Type classes for mapping, folding, and traversing monomorphic containers"
url="https://github.com/snoyberg/mono-traversable"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-split' 'haskell-text' 'haskell-unordered-containers'
         'haskell-vector' 'haskell-vector-algorithms')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-foldl' 'haskell-hspec'
             'haskell-semigroups')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8b8a733d8d663d27e06e245aa73dbd21064031ab84d4687914673468a70c4524b570457cdfe98adc09270739bdb45f2a8c351cf9ce29c027b2032f9bf9ef2682')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
