# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=monoid-subclasses
pkgname=haskell-monoid-subclasses
pkgver=0.4.3.2
pkgrel=1
pkgdesc="Subclasses of Monoid"
url="https://github.com/blamario/monoid-subclasses/"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-primes' 'haskell-text' 'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('488cc6694ddb9756a1e97144baf3353db45abdabfdf66f1238b1307f55495b107844593e73037761402f1bf171bc7743b92fff385018e49f04334f3f86be3598')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "BSD3-LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/BSD3-LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/BSD3-LICENSE.txt"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
