# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=nettle
pkgname=haskell-nettle
pkgver=0.3.0
pkgrel=181
pkgdesc="Safe nettle binding"
url="https://github.com/stbuehler/haskell-nettle"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'nettle' 'haskell-byteable' 'haskell-crypto-cipher-types' 'haskell-tagged'
         'haskell-securemem')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9bc875c0674ea615f47c498bd40fcbd8ffb356e99b30bde6b0e350a9dcfed353bc21242ea625cecc23b16b617f1e163dab42740cadc49ac5c73f161fd0898ac7')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
