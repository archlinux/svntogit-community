# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=network-protocol-xmpp
pkgname=haskell-network-protocol-xmpp
pkgver=0.4.8
pkgrel=13
pkgdesc="Client library for the XMPP protocol."
url="https://john-millikin.com/software/haskell-xmpp/"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' "haskell-gnuidn" "haskell-gnutls" "haskell-gsasl" "haskell-libxml-sax"
         "haskell-monads-tf" "haskell-network" "haskell-text" "haskell-xml-types")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('029cbb1a0ea6691fd886b326eee7a24e8300bbece1f0c0f564b43b0e344f4107ca980d97342271cab0b9c1333fc445ac10f7dfae41dd2b6e6ea995bfb810311e')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
