# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=network-protocol-xmpp
pkgname=haskell-network-protocol-xmpp
pkgver=0.4.10
pkgrel=70
pkgdesc="Client library for the XMPP protocol."
url="https://git.sr.ht/~singpolyma/network-protocol-xmpp"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' "haskell-gnuidn" "haskell-gnutls" "haskell-gsasl" "haskell-libxml-sax"
         "haskell-monads-tf" "haskell-network" "haskell-network-simple" "haskell-xml-types")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('5d809eec7243b081c9b2d8f8887b7ed4e1e767ace90ec0a58142ee6ee5e53556b51d2dbd71dbcfece751f197a3255724c904f5d2727241b1d5007934c40f1297')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
