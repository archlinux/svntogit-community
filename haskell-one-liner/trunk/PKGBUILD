# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=one-liner
pkgname=haskell-one-liner
pkgver=2.1
pkgrel=37
pkgdesc="Constraint-based generics"
url="https://github.com/sjoerdvisscher/one-liner"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-contravariant' 'haskell-linear-base'
         'haskell-profunctors' 'haskell-tagged')
makedepends=('ghc' 'haskell-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e90a587686b7e6a41afd97d0f00af14e38de43812af8973cc54457364ecf67459357a947c603f5f50c7ea7fc3afc329f9611890d0c7fcb1fb578e965409af5cc')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
