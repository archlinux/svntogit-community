# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ormolu
pkgname=haskell-ormolu
pkgver=0.1.3.1
pkgrel=5
pkgdesc="A formatter for Haskell source code"
url="https://github.com/tweag/ormolu"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-ghc' 'haskell-ghc-boot' 'haskell-ghc-boot-th'
         'haskell-gitrev' 'haskell-optparse-applicative' 'haskell-syb')
makedepends=('ghc' 'haskell-hspec' 'haskell-path' 'haskell-path-io')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('87f0c096d55156263532d7877aa296a4514127c1b776bf1483dd9464f96f2285e1c1a269dc563d5d9b1236b0a88fd9de97fc8d99ff01a065b99c5b3425ef5a7a')

prepare() {
  cd $_hkgname-$pkgver
  sed -i -e 's/ghc-lib-parser/ghc, ghc-boot, ghc-boot-th/' $_hkgname.cabal
  sed -i 's/using ghc-lib-parser/using ghc/;s/VERSION_ghc_lib_parser/VERSION_ghc/' app/Main.hs
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
