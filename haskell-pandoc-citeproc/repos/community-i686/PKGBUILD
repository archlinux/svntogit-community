# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=pandoc-citeproc
pkgname=haskell-pandoc-citeproc
pkgver=0.8.1.3
pkgrel=12
pkgdesc="Supports using pandoc with citeproc"
url="http://hackage.haskell.org/package/${_hkgname}"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=7.10.3" "haskell-aeson" "haskell-aeson-pretty" "haskell-attoparsec"
         "haskell-data-default" "haskell-hs-bibutils" "haskell-mtl"
         "haskell-old-locale" "pandoc" "haskell-pandoc-types" "haskell-parsec"
         "haskell-rfc5051" "haskell-setenv" "haskell-split" "haskell-syb"
         "haskell-tagsoup" "haskell-temporary" "haskell-text" "haskell-unordered-containers"
         "haskell-vector" "haskell-xml-conduit" "haskell-yaml")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('aa15ff9cbb9c1cca504943c94cd891e23944cb0106172886821cb048a9726c7d')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-debug -f-test_citeproc -f-unicode_collation -f-embed_data_files -fbibutils
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
