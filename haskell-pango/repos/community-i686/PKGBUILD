# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-pango
pkgver=0.12.1
pkgrel=2
pkgdesc="Binding to the pango library for Gtk2Hs."
url="http://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'pango' 'haskell-glib' 'haskell-cairo')
makedepends=("gtk2hs-buildtools=$pkgver")
provides=('gtk2hs-pango')
replaces=('gtk2hs-pango')
conflicts=('gtk2hs-pango')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/pango/${pkgver}/pango-${pkgver}.tar.gz)
install=gtk2hs-pango.install
md5sums=('a9127d5b1dd5935ecdb0a5cc2d406f48')

build() {
    cd ${srcdir}/pango-${pkgver}
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/haskell-pango \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
    depends=("ghc=${_ghcver}" 'pango' 'haskell-glib' 'haskell-cairo')

    cd ${srcdir}/pango-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-pango/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-pango/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/haskell-pango/html ${pkgdir}/usr/share/doc/ghc/html/libraries/pango
    runhaskell Setup copy --destdir=${pkgdir}
}
