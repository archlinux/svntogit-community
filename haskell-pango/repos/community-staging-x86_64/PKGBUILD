# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-pango
pkgver=0.13.1.0
pkgrel=5
pkgdesc="Binding to the pango library for Gtk2Hs."
url="http://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('i686' 'x86_64')
# deps adjusted to specific versions in package()
depends=('ghc' 'pango' 'haskell-glib' 'haskell-cairo')
makedepends=("gtk2hs-buildtools")
provides=('gtk2hs-pango')
replaces=('gtk2hs-pango')
conflicts=('gtk2hs-pango')
options=('strip' 'staticlibs')
install=gtk2hs-pango.install
source=(http://hackage.haskell.org/packages/archive/pango/${pkgver}/pango-${pkgver}.tar.gz)
md5sums=('14ef94d811e8b32ce9626c5b289bd438')

build() {
  cd ${srcdir}/pango-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/haskell-pango \
    --libsubdir=\$compiler/site-local/\$pkgid
  sed -i 's|import Foreign.C.Types.*(CULong, CUInt)|import Foreign.C.Types (CULong(..), CUInt(..))|' Graphics/Rendering/Pango/Types.chs
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

_update_deps() {
    _ver=`pacman -Q $1 | cut -f2 -d\  | cut -f1 -d-`
    for i in `seq 0 $(expr ${#depends[@]} - 1)`; do
        if [ ${depends[$i]} == $1 ]; then
            depends[$i]="$1=${_ver}"
        fi
    done
}

package() {
  _update_deps ghc
  _update_deps haskell-glib
  _update_deps haskell-cairo

  cd ${srcdir}/pango-${pkgver}
  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/gtk2hs-pango/register.sh
  install -m744 unregister.sh ${pkgdir}/usr/share/haskell/gtk2hs-pango/unregister.sh
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/haskell-pango/html ${pkgdir}/usr/share/doc/ghc/html/libraries/pango
  runhaskell Setup copy --destdir=${pkgdir}
}
