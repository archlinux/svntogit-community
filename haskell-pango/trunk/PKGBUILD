# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

_hkgname=pango
pkgname=haskell-pango
pkgver=0.13.8.2
pkgrel=36
pkgdesc="Binding to the pango library for Gtk2Hs."
url="https://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('x86_64')
depends=('ghc-libs' 'pango' 'haskell-glib' 'haskell-cairo')
makedepends=('ghc' "haskell-gtk2hs-buildtools" "mesa")
provides=('gtk2hs-pango')
replaces=('gtk2hs-pango')
conflicts=('gtk2hs-pango')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('32a9030385ffc049503c9a39a00f48650d1a98b0f1beacb798072e0af19371d5178df3001719a67bbfb4a8e597fa11f86b7d419465087c63ee2962eee93f9567')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
}
