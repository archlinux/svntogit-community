# Maintainer: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=parsec
pkgname=haskell-parsec
pkgver=3.1.7
pkgrel=4
pkgdesc="Monadic parser combinators"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
options=('staticlibs')
depends=('ghc' 'sh' 'haskell-mtl' 'haskell-text')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install=${pkgname}.install
md5sums=('752664d7762bad11b46a392119658016')

build() {
  cd ${_hkgname}-${pkgver}

  runhaskell Setup configure -O -p \
    --enable-split-objs \
    --enable-shared \
    --prefix=/usr \
    --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid

  runhaskell Setup build

  runhaskell Setup haddock

  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

_update_deps() {
    _ver=`pacman -Q $1 | cut -f2 -d\  | cut -f1 -d-`
    for i in `seq 0 $(expr ${#depends[@]} - 1)`; do
        if [ ${depends[$i]} == $1 ]; then
            depends[$i]="$1=${_ver}"
        fi
    done
}

package() {
  cd ${_hkgname}-${pkgver}

  _update_deps ghc
  _update_deps haskell-mtl
  _update_deps haskell-text

  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh

  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}

  runhaskell Setup copy --destdir=${pkgdir}

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}

