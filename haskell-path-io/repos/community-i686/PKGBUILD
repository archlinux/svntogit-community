# $Id: PKGBUILD 162763 2016-02-20 14:34:32Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

_hkgname=path-io
pkgname=haskell-path-io
pkgver=1.3.1
pkgrel=3
pkgdesc="Interface to ‘directory’ package for users of ‘path’"
url="https://github.com/mrkkrp/path-io"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-dlist' 'haskell-exceptions' 'haskell-path' 'haskell-temporary'
         'haskell-unix-compat')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('9704cc8bc6f390dfc4d2158811d411ec766be555a7282629e1b27f3483aac88eb6238c15577a0dd432bb90f5589112e24673cba0648b8724488e22ffcb5482cb')

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev
  runhaskell Setup build
  LC_CTYPE=en_US.UTF-8 runhaskell Setup haddock --hoogle --html
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "${srcdir}/${_hkgname}-${pkgver}"
  
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s /usr/share/doc/$pkgname/html "$pkgdir/usr/share/doc/ghc/html/libraries/$_hkgname"
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE.md"

  # Remove static libs
  find "$pkgdir"/usr/lib -name "*.a" -delete
}
