# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=persistent-mysql
pkgname=haskell-persistent-mysql
pkgver=2.13.0.1
pkgrel=63
pkgdesc="Backend for the persistent library using MySQL database server."
url="http://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-blaze-builder' 'haskell-conduit' 'haskell-monad-logger'
         'haskell-mysql' 'haskell-mysql-simple' 'haskell-persistent' 'haskell-resource-pool'
         'haskell-resourcet' 'haskell-unliftio-core')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-fast-logger' 'haskell-hspec'
             'haskell-http-api-data' 'haskell-path-pieces' 'haskell-persistent-qq'
             'haskell-persistent-test' 'haskell-quickcheck-instances')
#checkdepends=('percona-server' 'percona-server-clients')
checkdepends=('mariadb')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('e726a54704b5eedd3f3c9ccb20516501a9f42c787d3e5e7998e900637f26f0abf7f5bd82900ad66d99091abe9112d0c697460dc536c7b07d24f2476a0e0552b4')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  mkdir -p /tmp/mysql_test
  # TODO: Try to make percona work?
  #mysqld --no-defaults --initialize-insecure --datadir=/tmp/mysql_test 
  #mysqld --no-defaults --datadir=/tmp/mysql_test --secure-file-priv=/tmp/mysql_test --socket=/tmp/socket.mysql --bind-address=127.0.0.1 --skip-grant-tables &
  
  mysql_install_db --datadir=/tmp/mysql_test
  mysqld --datadir=/tmp/mysql_test --socket=/tmp/socket.mysql --skip-grant-tables &
  MYSQL_PID=$!
  sleep 5
  mysql -h 127.0.0.1 -u root -e "create database IF NOT EXISTS test;"
  mysql -h 127.0.0.1 -u root -e "create database IF NOT EXISTS persistent;"

  # Compatibility issue with mariadb: https://github.com/yesodweb/persistent/issues/1158
  TRAVIS=true runhaskell Setup test --show-details=direct || echo "Tests failed"

  kill $MYSQL_PID
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
