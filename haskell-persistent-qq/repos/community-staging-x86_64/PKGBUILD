# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=persistent-qq
pkgname=haskell-persistent-qq
pkgver=2.12.0.2
pkgrel=120
pkgdesc="Provides a quasi-quoter for raw SQL for persistent"
url="https://github.com/yesod-web/persistent"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-src-meta' 'haskell-persistent')
makedepends=('ghc' 'haskell-aeson' 'haskell-fast-logger' 'haskell-hspec' 'haskell-hunit'
             'haskell-monad-logger' 'haskell-persistent-sqlite' 'haskell-resourcet'
             'haskell-unliftio')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d9d2f1b00002413e9bdc29ddd4cc0c6733a1d3fcfc4ada8d03c4e6011703fb9e680a07ea602a43f0d12aa04a4e03124dbd97da08dcd0fc28ce4330513a0db8bd')

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
