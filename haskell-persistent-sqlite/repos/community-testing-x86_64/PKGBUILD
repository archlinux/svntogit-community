# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent-sqlite
pkgname=haskell-persistent-sqlite
pkgver=2.10.5.1
pkgrel=8
pkgdesc="Backend for the persistent library using sqlite3"
url="https://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'sqlite' 'haskell-aeson' 'haskell-conduit' 'haskell-microlens-th'
         'haskell-monad-logger' 'haskell-persistent' 'haskell-resource-pool' 'haskell-resourcet'
         'haskell-unliftio-core' 'haskell-unordered-containers')
makedepends=('ghc' 'haskell-exceptions' 'haskell-fast-logger' 'haskell-hspec' 'haskell-hunit'
             'haskell-persistent-template' 'haskell-persistent-test' 'haskell-quickcheck'
             'haskell-system-fileio' 'haskell-system-filepath' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ce221a33deba9aaaee22a230fd19d3f86671a0b80ea0763be50ffdeb10508c80f75e7069ee0a6cec4055ba0239821d5ce033a0e2ece0c4aca893730f31ecc9c2')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-build-sanity-exe -fsystemlib -fuse-pkgconfig
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
