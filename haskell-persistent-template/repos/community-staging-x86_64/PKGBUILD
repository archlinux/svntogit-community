# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent-template
pkgname=haskell-persistent-template
pkgver=2.9.1.0
pkgrel=19
pkgdesc="Type-safe, non-relational, multi-backend persistence"
url="http://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-http-api-data" "haskell-monad-control"
         "haskell-monad-logger" "haskell-path-pieces" "haskell-persistent"
         'haskell-th-lift-instances' "haskell-unordered-containers")
makedepends=('ghc' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('56c89b4f02401cbf9716b97c4c314d6ef079dccf75ec1166f9b2b641049723002587fd5d074c1a87d20f7162d8b5677a87c84a1e4333eb93c425a5f09c84ea64')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
