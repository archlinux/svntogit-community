# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent-template
pkgname=haskell-persistent-template
pkgver=2.6.0
pkgrel=18
pkgdesc="Type-safe, non-relational, multi-backend persistence"
url="http://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-aeson-compat" "haskell-http-api-data"
         "haskell-monad-control" "haskell-monad-logger" "haskell-path-pieces" "haskell-persistent"
         "haskell-tagged" "haskell-unordered-containers")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('25ec2a40d77b9d05de370c6343e5377b5fe74932ef82797a8abd140c884d5a6aa1624c55a3e9617ada5de3bda2fe2e7c6fb59433a8acb7f0bb14833b321faad7')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
