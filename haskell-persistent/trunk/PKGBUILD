# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=persistent
pkgname=haskell-persistent
pkgver=2.6
pkgrel=29
pkgdesc="Type-safe, multi-backend data serialization"
url="http://www.yesodweb.com/book/persistent"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-attoparsec" "haskell-base64-bytestring"
         "haskell-blaze-html" "haskell-blaze-markup" "haskell-conduit" "haskell-exceptions"
         "haskell-fast-logger" "haskell-http-api-data" "haskell-lifted-base"
         "haskell-monad-control" "haskell-monad-logger" "haskell-mtl" "haskell-old-locale"
         "haskell-path-pieces" "haskell-resource-pool" "haskell-resourcet" "haskell-scientific"
         "haskell-silently" "haskell-tagged" "haskell-text" "haskell-transformers-base"
         "haskell-unordered-containers" "haskell-vector")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('42423362de94599581d18be644ba77181e87417087b3953daec2e8512d408e3e')

prepare() {
    sed -i 's/http-api-data            >= 0.2       && < 0.3/http-api-data            >= 0.2       \&\& < 0.4/' \
        $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-nooverlap
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
