# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=polysemy
pkgname=haskell-polysemy
pkgver=1.5.0.0
pkgrel=18
pkgdesc="Higher-order, low-boilerplate, zero-cost free monads"
url="https://github.com/isovector/polysemy"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-async' 'haskell-first-class-families' 'haskell-syb'
         'haskell-th-abstraction' 'haskell-type-errors' 'haskell-type-errors-pretty'
         'haskell-unagi-chan')
makedepends=('ghc' 'uusi' 'haskell-cabal-doctest' 'haskell-hspec-discover' 'haskell-doctest' 'haskell-hspec'
             'haskell-inspection-testing')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        ghc9.patch)
sha512sums=('4cc5c348b75e6bdf7b15c926786a4bd6fec2c6aa5e0862d865aefe8a49e7ce5eadd1203b641fa4c9e398a04bd66def40196725fbdee479ded8661b5bea3bb99e'
            '6bef2b9dac5a600d36aafabd3dffa3c5d81c3aff80cf039c9c2e1a2c39d5bd3e9ca234bb8b3366aae12c386c21a3922d8d68993367d5a35a9b21e0e5f0a86bcb')

prepare() {
  cd $_hkgname-$pkgver
  patch -p1 -i ../ghc9.patch
  uusi -u doctest $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # https://github.com/polysemy-research/polysemy/pull/410#issuecomment-868183832
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
