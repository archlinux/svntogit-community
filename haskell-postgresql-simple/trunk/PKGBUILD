# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=postgresql-simple
pkgname=haskell-postgresql-simple
pkgver=0.6.5
pkgrel=12
pkgdesc="Mid-Level PostgreSQL client library"
url="https://github.com/haskellari/postgresql-simple"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-only' 'haskell-aeson' 'haskell-attoparsec' 'haskell-case-insensitive'
         'haskell-hashable' 'haskell-postgresql-libpq' 'haskell-scientific' 'haskell-time-compat'
         'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hunit' 'haskell-base16-bytestring' 'haskell-cryptohash-md5'
             'haskell-inspection-testing' 'haskell-tasty' 'haskell-tasty-golden'
             'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('fda53ab2a72bbe402947526ccb02f9928563eae03e0832bb871c00912e455e96')

prepare() {
  cd $_hkgname-$pkgver
  uusi -d bytestring-builder $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
