# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=pretty-show
pkgname=haskell-pretty-show
pkgver=1.10
pkgrel=7
pkgdesc='Tools for working with derived `Show` instances and generic inspection of values.'
url="https://wiki.github.com/yav/pretty-show"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-lexer')
makedepends=('ghc' 'happy')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('90ecf41790246c85ee7c738de2bb1a23a56a0a1d547cee627cb0a5f6c2691adfc73851ca0ebc928f3584912bc2b1148b115db03eea271e67f55d6a1466db8711')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
