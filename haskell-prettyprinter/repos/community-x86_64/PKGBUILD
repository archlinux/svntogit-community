# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=prettyprinter
pkgname=haskell-prettyprinter
pkgver=1.3.0
pkgrel=8
pkgdesc="A modern, easy to use, well-documented, extensible prettyprinter."
url="https://github.com/hspec/prettyprinter"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-doctest' 'haskell-pgp-wordlist' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('5aa07888cc61e1431d852188f1571726b2929eca36c285026f4d7458f73bb568fe73de7a113c687b62b352efaaa4d2d4fb9bbdff6250d1d944ca13ee6141e51c')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-buildReadme
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
