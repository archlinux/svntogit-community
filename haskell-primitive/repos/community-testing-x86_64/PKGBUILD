# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-primitive
pkgver=0.6.2.0
pkgrel=3
pkgdesc="Wrappers for primitive operations"
url="https://hackage.haskell.org/package/primitive"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/primitive/${pkgver}/primitive-${pkgver}.tar.gz")
sha512sums=('bd19893c98de2fb2948b8d0f52587ae39ed97648c23fe4cb5bbb4ef15dec5b4f64d12ea6b686654f93b930cee4723829694e1e21edaa153c9501c7936e92d7ba')

prepare() {
  sed -i 's/base >= 4.5 && < 4.10/base >= 4.5/' primitive-$pkgver/primitive.cabal
}

build() {
  cd "$srcdir"/primitive-${pkgver}
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
      --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir"/primitive-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE

  # Remove static libs
  find "$pkgdir"/usr/lib -name "*.a" -delete
}
