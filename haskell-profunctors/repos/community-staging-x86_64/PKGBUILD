# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=profunctors
pkgname=haskell-profunctors
pkgver=5.3
pkgrel=11
pkgdesc="Profunctors"
url="https://github.com/ekmett/profunctors/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-bifunctors' 'haskell-comonad'
         'haskell-contravariant' 'haskell-distributive' 'haskell-tagged')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a4b5d386aec89b9ede94f0277ea29453435429e27993f9add315b1bba478a214ebb55ece68f74f68ca91dd3e65a1e5c1cc1829bdad81dbf643dfd97029473162')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/semigroups/d' $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
