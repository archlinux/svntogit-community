# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=quic
pkgname=haskell-quic
pkgver=0.1.0
pkgrel=6
pkgdesc="QUIC"
url="https://github.com/kazu-yamamoto/quic"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base16-bytestring' 'haskell-crypto-token' 'haskell-cryptonite'
         'haskell-data-default-class' 'haskell-fast-logger' 'haskell-iproute' 'haskell-memory'
         'haskell-network' 'haskell-network-byte-order' 'haskell-network-udp' 'haskell-psqueues'
         'haskell-random' 'haskell-tls' 'haskell-unix-time' 'haskell-unliftio'
         'haskell-unliftio-core' 'haskell-x509' 'haskell-x509-system')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-async' 'haskell-doctest' 'haskell-hspec'
             'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('65ae39a7711f950b7e8ebd4c7ef4ca1941b4b5a52bd883c952492a1e8810617b')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -f-devel

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
