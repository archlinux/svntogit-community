# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=quickcheck-instances
pkgname=haskell-quickcheck-instances
pkgver=0.3.26.1
pkgrel=6
pkgdesc="Common quickcheck instances"
url="https://github.com/haskellari/qc-instances"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-onetuple' 'haskell-quickcheck' 'haskell-case-insensitive'
         'haskell-data-fix' 'haskell-hashable' 'haskell-integer-logarithms' 'haskell-old-time'
         'haskell-scientific' 'haskell-splitmix' 'haskell-strict' 'haskell-tagged'
         'haskell-text-short' 'haskell-these' 'haskell-time-compat' 'haskell-transformers-compat'
         'haskell-unordered-containers' 'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('50cb9a7c28744a4bfb930420f6af2dcbc5925f5e1b108e345cee4d12eca2c27221fa7745c8cd697f047c649363a15d367da462acb2f7b8fbc7ecec728c5be57c')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-bytestring-builder
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
