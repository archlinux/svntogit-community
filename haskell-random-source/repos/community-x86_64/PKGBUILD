# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=random-source
pkgname=haskell-random-source
pkgver=0.3.0.11
pkgrel=21
pkgdesc="Generic basis for random number generators"
url="https://github.com/mokus0/random-fu"
license=("custom:PublicDomain")
arch=('x86_64')
depends=('ghc-libs' 'haskell-flexible-defaults' 'haskell-mersenne-random-pure64'
         'haskell-mwc-random' 'haskell-primitive' 'haskell-random' 'haskell-stateref' 'haskell-syb'
         'haskell-th-extras')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('5ba3647d7324f51e8c14a17666dca865c3329daedaa12be2cbba2402415b9853')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
}
