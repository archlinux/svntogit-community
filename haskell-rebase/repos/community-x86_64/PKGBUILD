# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=rebase
pkgname=haskell-rebase
pkgver=1.13.0.1
pkgrel=35
pkgdesc='A more progressive alternative to the "base" package'
url="https://github.com/nikita-volkov/rebase"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-contravariant' 'haskell-comonad' 'haskell-dlist'
         'haskell-either' 'haskell-hashable' 'haskell-hashable-time' 'haskell-profunctors'
         'haskell-scientific' 'haskell-selective' 'haskell-semigroupoids'
         'haskell-unordered-containers' 'haskell-uuid-types' 'haskell-vector'
         'haskell-vector-instances' 'haskell-void')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f0ac2c2f0b0596b01a7f2961566232b8966ce128acf8029f1d9ddcef67b44d955d6364d06b1127128a60cd947a4b0431da89e43e260da0b2eda300f2fc592e7a')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
