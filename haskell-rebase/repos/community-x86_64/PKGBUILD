# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=rebase
pkgname=haskell-rebase
pkgver=1.6.1
pkgrel=17
pkgdesc='A more progressive alternative to the "base" package'
url="https://github.com/nikita-volkov/rebase"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-contravariant' 'haskell-contravariant-extras'
         'haskell-comonad' 'haskell-dlist' 'haskell-either' 'haskell-hashable'
         'haskell-profunctors' 'haskell-scientific' 'haskell-selective' 'haskell-semigroupoids'
         'haskell-unordered-containers' 'haskell-uuid' 'haskell-vector' 'haskell-void')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c55fedcdc8dc35b0c34372965dfddaab774fdcc3093a8215ef9077eadc0af9103bf62095da9b494ebe13238736b5ed794bbe65f2e9926eaaa8efec8f8ea76d78')

prepare() {
    cd $_hkgname-$pkgver
    sed -e 's/>=1.3/>=1/' -e '/semigroups/d' -i $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
