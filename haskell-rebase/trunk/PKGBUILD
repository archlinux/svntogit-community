# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=rebase
pkgname=haskell-rebase
pkgver=1.3.1.1
pkgrel=24
pkgdesc='A more progressive alternative to the "base" package'
url="https://github.com/nikita-volkov/rebase"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-vector' 'haskell-unordered-containers'
         'haskell-scientific' 'haskell-uuid' 'haskell-dlist' 'haskell-void'
         'haskell-bifunctors' 'haskell-profunctors' 'haskell-contravariant'
         'haskell-contravariant-extras' 'haskell-semigroupoids' 'haskell-either'
         'haskell-base-prelude')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2418a89c2348f222320ce75001d944f2d1025bdf8d00f7cca5ae00fe3e701de1e79e1c24726faad23860f6c9a76a86a7fbc3248ec5b4b601c2c3d64086b7b8d1')

prepare() {
    cd $_hkgname-$pkgver
    sed -e '/fail/d' -e '/semigroups/d' -i $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
