# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=record-dot-preprocessor
pkgname=haskell-record-dot-preprocessor
pkgver=0.2.7
pkgrel=24
pkgdesc="Preprocessor to allow record.field syntax"
url="https://github.com/ndmitchell/record-dot-preprocessor#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-uniplate')
makedepends=('ghc' 'haskell-record-hasfield')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('a110fa2dc56177c0272138ad5e182357012fec85f9ec23ad293cd1351d25f6b91b3305d57f5470e8ba04255cc1c9897048d9d893522f7f62e73e3f7415965077')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
