# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=record-dot-preprocessor
pkgname=haskell-record-dot-preprocessor
pkgver=0.2.12
pkgrel=11
pkgdesc="Preprocessor to allow record.field syntax"
url="https://github.com/ndmitchell/record-dot-preprocessor#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-uniplate')
makedepends=('ghc' 'haskell-record-hasfield')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('724dfc316f39d8fc91b800d3c5f68593ba6b8076774f9e1442b843ab9e703c87dfe10e4a9f2879206d230764f6437fc92cdf6e89686ca49b202043fd2064edc6')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
