# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=refinery
pkgname=haskell-refinery
pkgver=0.3.0.0
pkgrel=16
pkgdesc="Toolkit for building proof automation systems"
url="https://github.com/totbwf/refinery#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-logict' 'haskell-mmorph')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-checkers' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('6fa5e7b2bf6607d43c6422ed67fb3c8b035e853d55dd5df0f333efe5dd3e3489fca6e4b77ddbd56f10f52a2cf7527327f014db728757bafe7452c11a93abc517')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
