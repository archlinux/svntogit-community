# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=resolv
pkgname=haskell-resolv
pkgver=0.1.2.0
pkgrel=41
pkgdesc="Domain Name Service (DNS) lookup via the libresolv standard library routines"
url="https://github.com/hvr/resolv"
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base16-bytestring')
makedepends=('ghc' 'uusi' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('fae2d0717a109b0d6cc19c51c773bc68c64c0048b9d930528cf5622ee270ebaddec180c1d444542e8eb036262611f5b88deadeea223fb2ef2b15b41369a2376e')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u base16-bytestring -u tasty $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
