# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=retry
pkgname=haskell-retry
pkgver=0.8.0.1
pkgrel=12
pkgdesc="Retry combinators for monadic actions that may fail"
url="https://github.com/Soostone/retry"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions' 'haskell-random')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-hunit' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-hedgehog')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('99f9e5fb90f686b8f621a5cc2bc34d72d5af5d119a11a11364355c514fa6c019b7d818f8f095c482fe388225d35052fed63f8112984c5af185a2e2328f7ad475')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lib-werror
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
