# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=retry
pkgname=haskell-retry
pkgver=0.7.6.1
pkgrel=6
pkgdesc="Retry combinators for monadic actions that may fail"
url="https://github.com/Soostone/retry"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default-class' 'haskell-exceptions' 'haskell-random')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-hunit' 'haskell-mtl' 'haskell-stm' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-hedgehog')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('eb9abf9866662926ed0df76cb65c842363d47f1598d56aaf4e45930dee7b09c2fdb43963429f376efbeed9929dcce009851a895ebc15b00ae7cea7da64b697ec')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lib-werror
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
