# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=rio
pkgname=haskell-rio
pkgver=0.1.8.0
pkgrel=10
pkgdesc="A standard library for Haskell"
url="https://github.com/commercialhaskell/rio"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions' 'haskell-hashable' 'haskell-microlens'
         'haskell-primitive' 'haskell-typed-process' 'haskell-unliftio'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('2bf741369f9fe93ad09540a9bba7c9e3a311a0c46c7af3a786f1eb5c260119ee6ca0d372ce64f09067af0876486a3ddec17ec003092061f2b8d8ffe319104474')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
