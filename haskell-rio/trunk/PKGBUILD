# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=rio
pkgname=haskell-rio
pkgver=0.1.2.0
pkgrel=1
pkgdesc="A standard library for Haskell"
url="https://github.com/commercialhaskell/rio"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-exceptions' 'haskell-hashable' 'haskell-microlens' 'haskell-mtl'
         'haskell-primitive' 'haskell-text' 'haskell-typed-process' 'haskell-unliftio'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('1f51ba6c42b21e9a0cb11c3a6cb847a624492b9156096df493556f420f28d702ec6ee9ba647e0d70d70a5126052de81295bc7bf38c4383f6b3566a9f57c58d0b')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
