# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=RSA
pkgname=haskell-rsa
pkgver=2.3.1
pkgrel=24
pkgdesc="Implementation of RSA, using the padding schemes of PKCS#1 v2.1."
url="https://github.com/GaloisInc/rsa"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-crypto-api' 'haskell-crypto-pubkey-types' 'haskell-sha')
makedepends=('ghc' 'haskell-drbg' 'haskell-quickcheck' 'haskell-tagged' 'haskell-test-framework'
             'haskell-test-framework-quickcheck2')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b4d3f406cd7b97237c2b36e2ba659f35871e02a6144b6cb48482fdc7999d9822758472c25b9eaef9ef6aeb8c503fa49b424350a543897217f3aec3084dd7572f')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '1i{-# language NoMonadFailDesugaring #-}' Test.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
