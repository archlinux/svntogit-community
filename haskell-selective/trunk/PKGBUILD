# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=selective
pkgname=haskell-selective
pkgver=0.4.1.1
pkgrel=35
pkgdesc="Selective applicative functors"
url="https://github.com/snowleopard/selective"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-expected-failure'
             'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('5f40ebb4c0b516ced83cdf91e7c138f3f802c6ce6307467d60cd01bf9e76e4aa74d53ae59c1a39fde781f4f83f33df7b72eea2a635b5931e57ddef8012ce5a4f')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
