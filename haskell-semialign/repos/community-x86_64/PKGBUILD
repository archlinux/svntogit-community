# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=semialign
pkgname=haskell-semialign
pkgver=1.2.0.1
pkgrel=103
pkgdesc="Align and Zip type-classes from the common Semialign ancestor."
url="https://github.com/haskellari/these"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-indexed-traversable'
         'haskell-indexed-traversable-instances' 'haskell-semigroupoids' 'haskell-tagged'
         'haskell-these' 'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('ba4c680ec2f301c6b76e2198a422bd9c540915fd89a12cbb1787e95c0ccd1bb05abdf59ab1678f46141939673418c650d9624af2ddb7370f2b6bccf704ade83d')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u vector $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
