# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=semigroupoids
pkgname=haskell-semigroupoids
pkgver=5.3.6
pkgrel=5
pkgdesc="Semigroupoids: Category sans id"
url="https://github.com/ekmett/semigroupoids"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-bifunctors' 'haskell-comonad' 'haskell-contravariant'
         'haskell-distributive' 'haskell-hashable' 'haskell-tagged' 'haskell-transformers-compat'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('98068c37c99b9c0abbe904e83332f1fb264b59439983edf558c1aca595ae2a1f332d272630ace67c2b2e7cfbc35b6ec5d4ac0d5b1844ed216e7b829278ae55da')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -ftagged -fcomonad -fdoctests -fdistributive -fcontravariant -fcontainers -funordered-containers
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
