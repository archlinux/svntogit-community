# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant
pkgname=haskell-servant
pkgver=0.12.1
pkgrel=2
pkgdesc="A family of combinators for defining webservices APIs"
url="http://haskell-servant.readthedocs.org/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-aeson' 'haskell-attoparsec'
         'haskell-case-insensitive' 'haskell-http-api-data' 'haskell-http-media'
         'haskell-http-types' 'haskell-natural-transformation' 'haskell-mtl' 'haskell-mmorph'
         'haskell-tagged' 'haskell-text' 'haskell-string-conversions' 'haskell-network-uri'
         'haskell-vault')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-aeson-compat' 'haskell-hspec'
             'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-url' 'haskell-doctest'
             'haskell-filemanip')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f7c4a7faa666e579f681db6fc9bd78ed93b09015561c8d390fb877b08502e85d1aeb182051e1ee3ea4513ed2328fea1fe752c37fdb619d602c98d791db235a55')

prepare() {
    sed -i '/test-suite doctests/a \ x-doctest-options: -dynamic' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
