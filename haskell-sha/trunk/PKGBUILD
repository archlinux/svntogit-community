# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Arch Haskell Team <>

pkgname=haskell-sha
pkgver=1.6.4.1
pkgrel=1
pkgdesc="Implementations of the SHA suite of message digest functions"
url="http://hackage.haskell.org/package/SHA"
license=('BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc')
depends=('haskell-array' 'haskell-binary' 'haskell-bytestring')
options=('strip')
install="${pkgname}.install"
source=("http://hackage.haskell.org/packages/archive/SHA/${pkgver}/SHA-${pkgver}.tar.gz")
sha256sums=('fdb9924aa31493ad198e3b3498a8204cc7013e0ef86215faf1c4e435a7166aec')
sha256sums=('743bc6d7dd3e74a44bfca8920f0f0ba5855722a62f6cc44f0a38d10c11bddc0d')

build() {
    cd ${srcdir}/SHA-${pkgver}
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/SHA-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/SHA
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
