# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shake
pkgname=haskell-shake
pkgver=0.19.2
pkgrel=22
pkgdesc="Build system library, like Make, but more accurate dependencies."
url="https://shakebuild.com"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-filepattern' 'haskell-hashable'
         'haskell-heaps' 'haskell-js-dgtable' 'haskell-js-flot' 'haskell-js-jquery'
         'haskell-primitive' 'haskell-random' 'haskell-unordered-containers' 'haskell-utf8-string')
makedepends=('ghc' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('874f38c86ae5de8f66eacdeb3f86360ff200948e76d66c6f89c8db6ceeadb97ea9dc914bdabe3fc4bf99873cbb520ee75f16229deb009efecc6ca19cdc4c5261')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/ghc --make/ghc -dynamic --make/' src/Test/{Command.hs,CloseFileHandles.hs}
    sed -e 's/ghc -package=Cabal/ghc -package=Cabal -dynamic/' \
        -e 's/ghc -fno-code/ghc -dynamic -fno-code/' \
        -e 's/"configure",/"configure","--enable-executable-dynamic","--disable-library-vanilla",/' \
        -i src/Test/Docs.hs
    sed -i 's/cmd "ghc" flags/cmd "ghc" "-dynamic" flags/' src/Test/Self.hs src/Test/SelfMake.hs
    sed -i 's/ghc --make/ghc -dynamic --make/' docs/manual/build.sh

    # Not sure about this one:
    sed -i 's/at cmd, called at/at want, called at/' src/Test/Errors.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname"  --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-portable -f-cloud -f-embed-files
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
