# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shake
pkgname=haskell-shake
pkgver=0.17.4
pkgrel=11
pkgdesc="Build system library, like Make, but more accurate dependencies."
url="http://shakebuild.com"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-hashable' 'haskell-heaps' 'haskell-js-flot'
         'haskell-js-jquery' 'haskell-primitive' 'haskell-random' 'haskell-unordered-containers'
         'haskell-utf8-string')
makedepends=('ghc' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0f53d37ba3eed0b42c76fb3e83a0c9d0d29108118f37c939149db1282ee6f9f72b74c0759da0b6c699710b72d7a396a32e39ac93d36f0c64d0e2f96afe7570ff')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/ghc --make/ghc -dynamic --make/' src/Test/Command.hs
    sed -e 's/runhaskell Setup.hs configure/runhaskell Setup.hs configure --enable-executable-dynamic --disable-library-vanilla/' \
        -e 's/ghc -fno-code/ghc -dynamic -fno-code/' \
        -i src/Test/Docs.hs
    sed -i 's/cmd "ghc" flags/cmd "ghc" "-dynamic" flags/' src/Test/Self.hs src/Test/SelfMake.hs
    sed -i 's/ghc --make/ghc -dynamic --make/' docs/manual/build.sh
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname"  --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-portable -f-cloud
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
