# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shakespeare
pkgname=haskell-shakespeare
pkgver=2.0.14
pkgrel=1
pkgdesc="A toolkit for making compile-time interpolated templates"
url="http://www.yesodweb.com/book/shakespearean-templates"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-blaze-html" "haskell-blaze-markup" "haskell-exceptions"
         "haskell-parsec" "haskell-scientific" "haskell-text"  "haskell-unordered-containers"
         "haskell-vector")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('75c793d35ecc6d78085038ca74167293e2553d66585fafedbbf28e1b35ab3456acdd5aa13cd307b154ee398cf4c878c2216907fb56642995e082bfbb2bd189fe')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-test_roy -f-test_coffee -f-test_export
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
