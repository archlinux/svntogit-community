# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: berberman <hatsue@typed.icu>

_hkgname=shellmet
pkgname=haskell-shellmet
pkgver=0.0.4.1
pkgrel=29
pkgdesc="Shellmet provides easy and convenient way to call shell commands from Haskell programs."
url="https://github.com/kowainik/shellmet"
license=('MPL2')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'uusi' 'haskell-doctest' 'haskell-glob' 'haskell-markdown-unlit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('ddb29dc2f7368b0bf3e3984eec4120e01609c23aa02191b6a742f1f59f71b10c9bd33c5d486b1e375909acf81c6aea4ac9c89d3e20289377fc43b8e119ccbb05')

prepare(){
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver    
    
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/{,shellmet-}readme
}
