# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=skylighting-core
pkgname=haskell-skylighting-core
pkgver=0.8.3.2
pkgrel=14
pkgdesc="Syntax highlighting library"
url="https://github.com/jgm/skylighting"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-attoparsec'
         'haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-case-insensitive'
         'haskell-colour' 'haskell-hxt' 'haskell-regex-pcre' 'haskell-safe'
         'haskell-utf8-string')
makedepends=('ghc' 'haskell-diff' 'haskell-hunit' 'haskell-pretty-show' 'haskell-quickcheck'
             'haskell-random' 'haskell-tasty' 'haskell-tasty-golden' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('cf7a09df89faf2141a7884a244d52d5691a0a5764041a5f02e89c8a3121f322a32287cef8fd53464e4b4ddde61d84d3fce3a46c273014fc289052c1ac6c2ffa0')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fexecutable -fsystem-pcre
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
