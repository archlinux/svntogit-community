# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=skylighting
pkgname=haskell-skylighting
pkgver=0.7.4
pkgrel=4
pkgdesc="Syntax highlighting library"
url="https://github.com/jgm/skylighting"
license=('GPL2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-attoparsec'
         'haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-case-insensitive'
         'haskell-colour' 'haskell-hxt' 'haskell-pretty-show' 'haskell-regex-pcre'
         'haskell-safe' 'haskell-skylighting-core' 'haskell-utf8-string')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0d9adccf0fd06ad91075f9a48d9ce5c892d8f2edc93d9251dd1b4ea43559cbbc2a732aa002ae5b73d05ee774d3fe02629894a44133ca8b514792fbb0adc22ab1')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsystem-pcre -fexecutable
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
