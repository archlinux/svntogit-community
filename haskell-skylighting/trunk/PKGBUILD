# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=skylighting
pkgname=haskell-skylighting
pkgver=0.6
pkgrel=15
pkgdesc="Syntax highlighting library"
url="https://github.com/jgm/skylighting"
license=('GPL2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-attoparsec'
         'haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-case-insensitive'
         'haskell-colour' 'haskell-hxt' 'haskell-mtl' 'haskell-pretty-show' 'haskell-regex-pcre'
         'haskell-safe' 'haskell-text' 'haskell-utf8-string')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-golden'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-random' 'haskell-diff')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('a28886deed006406de570d4ae065b43d0b8b66c743932ed62d33454caee3cf1d8433f70623a02489c0034b413cd021075122d1baf7b3f1b6e7b3deeb325bff00')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsystem-pcre
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
