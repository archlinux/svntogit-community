# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=smallcheck
pkgname=haskell-smallcheck
pkgver=1.1.3.1
pkgrel=1
pkgdesc="A property-based testing library"
url="https://github.com/feuerbach/smallcheck"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-logict' 'haskell-mtl')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('a7783dd6b7aaed58ba0e3017aecae8cc41d98b1f373c3f2c8a21300006766db9fa981c71bc300047f651d32459ba7f7ef59e1bed518c54b7c627ecc7f59a8cfa')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
