# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=spdx
pkgname=haskell-spdx
pkgver=1.0.0.2
pkgrel=110
pkgdesc="SPDX license expression language, Extras"
url="https://github.com/phadej/spdx"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'uusi' 'haskell-base-compat' 'haskell-tasty' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        cabal-3.4.patch)
sha512sums=('2fb48d204a0c85269d4dbd6bd98efefb0691adc3eb654228b90b244c0d062e0e2f23c42e5f7ef9ab27a2f988654485fec11c3bdc0642b9b17d35791dd80f1548'
            'edcd5676c3bdcc1f45bd41c8c465defb3b423e46330dbdf0037bf5d59658c4491c9979422077ba1fe598da12ca8b15a01d8f3d2de0a65d8bcc7f6e842a51d56e')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../cabal-3.4.patch
    uusi -u base -u base-compat -u tasty $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
