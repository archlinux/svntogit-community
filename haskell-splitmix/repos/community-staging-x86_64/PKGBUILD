# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=splitmix
pkgname=haskell-splitmix
pkgver=0.0.2
pkgrel=3
pkgdesc="Fast Splittable PRNG"
url="https://github.com/phadej/splitmix"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-random')
makedepends=('ghc' 'haskell-async' 'haskell-base-compat-batteries' 'haskell-tf-random'
             'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0ec2bacb1aa98aed62ad13e28f21486a106319ab613eeba749e9018354d22a6fcc07f0809eabfa3c261a1031d97254f44886c12c661ffbe32b3e640ea9083e85')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
