# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=haskell-src-exts-util
pkgname=haskell-src-exts-util
pkgver=0.2.2
pkgrel=7
pkgdesc="Helper functions for working with haskell-src-exts trees"
url="https://github.com/pepeiborra/haskell-src-exts-util"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-semigroups' 'haskell-src-exts'
         'haskell-uniplate')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e36471025ca8ac86a0f2c97e01db6f2516b3e9f2f0945479420a7f00f74d22fd68020ca5b3a1c6f0eabfad1b6ba43ef265319cd3c86d54d40df416c8beaa7c4b')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
