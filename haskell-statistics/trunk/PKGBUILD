# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=statistics
pkgname=haskell-statistics
pkgver=0.16.1.0
pkgrel=5
pkgdesc="A library of statistical types, data, and functions"
url="https://github.com/haskell/statistics"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-async' 'haskell-data-default-class'
         'haskell-dense-linear-algebra' 'haskell-math-functions' 'haskell-mwc-random'
         'haskell-parallel' 'haskell-primitive' 'haskell-random' 'haskell-vector'
         'haskell-vector-algorithms' 'haskell-vector-binary-instances' 'haskell-vector-th-unbox')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-erf' 'haskell-ieee754'
             'haskell-tasty' 'haskell-tasty-expected-failure' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('cba66ff20ffa95e54116f2bf04a335c195b44532f09035f80fd5ff7c0f61a5d0309f0e56fb82dd874c15c34f163755dfb780edd9c64a312fbafa3d7093b217b7')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
