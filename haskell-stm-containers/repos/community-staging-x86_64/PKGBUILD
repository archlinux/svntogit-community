# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=stm-containers
pkgname=haskell-stm-containers
pkgver=1.2
pkgrel=27
pkgdesc="Containers for STM"
url="https://github.com/nikita-volkov/stm-containers"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-deferred-folds' 'haskell-focus' 'haskell-hashable' 'haskell-list-t'
         'haskell-stm-hamt')
makedepends=('ghc' 'uusi' 'haskell-htf' 'haskell-quickcheck' 'haskell-foldl' 'haskell-free'
             'haskell-quickcheck-text' 'haskell-rerebase')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('6c4d98b6a3182fa0dd99235cea1aa95a3c876f8be5cbb78f7700a17d64b7177a')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u HTF $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
