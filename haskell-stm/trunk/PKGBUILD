# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
# Package generated by cabal2arch 0.7.5

_hkgname=stm
pkgname=haskell-stm
pkgver=2.4.4.1
pkgrel=6
pkgdesc="A modular composable concurrency abstraction."
url="https://hackage.haskell.org/package/stm"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'sh')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/$pkgver/${_hkgname}-$pkgver.tar.gz")
sha512sums=('12a8edb7489995245fb21d9f0782cad87e992f56343d4492eef788370ebc2dcb934193d8602b85b5f3935c4bdb4c9426717b6149437cd7b0bb5311bda4b237eb')

prepare() {
    sed -i 's/base  >= 4.3 && < 4.10,/base  >= 4.3,/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
          -f-developer -f-integer-simple
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
