# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=store-core
pkgname=haskell-store-core
pkgver=0.4.1
pkgrel=7
pkgdesc="Fast and lightweight binary serialization"
url="https://github.com/fpco/store"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-primitive' 'haskell-text')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('701f3a7bb767e1f20e149950fcf6d06603d09f3fa78d22d10b2ba23efdf4d5786646679259ca4e25ead1a95f2faa233853096f533fa49e9f86e1e69481b6a6df')

prepare() {
    sed -i '/fail/d' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-force-alignment
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
