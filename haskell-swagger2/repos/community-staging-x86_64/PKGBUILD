# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=swagger2
pkgname=haskell-swagger2
pkgver=2.3.1.1
pkgrel=59
pkgdesc="Swagger 2.0 data model"
url="https://github.com/GetShopTV/swagger2"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-compat-batteries' 'haskell-cookie'
         'haskell-hashable' 'haskell-generics-sop' 'haskell-http-media'
         'haskell-insert-ordered-containers' 'haskell-lens' 'haskell-network' 'haskell-quickcheck'
         'haskell-scientific' 'haskell-transformers-compat' 'haskell-unordered-containers'
         'haskell-vector' 'haskell-uuid-types')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit' 'haskell-quickcheck-instances'
             'haskell-hspec-discover' 'haskell-doctest' 'haskell-cabal-doctest' 'haskell-glob'
             'haskell-utf8-string')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        generics-sop-0.5.patch)
sha512sums=('1834e75cc6c8ff8287605e93d2b4d665b7d8de7e7e00f6537d25e0f54934c85e5431e5f2f1beb5264382c4b81f5ed19cc2b7af1f9323ebee9502e19d73d49fc1'
            '8f1b74df57a090f5cb1cfb9551288dc6ade326c486ebb432767bfb1a0571ff1104269b19b4e2fbed8c5427cd432b3d0c1d18adb19f971b5fd441affbc88c27b6')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../generics-sop-0.5.patch
    sed -i -e 's/< *0.8/<1/' -e 's/< *2.13/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
