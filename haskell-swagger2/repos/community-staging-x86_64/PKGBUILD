# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=swagger2
pkgname=haskell-swagger2
pkgver=2.8.2
pkgrel=1
pkgdesc="Swagger 2.0 data model"
url="https://github.com/GetShopTV/swagger2"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-base-compat-batteries'
         'haskell-cookie' 'haskell-hashable' 'haskell-generics-sop' 'haskell-http-media'
         'haskell-insert-ordered-containers' 'haskell-lens' 'haskell-network' 'haskell-optics-core'
         'haskell-optics-th' 'haskell-quickcheck' 'haskell-scientific'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-uuid-types')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-hunit' 'haskell-quickcheck-instances'
             'haskell-hspec-discover' 'haskell-doctest' 'haskell-cabal-doctest' 'haskell-glob'
             'haskell-utf8-string')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('801252a88778aa431a037fd9471e5df24fca8e5e8482e27197b81f607d3884746b3c240da66d83a30474e9996b589219eec43acb0dda987135b62cf5e654e072')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u hspec $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
