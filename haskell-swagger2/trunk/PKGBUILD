# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=swagger2
pkgname=haskell-swagger2
pkgver=2.6.1
_commit=44ad73cd0f49c0cd17ad9f9ec4a9fdb3d4e7e776
pkgrel=28
pkgdesc="Swagger 2.0 data model"
url="https://github.com/GetShopTV/swagger2"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-base-compat-batteries'
         'haskell-cookie' 'haskell-hashable' 'haskell-generics-sop' 'haskell-http-media'
         'haskell-insert-ordered-containers' 'haskell-lens' 'haskell-network' 'haskell-optics-core'
         'haskell-optics-th' 'haskell-quickcheck' 'haskell-scientific'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-uuid-types')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-hunit' 'haskell-quickcheck-instances'
             'haskell-hspec-discover' 'haskell-doctest' 'haskell-cabal-doctest' 'haskell-glob'
             'haskell-utf8-string')
source=("https://github.com/GetShopTV/swagger2/archive/$_commit/$pkgname-$_commit.tar.gz")
#source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2e2ba7f08b217533cbcf6d0d59e0f4da94645dc9ea6805cf7eebc3188f5d9ff5bb55471588ebd55bf13ade8c1ac5ff249dac58da7b5cc827dc8e4fcff65bec35')

prepare() {
    cd $_hkgname-$_commit
    uusi -u aeson -u base -u hspec -u lens -u optics-core -u optics-th -u template-haskell $_hkgname.cabal
}

build() {
    cd $_hkgname-$_commit

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$_commit
    # https://github.com/GetShopTV/swagger2/issues/222
    runhaskell Setup test || echo "Tests failed"
}

package() {
    cd $_hkgname-$_commit

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
