# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tagstream-conduit
pkgname=haskell-tagstream-conduit
pkgver=0.5.5.3
pkgrel=330
pkgdesc="Streamlined html tag parser"
url="https://github.com/yihuang/tagstream-conduit"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-attoparsec" "haskell-blaze-builder" "haskell-case-insensitive"
         "haskell-conduit" "haskell-conduit-extra" "haskell-data-default" "haskell-resourcet"
         "haskell-xml-conduit")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        tagstream-conduit-monadfail.patch::https://github.com/yihuang/tagstream-conduit/commit/9b31798bfb3ae637bb69ed805a7a2459ca79a3d1.patch)
sha512sums=('84625975df6c6c315011ebfd63d551b22c00927fa9add390d9e723bc104d4a7881743aaf6bceae0ff42efc6056a314baddb594fd5e344449333ea7ff6cbcf154'
            'c486a57bbc04ad6d3cff389f35be12d1ce6d891daf7e32fa166928cd7ce33ecedbcc2906023445c229bd8acc8d1b5694835da11f2f88eb3f42b93ba58504ce60')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../tagstream-conduit-monadfail.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
