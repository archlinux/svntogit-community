# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tamarin-prover-term
pkgname=haskell-tamarin-prover-term
pkgver=1.4.1
pkgrel=65
pkgdesc="Term manipulation library for the tamarin prover"
url="http://www.infsec.ethz.ch/research/software/tamarin"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-attoparsec' 'haskell-dlist'
         'haskell-safe' 'haskell-tamarin-prover-utils')
makedepends=('ghc')
source=("tamarin-prover-$pkgver.tar.gz::https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver.tar.gz"
        tamarin-prover-maude-3.patch::https://github.com/tamarin-prover/tamarin-prover/pull/353.patch)
sha512sums=('4d3aeae02be5d430bff6c55d78656e3c9a648674d235bfeb36ce227a39abd3054a132b99ff8040abf05d8e26506fe85d21ecfb6fce73062dd45b5ba98d941144'
            'cb3863c1d69ceff5b583a91aa442ccfbf61280ac04379dec67b4e564eddc8f1a393f85949130275eb6d5874295334c6bb34bb55b741632a288c65fb2e6459305')

prepare() {
    cd "${srcdir}/tamarin-prover-${pkgver}"
    patch -p1 -i "$srcdir"/tamarin-prover-maude-3.patch
}

build() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/term"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/term"

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
