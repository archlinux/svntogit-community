# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tamarin-prover-theory
pkgname=haskell-tamarin-prover-theory
pkgver=1.2.2
pkgrel=2
pkgdesc="Security protocol types and constraint solver library for the tamarin prover"
url="http://www.infsec.ethz.ch/research/software/tamarin"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-dlist' 'haskell-fclabels'
         'haskell-mtl' 'haskell-parallel' 'haskell-parsec' 'haskell-safe' 'haskell-text'
         'haskell-uniplate' 'haskell-tamarin-prover-utils' 'haskell-tamarin-prover-term')
source=("tamarin-prover-$pkgver.tar.gz::https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver.tar.gz")
sha512sums=('f61fc4f601f5abf865b41c9b3ee3607b20f5e44b2d80f8e5a32986bf7d301b0e50479c4b473e5e478f87388491671b8180a8ee0db0d903b3abd2478e7dc9ce60')

build() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/theory"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    # haddock failed to generate
    # runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/theory"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    # install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    # ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
