# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tar-conduit
pkgname=haskell-tar-conduit
pkgver=0.3.2
pkgrel=93
pkgdesc="Extract and create tar files using conduit for streaming"
url="https://github.com/snoyberg/tar-conduit"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-conduit' 'haskell-safe-exceptions')
makedepends=('ghc' 'haskell-conduit-extra' 'haskell-hspec' 'haskell-quickcheck' 'haskell-weigh')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('18efa362953641092ee5b4f65b32a3337af077608487e02421cf6427209592654b3b2d15f9b21835f4596434b65aa9a1f80b9f3408050c09b57f6be5cd2c73f7')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/conduit-combinators/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
