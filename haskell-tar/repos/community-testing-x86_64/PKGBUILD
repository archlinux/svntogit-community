# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haskell-tar
pkgver=0.4.0.0
pkgrel=3
pkgdesc="Reading, writing and manipulating \".tar\" archive files."
url="http://hackage.haskell.org/package/tar"
license=('custom:BSD3')
arch=('x86_64' 'i686')
depends=('ghc=7.6.1-1')
source=("http://hackage.haskell.org/packages/archive/tar/$pkgver/tar-$pkgver.tar.gz"
        '0001-update-changed-time-code-for-ghc-7.6.1.patch')
install=haskell-tar.install
sha256sums=('a3d37be1b8666c16c6371d193a0795b8abe7b0216e81870a2666e1e85d931113'
            '17b8afc519db5bed5d89807e6e659788f5d9c2f5c569150a9d1c6faac52e98e6')

build() {
  cd "$srcdir/tar-$pkgver"

  # time functions have changed in ghc 7.6.1
  patch -Np1 -i ${srcdir}/0001-update-changed-time-code-for-ghc-7.6.1.patch

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir="/usr/share/doc/$pkgname" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/tar-$pkgver"

  install -Dm 744 register.sh \
	  "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m 744 unregister.sh \
	  "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm 755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/$pkgname/html" \
	  "$pkgdir/usr/share/doc/ghc/html/libraries/tar"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
