# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-checklist
pkgname=haskell-tasty-checklist
pkgver=1.0.5.0
pkgrel=9
pkgdesc="Check multiple items during a tasty test"
url="https://github.com/kquick/tasty-checklist"
license=("custom:ISC")
arch=('x86_64')
depends=('ghc-libs' 'haskell-parameterized-utils')
makedepends=('ghc' 'uusi' 'haskell-doctest' 'haskell-hunit' 'haskell-tasty'
             'haskell-tasty-expected-failure' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('b300097d6d3a4445664635af092dcb37aaf908dde696f6ecba0a2a75e2050904f5773d28e383d2c34d5113ae2fc0bd0956e854aaf4a465680ea56ebee0154bc4')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
}
