# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-hspec
pkgname=haskell-tasty-hspec
pkgver=1.1.5.1
pkgrel=26
pkgdesc="Hspec support for the Tasty test framework"
url="https://github.com/mitchellwrosen/tasty-hspec"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hspec' 'haskell-hspec-core' 'haskell-quickcheck' 'haskell-tasty'
         'haskell-tasty-smallcheck' 'haskell-tasty-quickcheck')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('c03c2acdf57dc8aba762ba60a7448f92199ab55f8f311d4354e265c5c9ecb029c9242a65d0bc8e4d023bfaba89a674bf89f586e15f0e1c3f6fa66dc4ea682e6a')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/< *2/<3/' -e 's/< *1/<2/' -e 's/>= 4.12/>= 4.14/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
