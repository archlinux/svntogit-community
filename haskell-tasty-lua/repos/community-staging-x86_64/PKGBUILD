# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-lua
pkgname=haskell-tasty-lua
pkgver=0.2.2
pkgrel=27
pkgdesc="Write tests in Lua, integrate into tasty."
url="https://github.com/hslua/tasty-lua"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-file-embed' 'haskell-hslua' 'haskell-tasty')
makedepends=('ghc' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d0407fa44d794747506652ec924801559e9d5314550c3045a3f263dc6c4747cea559d77b835a4fe24909627664c11fef0e08341923c92e821968b8ec273aa9b3')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *1.3/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
