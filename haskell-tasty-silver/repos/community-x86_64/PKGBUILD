# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-silver
pkgname=haskell-tasty-silver
pkgver=3.3.1
pkgrel=5
pkgdesc="A fancy test runner, including support for golden tests"
url="https://github.com/phile314/tasty-silver"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-optparse-applicative'
         'haskell-process-extras' 'haskell-regex-tdfa' 'haskell-silently' 'haskell-tagged'
         'haskell-tasty' 'haskell-temporary')
makedepends=('ghc' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d2cad2269dcf7710ade6cd8ff4c76880d86bba3b62c4bf69825ef6c624db948d3f0c604fb33d7408794d5bd0c7d0d48c39997112ddf638bb3009b2c7915d0a23')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
