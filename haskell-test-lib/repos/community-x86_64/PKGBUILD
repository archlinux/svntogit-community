# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=test-lib
pkgname=haskell-test-lib
pkgver=0.3
pkgrel=21
pkgdesc="A library to make a quick test-runner script."
url="https://hackage.haskell.org/package/test-lib"
license=("custom:ISC")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-simple-get-opt' 'haskell-test-framework'
         'haskell-test-framework-hunit')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('5b198ada5b44a33b0af9b996051b9bd3a743c794f02cd100c370fc01bc7e6395')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u base $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  # Conflicts with bluez-utils
  mv "$pkgdir"/usr/bin/{,hs-}test-runner
}
