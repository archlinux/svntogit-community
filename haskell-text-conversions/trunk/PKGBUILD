# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=text-conversions
pkgname=haskell-text-conversions
pkgver=0.3.1
pkgrel=22
pkgdesc="Safe conversions between textual types"
url="https://github.com/cjdev/text-conversions"
license=('ISC')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base16-bytestring' 'haskell-base64-bytestring' 'haskell-errors')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-discover')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('4c0fe08024a56e326b632a67b945fa869a798b00fa45b56f4f181a0075ed5cd6802edec0f0e38c0f4b5cc4b81c6f6bd98e3382b968cec6ad884d24ee31dbf600')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
