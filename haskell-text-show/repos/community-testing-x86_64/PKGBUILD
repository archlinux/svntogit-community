# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=text-show
pkgname=haskell-text-show
pkgver=3.9
pkgrel=106
pkgdesc="Efficient conversion of values into Text"
url="https://github.com/RyanGlScott/text-show"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat-batteries' 'haskell-bifunctors' 'haskell-generic-deriving'
         'haskell-th-abstraction' 'haskell-th-lift' 'haskell-transformers-compat')
makedepends=('ghc' 'haskell-base-orphans' 'haskell-deriving-compat' 'haskell-hspec'
             'haskell-quickcheck' 'haskell-quickcheck-instances')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        $pkgname-base-4.16.patch::https://github.com/RyanGlScott/text-show/commit/92d4f5ec633bb2c131de41bf695054f33a748caa.patch
        $pkgname-generic-deriving-1.14.1.patch)
sha512sums=('fd2b1ae6e4b3de09950dff0557ee35e1754e24c5f1a88876418f8a3635fe4b3eb5d7fdea9893ba7d95324a286625805696e438fcef92ae580f2214409b514820'
            '6c748f94a7016f1a97add94bc12afb72e2dce8da0aad70f5cbb32498418054eebdc83f4c953ff884689bfa87c7e7f9d682fa94ff0ab3f260c03d1f6ca4d586f9'
            '328567f99dbac0952f4d006eec9e85bb762457339b988b815d1a28785c48c89ca5dab6103f7ac496d046dd41c0ffc1e5457f2e072e70eea910327ce9b7076445')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../$pkgname-base-4.16.patch
    patch -p1 -i ../$pkgname-generic-deriving-1.14.1.patch
    sed -i -e '/bytestring-builder/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
