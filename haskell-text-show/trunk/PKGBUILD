# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=text-show
pkgname=haskell-text-show
pkgver=3.8.5
pkgrel=35
pkgdesc="Efficient conversion of values into Text"
url="https://github.com/RyanGlScott/text-show"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat-batteries' 'haskell-bifunctors' 'haskell-generic-deriving'
         'haskell-th-abstraction' 'haskell-th-lift' 'haskell-transformers-compat')
makedepends=('ghc' 'haskell-base-orphans' 'haskell-deriving-compat' 'haskell-hspec'
             'haskell-quickcheck' 'haskell-quickcheck-instances')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('17e6e4d188280d4545867b31210f9e8e69337996c80b8c4f9fd846e50605acb29ebb2429e72ce76545426741055c4806c20a32b59d47e190a38d0f1ed9405947')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/bytestring-builder/d' -e 's/< *2.1/<3/;s/< *4.14/<5/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
