# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-desugar
pkgname=haskell-th-desugar1.11
pkgver=1.11
pkgrel=1
pkgdesc="Functions to desugar Template Haskell (legacy 1.11 version)"
url="https://github.com/goldfirere/th-desugar"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ordered-containers' 'haskell-syb' 'haskell-th-abstraction'
         'haskell-th-lift' 'haskell-th-orphans' 'haskell-transformers-compat')
makedepends=('ghc' 'uusi' 'haskell-hunit' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('30aaa03cf93858f043b46692564cb0d5855b2368bc464b4c95c9d389a40da71c45fc9dc91e7d601a0d3ed8b007361024f6b32c92b8609103f3b353d19c4ce37a')

prepare() {
    cd $_hkgname-$pkgver
    uusi -d fail -d semigroups $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
