# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-lift-instances
pkgname=haskell-th-lift-instances
pkgver=0.1.11
pkgrel=21
pkgdesc="Lift instances for template-haskell for common data types."
url="https://github.com/bennofs/th-lift-instances/"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-lift' 'haskell-vector')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e4e443637c10cd21d9b6159f29ad8eb61a65bda2b625dff48ccbf1310c9abdbb1f48ff0c7e74974b78e0517db6af2889abb3c5cfeaed202c7b3efbe1c83dbc7a')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
