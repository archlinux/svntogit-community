# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-orphans
pkgname=haskell-th-orphans
pkgver=0.13.5
pkgrel=23
pkgdesc="Orphan instances for TH datatypes"
url="https://github.com/ekmett/th-orphans/"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-lift' 'haskell-th-lift-instances'
         'haskell-th-reify-many')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('896c32861a208e8388ef0239db6ecb8e6e20a11422bd37daf74ea16f38c4d864e4f9f0cee7a3946a30ca6fa7ffb00c160ccf8e6791ed2d388a749dd986c12d25')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
