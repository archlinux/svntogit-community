# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-orphans
pkgname=haskell-th-orphans
pkgver=0.13.9
pkgrel=12
pkgdesc="Orphan instances for TH datatypes"
url="https://github.com/ekmett/th-orphans/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-lift' 'haskell-th-lift-instances'
         'haskell-th-reify-many')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('90ee9f24e1f30df7d01ba9196ec723e47b3c238bfac0a683d62f7e4515274993a4adb1f2f2834aace6e8d0c7a79d89400b68cb07ba24bf45e716ec2e0b6a1304')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
