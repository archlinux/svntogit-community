# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-utilities
pkgname=haskell-th-utilities
pkgver=0.2.1.0
pkgrel=10
pkgdesc="Collection of useful functions for use with Template Haskell"
url="https://github.com/fpco/th-utilities"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-primitive' 'haskell-syb' 'haskell-th-orphans')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9ce81cc2b6eecaf24a338ce3fc35de28c5705cc0a8d4b114231784e7660ee51e0bd552f618dc303fd970187fef435d632c21d8151ead886c71325a64fc38b8dd')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
