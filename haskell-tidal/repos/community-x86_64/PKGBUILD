# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tidal
pkgname=haskell-tidal
pkgver=1.9.2
pkgrel=8
pkgdesc="A domain specific language for live coding pattern."
arch=(x86_64)
url="https://tidalcycles.org/"
license=(GPL3)
groups=(pro-audio)
depends=(ghc ghc-libs glibc haskell-clock haskell-colour haskell-hosc
haskell-mwc-random haskell-network haskell-primitive haskell-random
haskell-tidal-link haskell-vector supercollider)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=("https://hackage.haskell.org/packages/archive/$_name/$pkgver/$_name-$pkgver.tar.gz")
sha512sums=('64deb1c3c9cc37623840f925c7eb3866b17e328b882a03f5b7a6fd370c4fae59fbf10a1a3c17df1bdc902235e0cb6880293c6a1de89a6ca422938ef0a4d42d69')
b2sums=('dbe4c2b5e7e23737b61f019fa9f45ab21ca7adfc6a6cb3f0cef6511b3eed3e61e1326971c0785eb6c007d61c97bbb98301c6e81b10575f677fd030b653d45d74')

build() {
  cd $_name-$pkgver
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
