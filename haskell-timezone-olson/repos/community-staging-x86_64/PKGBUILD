# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_hkgname=timezone-olson
pkgname=haskell-${_hkgname}
pkgver=0.1.9
pkgrel=5
pkgdesc='Pure Haskell parser and renderer for binary Olson timezone files'
url='http://projects.haskell.org/time-ng/'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base' 'haskell-binary' 'haskell-bytestring'
         'haskell-extensible-exceptions' 'haskell-time' 'haskell-timezone-series')
makedepends=('ghc')
source=(https://hackage.haskell.org/package/${_hkgname}-${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha256sums=('32230509029bcf9e1bd95b5ad7ee69b8b0250cffc4bb8f2df88a651b3af74b15')
sha512sums=('1ad8f98ba3573f11154e18c5c0d2d7ee3b32f165d388318622195199da4765d1a0352b8a13fa38a6b2fb008de93f34d2a6aa35d2d7ada53e1ae8c5404d68b645')

build() {
  cd ${_hkgname}-${pkgver}
    runhaskell Setup configure -O \
      --enable-shared \
      --enable-executable-dynamic \
      --disable-library-vanilla \
      --prefix=/usr \
      --docdir=/usr/share/doc/$pkgname \
      --dynlibdir=/usr/lib \
      --libsubdir=\$compiler/site-local/\$pkgid \
      --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
      --ghc-option='-pie'
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd ${_hkgname}-${pkgver}
  runhaskell Setup test
}

package() {
  cd ${_hkgname}-${pkgver}
  install -Dm 744 register.sh "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm 755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
  runhaskell Setup copy --destdir="$pkgdir"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
}

# vim: ts=2 sw=2 et:
