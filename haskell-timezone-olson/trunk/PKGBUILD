# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_hkgname=timezone-olson
pkgname=haskell-${_hkgname}
pkgver=0.2.0
pkgrel=2
pkgdesc='Pure Haskell parser and renderer for binary Olson timezone files'
url='http://projects.haskell.org/time-ng/'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base' 'haskell-binary' 'haskell-bytestring'
         'haskell-extensible-exceptions' 'haskell-time' 'haskell-timezone-series')
makedepends=('ghc')
source=(https://hackage.haskell.org/package/${_hkgname}-${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha256sums=('8f57c369a72c4da5ba546d6e62370567e835cc2f6da406fd00e8dbb48e803b2d')
sha512sums=('06c73b24b23caa41f3dce472bca384e3ee7ba02a3dd8bc4458bc758bc6aabdeca9be299f16419702e860f38ea213fd62d90e63fc4746dc2015c0e06b15aba6dd')

build() {
  cd ${_hkgname}-${pkgver}
    runhaskell Setup configure -O \
      --enable-shared \
      --enable-executable-dynamic \
      --disable-library-vanilla \
      --prefix=/usr \
      --docdir=/usr/share/doc/$pkgname \
      --dynlibdir=/usr/lib \
      --libsubdir=\$compiler/site-local/\$pkgid \
      --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
      --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup haddock
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd ${_hkgname}-${pkgver}
  runhaskell Setup test
}

package() {
  cd ${_hkgname}-${pkgver}
  install -Dm 744 register.sh "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm 755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
  runhaskell Setup copy --destdir="$pkgdir"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
}

# vim: ts=2 sw=2 et:
