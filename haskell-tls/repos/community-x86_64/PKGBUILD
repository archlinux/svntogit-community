# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tls
pkgname=haskell-tls
pkgver=1.4.0
pkgrel=9
pkgdesc="TLS/SSL protocol native implementation (Server and Client)"
url="https://github.com/vincenthz/hs-tls"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-asn1-encoding" "haskell-asn1-types" "haskell-async" "haskell-cereal"
         "haskell-cryptonite" "haskell-data-default-class" "haskell-memory" "haskell-mtl"
         "haskell-network" "haskell-x509" "haskell-x509-store" "haskell-x509-validation")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f4130b19c764f3423481b23c0d8f09540baf6cbb066045796ac28f030a3c43ac0efd342fe1ceceb0aa4d33a9cfc3b2c9d5d216e758a43a31d32a1fbee022921a')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-hans -fnetwork -fcompat
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
