# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: William J. Bowman <aur@williamjbowman.com>
# Contributer: Leif Warner <abimelech@gmail.com>

_hkgname=transformers-compat
pkgname=haskell-transformers-compat
pkgver=0.6.4
pkgrel=1
pkgdesc="A small compatibility shim exposing the new types from transformers 0.3 to older Haskell platforms."
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('38fb9420ac8015b35f454e123f0e59e83b4c27664a6f57255381ffc1266167c4a8c95c98885f7c32bafc52006d5021f88d6600b15fb9be90b41aaebac5ba6081')

build() {
    cd $_hkgname-$pkgver
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
