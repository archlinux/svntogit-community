# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=trifecta
pkgname=haskell-trifecta
pkgver=1.7.1.1
pkgrel=17
pkgdesc="A modern parser combinator library with convenient diagnostics"
url="https://github.com/ekmett/trifecta/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-ansi-terminal" "haskell-ansi-wl-pprint" "haskell-blaze-builder"
         "haskell-blaze-html" "haskell-blaze-markup" "haskell-charset" "haskell-comonad"
         "haskell-fingertree" "haskell-hashable" "haskell-lens" "haskell-mtl" "haskell-parsers"
         "haskell-profunctors" "haskell-reducers" "haskell-semigroups" "haskell-unordered-containers"
         "haskell-utf8-string")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('5da4e04be3f4f02220aa730b830a50b9a5df243815d6e00051beb7274af3bc78e50cf70747e3e1f448b2966d1c3051ab589e7d99a0432620390c6193d034a602')

prepare() {
    sed -i 's/ansi-terminal        >= 0.6     && < 0.7,/ansi-terminal        >= 0.6,/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
