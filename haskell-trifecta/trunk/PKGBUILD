# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=trifecta
pkgname=haskell-trifecta
pkgver=2.1
pkgrel=57
pkgdesc="A modern parser combinator library with convenient diagnostics"
url="https://github.com/ekmett/trifecta/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-ansi-terminal" "haskell-blaze-builder" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-charset" "haskell-comonad" "haskell-fingertree"
         "haskell-hashable" "haskell-lens" "haskell-parsers" 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal' "haskell-profunctors" "haskell-reducers"
         "haskell-unordered-containers" "haskell-utf8-string")
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0929ee4a3d9a53c211b13a711814b4b6ea488ad40f373befdc7e8a479b2a95c4e97a19381873af54d377ff2af49782677fa2ea892ea69522c242e245debd3a25')

prepare() {
    sed -i -e '/semigroups/d;s/< *0.17/<1/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
