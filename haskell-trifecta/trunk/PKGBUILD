# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=trifecta
pkgname=haskell-trifecta
pkgver=2.1.1
pkgrel=71
pkgdesc="A modern parser combinator library with convenient diagnostics"
url="https://github.com/ekmett/trifecta"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-ansi-terminal" "haskell-blaze-builder" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-charset" "haskell-comonad" "haskell-fingertree"
         "haskell-hashable" 'haskell-indexed-traversable' "haskell-lens" "haskell-parsers"
         'haskell-prettyprinter' 'haskell-prettyprinter-ansi-terminal' "haskell-profunctors"
         "haskell-reducers" "haskell-unordered-containers" "haskell-utf8-string")
makedepends=('ghc' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7284c56a616f82e5d498a823eb316b1000f0da7a296a2d3af1c300fa12c573e09b12b1c71e312fe7036b492b14d46abcc2b29d50b792c3cc407ed85552c2ab48')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
