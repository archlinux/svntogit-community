# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=turtle
pkgname=haskell-turtle
pkgver=1.3.5
pkgrel=2
pkgdesc="Shell programming, Haskell-style"
url="https://github.com/Gabriel439/Haskell-Turtle-Library"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc=8.0.1' 'haskell-ansi-wl-pprint' 'haskell-async' 'haskell-clock' 'haskell-foldl'
         'haskell-hostname' 'haskell-managed' 'haskell-optparse-applicative' 'haskell-optional-args'
         'haskell-semigroups' 'haskell-stm' 'haskell-system-fileio' 'haskell-system-filepath'
         'haskell-temporary' 'haskell-text' 'haskell-unix-compat')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('eaec4e3ed8e1209f589f3efdb0aa4987976f25228dfdf97f45bdf77cfc9b2c1d2882d746966a6940d7aed887f0920937b115970148750f4573e603c5380de966')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
