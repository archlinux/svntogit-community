# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=turtle
pkgname=haskell-turtle
pkgver=1.5.19
pkgrel=16
pkgdesc="Shell programming, Haskell-style"
url="https://github.com/Gabriel439/Haskell-Turtle-Library"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-wl-pprint' 'haskell-async' 'haskell-clock'
         'haskell-foldl' 'haskell-hostname' 'haskell-managed' 'haskell-optparse-applicative'
         'haskell-optional-args' 'haskell-streaming-commons' 'haskell-system-fileio'
         'haskell-system-filepath' 'haskell-temporary' 'haskell-unix-compat')
makedepends=('ghc' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7f220fbe827fd18120f5476ae87bc45e01ec6ff5557f9017990efd37c011cbace0560fa5943b50b8f9bc49cfa0e0218fa1600fd40d786140c8cfbe702c5dcf8f')

prepare() {
    sed -i 's/]/, "-dynamic"]/' $_hkgname-$pkgver/test/Main.hs
    sed -i '/semigroups/d;/fail,/d;s/< *0.17/<1/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
