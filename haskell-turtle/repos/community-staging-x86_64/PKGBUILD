# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=turtle
pkgname=haskell-turtle
pkgver=1.5.12
pkgrel=6
pkgdesc="Shell programming, Haskell-style"
url="https://github.com/Gabriel439/Haskell-Turtle-Library"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-wl-pprint' 'haskell-async' 'haskell-clock' 'haskell-exceptions'
         'haskell-foldl' 'haskell-hostname' 'haskell-managed' 'haskell-optparse-applicative'
         'haskell-optional-args' 'haskell-system-fileio' 'haskell-system-filepath'
         'haskell-temporary' 'haskell-unix-compat')
makedepends=('ghc' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('82405fb1e5a50b46787e8297f840259dc0e90e44a8522966f8f15a52471a6552b5476e25871ea968dda70f3ceda14501db5b8b2d5471a587f630f74b648850c5')

prepare() {
    sed -i 's/]/, "-dynamic"]/' $_hkgname-$pkgver/test/Main.hs
    sed -i '/semigroups/d' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
