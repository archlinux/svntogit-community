# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=turtle
pkgname=haskell-turtle
pkgver=1.6.1
pkgrel=3
pkgdesc="Shell programming, Haskell-style"
url="https://github.com/Gabriel439/Haskell-Turtle-Library"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-wl-pprint' 'haskell-async' 'haskell-clock' 'haskell-foldl'
         'haskell-hostname' 'haskell-managed' 'haskell-optparse-applicative' 'haskell-optional-args'
         'haskell-streaming-commons' 'haskell-temporary' 'haskell-unix-compat')
makedepends=('ghc' 'haskell-doctest' 'haskell-tasty' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('94575c10c084ad8173e63bcb304491b03158fada47d32976000a5dcb34ddca1141762765318d88860e94691f68a16c643020785dbb11a764aae2fb6c319fbc97')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/]/, "-dynamic"]/' test/Main.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
