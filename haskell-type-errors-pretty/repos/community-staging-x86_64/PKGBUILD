# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=type-errors-pretty
pkgname=haskell-type-errors-pretty
pkgver=0.0.1.1
pkgrel=5
pkgdesc="Combinators for writing pretty type errors easily"
url="https://github.com/chshersh/type-errors-pretty"
license=('MPL2')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-doctest' 'haskell-glob')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('7370343188979427d9433b5c0e2f1a39acb243f8177a7e217d7d1c88006bfaf3ca0001381c7ee8c0938b933cb479d720ce0397623194f2514b6149f8ed49932e')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
    sed -i 's/\^>= *0.16/>= 0.16/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
