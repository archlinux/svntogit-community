# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_hkgname=tz
pkgname=haskell-tz
pkgver=0.1.3.5
pkgrel=29
pkgdesc='Efficient time zone handling'
arch=(x86_64)
url='https://hackage.haskell.org/package/tz'
license=(BSD)
depends=(ghc-libs haskell-vector haskell-data-default haskell-tzdata)
checkdepends=(haskell-test-framework-th haskell-hunit haskell-quickcheck
              haskell-test-framework-quickcheck2 haskell-test-framework-hunit)
makedepends=(ghc)
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('1821d84bcb2da33a4a1c22fd1c809d3bd49a91d05b5e9f7ef1b073bd91b970dee393a767f84e43c6f681add1cb77f39878089d380b39e8c7855b5429a7f5f97c')

build() {
  cd $_hkgname-$pkgver
    
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --enable-tests \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
