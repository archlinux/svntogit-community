# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=unicode-transforms
pkgname=haskell-unicode-transforms
pkgver=0.4.0
pkgrel=18
pkgdesc="Unicode transforms (normalization NFC/NFD/NFKC/NFKD)"
url="https://github.com/composewell/unicode-transforms/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-unicode-data')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck' 'haskell-split' 'haskell-text-icu')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('56a718b4912e60270a74da681002010b78c85c6b6313e88202a34630aa6162a105c148cc9a54d3d989335edf02d2bc5be60c5f541b56efd696bc450c00c2b332')

prepare() {
    cd $_hkgname-$pkgver
    # https://github.com/composewell/unicode-transforms/issues/81
    sed -i 's/import qualified Data.Text.ICU as ICU/import qualified Data.Text.ICU.Normalize2 as ICU/' test/Properties.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-dev -fhas-icu -f-has-llvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
