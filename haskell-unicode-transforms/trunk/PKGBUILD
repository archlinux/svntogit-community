# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=unicode-transforms
pkgname=haskell-unicode-transforms
pkgver=0.3.3
pkgrel=4
pkgdesc="Unicode transforms (normalization NFC/NFD/NFKC/NFKD)"
url="https://github.com/ekmett/unicode-transforms/"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bitarray' 'haskell-text')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0835e826d45d47a04d142a43561da6cb467a65a3f6dae3187454a30480f6ed6d8a27f8d2ad9d0480dced33355d4557205a9a78e780cad573b45265a8ba20e71b')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev -f-has-icu -f-has-llvm
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
