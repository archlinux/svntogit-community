# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=Unixutils
pkgname=haskell-unixutils
pkgver=1.54.2
pkgrel=22
pkgdesc="A crude interface between Haskell and Unix-like operating systems"
url="https://github.com/seereason/haskell-unixutils"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-process-extras' 'haskell-puremd5'
         'haskell-regex-tdfa' 'haskell-zlib')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7d8e652ed6d7a19082ae5fb4ca4e05f808fc7d6599b7a41652743dfbd53f0ebd3333168ea76e480127b554bbaab7466ed8ac6c066edd6651fe9f54e69ea4f5df')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
