# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Martin Harvan <martinhrvn@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=unordered-containers
pkgname=haskell-unordered-containers
pkgver=0.2.12.0
pkgrel=14
pkgdesc="Efficient hashing-based container types"
url="https://github.com/haskell-unordered-containers/unordered-containers"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable')
makedepends=('ghc' 'haskell-chasingbottoms' 'haskell-hunit' 'haskell-quickcheck'
             'haskell-random' 'haskell-test-framework' 'haskell-test-framework-hunit'
             'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('516f45494c598e33301df08d2877fae2484b6574cb554def35ab3ef31e29424a140115be81b88cd7aa7a8c7fe7a77354a7379c62a65aa00cd8c5c674ee1dd00f')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-debug
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
