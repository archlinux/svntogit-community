# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=uri-bytestring
pkgname=haskell-uri-bytestring
pkgver=0.3.0.2
pkgrel=2
pkgdesc="Haskell URI parsing as ByteStrings"
url="https://github.com/Soostone/uri-bytestring"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-blaze-builder' 'haskell-th-lift-instances')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-base-compat' 'haskell-quickcheck-instances'
             'haskell-semigroups' 'haskell-generics-sop')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('1f058d6549283a160c52a77532b2769eca54b9d4e209a0cc5541127409e069e8aa86dba2f471149aaba6db2fdb09b3cd910b563a0700314bfcf557d61cf01fca')

prepare() {
    sed -i '/fail/d' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lib-Werror
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
