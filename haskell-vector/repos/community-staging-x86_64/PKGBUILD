# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.10.0.1
pkgrel=2
pkgdesc="Efficient Arrays"
url="http://code.haskell.org/vector"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=()
depends=("ghc" "haskell-primitive")
options=('strip')
install="${pkgname}.install"
source=("http://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
sha256sums=('ece945058b01a77c5fff31e89bbea76ac619677041c41286a2da5e8b515508af')

build() {
  cd ${srcdir}/vector-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${srcdir}/vector-${pkgver}
  install -Dm744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install -Dm744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
  install -dm755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/vector
  runhaskell Setup copy --destdir=${pkgdir}
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
