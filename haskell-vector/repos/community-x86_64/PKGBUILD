# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.12.3.1
pkgrel=10
pkgdesc="Efficient Arrays"
url="https://hackage.haskell.org/package/vector"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-primitive')
makedepends=('ghc' 'uusi' 'haskell-base-orphans' 'haskell-doctest' 'haskell-hunit' 'haskell-quickcheck'
             'haskell-random' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
sha512sums=('bf117d53c7844c010dc1cee8b778851bd714155fd34e671ae2eb7bd324427de972389eb9f3f315832f20e7bbbecc93ec0f6296ab10e4935957d2a4e14c6cfd98')

prepare() {
  cd vector-${pkgver}
  uusi -u doctest vector.cabal
}

build() {
  cd "$srcdir"/vector-${pkgver}

  if (( CHECKFUNC )); then
      _opts=('--enable-tests')
  else
      _opts=('--disable-tests')
  fi

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname "${_opts[@]}" \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd vector-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd "$srcdir"/vector-${pkgver}
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE
}
