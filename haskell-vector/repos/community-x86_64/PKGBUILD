# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.12.0.1
pkgrel=2
pkgdesc="Efficient Arrays"
url="https://hackage.haskell.org/package/vector"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc" "haskell-primitive")
source=("https://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
sha512sums=('17cae6c2e36e6e73190ebce0c3f38c4b8ec01d4336d2bb9b98f85ea299cab4140d4490493f6cbd472c1b0a32809f3ce344955ab6cd7dbacdca56ffa5aec4bad4')

build() {
  cd "$srcdir"/vector-${pkgver}
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
      --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
      --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir"/vector-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm755 "$pkgdir"/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html "$pkgdir"/usr/share/doc/ghc/html/libraries/vector
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE

  # Remove static libs
  find "$pkgdir"/usr/lib -name "*.a" -delete
}
