# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.12.0.1
pkgrel=10
pkgdesc="Efficient Arrays"
url="https://hackage.haskell.org/package/vector"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-primitive")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
sha512sums=('17cae6c2e36e6e73190ebce0c3f38c4b8ec01d4336d2bb9b98f85ea299cab4140d4490493f6cbd472c1b0a32809f3ce344955ab6cd7dbacdca56ffa5aec4bad4')

prepare() {
  sed -i 's/base >= 4.5 && < 4.10/base >= 4.5/' vector-$pkgver/vector.cabal
}

build() {
  cd "$srcdir"/vector-${pkgver}
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir"/vector-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE
}
