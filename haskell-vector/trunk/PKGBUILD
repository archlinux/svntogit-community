# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.12.1.2
pkgrel=42
pkgdesc="Efficient Arrays"
url="https://hackage.haskell.org/package/vector"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-primitive')
makedepends=('ghc' 'haskell-base-orphans' 'haskell-hunit' 'haskell-quickcheck' 'haskell-random'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
sha512sums=('36d0237cf7c5a6b78f88b33ff09ea8b19baeaf85609efdeba9ab802ca588956457d4607304b73e8d58e9e802a9101c23da62caee71508f069047a6d531e50170')

prepare() {
  cd vector-$pkgver
  sed -i 's/,semigroups$//' vector.cabal
}

build() {
  cd "$srcdir"/vector-${pkgver}

  if (( CHECKFUNC )); then
      _opts=('--enable-tests')
  else
      _opts=('--disable-tests')
  fi

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname "${_opts[@]}" \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir"/vector-${pkgver}
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE
}
