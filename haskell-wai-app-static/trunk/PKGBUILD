# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-app-static
pkgname=haskell-wai-app-static
pkgver=3.1.7.3
pkgrel=11
pkgdesc="WAI application for static serving"
url="https://www.yesodweb.com/book/web-application-interface"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-cryptonite'
         'haskell-file-embed' 'haskell-http-date' 'haskell-http-types' 'haskell-memory'
         'haskell-mime-types' 'haskell-old-locale' 'haskell-optparse-applicative'
         'haskell-unix-compat' 'haskell-unordered-containers' 'haskell-wai' 'haskell-wai-extra'
         'haskell-warp' 'haskell-zlib')
makedepends=('ghc' 'haskell-hspec' 'haskell-mockery' 'haskell-network' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ce360d2cc5e9d7a27934220e8851d82f8d7cf29f97f56241877a210e3f904ea7795bc0eb7859d5b08289e2ae6440d82fc525ec5c8aad50c737b68f8b12ba1f2b')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-print
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
