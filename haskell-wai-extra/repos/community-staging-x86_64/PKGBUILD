# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-extra
pkgname=haskell-wai-extra
pkgver=3.0.19.1
pkgrel=6
pkgdesc="Provides some basic WAI handlers and middleware."
url="http://github.com/yesodweb/wai"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-ansi-terminal" "haskell-base64-bytestring"
         "haskell-blaze-builder" "haskell-case-insensitive" "haskell-cookie"
         "haskell-data-default-class" "haskell-fast-logger" "haskell-http-types"
         "haskell-iproute" "haskell-lifted-base" "haskell-network" "haskell-old-locale"
         "haskell-resourcet" "haskell-streaming-commons" "haskell-stringsearch"
         "haskell-text" "haskell-unix-compat" "haskell-vault" "haskell-void" "haskell-wai"
         "haskell-wai-logger" "haskell-word8" "haskell-zlib")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('f7e7ca4432fd868bb549f16ff2671534cab4e0bcfff194b9de55aa561b21a7f6')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
