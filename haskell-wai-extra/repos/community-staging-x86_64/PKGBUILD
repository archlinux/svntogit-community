# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-extra
pkgname=haskell-wai-extra
pkgver=3.0.29.1
pkgrel=7
pkgdesc="Provides some basic WAI handlers and middleware."
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-base64-bytestring'
         'haskell-case-insensitive' 'haskell-cookie' 'haskell-data-default-class'
         'haskell-fast-logger' 'haskell-http-types' 'haskell-http2' 'haskell-iproute'
         'haskell-network' 'haskell-old-locale' 'haskell-resourcet' 'haskell-streaming-commons'
         'haskell-unix-compat' 'haskell-vault' 'haskell-void' 'haskell-wai'
         'haskell-wai-logger' 'haskell-word8' 'haskell-zlib')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f8c5f2d534a0ef4b16b4375cabfb49d11a643fd8f8d2b30f0f88b29b39f91e84db7170508a5ddb64540f4a33c30bde703a52345ac9195560141cd75626ef5ca4')

prepare(){
    cd $_hkgname-$pkgver
    sed -i 's/< *2.5/<4/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-build-example
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
