# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-middleware-static
pkgname=haskell-wai-middleware-static
pkgver=0.8.1
pkgrel=13
pkgdesc="WAI middleware that serves requests to static files"
url="https://github.com/scotty-web/wai-middleware-static"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-cryptonite" "haskell-memory" "haskell-expiring-cache-map"
         "haskell-http-types" "haskell-mime-types" "haskell-mtl" "haskell-old-locale"
         "haskell-semigroups" "haskell-text" "haskell-wai")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('84feb1301508e04f35e313e53c2bc59ff5e21f908d5850fef66645b2a0bb02b18d4b9e4a53d56f68d9de1db9e4847868a33e21c0b67bcf32f843073f7daa7f8f')

prepare() {
    sed -e 's/directory          >= 1.2.0.1  && < 1.3,/directory          >= 1.2.0.1,/' \
        -e 's/time               >= 1.4      && < 1.7,/time               >= 1.4,/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
