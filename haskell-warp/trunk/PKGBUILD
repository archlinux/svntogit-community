# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=warp
pkgname=haskell-warp
pkgver=3.2.27
pkgrel=38
pkgdesc="A fast, light-weight web server for WAI applications."
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-auto-update' 'haskell-bsb-http-chunked'
         'haskell-case-insensitive' 'haskell-hashable' 'haskell-http-date' 'haskell-http-types'
         'haskell-http2' 'haskell-iproute' 'haskell-network' 'haskell-simple-sendfile'
         'haskell-streaming-commons' 'haskell-unix-compat' 'haskell-vault'
         'haskell-wai' 'haskell-word8')
makedepends=('ghc' 'haskell-doctest' 'haskell-http-client' 'haskell-hspec' 'haskell-hunit'
             'haskell-lifted-base' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('34d66ba4dc7be03215ad22f6f47e5d7b1786227c416e49b2a97f5a252cfa0b3edfdf6a76390065c26dbfac728c837eecc57493055f28bc4137945343f8d435b0')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-warp-debug -fallow-sendfilefd -f-network-bytestring
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
