# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=wreq
pkgname=haskell-wreq
pkgver=0.5.3.3
pkgrel=269
pkgdesc="An easy-to-use HTTP client library."
url="https://github.com/bos/wreq"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-authenticate-oauth'
         'haskell-base16-bytestring' 'haskell-case-insensitive' 'haskell-cryptonite'
         'haskell-hashable' 'haskell-http-client' 'haskell-http-client-tls'
         'haskell-http-types' 'haskell-lens' 'haskell-lens-aeson' 'haskell-memory'
         'haskell-mime-types' 'haskell-psqueues' 'haskell-time-locale-compat'
         'haskell-unordered-containers' 'haskell-aeson-pretty' 'haskell-base64-bytestring'
         'haskell-snap-core' 'haskell-snap-server' 'haskell-unix-compat' 'haskell-uuid')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-hunit' 'haskell-quickcheck'
             'haskell-network-info' 'haskell-temporary' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        wreq-aeson-2-lens-aeson-1.2.patch::https://github.com/haskell/wreq/pull/149.patch)
sha512sums=('f074790cf3370d9356efbf5aeabb7421d59299471e02d93010453bb6f4e8daee517fcdf6891245272a9b08a35bc7b1b2233b75825ec377f6403c99cf91041f2e'
            'cd15090c0ce82a6737ad04af5d3f19c4804607b984ba58af721f0118e2eedff76771c02bc216d97f55580d3d41e019773aa082ef1b8e18f71289d249717873f4')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../wreq-aeson-2-lens-aeson-1.2.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -fdoctest -f-aws -fhttpbin -f-developer
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
