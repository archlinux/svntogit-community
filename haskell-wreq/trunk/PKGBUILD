# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=wreq
pkgname=haskell-wreq
pkgver=0.5.2.1
pkgrel=55
pkgdesc="An easy-to-use HTTP client library."
url="https://github.com/bos/wreq"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-authenticate-oauth'
         'haskell-base16-bytestring' 'haskell-case-insensitive' 'haskell-cryptonite'
         'haskell-exceptions' 'haskell-hashable' 'haskell-http-client' 'haskell-http-client-tls'
         'haskell-http-types' 'haskell-lens' 'haskell-lens-aeson' 'haskell-memory'
         'haskell-mime-types' 'haskell-psqueues' 'haskell-time-locale-compat'
         'haskell-unordered-containers' 'haskell-aeson-pretty' 'haskell-base64-bytestring'
         'haskell-snap-core' 'haskell-snap-server' 'haskell-unix-compat' 'haskell-uuid')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-hunit' 'haskell-quickcheck'
             'haskell-network-info' 'haskell-temporary' 'haskell-test-framework'
             'haskell-test-framework-hunit' 'haskell-test-framework-quickcheck2' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('4054c9951417e65ad7b215f1fdfa497513daa52b18242b4c20ea14a76030ba91a34fed664bd3bd06f6a62a38c3d5d09ada171ad3ca1d43928c709ffc44560b2f')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fdoctest -f-aws -fhttpbin -f-developer
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
