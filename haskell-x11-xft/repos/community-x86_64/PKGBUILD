# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

_hkgname=X11-xft
pkgname=haskell-x11-xft
pkgver=0.3.3
pkgrel=4
pkgdesc="Bindings to the Xft and some Xrender parts"
url="https://github.com/xmonad/X11-xft"
license=('LGPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-x11' 'haskell-utf8-string' 'libxft')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/X11-xft/$pkgver/X11-xft-$pkgver.tar.gz)
sha512sums=('2d76c11aefd0a209dccdd44ce0da32fd0cddc7e852fec86ff3ed97a7d80bf5bf57542c39025fdedf281584c9cea90e20ab6d14374ac50fea63dcfed8622702d5')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
