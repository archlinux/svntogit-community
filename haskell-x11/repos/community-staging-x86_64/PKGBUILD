# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: shild <sxp@bk.ru>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

_hkgname=X11
pkgname=haskell-x11
pkgver=1.10.2
pkgrel=1
pkgdesc="A Haskell binding to the X11 graphics library."
arch=(x86_64)
url="https://hackage.haskell.org/cgi-bin/hackage-scripts/package/X11"
license=('BSD')
depends=('ghc-libs' 'libx11' 'libxinerama' 'libxrandr' 'libxss' 'haskell-data-default-class')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/X11/$pkgver/X11-$pkgver.tar.gz)
sha512sums=('5013dc359e081900193efd201c38c41ca73bc8dae3610e8e3b46e42aad7ae4a5917ecd3219d77e40141be7179963aaa2454032c820945e93ff4310ef359b2df4')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
