# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: shild <sxp@bk.ru>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

_hkgname=X11
pkgname=haskell-x11
pkgver=1.9
pkgrel=14
pkgdesc="A Haskell binding to the X11 graphics library."
arch=(x86_64)
url="https://hackage.haskell.org/cgi-bin/hackage-scripts/package/X11"
license=('BSD')
depends=('ghc-libs' "libx11" "libxinerama" "libxrandr" "libxss" "haskell-data-default")
conflicts=(haskell-x11-extras)
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/X11/$pkgver/X11-$pkgver.tar.gz)
sha512sums=('6d6cc42e2b4a996478f40511876ef07b36bb047673da8f0471d37f9dc4f50bf763b484aa421c987be6fe7726fad0127f9ceb558435af3550622b9e7649e91516')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
