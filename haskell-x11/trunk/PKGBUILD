# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: shild <sxp@bk.ru>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

_hkgname=X11
pkgname=haskell-x11
pkgver=1.8
pkgrel=2
pkgdesc="A Haskell binding to the X11 graphics library."
arch=(i686 x86_64)
url="https://hackage.haskell.org/cgi-bin/hackage-scripts/package/X11"
license=('custom:BSD3')
depends=("ghc" "libx11" "libxinerama" "libxrandr" "haskell-data-default")
conflicts=(haskell-x11-extras)
source=(https://hackage.haskell.org/packages/archive/X11/$pkgver/X11-$pkgver.tar.gz)
md5sums=('97f24170b74395ba926fdfa6d40dd3d2')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
