# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=xcffib
pkgname=haskell-xcffib
pkgver=0.5.1
pkgrel=49
pkgdesc="A cffi-based python binding for X"
url="https://github.com/tych0/xcffib"
license=('Apache')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-either' 'haskell-filemanip' 'haskell-language-python'
         'haskell-mtl' 'haskell-optparse-applicative' 'haskell-semigroups' 'haskell-split'
         'haskell-xcb-types')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d182c203653eeaf2cca2cd27fef585b1882524f4daf1b9fe5c4e5d1123757294418e67ae56256edcafef4e052770e6726a19062e12cfd9ab56a4549f771078d0')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
