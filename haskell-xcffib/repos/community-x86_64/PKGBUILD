# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=xcffib
pkgname=haskell-xcffib
pkgver=0.11.1
pkgrel=178
pkgdesc="A cffi-based python binding for X"
url="https://github.com/tych0/xcffib"
license=('Apache')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-either' 'haskell-filemanip' 'haskell-language-python'
         'haskell-optparse-applicative' 'haskell-split' 'haskell-xcb-types')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        $_hkgname-xcb-proto-1.15.patch)
sha512sums=('dd70a762184b714fa53fd906ac192edcf990d8ff7674be1369eace38ee750da5eb6f4c5c2b7c6e5fab980f4bee96a7de4e2e38a27702cef28875a09bde8e5dd2'
            'edc61ae0eddfda36122e9752ecae061b0caf59f2a448b1a0ccac1c5f03a2b281e6d8a3c5e777653782f454a1da7ef5080154492f1d1c17c667b5ac06a5052be0')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../$_hkgname-xcb-proto-1.15.patch
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
