# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=xml-conduit
pkgname=haskell-xml-conduit
pkgver=1.6.0
pkgrel=1
pkgdesc="Pure-Haskell utilities for dealing with XML with the conduit package."
url="https://github.com/snoyberg/xml"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-attoparsec" "haskell-blaze-builder" "haskell-text"
         "haskell-blaze-markup" "haskell-conduit" "haskell-conduit-extra"
         "haskell-data-default-class" "haskell-monad-control" "haskell-resourcet"
         "haskell-blaze-html" "haskell-xml-types")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ca6ff685ad052db5e51c64937f658a933e7fb1ed778eb60e4514af5998604d37cf08bc01c27d4fb6a58f99bc1c1eb531a7488cad26ffcd3cc4f2de637dc7ca73')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
