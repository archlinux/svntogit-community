# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-auth
pkgname=haskell-yesod-auth
pkgver=1.4.16
pkgrel=21
pkgdesc="Authentication for Yesod."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-authenticate" "haskell-base16-bytestring"
         "haskell-base64-bytestring" "haskell-blaze-builder" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-byteable" "haskell-conduit" "haskell-conduit-extra"
         "haskell-cryptohash" "haskell-data-default" "haskell-email-validate"
         "haskell-file-embed" "haskell-http-client" "haskell-http-conduit"
         "haskell-http-types" "haskell-lifted-base" "haskell-mime-mail" "haskell-network-uri"
         "haskell-nonce" "haskell-persistent" "haskell-persistent-template" "haskell-random"
         "haskell-resourcet" "haskell-safe" "haskell-shakespeare" "haskell-text"
         "haskell-unordered-containers" "haskell-wai" "haskell-yesod-core" "haskell-yesod-form"
         "haskell-yesod-persistent")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('a2c76409522ac276b92d31e13ffa94ae51194ffdb902a41f979d25181a5182d2')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    LC_CTYPE=en_US.UTF-8 runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
