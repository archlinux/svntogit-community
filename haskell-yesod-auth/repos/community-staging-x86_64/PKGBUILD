# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-auth
pkgname=haskell-yesod-auth
pkgver=1.4.20
pkgrel=6
pkgdesc="Authentication for Yesod."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-authenticate" "haskell-base16-bytestring"
         "haskell-base64-bytestring" "haskell-blaze-builder" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-byteable" "haskell-conduit" "haskell-conduit-extra"
         "haskell-cryptonite" "haskell-data-default" "haskell-email-validate"
         "haskell-file-embed" "haskell-http-client" "haskell-http-conduit" "haskell-http-types"
         "haskell-lifted-base" "haskell-memory" "haskell-mime-mail" "haskell-network-uri"
         "haskell-nonce" "haskell-persistent" "haskell-persistent-template" "haskell-random"
         "haskell-resourcet" "haskell-safe" "haskell-shakespeare" "haskell-text"
         "haskell-unordered-containers" "haskell-wai" "haskell-yesod-core" "haskell-yesod-form"
         "haskell-yesod-persistent")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d4a25f7b6bf4b50d7b9780a569a695986ab8780555f9de5ab2e8c4917174ade867b896ff289a6a042cfbb960ed966b66d3a0ac4e5d7add1d67be35c8100aa68d')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
