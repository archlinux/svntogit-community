# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-core
pkgname=haskell-yesod-core
pkgver=1.6.5
pkgrel=35
pkgdesc="Creation of type-safe, RESTful web applications."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-auto-update' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-byteable' 'haskell-case-insensitive' 'haskell-cereal'
         'haskell-clientsession' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-cookie'
         'haskell-deepseq-generics' 'haskell-fast-logger' 'haskell-http-types'
         'haskell-monad-logger' 'haskell-old-locale' 'haskell-path-pieces' 'haskell-primitive'
         'haskell-random' 'haskell-resourcet' 'haskell-safe' 'haskell-shakespeare'
         'haskell-unix-compat' 'haskell-unliftio' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-wai' 'haskell-wai-extra' 'haskell-wai-logger' 'haskell-warp' 'haskell-word8')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('6bf98448abe8ab9ed9f7b602bc731d7aa67a31ce0440b7e3e633c2958258d98315c33a431e08b8ba2aa3bcc9fbde1945cafa823b3201986a7d0d33c2df6fe427')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
