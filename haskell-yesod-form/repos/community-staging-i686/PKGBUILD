# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-form
pkgname=haskell-yesod-form
pkgver=1.4.15
pkgrel=1
pkgdesc="Form handling support for Yesod Web Framework"
url="http://www.yesodweb.com/"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-attoparsec" "haskell-blaze-builder" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-byteable" "haskell-data-default" "haskell-email-validate"
         "haskell-network-uri" "haskell-persistent" "haskell-resourcet" "haskell-semigroups"
         "haskell-shakespeare" "haskell-text" "haskell-wai" "haskell-xss-sanitize"
         "haskell-yesod-core" "haskell-yesod-persistent")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4a91f069c617b48c644ed37b8ba9a41c444fd4bee6022338f22f96c3f28cb39374d8099c9e190a7b957f9468481d9319fd81f9f742da9993fd8c8296c62d65be')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
