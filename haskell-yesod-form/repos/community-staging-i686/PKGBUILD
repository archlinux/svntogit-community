# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-form
pkgname=haskell-yesod-form
pkgver=1.4.13
pkgrel=1
pkgdesc="Form handling support for Yesod Web Framework"
url="http://www.yesodweb.com/"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-aeson" "haskell-attoparsec" "haskell-blaze-builder" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-byteable" "haskell-data-default" "haskell-email-validate"
         "haskell-network-uri" "haskell-persistent" "haskell-resourcet" "haskell-semigroups"
         "haskell-shakespeare" "haskell-text" "haskell-wai" "haskell-xss-sanitize"
         "haskell-yesod-core" "haskell-yesod-persistent")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('6b023cdb536bdf9b88b9a503e20a1190e4eb1c69e14663527cab84c22c69be30268e9cf58073ac3ea3a9004e588175dc0e11dfcc78efbb477aab856cde93cde8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    LC_CTYPE=en_US.UTF-8 runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    install -d -m755 "$pkgdir"/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/$pkgname/html "$pkgdir"/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
