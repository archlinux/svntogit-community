# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-persistent
pkgname=haskell-yesod-persistent
pkgver=1.4.3
pkgrel=19
pkgdesc="Some helpers for using Persistent from Yesod."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-blaze-builder" "haskell-conduit" "haskell-persistent"
         "haskell-persistent-template" "haskell-resource-pool" "haskell-resourcet"
         "haskell-yesod-core")
makedepends=('ghc' 'haskell-hspec' 'haskell-wai-extra' 'haskell-persistent-sqlite' 'haskell-text')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d609ed1f49d11936fb162eb643006b0e2bebe707a1d94e595d38b568769b30bb507f368fbf2801330f34824609e60e9230db3a24b7ca51b6a13f29b6cbd9af9b')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
