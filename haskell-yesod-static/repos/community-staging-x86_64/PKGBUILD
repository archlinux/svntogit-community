# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-static
pkgname=haskell-yesod-static
pkgver=1.5.1.1
pkgrel=38
pkgdesc="Static file serving subsite for Yesod Web Framework."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-async" "haskell-attoparsec" "haskell-base64-bytestring"
         "haskell-blaze-builder" "haskell-byteable" "haskell-conduit" "haskell-conduit-extra"
         "haskell-cryptohash" "haskell-cryptohash-conduit" "haskell-css-text" "haskell-data-default"
         "haskell-exceptions" "haskell-file-embed" "haskell-hashable" "haskell-hjsmin"
         "haskell-http-types" "haskell-mime-types" "haskell-old-time" "haskell-resourcet"
         "haskell-text" "haskell-unix-compat" "haskell-unordered-containers" "haskell-wai"
         "haskell-wai-app-static" "haskell-yesod-core")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('cdb50763c4cbd2b8fcdb2b9f2f2526648e82454c62d49bfd6d165af80a192a92')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
