# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=yesod-test
pkgname=haskell-yesod-test
pkgver=1.6.14
pkgrel=8
pkgdesc="Integration testing for WAI/Yesod Applications"
url="https://www.yesodweb.com"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-aeson' 'haskell-attoparsec' 'haskell-blaze-builder'
         'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-case-insensitive' 'haskell-conduit'
         'haskell-cookie' 'haskell-hspec-core' 'haskell-html-conduit' 'haskell-http-types'
         'haskell-memory' 'haskell-network' 'haskell-pretty-show' 'haskell-wai'
         'haskell-wai-extra' 'haskell-xml-conduit' 'haskell-xml-types' 'haskell-yesod-core')
makedepends=('ghc' 'haskell-yesod-form' 'haskell-hspec' 'haskell-unliftio' 'haskell-unliftio-core')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('63c3f168f70e8ef1a9ba409a3052bab6c21aed8cb38df3c3ae68d553f74f1ec213db2120d9f985363d167726c161a24bc4a4e4ebd21a5f00b11259e931c0321e')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
