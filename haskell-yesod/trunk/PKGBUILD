# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod
pkgname=haskell-yesod
pkgver=1.6.1.0
pkgrel=29
pkgdesc="Creation of type-safe, RESTful web applications."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-conduit' 'haskell-data-default-class'
         'haskell-fast-logger' 'haskell-file-embed' 'haskell-monad-logger' 'haskell-shakespeare'
         'haskell-streaming-commons' 'haskell-unordered-containers' 'haskell-wai'
         'haskell-wai-extra' 'haskell-wai-logger' 'haskell-warp' 'haskell-yaml'
         'haskell-yesod-core' 'haskell-yesod-form' 'haskell-yesod-persistent')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('712eed39b64f515cb949186ddb950133cc2daccde6d4a9b0115809e5f4726a957b2809ee785b348971c617b3d173bcd40776b065f5bf1d68b79af12905ada77a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
