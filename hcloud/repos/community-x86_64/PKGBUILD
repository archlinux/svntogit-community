# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Konrad Tegtmeier <konrad.tegtmeier+aur@gmail.com>

_pkgname=cli
pkgname=hcloud
pkgver=1.29.0
pkgrel=1
pkgdesc="CLI for Hetzner Cloud"
arch=('x86_64')
url="https://github.com/hetznercloud/cli"
license=('MIT')
optdepends=('bash-completion: tab auto-completion'
            'zsh: tab auto-completion')
makedepends=('go-pie')
depends=('glibc')
provides=('hcloud')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hetznercloud/cli/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('2bf7a0961e1d35c06c6e4566459958875c49fed028b14888d80716d1ac03b43ccdfa29b977a7af898f84d861aebc94ab967efa9044e813630d6ff43df2963768')

build() {
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  cd "cli-${pkgver}"
  go build \
    -ldflags "-linkmode=external -w -X github.com/hetznercloud/cli/internal/version.Version=${pkgver}" \
    github.com/hetznercloud/cli/cmd/hcloud
  chmod +x ./hcloud
}

check() {
  cd "cli-${pkgver}"
  go test -v -x ./...
}

package() {
  cd "cli-${pkgver}"
  install -Dsm755 ./hcloud "${pkgdir}/usr/bin/hcloud"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mkdir -p "$pkgdir"/usr/share/{bash-completion/completions,zsh/site-functions,fish/vendor_completions.d}
  ./hcloud completion bash > "${pkgdir}/usr/share/bash-completion/completions/hcloud"
  ./hcloud completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_hcloud"
  ./hcloud completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/hcloud.fish"
}
