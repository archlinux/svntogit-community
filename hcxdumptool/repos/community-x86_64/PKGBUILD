# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxdumptool
pkgver=6.2.0
pkgrel=1
pkgdesc='Small tool to capture packets from wlan devices'
url='https://github.com/ZerBea/hcxdumptool'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'openssl')
source=(https://github.com/ZerBea/hcxdumptool/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxdumptool/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('e657942197325f5a1c96f1244f6b5fa2e06c025ac82f69108f6fd13c31dc375e54c4f464e6c729652d105d83ff79fee747c862d9c1271da559b22c6112dfcefb'
            'SKIP')
b2sums=('c1a86c800cb1d59d0041518b5fbd42b6f5c44e1a648780e6db3faf07326cab2302f81090c894a8483e6ae01e6c371c71f3810efb3c5110b135ccae3dac16adb4'
        'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

build() {
  cd ${pkgname}-${pkgver}
  make PRODUCTION=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr PRODUCTION=1 install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
