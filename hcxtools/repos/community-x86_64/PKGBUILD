# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxtools
_gitcommit=199019a15ff366bb44ac62425945e42442545230
pkgver=5.1.1
pkgrel=1
pkgdesc='Portable solution for capturing wlan traffic and conversion to hashcat and John the Ripper formats'
url='https://github.com/ZerBea/hcxtools'
arch=('x86_64')
license=('MIT')
depends=('curl' 'libpcap' 'zlib' 'openssl')
makedepends=('git')
source=("${pkgname}::git+https://github.com/ZerBea/hcxtools#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --long --tags | sed -E 's|v?(.+)|\1|' | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed -E 's|\.r0\.\w+$||'
}

build() {
  cd ${pkgname}
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
