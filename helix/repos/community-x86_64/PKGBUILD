# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Wojciech Kępka (wojciech@wkepka.dev)

pkgname=helix
pkgver=0.5.0
_gitcommit=a1b7f003a6ea61b2a77056ce8865a779b3452975
pkgrel=1
pkgdesc="A post-modern modal text editor"
arch=('x86_64')
url="https://helix-editor.com"
license=('MPL2')
depends=('bash')
makedepends=('rust' 'git')
install="$pkgname.install"
source=("${pkgname}::git+https://github.com/helix-editor/helix.git#commit=${_gitcommit}"
  "${pkgname}-tree-sitter-python::git+https://github.com/tree-sitter/tree-sitter-python"
  "${pkgname}-tree-sitter-cpp::git+https://github.com/tree-sitter/tree-sitter-cpp"
  "${pkgname}-tree-sitter-agda::git+https://github.com/tree-sitter/tree-sitter-agda"
  "${pkgname}-tree-sitter-typescript::git+https://github.com/tree-sitter/tree-sitter-typescript"
  "${pkgname}-tree-sitter-php::git+https://github.com/tree-sitter/tree-sitter-php"
  "${pkgname}-tree-sitter-java::git+https://github.com/tree-sitter/tree-sitter-java"
  "${pkgname}-tree-sitter-html::git+https://github.com/tree-sitter/tree-sitter-html"
  "${pkgname}-tree-sitter-css::git+https://github.com/tree-sitter/tree-sitter-css"
  "${pkgname}-tree-sitter-javascript::git+https://github.com/tree-sitter/tree-sitter-javascript"
  "${pkgname}-tree-sitter-bash::git+https://github.com/tree-sitter/tree-sitter-bash"
  "${pkgname}-tree-sitter-scala::git+https://github.com/tree-sitter/tree-sitter-scala"
  "${pkgname}-tree-sitter-json::git+https://github.com/tree-sitter/tree-sitter-json"
  "${pkgname}-tree-sitter-go::git+https://github.com/tree-sitter/tree-sitter-go"
  "${pkgname}-tree-sitter-c::git+https://github.com/tree-sitter/tree-sitter-c"
  "${pkgname}-tree-sitter-c-sharp::git+https://github.com/tree-sitter/tree-sitter-c-sharp"
  "${pkgname}-tree-sitter-rust::git+https://github.com/tree-sitter/tree-sitter-rust"
  "${pkgname}-tree-sitter-elixir::git+https://github.com/elixir-lang/tree-sitter-elixir"
  "${pkgname}-tree-sitter-protobuf::git+https://github.com/yusdacra/tree-sitter-protobuf.git"
  "${pkgname}-tree-sitter-toml::git+https://github.com/ikatyang/tree-sitter-toml"
  "${pkgname}-tree-sitter-haskell::git+https://github.com/tree-sitter/tree-sitter-haskell"
  "${pkgname}-tree-sitter-ruby::git+https://github.com/tree-sitter/tree-sitter-ruby"
  "${pkgname}-tree-sitter-latex::git+https://github.com/latex-lsp/tree-sitter-latex"
  "${pkgname}-tree-sitter-ocaml::git+https://github.com/tree-sitter/tree-sitter-ocaml"
  "${pkgname}-tree-sitter-yaml::git+https://github.com/ikatyang/tree-sitter-yaml"
  "${pkgname}-tree-sitter-lua::git+https://github.com/nvim-treesitter/tree-sitter-lua"
  "${pkgname}-tree-sitter-svelte::git+https://github.com/Himujjal/tree-sitter-svelte"
  "${pkgname}-tree-sitter-julia::git+https://github.com/tree-sitter/tree-sitter-julia"
  "${pkgname}-tree-sitter-cmake::git+https://github.com/uyha/tree-sitter-cmake"
  "${pkgname}-tree-sitter-tsq::git+https://github.com/tree-sitter/tree-sitter-tsq"
  "${pkgname}-tree-sitter-zig::git+https://github.com/maxxnino/tree-sitter-zig"
  "${pkgname}-tree-sitter-vue::git+https://github.com/ikatyang/tree-sitter-vue"
  "${pkgname}-tree-sitter-swift::git+https://github.com/tree-sitter/tree-sitter-swift"
  "${pkgname}-tree-sitter-nix::git+https://github.com/cstrahan/tree-sitter-nix"
  "${pkgname}-tree-sitter-ledger::git+https://github.com/cbarrete/tree-sitter-ledger"
  "$pkgname.sh"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'f782c93bc0f2c7b27ec62fb2ba36e11b3354c3953405f8d2017b039c9eac500a')

prepare() {
  cd "$pkgname"

  git submodule init
  git config submodule."helix-syntax/languages/tree-sitter-cpp".url "${srcdir}/${pkgname}"-tree-sitter-cpp
  git config submodule."helix-syntax/languages/tree-sitter-javascript".url "${srcdir}/${pkgname}"-tree-sitter-javascript
  git config submodule."helix-syntax/languages/tree-sitter-julia".url "${srcdir}/${pkgname}"-tree-sitter-julia
  git config submodule."helix-syntax/languages/tree-sitter-python".url "${srcdir}/${pkgname}"-tree-sitter-python
  git config submodule."helix-syntax/languages/tree-sitter-typescript".url "${srcdir}/${pkgname}"-tree-sitter-typescript
  git config submodule."helix-syntax/languages/tree-sitter-agda".url "${srcdir}/${pkgname}"-tree-sitter-agda
  git config submodule."helix-syntax/languages/tree-sitter-go".url "${srcdir}/${pkgname}"-tree-sitter-go
  git config submodule."helix-syntax/languages/tree-sitter-ruby".url "${srcdir}/${pkgname}"-tree-sitter-ruby
  git config submodule."helix-syntax/languages/tree-sitter-java".url "${srcdir}/${pkgname}"-tree-sitter-java
  git config submodule."helix-syntax/languages/tree-sitter-php".url "${srcdir}/${pkgname}"-tree-sitter-php
  git config submodule."helix-syntax/languages/tree-sitter-html".url "${srcdir}/${pkgname}"-tree-sitter-html
  git config submodule."helix-syntax/languages/tree-sitter-scala".url "${srcdir}/${pkgname}"-tree-sitter-scala
  git config submodule."helix-syntax/languages/tree-sitter-bash".url "${srcdir}/${pkgname}"-tree-sitter-bash
  git config submodule."helix-syntax/languages/tree-sitter-rust".url "${srcdir}/${pkgname}"-tree-sitter-rust
  git config submodule."helix-syntax/languages/tree-sitter-json".url "${srcdir}/${pkgname}"-tree-sitter-json
  git config submodule."helix-syntax/languages/tree-sitter-css".url "${srcdir}/${pkgname}"-tree-sitter-css
  git config submodule."helix-syntax/languages/tree-sitter-c-sharp".url "${srcdir}/${pkgname}"-tree-sitter-c-sharp
  git config submodule."helix-syntax/languages/tree-sitter-c".url "${srcdir}/${pkgname}"-tree-sitter-c
  git config submodule."helix-syntax/languages/tree-sitter-haskell".url "${srcdir}/${pkgname}"-tree-sitter-haskell
  git config submodule."helix-syntax/languages/tree-sitter-swift".url "${srcdir}/${pkgname}"-tree-sitter-swift
  git config submodule."helix-syntax/languages/tree-sitter-toml".url "${srcdir}/${pkgname}"-tree-sitter-toml
  git config submodule."helix-syntax/languages/tree-sitter-elixir".url "${srcdir}/${pkgname}"-tree-sitter-elixir
  git config submodule."helix-syntax/languages/tree-sitter-nix".url "${srcdir}/${pkgname}"-tree-sitter-nix
  git config submodule."helix-syntax/languages/tree-sitter-latex".url "${srcdir}/${pkgname}"-tree-sitter-latex
  git config submodule."helix-syntax/languages/tree-sitter-ledger".url "${srcdir}/${pkgname}"-tree-sitter-ledger
  git config submodule."helix-syntax/languages/tree-sitter-protobuf".url "${srcdir}/${pkgname}"-tree-sitter-protobuf
  git config submodule."helix-syntax/languages/tree-sitter-ocaml".url "${srcdir}/${pkgname}"-tree-sitter-ocaml
  git config submodule."helix-syntax/languages/tree-sitter-lua".url "${srcdir}/${pkgname}"-tree-sitter-lua
  git config submodule."helix-syntax/languages/tree-sitter-yaml".url "${srcdir}/${pkgname}"-tree-sitter-yaml
  git config submodule."helix-syntax/languages/tree-sitter-zig".url "${srcdir}/${pkgname}"-tree-sitter-zig
  git config submodule."helix-syntax/languages/tree-sitter-svelte".url "${srcdir}/${pkgname}"-tree-sitter-svelte
  git config submodule."helix-syntax/languages/tree-sitter-vue".url "${srcdir}/${pkgname}"-tree-sitter-vue
  git config submodule."helix-syntax/languages/tree-sitter-tsq".url "${srcdir}/${pkgname}"-tree-sitter-tsq
  git config submodule."helix-syntax/languages/tree-sitter-cmake".url "${srcdir}/${pkgname}"-tree-sitter-cmake

  git submodule update --init --recursive
  cargo fetch --locked
}

build() {
  cd "$pkgname"
  cargo build --frozen --release
}

check() {
  cd "$pkgname"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"
  install -Dm 755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "target/release/hx" "$pkgdir/usr/lib/$pkgname/hx"
  mkdir -p "$pkgdir/usr/lib/helix/"
  cp -r "runtime" "$pkgdir/usr/lib/helix/"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
