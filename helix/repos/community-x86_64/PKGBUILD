# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Maintainer: Maxim Baz <archlinux at maximbaz dot com>
# Contributor: Wojciech Kępka (wojciech@wkepka.dev)

pkgname=helix
pkgver=22.12
pkgrel=3
pkgdesc="A post-modern modal text editor"
arch=('x86_64')
url="https://helix-editor.com"
license=('MPL2')
depends=('bash' 'hicolor-icon-theme')
makedepends=('cargo' 'git')
optdepends=(
  'clang: for C/C++ language support'
  'dart: for Dart language support'
  'elvish: for elvish language support'
  'gopls: for Go language support'
  'haskell-language-server: for Haskell language support'
  'bash-language-server: for Bash language support'
  'julia: for Julia language support'
  'lua-language-server: for Lua language support'
  'python-lsp-server: for Python language support'
  'r: for R and rmarkdown language support'
  'racket: for racket language support'
  'rust-analyzer: for Rust language support'
  'taplo: for TOML language support'
  'texlab: for LaTeX language support'
  'typescript-language-server: for jsx, tsx, typescript language support'
  'vscode-css-languageserver: for CSS and SCSS support'
  'vscode-html-languageserver: for HTML language support'
  'yaml-language-server: for YAML language support'
)
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/helix-editor/helix/archive/$pkgver.tar.gz"
        "$pkgname.sh")
sha256sums=('edae8af46401b45c3e71c38b4fa99f931c4458127978ccd1b29aaae79331d972'
            '90d14e5c8fb65e64b508fcf4358cb45191b39e24bd6c5981814139a2a627fa5c')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i "s|hx|helix|g" contrib/completion/hx.*
  sed -i 's|hx|helix|g' contrib/Helix.desktop
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "target/release/hx" "$pkgdir/usr/lib/$pkgname/hx"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"

  local runtime_dir="$pkgdir/var/lib/$pkgname/runtime"
  mkdir -p "$runtime_dir/grammars"
  cp -r "runtime/queries" "$runtime_dir"
  cp -r "runtime/themes" "$runtime_dir"
  find "runtime/grammars" -type f -name '*.so' -exec \
    install -Dm 755 {} -t "$runtime_dir/grammars" \;
  install -Dm 644 runtime/tutor -t "$runtime_dir"
  ln -s "/var/lib/$pkgname/runtime" "$pkgdir/usr/lib/$pkgname/runtime"

  install -Dm 644 "contrib/completion/hx.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "contrib/completion/hx.fish" "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm 644 "contrib/completion/hx.zsh" "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm 644 "contrib/Helix.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm 644 "contrib/$pkgname.png" -t "$pkgdir/usr/share/icons/hicolor/256x256/apps"
}

# vim: ts=2 sw=2 et:
