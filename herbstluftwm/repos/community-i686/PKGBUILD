# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: thorsten w. <p@thorsten-wissmann.de>

pkgname=herbstluftwm
pkgver=0.6.2
pkgrel=3
pkgdesc="Manual tiling window manager for X"
arch=('i686' 'x86_64')
url="http://herbstluftwm.org"
license=('BSD')
depends=('glib2' 'libxinerama')
optdepends=('xorg-xsetroot: to set wallpaper color in default autostart'
            'xterm: used as the terminal in default autostart'
            'dzen2: used in the default panel.sh'
            'dmenu: used in some example scripts')
source=($url/tarballs/$pkgname-$pkgver.tar.gz)
md5sums=('8bfbbdb16cf88821c8dacd5165590fd2')

build() {
  cd $pkgname-$pkgver

  TERM=linux make
}

package() {
  cd $pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses/$pkgname/

  ln -s /usr/share/doc/$pkgname/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
