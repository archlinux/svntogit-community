# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: thorsten w. <p@thorsten-wissmann.de>

pkgname=herbstluftwm
pkgver=0.7.0
pkgrel=1
pkgdesc="Manual tiling window manager for X"
arch=('x86_64')
url="http://herbstluftwm.org"
license=('BSD')
depends=('glib2' 'libxinerama')
optdepends=('xorg-xsetroot: to set wallpaper color in default autostart'
            'xterm: used as the terminal in default autostart'
            'dzen2: used in the default panel.sh'
            'dmenu: used in some example scripts')
source=($url/tarballs/$pkgname-$pkgver.tar.gz)
md5sums=('2a4f05d113fd81ceda0246ba63288b05')

build() {
  cd $pkgname-$pkgver

  TERM=linux make
}

package() {
  cd $pkgname-$pkgver

  make PREFIX=/usr DESTDIR="$pkgdir" \
    BASHCOMPLETIONDIR=/usr/share/bash-completion/completions install

  install -d "$pkgdir"/usr/share/licenses/$pkgname/

  ln -s /usr/share/doc/$pkgname/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/share/bash-completion/completions/herbstclient{-completion,}
}
