# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brian Nguyen <bhnguyen@gmail.com>
# Original Packager: Tom K <tomk@runbox.com>

pkgname=hevea
pkgver=2.30
pkgrel=1
pkgdesc="A quite complete and fast LATEX to HTML translator"
arch=(x86_64)
depends=('texlive-bin')
makedepends=('ocaml' 'ocamlbuild')
license=('GPL')
url="http://pauillac.inria.fr/~maranget/hevea/"
install=hevea.install
source=(http://pauillac.inria.fr/~maranget/$pkgname/distri/$pkgname-$pkgver.tar.gz)
sha256sums=('9e93deac8d2cc62a8d9eae2817094cdba81cabef264d009f3d434d85ab9a249c')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i 's:usr/local:usr:' Makefile
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -i 's:/usr:$PREFIX:' config.sh
  make install PREFIX="$pkgdir"/usr
  mkdir -p "$pkgdir"/usr/share/texmf/tex/latex
  ln -s /usr/lib/hevea/hevea.sty "$pkgdir"/usr/share/texmf/tex/latex/hevea.sty
}
