# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: TingPing <tingping@tingping.se>

pkgname=hexchat
pkgver=2.10.2
pkgrel=3
pkgdesc='A popular and easy to use graphical IRC (chat) client'
arch=('i686' 'x86_64')
url='http://hexchat.github.io/'
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gtk2' 'libcanberra' 'libnotify'
         'libproxy' 'pciutils')
makedepends=('intltool' 'iso-codes' 'perl' 'python')
optdepends=('enchant: Spell check'
            'perl: Perl plugin'
            'python: Python plugin')
install='hexchat.install'
source=("http://dl.hexchat.net/hexchat/hexchat-${pkgver}.tar.xz")
sha256sums=('87ebf365c576656fa3f23f51d319b3a6d279e4a932f2f8961d891dd5a5e1b52c')

prepare() {
  cd hexchat-${pkgver}

#  sed 's/CFLAGS="$PERL_CFLAGS"/CFLAGS="$PERL_CFLAGS -fuse-ld=gold"/' -i configure
}

build() {
  cd hexchat-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-python='python3' \
    --enable-textfe
  make
}

package() {
  cd hexchat-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
