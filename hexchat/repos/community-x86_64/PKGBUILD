# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: TingPing <tingping@tingping.se>

pkgname=hexchat
pkgver=2.9.6.1
pkgrel=1
pkgdesc="A popular and easy to use graphical IRC (chat) client"
arch=('i686' 'x86_64')
url="http://hexchat.github.io/"
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'libcanberra' 'libnotify' 'libproxy' 'libsexy' 'pciutils')
makedepends=('perl' 'python')
optdepends=('enchant: Spell check'
            'perl: Perl plugin'
            'python: Python plugin')
install="${pkgname}.install"
source=("http://dl.hexchat.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('6267ff3c4d0aec8a808365b384115a769d5001d99dbb10403634667c43966470')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --enable-{python='python3',shm,spell='libsexy',textfe}
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
