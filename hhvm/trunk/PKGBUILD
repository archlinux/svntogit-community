# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: James Miller <james@pocketrent.com>

pkgname=hhvm
pkgver=3.7.2
pkgrel=2

_thirdparty_commit=a7d0e6834ac6280b15ba2777f7d9c621bc8446cb
_folly_commit=7c9f26357a1a2852ea11b051e1e0402dcfbce861
_thrift_commit=385419f71e40a77df1fcfe6d1a9349d99d200902
_proxygen_commit=72622b8af97bd56a0dd271b10be18864653a8bb7
_webscalesql_commit=19c1a57078cdec7d238037258345151e871caef4
_mcrouter_commit=55dece4d60de21d68e7651bd6083c403f9edf0f5
_squangle_commit=a608c72296a0cb954de4bc2c22939fb24f482daa

pkgdesc="Virtual Machine, Runtime, and JIT for PHP"
arch=('x86_64')
url="http://hhvm.com"
license=('PHP')
depends=('boost-libs' 'google-glog' 'libmysqlclient' 'libmemcached' 'libzip'
         'libxslt' 'intel-tbb' 'libmcrypt' 'oniguruma' 'jemalloc' 'curl' 'libvpx'
         'libdwarf' 'imagemagick' 'libedit' 'sqlite' 'libyaml' 'fribidi' 're2'
         'gperf' 'c-client')
makedepends=('git' 'cmake' 'gcc' 'boost' 'gflags' 'python2' 'pfff' 'mongodb'
             'ragel' 'libmariadbclient' 'unixodbc')
source=("https://github.com/facebook/hhvm/archive/HHVM-$pkgver.tar.gz"
        "https://github.com/hhvm/hhvm-third-party/archive/$_thirdparty_commit/hhvm-third-party-$_thirdparty_commit.tar.gz"
        "https://github.com/facebook/folly/archive/$_folly_commit/folly-$_folly_commit.tar.gz"
        "https://github.com/facebook/fbthrift/archive/$_thrift_commit/thrift-$_thrift_commit.tar.gz"
        "https://github.com/facebook/proxygen/archive/$_proxygen_commit/proxygen-$_proxygen_commit.tar.gz"
        "https://github.com/webscalesql/webscalesql-5.6/archive/$_webscalesql_commit/webscalesql-$_webscalesql_commit.tar.gz"
        "https://github.com/facebook/mcrouter/archive/$_mcrouter_commit/mcrouter-$_mcrouter_commit.tar.gz"
        "https://github.com/facebook/squangle/archive/$_squangle_commit/squangle-$_squangle_commit.tar.gz"
        'hhvm.tmpfile'
        'hhvm.service'
        'hhvm@.service'
        'php.ini'
        'server.ini'
        'gcc51.patch'
        'libstdcxx-dual-abi.patch'
        'libvpx14.patch::https://patch-diff.githubusercontent.com/raw/facebook/hhvm/pull/5191.diff')
install=hhvm.install
backup=(etc/hhvm/{php,server}.ini)
options+=('!buildflags')

prepare() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver

    sed -r 's/service hhvm (start|stop|restart)/systemctl \1 hhvm.service/' \
        -i hphp/tools/oss-repo-mode

    patch -p1 -i "$srcdir"/libvpx14.patch
    patch -p1 -i "$srcdir"/gcc51.patch

    # Fix sendmail path
    sed -r 's#/usr/lib/sendmail#/usr/bin/sendmail#g' -i \
        hphp/runtime/base/runtime-option.cpp

    rm -rf third-party
    ln -s "$srcdir"/hhvm-third-party-$_thirdparty_commit third-party

    cd third-party/folly
    rm -rf src
    ln -s "$srcdir"/folly-$_folly_commit src
    patch -d src -p1 -i "$srcdir"/libstdcxx-dual-abi.patch

    cd ../thrift
    rm -rf src
    ln -s "$srcdir"/fbthrift-$_thrift_commit src

    cd ../proxygen
    rm -rf src
    ln -s "$srcdir"/proxygen-$_proxygen_commit src

    cd ../mcrouter
    rm -rf src
    ln -s "$srcdir"/mcrouter-$_mcrouter_commit src

    cd ../squangle
    rm -rf src squangle
    ln -s "$srcdir"/squangle-$_squangle_commit src
    ln -s src/squangle

    cd ../webscalesqlclient
    rm -rf src webscalesql-5.6
    ln -s "$srcdir"/webscalesql-5.6-$_webscalesql_commit webscalesql-5.6
    ln -s "$srcdir"/webscalesql-5.6-$_webscalesql_commit src
}

build() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver
    msg2 "Building hhvm"

    cmake -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_PREFIX_PATH="$srcdir" \
        -DENABLE_MONGO:BOOL=ON \
        -DMYSQL_UNIX_SOCK_ADDR=/run/mysqld/mysqld.sock \
        .

    make

    for hacktool in hackificator remove_soft_types; do
        cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/hack/tools/$hacktool
        make depend
        make
    done
}

# check() {
#     cd "$srcdir"/$pkgname-HHVM-$pkgver/hphp/test
#     ./run --threads 8 quick
# }

package() {
    cd "$srcdir"/$pkgname-HHVM-$pkgver
    make DESTDIR="$pkgdir/" install

    cd hphp/hack/bin
    for bin in hh_* tools/*; do
        install -Dm755 $bin "$pkgdir"/usr/bin/$(basename $bin)
    done

    cd "$srcdir"
    install -Dm644 hhvm.tmpfile "$pkgdir"/usr/lib/tmpfiles.d/hhvm.conf
    install -Dm644 hhvm.service "$pkgdir"/usr/lib/systemd/system/hhvm.service
    install -Dm644 hhvm@.service "$pkgdir"/usr/lib/systemd/system/hhvm@.service

    install -Dm644 php.ini "$pkgdir"/etc/hhvm/php.ini
    install -Dm644 server.ini "$pkgdir"/etc/hhvm/server.ini
}

sha256sums=('4f1ee67a7e848002361ec628833adb89b78d450f1087f75dc69807ed0593dd7a'
            '1e7e62392d313e7dd0dad85c729d195a486470c766881296685f9574e08a9926'
            '07a9b48818b0695354baf364a26a5d85a29020b61739dd2e1a281509a9f7ff8e'
            '8124fce00a89a10ce76e8a54dfe398cfe35347283c7088eeb3de9528371db798'
            'db3f1252dc9a63a512d7be04c8800ab7b6fcee1162ba6865471b39e94b3c76ed'
            '8856043cf6939801638378c27817e1cba27f0faa4e5961b7487e195a8090dde8'
            'd9a7a745db79796b2f9f10f9a533459085f29289ea9ed80df12cb4ca451607e6'
            'e496de037ce604c4661faec6279791cb307d051dc6fcfdf690001249f636174c'
            'c356010a6d6b976f387bb205a75ea07d5f40593a8010483f2ed0f66f112331bc'
            '8b50d1ef9f5f726e6d8d469a8c84d85ad63f8b507b97d258b4d751a0e3e221df'
            '59c640602929dac0aa34d06c668ed69361eb4b7b47a77f9aa0badb4d0b61571c'
            '3e3093f817706c238fad021483f114fd4ce0b45d84097dcb7870157fc9ec769f'
            '5b53bc57965e1c5151d720dc7f63f1b2e8ebd5e758b2ef0be3b74df38ebcbce0'
            '3a7d1cfa7fb87365bbfc65975b8a96627c34d5389eb0de9c360f195cb717dfd0'
            '8fe2192e3e10d46a77bbe4fda7ed588eecda22fd83d5952c79985f642bf00d68'
            '975885203cef50997a227f5355d49cb9135f0668981059df213f7f0b44597cf8')
