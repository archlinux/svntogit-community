# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=higan-qt
pkgver=094
pkgrel=1
pkgdesc='Nintendo multi-system emulator'
arch=('i686' 'x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('libao' 'libpulse' 'openal' 'qt4' 'sdl' 'xdialog')
makedepends=('mesa')
optdepends=('beat: Delta patcher')
conflicts=('higan-gtk')
source=("http://byuu.org/files/${pkgname%-*}_v${pkgver}-source.tar.xz"
        'higan')
sha256sums=('5e149df9d50c1066eb97c0d66665428d75304da782bba5a87078b87fc311151b'
            'c799232a0f67c4b7f40fc6ad05325a6c04e86d78e40f361d02f168a16d11d048')

_profiles='accuracy balanced performance'

prepare() {
  cd higan_v${pkgver}-source

  sed "s/-march=native/-march=${CARCH/_/-} -mtune=generic/g" -i Makefile

  moc-qt4 -i -Iphoenix/qt/ -o phoenix/qt/platform.moc phoenix/qt/platform.moc.hpp
}

build() {
  cd higan_v${pkgver}-source

  make phoenix=qt -C ananke

  for _profile in ${_profiles}; do
    make phoenix=qt profile=${_profile}
    mv out/higan{,-${_profile}}
    make clean
  done
}

package() {
  cd higan_v${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/{bin,lib,share/{applications,pixmaps,higan/Video\ Shaders}}
  install -m 755 {..,"${pkgdir}"/usr/bin}/higan
  install -m 644 data/higan.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 data/higan.png "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve=ownership profile/* data/cheats.bml "${pkgdir}"/usr/share/higan/
  cp -dr --no-preserve=ownership shaders/*.shader "${pkgdir}"/usr/share/higan/Video\ Shaders/

  install -m 644 ananke/libananke.so "${pkgdir}"/usr/lib/libananke.so.1
  ln -s libananke.so.1 "${pkgdir}"/usr/lib/libananke.so

  for _profile in ${_profiles}; do
    install -m 755 {out,"${pkgdir}"/usr/bin}/higan-${_profile}
  done
}

# vim: ts=2 sw=2 et:
