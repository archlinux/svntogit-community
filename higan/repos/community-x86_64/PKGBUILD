# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=higan
pkgver=095
pkgrel=2
pkgdesc='Nintendo multi-system emulator'
arch=('i686' 'x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('alsa-lib' 'cairo' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'gtksourceview2' 'libao' 'libgl' 'libpulse' 'libx11' 'libxext' 'libxv'
         'openal' 'pango' 'sdl' 'zenity'
         'libudev.so')
makedepends=('mesa')
conflicts=('higan-gtk' 'higan-qt')
replaces=('higan-gtk' 'higan-qt')
source=("http://download.byuu.org/higan_v${pkgver}-source.7z"
        'higan-makefile.patch'
        'higan')
sha256sums=('443a11ef64b817861e9cb7b818fc9283afb50f8e254345eb88fa12c764f44f77'
            'cbdb3524bc5f0380df4d96312a15bc0bf641a7a82723bfb30998efeb977d4b7f'
            '4a9f59253caa51fa8313db421bf7ce431722c601594b2bdb0fa0459652284168')

prepare() {
  cd higan_v${pkgver}-source

  patch -Np1 -i ../higan-makefile.patch
}

build() {
  cd higan_v${pkgver}-source

  make -C icarus

  for p in {accuracy,balanced,performance}; do
    make profile=$p
    mv out/tomoko tomoko-$p
    make clean
  done
}

package() {
  cd higan_v${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,higan,pixmaps}}
  install -m 755 ../higan "${pkgdir}"/usr/bin/
  install -m 644 data/higan.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 data/higan.png "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' profile/* data/cheats.bml "${pkgdir}"/usr/share/higan/

  install -m 755 icarus/icarus "${pkgdir}"/usr/bin/

  for p in {accuracy,balanced,performance}; do
    install -m 755 tomoko-$p "${pkgdir}"/usr/bin/
  done
}

# vim: ts=2 sw=2 et:
