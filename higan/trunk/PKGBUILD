# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=higan
pkgver=106
pkgrel=1
pkgdesc='Nintendo multi-system emulator'
arch=('x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('alsa-lib' 'cairo' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'gtksourceview2' 'libao' 'libgl' 'libpulse' 'libx11' 'libxext' 'libxv'
         'openal' 'pango' 'sdl'
         'libudev.so')
makedepends=('mesa')
conflicts=('higan-gtk' 'higan-qt')
replaces=('higan-gtk' 'higan-qt')
source=("https://download.byuu.org/higan_v${pkgver}-source.7z"
        'higan-flags.patch'
        'higan-paths.patch')
sha256sums=('6564f91f232040c46c052de763dc139e94005e0f2376959fdacbb6ccd3fd6d18'
            '6cff6f9570de186bdfa4620d3a2b6e315118b6f6be45b35f3346995865f1718e'
            '6d0bf69e00c9ea5c5155b52b5ee59c253f2bacbc64f044826f53d4d6ae175c68')

prepare() {
  cd higan_v${pkgver}-source

  patch -Np1 -i ../higan-flags.patch
  patch -Np1 -i ../higan-paths.patch
}

build() {
  cd higan_v${pkgver}-source

  make -C higan
  make -C icarus
}

package() {
  cd higan_v${pkgver}-source

  install -Dm 755 higan/out/higan -t "${pkgdir}"/usr/bin/
  install -Dm 644 higan/data/higan.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 higan/data/higan.png -t "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' higan/systems "${pkgdir}"/usr/share/higan

  install -Dm 755 icarus/out/icarus -t "${pkgdir}"/usr/bin/
  install -Dm 644 icarus/data/icarus.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 icarus/data/icarus.png -t "${pkgdir}"/usr/share/pixmaps/
  install -dm 755 "${pkgdir}"/usr/share/icarus
  cp -dr --no-preserve='ownership' icarus/Database "${pkgdir}"/usr/share/icarus/
}

# vim: ts=2 sw=2 et:
