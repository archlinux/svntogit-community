# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Jan Fader <jan.fader@web.de>
pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.16.1
_pkgver=3.16
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(qt4 lua boost)
source=(http://www.andre-simon.de/zip/$pkgname-$_pkgver.tar.bz2{,.asc})
md5sums=('688bb537db149dc720b9ef1ffc796ced'
         'SKIP')

build() {
  cd "$srcdir/$pkgbase-$_pkgver"

  make QMAKE=qmake-qt4
  make QMAKE=qmake-qt4 gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua')
  cd "$srcdir/$pkgbase-$_pkgver"

  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (Qt version)"
  depends=('qt4' 'highlight')
  cd "$srcdir/$pkgbase-$_pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install-gui
}

# vim:set ts=2 sw=2 et:
