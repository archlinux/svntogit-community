# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Jan Fader <jan.fader@web.de>
pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.6
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(qt lua boost)
source=(http://www.andre-simon.de/zip/$pkgname-$pkgver.tar.bz2)
md5sums=('b23093dba7c44a7628ee5c97e68c9941')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  make
  make gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua')
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (QT version)"
  depends=('qt' 'highlight')
  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" install-gui
}

# vim:set ts=2 sw=2 et:
