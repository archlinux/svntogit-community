# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgbase=himalaya
pkgname=('himalaya' 'vim-himalaya')
pkgver=0.4.0
pkgrel=4
pkgdesc="A CLI email client"
arch=('x86_64')
url="https://github.com/soywod/himalaya"
license=('BSD')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('abf3559b1f12c04761f0707cf5b3913afd76a6b1f05de22e2a936d7bbde64aa1fe8854216f7165af33acc615c7fa7f419c17ed9efd6b99bd8a8e51276d422ade')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --locked --lib
}

package_himalaya() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "assets/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
}

package_vim-himalaya() {
  arch=('any')
  depends=('vim-plugin-runtime' 'himalaya')
  pkgdesc+=' - Vim UI plugin'

  cd "$pkgbase-$pkgver/vim/"
  find autoload doc ftplugin lua plugin syntax \
    -type f -exec install -Dm 644 '{}' "$pkgdir/usr/share/vim/vimfiles/{}" \;
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 ../LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
