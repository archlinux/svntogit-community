# Maintainer: Torsten Keßler <tpkessler at archlinux dot org>
# Contributor: acxz <akashpatel2008 at yahoo dot com>
# Contributor: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: bartus <arch-user-repoᘓbartus.33mail.com>
# Contributor: pingplug <pingplug@foxmail.com>
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=hipmagma
pkgver=2.7.0
pkgrel=2
pkgdesc="Matrix Algebra on GPU and Multicore Architectures (HIP backend)"
arch=('x86_64')
url="https://icl.cs.utk.edu/magma/"
license=('custom')
depends=('blas' 'lapack' 'openmp' 'hip-runtime-amd' 'hipblas' 'hipsparse')
makedepends=('gcc-fortran' 'cmake' 'ninja')
optdepends=('python: for examples and tests'
            'gcc-fortran: Fortran interface')
_pkgname="magma"
source=("${pkgname}-${pkgver}.tar.gz::http://icl.cs.utk.edu/projectsfiles/$_pkgname/downloads/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('fda1cbc4607e77cacd8feb1c0f633c5826ba200a018f647f1c5436975b39fd18')
options=(!lto)

build() {
  cd ${_pkgname}-${pkgver}

  CXXFLAGS="${CXXFLAGS} -fcf-protection=none" \
  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_CXX_COMPILER=/opt/rocm/bin/hipcc \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DMAGMA_ENABLE_HIP=ON \
    -DGPU_TARGET="gfx803 gfx900 gfx906:xnack- gfx908:xnack- gfx90a:xnack+ gfx90a:xnack- gfx1010 gfx1011 gfx1012 gfx1030"
    #List of GPU targets from rocBLAS
  ninja -C build
}

package() {
  cd "${_pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -Cbuild install

  install -d "${pkgdir}"/usr/share/magma/example
  cp -r "${srcdir}"/magma-${pkgver}/example/* "${pkgdir}"/usr/share/magma/example/
  install -d "${pkgdir}"/usr/share/magma/testing
  cp -r "${srcdir}"/magma-${pkgver}/testing/* "${pkgdir}"/usr/share/magma/testing/
  install -Dm644 "${srcdir}"/magma-${pkgver}/COPYRIGHT "${pkgdir}"/usr/share/licenses/magma/LICENSE
}
