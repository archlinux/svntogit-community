# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hledger-iadd
pkgname=hledger-iadd
pkgver=1.3.14
pkgrel=32
pkgdesc="A terminal UI as drop-in replacement for hledger add"
url="https://github.com/hpdeifel/hledger-iadd#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-brick' 'haskell-free' 'haskell-hledger-lib' 'haskell-megaparsec'
         'haskell-microlens' 'haskell-microlens-th' 'haskell-optparse-applicative'
         'haskell-text-zipper' 'haskell-unordered-containers' 'haskell-vector' 'haskell-vty'
         'haskell-xdg-basedir')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        hledger-lib-1.22.patch)
sha256sums=('c5472492ec443948762e4e236d91d9057ed23e8f0866c2aa32929997ae479c4d'
            '37a144b21334670e126a6aa76ab806f72a28456d6a969496db2a7be2c70bf328')

prepare() {
  cd $_hkgname-$pkgver
  patch -p1 -i ../hledger-lib-1.22.patch
  uusi -d semigroups $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
