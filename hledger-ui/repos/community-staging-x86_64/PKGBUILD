# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.12.1
pkgrel=14
pkgdesc="Curses-style user interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-ansi-terminal' 'haskell-async'
         'haskell-base-compat-batteries' 'haskell-cmdargs' 'haskell-data-default' 'haskell-fsnotify'
         'haskell-microlens' 'haskell-microlens-platform' 'haskell-megaparsec' 'haskell-pretty-show'
         'haskell-safe' 'haskell-split' 'haskell-text-zipper' 'haskell-vector' 'haskell-brick'
         'haskell-vty')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('60bffe992673b7f1f7d4ac9c94a45daa24626314952e569ee66b543dc4ef7d6832e76bd785a067224e5c273c07ffddd4ccf40b656c42f33eb54027cee0b36baa')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -fthreaded
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
