# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.26.1
pkgrel=43
pkgdesc="Curses-style terminal interface for the hledger accounting system"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-ansi-terminal' 'haskell-async'
         'haskell-cmdargs' 'haskell-data-default' 'haskell-doclayout' 'haskell-extra'
         'haskell-fsnotify' 'haskell-microlens' 'haskell-microlens-platform' 'haskell-megaparsec'
         'haskell-safe' 'haskell-split' 'haskell-text-zipper'
         'haskell-vector' 'haskell-brick' 'haskell-vty')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('688aa88299693d06f1ce95fd653bc5450accb511bc90415ec02f40242b11324b0ff38ba9f51c9538ff6f3903c5ab77551b9077e38053412f9ce05e6d1ab3a145')

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir=$pkgname \
            -fthreaded
    runhaskell Setup build $MAKEFLAGS
}

package() {
    cd $pkgname-$pkgver
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    install -Dm644 hledger-ui.1 -t "$pkgdir"/usr/share/man/man1/
}
