# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger
pkgver=1.4
pkgrel=10
pkgdesc="Command-line interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-base-compat' 'haskell-diff'
         'haskell-file-embed' 'haskell-here' 'haskell-pretty-show' 'haskell-temporary'
         'haskell-tabular' 'haskell-hledger-lib' 'haskell-unordered-containers' 'haskell-cmdargs'
         'haskell-csv' 'haskell-data-default' 'haskell-hashable' 'haskell-hunit' 'haskell-mtl'
         'haskell-old-time' 'haskell-megaparsec' 'haskell-regex-tdfa' 'haskell-safe'
         'haskell-shakespeare' 'haskell-split' 'haskell-text' 'haskell-utf8-string'
         'haskell-utility-ht' 'haskell-wizards')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('25d4003cbfbfb672c665a5e0a2c6852df8c1ca95b8d33154376a527d27d1a7a114c00e75b5e2c8b8ac5bfe9cb7d8f8d0a410cadbf2900691e4e2b6e0645c7369')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -e 's/, megaparsec >=5.0 && < 6.2/, megaparsec >=5/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fterminfo -fthreaded
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
