# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hlint
pkgver=2.0.9
pkgrel=4
pkgdesc="Source code suggestions"
url="http://community.haskell.org/~ndm/hlint/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-ansi-terminal" "haskell-cmdargs" "haskell-cpphs" "haskell-extra"
         "haskell-src-exts" "haskell-hscolour" "haskell-uniplate" "haskell-refact" "haskell-text"
         "haskell-unordered-containers" "haskell-vector" "haskell-yaml")
conflicts=('haskell-hlint')
replaces=('haskell-hlint')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f2082eec31b73c2c1b1f7ce246b15178d00c8e6f9840619fc11c545cde4120713c274236c85993e217c17886cd824276cf2738c32aeaf67af9c8b4c147c09cf7')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fgpl -fthreaded
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.BSD3"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
