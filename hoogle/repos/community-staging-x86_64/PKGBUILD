# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hoogle
pkgver=5.0.17.12
pkgrel=1
pkgdesc="Haskell API Search"
url="https://www.haskell.org/hoogle/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-aeson' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-cmdargs' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-connection' 'haskell-extra' 'haskell-foundation' 'haskell-hashable'
         'haskell-http-conduit' 'haskell-http-types' 'haskell-js-flot' 'haskell-js-jquery'
         'haskell-mmap' 'haskell-network' 'haskell-old-locale' 'haskell-process-extras'
         'haskell-resourcet' 'haskell-src-exts' 'haskell-storable-tuple' 'haskell-tar'
         'haskell-uniplate' 'haskell-utf8-string' 'haskell-vector' 'haskell-wai'
         'haskell-wai-logger' 'haskell-warp' 'haskell-warp-tls' 'haskell-zlib')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1f27e3d25e00cf4ef82322130e8aad448d1e5cb02879e50d8312bd0db64ef5ab2632867f140cedbc90f74e4ac727930fe9a3b56f9c9f6c7782ec7a9ccffa8f39')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        --ghc-option='-pie'
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
