# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hoogle
pkgver=5.0.16
pkgrel=60
pkgdesc="Haskell API Search"
url="http://www.haskell.org/hoogle/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-aeson' 'haskell-cmdargs' 'haskell-conduit'
         'haskell-conduit-extra' 'haskell-connection' 'haskell-extra' 'haskell-src-exts'
         'haskell-http-conduit' 'haskell-http-types' 'haskell-js-flot' 'haskell-js-jquery'
         'haskell-mmap' 'haskell-network' 'haskell-network-uri' 'haskell-old-locale'
         'haskell-process-extras' 'haskell-resourcet' 'haskell-storable-tuple' 'haskell-tar'
         'haskell-text' 'haskell-uniplate' 'haskell-utf8-string' 'haskell-vector' 'haskell-wai'
         'haskell-wai-logger' 'haskell-warp' 'haskell-warp-tls' 'haskell-zlib')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        extra-1.6.6.patch)
sha512sums=('14b0d6290927234be7e6e6548529fef61d44972a21b9ac22815325214241095b61d60e3b95b6f20a5d40bf9386beff4036942a2c61925c745b767f2525b915c9'
            'be1cac2d0833e8fff846af9db097dbef23d97d69531b592fc33873813b0ec7c0c58a268f3b26ec4b079e474b9c0f7782355272d955eda54bf1aee77d294d39b2')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../extra-1.6.6.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
