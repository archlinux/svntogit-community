# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=hping
pkgver=3.0.0
pkgrel=4
pkgdesc='A command-line oriented TCP/IP packet assembler/analyzer.'
arch=('x86_64')
url='http://www.hping.org'
license=('GPL2' 'BSD')
depends=('libpcap')
source=("http://www.hping.org/hping3-20051105.tar.gz"
        'Makefile.patch'
        'bytesex.h.patch')
md5sums=('ca4ea4e34bcc2162aedf25df8b2d1747'
         '339a38f86169d1b9344287a85201afbe'
         '8af8e336819df1447b0c1b879a704be9')

build() {
  cd "${srcdir}/hping3-20051105"

  [[ "$CARCH" == "x86_64" ]] && patch -Np1 -i ../bytesex.h.patch

  MANPATH=/usr/share/man ./configure --no-tcl
  make
}

package() {
  cd "${srcdir}/hping3-20051105"

  patch -p1 < ../Makefile.patch

  make DESTDIR="${pkgdir}" install
  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
