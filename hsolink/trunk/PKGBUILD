# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>

pkgname=hsolink
pkgver=1.0.118
pkgrel=10
pkgdesc="HSO command line connection utility"
arch=('i686' 'x86_64')
url="http://www.pharscape.org/hsolinkcontrol.html"
license=('GPL2')
depends=('ozerocdoff' 'net-tools')
install=hsolink.install
source=("https://launchpad.net/ubuntu/+archive/primary/+files/hsolink_$pkgver.orig.tar.gz"
	"https://launchpad.net/ubuntu/+archive/primary/+files/hsolink_$pkgver-3.diff.gz")
md5sums=('8b5b179c487b7ad95112fe74dea7e302'
         'fe11286cdbb66f3323af5e1798e90ec3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 <$srcdir/hsolink_$pkgver-3.diff
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  chgrp network "$pkgdir"/usr/bin/hsolinkcontrol
  chmod ug+s "$pkgdir"/usr/bin/hsolinkcontrol
}
