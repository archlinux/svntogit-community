# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.9.12
pkgrel=1
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/'
license=('GPL2')
depends=('fltk' 'gnutls' 'libjpeg-turbo' 'libpng' 'libxpm' 'zlib'
         'hicolor-icon-theme')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('0906ec61cdac8d7349b13922f6566aebfe737b1695422a59d6c11ee7e6eaf99f'
            'SKIP')
validpgpkeys=('845464660B686AAB36540B6F999559A027815955') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix='/usr' \
        --enable-ssl \
        --enable-gnutls \
        --with-gui
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
