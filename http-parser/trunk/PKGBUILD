# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=http-parser
pkgver=2.7.1
pkgrel=1
pkgdesc="Parser for HTTP Request/Response written in C"
arch=('x86_64')
url="https://github.com/nodejs/http-parser"
license=('MIT')
depends=('glibc')
makedepends=('git')
source=("git+https://github.com/nodejs/http-parser.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i 's|-Werror||' $pkgname/Makefile
}

build() {
  cd $pkgname
  make library
}

check() {
  cd $pkgname
  make test
}

package() {
  cd $pkgname
  make PREFIX="$pkgdir/usr" install

  install -Dm644 LICENSE-MIT \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"

  ln -sf libhttp_parser.so.$pkgver "${pkgdir}/usr/lib/libhttp_parser.so"
}
