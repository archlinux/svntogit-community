# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>

pkgname=httpie
pkgver=0.9.1
pkgrel=1
pkgdesc="cURL for humans"
url="https://github.com/jkbr/httpie"
depends=('python-requests' 'python-pygments')
makedepends=('python-setuptools')
conflicts=(python-httpie)
replaces=(python-httpie python2-httpie)
license=('BSD')
arch=('any')
source=("http://pypi.python.org/packages/source/h/httpie/httpie-$pkgver.tar.gz")
md5sums=('eaa335be79938c3c0969056a5e392c58')

build() {
  cd httpie-$pkgver
  python3 setup.py build
}

package() {
  cd httpie-$pkgver
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/httpie/LICENSE"
  python3 setup.py install --root="$pkgdir" --optimize=1
}
