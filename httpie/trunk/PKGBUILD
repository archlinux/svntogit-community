# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>

pkgname=httpie
pkgver=2.0.0
pkgrel=2
pkgdesc="cURL for humans"
url="https://github.com/jakubroztocil/httpie"
depends=('python-requests' 'python-pygments' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-httpbin' 'python-mock')
conflicts=(python-httpie)
replaces=(python-httpie python2-httpie)
license=('BSD')
arch=('any')
source=($pkgname-$pkgver.tar.gz::"https://github.com/jakubroztocil/httpie/archive/$pkgver.tar.gz")
sha1sums=('67a017fd43498dc1da507622367bdb5cceef3222')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/httpie/LICENSE"
  python3 setup.py install --root="$pkgdir" --optimize=1

  # Fix upstream, include them in MANIFEST.in and use data_files in setup.py to install them automatically
  # TODO: add zsh support
  install -Dm644 extras/httpie-completion.bash $pkgdir/usr/share/bash-completion/completions/http
  install -Dm644 extras/httpie-completion.fish $pkgdir/usr/share/fish/vendor_completions.d/http.fish
}

check() {
  cd $pkgname-$pkgver
  python3 setup.py test
}
