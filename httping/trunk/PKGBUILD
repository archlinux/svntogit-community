# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: Ondrej Kucera <ondrej.kucera@centrum.cz>

pkgname=httping
pkgver=2.4
pkgrel=4
pkgdesc='A ping-like tool for http-requests'
url='https://www.vanheusden.com/httping/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openssl' 'ncurses' 'fftw')
source=(${pkgname}-${pkgver}.tgz::https://www.vanheusden.com/httping/${pkgname}-${pkgver}.tgz)
sha512sums=('11d9e9e3b222548c9754cc0e7bf947f1a55ccc9f1a2401681f95d21b7b7a56c07665955b558a56d07a5c98497ea3644758e4f85006c42fda2134556be8d9e804')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --with-tfo
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
