# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
pkgver=2.2.3
pkgrel=1

pkgdesc="cli interface for Github"
url="https://hub.github.com"
arch=('x86_64' 'i686')
license=('MIT')

depends=('git')
makedepends=('go')

_source_dir=github-hub-88296d1
source=("$pkgname-$pkgver.tar.gz::https://github.com/github/hub/tarball/v$pkgver")
sha256sums=('db21e3ff93ef1747970bb58a59dba4e84c85047ce6ef99cc640a98be71c3702f')

build() {
  cd $_source_dir

  ./script/build
  gzip --best -c man/hub.1> hub.1.gz

}

package() {
  cd $_source_dir

  install -Dm755 hub "$pkgdir"/usr/bin/hub

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 etc/hub.bash_completion.sh "$pkgdir"/usr/share/bash-completion/completions/hub
  install -Dm644 etc/hub.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_hub
  install -Dm644 man/hub.1 "$pkgdir"/usr/share/man/man1/hub.1
}
