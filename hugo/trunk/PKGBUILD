# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: fordprefect
# Contributor: Jonathon Fernyhough <jonathon_at_manjaro_dot_org>
# Contributor: Brenton Horne

pkgname=hugo
pkgver=0.38
pkgrel=1
pkgdesc="Fast and Flexible Static Site Generator in Go"
arch=('x86_64')
url="https://gohugo.io/"
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
optdepends=('pygmentize: syntax-highlight code snippets')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gohugoio/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('08549563b718cc904e9a71b084f975b4b1fe05ab431e1258cbae23d4b76846ab')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  export GOPATH="${srcdir}"
  export PATH="${PATH}:${srcdir}/bin"
  install -d "${GOPATH}/src/github.com/gohugoio"
  cp -a "$(pwd)" "${GOPATH}/src/github.com/gohugoio/hugo"
}

build() {
  cd "${GOPATH}/src/github.com/gohugoio/hugo"
  go get -u github.com/golang/dep/cmd/dep
  dep ensure
  go get github.com/magefile/mage
  mage hugo
}

package() {
  cd "${GOPATH}/src/github.com/gohugoio/hugo"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
