# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Dmitry N.Shilov <stormblast@land.ru>

pkgname=human-icon-theme
pkgver=0.36
pkgrel=1
pkgdesc="Ubuntu's Human icon theme"
arch=('any')
url="http://launchpad.net/human-icon-theme/"
license=('custom:cc-by-sa-2.5')
depends=('tangerine-icon-theme')
makedepends=('intltool' 'gettext' 'imagemagick' 'icon-naming-utils' 'librsvg'
             'python')
source=(http://archive.ubuntu.com/ubuntu/pool/universe/h/$pkgname/${pkgname}_$pkgver.tar.gz
        archlinux-icon-crystal-48.svg)
md5sums=('f70beba922bbea8441799ac32db1ce7c'
         '566b77b3a0c7d8f9dd59096b2363a009')

build() {
  cd "$srcdir/$pkgname"

  make || return 1
  make DESTDIR="$pkgdir" install

  # Replace Ubuntu's logo with Arch Linux's
  for size in 22 24 48; do
    rsvg -w $size -h $size "$srcdir/archlinux-icon-crystal-48.svg" \
         "$pkgdir/usr/share/icons/Human/${size}x${size}/places/start-here.png"
  done
  install -Dm644 "$srcdir/archlinux-icon-crystal-48.svg" \
          "$pkgdir/usr/share/icons/Human/scalable/places/start-here.svg"

  # Copy over the license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
