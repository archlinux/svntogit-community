# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Dmitry N.Shilov <stormblast@land.ru>

pkgname=human-icon-theme
pkgver=0.36
pkgrel=5
pkgdesc="Ubuntu's Human icon theme"
arch=('any')
url="http://launchpad.net/human-icon-theme/"
license=('custom:cc-by-sa-2.5')
depends=('tangerine-icon-theme' 'gtk-update-icon-cache')
makedepends=('intltool' 'gettext' 'imagemagick' 'icon-naming-utils' 'librsvg'
             'python2')
source=(http://archive.ubuntu.com/ubuntu/pool/universe/h/$pkgname/${pkgname}_$pkgver.tar.gz
        archlinux-icon-crystal-48.svg)
sha256sums=('56d9c9da5fe161634f28bdc45171f3f615034819fbf45f1f7bf4bf9675c59777'
            '6c5307e8a8ae06970c16278db557184dd91366bf6233ca55d2b7de942e66b821')

prepare() {
  cd "$srcdir/$pkgname"

  # Lower minimum size for scalable icons; fixes issue with oversized
  # emblem-imporant icon in Evolution (FS#44241)
  sed -i 's/MinSize=32/MinSize=8/' index.theme.in
}

build() {
  cd "$srcdir/$pkgname"

  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir" install

  # Replace Ubuntu's logo with Arch Linux's
  for size in 22 24 48; do
    rsvg-convert -w $size -h $size "$srcdir/archlinux-icon-crystal-48.svg" \
      -o "$pkgdir/usr/share/icons/Human/${size}x${size}/places/start-here.png"
  done
  install -Dm644 "$srcdir/archlinux-icon-crystal-48.svg" \
    "$pkgdir/usr/share/icons/Human/scalable/places/start-here.svg"

  # Copy over the license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
