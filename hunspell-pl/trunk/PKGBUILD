# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Maciej Sitarz <macieks@freesco.pl>

pkgname=hunspell-pl
pkgver=20120911
pkgrel=2
pkgdesc='Polish dictionary for Hunspell'
arch=('any')
url='http://www.sjp.pl/slownik/ort/'
license=('GPL')
optdepends=('hunspell: the spell checking libraries and apps')
source=(https://sources.archlinux.org/other/community/$pkgname/sjp-myspell-pl-$pkgver.zip)
md5sums=('cc1b05d329be79847b9e30a8f056753d')

prepare() {
  bsdtar -xf pl_PL.zip
}

package() {
  install -dm755 "$pkgdir"/usr/share/hunspell
  install -m644 pl_PL.aff "$pkgdir"/usr/share/hunspell
  install -m644 pl_PL.dic "$pkgdir"/usr/share/hunspell

    # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
  for file in "$pkgdir"/usr/share/hunspell/*; do
    ln -sv /usr/share/hunspell/$(basename $file) .
  done
  popd

  # docs
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README_pl_PL.txt "$pkgdir"/usr/share/doc/$pkgname
}
