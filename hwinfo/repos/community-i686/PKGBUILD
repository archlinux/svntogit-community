# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
# Contributor: champus
# Contributor: madeye
# Contributor: Ranguvar
# Contributor: ninja_pt

pkgname=hwinfo
pkgver=20.1
_subver=3.10
pkgrel=2
pkgdesc='Hardware detection tool from openSUSE'
arch=('x86_64' 'i686')
url='http://download.opensuse.org/source/factory/repo/oss/suse/src/'
license=('GPL2')
depends=('perl' 'libx86emu')
makedepends=('sysfsutils' 'rpmextract' 'flex')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/$pkgname-$pkgver-$_subver.src.rpm"
        'custom_ioctl.patch')
sha256sums=('b0e2e51b4dd754071fb40843deaaafb0755388de5335b1d2c4c081d0f6393841'
            '6e83b50aa34f31db5fe6de938402e7c4dda6ffa9ffe1346c076e4779018fcf1d')

prepare() {
  cd "$srcdir"

  rpmextract.sh "$pkgname-$pkgver-$_subver.src.rpm"
  tar jxf $pkgname-$pkgver.tar.bz2
  cd "$pkgname-$pkgver"
  patch -p0 -i ../custom_ioctl.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 CFLAGS+="-fPIC -I$srcdir/$pkgname-$pkgver/src/hd" LIBDIR=/usr/lib
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
  rmdir "$pkgdir/sbin"
}

# vim:set ts=2 sw=2 et:
