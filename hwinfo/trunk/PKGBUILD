# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
# Contributor: champus
# Contributor: madeye
# Contributor: Ranguvar
# Contributor: ninja_pt
pkgname=hwinfo
pkgver=19.0
pkgrel=2
pkgdesc='Hardware detection tool from openSUSE'
arch=('x86_64' 'i686')
url='http://www.opensuse.org/'
license=('GPL2')
depends=('perl' 'libx86emu')
makedepends=('sysfsutils' 'rpmextract' 'flex')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/${pkgname}-${pkgver}-1.1.src.rpm"
        'custom_ioctl.patch')
md5sums=('751238feaa13449324593ee5e145cd56'
         '1500ecfa26f6aaf4d7967eea74e86e5d')

build() {
  cd "${srcdir}"

  msg2 "Extracting source rpm..."
  rpmextract.sh "${pkgname}-${pkgver}-1.1.src.rpm"
  tar jxf ${pkgname}-${pkgver}.tar.bz2
  cd "${pkgname}-${pkgver}"
  msg2 "Patching..."
  patch -p0 < ../custom_ioctl.patch
  msg2 "Compiling..."
  make -j1 CFLAGS+="-fPIC -I${srcdir}/${pkgname}-${pkgver}/src/hd" LIBDIR=/usr/lib
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg2 "Packaging files..."
  make LIBDIR=/usr/lib DESTDIR=${pkgdir} install
  msg2 "Cleaning up..."
  rmdir "${pkgdir}/sbin"
}

# vim:set ts=2 sw=2 et:
