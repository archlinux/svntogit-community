# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehg@gmail.com>
# Contributor: champus, madeye, ranguvar and ninja_pt

pkgname=hwinfo
pkgver=21.50
pkgrel=1
pkgdesc='Hardware detection tool from openSUSE'
arch=('x86_64')
url='https://github.com/openSUSE/hwinfo'
license=('GPL2')
depends=('libx86emu' 'bash' 'perl')
makedepends=('sysfsutils' 'flex' 'git')
options=('!emptydirs')
source=("https://github.com/openSUSE/$pkgname/archive/$pkgver.tar.gz"
        'custom_ioctl.patch')
sha256sums=('e1008aab25e8da2a4f21d48b0ea9fdba5f1182b95a7f4e8905d4fc16229b0fad'
            '6e83b50aa34f31db5fe6de938402e7c4dda6ffa9ffe1346c076e4779018fcf1d')

prepare() {
  # Patching
  cd "$pkgname-$pkgver"
  patch -p0 -i ../custom_ioctl.patch
  echo 'touch changelog' > git2log
}

build() {
  make -C "$pkgname-$pkgver" -j1 \
    HWINFO_VERSION="$pkgver" \
    CFLAGS+="-fPIC -I$srcdir/$pkgname-$pkgver/src/hd" \
    LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname-$pkgver" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}

# getver: https://github.com/openSUSE/hwinfo/releases
# vim:set ts=2 sw=2 et:
