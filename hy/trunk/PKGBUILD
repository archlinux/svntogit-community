# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: JP Cimalando <jp-dev@inbox.ru>

pkgname=hy
pkgver=0.19.0
pkgrel=3
pkgdesc="A dialect of Lisp that's embedded in Python"
arch=('any')
url="http://hylang.org/"
license=('MIT')
depends=('python-rply' 'python-astor' 'python-clint' 'python-funcparserlib')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hylang/hy/archive/$pkgver.tar.gz")
sha256sums=('3a24f02a22ab8679783b1b817eba5fd1cfa1b9fb334e16fad74fc1887d257efc')

prepare() {
    cd "$pkgname-$pkgver"

    # usually generated from git metadata and aded to PyPI sdist
    # PyPI does not include testsuite files.
    echo "__version__ = '$pkgver'" > hy/version.py
}

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

check(){
    cd "$pkgname-$pkgver"

    python setup.py develop --user
    PATH="$HOME/.local/bin:$PATH"
    python setup.py pytest #--addopts "-k 'not test_bin'"

    # Hy does magic to the bytecode, but we need this gone from check.
    # this should be generated as part of the build step
    for file in hy/contrib/__pycache__/__init__.cpython-*.pyc \
	hy/contrib/__pycache__/hy_repr.cpython-*.pyc \
	hy/contrib/__pycache__/loop.cpython-*.pyc \
	hy/contrib/__pycache__/sequences.cpython-*.pyc \
	hy/contrib/__pycache__/walk.cpython-*.pyc \
	hy/core/__pycache__/__init__.cpython-*.pyc \
	hy/core/__pycache__/bootstrap.cpython-*.pyc \
	hy/core/__pycache__/language.cpython-*.pyc \
	hy/core/__pycache__/macros.cpython-*.pyc \
	hy/core/__pycache__/shadow.cpython-*.pyc \
	hy/extra/__pycache__/__init__.cpython-*.pyc \
	hy/extra/__pycache__/anaphoric.cpython-*.pyc \
	hy/extra/__pycache__/reserved.cpython-*.pyc
    do
        rm "$file"
    done

}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    rm -rf "$pkgdir/usr/get_version"
}
