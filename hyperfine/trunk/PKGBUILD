# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: cauebs <cauebs@pm.me>
pkgname="hyperfine"
pkgver=1.7.0
pkgrel=1
pkgdesc="A command-line benchmarking tool"
url="https://github.com/sharkdp/hyperfine"
arch=("x86_64")
license=("APACHE" "MIT")
makedepends=(cargo)
source=("https://github.com/sharkdp/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('d936aab473775e76c3a749828054e3f7d42e3909e8b0f56f99ecf6aa169a9bd3')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    cargo build --release --locked
}
package() {
    cd "$srcdir/$pkgname-$pkgver"
    # Apache doesn't need to be installed, but because of the dual license
    # it makes more sense to not exclude it.
    install -Dm 644 "LICENSE-APACHE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm 644 "LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
    install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
}
