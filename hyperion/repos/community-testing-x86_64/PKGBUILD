# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Christopher Reimer <mail+aur[at]c-reimer[dot]de>

pkgname=hyperion
pkgver=1.03.2
pkgrel=16
pkgdesc="An opensource 'AmbiLight' implementation"
arch=('x86_64')
url="https://github.com/hyperion-project/hyperion"
license=('MIT')
depends=('libusb' 'protobuf' 'python' 'qt5-base')
optdepends=('xorg-server: X11 grabbing')
makedepends=('cmake')
backup=('etc/hyperion/hyperion.config.json')
install='hyperion.install'
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tvdzwan/${pkgname}/archive/${pkgver}.tar.gz"
        "0001-Fix-no-decleration-of-assert-with-GCC-7.2.patch"
        "hyperion.sysusers"
        "hyperiond.service"
        "hyperion.install")
sha512sums=('7406f5bdf323d2799fb375557603fefd1f077cda287b5aa9ff10251b22d8dd07590458515b0e01ef97fba80885aab1aa72bd8b5d26873ad8ebcc1ba53d6776ec'
            '11c51f6085d78bb75a7d73372262eedc2dab84b9d37ee1850f6b8d8c9cfe85d72a69b7d755e169028c6f7150f09201ded7d32a4edf15130ca2a12a188fe5945c'
            '80764d3abdfb7e6e7f65913e9c89dd61b425ffb80e9ab4f9df7739abe32117441095ac7b45792acd5d2a516cf4aca3e21062b119f5790aae3b178036db74ce61'
            'f8c23ba920ce683e731237c164f4f70f94d635398be5cc43ce1af48bb8ab672da47397979a825a5761dacb1ae18a1c5fc840b168c8f1e3ae411908e0e92a0c5b'
            'ff5ed8ed647a621b73e922590616e4f5d63f0a7e243aa3de0f8382a835e83166a0e04944396a61a03645cc00853251690a514b3772f163f445240e4f55f18fb5')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/0001-Fix-no-decleration-of-assert-with-GCC-7.2.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    mkdir -p build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX="${pkgdir}/usr" \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_QT5=ON \
        -DPLATFORM=x86 \
        -DPROTOBUF_PROTOC_EXECUTABLE=/usr/bin/protoc \
        -DUSE_SYSTEM_PROTO_LIBS=ON \
        ..
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cd build
    make install
    cd ..

    install -Dm 644 config/hyperion.config.json.example \
        "${pkgdir}/etc/hyperion/hyperion.config.json"

    install -Dm 644 "${srcdir}/hyperiond.service" \
        "${pkgdir}/usr/lib/systemd/system/hyperiond.service"

    rm -rf "${pkgdir}/usr/share/hyperion/service"

    install -Dm 644 "${srcdir}/hyperion.sysusers" \
        "${pkgdir}"/usr/lib/sysusers.d/hyperion.conf

    install -Dm 644 LICENSE \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
