# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alireza Ayinmehr <alireza.darksun@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: JSkier <jskier at gmail dot com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=hyperscan
pkgver=5.4.0
pkgrel=2
pkgdesc='High-performance regular expression matching library'
arch=(x86_64)
url=https://www.hyperscan.io
license=(BSD)
makedepends=(
  boost
  cmake
  git
  ninja
  python
  ragel
)
_tag=64a995bf445d86b74eb0f375624ffc85682eadfe
source=(
  git+https://github.com/intel/hyperscan.git#tag=${_tag}
  https://github.com/Mic92/hyperscan/commit/e2c4010b1fc1272cab816ba543940b3586e68a0c.patch
)
b2sums=('SKIP'
        '9c703ab4fec035765c3c513589b79dfb98aa9ec79360d0cbda202a8b1957ad81cb0b1107a3cbbb793adb209f3e171d4831cb138c86817ee5533d262a06dd14a4')

prepare() {
  cd hyperscan
  patch -Np1 -i ../e2c4010b1fc1272cab816ba543940b3586e68a0c.patch
}

pkgver() {
  cd hyperscan
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S hyperscan -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 hyperscan/COPYING -t "${pkgdir}"/usr/share/licenses/hyperscan/
}

# vim: ts=2 sw=2 et:
