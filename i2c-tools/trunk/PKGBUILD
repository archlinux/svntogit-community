# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=i2c-tools
pkgver=3.1.1
pkgrel=2
pkgdesc="Heterogeneous set of I2C tools for Linux that used to be part of lm-sensors"
arch=('i686' 'x86_64')
url="http://www.lm-sensors.org/wiki/I2CTools"
license=('GPL')
depends=('perl')
makedepends=('python2')
optdepends=('read-edid: for decode-edid script'
            'python2: for smbus module')
# Upstream website died...
# XXX: should become https://sources.archlinux.org/other/community/i2c-tools/
source=(http://pkgbuild.com/~jelle/sources/i2c-tools-3.1.1.tar.bz2)
md5sums=('0fdbff53ebd0b8d9249256d6c56480b1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
  make -C eepromer CFLAGS="$CFLAGS -I../include"
  cd py-smbus
  CPPFLAGS="$CPPFLAGS -I../include" python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" sbindir="$pkgdir/usr/bin" install
  install -Dm755 eepromer/eeprog eepromer/eeprom eepromer/eepromer "${pkgdir}/usr/bin"
  cd py-smbus
  python2 setup.py install --prefix "${pkgdir}/usr"
  rm -rf "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
