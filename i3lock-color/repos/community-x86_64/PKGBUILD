# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: ibrokemypie <ibrokemypie@bastardi.net>
pkgname=i3lock-color
pkgver=2.12.c
pkgrel=1
_commit=60d68356db80549cb6e40a093b2f884b500aac44 # 2.12.c
pkgdesc="improved screenlocker with color configuration support"
arch=('x86_64')
url=https://github.com/PandorasFox/i3lock-color
license=('MIT')
depends=('xcb-util-image' 'pam' 'libev' 'cairo' 'libxkbcommon-x11'
         'libjpeg-turbo' 'xcb-util-xrm' 'pkgconf')
makedepends=('git')
provides=('i3lock')
conflicts=('i3lock')
source=("$pkgname-$pkgver::git+https://github.com/PandorasFox/$pkgname#commit=$_commit"
        '0001-Also-pass-screen-brightness-keys-as-media-keys.patch')
sha256sums=('SKIP'
            'f09ebac78d0f021449734fe38afbc9e389a350255ba88cd5a89d86988fe639db')

prepare() {
    cd "$pkgname-$pkgver"
    patch -p1 < "$srcdir"/0001-Also-pass-screen-brightness-keys-as-media-keys.patch

	# Fix ticket FS#31544, sed line taken from gentoo
	sed -i -e 's:login:system-auth:' pam/i3lock 
    autoreconf -fiv	
}

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
