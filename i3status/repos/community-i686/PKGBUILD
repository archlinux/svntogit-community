# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=i3status
pkgver=2.9
pkgrel=1
pkgdesc='Generates status bar to use with i3bar, dzen2 or xmobar'
arch=('i686' 'x86_64')
url='http://i3.zekjur.net/i3status/'
license=('BSD')
groups=('i3')
depends=('wireless_tools' 'confuse' 'alsa-lib' 'yajl')
makedepends=('asciidoc')
backup=('etc/i3status.conf')
install=i3status.install
options=('docs')
source=("http://i3.zekjur.net/i3status/$pkgname-$pkgver.tar.bz2"
        "http://i3.zekjur.net/i3status/$pkgname-$pkgver.tar.bz2.asc")
md5sums=('f02a61ade525fd9e9b092f778632a8b0'
         'SKIP')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  make clean
}

# vim:set ts=2 sw=2 et:

