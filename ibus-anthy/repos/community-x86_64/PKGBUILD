# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.5.12
pkgrel=1
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('x86_64')
license=('LGPL')
url='https://github.com/ibus/ibus-anthy'
depends=('ibus' 'anthy' 'python' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('intltool' 'gobject-introspection')
source=("https://github.com/ibus/ibus-anthy/releases/download/$pkgver/ibus-anthy-$pkgver.tar.gz")
sha512sums=('6950a2865e4753831f8918f4a5926571bc8304454b6368662ef56a6fb11a60f0675e3d589108ebfd38cb5d193f7b61053a10baa6dad23adad67b3b3fc58760e8')

prepare() {
  cd $pkgname-$pkgver

  # Remove bad config file found in the source tarball so it gets regenerated
  # https://bugs.archlinux.org/task/64520
  rm {engine,setup}/python3/_config.py
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
