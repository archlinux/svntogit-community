# $Id: PKGBUILD 57030 2011-10-18 21:29:59Z ebelanger $
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.3.10
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('i686' 'x86_64')
license=('GPL')
url='http://ibus.googlecode.com'
depends=('ibus' 'libchewing' 'libxtst')
makedepends=('swig' 'cmake' 'gob2')
install=ibus-chewing.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}-Source.tar.gz
        https://fedorahosted.org/releases/c/m/cmake-fedora/cmake-fedora-modules-only-latest.tar.gz)
md5sums=('2a447828ca9290dd6bdf2336b2889bb0'
         'eb24ff0ec5b7646dd1b077f3b379712b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"
  tar -xzf ../cmake-fedora-modules-only-latest.tar.gz
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/usr/share \
    -DLIBEXEC_DIR=/usr/lib/ibus \
    .
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"
  make DESTDIR="${pkgdir}" install
}
