# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.4.7
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('i686' 'x86_64')
license=('GPL')
url='http://ibus.googlecode.com'
depends=('ibus' 'libchewing' 'libxtst' 'gconf')
makedepends=('swig' 'cmake' 'gob2')
install=ibus-chewing.install
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}-Source.tar.gz")

build() {
  cd ${pkgname}-${pkgver}-Source
  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/usr/share \
    -DLIBEXEC_DIR=/usr/lib/ibus
  make
  make translations
}

package() {
  cd ${pkgname}-${pkgver}-Source
  make DESTDIR="${pkgdir}" install

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  mv "$pkgdir/etc/gconf/schemas/${pkgname}.schemas" "$pkgdir/usr/share/gconf/schemas/"
  rm -r "$pkgdir/etc"
}

sha512sums=('70e9c4950ac846c2f0087200d3b42f845ef8f911962d3f6bf37a3a0054606fa0fc061daa5dac390d022dbd29801fbcae596d2c5126270eb6e9843a9e0af4486e')
