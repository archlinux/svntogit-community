# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Author: Jiahua Huang <jhuangjiahua@gmail.com>
# Contributor: Yangtse <yangtsesu@gmail.com>

pkgname=ibus-googlepinyin
pkgver=0.1.2
pkgrel=6
pkgdesc="IBus Wrapper for libgooglepinyin"
arch=('any')
url="https://code.google.com/archive/p/libgooglepinyin/"
license=('APACHE')
optdepends=('opencc: for simplified chinese translation to tranditional chinese')
depends=('libgooglepinyin' 'ibus' 'iso-codes' 'pygtk')
makedepends=('cmake')
source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/libgooglepinyin/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f9af459849c29934e8d59fc7f23a9318')

prepare() {
  sed -e 's/zhs2zht.ini/s2t.json/' -i $pkgname/libopencc.py
  find $pkgname -type f -name '*.py' -exec sed -i 's|/usr/bin/python|/usr/bin/python2|g' {} \;

  rm -rf build
  mkdir -p build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$pkgname
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
