# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Radim Hvizd√°k <hvizdakr at gmail dot com>

pkgname=ibus-hangul
pkgver=1.4.1
pkgrel=2
pkgdesc='Korean input engine for IBus'
arch=('i686' 'x86_64')
url='http://ibus.googlecode.com'
license=('GPL')
depends=('ibus' 'libhangul')
makedepends=('intltool')
install=ibus-hangul.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
sha1sums=('659899cf942a983fbd2ffbb4bd4f5ecd883f5c0a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/python/python2/' setup/ibus-setup-hangul.in
  PYTHON=python2 ./configure --prefix=/usr --libexecdir=/usr/lib/ibus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make NO_INDEX=true DESTDIR="${pkgdir}" install
}
