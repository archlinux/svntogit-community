# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Radim Hvizd√°k <hvizdakr at gmail dot com>

pkgname=ibus-hangul
pkgver=1.4.2
pkgrel=1
pkgdesc='Korean input engine for IBus'
arch=('i686' 'x86_64')
url='http://ibus.googlecode.com'
license=('GPL')
depends=('ibus' 'libhangul' 'iso-codes' 'pygtk' 'gtk-update-icon-cache')
makedepends=('intltool')
install=ibus-hangul.install
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/python/python2/' setup/ibus-setup-hangul.in
  PYTHON=python2 ./configure --prefix=/usr --libexecdir=/usr/lib/ibus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make NO_INDEX=true DESTDIR="${pkgdir}" install
}

md5sums=('0229eab9481355bb5f54230890bfa910')
