# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor:  Yangtse Su<yangtsesu@gmail.com>
# Contributor:  Jekyll Wu<adaptee [at] gmail [dot] com>
# Contributor: riverscn<riverscn at gmail.com>
# Contributor: rainy<rainylau at gmail.com>
# Contributor: Lee.MaRS<leemars at gmail.com>

pkgname=ibus-libpinyin
pkgver=1.12.1
pkgrel=2
pkgdesc="Intelligent Pinyin engine based on libpinyin for IBus"
arch=('x86_64')
license=('LGPL')
url="https://github.com/libpinyin/ibus-libpinyin"
depends=('ibus' 'libpinyin' 'lua' 'opencc' 'python-pyxdg')
makedepends=('boost' 'git' 'gnome-common' 'intltool')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libpinyin/ibus-libpinyin/archive/$pkgver.tar.gz")
sha512sums=('ad6e7507bdb468d506071135449383e5bd4fc878bcffda4be6e4e7915b79dbaf919d2fa941b92b177dc7790bd895eaa9a6d04a2610f85e1e80e8eb50f188211d')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/$pkgname --enable-boost --enable-opencc --enable-cloud-input-mode
  make
}

package() {
  cd $pkgname-$pkgver
  make NO_INDEX=true DESTDIR="$pkgdir" install
}
