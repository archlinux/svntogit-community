# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-sunpinyin
_pkgname=sunpinyin
pkgver=3.0.0rc1
_tag=v3.0.0-rc1
pkgrel=3
pkgdesc="IBus Wrapper for sunpinyin"
arch=('x86_64')
url="http://sunpinyin.googlecode.com"
license=('LGPL')
depends=('sunpinyin' 'sunpinyin-data' 'ibus' 'pygtk')
makedepends=('git' 'scons')
source=("git://github.com/sunpinyin/sunpinyin.git#tag=$_tag")
md5sums=("SKIP")

build() {
  cd "$srcdir/$_pkgname/wrapper/ibus"
  sed -i -e "s|python|python2|" setup/ibus-setup-sunpinyin.in
  
  scons \
    --prefix=/usr
}

package() {
  cd "$srcdir/$_pkgname/wrapper/ibus"

  scons \
    --prefix=/usr \
    --install-sandbox="$pkgdir" \
    install
}
