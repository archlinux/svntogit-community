# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.12.1
pkgrel=2
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL')
makedepends=('docbook2x' 'docbook-sgml')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaio/ibus-table/archive/$pkgver.tar.gz"
        sgml_fix.patch)
sha512sums=('d6b7e0ed1226b9515e6b0883b14f55ecd7cd6c185f9ada443b9ca4ce029d371891510788592c4701d199b41981203701177ea9a0e3a9dbe122dbc752d3a1e51a'
            '930cd45e9687295159c44d5d4133e99a2259fb8f2ed51675b55b18ca59acb410144689a2d51b052b7bbcbe061f8135ebd511898658d1c71e7004303df969c880')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../sgml_fix.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
