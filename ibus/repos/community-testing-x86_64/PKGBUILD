# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus
pkgver=1.3.9
pkgrel=3
pkgdesc='Next Generation Input Bus for Linux.'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('dbus-python>=0.83.0' 'gconf' 'python2' 'pygtk' 'pyxdg' 'iso-codes' 'librsvg' 'python-notify' 'hicolor-icon-theme' 'gtk3')
optdepends=('notification-daemon')
makedepends=('intltool')
options=('!libtool')
install=ibus.install
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('57345a3c1cfe37ce3825f6221676dcfe')

build() {
  cd ${pkgname}-${pkgver}

  export PYTHON=python2

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
    --enable-gtk3
    #--enable-introspection=no # disabled because build fails with 
                              # gobject-introspection 0.9.10-2

  # python2 fix
  for file in setup/ibus-setup.in ui/gtk/ibus-ui-gtk.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  make
}

package() {
  cd ${pkgname}-${pkgver}

  export PYTHON=python2

  make DESTDIR=${pkgdir} install

  find ${pkgdir} -type f -exec sed -i 's_exec /usr/bin/python_exec /usr/bin/python2_' {} \;
}
