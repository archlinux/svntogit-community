# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Claudio Sabattoli <gasherbrum3@alice.it>

pkgname=idesk
pkgver=0.7.5
pkgrel=4
pkgdesc="iDesk gives users of minimal wm's (fluxbox, blackbox, openbox, windowsmaker...) icons on their desktop"
arch=("i686" "x86_64")
url="http://idesk.sourceforge.net/wiki/index.php"
license=('GPL')
depends=('pkgconfig' 'imlib2' 'libpng' 'libxpm' 'libxft' 'gcc-libs')
source=("http://downloads.sourceforge.net/sourceforge/idesk/$pkgname-$pkgver.tar.bz2")
md5sums=('beb48c97815c7b085e3b3d601297fbb8')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  sed -i \
    -e '1,1i#include <unistd.h>' \
    -e '1,1i#include <sys/stat.h>' \
    -e '1,1i#include <sys/types.h>' \
    src/DesktopConfig.cpp
  sed -i 's#usr/local#usr#' examples/default.lnk
  sed -i 's#IMLIB2_LIBS=.*#IMLIB2_LIBS=-lImlib2#g' configure
  sed -i 's#IMLIB_LIBS=.*#IMLIB_LIBS="-L/usr/lib -lImlib -Wl,-O1,--sort-common,--as-needed,-z,relro -ljpeg -ltiff -lgif -lpng -lz -lm -lXext -lXext -lX11 -lImlib2"#g' configure
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
