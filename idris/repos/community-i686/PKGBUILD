# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=idris
pkgver=0.11
pkgrel=7
pkgdesc="Functional Programming Language with Dependent Types"
url="http://www.idris-lang.org/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib' 'gcc')
makedepends=("ghc=7.10.3" "haskell-annotated-wl-pprint" "haskell-ansi-terminal" "haskell-ansi-wl-pprint"
             "haskell-async" "haskell-base64-bytestring" "haskell-blaze-html" "haskell-blaze-markup"
             "haskell-cheapskate" "haskell-fingertree" "haskell-fsnotify" "haskell-mtl" "haskell-libffi"
             "haskell-network" "haskell-optparse-applicative" "haskell-parsers" "haskell-safe"
             "haskell-split" "haskell-terminal-size" "haskell-text" "haskell-transformers-compat"
             "haskell-trifecta" "haskell-uniplate" "haskell-unordered-containers" "haskell-utf8-string"
             "haskell-vector" "haskell-vector-binary-instances" "haskell-zip-archive")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e0ea4df41f63f9d4292fe2ae9d3031cbc511a96c80e43240df16374335261a2a')

prepare() {
    sed -i 's/zip-archive > 0.2.3.5 && < 0.2.4/zip-archive > 0.2.3.5 \&\& < 0.4/' ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fFFI -fGMP
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    LC_CTYPE=en_US.UTF-8 runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
}
