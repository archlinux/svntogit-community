# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iec16022
pkgver=0.2.5
pkgrel=1
pkgdesc="Produce 2D barcodes often also referenced as DataMatrix"
arch=('i686' 'x86_64')
url="http://datenfreihafen.org/projects/iec16022.html"
license=('GPL')
depends=('popt' 'zlib')
source=($pkgname-$pkgver.tar.gz::https://github.com/rdoeffinger/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('8cda3b99310934db138383fafe23274d2d93372aaed9acd1f7fc5210eed45be0')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
