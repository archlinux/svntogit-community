# $Id$
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Thomas Dziedzic  < gostrc at gmail >
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=ifuse
pkgver=1.1.2
pkgrel=5
pkgdesc='A fuse filesystem to access the contents of an iPhone or iPod Touch'
url='http://libimobiledevice.org/'
arch=('i686' 'x86_64')
license=('LGPL2.1')
depends=('libimobiledevice' 'fuse')
source=("http://www.libimobiledevice.org/downloads/$pkgname-$pkgver.tar.bz2"
        '0001-Updated-for-libimobiledevice-1.1.5.patch')
md5sums=('4152526b2ac3c505cb41797d997be14d'
	 'ca4d4af7f830c10645b605f1572c0ba6')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir/0001-Updated-for-libimobiledevice-1.1.5.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
