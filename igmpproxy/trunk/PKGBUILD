# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: sda <sda00@himki.net>

pkgname=igmpproxy
pkgver=0.1.20161003next
pkgrel=2
_commit=17de79cc3ec459f5660616573d6edd0b2288b664
pkgdesc="a simple multicast router for Linux only using the IGMP protocol"
arch=(i686 x86_64)
url="https://github.com/pali/igmpproxy"
license=('GPL')
depends=()
makedepends=('git')
backup=("etc/igmpproxy.conf")
source=("git+https://github.com/pali/igmpproxy.git#commit=${_commit}"
	"igmpproxy.service")
sha256sums=('SKIP'
            '9f2eed64578b8bfb3091594cb32b3cd29798ba148d790286429cc6c86b8d423f')

prepare() {
  cd "$srcdir"/$pkgname
  ./bootstrap
}

build() {
  cd "$srcdir"/$pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir"/$pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
