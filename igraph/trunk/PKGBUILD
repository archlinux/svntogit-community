# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.9.4
pkgrel=1
pkgdesc='A library for creating and manipulating (un)directed graphs'
arch=(x86_64)
url='https://igraph.org/c/'
license=(GPL2)
depends=(glpk arpack libxml2 suitesparse)
makedepends=(cmake)
source=(https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a3285cccf4f043c9ced2bc8d8d2609ff8398cb92ed49fdf86264ed91929137dd')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
