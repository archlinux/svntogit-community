# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.9.0
pkgrel=2
pkgdesc="A library for creating and manipulating (un)directed graphs."
arch=(x86_64)
url="https://igraph.org/c/"
license=(GPL2)
depends=(glpk arpack libxml2 suitesparse)
makedepends=(cmake)
source=("https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"
         igraph-fix-symbol.patch)
sha256sums=('012e5d5a50420420588c33ec114c6b3000ccde544db3f25c282c1931c462ad7a'
            '977d9b996746b82d347567b7d069f241833dd95f8b80aec39674cc0e1164eb40')

prepare() {
  patch -d $pkgname-$pkgver -p1 < igraph-fix-symbol.patch # Fix missing igraph_subcomponent symbol https://github.com/igraph/igraph/pull/1705
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
