# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Eric Lehmann <katyl@katyl.info>
# Contributor: Thomas Fanninger <thomas@fanninger.at>
# Contributor: ultraviolet <ultravioletnanokitty@gmail.com>
# Contributor: Pablo Lezeta <prflr88@gmail.com>

pkgname=iio-sensor-proxy
pkgver=3.3
pkgrel=1
pkgdesc='IIO accelerometer sensor to input device proxy'
arch=('x86_64')
url='https://gitlab.freedesktop.org/hadess/iio-sensor-proxy/'
license=('GPL2')
depends=('systemd' 'libgudev' 'gtk3')
makedepends=('meson')
checkdepends=('python-gobject' 'python-dbusmock' 'python-psutil' 'umockdev')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
         fix-tests.patch)
sha512sums=('f5c5e60e888be54ace4f0f2bb941fccce21b122c0755050f91aebe8dfa9e2c126e6de57dd80873ea7148fc2dfc087b23823f29d73a223c68d531502d06438ee3'
            'b53673ad43b10eddfe484d894fa2e0e36e8ea45b34e1fc3dc8f85c92fa7c075fcbf28552f31d8976a32c8ce07b9f5ae9746dce7daffabc4130231f3719a3663d')

prepare() {
  cd $pkgname-$pkgver
# Make tests work with C locale
  patch -p1 -i ../fix-tests.patch
}

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson .. \
    -Dsystemdsystemunitdir=/usr/lib/systemd/system \
    -Dudevrulesdir=/usr/lib/udev/rules.d \
    -Dsysconfdir=/usr/share

  ninja
}

check() {
  cd $pkgname-$pkgver/build

  ninja test
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}

