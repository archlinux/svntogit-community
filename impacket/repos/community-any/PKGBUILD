# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>

pkgname=impacket
pkgver=0.9.15
pkgrel=1
pkgdesc='Collection of classes for working with network protocols'
url='https://github.com/CoreSecurity/impacket'
arch=('any')
license=('Apache')
depends=('python2-crypto' 'python2-pyasn1' 'python2-pcapy' 'python2-pyopenssl')
checkdepends=('python2-pytest')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/CoreSecurity/impacket/archive/impacket_${pkgver//./_}.tar.gz)
sha512sums=('31b1b059d01071319bccfc9a57b23eed175bfba61961ec425553916e7d1940ff6ef53d79ef5e43963e1b4f6037f50489ad22cd6935a53f2cd82bc5b2c6d741ce')

prepare() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  find . -type f -exec sed -r 's|(#!/usr/bin/(env )?python)$|\12|' -i '{}' \;
}

check() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  PYTHONPATH="./impacket:${PYTHONPATH}" \
    PYTHONDONTWRITEBYTECODE=1 \
    py.test2 impacket/testcases/ImpactPacket
}

package() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  ln -s "/usr/lib/python${PYTHONVERSION}/site-packages/impacket/examples" "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
