# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>

pkgname=impacket
pkgver=0.9.14
pkgrel=3
pkgdesc='Collection of classes for working with network protocols'
url='https://github.com/CoreSecurity/impacket'
arch=('any')
license=('Apache')
depends=('python2-crypto' 'python2-pyasn1' 'python2-pcapy' 'python2-pyopenssl')
checkdepends=('python2-pytest')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/CoreSecurity/impacket/archive/impacket_${pkgver//./_}.tar.gz)
sha512sums=('8f2ac11ce2c76e11190c06868f80ceef588bbbfbb30f6574d093f371eb5d65af70ad04ee8fc2e5300c9b14c84d2f5ce6c46dc85ffe8e5003f8f8b9081c700854')

prepare() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  find . -type f -exec sed -r 's|(#!/usr/bin/(env )?python)$|\12|' -i '{}' \;
}

check() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  PYTHONPATH="./impacket:${PYTHONPATH}" \
    PYTHONDONTWRITEBYTECODE=1 \
    py.test2 impacket/testcases/ImpactPacket
}

package() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
