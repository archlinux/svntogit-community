# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Paolo Giangrandi <peoro.noob@gmail.com>

pkgname=impacket
pkgver=0.9.17
pkgrel=1
pkgdesc='Collection of classes for working with network protocols'
url='https://github.com/CoreSecurity/impacket'
arch=('any')
license=('Apache')
depends=('python2-crypto' 'python2-pyasn1' 'python2-pcapy' 'python2-pyopenssl' 'python2-six' 'python2-ldap3' 'python2-flask')
checkdepends=('python2-pytest' 'python2-pytest-runner')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/CoreSecurity/impacket/archive/impacket_${pkgver//./_}.tar.gz)
sha512sums=('c6a84b5f3e0e08bc44c6648c0c65048c5b9ee63ce7706896f4f9a3c061b3bf64e5e68ca87794162cfc730144eff60c392eea2548edeaba61e04350c857e69c9b')

prepare() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  find . -type f -exec sed -r 's|(#!/usr/bin/(env )?python)$|\12|' -i '{}' \;
}

package() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python2 setup.py build
}

check() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python2 setup.py test
  PYTHONPATH="./impacket:${PYTHONPATH}" \
    PYTHONDONTWRITEBYTECODE=1 \
    py.test2 tests/ImpactPacket
}

package() {
  cd ${pkgname}-${pkgname}_${pkgver//./_}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  ln -s "/usr/lib/python${PYTHONVERSION}/site-packages/impacket/examples" "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
