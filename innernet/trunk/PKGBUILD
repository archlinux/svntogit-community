# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Jake McGinty <jake@tonari.no>

pkgname=innernet
pkgver=1.5.2
pkgrel=1
pkgdesc="A private network system that uses WireGuard under the hood"
arch=('x86_64')
url="https://github.com/tonarino/innernet"
license=('MIT')
depends=('sqlite')
makedepends=('rust' 'git' 'clang' 'llvm')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('e5ebec0b17a74d3bb241faeeef7ea2069aa61db169e3de07394b1f62d4b1b830')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm 755 "target/release/$pkgname-server" "$pkgdir/usr/bin/$pkgname-server"

  # will be removed soon
  ln -s "/usr/bin/$pkgname" "$pkgdir/usr/bin/inn"

  install -Dm 644 "client/$pkgname@.service" "$pkgdir/usr/lib/systemd/system/$pkgname@.service"
  install -Dm 644 "client/$pkgname.target" "$pkgdir/usr/lib/systemd/system/$pkgname.target"
  
  install -Dm 644 "server/$pkgname-server@.service" "$pkgdir/usr/lib/systemd/system/$pkgname-server@.service"

  for binary in "$pkgname" "$pkgname-server"; do
    install -Dm 644 "doc/$binary.completions.bash" "$pkgdir/usr/share/bash-completion/completions/$binary"
    install -Dm 644 "doc/$binary.completions.fish" "$pkgdir/usr/share/fish/vendor_completions.d/$binary.fish"
    install -Dm 644 "doc/$binary.completions.zsh" "$pkgdir/usr/share/zsh/site-functions/_$binary"
    install -Dm 644 "doc/$binary.8.gz" "$pkgdir/usr/share/man/man8/$binary.8.gz"
  done

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}

# vim:set ts=2 sw=2 et:
