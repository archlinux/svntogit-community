# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Nassim Kacha <nassim.kacha@gmail.com>

pkgname=innotop
pkgver=1.11.4
pkgrel=3
pkgdesc='A powerful "top" clone for MySQL (MariaDB)'
arch=('any')
url="https://github.com/innotop/innotop"
license=('GPL2')
depends=('perl-dbd-mysql' 'perl-term-readkey')
options=('!emptydirs')
source=($pkgname-$pkgver.tar.gz::https://github.com/innotop/innotop/archive/v$pkgver.tar.gz)
md5sums=('10f241de71e06923beeee689bf263345')

build() {
  cd $pkgname-$pkgver

  perl Makefile.PL
}

package() {
  cd $pkgname-$pkgver

  make INSTALLSITESCRIPT=/usr/bin DESTDIR="$pkgdir"/ install 
}
