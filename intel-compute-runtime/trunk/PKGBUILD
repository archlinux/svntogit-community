# Maintainer: Daniel Bermond < gmail-com: danielbermond >

_srcname=compute-runtime
pkgname=intel-${_srcname}
pkgver=19.11.12599
pkgrel=2
pkgdesc="Intel(R) Graphics Compute Runtime for OpenCL(TM). Replaces Beignet for Gen8 (Broadwell) and beyond."
arch=(x86_64)
url="https://01.org/compute-runtime"
license=(MIT)
depends=(gcc-libs intel-gmmlib intel-graphics-compiler)
makedepends=(cmake libva)
optdepends=('libva: for cl_intel_va_api_media_sharing'
            'libdrm: for cl_intel_va_api_media_sharing')
provides=(opencl-driver)
source=("https://github.com/intel/compute-runtime/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        intel-compute-runtime-fix-llvm8-build.patch::"https://github.com/intel/compute-runtime/commit/7a7bb50328106f9f86b28bc404355fc891e104c6.patch")
sha256sums=('9601e776bc8ea51112ed2c713f8f12bb1de7a0dcc5f90d8b80f9a2214b851761'
            'a41392d2653ff5e3f75e6a211a0ed5c22f2ddbd2246f3a8217b6d3cd35098bb5')

prepare() {
    mkdir -p build
    cd ${_srcname}-${pkgver}
    patch -p1 -i ../intel-compute-runtime-fix-llvm8-build.patch
}

build() {
    cd build
    cmake ../${_srcname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DNEO_DRIVER_VERSION=${pkgver}
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    install -Dm644 ../${_srcname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
