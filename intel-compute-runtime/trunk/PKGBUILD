# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-compute-runtime
pkgver=20.02.15268
pkgrel=1
pkgdesc="Intel(R) Graphics Compute Runtime for OpenCL(TM). Replaces Beignet for Gen8 (Broadwell) and beyond."
arch=(x86_64)
url="https://01.org/compute-runtime"
license=(MIT)
depends=(gcc-libs intel-gmmlib intel-graphics-compiler)
makedepends=(cmake libva)
optdepends=('libva: for cl_intel_va_api_media_sharing'
            'libdrm: for cl_intel_va_api_media_sharing')
provides=(opencl-driver)
source=(https://github.com/intel/compute-runtime/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('e5b2893b7e129ad005b3b9b4ec514343c1f44beca670d5a5d65d0dc6e01cf04d')

build() {
    cmake -B build -S compute-runtime-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DNEO_DRIVER_VERSION=${pkgver}
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 compute-runtime-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
