# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.4427
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses zlib)
makedepends=(git cmake clang llvm python)
options=(!emptydirs)
source=(${url}/archive/igc-${pkgver}.tar.gz
        git+https://github.com/intel/vc-intrinsics.git#commit=d7c5f99bd910a681b907815ebd44ef319ff417c4
        git+https://github.com/KhronosGroup/SPIRV-LLVM-Translator.git#commit=e87b59a77abb30d3b5fb0b3e0555a39acbe5ebb4)
sha256sums=('911d3fa853122ef3f603797e9da289d08c09541fd6fbf4d476c1d14d5632b048'
            'SKIP'
            'SKIP')

build() {
    cmake -B build -S ${pkgname}-igc-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_PREFERRED_LLVM_VERSION='10.0.1' \
        -DVC_INTRINSICS_SRC="${srcdir}/vc-intrinsics" \
        -DSPIRV_SRC="${srcdir}/SPIRV-LLVM-Translator" \
        -DINSTALL_GENX_IR=ON \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
    mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
