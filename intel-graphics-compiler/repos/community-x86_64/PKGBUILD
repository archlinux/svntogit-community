# Maintainer: Daniel Bermond < gmail-com: danielbermond >

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.1
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang)
makedepends=(cmake clang llvm zlib python2)
options=('!emptydirs')
source=("${url}/archive/igc-${pkgver}.tar.gz")
noextract=("igc-${pkgver}.tar.gz")
sha256sums=('d32a44da260f934c821b3285fd527c1ed1233e790913699e9e2234ba2a211d00')

prepare() {
    mkdir -p build ${pkgname}-${pkgver}
    bsdtar xf igc-${pkgver}.tar.gz -C ${pkgname}-${pkgver} --strip-components='1'
}

build() {
    cd build
    cmake ../"${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_OPTION__OUTPUT_DIR='../igc-install/Release' \
        -DIGC_PREFERRED_LLVM_VERSION='8.0.0' \
        -Wno-dev
    make
    
    # License
    sed -n '2,20p' IGC/AdaptorOCL/igc.opencl.h > LICENSE # create file
    sed -i '1,22s/^.\{,3\}//' LICENSE # erase C comments
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
