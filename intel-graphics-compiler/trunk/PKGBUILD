# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.7423
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses zlib)
makedepends=(git cmake clang llvm python)
options=(!emptydirs)
source=(${url}/archive/igc-${pkgver}.tar.gz
        git+https://github.com/intel/vc-intrinsics.git#commit=2d0795c6799d262a464e5e8b8f74b09dd3ec1054
        ${pkgname}-fix-build-with-LLVM-12.patch::https://github.com/intel/intel-graphics-compiler/commit/1c28c742daac.patch
        more-build-fixes-for-LLVM-12.patch)
sha256sums=('4213710fcb5a299a1fc4c7429375312f7875b9efab2323e6e180ba908ba4cb00'
            'SKIP'
            'c464562f2bc6a5434ab33a5799062c0459fa81b2d7e79df015d003266dc850d0'
            '0ab3dfa84c2f67aad99f086c3d0b2abba0be86a4a80a75883cf4e89282b166ce')

prepare() {
    cd ${pkgname}-igc-${pkgver}
    # two patches to build with LLVM 12 -- the first one was committed but
    # later reverted upstream -- the second one is untested but it builds!
    # https://github.com/intel/intel-graphics-compiler/pull/171
    patch -Np1 -i ../${pkgname}-fix-build-with-LLVM-12.patch
    patch -Np1 -i ../more-build-fixes-for-LLVM-12.patch
}

build() {
    CXXFLAGS+=' -Wno-error=deprecated-declarations'
    cmake -B build -S ${pkgname}-igc-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_OPTION__LLVM_PREFERRED_VERSION='12.0.0' \
        -DVC_INTRINSICS_SRC="${srcdir}/vc-intrinsics" \
        -DINSTALL_SPIRVDLL=0 \
        -DINSTALL_GENX_IR=ON \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
    mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
