# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.12812.26
pkgrel=3
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses spirv-tools zlib lld)
makedepends=(git cmake clang llvm python libunwind spirv-headers vc-intrinsics)
options=(!emptydirs)
source=(${url}/archive/igc-${pkgver}.tar.gz
        ${pkgname}-additional-vc-fix-of-pointer-element-type-llvm15.patch::${url}/commit/e09e752949e7af0231884d1b11ea907e3e8b1611.patch
        ${pkgname}-disable-opaque-pointers-llvm15.patch::${url}/commit/c707d1e2244aec988bdd5d2a7473ef3a32a5bac7.patch
        ${pkgname}-promotebools.patch::https://github.com/intel/intel-graphics-compiler/commit/d6bb93ee1a5ecae8df55b82747c7375b6d12c1c6.patch
        ${pkgname}-explicitly-cast-pointers-to-int-llvm15.patch::${url}/commit/d1761dfc3ca6b54bac0ee213389a65f84d2aa9b7.patch)
sha256sums=('d1a8ff080ea3836b4dd5bba4b49774abe0970c40e4becbd0a7d74f665bda3d83'
            '5eb1cebbd0de3fe245ab9709cd58bc4b065bf36d7c308aa622c19cd8dd8e2e2f'
            'efad92db5319bb1667f4202681407cb350b1cdddab1fca7538df0ebec16f4563'
            'fa681179cd44dfa1b9b1bf17618f6782d1d86b24de2959ec9ab5e70ff9206766'
            'e067498a8235359ae2bb955e465faf34c64f35eb2168d67890a1dd2ecbcb7b18')

prepare() {
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-additional-vc-fix-of-pointer-element-type-llvm15.patch
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-disable-opaque-pointers-llvm15.patch
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-promotebools.patch
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-explicitly-cast-pointers-to-int-llvm15.patch
}

build() {
  export CXXFLAGS="${CXXFLAGS/-Wp,-D_GLIBCXX_ASSERTIONS}"

  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='15.0.6' \
    -DIGC_OPTION__SPIRV_TOOLS_MODE=Prebuilds \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DIGC_OPTION__USE_PREINSTALLED_SPRIV_HEADERS=ON \
    -DIGC_OPTION__VC_INTRINSICS_MODE=Prebuilds \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
