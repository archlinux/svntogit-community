# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Redistribution information:
# https://software.intel.com/en-us/mkl
# https://software.intel.com/en-us/articles/intel-math-kernel-library-license-faq
pkgname=intel-mkl
pkgver=2020.0.166
_pkgver=${pkgver/%./-/}
pkgrel=4
pkgdesc="Intel Math Kernel Library"
arch=(x86_64)
url="https://software.intel.com/en-us/mkl"
license=("custom:ISSL")
source=("http://registrationcenter-download.intel.com/akdlm/irc_nas/tec/16318/l_mkl_$pkgver.tgz"
        intel-mkl.sh
        intel-mkl.conf)
sha512sums=('470bdb6b435ad938a520185fcd35ab4ce1656da0884c32506575077f398b3db84d5eeb4ac9f1af5a4d29dc9c30d473c4ff82effe8015c4f2dd8e0e4038155127'
            '1268685ff3ccd47733084dedcd4ea0d393f4ed3f6d00cd92125765ae5c8e14e7fe49b82692b3f667bfea931ad3c284a1c1c37235b474db09b5350c0046566c3c'
            '8dd145af32158978f2257c5916a316c3a4f427224968f54b429d47651f38f8df7da5aba35c45c6d87fd35bc659e01e99f708404743d53d0034bd46992c3b4b3a')

prepare() {
  cd l_mkl_$pkgver/rpm
  for rpm in *.rpm; do
    bsdtar -xf $rpm
  done
}

package() {
  cd l_mkl_$pkgver/rpm

  # We'll ship the terrible mklvars.sh file because some users surely expect it.
  # TODO: This is basically just hard-coded paths anyway. Just ship a static file.
  # It's a terrible pkg-config replacement by Intel.
  # install -Dm755 opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/bin/mklvars.sh "${pkgdir}"/usr/bin/mklvars.sh
  # sed -i "s|<INSTALLDIR>|/opt/intel/mkl|g" "${pkgdir}"/usr/bin/mklvars.sh

  mkdir -p "${pkgdir}/usr/bin"
  install -Dm755 opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/tools/mkl_link_tool "${pkgdir}"/opt/intel/mkl/tools/mkl_link_tool
  ln -s /opt/intel/mkl/tools/mkl_link_tool "${pkgdir}"/usr/bin/mkl_link_tool

  mkdir -p "${pkgdir}"/opt/intel/mkl/lib

  # Sadly Intel seems to expect a certain structure for MKL
  # See https://software.intel.com/en-us/mkl-linux-developer-guide-high-level-directory-structure
  # We'll try to follow that as per upstream suggestion.
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/lib/intel64_lin/*.{a,so} "${pkgdir}"/opt/intel/mkl/lib
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/compiler/lib/intel64_lin/*.{a,so} "${pkgdir}"/opt/intel/mkl/lib
  cp -r opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/include "${pkgdir}"/opt/intel/mkl/include
  install -Dm644 opt/intel/compilers_and_libraries_${pkgver}/licensing/mkl/en/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # Install profile and ld.so.config files
  # See https://software.intel.com/en-us/mkl-linux-developer-guide-scripts-to-set-environment-variables
  install -Dm755 "${srcdir}/intel-mkl.sh" "${pkgdir}/etc/profile.d/intel-mkl.sh"
  install -Dm644 "${srcdir}/intel-mkl.conf" "${pkgdir}/etc/ld.so.conf.d/intel-mkl.conf"
}
# vim:set ts=2 sw=2 et:
