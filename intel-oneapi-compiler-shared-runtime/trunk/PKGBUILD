# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname='intel-oneapi-compiler-shared'
pkgname="${_pkgname}-runtime"
pkgver=2022.1.0
_debpkgrel='3768'
pkgrel=8
pkgdesc="Intel oneAPI compiler runtime libraries"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("custom")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
        "https://apt.repos.intel.com/oneapi/pool/main/${_pkgname}-common-${pkgver}-${pkgver}-${_debpkgrel}_all.deb")
b2sums=('1e014fd80d1146f8c8cc9339dd002722e2378c51d0ff83935aad64d0386ce14dbde37291f9630ca85b3eadaa83ddf7954c7e83898e0c80a31eb8b9b949fa2e1a'
        '69bbd3e2b205d4a12b76475ee0bd400e7f9549e162f08afdc9b5c78ea62e6328509c0ba8fb1ed8beb2d0c267962eb339ff5b408fb99b2a5cf55093344c65b20d')
depends=('intel-oneapi-openmp' 'intel-oneapi-common')
provides=("${_pkgname}-common")
conflicts=('intel-oneapi-basekit')

noextract=(
    "${pkgname}-${pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    "${_pkgname}-common-${pkgver}-${pkgver}-${_debpkgrel}_all.deb"
)

package() {
    cd "${srcdir}"
    ar x "${srcdir}/${pkgname}-${pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    tar xvf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz
    ar x "${srcdir}/${_pkgname}-common-${pkgver}-${pkgver}-${_debpkgrel}_all.deb"
    tar xvf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz


    # add latest and common symlink
    local _lib_path='/opt/intel/oneapi/compiler'
    ln -s "${_lib_path}/${pkgver}" "${pkgdir}/${_lib_path}/latest"
    ln -s "${_lib_path}/latest/linux/compiler/lib/intel64_lin" \
          "${pkgdir}/${_lib_path}/${pkgver}/linux/compiler/lib/intel64"

    # allow libs to be found
    local _ldso_conf="${pkgdir}"/etc/ld.so.conf.d
    install -d "${_ldso_conf}"
    echo "${_lib_path}/latest/linux/lib" > "${_ldso_conf}/${pkgname}.conf"
    echo "${_lib_path}/latest/linux/lib/x64" >> "${_ldso_conf}/${pkgname}.conf"
    echo "${_lib_path}/latest/linux/compiler/lib/intel64_lin" >> "${_ldso_conf}/${pkgname}.conf"

    # fix link bug
    rm "${pkgdir}/${_lib_path}/${pkgver}"/linux/lib/libffi.so.6
    cd "${pkgdir}/${_lib_path}/${pkgver}"/linux/lib
    ln -s libffi.so.6.0.1 libffi.so.6

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
