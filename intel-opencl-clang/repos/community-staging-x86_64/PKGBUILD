# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=opencl-clang
pkgname=intel-${_srcname}
pkgver=12.0.0
pkgrel=1
pkgdesc="Wrapper library around clang that can compile OpenCL C kernels to SPIR-V modules"
arch=(x86_64)
url="https://github.com/intel/opencl-clang/"
license=(custom)
depends=(clang llvm-libs spirv-llvm-translator)
makedepends=(cmake llvm git)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('da3cde9c3b360526371d255d99283f390a01a984fef84378d2df1e34021dda27')

build() {
    # PREFERRED_LLVM_VERSION is used to allow building with upcoming LLVM minor releases
    cmake -B build -S ${_srcname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLLVMSPIRV_INCLUDED_IN_LLVM=OFF \
        -DSPIRV_TRANSLATOR_DIR=/usr \
        -DLLVM_NO_DEAD_STRIP=ON \
        -DPREFERRED_LLVM_VERSION=12.0.0 \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 ${_srcname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
