# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Francisco Lopes

pkgname=interception-tools
pkgver=0.6.7
pkgrel=1
pkgdesc='A minimal composable infrastructure on top of libudev and libevdev'
arch=('x86_64')
license=('GPL3')
url='https://gitlab.com/interception/linux/tools'
depends=(
    'libevdev' 'libevdev.so'
    'yaml-cpp' 'libyaml-cpp.so'
    'systemd-libs' 'libudev.so'
)
makedepends=('cmake' 'systemd' 'boost')
source=(
    "$pkgname-$pkgver.tar.gz::https://gitlab.com/interception/linux/tools/-/archive/v$pkgver/tools-v$pkgver.tar.gz"
)
sha256sums=('58a388899c7bb765c5d35306b57894fb220dc8ab4d37475a33898bb2934bca81')

build() {
    cmake -B build \
          -S "tools-v${pkgver}" \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=None \
          -Wno-dev
    cmake --build build
}

package() {
    install -dm755 "$pkgdir/etc/interception/udevmon.d"
    install -Dm644 "tools-v$pkgver/udevmon.service" -t "$pkgdir/usr/lib/systemd/system"
    install -Dm644 "tools-v$pkgver/README.md" -t "$pkgdir/usr/share/doc/$pkgname"

    cd build
    make DESTDIR="$pkgdir/" install
}
