# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: nofxx <x@nofxx.com>

pkgname=iodine
pkgver=0.7.0
pkgrel=1
pkgdesc='Tunnel IPv4 data through a DNS server'
arch=('i686' 'x86_64')
url='http://code.kryo.se/iodine'
license=('custom:ISC')
depends=('zlib' 'net-tools')
source=("http://code.kryo.se/${pkgname}/${pkgname}-${pkgver/rc/-rc}.tar.gz"
       'iodined.service'
       'iodined.conf.d')

build() {
  cd ${pkgname}-${pkgver/rc/-rc}/

  make
}

package() {
  cd ${pkgname}-${pkgver/rc/-rc}/

  make prefix=/usr sbindir=/usr/bin DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/licenses/iodine"
  sed -n '/AUTHORS & LICENSE/,$p' README > "${pkgdir}/usr/share/licenses/iodine/LICENSE"

  install -Dm644 "$srcdir/iodined.service" "$pkgdir/usr/lib/systemd/system/iodined.service"
  install -Dm644 "$srcdir/iodined.conf.d" "$pkgdir/etc/conf.d/iodined"
}

sha256sums=('ad2b40acf1421316ec15800dcde0f587ab31d7d6f891fa8b9967c4ded93c013e'
            '4d2c45ab783ce07911afe48403884edc0ee5ccbb463bc4026f83b511d5dde418'
            '6babc497d86e7b142f61e529f9facacb95e868e920b15dcf66e039891b02929b')
