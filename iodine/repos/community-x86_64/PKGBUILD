# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: nofxx <x@nofxx.com>

pkgname=iodine
pkgver=0.6.0rc1
pkgrel=3
pkgdesc='Tunnel IPv4 data through a DNS server'
arch=('i686' 'x86_64')
url='http://code.kryo.se/iodine'
license=('custom:ISC')
depends=('zlib')
source=("http://code.kryo.se/${pkgname}/${pkgname}-${pkgver/rc/-rc}.tar.gz"
       'iodined.service'
       'iodined.conf.d')

build() {
  cd ${pkgname}-${pkgver/rc/-rc}/

  make
}

package() {
  cd ${pkgname}-${pkgver/rc/-rc}/

  make prefix=/usr sbindir=/usr/bin DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/licenses/iodine"
  sed -n '/AUTHORS & LICENSE/,$p' README > "${pkgdir}/usr/share/licenses/iodine/LICENSE"

  install -Dm644 "$srcdir/iodined.service" "$pkgdir/usr/lib/systemd/system/iodined.service"
  install -Dm644 "$srcdir/iodined.conf.d" "$pkgdir/etc/conf.d/iodined"
}

sha256sums=('dacf950198b68fd1dae09fe980080155b0c75718f581c08e069eee0c1b6c5e60'
            '4d2c45ab783ce07911afe48403884edc0ee5ccbb463bc4026f83b511d5dde418'
            '6babc497d86e7b142f61e529f9facacb95e868e920b15dcf66e039891b02929b')
