# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=iotop
pkgver=0.6
pkgrel=6
pkgdesc='View I/O usage of processes'
arch=('any')
url='http://guichaz.free.fr/iotop/'
license=('GPL')
depends=('python')
changelog=$pkgname.changelog
source=(http://guichaz.free.fr/$pkgname/files/$pkgname-$pkgver.tar.bz2)
sha256sums=('3adea2a24eda49bbbaeb4e6ed2042355b441dbd7161e883067a02bfc8dcef75b')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

# Install binary to /usr/bin
  sed -i '7,13d' setup.py
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

#FS#33906 fix
  chmod 644 "${pkgdir}"/usr/share/man/man8/iotop.8
}
