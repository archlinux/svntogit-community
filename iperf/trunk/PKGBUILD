# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=iperf
pkgver=2.0.5
pkgrel=4
pkgdesc='A tool to measure maximum TCP bandwidth'
arch=('i686' 'x86_64')
license=('custom')
url="http://iperf.sourceforge.net"
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/iperf/iperf-2.0.5.tar.gz"
        'client.cpp.patch')
md5sums=('44b5536b67719f4250faed632a3cd016'
         '82ce63c87cc8bb2f0f94069857ac14a9')

build() {
  cd $pkgname-$pkgver
  # fix FS#25159
  patch -Np1 -i "$srcdir/client.cpp.patch"
  ./configure --prefix=/usr \
              --enable-ipv6 \
              --enable-threads
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/iperf/LICENSE"
}

# vim:set ts=2 sw=2 ft=sh et:
