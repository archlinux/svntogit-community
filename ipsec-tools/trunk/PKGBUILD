# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Allan Henriksen <allan.henriksen@gmail.com>

pkgname=ipsec-tools
pkgver=0.8.1
pkgrel=4
pkgdesc="KAME IPSec tools ported to Linux"
arch=('i686' 'x86_64')
url="http://ipsec-tools.sourceforge.net/"
depends=('readline' 'openssl' 'krb5')
makedepends=('linux-headers')
license=('GPL')
options=('!makeflags' '!libtool')
source=(http://downloads.sourceforge.net/sourceforge/ipsec-tools/$pkgname-$pkgver.tar.bz2
	racoon.service
	ipsec.service
	ipsec-tools-linux-3.7-compat.patch)
md5sums=('d38b39f291ba2962387c3232e7335dd8'
         '3a64a9e3b498c6da90450ffb0b758aea'
         '387a0b0c4f10e42b2bb62282885cdc9c'
         'ae1dd20c83dcfce3dedb46ee73e83613')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -p1 <$srcdir/ipsec-tools-linux-3.7-compat.patch
  sed -i 's#-Werror##' configure.ac

  ./bootstrap
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin \
	--enable-security-context=no --enable-hybrid --enable-dpd --enable-natt \
	--enable-adminport --enable-gssapi \
	--with-kernel-headers=/lib/modules/`pacman -Q linux-headers | cut -f2 -d\ `-ARCH/build/include
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm0644 $srcdir/racoon.service $pkgdir/usr/lib/systemd/system/racoon.service
  install -Dm0644 $srcdir/ipsec.service $pkgdir/usr/lib/systemd/system/ipsec.service
}
