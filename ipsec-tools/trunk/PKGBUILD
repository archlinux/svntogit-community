# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Allan Henriksen <allan.henriksen@gmail.com>

pkgname=ipsec-tools
pkgver=0.7.2
pkgrel=3
pkgdesc="KAME IPSec tools ported to Linux"
arch=('i686' 'x86_64')
url="http://ipsec-tools.sourceforge.net/"
depends=('readline' 'openssl')
license=('GPL')
options=('!makeflags' '!libtool')
source=(http://downloads.sourceforge.net/sourceforge/ipsec-tools/$pkgname-$pkgver.tar.bz2
	racoon.rc)
md5sums=('72861f005746ee27984b2ee715ecc629'
         'a9d89177f0eecd1df4fbe21c201ee046')

build() {
  cd $startdir/src/$pkgname-$pkgver

  sed -i 's#-Werror##' configure

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-security-context=no --enable-hybrid --enable-dpd --enable-natt \
	--enable-adminport

  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

  install -Dm0755 $srcdir/racoon.rc $pkgdir/etc/rc.d/racoon
}
