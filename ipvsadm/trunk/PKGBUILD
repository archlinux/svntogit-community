# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ipvsadm
pkgver=1.29
pkgrel=1
pkgdesc='The IP Virtual Server administration utility'
arch=('x86_64')
url='http://www.linuxvirtualserver.org/software/ipvs.html'
license=('GPL2')
depends=('libnl1' 'popt' 'bash')
backup=("etc/$pkgname.conf")
options=('!makeflags')
install=$pkgname.install
source=("https://kernel.org/pub/linux/utils/kernel/ipvsadm/$pkgname-$pkgver.tar."{xz,sign}
        "$pkgname.systemd"
        "$pkgname.service")
validpgpkeys=('D893F72888349DB4F170F9A23A554D033B37BCED') # Jesper Dangaard Brouer (RedHat)
# upstream provides sha256sums of release
sha256sums=('c3de4a21d90a02c621f0c72ee36a7aa27374b6f29fd4178f33fbf71b4c66c149'
            'SKIP'
            '4960ea254128347b03665d51817f29cb643cf2ebca13f5f46649257552ad3ceb'
            '25506d07f412cccab6f2f3a2a1900d5eeda27bc58602d14f44910c4e036b5341')

prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
  :
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  pushd $pkgname-$pkgver
  make \
    BUILD_ROOT="$pkgdir" \
    MANDIR=usr/share/man \
    SBIN="$pkgdir/usr/bin" \
    INIT="$pkgdir/etc/rc.d" \
    install
  popd
  # systemd
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # remove legacy init system
  rm -rf "$pkgdir/etc/rc.d"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
