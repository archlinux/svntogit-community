# Maintainer: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=ipython-0.10
pkgname=(ipython-0.10 ipython-0.10-docs)
pkgver=0.10.2
pkgrel=1
arch=('any')
url="http://ipython.scipy.org/"
license=('custom')
  makedepends=('python2') # for setup.py
source=("http://ipython.scipy.org/dist/$pkgver/ipython-$pkgver.tar.gz")
md5sums=('dda59bfc6e256ce786c4bc4aadb220a4')

package_ipython-0.10() {
  pkgdesc="An enhanced Interactive Python shell."
  depends=('python2')
  conflicts=('ipython')
  optdepends=("wxpython: needed for ipythonx and ipython-wx"
              "twisted: networking-related tasks"
              "python-foolscap: for IPython's parallel computing features"
              "python-pexpect: for irunner"
              "python2-nose: if you want to run IPython's test suite")

  cd "$srcdir/ipython-$pkgver"

  install -Dm644 docs/source/license_and_copyright.txt "$pkgdir/usr/share/licenses/ipython/license.txt"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  rm -rf "$pkgdir/usr/share/doc"
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

package_ipython-0.10-docs() {
  pkgdesc='Documentation and examples for IPython'
  conflicts=('ipython-docs')

  cd "$srcdir/ipython-$pkgver"

  install -Dm644 docs/source/license_and_copyright.txt "$pkgdir/usr/share/licenses/ipython-docs/license.txt"
  # Can I use $pkgname in that install command?

  python2 setup.py install --prefix=/usr --root="$pkgdir"
  rm -rf "$pkgdir/usr/lib" "$pkgdir/usr/bin" "$pkgdir/usr/share/man"
  # This seems wrong.  We're running setup.py for both
  # packages, and removing different things in each.
}
