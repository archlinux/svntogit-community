# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# IPython2 Maintainer: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>
 
pkgname=ipython
pkgver=0.12
pkgrel=4
pkgdesc="An enhanced Interactive Python shell."
arch=('any')
url="http://ipython.org"
provides=('ipython3')
replaces=('ipython3')
license=('Modified BSD')
depends=('python' 'sqlite' 'python-distribute')
optdepends=("wxpython: needed for ipythonx and ipython-wx"
            "python-nose: if you want to run IPython's test suite"
            "pyqt: for ipython-qtconsole"
            "sip: for ipython-qtconsole"
            "python-pygments: for ipython-qtconsole"
            "python-pyzmq: for ipython-qtconcole"
            "python-tornado: for ipython notebook")
source=("http://archive.ipython.org/release/$pkgver/ipython-$pkgver.tar.gz"
        "execfile.patch")
md5sums=('4cf9f23b1171b3517f9a7a2bfc875b5f'
         'e2e7b9eeebb3bf207e65f8c75f3731b8')

package() {
  cd "$srcdir/ipython-$pkgver"

  # added in 0.12-4
  # fixes arch fs#29476, ipython #1421
  patch -Np0 -i  ../execfile.patch

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete

  cd "$pkgdir/usr/bin/"
  mv ipcluster3        ipcluster
  mv ipcontroller3     ipcontroller
  mv ipengine3         ipengine
  mv iplogger3         iplogger
  mv iptest3           iptest
  mv ipython3          ipython
  mv irunner3          irunner
  mv pycolor3          pycolor
}
