# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Hilton Medeiros <medeiros.hilton AT gmail DOT com>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Paolo Fagni <paolo.fagni AT mail DOT com>
# Contributor: Lukas Kropatschek <lukas.krop AT gmail DOT com>

pkgname=irrlicht
pkgver=1.8
pkgrel=1
pkgdesc="An open source high performance realtime 3D graphics engine."
arch=('i686' 'x86_64')
url="http://irrlicht.sourceforge.net/"
license=('ZLIB')
depends=('libgl' 'libjpeg' 'bzip2' 'libpng')
makedepends=('mesa' 'unzip' 'libxcursor')
optdepends=('libxcursor: run examples')
source=("http://downloads.sourceforge.net/irrlicht/$pkgname-$pkgver.zip")
noextract=($pkgname-$pkgver.zip)
md5sums=('d06329d8f466658caaf4838ba349e393')

build() {
  unzip $pkgname-$pkgver.zip

  cd $srcdir/$pkgname-$pkgver

  cd source/Irrlicht
  make NDEBUG=1 sharedlib
  make NDEBUG=1

  # Edit, build and install the examples
  cd $srcdir/$pkgname-$pkgver/examples
  sed -i '/define USE_IRRKLANG/s:.*://&:' ./Demo/CDemo.h
  sed -i '/^CXXFLAGS/d' $(grep -Rl "^CXXFLAGS =" *)

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/source/Irrlicht

  install -d $pkgdir/usr/lib \
             $pkgdir/usr/share/licenses/$pkgname \
             $pkgdir/usr/share/$pkgname/examples/bin \
             $pkgdir/usr/share/doc/$pkgname

  sed -i "/^INSTALL_DIR/s:=.*:=$pkgdir/usr/lib:" Makefile

  make install

  cd $srcdir/$pkgname-$pkgver/
  install -m644 readme.txt $pkgdir/usr/share/licenses/$pkgname

  # Install static library and fix headers permissions
  install -m644 lib/Linux/libIrrlicht.a $pkgdir/usr/lib
  chmod 644 $pkgdir/usr/include/$pkgname/*

  # Install media files for examples
  cp -r media $pkgdir/usr/share/$pkgname

  # Install documentation
  cp -r doc/* $pkgdir/usr/share/doc/$pkgname
  rm -f $pkgdir/usr/share/doc/$pkgname/*.txt

  cd $pkgdir/usr/lib
  ln -s libIrrlicht.so.$pkgver libIrrlicht.so.1

  # Just a helper for examples compilation
  ln -s libIrrlicht.so.$pkgver $srcdir/$pkgname-$pkgver/lib/Linux/libIrrlicht.so

  cd $srcdir/$pkgname-$pkgver
  install -m755 bin/Linux/* /$pkgdir/usr/share/$pkgname/examples/bin/
}
