# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Hilton Medeiros <medeiros.hilton AT gmail DOT com>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Paolo Fagni <paolo.fagni AT mail DOT com>
# Contributor: Lukas Kropatschek <lukas.krop AT gmail DOT com>

pkgbase=irrlicht
pkgname=('irrlicht' 'irrlicht-docs')
pkgver=1.8.2
pkgrel=1
pkgdesc="An open source high performance realtime 3D graphics engine."
arch=('i686' 'x86_64')
url="http://irrlicht.sourceforge.net/"
license=('ZLIB')
depends=('libgl' 'libjpeg' 'bzip2' 'libpng')
makedepends=('mesa' 'unzip' 'libxcursor' 'wget')
optdepends=('libxcursor: run examples')
source=("http://downloads.sourceforge.net/irrlicht/$pkgname-$pkgver.zip")
noextract=($pkgname-$pkgver.zip)
md5sums=('fc842c84c8081406d358cb94f6fe5500')

prepare() {
  unzip $pkgbase-$pkgver.zip

  cd $srcdir/$pkgbase-$pkgver/source/Irrlicht

  # I'm so sorry for this but Irrlicht people don't care about Linux much
  #for file in COpenGLExtensionHandler.h glext.h glxext.h wglext.h; do
  #    wget http://sourceforge.net/p/irrlicht/code/4600/tree/trunk/source/Irrlicht/${file}?format=raw --output-document ${file}
  #done
}

build() {
  cd $srcdir/$pkgbase-$pkgver/source/Irrlicht

  make NDEBUG=1 sharedlib
}

package_irrlicht() {
  cd $srcdir/$pkgbase-$pkgver/source/Irrlicht

  sed -i "/^INSTALL_DIR/s:=.*:=$pkgdir/usr/lib:" Makefile

  make install

  cd $srcdir/$pkgbase-$pkgver/
  install -Dm644 readme.txt $pkgdir/usr/share/licenses/$pkgbase/LICENSE

  # fix headers permissions
  chmod 644 $pkgdir/usr/include/$pkgbase/*

  cd $pkgdir/usr/lib
  ln -s libIrrlicht.so.$pkgver libIrrlicht.so.1
}

package_irrlicht-docs() {
  cd $srcdir/$pkgbase-$pkgver/

  install -d $pkgdir/usr/share/$pkgbase $pkgdir/usr/share/doc/$pkgbase

  # install media files for examples
  cp -r media $pkgdir/usr/share/$pkgbase

  # install documentation
  cp -r doc/* $pkgdir/usr/share/doc/$pkgbase
  rm -f $pkgdir/usr/share/doc/$pkgbase/*.txt
}
