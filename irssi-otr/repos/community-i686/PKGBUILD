# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dominik George <nik@naturalnet.de>
# Contributor: quantax -- contact via Arch Linux forum or AUR

pkgname=irssi-otr
pkgver=1.0.2
pkgrel=2
pkgdesc='Off-the-Record Messaging (OTR) for the Irssi IRC client'
url='https://github.com/cryptodotis/irssi-otr'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('irssi' 'libotr' 'glib2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/cryptodotis/irssi-otr/archive/v${pkgver}.tar.gz)
sha512sums=('983bc861e5d38774305425b412368689f17a1e11298bcef49b9f0ea7a2275b1443ba2ce0a9ef9866e537097f1c6c00e9264085f5cd711f0d78faf252fba18d2f')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
