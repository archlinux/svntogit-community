# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.10.0
pkgrel=1
pkgdesc="A compiler for high-performance SIMD programming on the CPU"
arch=('x86_64')
url="https://ispc.github.io/"
license=('BSD')
depends=('ncurses' 'zlib' 'llvm-libs' 'clang')
makedepends=('llvm' 'python' 'lib32-glibc')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ispc/ispc/archive/v${pkgver}.tar.gz"
        '0001-Remove-uses-of-LLVM-dump-functions.patch' # Borrowed from Fedora
        'Makefile.patch') # Borrowed from Fedora
sha256sums=('0aa30e989f8d446b2680c9078d5c5db70634f40b9aa07db387aa35aa08dd0b81'
            '2cd47ff285b2a16053ee6b10fb9e7042b1c4e33cc195f00ecb0127a16cb1f737'
            'c3bcbe52472973afa445989723f16f047cadda980b0adf11ba7ab4bf30e746f2')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../0001-Remove-uses-of-LLVM-dump-functions.patch
  patch -p1 -i ../Makefile.patch
}

build() {
  cd ${pkgname}-${pkgver}
  # FS#60340
  make gcc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ispc -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

