# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ispc
pkgver=1.9.1
pkgrel=1
pkgdesc="A compiler for high-performance SIMD programming on the CPU"
arch=('x86_64')
url="https://ispc.github.io/"
license=('BSD')
depends=('ncurses' 'zlib')
makedepends=('llvm' 'clang' 'python2')
makedepends_x86_64=('lib32-glibc')
source=("$pkgname-${pkgver}.tar.gz::https://github.com/ispc/ispc/archive/v${pkgver}.tar.gz")
md5sums=('b9a4e7c21c50900e836ef1626191e03b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # fix python version
  find . -name "*.py" -exec sed -i 's|python$|python2|' '{}' ';'
  sed -i 's|python |python2 |' Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -D -m755 ispc "$pkgdir/usr/bin/ispc"
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

