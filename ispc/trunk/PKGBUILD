# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.9.2
pkgrel=5
pkgdesc="A compiler for high-performance SIMD programming on the CPU"
arch=('x86_64')
url="https://ispc.github.io/"
license=('BSD')
depends=('ncurses' 'zlib' 'llvm-libs' 'clang')
makedepends=('llvm' 'python' 'lib32-glibc')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ispc/ispc/archive/v${pkgver}.tar.gz"
        '0001-Remove-uses-of-LLVM-dump-functions.patch' # Borrowed from Fedora
        'Makefile.patch' # Borrowed from Fedora
        LLVM6-1.patch::"https://patch-diff.githubusercontent.com/raw/ispc/ispc/pull/1307.patch"
        LLVM6-2.patch::"https://patch-diff.githubusercontent.com/raw/ispc/ispc/pull/1315.patch"
        LLVM7-1.patch::"https://patch-diff.githubusercontent.com/raw/ispc/ispc/pull/1312.patch"
        LLVM7-2.patch::"https://patch-diff.githubusercontent.com/raw/ispc/ispc/pull/1316.patch"
        LLVM7-3.patch::"https://patch-diff.githubusercontent.com/raw/ispc/ispc/pull/1325.patch"
        LLVM7-4.patch::"https://patch-diff.githubusercontent.com/raw/ispc/ispc/pull/1330.patch")
sha256sums=('76a14e22f05a52fb0b30142686a6cb144b0415b39be6c9fcd3f17ac23447f0b2'
            'aed52cba60197bd20b2aef12e93e2e640d3ef2596bf154fe4a6e0980cfe78cca'
            '95fbfd84e0da1fca3fabbfbcab597a7756599ddc1539ac98e49b6e269f5bc538'
            'e0846b0b7793bec1522a87696330bfdbd5c6b976d2f77389f5fe93c9de92386d'
            '205468666cf985dd0634103c00a727e942ab330bddd2520f0d89e98e662cc495'
            'ac02472507690def70643b96d01bf70a6dfab7e465351c6f4588696679f5fb61'
            '3cb8a31ea5228972e2ecc3bd9c51d934fbd31f96babb93c2ef5d16e7f4e8aabd'
            '0a8b878aa6e07c2cc561e9f60e9ecea274d1f77bf46a6413f151cdc0489a7ce2'
            '39d2b606b4bfdd0c59ad384facc0d51894e6856439fe6fe4c2a09edaf4145f8b')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../0001-Remove-uses-of-LLVM-dump-functions.patch
  patch -p1 -i ../Makefile.patch
  patch -p1 -i ../LLVM6-1.patch
  patch -p1 -i ../LLVM6-2.patch
  patch -p1 -i ../LLVM7-1.patch
  patch -p1 -i ../LLVM7-2.patch
  patch -p1 -i ../LLVM7-3.patch
  patch -p1 -i ../LLVM7-4.patch
}

build() {
  cd ${pkgname}-${pkgver}
  # FS#60340
  make gcc
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ispc -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

