# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=iwd
pkgver=0.7
pkgrel=1
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('glibc')
source=("https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar"{.xz,.sign})
sha512sums=('e6029e33f43fda24f3ddb87b1bb878ad8926066380a0e71415e645bc4f8aa1a6aaa43d2482987b1d473c267fef6d5a2a676af80282ba4c77ab64605c348cabb1'
            'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/iwd \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# vim: set ts=2 sw=2 ft=sh noet:
