# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=iwd
pkgver=0.19
pkgrel=1
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('glibc')
source=("https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar"{.xz,.sign}
        'iwd.tmpfiles')
sha512sums=('72be037974664260ba570affbb400b8261d1f7923877a9b03fd748ac1527c885f96fd70c8305300d81918a232a08ec7d71866bb3b9c4c86ac03b0b77fce85ade'
            'SKIP'
            '03a3a0fba53706ef46bb12e75ad0bc30969044662df5bdcebc608c57b6ab9c5b3b6944e7941549db3172577c7d86c42e39a06e285459663ae48032fd3ab697d4')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')
changelog=ChangeLog

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/iwd \
	--enable-wired \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
	install -Dm644 "${srcdir}/iwd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/iwd.conf"
}

# vim: set ts=2 sw=2 ft=sh noet:
