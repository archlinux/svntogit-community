# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniele Paolella <danielepaolella@email.it>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>
# Contributor: Thomas Bahn <thomas-bahn at gmx dot net>

pkgbase=jack2
pkgname=('jack2' 'jack2-dbus')
pkgdesc="The next-generation JACK with SMP support"
pkgver=1.9.12
pkgrel=2
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL')
groups=('pro-audio')
makedepends=('python' 'python2-dbus' 'libffado' 'celt' 'opus' 'doxygen')
source=("${pkgbase}-${pkgver}::https://github.com/jackaudio/${pkgbase}/archive/v${pkgver}.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules'
)
sha512sums=('f0271dfc8f8e2f2489ca52f431ad4fa420665816d6c67a01a76da1d4b5ae91f6dad8c4e3309ec5e0c159c9d312ed56021ab323d74bce828ace26f1b8d477ddfa'
            'c21f593d46ff12d1b8d3ce7ca4593c230a673e67ed4e486944f52fd51bdf902873d3fb253220c8ec9c6c5d5b9f002ecf3dd72318222cd748b1925bfcbf1df5d7'
            '1f876aa61de4bc4e54d46dde7f8c8c2aa575fc382c09f3dc6819c328224b1998c7171cfd3039d35ca214e40909ad71f8d56a9b3b5652046beb5ca97e44a780b1')
prepare() {
  cd "${pkgbase}-${pkgver}"

  # https://github.com/jackaudio/jack2/issues/308
  sed -i 's:bin/env python:bin/env python2:' \
    "example-clients/jack_control"

  # copy of the source for jack2-dbus
  cp -r "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/${pkgname[1]}-${pkgver}"

  # configure jack2
  python waf configure --prefix=/usr \
                       --htmldir="/usr/share/doc/${pkgbase}/" \
                       --freebob=no \
                       --classic \
                       --dbus

  # configure jack2-dbus
  cd "${srcdir}/${pkgname[1]}-${pkgver}"
  python waf configure --prefix=/usr \
                       --htmldir="/usr/share/doc/${pkgbase}/" \
                       --freebob=no \
                       --dbus
}

build() {
  # build jack2
  cd "${pkgname[0]}-${pkgver}"
  python waf build
  # build jack2-dbus
  cd "${srcdir}/${pkgname[1]}-${pkgver}"
  python waf build
}

package_jack2() {
  pkgdesc="The next-generation JACK with SMP support"
  depends=('celt' 'opus' 'libffado' 'python2-dbus')
  conflicts=('jack')
  provides=('jack' 'jackmp' 'jackdmp' 'jackdbus')
  backup=(etc/security/limits.d/99-audio.conf)

  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -vDm 644 "${srcdir}/99-audio.conf" \
    "${pkgdir}/etc/security/limits.d/99-audio.conf"

  install -vDm 644 "${srcdir}/40-hpet-permissions.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules"
}

package_jack2-dbus() {
  pkgdesc="The next-generation JACK with SMP support (for D-BUS interaction only)"
  depends=('celt' 'opus' 'libffado' 'python2-dbus')
  conflicts=('jack' 'jack2')
  provides=('jack' 'jack2' 'jackmp' 'jackdmp' 'jackdbus')
  backup=(etc/security/limits.d/99-audio.conf)

  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -vDm 644 "${srcdir}/99-audio.conf" \
    "${pkgdir}/etc/security/limits.d/99-audio.conf"

  install -vDm 644 "${srcdir}/40-hpet-permissions.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/40-hpet-permissions.rules"
}

# vim:set ts=2 sw=2 et:
