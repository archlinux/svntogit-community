# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniele Paolella <danielepaolella@email.it>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>
# Contributor: Thomas Bahn <thomas-bahn at gmx dot net>

pkgname=jack2
pkgdesc="C++ version of the JACK low-latency audio server for multi-processor machines"
pkgver=1.9.12
pkgrel=11
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL2' 'LGPL2.1')
depends=('celt' 'libsamplerate' 'opus')
makedepends=('doxygen' 'libffado' 'portaudio' 'waf')
optdepends=('a2jmidid: Expose ALSA MIDI in JACK MIDI'
            'libffado: Firewire support'
            'portaudio: Portaudio support'
            'python2-dbus: For jack_control'
            'realtime-privileges: Acquire realtime privileges')
provides=('jack' 'libjack.so' 'libjacknet.so' 'libjackserver.so')
conflicts=('jack')
replaces=('jack2-dbus')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jackaudio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('f0271dfc8f8e2f2489ca52f431ad4fa420665816d6c67a01a76da1d4b5ae91f6dad8c4e3309ec5e0c159c9d312ed56021ab323d74bce828ace26f1b8d477ddfa')

prepare() {
  cd "${pkgname}-${pkgver}"
  # copying relevant custom waf scripts and loading them specifically using
  # wscript
  (
    touch __init__.py
    mkdir -vp tools
    cp -v waflib/extras/xcode*.py tools
    rm -rv waflib
    sed -e "s/load('xcode'/load('xcode', tooldir='tools'/g" \
        -e "s/load('xcode6'/load('xcode6', tooldir='tools'/g" \
        -i wscript
  )
  # https://github.com/jackaudio/jack2/issues/308
  sed -i 's:bin/env python:bin/env python2:' \
    "example-clients/jack_control"
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                        --htmldir="/usr/share/doc/${pkgbase}/" \
                        --freebob=no \
                        --classic \
                        --dbus
  waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
}
# vim:set ts=2 sw=2 et:
