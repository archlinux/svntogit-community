# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniele Paolella <danielepaolella@email.it>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>
# Contributor: Thomas Bahn <thomas-bahn at gmx dot net>

pkgname=jack2
pkgdesc="C++ version of the JACK low-latency audio server for multi-processor machines"
pkgver=1.9.16
pkgrel=1
arch=('x86_64')
url="https://github.com/jackaudio/jack2"
license=('GPL2' 'LGPL2.1')
depends=('db' 'expat' 'gcc-libs' 'glibc' 'opus')
makedepends=('alsa-lib' 'celt' 'dbus' 'git' 'libffado' 'libsamplerate'
'libsndfile' 'readline' 'systemd' 'waf')
optdepends=('a2jmidid: Expose ALSA MIDI in JACK MIDI'
            'libffado: Firewire support'
            'python-dbus: For jack_control'
            'realtime-privileges: Acquire realtime privileges'
            'zita-ajbridge: for using multiple ALSA devices')
provides=('jack' 'libjack.so' 'libjacknet.so' 'libjackserver.so')
conflicts=('jack')
replaces=('jack2-dbus')
source=("git+https://github.com/jackaudio/${pkgname}.git#tag=v${pkgver}?signed")
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0') # falkTX <falktx@falktx.com>
sha512sums=('SKIP')

prepare() {
  cd "${pkgname}"
  # copying relevant custom waf scripts and loading them specifically using
  # wscript
  (
    touch __init__.py
    mkdir -vp tools
    cp -v waflib/extras/xcode*.py tools
    rm -rv waflib
    sed -e "s/load('xcode'/load('xcode', tooldir='tools'/g" \
        -e "s/load('xcode6'/load('xcode6', tooldir='tools'/g" \
        -i wscript
  )
}

build() {
  cd "${pkgname}"
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  waf configure --prefix=/usr \
                --htmldir="/usr/share/doc/${pkgbase}/" \
                --systemd-unit \
                --classic \
                --dbus
  waf build
}

package() {
  depends+=('libasound.so' 'libcelt0.so' 'libdbus-1.so' 'libreadline.so'
  'libsamplerate.so' 'libsndfile.so' 'libsystemd.so')
  cd "${pkgname}"
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  waf install --destdir="${pkgdir}"
}
# vim:set ts=2 sw=2 et:
