# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniele Paolella <danielepaolella@email.it>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>
# Contributor: Thomas Bahn <thomas-bahn at gmx dot net>

pkgbase=jack2
pkgname=('jack2' 'jack2-dbus')
pkgdesc="The next-generation JACK with SMP support"
pkgver=1.9.12
pkgrel=6
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL')
# currently waf is broken with python 3.7: https://github.com/jackaudio/jack2/issues/366
makedepends=('celt' 'doxygen' 'libffado' 'opus' 'python2')
optdepends=('realtime-privileges: Acquire realtime privileges')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/jackaudio/${pkgbase}/archive/v${pkgver}.tar.gz")
sha512sums=('f0271dfc8f8e2f2489ca52f431ad4fa420665816d6c67a01a76da1d4b5ae91f6dad8c4e3309ec5e0c159c9d312ed56021ab323d74bce828ace26f1b8d477ddfa')

prepare() {
  cd "${pkgbase}-${pkgver}"

  # https://github.com/jackaudio/jack2/issues/308
  sed -i 's:bin/env python:bin/env python2:' \
    "example-clients/jack_control"

  # copy of the source for jack2-dbus
  cp -r "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/${pkgname[1]}-${pkgver}"

  # configure jack2
  python2 waf configure --prefix=/usr \
                        --htmldir="/usr/share/doc/${pkgbase}/" \
                        --freebob=no \
                        --classic \
                        --dbus

  # configure jack2-dbus
  cd "${srcdir}/${pkgname[1]}-${pkgver}"
  python2 waf configure --prefix=/usr \
                        --htmldir="/usr/share/doc/${pkgbase}/" \
                        --freebob=no \
                        --dbus
}

build() {
  # build jack2
  cd "${pkgname[0]}-${pkgver}"
  python2 waf build
  # build jack2-dbus
  cd "${srcdir}/${pkgname[1]}-${pkgver}"
  python2 waf build
}

package_jack2() {
  pkgdesc="JACK low-latency audio server for multi-processor machines (daemon activation)"
  depends=('celt' 'opus' 'libffado' 'python2-dbus')
  conflicts=('jack' 'jack2-dbus')
  provides=('jack')

  cd "${pkgname}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
}

package_jack2-dbus() {
  pkgdesc="JACK low-latency audio server for multi-processor machines (only dbus activation)"
  depends=('celt' 'opus' 'libffado' 'python2-dbus')
  conflicts=('jack' 'jack2')
  provides=('jack' 'jack2')

  cd "${pkgname}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
