# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.5.3
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base)
makedepends=(help2man meson jack python-jinja python-yaml)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('ab8faabfaf5aefcc035d19e8a00480f84be5a83b8330966d762029ed9187f3710968679c518d4cf7f6b372b4b4e7da53c3184654dc3648e3e75589adab8bd520')
b2sums=('59cf75f55a7396c045ac6866d260ef4d28b5ff87cfc74b2d18a6674ad334c03a65859a611280d2401071823e9a35c2ece1b695629943cb1b94fcbd9c96376df8')

build() {
  arch-meson -Drtaudio=disabled build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
