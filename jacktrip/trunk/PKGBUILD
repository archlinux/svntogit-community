# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.6.1
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base qt5-declarative qt5-networkauth qt5-svg)
makedepends=(help2man meson jack python-jinja python-yaml)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('3b89017c37bacbdbcd07458926ffae29bf2d310a40dc8fcb7390e8aa8f2860f9fab053810d613e9c7c9184acd4e4876848cb1f4a8e7044898a298c19c7ee213f')
b2sums=('24084ca45731906086668435c5b81ce84972907d9052236e1636f38ecbdc70e2bc737cf647f42633c19a81c9ba3cc7d1ac4c8ec146b4540383b79490bad00338')

build() {
  arch-meson -Drtaudio=disabled build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
