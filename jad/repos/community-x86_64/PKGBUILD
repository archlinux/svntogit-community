# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: astaroth <astaroth_@web.de>

pkgname=jad
pkgver=1.5.8e
pkgrel=5
pkgdesc='Java decompiler'
license=('custom')
arch=('x86_64' 'i686')
url='http://www.varaneckas.com/jad'
optdepends=('vim-jad: open class files directly in vim and see the decompiled source code')
makedepends=('links')
source=("http://www.varaneckas.com/$pkgname/${pkgname}${pkgver//./}.linux.static.zip"
        'jad.html::http://www.varaneckas.com/jad')
sha256sums=('2878e19fc1fdd725b516f538a57b02aaec1b2d1e4b106d550230381ffa9c0c81'
            '5a0a0a26ca84c733c3a213960c070285bb2046b149adec49d6f47f00b2736dd0')

prepare() {
  cd "$srcdir"

  links -dump jad.html | grep -A19 Copyright > LICENSE
}

package() {
  cd "$srcdir"

  install -Dm755 "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 Readme.txt "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
