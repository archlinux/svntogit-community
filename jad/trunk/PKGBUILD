# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: astaroth <astaroth_@web.de>

pkgname=jad
pkgver=1.5.8e
pkgrel=6
pkgdesc='Java decompiler'
license=('custom')
arch=('x86_64' 'i686')
url='http://www.varaneckas.com/jad'
optdepends=('vim-jad: open class files directly in vim and see the decompiled source code')
source=("http://www.varaneckas.com/$pkgname/${pkgname}${pkgver//./}.linux.static.zip"
        'jad.html::http://www.varaneckas.com/jad')
sha256sums=('2878e19fc1fdd725b516f538a57b02aaec1b2d1e4b106d550230381ffa9c0c81'
            '9ebc6c135fb169de6f79cc8993c914648b915cf071f54c4a31dc666b69649e5b')

prepare() {
  grep '<blockquote>' -A18 jad.html | cut -d">" -f2 | cut -d"<" -f1 > LICENSE
}

package() {
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 Readme.txt "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
