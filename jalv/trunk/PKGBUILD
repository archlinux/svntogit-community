# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=jalv
pkgver=1.6.0
pkgrel=3
pkgdesc="A simple but fully featured LV2 host for Jack"
arch=('x86_64')
url="https://drobilla.net/software//"
license=('custom:ISC')
groups=('pro-audio')
depends=('lilv' 'suil')
makedepends=('python' 'qt4' 'qt5-base' 'gtk3' 'gtkmm')
optdepends=('qt4: Qt 4.x frontend'
            'qt5-base: Qt 5.x frontend'
            'gtk3: Gtk+ 3.x frontend'
            'gtkmm: Gtk+ 2.x frontend')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('70453404788846774ef88b43653379f97140301e09e8de61779683df9ee85120485bebf03c959624639dc4d8dd71ce83db2a89b14b66be17696a88d1820ac1b3'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard

build() {
  cd "${pkgname}-${pkgver}"
  python waf configure --prefix=/usr
  python waf
}

package() {
  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,NEWS,README}
}
