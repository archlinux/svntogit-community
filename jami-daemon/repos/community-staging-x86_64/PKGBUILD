# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=ring-daemon
pkgname=jami-daemon
pkgver=20190813
pkgrel=2
_pjprojectver=5dfa75be7d69047387f9b0436dd9492bbbf03fe4
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(opendht yaml-cpp alsa-lib libpulse jack jsoncpp dbus-c++ ffmpeg gnutls
         expat libupnp libnatpmp libva libvdpau restbed libsecp256k1 util-linux
         opus libjsoncpp.so)
makedepends=(git boost msgpack-c autoconf-archive)
#checkdepends=(cppunit)
provides=(${_pkgname})
conflicts=(${_pkgname})
replaces=(${_pkgname})
_commit=26b35255a94aa9a13cbf3b3aa530d4b282a79565
source=("git+https://git.jami.net/savoirfairelinux/ring-daemon.git#commit=${_commit}"
        "https://github.com/pjsip/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz")
noextract=("pjproject-${_pjprojectver}.tar.gz")
sha512sums=(SKIP
            a9433b47294434288d61524dea556687fb02137ed56a10e8e2ba85d7888a2ca2a5bea4ae4a9ad008a4c208c5ec53fe364b10ed14481700d6aa8b9b6137e5e9ee)

prepare() {
  cd ${_pkgname}
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs
  mkdir contrib/native
  autoreconf -fvi
}

build() {
  cd ${_pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject
  make DEPS_pjproject=

  cd ../..
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-ipv6
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# Disabled because some tests (TURN) use the network.
#check() {
#  cd ${_pkgname}
#  make -k check
#}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}" install
}
