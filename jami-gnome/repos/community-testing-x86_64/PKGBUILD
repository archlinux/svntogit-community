# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-gnome
pkgver=20211101
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (GNOME client)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(libjamiclient jami-daemon
         gtk3 dconf clutter clutter-gtk webkit2gtk qrencode qt5-base libnotify libcanberra libnm)
makedepends=(git cmake qt5-tools)
_commit=b4da4a9165cc51c77a003614ab502f6eabef6774
source=(git+https://git.jami.net/savoirfairelinux/jami-client-gnome.git#commit=${_commit})
md5sums=(SKIP)

prepare() {
  cd jami-client-gnome
  # Fix build system looking for lrc sources
  rm -r web
  ln -s /usr/include/libringclient/web-chatview/ web
}

build() {
  cmake -B build -S jami-client-gnome \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
