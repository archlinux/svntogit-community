# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=jami-libclient
pkgver=20220317
pkgrel=1
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (client communication library)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(jami-daemon qt6-base)
makedepends=(git cmake qt6-tools)
provides=(libjamiclient)
conflicts=(libjamiclient)
replaces=(libjamiclient)
_commit=0c88ebafe0f640afd74c215533f9315f171e0d8d
source=(git+https://git.jami.net/savoirfairelinux/jami-libclient.git#commit=${_commit})
md5sums=(SKIP)

pkgver() {
  cd ${pkgname}
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

build() {
  cmake -B build -S jami-libclient \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
