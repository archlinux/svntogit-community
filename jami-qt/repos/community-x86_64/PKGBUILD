# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=jami-qt
pkgver=20220317
pkgrel=2
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (Qt client)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(jami-libclient glib2 gdk-pixbuf2 libnm libnotify qrencode
         qt6-declarative qt6-multimedia qt6-networkauth qt6-svg
         qt6-5compat qt6-webengine qt6-webchannel)
makedepends=(git cmake python qt6-tools)
replaces=(jami-gnome)
conflicts=(jami-gnome)
_commit=bef42fb1ce9b4cbd1418bae539d0ac8f75e8a590
source=(git+https://git.jami.net/savoirfairelinux/jami-client-qt.git#commit=${_commit})
md5sums=(SKIP)

pkgver() {
  cd jami-client-qt
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

build() {
  cmake -B build -S jami-client-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
