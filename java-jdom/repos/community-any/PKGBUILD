# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=java-jdom
pkgver=1.1.1
pkgrel=2
pkgdesc="XML Parsing Library for Java with contributions and tests"
arch=('any')
url="http://www.jdom.org"
license=('GPL')
depends=('java-runtime')
makedepends=('apache-ant')
source=(http://www.jdom.org/dist/binary/jdom-$pkgver.tar.gz \
        http://www.jdom.org/dist/binary/jdom-contrib-$pkgver.tar.gz \
        http://www.jdom.org/dist/binary/jdom-test-$pkgver.tar.gz)
md5sums=('0ad116194e3101fb08fab8f6f00cc58f' \
         '01eb3c5938d25dc10f23881c6a0b8786' \
         '2badb19633a7b97072254f2933d24f9b')

build() {
  for _i in jdom jdom-contrib jdom-test
  do
    pushd ${_i}
    ./build.sh
    popd
  done
}

package() {
  for _i in jdom jdom-contrib jdom-test
  do
    install -D -m755 ${_i}/build/${_i}.jar \
      ${pkgdir}/usr/share/java/jdom/${_i}.jar
  done
}
