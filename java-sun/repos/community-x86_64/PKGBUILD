# $Id$
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Guillaume ALAUX <Guillaume at alaux dot net>

pkgname=('jre' 'jdk')
pkgbase=java-sun
pkgver=6u22
pkgrel=1
pkgdesc="Sun/Oracle's implementation of tha Java 6 specification"
arch=('i686' 'x86_64')
	[ "$CARCH" = "i686" ]   && _arch=i586
	[ "$CARCH" = "x86_64" ] && _arch=amd64
url="http://java.sun.com"
license=('custom')
makedepends=('java-runtime')
source=(http://download.java.net/dlj/binaries/jdk-${pkgver}-dlj-linux-${_arch}.bin
	https://jdk-distros.dev.java.net/source/browse/*checkout*/jdk-distros/trunk/utils/construct.sh
	jdk.profile
	java-control-panel.desktop
	java-monitoring-and-management-console.desktop
	java-policy-settings.desktop
	java-visualvm.desktop
	java-web-start.desktop
	jre.profile)

[ "$CARCH" = "i686" ]   && md5sums=('2204fea1b2593f3cc196a0a92b80b18f'
         '94065b612df0046d9ae758943f9f6a75'
         '7aaac9ec3e440b92361c6561ac00424a'
         'e4d814c0f310d77ed6990c731bccd0fb'
         '4bc2b0be93f76ed07b471e3c89eb5a8a'
         '8e3cec7ab641ec449cabd068be583f5c'
         '9ba148ebabce8ed3351b813a9a66b6e5'
         '568451cf45fc8c7adb83a222a61ca2c6'
         '50b4f5ac4129097461d246645d73a622')
[ "$CARCH" = "x86_64" ] && md5sums=('e5e102ef16d8938f547fe2a73e659608'
         '94065b612df0046d9ae758943f9f6a75'
         '7aaac9ec3e440b92361c6561ac00424a'
         'e4d814c0f310d77ed6990c731bccd0fb'
         '4bc2b0be93f76ed07b471e3c89eb5a8a'
         '8e3cec7ab641ec449cabd068be583f5c'
         '9ba148ebabce8ed3351b813a9a66b6e5'
         '568451cf45fc8c7adb83a222a61ca2c6'
         '50b4f5ac4129097461d246645d73a622')

build() {
  cd ${srcdir}
  mkdir unbundle-jdk
  cd unbundle-jdk
  sh ../jdk-${pkgver}-dlj-linux-$_arch.bin --accept-license
  cd ..
  sh construct.sh unbundle-jdk linux-jdk linux-jre
}

package_jre() {
  pkgdesc="Sun/Oracle's Java Runtime Environment"
  depends=('glibc' 'libxtst')
  provides=('j2re' 'java-runtime=6')
  conflicts=('j2re' 'java-runtime')
  replaces=('j2re')
  install='jre.install'

  cd ${srcdir}

  mkdir -p ${pkgdir}/opt/java
  cp -R linux-jdk/jre ${pkgdir}/opt/java

  install -Dm755 ${srcdir}/jre.profile ${pkgdir}/etc/profile.d/jre.sh

  mkdir -p ${pkgdir}/usr/lib/mozilla/plugins

  if [ "$CARCH" = "i686" ]; then
    ln -s /opt/java/jre/lib/i386/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
  else ln -s /opt/java/jre/lib/amd64/libnpjp2.so ${pkgdir}/usr/lib/mozilla/plugins
  fi

  mkdir -p ${pkgdir}/usr/share/licenses/jre
  cp ${pkgdir}/opt/java/jre/COPYRIGHT ${pkgdir}/usr/share/licenses/jre
  cp ${pkgdir}/opt/java/jre/LICENSE ${pkgdir}/usr/share/licenses/jre
  cp ${pkgdir}/opt/java/jre/THIRDPARTYLICENSEREADME.txt ${pkgdir}/usr/share/licenses/jre

  # Fix system prefs folder (FS#18872)
  install -dm755 ${pkgdir}/etc/.java/.systemPrefs
}

package_jdk() {
  pkgdesc="Sun/Oracle's Java Development Kit"
  depends=('glibc' 'jre')
  provides=('java-environment=6' 'j2sdk')
  conflicts=('java-environment' 'j2sdk')
  replaces=('j2sdk')
  install='jdk.install'

  cd ${srcdir}

  #copy icon
  install -Dm644 ${srcdir}/linux-jdk/jre/lib/desktop/icons/hicolor/48x48/apps/sun-java.png \
  		 ${pkgdir}/usr/share/pixmaps/java.png

  rm -rf linux-jdk/jre
  mkdir -p ${pkgdir}/opt
  mv linux-jdk ${pkgdir}/opt/java

  install -D -m755 ${srcdir}/jdk.profile ${pkgdir}/etc/profile.d/jdk.sh

  mkdir -p ${pkgdir}/usr/share/licenses/jdk
  cp ${pkgdir}/opt/java/COPYRIGHT ${pkgdir}/usr/share/licenses/jdk
  cp ${pkgdir}/opt/java/LICENSE ${pkgdir}/usr/share/licenses/jdk
  cp ${pkgdir}/opt/java/THIRDPARTYLICENSEREADME.txt ${pkgdir}/usr/share/licenses/jdk

  #desktop entries
  install -Dm644 ${srcdir}/java-control-panel.desktop \
  		 ${pkgdir}/usr/share/applications/java-control-panel.desktop
  install -Dm644 ${srcdir}/java-monitoring-and-management-console.desktop \
  		 ${pkgdir}/usr/share/applications/java-monitoring-and-management-console.desktop
  install -Dm644 ${srcdir}/java-policy-settings.desktop \
  		 ${pkgdir}/usr/share/applications/java-policy-settings.desktop
  install -Dm644 ${srcdir}/java-visualvm.desktop \
  		 ${pkgdir}/usr/share/applications/java-visualvm.desktop
  install -Dm644 ${srcdir}/java-web-start.desktop \
  		 ${pkgdir}/usr/share/applications/java-web-start.desktop
}
