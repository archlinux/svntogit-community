# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <j@codemac.net>
# Contributor: Dusty Phillips <dusty@buchuki.com>

pkgname=jedit
pkgver=5.3.0
pkgrel=4
pkgdesc='Editor for programmers'
arch=('any')
url='http://jedit.org/'
license=('GPL')
depends=('java-runtime')
makedepends=('gendesk')
optdepends=('java-environment: for java development'
            'noto-fonts: perfectly usable font')
options=('!emptydirs')
source=('jedit.sh'
        'jeditbg.sh'
        'jedit.props'
        "http://sourceforge.net/projects/jedit/files/jedit/$pkgver/jedit-${pkgver}-noarch-1sao.tgz")
sha256sums=('ae53cda453152dd7835d6428910171ca1383bb5e36a4b9b1164d98a940f6ed10'
            'a1dd1b688f08c5c967861fe80df13cac201afa6dcfcecc9a289b2c3bec7c8915'
            'dc5fe0050321d4f34bce556895fbc39cc6f7df194e14eb4f6ea53166a4d82347'
            '888740e33376781cd991ba7ba6abfa3ac1fe79cd939f5278ae7310f6a2084855')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --exec 'jeditbg %U' \
    --name 'jEdit' --genericname 'Editor'
}

package() {
  install -d "$pkgdir/usr/share/"{doc,java}
  cp -r "usr/doc/$pkgname-$pkgver" "$pkgdir/usr/share/doc/$pkgname"
  cp -r "usr/share/$pkgname-$pkgver" "$pkgdir/usr/share/java/$pkgname"

  # Launchers, properties and desktop shortcuts
  install -Dm755 jedit.sh "$pkgdir/usr/bin/jedit"
  install -Dm755 jeditbg.sh "$pkgdir/usr/bin/jeditbg"
  install -Dm644 jedit.props "$pkgdir/usr/share/jedit/default.props"
  install -Dm644 jedit.desktop "$pkgdir/usr/share/applications/jedit.desktop"
  install -Dm644 "$pkgdir/usr/share/java/jedit/doc/jedit.png" \
    "$pkgdir/usr/share/pixmaps/jedit.png"
}

# vim:set ts=2 sw=2 et:
