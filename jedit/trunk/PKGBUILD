# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <j@codemac.net>
# Contributor: Dusty Phillips <dusty@buchuki.com>

pkgname=jedit
pkgver=4.5.0
pkgrel=2
pkgdesc="Java based text editor for programmers"
arch=('any')
url="http://www.jedit.org/"
license=('GPL')
depends=('sh' 'java-runtime' 'desktop-file-utils')
optdepends=('java-environment: for java development')
install=$pkgname.install
noextract=("${pkgname}${pkgver}install.jar")
source=("http://downloads.sourceforge.net/$pkgname/${pkgname}${pkgver}install.jar"
        'jedit.sh'
        'jeditbg.sh'
        'jedit.props'
        'jedit.desktop')
sha256sums=('ed8b2ec5cd2ddeddf4f52eaa7c1af70a65fb50ff244abc50368a9b3698c29a39'
            'e908f2d4b15aab76e5e4633b71f25f7f25baa5b9e375d373e8729205700a57de'
            'a1dd1b688f08c5c967861fe80df13cac201afa6dcfcecc9a289b2c3bec7c8915'
            '1ada993b3f079bba40be736a66eae390ee0427c18289be5965338f8e3adc7f27'
            '6dc2eac7fb04481bbeb7a892dd74eda40988bf85de0af0c48eebea3afe30fb5d')

build() {
  cd "$srcdir"

  java -jar "${pkgname}${pkgver}install.jar" auto \
    "$pkgdir/usr/share/java/$pkgname/" \
    unix-script="$pkgdir/usr/bin/" \
    unix-man="$pkgdir/usr/share/man/man1" > inst.log
}

package() {
  cd "$srcdir"

  install -Dm755 "jedit.sh" "$pkgdir/usr/bin/jedit"
  install -Dm755 "jeditbg.sh" "$pkgdir/usr/bin/jeditbg"
  install -Dm644 "jedit.props" "$pkgdir/usr/share/jedit/default.props"
  install -Dm644 "jedit.desktop" "$pkgdir/usr/share/applications/jedit.desktop"
  install -Dm644 "$pkgdir/usr/share/java/jedit/doc/jedit.png" \
    "$pkgdir/usr/share/pixmaps/jedit.png"
}

# vim:set ts=2 sw=2 et:
