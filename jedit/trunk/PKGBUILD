# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <j@codemac.net>
# Contributor: Dusty Phillips <dusty@buchuki.com>

# Does not currently build in a chroot.
# TODO: move apache-ivy from AUR

pkgname=jedit
pkgver=5.3.0
pkgrel=2
pkgdesc='Text editor for programmers'
arch=('any')
url='http://www.jedit.org/'
license=('GPL')
depends=('java-runtime' 'desktop-file-utils')
makedepends=('apache-ant' 'gendesk' 'unzip' 'tar' 'maven' 'java-environment')
optdepends=('java-environment: for java development'
            'noto-fonts: usable font')
options=('!emptydirs')
install=$pkgname.install
noextract=("${pkgname}${pkgver}install.jar")
source=('jedit.sh'
        'jeditbg.sh'
        'jedit.props'
        "http://sourceforge.net/projects/jedit/files/jedit/$pkgver/jedit${pkgver}source.tar.bz2")
sha256sums=('b2e5a8f2f4818b336b5d71f677114f1bc01e929d7fa60918675cc2234dae1a3a'
            'a1dd1b688f08c5c967861fe80df13cac201afa6dcfcecc9a289b2c3bec7c8915'
            'dc5fe0050321d4f34bce556895fbc39cc6f7df194e14eb4f6ea53166a4d82347'
            '8d56d036be4dd45254748f063ade01293b862a615cdff6aed274ab58085f6304')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" --exec 'jeditbg %U' \
    --name 'jEdit' --genericname 'Editor'

  # Extract the documentation so that it can be installed manually
  #unzip "${pkgname}${pkgver}install.jar"
  #tar jxf installer/jedit-api.tar.bz2
}

build() {
  cd jEdit

  ant -k dist-java
}

package() {
  install -d "$pkgdir/usr/share/java/$pkgdir" \
    "$pkgdir/usr/bin" \
    "$pkgdir/usr/share/man/man1"

  cd jEdit/dist

  # Run the installer
  (java -jar "${pkgname}${pkgver}install.jar" auto \
    "$pkgdir/usr/share/java/$pkgname/" \
    unix-script="$pkgdir/usr/bin/" \
    unix-man="$pkgdir/usr/share/man/man1/" 2>&1) > inst.log

  cd "$srcdir"

  # Launchers, properties and desktop shortcuts
  install -Dm755 jedit.sh "$pkgdir/usr/bin/jedit"
  install -Dm755 jeditbg.sh "$pkgdir/usr/bin/jeditbg"
  install -Dm644 jedit.props "$pkgdir/usr/share/jedit/default.props"
  install -Dm644 jedit.desktop "$pkgdir/usr/share/applications/jedit.desktop"
  install -Dm644 "$pkgdir/usr/share/java/jedit/doc/jedit.png" \
    "$pkgdir/usr/share/pixmaps/jedit.png"

  # The missing documentation files
  cd jEdit
  cp -ru doc "$pkgdir/usr/share/java/jedit/doc"
}

# vim:set ts=2 sw=2 et:
