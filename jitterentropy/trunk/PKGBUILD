# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com
# Contributor: Timothy Redaelli <timothy.redaelli+aur@gmail.com>

pkgname=jitterentropy
_pkgname=$pkgname-library
pkgver=3.0.2
pkgrel=1
pkgdesc='Hardware RNG based on CPU timing jitter'
arch=('x86_64')
url='https://www.chronox.de/jent.html'
license=('BSD')
depends=('glibc')
source=(https://www.chronox.de/jent/$_pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('4a6b1e5723b2bd7f5be25f3d81c968b2306f63973bb6db80fc49a42d3e078c38'
            'SKIP')
validpgpkeys=(3BCC43D4D2C87D1784B69EE4421EE936326AC15B)

prepare() {
  cd "$_pkgname-$pkgver"

  # Disable man page compression on install
  sed -e '/\tgzip .*\/man\// d' -i Makefile
  # Let the package manager handle stripping
  sed -e '/\tinstall.*-s / s/-s //g' -i Makefile
}

build() {
  cd "$_pkgname-$pkgver"
  LDFLAGS=-lpthread make jitterentropy
}

package() {
  cd "$_pkgname-$pkgver"
  install -dm755 "$pkgdir/usr/include"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 COPYING.bsd "$pkgdir/usr/share/licenses/$pkgname/COPYING.bsd"
}

# vim:set ts=2 sw=2 et:
