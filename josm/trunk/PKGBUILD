# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michele Sch√§uble <mschaeuble@swissonline.ch>

pkgname=josm
pkgver=7643
pkgrel=1
pkgdesc="An editor for OpenStreetMap written in Java"
arch=('any')
url="http://josm.openstreetmap.de/"
license=('GPL')
depends=('java-runtime' 'libxtst' 'ttf-font')
changelog=$pkgname.changelog
backup=('etc/conf.d/josm')
source=(http://josm.openstreetmap.de/$pkgname-tested.jar \
        $pkgname.desktop \
        $pkgname.conf.d)
sha256sums=('55d27a197dcb48754a47811bdbb56115eaefbdb7d32b4665ec75330ecfef470d'
            'edfb2cbf2aa7e6582b4d43cd402ea58be7990702e5ffffc3ff7afdc518c28ca9'
            'c86a73251eed42fcb129ae9b88a0ee3cf03d8d00a9385519ad536d5af0907663')

package() {
  cd "${srcdir}"
  
  install -D -m644 $pkgname-tested.jar "${pkgdir}"/usr/share/java/$pkgname/$pkgname.jar

#.desktop and icon file
  install -D -m644 "${srcdir}"/${pkgname}.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop
  install -D -m644 images/logo.png "${pkgdir}"/usr/share/pixmaps/$pkgname.png

#executable file
  install -d "${pkgdir}"/usr/bin
  cat <<"EOF" >"${pkgdir}"/usr/bin/$pkgname 
#!/bin/sh
# source application-specific settings
while true; do
    JOSM_ARGS=
    [ -f /etc/conf.d/josm ] && . /etc/conf.d/josm
    CLASSPATH="/usr/share/java/josm/josm.jar"
    java ${JOSM_ARGS} -cp "${CLASSPATH}" -Djosm.restart=true org.openstreetmap.josm.gui.MainApplication "$@"
    [ $? -eq 9 ] || break
done
EOF
  chmod 755 "${pkgdir}"/usr/bin/$pkgname
  install -Dm644 "${srcdir}"/$pkgname.conf.d "${pkgdir}"/etc/conf.d/$pkgname
}
