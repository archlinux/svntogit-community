# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: arjan5 <aur@anymore.nl>

pkgname=jpeg-archive
pkgver=2.1.1
pkgrel=2
pkgdesc='Utilities for archiving JPEGs for long term storage'
arch=(x86_64 i686)
url="https://github.com/danielgtaylor/jpeg-archive/"
license=(MIT)
depends=(bash)
makedepends=(git nasm)
optdepends=('parallel: for jpeg-archive'
            'dcraw: for jpeg-archive'
            'perl-image-exiftool: for jpeg-archive')
_commit=fc331be551a734372437fb71bdf1e595e6b5724c # 2.1.1
_mozjpeg_commit=72d249b89ca348c991d5e27c82cf32ba0f12d05a # v3.2
source=(git://github.com/danielgtaylor/jpeg-archive.git#commit=$_commit
        git://github.com/mozilla/mozjpeg.git#commit=$_mozjpeg_commit
        Fix-path-to-libjpeg-a.patch
        Replace-ladon-with-GNU-parallel.patch
        parallel.config)
md5sums=('SKIP'
         'SKIP'
         'fee4531e218dc868ccfa5f7a1ab03e48'
         '077f16cb6a5206819fbf2d0f1e80c1be'
         '1534de688b10c4dad77ccfbffecb9a59')

prepare() {
  cd "$srcdir/jpeg-archive"

  # Ignore empty lines in PPM header# 
  git cherry-pick -n 8d687e1b0da1df57cc28322bc2e0e09998e2981a

  # Fix bash IF 'unary operator expected' error
  git cherry-pick -n 2d6e03b7c089319f8f55a6f36f6100ece5712faf

  # Ignore lib64 silliness
  patch -p1 -i "$srcdir/Fix-path-to-libjpeg-a.patch"

  # Replace ladon with GNU parallel
  patch -p1 -i "$srcdir/Replace-ladon-with-GNU-parallel.patch"

  cd "$srcdir/mozjpeg"
  autoreconf -fiv
}

build() {
  cd "$srcdir/mozjpeg"
  ./configure --with-jpeg8 --prefix="$srcdir/jpeg-archive/mozjpeg"
  make
  make install

  cd "$srcdir/jpeg-archive"
  MOZJPEG_PREFIX="$srcdir/jpeg-archive/mozjpeg" make
}

package() {
  cd "$srcdir/jpeg-archive"
  make PREFIX="$pkgdir/usr" install
  install -Dm644 "$srcdir/parallel.config" \
    "$pkgdir/usr/share/$pkgname/parallel.config"

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  tail -n7 README.md > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
