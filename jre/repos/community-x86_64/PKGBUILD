# $Id$
# Maintainer: Jason Chu <jason@archlinux.org>

pkgname=jre
pkgver=6u17
pkgrel=1
pkgdesc="Sun's java runtime environment"
arch=('i686' 'x86_64')
  [ "$CARCH" = "i686" ]   && _arch=i586
  [ "$CARCH" = "x86_64" ] && _arch=amd64
url="http://java.sun.com"
depends=('glibc' 'libxtst')
install="jre.install"
source=(http://download.java.net/dlj/binaries/jdk-$pkgver-dlj-linux-$_arch.bin
        https://jdk-distros.dev.java.net/source/browse/*checkout*/jdk-distros/trunk/utils/construct.sh
        jre.profile)
[ "$CARCH" = "i686" ]   && md5sums=('3011863492d567a5b1f847c327ceed67'
         			    '94065b612df0046d9ae758943f9f6a75'
	          		    '50b4f5ac4129097461d246645d73a622')
[ "$CARCH" = "x86_64" ] && md5sums=('1b5c229b332cdf40227e7112c2c94105'
			            '94065b612df0046d9ae758943f9f6a75'
	          		    '50b4f5ac4129097461d246645d73a622')
replaces=('j2re')
conflicts=('j2re' 'java-runtime')
provides=('j2re' 'java-runtime=6')
license=('custom')

build() {
  cd $srcdir

  mkdir unbundle-jdk
  cd unbundle-jdk

  sh ../jdk-$pkgver-dlj-linux-$_arch.bin --accept-license

  cd ..

  sh construct.sh unbundle-jdk linux-jdk linux-jre

  mkdir -p $pkgdir/opt/java
  mv linux-jdk/jre $pkgdir/opt/java

  install -D -m755 $srcdir/${pkgname}.profile $pkgdir/etc/profile.d/${pkgname}.sh

  mkdir -p $pkgdir/usr/lib/mozilla/plugins

  if [ "$CARCH" = "i686" ]; then
    ln -s /opt/java/jre/lib/i386/libnpjp2.so $pkgdir/usr/lib/mozilla/plugins
    else ln -s /opt/java/jre/lib/amd64/libnpjp2.so $pkgdir/usr/lib/mozilla/plugins
  fi

  mkdir -p $pkgdir/usr/share/licenses/jre
  cp $pkgdir/opt/java/jre/COPYRIGHT $pkgdir/usr/share/licenses/jre
  cp $pkgdir/opt/java/jre/LICENSE $pkgdir/usr/share/licenses/jre
  cp $pkgdir/opt/java/jre/THIRDPARTYLICENSEREADME.txt $pkgdir/usr/share/licenses/jre
}
