# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jrnl
pkgver=3.0
pkgrel=1
pkgdesc="Collect your thoughts and notes without leaving the command line"
arch=('any')
url="https://jrnl.sh/"
license=('GPL3')
depends=('python-ansiwrap' 'python-colorama' 'python-cryptography' 'python-keyring'
         'python-parsedatetime' 'python-dateutil' 'python-pyxdg' 'python-pytz' 'python-rich'
         'python-ruamel-yaml' 'python-tzlocal')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-bdd' 'python-toml')
source=("https://github.com/jrnl-org/jrnl/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('80c086d1d360deb6bde10aa6a5b284d7e99d1c87ff2ff165aed3611fcfb132577a41c1a7c7d84d2eebb176f49685a1e7351b5c9041bdb17c9098966d5d5313bc')

prepare() {
  cd jrnl-$pkgver
  # https://github.com/pytest-dev/pytest-bdd/issues/501
  sed -i '/-n=auto/d' pyproject.toml
}

build() {
  cd jrnl-$pkgver
  python -m build -nw
}

check() {
  cd jrnl-$pkgver
  python -m pytest
}

package() {
  cd jrnl-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
