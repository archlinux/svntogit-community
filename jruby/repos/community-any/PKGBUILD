# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=1.7.18
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="http://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
depends=(java-runtime-headless bash)
options=(!strip)
noextract=("$pkgname-bin-${pkgver}.tar.gz")
source=(http://jruby.org.s3.amazonaws.com/downloads/$pkgver/$pkgname-bin-${pkgver}.tar.gz)
sha256sums=('eba6332d301c64bc5623c1d6f161762ef4fb9b38202350cdfc4e105775fbbf3c')

package() {
  cd "$pkgdir"

  mkdir opt
  bsdtar -C opt -xf "$srcdir/$pkgname-bin-$pkgver.tar.gz"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -delete
  rm -r opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-FreeBSD}

  mkdir -p usr/bin
  for _file in jirb{,_swing} jgem jruby{,c}; do
    ln -s ../../opt/jruby/bin/$_file usr/bin/$_file
  done

  mkdir -p usr/share/licenses/$pkgname
  ln -s ../../../../opt/jruby/COPYING usr/share/licenses/$pkgname/COPYING
}
