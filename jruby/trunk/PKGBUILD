# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=9.0.1.0
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="http://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
options=(!strip)
noextract=("$pkgname-bin-${pkgver}.tar.gz")
source=(http://jruby.org.s3.amazonaws.com/downloads/$pkgver/$pkgname-bin-${pkgver}.tar.gz)
sha256sums=('3cab8ead2f080eb9cdf16fc30cbc1974c36a4a4e6c9d321d5a3bbd973b64527f')

package() {
  depends=(java-runtime-headless bash)

  cd "$pkgdir"

  mkdir opt
  bsdtar -C opt -xf "$srcdir/$pkgname-bin-$pkgver.tar.gz"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -delete
  rm -r opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-FreeBSD}

  mkdir -p usr/bin
  for _file in jirb{,_swing} jgem jruby{,c}; do
    ln -s ../../opt/jruby/bin/$_file usr/bin/$_file
  done

  mkdir -p usr/share/licenses/$pkgname
  ln -s ../../../../opt/jruby/COPYING usr/share/licenses/$pkgname/COPYING
}
