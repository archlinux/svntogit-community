# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>

pkgname=jsampler
_name=JSampler
pkgver=0.9
pkgrel=6
pkgdesc="LinuxSampler Java GUI"
arch=('any')
url="http://jsampler.sourceforge.net/"
license=('GPL')
groups=('pro-audio')
depends=('java-runtime=8' 'linuxsampler')
makedepends=('java-environment=8' 'apache-ant')
source=("https://downloads.sourceforge.net/${pkgname}/${_name}-${pkgver}.tar.bz2"
        "${pkgname}.sh"
        "${pkgname}-classic.sh"
        "${pkgname}.desktop"
        "${pkgname}-classic.desktop")
sha512sums=('cbb1d476b1f3882592cc0fb7a61348802c0c399dfc3fcc35f4a8ec3396e8417754cafc3e1a14a90b0014bebd29d28b9d3771dcbb772df40d241eaadedad5299f'
            '632d6a160e026267a85c589b364076b7c439207022a2cdee7a6f432f13c9e34ca17a511fcdf35cd39030ed20dc5c92c670b06cf8ba862cb2f94ee8b552969a40'
            '8bf4db721e1dc6489186b7c70d9bea1c00cd20d567d16e9940497ed2ba17eb02481faff43ab83efd15e2f254896abada19ae6c1148fffca8860d3d52fee3aaf8'
            '9fd3b2ea525fc81abb8dc0611fbfc2636852d82811db8b2b66c821346d1cc8f84bd4d13123af5ee8993689e9c9f201302dbbe6c707883a900e87d0413b9278f6'
            '490c95e62b73d0d708d75c0d7f95074e2547ee3d4bb28996105c58d860067e8b7279413c7106adf19f59d899f2c6551eeb4f68d902446b211ce49e1340d51b68')

build() {
  cd "${_name}-${pkgver}"

  ant build-fantasia
  ant build-jsclassic
}

package() {
  cd "${_name}-${pkgver}"

  # install java files
  install -vDm 644 dist/Fantasia-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/fantasia.jar"
  install -vDm 644 dist/JS_Classic-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/js-classic.jar"

  # install shell scripts
  install -vDm 755 "${srcdir}/${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 755 "${srcdir}/${pkgname}-classic.sh" \
    "${pkgdir}/usr/bin/${pkgname}-classic"

  # install freedesktop.org compatibility
  install -vDm 644 res/fantasia/icons/app_icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 res/js-classic/icons/app-icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}-classic.png"

  install -vDm 644 "${srcdir}/${pkgname}"{,-classic}.desktop \
    -t "${pkgdir}/usr/share/applications/"
}

# vim:set ts=2 sw=2 et:
