# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=jshon
pkgver=20131105
pkgrel=5
pkgdesc="A json parser for the shell."
arch=('x86_64')
url="http://kmkeen.com/jshon/"
license=('MIT')
depends=('jansson')
source=(http://kmkeen.com/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('84596bcf2d6cde7bbc0fcb4626765b99')
sha256sums=('c8bcf5633b5964bdc1c56305e004510af19bed8714d5db696dfd6c19b72746e5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 $pkgname   "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.1 "$pkgdir/usr/share/man/man1/$pkgname.1"
  install -Dm644 LICENSE    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
