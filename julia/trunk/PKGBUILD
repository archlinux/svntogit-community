# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# Contributor: devmotion <nospam-archlinux.org@devmotion.de>
# Contributor: Valentin Churavy <v.churavy@gmail.com>

pkgbase=julia
pkgname=(julia julia-docs)
epoch=2
pkgver=1.0.0
pkgrel=1
arch=('x86_64')
pkgdesc='High-level, high-performance, dynamic programming language'
url='https://julialang.org/'
license=('MIT')
depends=('fftw' 'hicolor-icon-theme' 'libgit2' 'libutf8proc' 'mpfr' 'pcre2' 'suitesparse')
makedepends=('chrpath' 'cmake' 'gcc-fortran' 'gmp' 'gtk-update-icon-cache' 'patchelf' 'python2')
options=('!emptydirs' 'staticlibs')
source=("https://github.com/JuliaLang/$pkgbase/releases/download/v$pkgver/$pkgbase-$pkgver-full.tar.gz"{,.asc})
sha256sums=('1a2497977b1d43bb821a5b7475b4054b29938baae8170881c6b8dd4099d133f1'
            'SKIP')
validpgpkeys=('3673DF529D9049477F76B37566E3C7DC03D6E495') # Julia (Binary signing key) <buildbot@julialang.org>

build() {
  export CFLAGS="$CFLAGS -w"
  export CXXFLAGS="$CXXFLAGS -w"
  #
  # USE_SYSTEM_LIBUNWIND=1 fails
  # USE_SYSTEM_LIBM=1 fails
  # USE_SYSTEM_LLVM=1 fails (see FS#57387)
  #
  # Setting USE_SYSTEM_*=1 for quite a few things, for now,
  # just to make Julia 1.0.0 compile.
  #
  make -C "$pkgbase" \
    prefix=/usr \
    sysconfdir=/etc \
    MARCH=x86-64 \
    JULIA_BUILD_MODE=release \
    USE_BLAS64=0 \
    USE_INTEL_MKL=0 \
    USE_LLVM_SHLIB=0 \
    USE_SYSTEM_BLAS=0 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_LAPACK=0 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_LIBM=0 \
    USE_SYSTEM_LIBUNWIND=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_LLVM=0 \
    USE_SYSTEM_MPFR=0 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_PCRE=0 \
    USE_SYSTEM_SUITESPARSE=1 \
    USE_SYSTEM_UTF8PROC=1
}

package_julia() {
  backup=('etc/julia/juliarc.jl')
  optdepends=('gnuplot: If using the Gaston Package from julia')

  export CFLAGS="$CFLAGS -w"
  export CXXFLAGS="$CXXFLAGS -w"
  make -C "$pkgbase" \
    DESTDIR="$pkgdir" \
    prefix=/usr \
    sysconfdir=/etc \
    MARCH=x86-64 \
    USE_BLAS64=0 \
    USE_INTEL_MKL=0 \
    USE_LLVM_SHLIB=0 \
    USE_SYSTEM_BLAS=0 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_LAPACK=0 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_LIBM=0 \
    USE_SYSTEM_LIBUNWIND=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_LLVM=0 \
    USE_SYSTEM_MPFR=0 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_PCRE=0 \
    USE_SYSTEM_SUITESPARSE=1 \
    USE_SYSTEM_UTF8PROC=1 \
    install

  # Remove duplicate man-page from julia/doc
  rm -rf "$pkgdir/usr/share/julia/doc/man"

  # FS#58211 && https://github.com/JuliaLang/julia/issues/26830
  #chrpath -r '$ORIGIN/julia' "$pkgdir/usr/lib/libjulia.so.$pkgver"
  # points to /usr/lib
  #chrpath -d "$pkgdir/usr/bin/julia"

  # Documentation is in the julia-docs package
  rm -rf "$pkgdir/usr/share/"{doc,julia/doc}

  # License
  install -Dm644 "$pkgbase/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_julia-docs() {
  pkgdesc='Documentation and examples for Julia'
  depends=('julia')

  cd "$pkgbase"

  install -d "$pkgdir/usr/share/doc"
  cp -r doc "$pkgdir/usr/share/doc/$pkgbase"
  rm -rf "$pkgdir"/usr/share/doc/julia/man/
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# getver: julialang.org/downloads
# vim: ts=2 sw=2 et:
