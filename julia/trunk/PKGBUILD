# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# Contributor: devmotion <nospam-archlinux.org@devmotion.de>
# Contributor: Valentin Churavy <v.churavy@gmail.com>

pkgname=julia
epoch=2
pkgver=1.7.0
pkgrel=1
arch=(x86_64)
pkgdesc='High-level, high-performance, dynamic programming language'
url='https://julialang.org/'
license=(MIT)
depends=(cblas fftw hicolor-icon-theme libgit2 libunwind libutf8proc openblas
         suitesparse mbedtls openlibm pcre2 llvm-libs p7zip libblastrampoline)
makedepends=(cmake gcc-fortran python llvm patchelf libwhich)
optdepends=('gnuplot: If using the Gaston Package from julia')
replaces=(julia-docs)
source=(https://github.com/JuliaLang/julia/releases/download/v$pkgver/$pkgname-$pkgver-full.tar.gz{,.asc}
        https://github.com/JuliaLang/julia/commit/677ce6d3.patch
        https://github.com/JuliaLang/julia/commit/47f9139e.patch
        julia-llvm13.patch
        julia-libgit-1.2.patch
        julia-system-cblas.patch
        julia-hardcoded-libs.patch
        make-install-no-build.patch
        julia-llvm-patchelf.patch)
backup=(etc/julia/startup.jl)
sha256sums=('d40d83944f8e1709de1d6f7544e1a6721e091f70ba06b44c25b89bdba754dfa6'
            'SKIP'
            'a798c58ab518def84e4112538de59a10802e7dc854c20b08990a1619ba2aa95b'
            'c76c6fbb4e04b185d11e3c3e0aec99a2088f3b06621ce61d29cd21227a044a7a'
            'ab8a8ec61963661dca5bb2fa965dccaa36c500d55ef7b6767b8faef5fff461b5'
            'c57ea92a11fa8dac72229e6a912d2372ec0d98d63486426fe3bdeeb795de48f7'
            '8f8c12853ce847f5d1b5a4a461ddec701decdb81dae7bb31d66560c1deaed97a'
            '03043f005c133ac9af1d4dc113ea8b525ad3b393690625be77975f0e29dd6457'
            '8be4605f92a009072ca7e843549c225fc4e959893498e7c4f8f79e861e63714d'
            '6048c69c987f33f2b17d78b63368b0762d1d6a1e531ef9932d0c23bda49d1384')
validpgpkeys=('3673DF529D9049477F76B37566E3C7DC03D6E495') # Julia (Binary signing key) <buildbot@julialang.org>

prepare() {
  cd $pkgname-$pkgver

# fix build with LLVM 13
  patch -p1 -i ../677ce6d3.patch
  patch -p1 -i ../47f9139e.patch
# Adapt to LLVM 13 type changes
  patch -p1 -i ../julia-llvm13.patch
# libgit2 1.2 compatibility
  patch -p1 -i ../julia-libgit-1.2.patch
# Add and use option to build with system cblas
  patch -p1 -i ../julia-system-cblas.patch # Add and use option to build with system cblas
# Don't hardcode library names
  patch -p1 -i ../julia-hardcoded-libs.patch
# Don't build again in install
  patch -p1 -i ../make-install-no-build.patch
# Fix test failure
  sed -e 's|0.22314355f0 + 3.1415927f0im|0.22314355f0 - 3.1415927f0im|' -i stdlib/LinearAlgebra/test/lu.jl
# Don't try to run patchelf on system LLVM
  patch -p1 -i ../julia-llvm-patchelf.patch
}

_buildopts="prefix=/usr \
    bindir=/usr/bin \
    sysconfdir=/etc \
    libexecdir=/usr/lib \
    USE_BINARYBUILDER=0 \
    USE_SYSTEM_CSL=1 \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_BLAS=1 \
    USE_SYSTEM_LAPACK=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_LIBSUITESPARSE=1 \
    USE_SYSTEM_LIBBLASTRAMPOLINE=1 \
    USE_SYSTEM_LIBWHICH=1 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=1 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_LIBSSH2=1 \
    USE_SYSTEM_MBEDTLS=1 \
    USE_SYSTEM_CURL=1 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_ZLIB=1 \
    USE_SYSTEM_P7ZIP=1 \
    USE_SYSTEM_OPENLIBM=1 \
    MARCH=x86-64"

build() {
  cd $pkgname-$pkgver
  make release VERBOSE=1 JLDFLAGS=${LDFLAGS} $_buildopts
}

check() {
  cd $pkgname-$pkgver/test

  # this is the make testall target, plus the --skip option from
  # travis/appveyor/circleci (one test fails with DNS resolution errors)
  # Also skip tests that check for a hardcoded version number
  ../julia --check-bounds=yes --startup-file=no ./runtests.jl \
    --skip Sockets \
    --skip broadcast \
    --skip Distributed \
    --skip nghttp2_jll \
    --skip GMP_jll \
    --skip LibCURL \
    --skip LibSSH2_jll \
    --skip MbedTLS_jll \
    --skip MPFR_jll \
    --skip SuiteSparse_jll \
    --skip PCRE2_jll \
    --skip LibGit2_jll \
    --skip MozillaCACerts_jll \
    --skip NetworkOptions \
    --skip Downloads
  find ../stdlib \( -name \*.cov -o -name \*.mem \) -delete
  rm -fr ../stdlib/Artifacts/test/artifacts
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install $_buildopts

  ln -s /usr/lib/libopenblas.so "$pkgdir"/usr/lib/julia/libopenblas64_.so # Needed by some 3rd party packages

  rm "$pkgdir"/usr/lib/julia/libccalltest.so.debug # Remove debug testing library
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
