# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbconvert
_name=nbconvert
pkgver=6.0.7
pkgrel=3
pkgdesc="A language-agnostic web-based interactive shell/notebook server."
arch=('any')
url="https://pypi.python.org/pypi/nbconvert"
license=('BSD')
depends=('jupyter' 'jupyter-nbformat' 'python-pygments' 'python-mistune'
         'python-jinja' 'python-entrypoints' 'python-traitlets'
         'python-pandocfilters' 'python-bleach' 'python-testpath'
         'python-defusedxml' 'jupyter-nbclient' 'jupyterlab_pygments')
# really depends on jupyter-client, not jupyter
makedepends=('python-setuptools')
optdepends=('pandoc: non-html conversion output')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/nbconvert/archive/$pkgver.tar.gz")
md5sums=('7327576d903f4eb09968ed70524713d9')

build() {
  cd nbconvert-$pkgver
  python setup.py build
}

package() {
  cd "$srcdir/nbconvert-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=0

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
