# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbconvert
_name=nbconvert
pkgver=6.1.0
pkgrel=2
pkgdesc="Jupyter Notebook Conversion"
arch=('any')
url="https://pypi.python.org/pypi/nbconvert"
license=('BSD')
depends=(
  'jupyter'
  'jupyter-nbclient'
  'jupyter-nbformat'
  'jupyterlab_pygments'
  'python-bleach'
  'python-defusedxml'
  'python-entrypoints'
  'python-jinja'
  'python-pandocfilters'
  'python-mistune1'
  'python-pygments'
  'python-testpath'
  'python-traitlets'
)
# really depends on jupyter-client, not jupyter
makedepends=('python-pyppeteer' 'python-setuptools')
checkdepends=('chromium' 'python-pytest')
optdepends=('pandoc: non-html conversion output'
            'python-pyppeteer: for webPDF conversion support')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/nbconvert/archive/$pkgver.tar.gz")
sha512sums=('beb1d66788871b2d8b1576bf4e615053a870e6959c832294872a84c89b0329a28364f2d88625dedf3947b27d68e2915155cd6d1152610aec50c4bd7226698483')
b2sums=('9eceb9ed42d63bb495583139400ba1d6d68979379bec7ba408ba6c7e3954604f637a55f5c9e76573d0031f41269516877bfb745f685128d2c6161dff9d352818')

build() {
  cd nbconvert-$pkgver
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  # disable tests that download chromium and (probably) require a graphical session
  pytest -vv -k 'TestWebPDFExporter and not (test_export or test_webpdf_without_pyppeteer)'
  pytest -vv -k 'TestNbConvertApp and not test_webpdf_with_chromium'
  # run all remaining tests
  pytest -vv -k 'not TestWebPDFExporter and not TestNbConvertApp'
}

package() {
  cd "nbconvert-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=0

  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
