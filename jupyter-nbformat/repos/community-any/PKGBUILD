# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbformat
pkgver=5.5.0
pkgrel=1
pkgdesc="The base implementation of the Jupyter Notebook format and Python APIs for working with notebooks"
arch=('any')
url="https://pypi.python.org/pypi/nbformat"
license=('BSD')
depends=('python-traitlets' 'python-jupyter_core' 'python-jsonschema' 'python-fastjsonschema')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest' 'python-testpath' 'python-pep440')
source=(https://github.com/jupyter/nbformat/archive/$pkgver/nbformat-$pkgver.tar.gz)
sha256sums=('4d6bd882a6fb60e1204e7b9310846b790c710b138e21643750f6420b7aa09d6c')

build() {
  cd nbformat-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbformat-$pkgver
  pytest
}

package() {
  cd nbformat-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
