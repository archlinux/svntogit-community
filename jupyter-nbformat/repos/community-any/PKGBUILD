# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbformat
pkgver=5.6.0
pkgrel=1
pkgdesc="The base implementation of the Jupyter Notebook format and Python APIs for working with notebooks"
arch=('any')
url="https://pypi.python.org/pypi/nbformat"
license=('BSD')
depends=('python-traitlets' 'python-jupyter_core' 'python-jsonschema' 'python-fastjsonschema')
makedepends=('python-build' 'python-installer' 'python-hatch-nodejs-version')
checkdepends=('python-pytest' 'python-testpath' 'python-pep440')
source=(https://github.com/jupyter/nbformat/archive/$pkgver/nbformat-$pkgver.tar.gz)
sha256sums=('f47ee049abd3c58fd0c6d5612dfa971c16387bcf83c1d49055b6bca6d4ae49a8')

build() {
  cd nbformat-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

check() {
  cd nbformat-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd nbformat-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
