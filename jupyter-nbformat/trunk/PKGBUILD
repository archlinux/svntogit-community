# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbformat
pkgver=5.3.0
pkgrel=1
pkgdesc="The base implementation of the Jupyter Notebook format and Python APIs for working with notebooks"
arch=('any')
url="https://pypi.python.org/pypi/nbformat"
license=('BSD')
depends=('python-traitlets' 'python-jupyter_core' 'python-jsonschema')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-testpath' 'python-fastjsonschema')
source=(https://github.com/jupyter/nbformat/archive/$pkgver/nbformat-$pkgver.tar.gz)
sha256sums=('b5a31ef91530af8338406884f798400b9c64d8ff85db79b5c6e8d4971885dea8')

prepare() {
  cd nbformat-$pkgver
  sed -e 's|setuptools>=60.0|setuptools|' -i pyproject.toml
}

build() {
  cd nbformat-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbformat-$pkgver
  pytest
}

package() {
  cd nbformat-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
