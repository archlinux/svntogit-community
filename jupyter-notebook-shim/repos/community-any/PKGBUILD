# Maintainer: Antonio Rojas <arojas@archlinux.com>

_pyname=notebook_shim
pkgname=jupyter-notebook-shim
pkgver=0.2.0
pkgrel=1
pkgdesc='Provides a way for JupyterLab and other frontends to switch to Jupyter Server for their Python Web application backend'
arch=(any)
url='https://github.com/jupyterlab/notebook_shim'
license=(BSD)
depends=(jupyter-server)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest-tornasync)
source=(https://pypi.python.org/packages/source/n/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('fdb81febb05932c6d19e44e10382ce05469cac5e1b6e99b49be6159ddb5e4804')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver/$_pyname
  pytest
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
