# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-server
pkgver=1.12.0
pkgrel=1
pkgdesc='Provides the backend for Jupyter web applications like Jupyter notebook, JupyterLab, and Voila'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(python-jupyter_client jupyter-nbformat jupyter-nbconvert python-terminado python-prometheus_client python-traitlets python-tornado
         python-jinja python-send2trash python-anyio python-jupyter_packaging python-websocket-client python-requests-unixsocket)
makedepends=(python-setuptools)
checkdepends=(python-pytest-tornasync python-pytest-console-scripts)
optdepends=('jupyter-server-mathjax: to use local MathJax')
source=(https://pypi.io/packages/source/j/${pkgname/-/_}/${pkgname/-/_}-$pkgver.tar.gz)
sha256sums=('0d8ab06f34e1cad224373e3b127e07d9cbb48babd66286136fbd075ce509b6ff')

build() {
  cd ${pkgname/-/_}-$pkgver
  python setup.py build 
}

check() {
  cd ${pkgname/-/_}-$pkgver
  python setup.py install --root="$PWD"/tmp_install --optimize=1
  PYTHONPATH="$PWD"/`ls tmp_install/usr/lib/python*/site-packages`
  PATH="$PWD/tmp_install/usr/bin:$PATH"
  pytest jupyter_server
}

package() {
  cd ${pkgname/-/_}-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 COPYING.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
