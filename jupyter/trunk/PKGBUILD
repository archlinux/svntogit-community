# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=jupyter
pkgname=(jupyter ipython2-notebook)
pkgver=4.0.6
pkgrel=3
pkgdesc="A language-agnostic web-based interactive shell/notebook server."
arch=('any')
url="http://jupyter.org/"
license=('BSD')
depends=('ipython' 'python-setuptools'
         'python-jinja' 'python-pyzmq' 'python-tornado' 'python-jsonschema'
         'python-terminado' 'python-mistune' 'python-pygments')
makedepends=('python-setuptools' 'python2-setuptools' 'npm' 'git')
optdepends=('mathjax: latex equation rendering'
            'qt5-svg: jupyter-qtconsole'
            'python-pyqt5: jupyter-qtconsole'
            'python-sip: jupyter-qtconsole')

# rebundle all the parts that were split out
_ipykv=4.2.1  # ipykernel
_ipywv=4.1.1  # ipywidgets
_jnbcv=4.1.0  # notebook convert
_jnbfv=4.0.1  # notebook format
_jconv=4.0.3  # jupyter console
_jcliv=4.1.1  # jupyter client
_notev=4.0.6  # notebook
_qtcv=4.1.1   # qtconsole

source=("jupyter_core.tgz::https://github.com/jupyter/jupyter_core/archive/$pkgver.tar.gz"
        "ipykernel.tgz::https://github.com/ipython/ipykernel/archive/$_ipykv.tar.gz"
        "ipywidgets.tgz::https://github.com/ipython/ipywidgets/archive/$_ipywv.tar.gz"
        "nbconvert.tgz::https://github.com/jupyter/nbconvert/archive/$_jnbcv.tar.gz"
        "nbformat.tgz::https://github.com/jupyter/nbformat/archive/$_jnbfv.tar.gz"
        "jupyter_console.tgz::https://github.com/jupyter/jupyter_console/archive/$_jconv.tar.gz"
        "jupyter_client.tgz::https://github.com/jupyter/jupyter_client/archive/$_jcliv.tar.gz"
        "notebook.tgz::https://github.com/jupyter/notebook/archive/$_notev.tar.gz"
        "qtconsole.tgz::https://github.com/jupyter/qtconsole/archive/$_qtcv.tar.gz")
md5sums=('50a73c3a4a8ed047a3674d2b5274cc3b'
         'de583ee9c84db6296269ce7de0afb63f'
         '099e9b76c52839dc33604aad7a2f1a42'
         '06655576713ba1ff7cece2b92760c187'
         'ab7172e517c9d561c0c01eef5631b4c8'
         '0e928ea261e7f8154698cf69ed4f2459'
         '8436e4a3266a442f576cdfef39dc0e19'
         'd70d8a6d01893f4b64df9edbc0e13b52'
         '09edb4932e59a07f26ff1fd492c9f740')

# notebook optdeps ?
# 'haskell-pandoc: ipython notebook conversion'
# 'texlive-bin: notebook pdf export'

# qtconsole deps?
# python-pyqt4 python-sip python-pygments python-pyzmq
# is qt5 now?

prepare() {
  cd "$srcdir"
  cp -r jupyter_core-$pkgver python2-jupyter_core-$pkgver
  cp -r jupyter_client-$_jcliv python2-jupyter_client-$_jcliv
  cp -r ipykernel-$_ipykv    python2-ipykernel-$_ipykv
  cp -r qtconsole-$_qtcv     python2-qtconsole-$_qtcv
}

build() {
  cd "$srcdir"

  # FS45999 disable bundled mathjax
  cd "$srcdir/notebook-$_notev"
  sed -i 's/^.*MathJax.*$//' bower.json 
  #sed -i '/Trim mathjax/,/^                static_data.append/d' setupbase.py
  sed -i 's/mj(/#mj(/' setupbase.py
  # needs node, downloads a lot of stuff
  python3 setup.py build

  cd "$srcdir/ipywidgets-$_ipywv"
  # needs node, downloads a lot of stuff
  python3 setup.py build
}

package_jupyter() {
  provides=('ipython-notebook')
  #replaces=('ipython-notebook')

  cd "$srcdir/jupyter_core-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipykernel-$_ipykv"
  # this is in the ipython package
  #python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipywidgets-$_ipywv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/notebook-$_notev"
  # needs node
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  # FS45999 use system mathjax
  ln -s /usr/share/mathjax \
  "$pkgdir/usr/lib/python3.5/site-packages/notebook/static/components/MathJax"

  cd "$srcdir/nbconvert-$_jnbcv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/nbformat-$_jnbfv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_console-$_jconv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_client-$_jcliv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/qtconsole-$_qtcv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  cd examples
  # FS#47046 fix .desktop icon
  sed -i 's|^Icon=.*$|Icon=/usr/lib/python3.5/site-packages/qtconsole/resources/icon/JupyterConsole.svg|' \
    jupyter-qtconsole.desktop
  install -Dm644 jupyter-qtconsole.desktop \
    "$pkgdir/usr/share/applications/jupyter-qtconsole.desktop"

  cd "$srcdir"
  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_ipython2-notebook() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('ipython2' 'jupyter' 'python2-pyzmq')
  optdepends=('python2-pyqt5: jupyter-qtconsole-py2'
              'qt5-svg: jupyter-qtconsole-py2'
              'python2-sip: jupyter-qtconsole-py2')

  cd "$srcdir/python2-ipykernel-$_ipykv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_core-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_client-$_jcliv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-qtconsole-$_qtcv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$pkgdir/usr/bin"
  rm jupyter
  rm jupyter-kernelspec
  rm jupyter-migrate
  mv jupyter-qtconsole jupyter-qtconsole-py2

  cd "$srcdir"
  install -d "$pkgdir/usr/share/jupyter/kernels/python2"
  echo '{"display_name": "Python 2", "language": "python2", "argv": ["python2", "-c", "from IPython.kernel.zmq.kernelapp import main; main()", "-f", "{connection_file}"],"codemirror_mode": {"version": 2, "name": "ipython2"}}' > "$pkgdir/usr/share/jupyter/kernels/python2/kernel.json"

  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

