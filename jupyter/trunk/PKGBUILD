# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=jupyter
pkgname=(jupyter ipython2-notebook)
pkgver=4.1.0
pkgrel=2
pkgdesc="A language-agnostic web-based interactive shell/notebook server."
arch=('any')
url="http://jupyter.org/"
license=('BSD')
depends=('ipython' 'python-ipykernel' 'python-setuptools'
         'python-jinja' 'python-pyzmq' 'python-jsonschema'
         'python-mistune' 'python-pygments')
makedepends=('python-setuptools' 'python2-setuptools' 'npm' 'git')
optdepends=('pandoc: notebook conversion'
            'qt5-svg: jupyter-qtconsole'
            'python-pyqt5: jupyter-qtconsole'
            'python-sip: jupyter-qtconsole')

# todo: figure out which deps left with jupyter-notebook

# rebundle all the parts that were split out
# but split them out officially over time
_ipywv=4.1.1  # ipywidgets
_jnbcv=4.1.0  # notebook convert
_jnbfv=4.0.1  # notebook format
_jconv=4.1.1  # jupyter console
_jcliv=4.2.2  # jupyter client
_qtcv=4.2.1   # qtconsole

# future order of splits:
# python{,2}-jupyter_core
# python{,2}-jupyter_client
# python-ipywidgets

source=("jupyter_core-$pkgver.tgz::https://github.com/jupyter/jupyter_core/archive/$pkgver.tar.gz"
        "ipywidgets-$_ipywv.tgz::https://github.com/ipython/ipywidgets/archive/$_ipywv.tar.gz"
        "nbconvert-$_jnbcv.tgz::https://github.com/jupyter/nbconvert/archive/$_jnbcv.tar.gz"
        "nbformat-$_jnbfv.tgz::https://github.com/jupyter/nbformat/archive/$_jnbfv.tar.gz"
        "jupyter_console-$_jconv.tgz::https://github.com/jupyter/jupyter_console/archive/$_jconv.tar.gz"
        "jupyter_client-$_jcliv.tgz::https://github.com/jupyter/jupyter_client/archive/$_jcliv.tar.gz"
        "qtconsole-$_qtcv.tgz::https://github.com/jupyter/qtconsole/archive/$_qtcv.tar.gz")
md5sums=('b7e928f965f68aef13fea1bf9d6384aa'
         '099e9b76c52839dc33604aad7a2f1a42'
         '06655576713ba1ff7cece2b92760c187'
         'ab7172e517c9d561c0c01eef5631b4c8'
         'a8b077ae0a5c57e9518ac039ad5febb8'
         '988ea87554215a83c6ad52e554d8d8c4'
         '97a3e29cd69bc0270411b313c907c173')

# qtconsole deps?
# python-pyqt5 python-sip python-pygments python-pyzmq

prepare() {
  cd "$srcdir"
  cp -r jupyter_core-$pkgver   python2-jupyter_core-$pkgver
  cp -r jupyter_client-$_jcliv python2-jupyter_client-$_jcliv
  cp -r qtconsole-$_qtcv       python2-qtconsole-$_qtcv
}

build() {
  cd "$srcdir/ipywidgets-$_ipywv"
  # needs node, downloads a lot of stuff
  python3 setup.py build
}

package_jupyter() {
  cd "$srcdir/jupyter_core-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/ipywidgets-$_ipywv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/nbconvert-$_jnbcv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/nbformat-$_jnbfv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_console-$_jconv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/jupyter_client-$_jcliv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/qtconsole-$_qtcv"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  cd examples
  # FS#47046 fix .desktop icon
  sed -i 's|^Icon=.*$|Icon=/usr/lib/python3.5/site-packages/qtconsole/resources/icon/JupyterConsole.svg|' \
    jupyter-qtconsole.desktop
  install -Dm644 jupyter-qtconsole.desktop \
    "$pkgdir/usr/share/applications/jupyter-qtconsole.desktop"

  cd "$srcdir"
  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_ipython2-notebook() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('ipython2' 'python2-ipykernel' 'python2-pyzmq')
  optdepends=('python2-pyqt5: jupyter-qtconsole-py2'
              'qt5-svg: jupyter-qtconsole-py2'
              'python2-sip: jupyter-qtconsole-py2')

  cd "$srcdir/python2-jupyter_core-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-jupyter_client-$_jcliv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-qtconsole-$_qtcv"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$pkgdir/usr/bin"
  rm jupyter
  rm jupyter-kernelspec
  rm jupyter-migrate
  mv jupyter-qtconsole jupyter-qtconsole-py2

  cd "$srcdir"
  install -Dm644 "jupyter_core-$pkgver/COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

