# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter_console
pkgver=6.4.4
pkgrel=1
pkgdesc="An IPython-like terminal frontend for Jupyter kernels in any language."
arch=('any')
url="https://pypi.org/project/jupyter_console/"
license=('BSD')
depends=('ipython' 'python-jupyter_client' 'python-ipykernel'
         'python-pygments' 'python-prompt_toolkit')
makedepends=('python-build' 'python-installer' 'python-wheel')
#source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/jupyter_console/archive/$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/j/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('a5e0426e44738c96c0a63663cdc0cfb1')

build() {
  cd "$srcdir/jupyter_console-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/jupyter_console-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
