# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Guillaume Duboc <guilduboc@gmail.com>

pkgname=jupyterlab_server
pkgver=0.2.0
pkgrel=1
pkgdesc="Launch an application built using JupyterLab"
arch=(any)
url="https://jupyter.org/"
license=(custom)
depends=(jupyter-notebook)
makedepends=(python-setuptools)
source=(https://pypi.io/packages/source/j/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('72d916a73957a880cdb885def6d8664a6d1b2760ef5dca5ad665aa1e8d1bb783')

build() {
  cd $pkgname-$pkgver
  python setup.py build 
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
