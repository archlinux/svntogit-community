# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=jwm
pkgver=2.4.0
pkgrel=1
pkgdesc="A lightweight window manager for the X11 Window System"
arch=('x86_64')
url="http://joewing.net/projects/jwm/"
license=('MIT')
depends=('libx11' 'libxft' 'libjpeg-turbo' 'libxpm' 'libxinerama' 'libpng' 'cairo' 'librsvg')
backup=('etc/system.jwmrc')
source=(http://joewing.net/projects/jwm/releases/jwm-$pkgver.tar.xz
        jwm.desktop)
# contacted upstream about desktop
# no reply
sha512sums=('3e91aed42eaabb6df508c2b71c78bbc3e7391ea843fba42f565a4d163df55147ddacbb8567d6e21d49a71d7d79d9b58bcd9979b2dee83ba3c1e65a789e5cec41'
            '1014988cc17a439ad3ae36a1e137ad8c9c46b2afbe6966c68ecc3258a0320fbad96c562fb39bdc41db624ebddaa45617938b621b489f3a740bda11a40f4f7718')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|/usr/local/share/|/usr/share/|' contrib/Makefile po/Makefile* example.jwmrc
  # locales were broken in 2.4.0
  sed -i 's|mkdir_p = @mkdir_p@|mkdir_p = /usr/bin/mkdir -p --|' po/Makefile*
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-fribidi 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make BINDIR="$pkgdir/usr/bin" MANDIR="$pkgdir/usr/share/man" \
       DESTDIR="$pkgdir" SYSCONF="$pkgdir/etc" install
  install -Dm644 "$srcdir/jwm.desktop" "$pkgdir/usr/share/xsessions/jwm.desktop"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
