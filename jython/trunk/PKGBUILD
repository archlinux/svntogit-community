# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
pkgname=jython
pkgver=2.5.1
pkgrel=2
pkgdesc="An implementation of the Python language written in Java"
arch=('any')
url="http://www.jython.org/"
license=('PSF' 'APACHE' 'custom')
depends=('java-runtime' 'bash')
options=('!emptydirs')
install='jython.install'
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}_installer-${pkgver}.jar
        'README.ArchLinux')
md5sums=('2ee978eff4306b23753b3fe9d7af5b37'
         '0a2e265af61c7d695e13b605dc180553')
noextract=(${pkgname}_installer-${pkgver}.jar)
build() {
	cd "$srcdir"
	java -jar ${pkgname}_installer-${pkgver}.jar -s -t standard -d "$pkgdir"/opt/jython
	sed -i s*"${pkgdir}"**g "${pkgdir}"/opt/jython/jython
	# Get rid of hard-coded JAVA_HOME, since all the JDKs don't use
	# the same directory.
	sed -i '/^JAVA_HOME=.*$/d' "${pkgdir}"/opt/jython/jython
	install -m755 -D "$pkgdir/opt/jython/jython" "$pkgdir/usr/bin/jython" \
	    || return 1
	install -m644 -D "$srcdir/README.ArchLinux" \
	    "$pkgdir/usr/share/doc/$pkgname/README.ArchLinux" || return 1
	rm "$pkgdir/opt/jython/jython" "$pkgdir/opt/jython/bin/jython" || return 1
	install -m644 -D "$pkgdir/opt/jython/LICENSE.txt" \
	    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt" || return 1
}
