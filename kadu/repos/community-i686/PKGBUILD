# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Mateusz Herych
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=0.12.3
pkgrel=4
pkgdesc='Qt-based Jabber/XMPP and Gadu-Gadu client'
arch=('i686' 'x86_64')
url='http://www.kadu.net/'
license=('GPL')
depends=('libgadu' 'libxss' 'enchant' 'phonon' 'qca-ossl' 'libidn'
         'libmpdclient' 'qtwebkit' 'xdg-utils')
makedepends=('cmake' 'libao' 'libsndfile' 'libxtst' 'curl' 'optipng')
install=kadu.install
source=(http://download.kadu.im/stable/$pkgname-$pkgver.tar.bz2
        kadu-0.12.3-QT4_CREATE_MOC_COMMAND.patch)
md5sums=('c5d6f0bd384cd96090819ea018eb8ad8'
         '76e53a1f77699ba81c076f9254bb28c5')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../kadu-0.12.3-QT4_CREATE_MOC_COMMAND.patch
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" LIBDIR=/usr/lib install

  mv "$pkgdir"/usr/sdk "$pkgdir"/usr/share/kadu/sdk
  rm -rf "$pkgdir"/usr/{lib,include}/{libgadu*,pkgconfig}
}
