# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Mateusz Herych
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=3.0
pkgrel=2
pkgdesc='Qt-based Jabber/XMPP and Gadu-Gadu client'
arch=('i686' 'x86_64')
url='http://www.kadu.net/'
license=('GPL')
depends=('libgadu' 'libxss' 'enchant' 'phonon-qt5' 'qca-qt5'
         'libmpdclient' 'libotr' 'libarchive' 'injeqt' 'xdg-utils' 'qt5-multimedia'
         'qt5-x11extras' 'qt5-svg' 'qt5-script' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('cmake' 'libao' 'libsndfile' 'libxtst' 'curl' 'chrpath' 'ninja'
             'qt5-tools' 'qxmpp')
optdepends=('qxmpp: Jabber protocol')
source=(http://download.kadu.im/stable/$pkgname-$pkgver.tar.bz2)
md5sums=('9d42c16682b338c14e6a912efd739447')

prepare() {
  sed -i 's/unity_integration//g' $pkgname-$pkgver/Plugins.cmake
  sed -i 's/indicator_docking//g' $pkgname-$pkgver/Plugins.cmake
}

build() {
  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  ninja
}

package() {
  DESTDIR="$pkgdir" LIBDIR=/usr/lib ninja -C build install
  chrpath -d "$pkgdir"/usr/lib/kadu/plugins/*.so
}
