# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Mateusz Herych
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=1.0
pkgrel=1
pkgdesc='Qt-based Jabber/XMPP and Gadu-Gadu client'
arch=('i686' 'x86_64')
url='http://www.kadu.net/'
license=('GPL')
depends=('libgadu' 'libxss' 'enchant' 'phonon' 'qca-ossl' 'libidn'
         'libmpdclient' 'qtwebkit' 'xdg-utils' 'libotr' 'libarchive')
makedepends=('cmake' 'libao' 'libsndfile' 'libxtst' 'curl' 'chrpath')
install=kadu.install
source=(http://download.kadu.im/stable/$pkgname-$pkgver.tar.bz2)
md5sums=('a91c00707d73e0ba7fe8245885dd59a5')

build() {
  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  make -C build DESTDIR="$pkgdir" LIBDIR=/usr/lib install
  chrpath -d "$pkgdir"/usr/lib/kadu/plugins/*.so
}
