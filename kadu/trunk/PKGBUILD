# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=0.6.5.3
pkgrel=1.1
pkgdesc="A Qt-based Gadu-Gadu client"
arch=('i686' 'x86_64')
url="http://www.kadu.net/"
license=('GPL')
depends=('qt' 'libgadu' 'hicolor-icon-theme')
makedepends=('cmake' 'aspell' 'libao' 'libsndfile' 'libxtst' 'curl' 'xmms' 'qca' 'libxss')
conflicts=('kadu-svn')
install=$pkgname.install
source=(http://www.kadu.net/download/stable/$pkgname-$pkgver.tar.bz2
        mod-weather.diff)
md5sums=('7914d941020b0fa092748ffe0295174f'
         'e35a10647a48d3cff611ae6b20ebef07')

_static="sound_.+ icons_.+ emoticons_.+"
_disable="module_macosx_docking module_growl_notify module_dshow_sound module_win32_sound module_itunes_mediaplayer module_winamp_mediaplayer"

build() {
  cd $srcdir/kadu

  # kadu.net tends to get sluggish
  sed -r -i 's/-T [0-9]+/-T 60/g' varia/scripts/autodownload

  sed -r -i 's/(^[0-9a-z_]+=)[yn]$/\1m/' .config
  for m in $_static; do
    sed -r -i "s/^($m)=m/\1=y/" .config || return 1
  done
  for m in $_disable; do
    sed -r -i "s/^($m)=m/\1=n/" .config || return 1
  done

  for p in $startdir/src/mod-*.diff; do 
    m=`basename $p .diff | sed 's/mod-\(.*\)/\1/'`
    mkdir -p modules/patches/$m
    cp $p modules/patches/$m
  done

  sed 's#set (LIBDIR ${CMAKE_INSTALL_PREFIX}/lib${LIB_SUFFIX})#set (LIBDIR ${CMAKE_INSTALL_PREFIX}/lib)#' -i CMakeLists.txt
  sed 's#set (MODULEDIR lib${LIB_SUFFIX}/kadu/modules)#set (MODULEDIR lib/kadu/modules)#' -i CMakeLists.txt
 
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  rm -rf $pkgdir/usr/{lib,include}/{libgadu*,pkgconfig}
  rm -rf $pkgdir/usr/share/kadu/{HISTORY,README}
}
