# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Mateusz Herych
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=1.5
pkgrel=1
pkgdesc='Qt-based Jabber/XMPP and Gadu-Gadu client'
arch=('i686' 'x86_64')
url='http://www.kadu.net/'
license=('GPL')
depends=('libgadu' 'libxss' 'enchant' 'phonon' 'qca-ossl' 'libidn'
         'libmpdclient' 'qtwebkit' 'xdg-utils' 'libotr' 'libarchive')
makedepends=('cmake' 'libao' 'libsndfile' 'libxtst' 'curl' 'chrpath' 'ninja')
install=kadu.install
source=(http://download.kadu.im/stable/$pkgname-$pkgver.tar.bz2
        kadu-1.5-Rename-INSTALL-PREFIX-to-KADU_INSTALL_PREFIX.patch)
md5sums=('bfc653bc3a5b542877a299209db6adaf'
         'd706ee1839c629bc8cb06153211b2630')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../kadu-1.5-Rename-INSTALL-PREFIX-to-KADU_INSTALL_PREFIX.patch
}

build() {
  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  ninja
}

package() {
  DESTDIR="$pkgdir" LIBDIR=/usr/lib ninja -C build install
  chrpath -d "$pkgdir"/usr/lib/kadu/plugins/*.so
}
