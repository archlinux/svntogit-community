# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Mateusz Herych
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=0.12.3
pkgrel=3
pkgdesc='Qt-based Jabber/XMPP and Gadu-Gadu client'
arch=('i686' 'x86_64')
url='http://www.kadu.net/'
license=('GPL')
depends=('libgadu' 'libxss' 'enchant' 'phonon' 'qca-ossl'
         'libidn' 'libmpdclient' 'qtwebkit' 'xdg-utils')
makedepends=('cmake' 'libao' 'libsndfile' 'libxtst' 'curl' 'optipng')
install=kadu.install
source=(http://download.kadu.im/stable/$pkgname-$pkgver.tar.bz2)
sha256sums=('d607bbd0d00b01bfb70dd15f15c8be4076896a935041651e67a3887ad12ab8a8')

prepare() {
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir LIBDIR=/usr/lib install

  mv $pkgdir/usr/sdk $pkgdir/usr/share/kadu/sdk
  rm -rf $pkgdir/usr/{lib,include}/{libgadu*,pkgconfig}
}
