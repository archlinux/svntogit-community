# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Mateusz Herych
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=kadu
pkgver=2.0
pkgrel=2
pkgdesc='Qt-based Jabber/XMPP and Gadu-Gadu client'
arch=('i686' 'x86_64')
url='http://www.kadu.net/'
license=('GPL')
depends=('libgadu' 'libxss' 'enchant' 'phonon-qt5' 'qca-ossl' 'qca-qt5' 'libidn'
         'libmpdclient' 'libotr' 'libarchive' 'injeqt' 'xdg-utils' 'qt5-multimedia'
         'qt5-quick1' 'qt5-declarative' 'qt5-webkit' 'qt5-x11extras' 'qt5-svg')
makedepends=('cmake' 'libao' 'libsndfile' 'libxtst' 'curl' 'chrpath' 'ninja'
             'qt5-tools')
install=kadu.install
source=(http://download.kadu.im/stable/$pkgname-$pkgver.tar.bz2)
md5sums=('7239fab871f32082a7cf27433219b08b')

prepare() {
  sed -i 's/unity_integration//g' $pkgname-$pkgver/Plugins.cmake
  sed -i 's/indicator_docking//g' $pkgname-$pkgver/Plugins.cmake
}

build() {
  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  ninja
}

package() {
  DESTDIR="$pkgdir" LIBDIR=/usr/lib ninja -C build install
  chrpath -d "$pkgdir"/usr/lib/kadu/plugins/*.so
}
