# $Id$
# Maintainer: 
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=1.3.1
pkgrel=2
pkgdesc='KDE media player'
license=(GPL)
arch=(i686 x86_64)
url="http://kaffeine.kde.org"
depends=(kdebase-runtime vlc)
makedepends=(cmake automoc4)
install=kaffeine.install
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz
        include-CheckIncludeFiles.patch)
md5sums=('f2308cf7abf5ec7ad5c724593594c198'
         '1deeb1bff0e9ed705a9fc0552487ae2c')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../include-CheckIncludeFiles.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
