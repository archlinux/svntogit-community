# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=2.0.14
pkgrel=1
pkgdesc='KDE media player'
license=(GPL)
arch=(x86_64)
url="https://kaffeine.kde.org"
depends=(kio libxss vlc)
makedepends=(extra-cmake-modules kidletime kdoctools)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2c73fd473f0579cffa7e797e3d00c41b09d7fb4dee486bb507877a56bd1d7f3a'
            'SKIP')
validpgpkeys=(F909AE68FC11DF09C1755C00085F3EBD8EE4E115) # Mauro Carvalho Chehab <mchehab@s-opensource.com>

prepare() {
  mkdir -p build

  rm -r $pkgname-$pkgver/po/pt_BR/docs # Doesn't build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
