# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: said
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=kamoso
pkgver=3.1.0
pkgrel=3
pkgdesc="A webcam recorder from KDE community"
url="https://userbase.kde.org/Kamoso"
arch=(i686 x86_64)
license=(GPL)
depends=(qt5-gstreamer purpose)
makedepends=(extra-cmake-modules kdoctools boost python)
source=("http://download.kde.org/stable/$pkgname/${pkgver%.*}/src/$pkgname-$pkgver.tar.xz")
md5sums=('923af7f7e13caec1082efd9a04670409')

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}

