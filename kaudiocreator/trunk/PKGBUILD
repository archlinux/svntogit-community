# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=kaudiocreator
pkgver=3.92.0+295+ge64e16a
pkgrel=1
pkgdesc="A program for ripping and encoding Audio-CDs, encoding files from disk"
arch=('x86_64')
url="https://www.kde.org/applications/multimedia/kaudiocreator/"
license=('GPL')
depends=('audiocd-kio' 'taglib' 'libdiscid' 'kcmutils' 'knotifyconfig' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'git' 'kdesignerplugin' 'kdoctools')
_commit=e64e16a745417f76455c3c63e2d3a05857a8e52e  # master
source=("git://git.kde.org/kaudiocreator.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g' -e 's/v//'
}

build() {
  mkdir build
  cd build

  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
