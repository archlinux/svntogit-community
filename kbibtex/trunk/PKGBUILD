# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
pkgver=0.6.2
pkgrel=1
pkgdesc="A BibTeX editor for KDE"
arch=('i686' 'x86_64')
url='http://home.gna.org/kbibtex/'
license=('GPL')
depends=('kdebase-runtime' 'poppler-qt4')
optdepends=('okular: Document preview')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/KBibTeX/$pkgname-$pkgver.tar.xz"{,.asc} kbibtex-no-qtwebkit.patch)
sha256sums=('d99e419aec1d3af0a57eba6d7498de4e6be25ddbd5a5e56fc9de09e7e17dcdf3'
            'SKIP'
            '8abf53b295c2271f90e673ebc4dff88283732e08e138afdd0de90b145aa8f328')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

prepare() {
  cd $pkgname-$pkgver

# Disable insecure qtwebkit
  patch -p1 -i ../kbibtex-no-qtwebkit.patch
}

build() {
  cd "$pkgname-$pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
