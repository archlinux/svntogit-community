# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kcheckers
pkgver=0.8.1
pkgrel=8
pkgdesc="Qt4-based checkers boardgame"
arch=('i686' 'x86_64')
url="http://qcheckers.sourceforge.net/"
license=('GPL')
depends=('qt4')
source=(http://downloads.sourceforge.net/qcheckers/${pkgname}-${pkgver}.tar.gz kcheckers.desktop)
md5sums=('793736a007cd92122dfd91bcfd14ab69'
         '25b2605f4d605615e265150b722f1897')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/usr/local|/usr|' common.h
  qmake kcheckers.pro
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}/usr" install
  install -Dm644 "${srcdir}/kcheckers.desktop" "${pkgdir}/usr/share/applications/kcheckers.desktop"
}
