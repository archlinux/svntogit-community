# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=kcm-fcitx5
pkgver=0.0.0.20200419
_commit=c932375a19e75ff4e81fe95669c14eacd15a2fad
pkgrel=1
pkgdesc="KDE Config Module for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/kcm-fcitx5"
license=('GPL')
depends=('fcitx5-qt' 'kdeclarative' 'kitemviews' 'kirigami2' 'kpackage')
makedepends=('extra-cmake-modules' 'python')
source=("https://github.com/fcitx/kcm-fcitx5/archive/$_commit/kcm-fcitx5-$_commit.tar.gz")
sha512sums=('86db76764439a297d4a07b1c17625f82b0b008b324890c71f1c95759598cbe120394d3d523075a0f5630b0ab7e181cb0c94ac07a8aed562093de8e01d4dc87ac')

build(){
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
