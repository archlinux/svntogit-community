# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: A Rojas < nqn1976 @ gmail.com >
# Contributor: Alexander Mezin <mezin.alexander@gmail.com>

pkgname=kcm-touchpad
pkgver=1.1
pkgrel=1
pkgdesc='KCM, daemon and applet for touchpad'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/utils/kcm-touchpad'
license=('GPL')
depends=('kdebase-workspace' 'xf86-input-synaptics')
makedepends=('cmake' 'automoc4' 'xorg-server-devel')
install="${pkgname}.install"
source=("https://github.com/sanya-m/kde-touchpad-config/archive/v${pkgver}.tar.gz")
md5sums=('e8905e7888f3a6f002338fdf1417a5b5')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kde-touchpad-config-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
