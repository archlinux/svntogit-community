# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dany Martineau <dany.luc.martineau at gmail.com>
# Contributor: Dylon Edwards <deltaecho@archlinux.us>
pkgname=kcm-wacomtablet
pkgver=3.1.1
pkgrel=1
epoch=1
pkgdesc="KDE GUI for the Wacom Linux Drivers"
arch=('i686' 'x86_64')
url="https://cgit.kde.org/wacomtablet.git"
license=('GPL')
depends=('plasma-framework' 'xf86-input-wacom')
makedepends=('git' 'cmake' 'extra-cmake-modules' 'kdoctools')
source=("https://download.kde.org/stable/wacomtablet/$pkgver/wacomtablet-$pkgver.tar.xz"{,.sig}
        "add_missing_includes.diff")
sha256sums=('b328a56700f707694d879a69c7626dcfbe04c18be10bdde77c6a71770729678b'
            'SKIP'
            '143b2e81e5152c2f1db6e03089b32d9b9fdf1e61739743179b0efb299eb5b44d')
validpgpkeys=('E6E4C916590026E45836A12EA8896AFDFEF5DF53') # Valeriy Malov

prepare() {
  cd "$srcdir/wacomtablet-$pkgver"
  patch -Np1 < "$srcdir/add_missing_includes.diff"
}

build() {
  cd "$srcdir/wacomtablet-$pkgver"

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/wacomtablet-$pkgver"

  make DESTDIR="$pkgdir" install
}

