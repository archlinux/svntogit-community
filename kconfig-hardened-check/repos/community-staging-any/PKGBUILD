# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=kconfig-hardened-check
pkgver=0.6.1
pkgrel=2
pkgdesc='Tool for checking the security hardening options of the Linux kernel'
url="https://github.com/a13xp0p0v/kconfig-hardened-check"
arch=(any)
license=(GPL3)
depends=(python)
makedepends=(python-setuptools python-build python-installer python-wheel)
source=(https://github.com/a13xp0p0v/kconfig-hardened-check/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('dd5409b865d062a19e225a60c40df03c02a3dee75220e954029e3ba601406f65')
b2sums=('6d464cdfb604cb8f00399c3d2a1285f2c149832e4f51e4be2b8c2e567137dce6848831adebbf8d1593013fabdcf3fd3bc060024c2c7210f620639cd84fd5816e')

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 README.md -t "$pkgdir/usr/share/docs/$pkgname"
}
