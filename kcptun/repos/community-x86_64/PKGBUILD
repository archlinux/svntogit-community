# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=kcptun
pkgver=20170322
pkgrel=1
pkgdesc="A Secure Tunnel Based On KCP with N:M Multiplexing"
arch=('i686' 'x86_64')
url="https://github.com/xtaci/kcptun"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xtaci/kcptun/archive/v$pkgver.tar.gz"
        'kcptun@.service' 'kcptun-server@.service')
sha512sums=('af1a9cedfa4537919ffd228d7c9213bca4d87f14ece6debcbd3687f3090e162046c6a7dba5dddc0f139dded68cc55669c1817734f174dd47c8d11b05342f53f0'
            '6fbe0bd687ec9927fe46484f97a0e1a7605c74af673d2a2257a41819bcbf8b704c1ab8b063993251fc3803fa3caf38b7bfc3cededbd7fe1b57c5e2d234c2e6f6'
            '5526608f2f5056462813812436605d537a5da950d7d2b5b93dab1da443b25ebf15f69609d18ba1c318b0cc75fcc2a8e14f0d33ff7765035063dd89c041b1bd11')

prepare() {
  cd kcptun-$pkgver

  mkdir build
  mkdir -p .gopath/src/github.com/xtaci
  ln -sf "$PWD" .gopath/src/github.com/xtaci/kcptun
  export GOPATH="$PWD/.gopath"

  go get golang.org/x/crypto/pbkdf2 github.com/xtaci/smux github.com/xtaci/kcp-go \
         github.com/urfave/cli github.com/pkg/errors github.com/golang/snappy
}

build() {
  cd "$srcdir"/kcptun-$pkgver/client
  go build -o build/kcptun-client -ldflags "-X main.VERSION=$pkgver" # -gccgoflags "$CFLAGS $LDFLAGS"

  cd "$srcdir"/kcptun-$pkgver/server
  go build -o build/kcptun-server -ldflags "-X main.VERSION=$pkgver" # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd kcptun-$pkgver

  install -Dm755 client/build/kcptun-client "$pkgdir"/usr/bin/kcptun-client
  install -Dm755 server/build/kcptun-server "$pkgdir"/usr/bin/kcptun-server

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md

  install -dm755 "$pkgdir"/etc/kcptun
  install -Dm644 "$srcdir/kcptun@.service" "$pkgdir"/usr/lib/systemd/system/kcptun@.service
  install -Dm644 "$srcdir/kcptun-server@.service" "$pkgdir"/usr/lib/systemd/system/kcptun-server@.service
}
