# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Georg Grabler <ggrabler@chello.at>

pkgname=kdbg
pkgver=2.5.5
pkgrel=3
pkgdesc="A gdb GUI for KDE"
arch=('i686' 'x86_64')
url="http://www.kdbg.org"
license=('GPL')
depends=('kdebase-runtime' 'gdb')
makedepends=('automoc4' 'cmake' 'optipng')
source=("http://downloads.sourceforge.net/project/kdbg/Source%20Code/$pkgver/kdbg-$pkgver.tar.gz" kdbg-cmake-3.4.patch)
md5sums=('3c4271fb0c3be75d658a94c3ea6c6167'
         '23ad6b2600e835ed7985479c4ec4c433')

prepare() {
  cd $pkgname-$pkgver
# Fix build with cmake 3.4
  patch -p1 -i ../kdbg-cmake-3.4.patch
}

build() {
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} -DCMAKE_INSTALL_PREFIX=/usr
  make
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
