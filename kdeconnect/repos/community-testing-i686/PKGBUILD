# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=kdeconnect
pkgver=0.7.2
pkgrel=2
pkgdesc='Adds communication between KDE and your smartphone'
url='https://projects.kde.org/projects/playground/base/kdeconnect-kde'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdelibs' 'qjson' 'qca-ossl')
makedepends=('automoc4' 'cmake')
optdepends=('sshfs: remote filesystem browser')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgname}/${pkgver}/src/${pkgname}-kde-${pkgver}.tar.xz")
md5sums=('1cd10d54bb3dc6e5b854ec110f63cecb')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-kde-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
