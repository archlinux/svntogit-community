# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=kdeconnect
pkgver=1.3.5
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
url='https://community.kde.org/KDEConnect'
license=(GPL)
arch=(x86_64)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca)
makedepends=(extra-cmake-modules kdoctools qt5-declarative)
optdepends=('sshfs: remote filesystem browser' 'kde-cli-tools: configuration UI'
            'qt5-declarative: QML bindings' 'python-nautilus: Nautilus integration')
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-kde-$pkgver.tar.xz"{,.sig})
sha256sums=('e5ce7084427a831e6506494b656af9c1dd7bf29b7458c2546baf0a5d7a1f990a'
            'SKIP')
validpgpkeys=(B3487CA0FF36258E2A2F338BD33E721DF877EFE8  # Albert Vaca Cintora <albertvaka@gmail.com>
              F53223F3337665C2CCEA122E873AC3459BBCF5C0) # Nicolas Fella <nicolas.fella@kdab.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-kde-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
