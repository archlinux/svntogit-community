# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=kdeconnect
pkgver=0.5.1
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
url='https://projects.kde.org/projects/playground/base/kdeconnect-kde'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdelibs' 'qjson' 'qca-ossl')
makedepends=('automoc4' 'cmake')
#optdepends=('sshfs: remote filesystem browser')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgname}/${pkgver}/src/${pkgname}-kde-${pkgver}.tar.xz")
md5sums=('0b85af9a0cd7e3feacffa2c0210dc1ec')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-kde-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
