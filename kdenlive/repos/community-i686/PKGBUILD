# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=kdenlive
pkgver=0.9.6
pkgrel=1
pkgdesc="A non-linear video editor for Linux"
arch=('i686' 'x86_64')
# http://download.kde.org/stable/kdenlive/
url="http://www.kdenlive.org/"
license=('GPL')
depends=('kdebase-runtime' 'mlt' 'qjson' 'libqzeitgeist')
makedepends=('automoc4' 'cmake' 'gettext' 'mesa' 'glu')
optdepends=('recordmydesktop: screen capture'
	    'dvdauthor: dvd creation'
	    'dvgrab: firewire capture'
	    'xine-ui: dvd preview'
	    'cdrkit: dvd iso image creation')
install=kdenlive.install
options=('docs')
source=(http://download.kde.org/stable/kdenlive/$pkgver/src/kdenlive-$pkgver.tar.bz2)
md5sums=('3bd3a511b860490acc367e7c9d7ffa43')

build() {
  export LDFLAGS="$LDFLAGS -lX11"
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kdenlive-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=/usr/lib \
    -DLINK_LIBRARIES=-lX11
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
