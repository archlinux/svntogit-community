# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=kdenlive
pkgver=0.9
pkgrel=1
pkgdesc="A non-linear video editor for Linux"
arch=('i686' 'x86_64')
url="http://www.kdenlive.org/"
license=('GPL')
depends=('kdebase-runtime' 'mlt' 'dvgrab' 'qjson' 'qimageblitz'
	 'libqzeitgeist')
makedepends=('automoc4' 'cmake' 'gettext' 'mesa')
optdepends=('recordmydesktop'
	    'dvdauthor'
	    'mkisolinux'
	    'xine-ui')
install=kdenlive.install
options=('docs')
source=(http://download.kde.org/stable/kdenlive/$pkgver/src/kdenlive-$pkgver.tar.gz
	glu.patch)
md5sums=('8ceb59444df9aa950fd6bc7300c4a90d'
         'fd6c5fe46419ee505c562363d41f6e9f')

build() {
  export LDFLAGS="$LDFLAGS -lX11"
  cd "${srcdir}"
  mkdir -p build
  cd build
  (cd ../kdenlive-${pkgver} && patch -p1 <$srcdir/glu.patch)
  cmake ../kdenlive-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=/usr/lib \
    -DLINK_LIBRARIES=-lX11
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
