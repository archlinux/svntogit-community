# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=kdenlive
pkgver=0.8
pkgrel=3
pkgdesc="A non-linear video editor for Linux"
arch=('i686' 'x86_64')
url="http://www.kdenlive.org/"
license=('GPL')
depends=('kdebase-runtime' 'mlt' 'dvgrab' 'qjson' 'qimageblitz')
makedepends=('automoc4' 'cmake' 'gettext')
install=kdenlive.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('26170b4360285e8a7bf53fd122d8854e')

build() {
  export LDFLAGS="$LDFLAGS -lX11"

  cd "${srcdir}"
  mkdir build || true
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=/usr/lib \
    -DLINK_LIBRARIES=-lX11
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
