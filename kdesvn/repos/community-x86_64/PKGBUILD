# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=kdesvn
pkgver=1.6.0
pkgrel=5
pkgdesc="A pure C++ SVN-client for KDE"
arch=('i686' 'x86_64')
url="http://kdesvn.alwins-world.de/"
license=('GPL')
depends=('kdebase-runtime' 'subversion')
makedepends=('automoc4' 'cmake' 'patch')
install=$pkgname.install
changelog=$pkgname.changelog
source=(https://launchpad.net/ubuntu/+archive/primary/+files/${pkgname}_${pkgver}.orig.tar.bz2 kdesvn-cmake.patch)
sha256sums=('05686f246b76810f90a9f80f1d45cdccf812b42e18fa10a16b14e7d27d370f96'
            '2e0b5b67c5bd7e282968766e00bc81ff41680c69fae217e69a794106a065c122')

prepare() {
  cd $pkgname-$pkgver
# Fix build failure
  patch -p1 -i ../kdesvn-cmake.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-DQT_THREAD_SUPPORT" \
    -DLIB_SUFFIX=

#FS#44135 fix
  sed -i 's|/share/locale|/share/locale/kde4|' po/cmake_install.cmake

  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#removing conflicting files - kdesdk
  rm -f "${pkgdir}"/usr/share/kde4/services/svn*

#removing $srcdir reference
  rm -rf "${pkgdir}"/usr/share/doc
}
