# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# AUR Maintainer: Gabriel Rauter <rauter.gabriel@gmail.com>

arch=('x86_64')
_pkgname=KDiskMark
pkgname=kdiskmark
pkgver=2.3.0
pkgrel=2
pkgdesc="HDD and SSD benchmark tool with a very friendly graphical user interface"
url="https://github.com/JonMagon/${_pkgname}"
license=(GPL3)
depends=(qt5-base fio libaio hicolor-icon-theme kauth)
makedepends=(git cmake extra-cmake-modules qt5-tools)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/JonMagon/${_pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('06d21c2f52f1dc345bd0e3c965756b70327e96775fe9561aea53efe792d736a7')

build() {
    cmake -B build -S "${_pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE='Release' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
    make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
}
