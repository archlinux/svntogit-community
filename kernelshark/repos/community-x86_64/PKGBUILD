# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kernelshark
pkgver=2.0.2
pkgrel=1
pkgdesc="GUI frontend for trace-cmd based Linux kernel Ftrace captures"
arch=('x86_64')
url="https://kernelshark.org/"
license=('GPL2')
depends=(freeglut gcc-libs glibc glu libglvnd qt5-base)
makedepends=(boost cmake json-c libtracefs libtraceevent trace-cmd ttf-fira-sans)
provides=(libkshark.so libkshark-plot.so libkshark-gui.so)
source=("${pkgname}-${pkgver}.tar.gz::https://git.kernel.org/pub/scm/utils/trace-cmd/kernel-shark.git/snapshot/kernel-shark-${pkgname}-v${pkgver}.tar.gz")
sha512sums=('3e293dbc7802f1a9245f0b9081cfabb7845428239aafe8e552038b29c49d6693c970f1e49bf33ef87c23bb9e047b04f1014d3716cbd71859a1e12a54ff6a5ca5')
b2sums=('09970743e6cc125c63f70a4967ad63db0f71b7006c1fb9e4152393f338638679a2d1655c2b1859bfd9ef317e2fb705aa85e1d087fa45baf058037bcf79cfa177')

prepare() {
  mv -v "kernel-shark-${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"

  # replace FreeSans with ttf-fira-sans
  sed -e 's/FreeSans/FiraSans-Regular/g' \
      -i CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -D_INSTALL_PREFIX=/usr \
        -D_LIBDIR=/usr/lib \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=(libjson-c.so libtracefs.so libtracecmd.so libtraceevent.so)
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" prefix='/usr' VERBOSE=1 -C build install
}
