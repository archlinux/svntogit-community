# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Nikita Volodin <volodin.n at gmail dot com>

pkgname=keycloak
pkgver=12.0.0
_postgresql_jdbc=42.2.14
pkgrel=1
pkgdesc="Open Source Identity and Access Management For Modern Applications and Services"
arch=('any')
url="https://www.keycloak.org/"
license=('Apache')
depends=('java-runtime>=8' 'grep' 'bash' 'coreutils' 'util-linux')
makedepends=('maven' 'git' 'java-environment=8')
backup=(
  opt/keycloak/standalone/configuration/application-roles.properties
  opt/keycloak/standalone/configuration/application-users.properties
  opt/keycloak/standalone/configuration/logging.properties
  opt/keycloak/standalone/configuration/mgmt-groups.properties
  opt/keycloak/standalone/configuration/mgmt-users.properties
  opt/keycloak/standalone/configuration/standalone-ha.xml
  opt/keycloak/standalone/configuration/standalone.xml
)
options=(emptydirs)
source=(https://github.com/keycloak/keycloak/archive/$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/keycloak/keycloak/pull/7685.patch
        keycloak.service
        keycloak.sysusers
        keycloak.tmpfiles
        layers.conf
        postgresql-module.xml
        https://jdbc.postgresql.org/download/postgresql-${_postgresql_jdbc}.jar)
sha512sums=('3c7b4e69d2614142f94543f49b7f4a7c43b7c577f7b62f2f2c569288aa5accdda43b87b671e7106d02964c62594356c361ef27df7a55a92d6cea34fcabbcdd30'
            '9af0185dd9be7169792e72fcf48271afa85701ee47f78b8e3f49a2893c8ed6d9fd2276f78351cb1a677667f4dcd95c60bffdb63b352d6410dd906460e8577df3'
            'dd10a38e4c8867de6d1fcbb4939c6c5946684842462eba3d5355bf1de13fa059edfd5eba37adce587c824ddd618b6bb88d4bbe1ee568ca5abd0f72296da3ddfe'
            'c53b734598ccf675930754514bd2af628355025197e77541e5acb8718629a8ece763c814e8373278758a30475e6a716febeff10bdebf0847d914bc193e732c79'
            '9382b42f09040e34fe1a27d9ff4fb87cabd41525ca26f8ea9629df0169cf93d95cc4bb62bcc5c2ca30efdfd7eeeb13a6f090df9a05823baf701ba729a47f6fd1'
            'd54a04c120edb933c4f7168460bd813ff92a4e3771cedcabfeeb76c5d3e057b3cd0d0486dc11ddbd91e8d87ca90193e4c74cc3414e45106dd556d455ac7e60b2'
            '5eece4f3eb57372969f0e5319804198b546455828e938ff7f37bbcfb58be08efde43a5b0376c98f359c4bc5eb069e0cd9410daefe19da1c03e9e9c57d3c1b75b'
            'd83b2b3a782d8cea341a03f3af0d24355885c5fcf377c71449d4cfa7e0362c483e6f66c2c73b76fcfe833761b4c38d2f22c6a60469efce21c8f175b1336b467d')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "$srcdir"/7685.patch
}

build() {
  cd $pkgname-$pkgver

  export PATH="/usr/lib/jvm/java-8-openjdk/bin:$PATH"
  mvn -Pdistribution \
    -pl distribution/server-dist \
    -am \
    -Dmaven.test.skip \
    clean install
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/opt"
  cp -r "distribution/server-dist/target/keycloak-$pkgver" "$pkgdir/opt/keycloak"
  install -Dm 644 "$srcdir"/layers.conf -t "$pkgdir/opt/keycloak/modules"
  install -d "$pkgdir"/opt/keycloak/standalone/{data,log}

  install -Dm644 "$srcdir"/postgresql-${_postgresql_jdbc}.jar "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/postgresql-${_postgresql_jdbc}.jar
  sed "s/@JDBC_POSTGRESQL_JAR@/postgresql-${_postgresql_jdbc}.jar/g" "$srcdir"/postgresql-module.xml > "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/module.xml

  install -d "$pkgdir/"{etc,var/log,var/lib/keycloak}
  ln -s /opt/keycloak/standalone/configuration "$pkgdir/etc/keycloak"
  ln -s /opt/keycloak/standalone/log "$pkgdir/var/log/keycloak"
  ln -s /opt/keycloak/standalone/deployments "$pkgdir/var/lib/keycloak/deployments"

  install -Dm644 "$srcdir"/keycloak.service "$pkgdir"/usr/lib/systemd/system/keycloak.service
  install -Dm644 "$srcdir"/keycloak.sysusers "$pkgdir"/usr/lib/sysusers.d/keycloak.conf
  install -Dm644 "$srcdir"/keycloak.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/keycloak.conf

  mkdir -p "$pkgdir"/usr/bin
  ln -s /opt/keycloak/bin/kcadm.sh "$pkgdir"/usr/bin/kcadm
  ln -s /opt/keycloak/bin/kcreg.sh "$pkgdir"/usr/bin/kcreg
}

# vim: ts=2 sw=2 et:
