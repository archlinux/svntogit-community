# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Matheus Gabriel Werny de Lima <matheusgwdl@protonmail.com>

pkgname=keysmith
pkgver=22.09
pkgrel=2
pkgdesc='OTP client for Plasma Mobile and Desktop'
license=(GPL3)
arch=(x86_64)
url='https://apps.kde.org/keysmith'
depends=(kirigami2 ki18n kdbusaddons kwindowsystem libsodium qt5-svg hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('fec90353b2c7dea2731d436eb7086a273bfa88333655273c85cad89c6edd7b70'
            'SKIP')
validpgpkeys=(B3ED0BAE371BEDA00717921D18F4795C1CE7042B  # Carl Schwan <carl@carlschwan.eu>
              0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D) # Bhushan Shah (mykolab address) <bshah@mykolab.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
