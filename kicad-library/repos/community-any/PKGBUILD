# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=kicad-library
pkgname=('kicad-library' 'kicad-library-3d')
pkgver=4.0.7
pkgrel=1
pkgdesc="Kicad component and footprint libraries"
arch=('any')
url="https://github.com/KiCad/kicad-library"
license=('GPL')
makedepends=('cmake' 'tar')
options=('!strip')
source=("http://downloads.kicad-pcb.org/libraries/kicad-footprints-$pkgver.tar.gz"
        "http://downloads.kicad-pcb.org/libraries/kicad-library-$pkgver.tar.gz")
noextract=("kicad-footprints-$pkgver.tar.gz")
md5sums=('280b065c34ca9ea225772893e84c65cc'
         'b8517ed459d1f9d73a85b0a79691df29')

prepare() {
  # noextract will hopefully only be needed for 4.0.4
  cd "$srcdir"
  tar -xaf "kicad-footprints-$pkgver.tar.gz"
}

build() {
  # schematics, 3D files, docs
  cd "$srcdir/kicad-library-$pkgver"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr

  # footprints
  cd "$srcdir/kicad-footprints-$pkgver"
  # nothing to build
}

package_kicad-library() {
  pkgdesc="Kicad component and footprint libraries"
  cd "$srcdir/kicad-library-$pkgver/build/Release"
  make DESTDIR="$pkgdir" install

  # not libraries (and provided by kicad)
  #rm -rf "$pkgdir/usr/share/kicad/template"

  # footprints
  cd "$srcdir/kicad-footprints-$pkgver"
  # no install either, do it manually
  install -d "$pkgdir/usr/share/kicad/footprints/"
  cp -r * "$pkgdir/usr/share/kicad/footprints/"

  # nuke 3D, nuke docs
  rm -rf "$pkgdir/usr/share/kicad/modules/packages3d"
  rm -rf "$pkgdir/usr/share/kicad/modules/footprints_doc"
}

package_kicad-library-3d() {
  pkgdesc="Kicad 3D render model libraries"

  install -d "$pkgdir/usr/share/kicad/modules/packages3d"
  cd "$srcdir/kicad-library-$pkgver/modules/packages3d"
  cp -r * "$pkgdir/usr/share/kicad/modules/packages3d/"
}
