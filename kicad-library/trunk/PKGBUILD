# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=kicad-library
pkgname=('kicad-library' 'kicad-library-3d')
pkgver=4.0.0
pkgrel=1
pkgdesc="Kicad component and footprint libraries"
arch=('any')
url="https://github.com/KiCad/kicad-library"
license=('GPL')
makedepends=('cmake')
#replaces=('kicad-library-bzr')
options=('!strip')
source=("http://downloads.kicad-pcb.org/libraries/kicad-footprints-$pkgver.tar.gz"
        "http://downloads.kicad-pcb.org/libraries/kicad-library-$pkgver.tar.gz")
md5sums=('e31d83fed31b10c30918fcfd7db4c6b7'
         '676bafdb4eb003083cded3245bffb7e6')

build() {
  # schematics, 3D files, docs
  cd "$srcdir/kicad-library-$pkgver"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr

  # footprints
  cd "$srcdir/kicad-footprints-$pkgver"
  # nothing to build
}

package_kicad-library() {
  pkgdesc="Kicad component and footprint libraries"
  cd "$srcdir/kicad-library-$pkgver/build/Release"
  make DESTDIR="$pkgdir" install

  # not libraries (and provided by kicad)
  #rm -rf "$pkgdir/usr/share/kicad/template"

  # footprints
  cd "$srcdir/kicad-footprints-$pkgver"
  # no install either, do it manually
  install -d "$pkgdir/usr/share/kicad/footprints/"
  cp -r * "$pkgdir/usr/share/kicad/footprints/"

  # nuke 3D, nuke docs
  rm -rf "$pkgdir/usr/share/kicad/modules/packages3d"
  rm -rf "$pkgdir/usr/share/kicad/modules/footprints_doc"
}

package_kicad-library-3d() {
  pkgdesc="Kicad 3D render model libararies"

  install -d "$pkgdir/usr/share/kicad/modules/packages3d"
  cd "$srcdir/kicad-library-$pkgver/modules/packages3d"
  cp -r * "$pkgdir/usr/share/kicad/modules/packages3d/"
}
