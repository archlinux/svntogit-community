# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=4.0.0
pkgrel=2
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
#url="http://iut-tice.ujf-grenoble.fr/kicad/"
url="http://kicad-pcb.org/"
license=('GPL')
depends=('glu' 'wxgtk' 'hicolor-icon-theme' 'desktop-file-utils' 'libsm' 'boost-libs' 'glew')
makedepends=('cmake' 'zlib' 'mesa' 'boost')
optdepends=('kicad-library: for footprints and symbols'
            'kicad-library-3d: for 3d models of components')
# python optdep for plugins?
install=kicad.install
source=("https://launchpad.net/kicad/4.0/4.0.0/+download/kicad-4.0.0.tar.xz"
        "https://github.com/KiCad/kicad-i18n/archive/4.0.0.tar.gz")
md5sums=('f4f7ea77ee854ca12e1fd4463f681657'
         '991a099359fbbe6fa0dbba960b44eecb')

# svn rm the patches!

build() {
  cd "$srcdir/kicad-$pkgver"

  # FS#31559
  # trying without this hack for 4.0.0 ...
  #sed -i 's/-O2/-O1/' CMakeLists.txt

  # build and install kicad
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr  \
              -DKICAD_SKIP_BOOST=ON        \
              -DBUILD_GITHUB_PLUGIN=ON
  make

  cd "$srcdir/kicad-i18n-$pkgver"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON
  make
}

package() {
  cd "$srcdir/kicad-$pkgver/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"

  cd "$srcdir/kicad-i18n-$pkgver/build/Release"
  make DESTDIR="$pkgdir" install
}
