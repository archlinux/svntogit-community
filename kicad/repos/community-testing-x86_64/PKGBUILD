# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>
# Contributor: Nick Ã˜stergaard

pkgname=kicad
pkgver=5.1.5
pkgrel=2
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('x86_64')
#url="http://iut-tice.ujf-grenoble.fr/kicad/"
url="http://kicad-pcb.org/"
license=('GPL')
depends=('wxgtk3' 'python' 'boost-libs' 'glew' 'curl' 'glm' 'ngspice' 'opencascade' 'python-wxpython')
# can ngspice/opencascade be an optdep?
# opencascade vs community edition?
makedepends=('cmake' 'zlib' 'mesa' 'boost' 'swig')
optdepends=('kicad-library: for footprints and symbols'
            'kicad-library-3d: for 3d models of components')
source=("https://launchpad.net/kicad/5.0/$pkgver/+download/kicad-$pkgver.tar.xz"
        "kicad-i18n.$pkgver.tgz::https://github.com/KiCad/kicad-i18n/archive/$pkgver.tar.gz")
sha512sums=('5ca19e219da07a906f13e236849eb4497c9f827726a5cd13aceffe1d8c7dee480823bf3e949527f59a805eb3b8176fad51576ae663dd07b10e2822f2b37866a5'
            '68e6602fcc7e73c22a594b45ecf53a956af8a134e28e86242bfaa4087df21c04b24ae4df9928f00c2cee7eb0631a02a4de1cd09547b36a466fb324f983ea5363')

prepare() {
  cd "$srcdir/kicad-$pkgver"
  #find -name '*.py' -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir/kicad-$pkgver"

  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release    \
              -DCMAKE_INSTALL_PREFIX=/usr   \
              -DCMAKE_INSTALL_LIBDIR=lib    \
              -DKICAD_USE_OCE=OFF           \
              -DKICAD_USE_OCC=ON            \
              -DKICAD_SCRIPTING=ON          \
              -DKICAD_SCRIPTING_PYTHON3=ON  \
              -DKICAD_SCRIPTING_MODULES=ON  \
              -DKICAD_SCRIPTING_WXPYTHON=ON \
              -DKICAD_SCRIPTING_ACTION_MENU=ON \
              -DKICAD_SCRIPTING_WXPYTHON_PHOENIX=ON \
              -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
              -DBUILD_GITHUB_PLUGIN=ON
  make

  cd "$srcdir/kicad-i18n-$pkgver"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-$pkgver/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"

  cd "$srcdir/kicad-i18n-$pkgver/build/Release"
  make DESTDIR="$pkgdir" install
}
