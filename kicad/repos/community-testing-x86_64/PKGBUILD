# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>
# Contributor: Nick Ã˜stergaard

pkgname=kicad
pkgver=5.1.10
pkgrel=4
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('x86_64')
#url="http://iut-tice.ujf-grenoble.fr/kicad/"
url="http://kicad-pcb.org/"
license=('GPL')
depends=('wxgtk3' 'python' 'boost-libs' 'glew' 'curl' 'glm' 'ngspice' 'opencascade' 'python-wxpython' 'libcloudproviders')
# can ngspice/opencascade be an optdep?
# opencascade vs community edition?
makedepends=('cmake' 'zlib' 'mesa' 'boost' 'swig')
optdepends=('kicad-library: for footprints and symbols'
            'kicad-library-3d: for 3d models of components')
source=("https://gitlab.com/kicad/code/kicad/-/archive/$pkgver/kicad-$pkgver.tar.gz"
        "https://gitlab.com/kicad/code/kicad-i18n/-/archive/$pkgver/kicad-i18n-$pkgver.tar.gz")
sha512sums=('41db79cd314528e6d23e7eaaa8f816e16230f02f5892b4729bd200cda01d009cb1c68d3f3e9fc98d04c4d9bc22a165252573666888468d93009b342f4d8b8e97'
            '6cdef52d03f6937001f1bb18519975f367915587e4d091574702cd576f099c8014f696cdb7c7b3349e5fc99ca049fae073a4eddbd26ada203fdd80118f1a2a24')

prepare() {
  cd "$srcdir/kicad-$pkgver"
  #find -name '*.py' -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir/kicad-$pkgver"

  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release    \
              -DCMAKE_INSTALL_PREFIX=/usr   \
              -DCMAKE_INSTALL_LIBDIR=lib    \
              -DKICAD_USE_OCE=OFF           \
              -DKICAD_USE_OCC=ON            \
              -DKICAD_SCRIPTING=ON          \
              -DKICAD_SCRIPTING_PYTHON3=ON  \
              -DKICAD_SCRIPTING_MODULES=ON  \
              -DKICAD_SCRIPTING_WXPYTHON=ON \
              -DKICAD_SCRIPTING_ACTION_MENU=ON \
              -DKICAD_SCRIPTING_WXPYTHON_PHOENIX=ON \
              -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
              -DBUILD_GITHUB_PLUGIN=ON
  make

  cd "$srcdir/kicad-i18n-$pkgver"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-$pkgver/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"

  cd "$srcdir/kicad-i18n-$pkgver/build/Release"
  make DESTDIR="$pkgdir" install
}
