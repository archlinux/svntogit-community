# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=20130331
_pkgver=${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}
_pkgbzr=4008
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('glu' 'wxgtk' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'zlib' 'mesa')
optdepends=('kicad-docs-bzr: for documentation' 
            'kicad-library-bzr: for footprints')
install=kicad.install
source=(http://iut-tice.ujf-grenoble.fr/cao/${pkgname}-sources_stable_${_pkgver}_BZR${_pkgbzr}.zip
        kicad-boost-polygon-declare-gtlsort-earlier.patch)
md5sums=('9c88273a5b323b6622d1704a90ffa399'
         'a2c39704238946e74a5ed0c38326345f')

build() {
  cd "$srcdir/stable_${_pkgver}_BZR${_pkgbzr}"

  patch -p0 < "$srcdir/kicad-boost-polygon-declare-gtlsort-earlier.patch"

  # harmless build blocking error
  # see https://lists.launchpad.net/kicad-developers/msg07841.html
  # for proper fix
  sed -i 's|^#.*error "You must use|//&|' include/common.h

  # FS#31559
  sed -i 's/-O2/-O1/' CMakeLists.txt

  # build and install kicad
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/stable_${_pkgver}_BZR${_pkgbzr}/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"
}

