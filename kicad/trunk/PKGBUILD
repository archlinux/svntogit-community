# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>
# Contributor: Nick Ã˜stergaard

pkgname=kicad
pkgver=5.1.7
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('x86_64')
#url="http://iut-tice.ujf-grenoble.fr/kicad/"
url="http://kicad-pcb.org/"
license=('GPL')
depends=('wxgtk3' 'python' 'boost-libs' 'glew' 'curl' 'glm' 'ngspice' 'opencascade' 'python-wxpython')
# can ngspice/opencascade be an optdep?
# opencascade vs community edition?
makedepends=('cmake' 'zlib' 'mesa' 'boost' 'swig')
optdepends=('kicad-library: for footprints and symbols'
            'kicad-library-3d: for 3d models of components')
source=("https://gitlab.com/kicad/code/kicad/-/archive/$pkgver/kicad-$pkgver.tar.gz"
        "https://gitlab.com/kicad/code/kicad-i18n/-/archive/$pkgver/kicad-i18n-$pkgver.tar.gz")
sha512sums=('4ae7da9e0c4c5d3b1ad196d33b7847c3c192fe5499e56be55ac706112883a14b2eb4aace65a120aac9ef3992017a6a5a0bea3b1f6c03cbd2f29ee9d6ada2b2ac'
            '17bd8e1093809beaaa06e14db1b3517a8fef936c57b1e00edb52a5a412028ef9dfe779559c0de81679af665d8d816a4e4ee049da2e3e389a868c892108dca1ed')

prepare() {
  cd "$srcdir/kicad-$pkgver"
  #find -name '*.py' -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir/kicad-$pkgver"

  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release    \
              -DCMAKE_INSTALL_PREFIX=/usr   \
              -DCMAKE_INSTALL_LIBDIR=lib    \
              -DKICAD_USE_OCE=OFF           \
              -DKICAD_USE_OCC=ON            \
              -DKICAD_SCRIPTING=ON          \
              -DKICAD_SCRIPTING_PYTHON3=ON  \
              -DKICAD_SCRIPTING_MODULES=ON  \
              -DKICAD_SCRIPTING_WXPYTHON=ON \
              -DKICAD_SCRIPTING_ACTION_MENU=ON \
              -DKICAD_SCRIPTING_WXPYTHON_PHOENIX=ON \
              -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
              -DBUILD_GITHUB_PLUGIN=ON
  make

  cd "$srcdir/kicad-i18n-$pkgver"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-$pkgver/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"

  cd "$srcdir/kicad-i18n-$pkgver/build/Release"
  make DESTDIR="$pkgdir" install
}
