# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: CSSlayer <wengxt@gmail.com>

pkgname=kimtoy
pkgver=1.10.1
pkgdesc="Standalone input method panel"
pkgrel=2
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/KIMToy?content=140967"
license=('GPL')
makedepends=('cmake' 'automoc4' 'fcitx' 'scim' 'dbus-c++' 'ibus')
depends=('kdebase-runtime')
optdepends=('fcitx: fcitx support'
            'scim: scim support'
            'dbus-c++: scim support'
            'ibus: ibus support')
install=$pkgname.install
source=("http://kde-apps.org/CONTENT/content-files/140967-kimtoy-${pkgver}.tar.bz2")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DIBUS_LIBEXEC_DIR='/usr/lib' .
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  mv "$pkgdir/usr/share/locale" "$srcdir/tmp_locale"
  mkdir -p "$pkgdir/usr/share/locale"
  mv "$srcdir/tmp_locale" "$pkgdir/usr/share/locale/kde4"
}

md5sums=('38ddfd6d1346a01cde913a114ea98b25')
