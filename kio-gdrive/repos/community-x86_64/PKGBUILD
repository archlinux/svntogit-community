# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=1.2.5
pkgrel=1
arch=(x86_64)
pkgdesc="KIO Slave to access Google Drive"
url="https://community.kde.org/KIO_GDrive"
license=(GPL)
depends=(libkgapi kaccounts-providers)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools intltool)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1523c87f3f679cecbef0a2158a133f84379a0b276000c0233c34041b9c14d605'
            'SKIP')
validpgpkeys=('F07D85CAA18ACF46A346FD017C7FC6EA8633B4EA') # Elvis Angelaccio <elvis.angelaccio@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd build
  make DESTDIR="$pkgdir" install
}
