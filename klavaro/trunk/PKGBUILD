# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=klavaro
pkgver=3.09
pkgrel=3
pkgdesc="Free touch typing tutor program"
arch=('x86_64')
url="http://klavaro.sourceforge.net/"
license=('GPL2')
depends=('curl' 'gtk3' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('espeakup: voice output')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('9983e501563a4d05e429700a2bd5bb078ac43b2f0d4014864e3cac42e0a1f589')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  sed -i 's|/usr/share/icons/hicolor/24x24/apps/klavaro.png|klavaro|' \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
