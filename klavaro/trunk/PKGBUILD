# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=klavaro
pkgver=3.01
pkgrel=1
pkgdesc="Free touch typing tutor program"
arch=('i686' 'x86_64')
url="http://klavaro.sourceforge.net/"
license=('GPL2')
depends=('curl' 'gtk3' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('espeakup: voice output')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('53e2824ccc89f70dd7dca4d52ccf9c0e')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # remove the gtkdatabox shared library
  # klavaro links to it statically due to some incompatibility wioth GTK3
  rm -r "$pkgdir/usr/lib"

  sed -i 's|/usr/share/icons/hicolor/24x24/apps/klavaro.png|klavaro|' \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
}
