# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Stefano Zamprogno <stefano.zamprogno@gmail.com>

pkgname=kmess
pkgver=2.0.6.2
pkgrel=10
pkgdesc="A MSN Messenger client for Linux"
arch=('i686' 'x86_64')
url='https://sourceforge.net/projects/kmess/'
license=('GPL')
depends=('kdebase-lib')
makedepends=('automoc4' 'cmake' 'optipng' 'patch')
optdepends=('cabextract: provides winks support')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        giflib-5.0.patch
        fix-crash-in-contactlistmodel.patch)
sha256sums=('d6af80d2144d3b859b46bc3aa18eaecb1329f1782bf165bda5617a92ec8a6a8e'
            '3026df002dd0a829588c8d3b2c9b4674a411c784e2e34e56c3b1829c868ea83c'
            'f496008e3a43eeb22a3f9126d410fb61e97ca7c5d9e5a2ba2f4a87c2f74d1dce')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

# Fix invalid PNG images to work with libpng 1.6
  find -name '*.png' -exec optipng -quiet -force -fix {} +

  patch -Np0 -i ../giflib-5.0.patch

# Fix crash in ContactListModel (FS#32402)
  patch -Np1 -i ../fix-crash-in-contactlistmodel.patch
}

build() {
  cd "${srcdir}"

  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
