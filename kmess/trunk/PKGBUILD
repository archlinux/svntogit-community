# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Stefano Zamprogno <stefano.zamprogno@gmail.com>

pkgname=kmess
pkgver=2.0.6.2
pkgrel=9
pkgdesc="A MSN Messenger client for Linux"
arch=('i686' 'x86_64')
url='http://kmess.sourceforge.net/'
license=('GPL')
depends=('kdebase-lib')
makedepends=('automoc4' 'cmake' 'optipng')
optdepends=('cabextract: provides winks support')
install=$pkgname.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-$pkgver.tar.bz2
        giflib-5.0.patch
        fix-crash-in-contactlistmodel.patch)
md5sums=('a2e57911713308bef81b9347135cc5dd'
         '29f7a0c0e27d6b5cafbc3ad498e3e30f'
         'fdfe837f4bd64a5123edc63019bd5cf2')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

# Fix invalid PNG images to work with libpng 1.6
  find -name '*.png' -exec optipng -quiet -force -fix {} +

  patch -Np0 -i ../giflib-5.0.patch

# Fix crash in ContactListModel (FS#32402)
  patch -Np1 -i ../fix-crash-in-contactlistmodel.patch
}

build() {
  cd "${srcdir}"

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
