# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.12.0
_commit=266320a
pkgrel=1
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
url="http://kmplayer.kde.org"
depends=(kmediaplayer kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools python)
source=($pkgname-$pkgver.tar.gz::"http://quickgit.kde.org/?p=kmplayer.git&a=snapshot&h=$_commit&fmt=tgz")
md5sums=('c8cdd60b67018954445bd5708a291a83')

prepare() {
  mkdir -p build

# Don't include kdelibs header
  sed -e '/kstdaction.h/d' -i $pkgname/src/kmplayerview.cpp -i $pkgname/src/viewarea.cpp
  sed -e '/ksimpleconfig.h/d' -i $pkgname/src/kmplayerpartbase.cpp
}

build() {
  cd build
  cmake ../$pkgname \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

