# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.12.0.b
_pkgver=0.12.0b
pkgrel=1
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=(x86_64)
license=(GPL LGPL FDL)
url="http://kmplayer.kde.org"
depends=(kmediaplayer kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools python kinit)
optdepends=('mplayer: mplayer backend')
source=("http://download.kde.org/stable/$pkgname/${_pkgver%.*}/$pkgname-$_pkgver.tar.bz2")
md5sums=('ad5ea3964cc23847e38683390f75386c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$_pkgver \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

