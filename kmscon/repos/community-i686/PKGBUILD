# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@googlemail.com>
pkgname=kmscon
pkgver=7
pkgrel=1
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS)'
arch=(i686 x86_64)
url='https://github.com/dvdhrm/kmscon'
license=('MIT')
depends=(systemd libdrm mesa libegl libgbm libgles pango libxkbcommon libpciaccess xkeyboard-config)
makedepends=(git libxslt docbook-xsl linux-api-headers)
options=(!libtool)

__gitroot=https://github.com/dvdhrm/kmscon.git
__gitname=kmscon

build() {
  cd "$srcdir"

  msg "Connecting to GIT server...."

  if [[ -d "$__gitname" ]]; then
    cd "$__gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$__gitroot" "$__gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$__gitname-build"
  git clone "$srcdir/$__gitname" "$srcdir/$__gitname-build"
  cd "$srcdir/$__gitname-build"

  git checkout $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --disable-wlterm
  make
}

package() {
  cd "$srcdir/$__gitname-build"
  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/usr/lib/systemd/system"
  cp COPYING "$pkgdir/usr/share/licenses/$pkgname/"
  cp docs/kmscon{,vt@}.service "$pkgdir/usr/lib/systemd/system/"
}
