# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=4.6.4
pkgrel=2
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('i686' 'x86_64')
url="http://kmymoney2.sourceforge.net/"
license=('GPL')
depends=('kdebase-workspace' 'shared-mime-info' 'aqbanking' 'libofx' 'libalkimia' 'boost-libs')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl' 'boost')
replaces=('kmymoney2')
install=$pkgname.install
changelog=$pkgname.changelog
#options=('!makeflags')
source=(http://downloads.sourceforge.net/project/kmymoney2/KMyMoney-KDE4/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('89522fadb4a9f06f3f20e2d6ac47a147f70b4265abad2fdf3aa6672fe2a3c012')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build
  cd build

  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}
