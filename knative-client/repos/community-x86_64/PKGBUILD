# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=knative-client
_pkgname=client
pkgver=1.3.1
pkgrel=2
pkgdesc="knative client"
arch=('x86_64')
url="https://github.com/knative/client"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/knative/${_pkgname}/archive/refs/tags/knative-v${pkgver}.tar.gz")
sha512sums=('d837831d8c7d76ebd7c439b317b00747a6afccb6853fa4a74c87690e70091c13f75a36921c803c21ef9dc1f9fff38e07eeaaa11199f637d2bfa71a7535a8baa8')
b2sums=('a723f125f142b40dc7bcb39d55a4add799ada159134b60e149183752635407579af11a4ed137799430447e71b8277064babeb9e5cd651795f2d470c7d4aaa5ca')

build() {
  cd "client-knative-v${pkgver}/cmd/kn"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build -v
}

check() {
  cd "client-knative-v${pkgver}"
  go test -v ./...
}

package() {
  cd "client-knative-v${pkgver}/cmd/kn"
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions"
  ./kn completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_kn"
  ./kn completion bash > "${pkgdir}/usr/share/bash-completion/completions/kn"
  install -Dm755 kn "${pkgdir}/usr/bin/kn"
}
