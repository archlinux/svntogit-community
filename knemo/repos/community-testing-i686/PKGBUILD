# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Zamprogno <stefano dot zamprogno at gmail dot com>

pkgname=knemo
pkgver=0.7.7
pkgrel=2
pkgdesc="The KDE Network Monitor"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=12956"
license=('GPL')
depends=('kdebase-workspace' 'wireless_tools' 'iw' 'libnl' 'gtk-update-icon-cache')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
options=('libtool')
source=(http://kde-apps.org/CONTENT/content-files/12956-${pkgname}-${pkgver}.tar.xz)
md5sums=('d7448d77145c82ae80bbbc6460fa108c')

prepare() {
  mkdir build
}

build() {
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # remove knemo autostart
  # rm -rf ${pkgdir}/usr/share/autostart
}
