# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=knights
pkgver=2.5.0+122+g62bd5d8
pkgrel=1
pkgdesc="Chess board by KDE with XBoard protocol support"
arch=(x86_64)
url="https://www.kde.org/applications/games/knights/"
license=(GPL)
depends=(libkdegames kplotting plasma-framework hicolor-icon-theme)
optdepends=('gnuchess: for playing against the computer')
makedepends=(extra-cmake-modules kdoctools git)
_commit=62bd5d8320c62d6155f0798dc0cc54fc7550a3ef  # master
source=("git://git.kde.org/knights.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g' -e 's/v//'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF   
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
