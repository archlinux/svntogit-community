# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-audioencoder-lame
epoch=1
pkgver=19.1.2
_codename=Matrix
pkgrel=2
pkgdesc="LAME Audio Encoder add-on for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/audioencoder.lame'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-audioencoder')
makedepends=('cmake' 'lame' 'kodi-dev')
options=(!lto debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/audioencoder.lame/archive/$pkgver-$_codename.tar.gz")
sha512sums=('1dc63fa652101ca4e03c305d05a9c406511400540c74e6a783aab6f0ee5a70ac137f6cf4ecb7b65878711fc6559d3f4ebdc6cd32a01d5d6d918893774163d054')

build() {
    cd "audioencoder.lame-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'lame')
    cd "audioencoder.lame-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

