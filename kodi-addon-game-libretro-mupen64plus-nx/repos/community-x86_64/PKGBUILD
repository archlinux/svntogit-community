# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-game-libretro-mupen64plus-nx
epoch=1
pkgver=2.1.0.11
_codename=Matrix
pkgrel=2
pkgdesc="Nintendo 64 mupen64plus-next for kodi"
arch=('x86_64')
url='https://github.com/kodi-game/game.libretro.mupen64plus'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-game')
makedepends=('cmake' 'kodi-dev' 'kodi-addon-game-libretro' 'libretro-mupen64plus-next')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kodi-game/game.libretro.mupen64plus/archive/$pkgver-$_codename.tar.gz")
sha512sums=('2c1d2a846a0a0dd61b94d3f528b006fd106ce2cfe1be0d0a56ff8ff32ff623a6834035ef61abf0f0d519d16f8fce61d4ad7361878e23f40ba97659bf77764165')

build() {
    cd "game.libretro.mupen64plus-nx-$pkgver-$_codename"
    sed -e 's/mupen64plus_libretro/mupen64plus_next_libretro/g' \
        -i CMakeLists.txt
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi-addon-game-libretro' 'libretro-mupen64plus-next')
    cd "game.libretro.mupen64plus-nx-$pkgver-$_codename"
	make DESTDIR="$pkgdir/" install
}

