# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-game-libretro
pkgver=19.0.0
_codename=Matrix
pkgrel=5
pkgdesc="Libretro wrapper for Kodi's Game API"
arch=('x86_64')
url='https://github.com/kodi-game/game.libretro'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-game')
makedepends=('cmake' 'libretro-core-info' 'kodi-platform' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kodi-game/game.libretro/archive/$pkgver-$_codename.tar.gz")
sha512sums=('a746b3b1e962e01af6b26a8f5c35c673f28e1248115a266528c1cd333ec132075948d0ca8d6ac0a5fcc07f80c4f881b0d819d59768b572f93f47daf43bbeff5b')

build() {
    cd "game.libretro-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'kodi-platform' 'libretro-core-info')
    cd "game.libretro-$pkgver-$_codename"
	make DESTDIR="$pkgdir/" install
}

