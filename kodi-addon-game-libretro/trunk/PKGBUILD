# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-game-libretro
pkgver=2.2.0
_codename=Matrix
pkgrel=3
pkgdesc="Libretro wrapper for Kodi's Game API"
arch=('x86_64')
url='https://github.com/kodi-game/game.libretro'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-game')
makedepends=('cmake' 'libretro-core-info' 'kodi-platform' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kodi-game/game.libretro/archive/$pkgver-$_codename.tar.gz")
sha512sums=('f1a5671b468ebf0ea3a86b757ae2481115bfb38082043757aa94d6f193b6eea560b37ccafafe63e686b729d80aa89ac58640899900894d0a72472badadbf27ac')

build() {
    cd "game.libretro-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi-common' 'kodi-platform' 'libretro-core-info')
    cd "game.libretro-$pkgver-$_codename"
	make DESTDIR="$pkgdir/" install
}

