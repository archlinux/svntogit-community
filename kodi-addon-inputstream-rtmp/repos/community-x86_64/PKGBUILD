# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: kevku <kevku@gmx.com>

pkgname=kodi-addon-inputstream-rtmp
pkgver=19.0.0
_codename=Matrix
pkgrel=5
pkgdesc="RTMP input stream add-on for Kodi"
arch=('x86_64')
url="https://github.com/xbmc/inputstream.rtmp"
license=('GPL2')
groups=('kodi-addons' 'kodi-addons-inputstream')
makedepends=('cmake' 'rtmpdump' 'p8-platform' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/inputstream.rtmp/archive/$pkgver-$_codename.tar.gz")
sha512sums=('8ea7a9fed8ad0d29365a8d0f67c0b0203c16da19607ccc9788e9e244f034b079667d2d1ba2a916fa0ce5af4008563573bbaabce7bface9879c3e1bc1c4c3aed7')

build() {
    cd "inputstream.rtmp-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'rtmpdump' 'p8-platform')
    cd "inputstream.rtmp-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}
