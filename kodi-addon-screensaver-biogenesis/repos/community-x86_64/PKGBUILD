# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-screensaver-biogenesis
epoch=1
pkgver=19.0.0
_codename=Matrix
pkgrel=5
pkgdesc="BioGenesis screensaver for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/screensaver.biogenesis'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-screensaver')
makedepends=('cmake' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/screensaver.biogenesis/archive/$pkgver-$_codename.tar.gz")
sha512sums=('e9f660371566c52282f8842c5cf1b425c8b87e81b01631b907108d8c2d2356a3a2a25070b8e73e477588e1d7516ee733a827356a55129fcc01aa51cc0f8a3543')

build() {
    cd "screensaver.biogenesis-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi')
    cd "screensaver.biogenesis-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

