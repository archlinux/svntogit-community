# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-screensaver-pingpong
epoch=1
pkgver=19.0.0
_codename=Matrix
pkgrel=5
pkgdesc="Ping-pong screensaver for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/screensaver.pingpong'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-screensaver')
makedepends=('cmake' 'glm' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/screensaver.pingpong/archive/$pkgver-$_codename.tar.gz")
sha512sums=('84f41d379e51e851a5c3a14005be949383663b9a0dea9ab6233997019247d09b171c5b3a053d5b3c6801f7670bfc81d3927e8445342244829144c6f85bf23f0d')

build() {
    cd "screensaver.pingpong-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'glm')
    cd "screensaver.pingpong-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

