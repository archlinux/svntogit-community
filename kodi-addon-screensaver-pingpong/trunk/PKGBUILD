# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-screensaver-pingpong
epoch=1
pkgver=2.4.0
_codename=Matrix
pkgrel=2
pkgdesc="Ping-pong screensaver for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/screensaver.pingpong'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-screensaver')
makedepends=('cmake' 'glm' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/screensaver.pingpong/archive/$pkgver-$_codename.tar.gz")
sha512sums=('da430c5e07e8a56212f04990a586e64e86bdcac3b094aa27f6d4e3b5553a9ca799a90927da4e2916e8d84d6d0e4ff3961c31fb557322a3219afbbb3db99cc2d4')

build() {
    cd "screensaver.pingpong-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'glm')
    cd "screensaver.pingpong-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

