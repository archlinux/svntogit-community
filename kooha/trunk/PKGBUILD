# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Mark Wagie <mark dot wagie at tutanota dot com>

pkgname=kooha
pkgver=2.2.2
pkgrel=1
pkgdesc="Elegantly record your screen"
arch=('x86_64')
url="https://github.com/SeaDve/Kooha"
license=('GPL3')
depends=('gst-plugin-pipewire' 'gstreamer-vaapi' 'gst-plugins-ugly' 'gtk4' 'libadwaita'
         'x264' 'xdg-desktop-portal' 'libpulse')
makedepends=('cargo' 'meson')
checkdepends=('appstream-glib' 'xorg-server-xvfb')
sha256sums=('6336d09344ac1da94f92db4b9c6445e73da95cac1d6a9f77a644d2e0e38b70f1')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")

prepare() {
  cd Kooha-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  arch-meson Kooha-$pkgver build
  meson compile -C build
}

# https://github.com/SeaDve/Kooha/issues/197
#check() {
#  xvfb-run meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
