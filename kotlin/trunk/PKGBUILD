# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Romain Gautier <romain.gautier@nimamoh.com>

pkgname=kotlin
pkgver=1.1.2.5
pkgrel=1
pkgdesc='The Kotlin programming language'
arch=('any')
url='http://kotlinlang.org/'
license=('apache' 'custom')
makedepends=('setconf')
depends=('bash' 'java-environment>=8')
source=("https://github.com/JetBrains/kotlin/releases/download/v${pkgver%.*}-${pkgver##*.}/kotlin-compiler-${pkgver%.*}-${pkgver##*.}.zip")
sha256sums=('2ddd001ec2a3a7f70f30ff22b7ee4f8eff2c195b8942b468a2d122c78fb50e72')

prepare() {
  cd "${pkgname}c/bin"

  setconf kotlin DIR /usr/bin
  setconf kotlinc KOTLIN_HOME "/usr/share/$pkgname"
  setconf kotlinc-js DIR /usr/bin
  setconf kotlinc-jvm DIR /usr/bin
}

package() {
  cd "${pkgname}c"

  # Executables
  install -Dm755 bin/kotlinc "$pkgdir/usr/bin/kotlinc"
  install -Dm755 bin/kotlin "$pkgdir/usr/bin/kotlin"
  install -Dm755 bin/kotlinc-js "$pkgdir/usr/bin/kotlinc-js"
  install -Dm755 bin/kotlinc-jvm "$pkgdir/usr/bin/kotlinc-jvm"

  # jar files
  cd "$srcdir/${pkgname}c/lib"
  install -d "$pkgdir/usr/share/$pkgname/lib"
  for jar in *.jar; do
    install -Dm644 "$jar" "$pkgdir/usr/share/$pkgname/lib"
  done

  # Licenses
  cd "$srcdir/${pkgname}c/license"
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname"
  cd third_party
  for txt in *.txt; do
    install -Dm644 "$txt" "$pkgdir/usr/share/licenses/$pkgname"
  done

  # build.txt must be installed for for "-version" to work
  cd "$srcdir/${pkgname}c"
  install -Dm644 build.txt "$pkgdir/usr/share/$pkgname"
}

# getver: github.com/JetBrains/kotlin/releases/latest
# vim: ts=2 sw=2 et:
