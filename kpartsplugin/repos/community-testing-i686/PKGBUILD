# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: Frikilinux <frikilinux at frikilinux.com.ar>

pkgname=kpartsplugin
pkgver=20120723
pkgrel=3
pkgdesc="This plugin uses KDE's KParts to embed file viewers into non-KDE browsers"
arch=('i686' 'x86_64')
url="http://www.unix-ag.uni-kl.de/~fischer/kpartsplugin/"
license=('GPL3' 'BSD')
depends=('kdelibs')
makedepends=('automoc4' 'cmake')
source=("http://pkgs.fedoraproject.org/repo/pkgs/kpartsplugin/kpartsplugin-20120723.tar.bz2/cf94ee866a934da97a04cd1619e16099/kpartsplugin-20120723.tar.bz2"
        "LICENSE")
md5sums=('cf94ee866a934da97a04cd1619e16099'
         'c4cc811349e40e9f34e77e27b902ad96')

prepare() {
  sed -i 's/nsbrowser/mozilla/g' ${pkgname}-${pkgver}/CMakeLists.txt
}

build(){
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  make -C build DESTDIR="${pkgdir}/" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
}
