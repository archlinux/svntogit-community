# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=5.0.1
pkgrel=2
pkgdesc="KDE Photo Album, picture collection manager"
url="http://www.kphotoalbum.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libkipi' 'libkdcraw' 'libkface' 'libkgeomap' 'exiv2')
makedepends=('extra-cmake-modules' 'python' 'appstream' 'plasma-framework' 'phonon-qt5' 'kdoctools' 'kparts' 'libkexiv2')
optdepends=()
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e43beb6cb93d749b30aa171ebce5ac5473b16704ea6d36bf6dcc2ce54328ce55')

build() {
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
