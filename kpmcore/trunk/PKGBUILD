# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpmcore
pkgver=3.3.0
pkgrel=1
pkgdesc="Library for managing partitions"
arch=(x86_64)
url="http://kde.org/applications/system/kdepartitionmanager/"
license=(GPL2)
depends=(parted libatasmart kwidgetsaddons kcoreaddons ki18n)
makedepends=(extra-cmake-modules)
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
            'dosfstools: FAT32 support'
            'f2fs-tools: F2FS support'
            'exfat-utils: exFAT support'
            'nilfs-utils: nilfs support'
            'udftools: UDF support')
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b8d6fa3c0d89feebf690a424d98ad36acb2438b1099a5b620ee349989d04db68'
            'SKIP')
validpgpkeys=(1EE5A3205904BAA2B88C0A9D24FD31940095C0E1) # Andrius Å tikonas <andrius@stikonas.eu>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
