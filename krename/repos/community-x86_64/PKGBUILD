# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=krename
pkgver=5.0.1
pkgrel=3
pkgdesc='A very powerful batch file renamer'
arch=(x86_64)
url='https://www.krename.net'
license=(GPL)
depends=(kio kjs exiv2 podofo taglib hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/krename/-/commit/9e21688a.patch)
sha256sums=('caac78afd3ba06613df47bbd1570bb900acf4185547354d6eb588e70656f6a7d'
            'SKIP'
            'e17096ee9b92b368341bdfc3c3398c0cfc0c3340b8c62dff010385e692b70d10')
validpgpkeys=(D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heirecka@exherbo.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 9e21688a.patch # Fix launching from desktop file on Wayland
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
