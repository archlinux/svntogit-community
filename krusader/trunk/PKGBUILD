# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=krusader
pkgver=2.7.2
pkgrel=1
pkgdesc="Advanced twin panel (commander style) file manager for KDE"
arch=('x86_64')
url="https://krusader.org/"
license=('GPL')
depends=('karchive' 'kbookmarks' 'kcodecs' 'kcompletion' 'kcoreaddons' 'kconfig'
         'kdoctools' 'ki18n' 'kiconthemes' 'kitemviews' 'kio' 'knotifications'
         'kparts' 'solid' 'ktextwidgets' 'kwallet' 'kwidgetsaddons' 'kwindowsystem'
         'kxmlgui' 'kguiaddons' 'hicolor-icon-theme')
makedepends=('cmake' 'extra-cmake-modules')
optdepends=(
            # archive formats
			# look for KrServices::cmdExist calls
            'xz: LZMA and XZ archive support'
            'unzip: ZIP decompression support'
            'zip: ZIP archive support'
            'lhasa: LHA archive support'
            'cpio: cpio archive support'
            'unrar: RAR decompression support'
            'arj: ARJ archive support'
            'unarj: ARJ decompression support'
            'unace: ACE decompression support'
            'p7zip: 7z archive support'
            # additional apps
            'keditbookmarks: Manage Bookmarks functionality'
            'kde-cli-tools: manage file associations, root mode'
            'kompare: file contents comparison'
            'kdiff3: file contents comparison'
            'krename: advanced rename tool'
            'konsole: terminal'
            'ktexteditor: file editing support'
)
source=("https://download.kde.org/stable/krusader/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('dcccc3a5e956ad0b1b63f236c4f41a7f')
sha1sums=('e0109c6f9f841695a31045c46a43eb70c0085750')
sha256sums=('41a39a43b3c42dd1d1ecaea86df30caff6a061fecc1d66f60859b2a3ca976109')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDESU_PATH="/usr/lib/kf5/kdesu"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
