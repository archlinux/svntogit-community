# $Id: PKGBUILD 149613 2012-02-08 20:20:01Z pierre $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname=krusader
pkgver=2.4.0beta3
_pkgver=2.4.0-beta3
pkgrel=9
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
arch=('i686' 'x86_64')
url="http://krusader.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime' 'kdebase-lib' 'kdebase-katepart' 'kuiserver')
makedepends=('cmake' 'automoc4')
optdepends=('kdebase-keditbookmarks: to edit bookmarks'
            'kdebase-konsole: open an embedded terminal')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$_pkgver.tar.bz2"
        "kde_bug309119.diff" "kde_bug325801.diff")
md5sums=('62a02db3f213bb76805ca6c958e9e263'
         '5e9952f6ac38e5ce323eb95ca6f20db6'
         '55fd27e4888e15e8b3bd8ab08b85aa9e')

prepare() {
  cd "$srcdir/$pkgname-$_pkgver"

  # fix KDE bug 309119 - "New directory" dialog: can't clear all typed text
  patch -Np1 < "$srcdir/kde_bug309119.diff" || true
  # fix KDE bug 32580 -  Showing contents of rar archives is broken with unrar 5.00
  patch -Np1 < "$srcdir/kde_bug325801.diff" || true
}

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install
}
