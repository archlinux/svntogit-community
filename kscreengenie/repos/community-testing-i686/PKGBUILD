# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreengenie
pkgver=2.0.1
pkgrel=2
pkgdesc="KDE screenshot capture utility"
arch=(i686 x86_64)
url='https://projects.kde.org/kscreengenie'
license=(GPL)
depends=(kio xcb-util-cursor libkscreen hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegraphics-ksnapshot)
provides=(kdegraphics-ksnapshot)
source=("http://download.kde.org/stable/$pkgname/${pkgver:0:3}/src/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('06d4f83ae20dcaed950c2343640b5d90')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

