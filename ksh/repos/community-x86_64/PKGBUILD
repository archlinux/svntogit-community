# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=ksh
_pkgver=2020.0.0-alpha1
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc="The Original AT&T Korn Shell"
arch=('x86_64')
url="http://kornshell.org/"
license=('EPL')
makedepends=('meson')
checkdepends=('ed' 'vi' 'expect' 'openbsd-netcat')
provides=('ksh93')
install=ksh.install
source=("https://github.com/att/ast/releases/download/${_pkgver}/ksh-${_pkgver}.tar.xz"{,.asc})
sha256sums=('f383f375902557561340372eca0d3fd5076d310de563e18271e406e7f42ff25b'
            'SKIP')
b2sums=('a6e030bdcde2561b97cb25e7914413f6133c49795be36ee57704f996a5592c0b05e21e4bf71b2abfeccd7107033a207bf62ba1b3919ebaaef113c1a9b91a87ec'
        'SKIP')
validpgpkeys=('4BF045ACC726FE4E9DFC1D7762213CE2D3CB82EA') # Siteshwar Vashisht <svashisht@redhat.com>

build() {
    mkdir -p "${srcdir}"/ksh-${_pkgver}/build
    cd "${srcdir}"/ksh-${_pkgver}/build

    meson --prefix /usr \
          --buildtype=plain \
          ..
    ninja
}

check() {
    cd "${srcdir}"/ksh-${_pkgver}/build

    # a couple tests are flaky and may time out
    meson test -t 3
}

package() {
    cd "${srcdir}"/ksh-${_pkgver}/build

    DESTDIR="${pkgdir}" ninja install
    ln -s ksh "${pkgdir}"/usr/bin/ksh93
}
