# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kshutdown
pkgver=3.2
pkgrel=5
pkgdesc='Shutdown Utility for KDE'
arch=('x86_64' 'i686')
url='http://kshutdown.sourceforge.net/'
license=('GPL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'ninja')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-source-$pkgver.zip")
sha256sums=('8be6c2a15bb539919f94ec0caf20f00b144185f996b7663c464fbf7ce8f4a39f')

build() {
  mkdir build
  cd build
  cmake "../$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
