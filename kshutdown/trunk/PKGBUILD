# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kshutdown
pkgver=4.2
pkgrel=1
pkgdesc='Shutdown Utility'
arch=(x86_64)
url='https://kshutdown.sourceforge.net/'
license=(GPL)
depends=(knotifyconfig kidletime hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=("https://downloads.sourceforge.net/$pkgname/KShutdown/$pkgver/$pkgname-source-$pkgver.zip")
sha256sums=('48e537b593fd2470c0cfeca7c2c48ac3221fdb48b0fb89bfb898e4faf531ee46')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKS_KF5=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
