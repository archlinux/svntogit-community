# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.2.2
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('96a70d6144bbf407e36f4a00380312a81745c6157c0adbf3be0d83fe5b8f388b0913c078c48e73d1c452c0fbefafb6aa1ad6f0e6a30132913ca7dc8a9690ac3c')
b2sums=('7a1ff91c4c1c460c062bfc58b8ff9dbec34e532ded0cb7d2c1cf3275294ac00993df0bd6b4ed4b2e6fcec8a523b0148ffc0ac7fa985bd299d9ddfe51382ce760')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 kube-linter -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
