# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: ml <>
pkgname=kubectl-cert-manager
pkgver=1.7.0
pkgrel=1
pkgdesc='Automatically provision and manage TLS certificates in Kubernetes'
arch=('x86_64' 'aarch64')
url='https://github.com/jetstack/cert-manager'
license=('Apache')
depends=('kubectl')
makedepends=('git' 'go')
groups=('kubectl-plugins')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('338cbe4c04844e7303587c5726cf1065e0e591b295286adf3107b56896247f9715ed52ccc42db749ae7c3feee1c36fe97ec8d70894ccc09c887fe3eaf2d65623')
b2sums=('b1516292c7c96f5cc0fa2c5457bff1fe7c7c125022ba86bd221a973204e8741e38ef1ac34e3d68cd603f5715f6b3003104d0f4eb4a9a8fc4b10232f6cad5f2cc')

build() {
  local _x _commit
  _commit=$(bsdcat "$pkgname-$pkgver.tar.gz" | git get-tar-commit-id)
  _x=(
    AppVersion="v$pkgver"
    AppGitCommit="${_commit:?}"
    AppGitState="clean"
  )

  cd cert-manager-"$pkgver"
  export CGO_ENABLED=1
  export CGO_LDFLAGS="$LDFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -modcacherw -mod=readonly'
  go build -ldflags="-linkmode=external ${_x[*]/#/-X=github.com/jetstack/cert-manager/pkg/util.}" ./cmd/ctl
}

check() {
  cd cert-manager-"$pkgver"
  # we don't want e2e and other tests that explicitly want bazel
  go test -short ./cmd/ctl/...
}

package() {
  cd cert-manager-"$pkgver"
  install -Dm755 ctl "$pkgdir"/usr/bin/kubectl-cert_manager
}
