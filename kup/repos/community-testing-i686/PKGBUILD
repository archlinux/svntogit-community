# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>
# PKGBUILD base; Simper < simonpersson1 AT gmail DOT com >

pkgname=kup
pkgver=0.7.0
pkgrel=2
pkgdesc="A KDE bup backup software for helping people to keep up-to-date backups"
arch=(i686 x86_64)
url="https://www.linux-apps.com/p/1127689/"
license=(GPL2)
depends=(kidletime kinit hicolor-icon-theme)
makedepends=(extra-cmake-modules python plasma-framework)
optdepends=('bup: support for incremental backups'
            'rsync: support for synced backups')
source=("https://github.com/spersson/Kup/archive/$pkgname-$pkgver.tar.gz")
md5sums=('91d5879a1282a8e0e8898ae6f2c3ecbe')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../Kup-$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
