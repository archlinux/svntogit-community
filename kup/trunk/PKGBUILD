# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>
# PKGBUILD base; Simper < simonpersson1 AT gmail DOT com >

pkgname=kup
pkgver=0.6.1
pkgrel=1
pkgdesc="A KDE bup backup software for helping people to keep up-to-date backups"
arch=(i686 x86_64)
url="http://kde-apps.org/content/show.php/Kup+Backup+System?content=147465"
license=(GPL2)
depends=(kidletime kinit hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
optdepends=('bup: support for incremental backups'
            'rsync: support for synced backups')
source=("https://github.com/spersson/Kup/archive/$pkgname-$pkgver.tar.gz")
md5sums=('b949d2ad1970bb981264440ce2fcfd27')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../Kup-$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
