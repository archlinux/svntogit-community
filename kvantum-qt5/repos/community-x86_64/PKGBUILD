# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=Kvantum
pkgname=kvantum-qt5
pkgver=0.10.9
pkgrel=1
pkgdesc="SVG-based theme engine for Qt5 (including config tool and extra themes)"
arch=('x86_64')
url="https://github.com/tsujan/Kvantum"
license=('GPL')
depends=('qt5-base' 'qt5-svg' 'qt5-x11extras' 'libx11' 'libxext' 'hicolor-icon-theme' 'kwindowsystem')
makedepends=('cmake' 'qt5-tools')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/V${pkgver}.tar.gz")
sha256sums=('e588c2e7acbc00b2ccf197dc34e09a3bc4c1f5d4574f26b929ad372177183041')

prepare() {
    mkdir -p build
}

build() {
    cd build
	cmake ../${_pkgname}-${pkgver}/${_pkgname} \
	      -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
    # KDE4 is long dead
    rm -r "${pkgdir}"/usr/share/kde4
}
