# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=lablgtk3
pkgver=3.0.beta6
pkgrel=2
pkgdesc="An Objective Caml interface to gtk3"
arch=('x86_64')
license=('LGPL')
url="http://lablgtk.forge.ocamlcore.org/"
depends=('gtk3' 'gtksourceview3' 'gtkspell3' 'ocaml-cairo')
makedepends=('ocaml-compiler-libs>=4.0.9' 'ocaml-findlib' 'dune')
optdepends=('ocaml: for using the tools')
source=("https://github.com/garrigue/lablgtk/releases/download/${pkgver}/${pkgname}-${pkgver}.tbz"
        "0001-disable-warn-error-on-Obj.truncate.patch")
sha256sums=('4014ac73afd502cfce862fc65a3c77372fb191a8f317d6803f1a9372172bd1ca'
            '22713c672519f7cdb67ba61e2b975504c8b740ca9e6728e52389978dd672270b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < "${srcdir}"/0001-disable-warn-error-on-Obj.truncate.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  dune build -p lablgtk3,lablgtk3-gtkspell3,lablgtk3-sourceview3
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir "lib/ocaml"

  install -dm755 "${pkgdir}/usr/share/"
  mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"
}
