# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: cromo <dawid@klej.net>

pkgname=laptop-detect
pkgver=0.13.8
pkgrel=1
pkgdesc='Attempts to detect a laptop'
arch=('any')
url='https://packages.qa.debian.org/l/laptop-detect.html'
license=('BSD')
depends=('dmidecode')
source=("http://ftp.debian.org/debian/pool/main/l/$pkgname/${pkgname}_$pkgver.tar.xz")
md5sums=('c289fdffe6954701eced7c51c704dd0c')

prepare() {
  cd $pkgname-$pkgver
  sed -e "s/@VERSION@/$pkgver/g" < laptop-detect.in > laptop-detect
}

package() {
  cd $pkgname-$pkgver
  install -D -m755 laptop-detect "$pkgdir/usr/bin/laptop-detect"
  install -D -m644 laptop-detect.8 "$pkgdir/usr/share/man/man8/laptop-detect.8"
  install -D -m644 debian/copyright "$pkgdir/usr/share/licenses/$pkgname/copyright"
}

# vim:set ts=2 sw=2 et:
