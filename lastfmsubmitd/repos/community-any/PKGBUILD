# $Id$
# Maintainer: Daenyth <Daenyth [at] aur [dot] archlinux [dot] org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=lastfmsubmitd
pkgver=1.0.6
pkgrel=2
pkgdesc="A Last.fm 'plugin' client for MPD, implemented in Python."
arch=('any')
url="http://www.red-bean.com/~decklin/software/lastfmsubmitd/"
license=('MIT')
depends=('python-mpdclient2')
makedepends=('python2')
optdepends=('mpd: For local audio playback')
backup=( etc/lastfmsubmitd.conf )
install=lastfmsubmitd.install
source=(http://www.red-bean.com/~decklin/software/$pkgname/$pkgname-$pkgver.tar.gz
	lastmp
	lastfmsubmitd
	lastmp.conf
	lastfmsubmitd.conf)
md5sums=('eadf6f8a7c3a5f8b6311a6fae5c117f6'
         'b8b51b54deea837250fba411f0983bda'
         '49559328f9e53eb840dcf0364afec818'
         'f331cb1fca46eb0b91be269babe49224'
         'c1ed7a40539e67998513968a9d8c6007')

package() {
  cd $srcdir/$pkgname-$pkgver

  mkdir -p $pkgdir/usr
  /usr/bin/python2 setup.py install --prefix=$pkgdir/usr --optimize=1
  install -D -m 600 ../lastfmsubmitd.conf $pkgdir/etc/lastfmsubmitd.conf
  install -D -m 600 ../lastmp.conf $pkgdir/etc/lastmp.conf
  install -D -m 755 ../lastfmsubmitd $pkgdir/etc/rc.d/lastfmsubmitd
  install -D -m 755 ../lastmp $pkgdir/etc/rc.d/lastmp
  install -D -m 755 contrib/lastmp $pkgdir/usr/bin/lastmp
  install -d $pkgdir/var/log/lastfm
  install -d $pkgdir/var/run/lastfm
  install -d $pkgdir/var/spool/lastfm
  install -d $pkgdir/var/cache/lastfm
  install -D -m644 README $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
