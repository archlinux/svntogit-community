# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=lat
pkgver=1.2.4
pkgrel=3
pkgdesc="LDAP Administration Tool"
arch=('any')
url="http://sourceforge.net/projects/ldap-at/"
license=("GPL")
depends=('gconf-sharp' 'libgnome-keyring')
makedepends=('intltool' 'rarian')
source=(http://downloads.sourceforge.net/ldap-at/lat-$pkgver.tar.gz
        drop-gnome-sharp.patch)
md5sums=('a09b07d585127fa0b9c8b484433bb705'
         'c302d0950b2b7d93f58e6981056135d6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Build for mono 4.0
  sed -i 's/gmcs/mcs/' configure{,.ac}

  # Remove gnome-sharp dependency
  patch -Np1 -i ../drop-gnome-sharp.patch

  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-avahi=yes
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
