# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=lat
pkgver=1.2.4
pkgrel=2
pkgdesc="LDAP Administration Tool"
arch=('any')
url="http://sourceforge.net/projects/ldap-at/"
license=("GPL")
depends=('libgnome-sharp' 'gconf-sharp')
makedepends=('intltool' 'rarian')
source=(http://downloads.sourceforge.net/ldap-at/lat-$pkgver.tar.gz)
md5sums=('a09b07d585127fa0b9c8b484433bb705')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Build for mono 4.0
  sed -i 's/gmcs/dmcs/' configure{,.ac}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-avahi=yes
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
