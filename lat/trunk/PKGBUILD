# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=lat
pkgver=1.2.3
pkgrel=3
pkgdesc="LDAP Administration Tool"
arch=('any')
url="http://sourceforge.net/projects/ldap-at/"
license=("GPL")
depends=('gnome-keyring' 'gtk-sharp-2' 'avahi' 'gnome-sharp' 'rarian')
makedepends=('gettext' 'intltool' 'pkgconfig')
install=lat.install
source=(http://downloads.sourceforge.net/ldap-at/lat-$pkgver.tar.gz)
md5sums=('0725508d180720d260a70c5b39d387ba')

build() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p $MONO_SHARED_DIR
  cd "$srcdir/lat-$pkgver"
  find . -name Makefile.in -exec sed -i -e 's/-scrollkeeper-update.*//' {} \;
  if [ -f omf.make ]; then
    sed -i -e 's/-scrollkeeper-update.*//' omf.make
  fi

  ./configure --prefix=/usr --enable-avahi=yes
  make
}

package() {
  cd "$srcdir/lat-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf $MONO_SHARED_DIR
}
