# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock
pkgver=0.10.8
pkgrel=2
pkgdesc='A dock based on Plasma Frameworks'
arch=(x86_64)
url='https://store.kde.org/p/1169519/'
license=(GPL)
depends=(plasma-framework knewstuff hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/latte-dock/-/commit/1d468a94.patch)
sha256sums=('00aab59d97b877f43292fc082c8bfe7f634b265cbee8ab5a2d2c78e0414c912a'
            'SKIP'
            '7fb90bbf22510e131986553592bb8b11df7241d4e6c6cc1191f03c9cc113ad81')
validpgpkeys=(325E97C32E601F5D4EADCF3A55999050A2D9110E) # Michail Vourlakos (mvourlakos) <mvourlakos@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 1d468a94.patch # Fix popup positions on Wayland
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
