# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=lazarus
pkgver=0.9.30.2
pkgrel=1
pkgdesc='Delphi-like IDE for FreePascal'
url='http://www.lazarus.freepascal.org/'
license=('GPL2' 'MPL' 'custom:LGPL')
arch=('i686' 'x86_64')
depends=('fpc' 'fpc-src' 'gtk2')
#makedepends=(rpmextract)
options=('!emptydirs' '!makeflags')
source=(http://downloads.sourceforge.net/project/lazarus/Lazarus%20Zip%20_%20GZip/Lazarus%20$pkgver/lazarus-$pkgver-src.tar.bz2)
md5sums=('0dcf54613c2f9d38a32d183431e2dfc9')

build() {
  cd $srcdir/${pkgname}
  make FPC=/usr/bin/fpc clean bigide
}

package() {
  cd $srcdir/${pkgname}

  # skip the 'make install' mess completely and do everything manually
  mkdir -p $pkgdir/usr/lib/lazarus $pkgdir/usr/bin $pkgdir/usr/share/man/man1 $pkgdir/usr/share/doc
  rm -r debian
  cp -R . $pkgdir/usr/lib/lazarus

  ln -s /usr/lib/lazarus/lazarus $pkgdir/usr/bin/lazarus
  ln -s /usr/lib/lazarus/startlazarus $pkgdir/usr/bin/startlazarus
  ln -s /usr/lib/lazarus/lazbuild $pkgdir/usr/bin/lazbuild

  cp -R install/man/man1/* $pkgdir/usr/share/man/man1/

  mv $pkgdir/usr/lib/lazarus/docs $pkgdir/usr/share/doc/lazarus

  # make 'desktop-file-validate' happy and fix missing .png icon
  sed -e 's|\(Categories\).*|\1=IDE;Development;|' \
      -e 's|\.png|\.xpm|' -i install/lazarus.desktop
  install -Dm644 install/lazarus.desktop $pkgdir/usr/share/applications/lazarus.desktop
  install -Dm644 images/ide_icon48x48.png $pkgdir/usr/share/pixmaps/lazarus.png
  rm -r $pkgdir/usr/lib/lazarus/install

  # license files: /usr/lib/lazarus/COPYING*
  install -D -m644 COPYING.modifiedLGPL.txt $pkgdir/usr/share/licenses/$pkgname/COPYING.modifiedLGPL

  # strip
  find $pkgdir -perm /ugo+x -type f -exec strip {} \;
  find $pkgdir -name \*.so -type f -exec strip {} \;
}
