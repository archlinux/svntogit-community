# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=ldc
pkgver=0.11.0
epoch=1
pkgrel=1
pkgdesc="A D Compiler based on the LLVM Compiler Infrastructure including D runtime and libphobos2"
arch=('i686' 'x86_64')
url="http://www.dsource.org/projects/ldc"
license=('BSD')
depends=('llvm' 'libconfig')
makedepends=('git' 'cmake')
backup=('etc/ldc2.conf')
source=("git://github.com/ldc-developers/ldc.git#tag=v${pkgver}")
sha1sums=('SKIP')

[[ $CARCH == "x86_64" ]] && _multilib="ON"
[[ $CARCH == "i686" ]] && _multilib="OFF"

build() {
  cd "$srcdir"/ldc
  git submodule update --init --recursive

  mkdir build && cd build
  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DINCLUDE_INSTALL_DIR=/usr/include/d/std-ldc \
      -DMULTILIB=$_multilib \
      ..
  make
}

package() {
  cd "$srcdir"/ldc

  cd build
  make DESTDIR=$pkgdir install
  cd ..

  mkdir -p $pkgdir/usr/share/bash-completion/
  mv $pkgdir/etc/bash_completion.d $pkgdir/usr/share/bash-completion/completions

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE

  # We don't actually want the multilib libs in this package
  rm -rf $pkgdir/usr/lib32
}
