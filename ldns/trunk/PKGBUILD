# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: jiribb <jiribb@gmail.com> and <mathieui> <mathieui@mathieui.net>
# Contributor: mathieui <mathieui@mathieui.net>

pkgname=ldns
pkgver=1.6.8
pkgrel=1
pkgdesc='Fast DNS library with DNSSEC support, including tools such as Drill'
arch=('i686' 'x86_64')
url='http://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
depends=('openssl')
makedepends=('libpcap')
optdepends=('libpcap: ldns-dpa tool')
options=('!libtool')
source=("http://www.nlnetlabs.nl/downloads/$pkgname/$pkgname-$pkgver.tar.gz")
sha1sums=('e736160f36b7dcc0303269d3c0a9be88b7ad10c4')

build() {
	_dirs='--prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-rpath'

	cd "$srcdir/$pkgname-$pkgver"
	./configure $_dirs
	make
	make DESTDIR="$pkgdir" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	cd drill
	./configure $_dirs --with-ldns="$pkgdir"
	make
	make DESTDIR="$pkgdir" install

	cd ../examples
	./configure $_dirs --with-ldns="$pkgdir"
	make
	make DESTDIR="$pkgdir" install
}
