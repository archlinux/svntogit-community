# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=leafnode
pkgver=1.11.8
pkgrel=5
pkgdesc='Implements a store & forward NNTP proxy (client and server) with IPv4 and IPv6'
arch=('i686' 'x86_64')
url="http://leafnode.sourceforge.net/"
depends=('pcre' 'xinetd')
license=('GPL')
install=leafnode.install
options=(!emptydirs)
backup=('etc/xinetd.d/leafnode')
source=(http://downloads.sf.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
	leafnode.xinetd)
md5sums=('a3edafeb854efaa3fbb0f7951d02160f'
         'db3c6ad1ff9fc03442949456d1d21304')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --localstatedir=/var --sysconfdir=/etc/leafnode \
	--with-user=9 \
	--with-group=13
  sed -i 's|#define NEWS_USER .*|#define NEWS_USER "news"|' config.h
  sed -i 's|#define NEWS_GROUP .*|#define NEWS_GROUP "news"|' config.h
  make
}
package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  chown -R root.root $pkgdir
  install -D -m0644 $srcdir/leafnode.xinetd $pkgdir/etc/xinetd.d/leafnode
}
