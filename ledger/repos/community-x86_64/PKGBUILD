# Maintainer: Ivy Foster <code@iff.ink>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Abdelhakim Qbaich <abdelhakim@qbaich.com>
# Contributor: korjjj <korjjj+aur@gmail.com>
# Contributor: TDY <tdy@archlinux.info>
# Contributor: Adam Ehlers Nyholm Thomsen <adament@gmail.com>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=ledger
pkgver=3.1.3
pkgrel=6
pkgdesc='Double-entry accounting system with a command-line reporting interface'
arch=(i686 x86_64)
url='https://ledger-cli.org'
license=(BSD)
depends=(boost-libs libedit mpfr)
makedepends=(boost cmake)
optdepends=('emacs: emacs interface')
source=("ledger-$pkgver.tar.gz::https://github.com/ledger/ledger/archive/v$pkgver.tar.gz")
sha256sums=(b248c91d65c7a101b9d6226025f2b4bf3dabe94c0c49ab6d51ce84a22a39622b)

build() {
	cd "ledger-$pkgver"

	cmake . \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DCMAKE_INSTALL_LIBDIR:PATH=lib \
		-DUSE_PYTHON:BOOL=FALSE \
		-DBUILD_EMACSLISP:BOOL=TRUE
	make
}

check() {
	make -C "ledger-$pkgver" check
}

package() {
	cd "ledger-$pkgver"

	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/ledger/LICENSE.md"
}
