# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.9.8
pkgrel=2
pkgdesc='Automate Clojure projects'
arch=(any)
url='https://github.com/technomancy/leiningen'
license=(EPL)
depends=(java-environment java-runtime)
makedepends=(git)
optdepends=('leiningen-completions: completion scripts'
            'rlwrap: for readline support')
source=("git+$url#commit=d11e193905d1237428fe705b56c2ee95be46fba9" # tag: 2.9.8
        "$url/releases/download/$pkgver/leiningen-$pkgver-standalone.jar"{,.asc})
validpgpkeys=('6A2D483DB59437EBB97D09B1040193357D0606ED') # Phil Hagelberg <phil@hagelb.org>
b2sums=('SKIP'
        '6c9bc9a2f990bb4959aa06f8f8b31c1084f0d2bad2e19c8343fb7e8a02acb6f6173737cbf36b028e113577ef26e380d7ace035316dee840c6da92512afa7ac0b'
        'SKIP')

package() {
  install -Dm755 $pkgname/bin/lein "$pkgdir/usr/bin/lein"
  install -Dm644 $pkgname/doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 "$srcdir/leiningen-$pkgver-standalone.jar" \
                 "$pkgdir/usr/share/java/leiningen-$pkgver-standalone.jar"
}

# getver: github.com/technomancy/leiningen/releases
