# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=leptonica
pkgver=1.69
pkgrel=9
pkgdesc="Software that is broadly useful for image processing and image analysis applications"
arch=('i686' 'x86_64')
url="http://www.leptonica.com/"
license=('custom')
depends=('giflib' 'libjpeg' 'libpng' 'libtiff' 'zlib' 'libwebp')
source=(http://www.leptonica.com/source/leptonica-${pkgver}.tar.gz
	giflib6.patch)
md5sums=('d4085c302cbcab7f9af9d3d6f004ab22'
         'f87e1b2c98107fe51544d4f4c44a9733')

prepare() {
  cd ${srcdir}/leptonica-${pkgver}
  patch -p1 <$srcdir/giflib6.patch
}

build() {
  cd ${srcdir}/leptonica-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/leptonica-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D leptonica-license.txt ${pkgdir}/usr/share/licenses/leptonica/leptonica-license.txt
}
