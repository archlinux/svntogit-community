# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=letshelp-certbot
pkgver=0.6.0
pkgrel=1
pkgdesc="Let's help Let's Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('python2-setuptools' 'python2-mock')
checkdepends=('python2-nose')
source=("https://pypi.io/packages/source/l/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
md5sums=('ee1db9c3934802d651ae899947cd3de4'
         'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python2 setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  nosetests2
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python2 setup.py install -O1 --root="$pkgdir"
}
