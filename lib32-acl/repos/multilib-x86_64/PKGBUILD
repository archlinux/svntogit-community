# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=lib32-acl
pkgver=2.2.52
pkgrel=3
pkgdesc="Access control list libraries (32-bit)"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
depends=('lib32-attr>=2.4.46' 'acl')
makedepends=('gcc-multilib')
source=(https://download.savannah.gnu.org/releases/acl/acl-${pkgver}.src.tar.gz{,.sig})
sha256sums=('179074bb0580c06c4b4137be4c5a92a701583277967acdb5546043c7874e0d23'
            'SKIP')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34') # Brandon Philips <brandon@ifup.co>

build() {
  cd "${srcdir}/acl-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export INSTALL_USER=root INSTALL_GROUP=root 
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32
  make 
}

package() {
  cd "${srcdir}/acl-${pkgver}"

  make DIST_ROOT="${pkgdir}" install install-lib install-dev

  # tidy up
  rm -f "${pkgdir}"/lib32/libacl.a
  chmod 0755 "$pkgdir"/usr/lib32/libacl.so.*.*.*

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
