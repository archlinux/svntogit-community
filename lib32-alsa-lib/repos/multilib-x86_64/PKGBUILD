# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-lib
pkgname=lib32-alsa-lib
pkgver=1.2.7
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
depends=(lib32-glibc "alsa-lib=$pkgver")
provides=(libasound.so libatopology.so)
options=(debug)
source=(
  https://www.alsa-project.org/files/pub/lib/$_name-$pkgver.tar.bz2{,.sig}
  $pkgname-1.2.7-fix_decoding_for_older_hardware.patch::https://github.com/alsa-project/alsa-lib/commit/89ee61914756a6f8bcafbad7fb1eca674b0a012f.patch
)
sha512sums=('6bd48b07d659fddad792876326a93b2a549ce94bc191131670365599c31f81f737bfd8e94dec6fe296bc5d86df2e3e381bbb7a694d24fe7e8e501f1c994ed084'
            'SKIP'
            '50b9036e58720ba7e726a73d94d99dc557c777682f3d977e51a5fd31cb3b54dfd33ab9567546ba59fa7bfa8c00c6db5c33d6b04fd1c6e27679fde22435636390')
b2sums=('a3bfbbfcf31625be0749218b3c02b307da9077d4bd82e9f980b8e3d6f9275f3a3f3c8158bb28fb1c61724a35e794eb3f6cc8872202ff42fb847725058109087e'
        'SKIP'
        '46328cbaac187222b57efc86e10db67d58499a103affbadbe8b60b5c3b226bd18702c7b5ffb80dc4ced5c0e3e9dacdb447d15db5afb1a50d9a26e31e7426adf5')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  # fix issues with older hardware: https://github.com/alsa-project/alsa-lib/issues/233
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-1.2.7-fix_decoding_for_older_hardware.patch

  cd $_name-$pkgver
  autoreconf -fiv
}

build() {
  # -flto=auto is not supported: https://github.com/alsa-project/alsa-lib/issues/110
  CFLAGS+=" -flto-partition=none"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  cd $_name-$pkgver
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --without-debug \
    --disable-python
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check -C $_name-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $_name-$pkgver
  rm -r "$pkgdir"/usr/{bin,include,share}
}
