# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Mikhail Vorozhtsov <mikhail.vorozhtsov@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-alsa-lib
pkgver=1.0.23
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
url="http://www.alsa-project.org"
license=(GPL)
options=(!libtool)
groups=(lib32)
provides=("lib32-alsa-lib-fixed=$pkgver-$pkgrel")

if [[ $CARCH == i686 ]]; then
  _pkgname32=${pkgname/lib32-/}
  arch=(i686)
  depends=(glibc)
  source=(ftp://ftp.alsa-project.org/pub/lib/$_pkgname32-$pkgver.tar.bz2)
  md5sums=('f48b50421d8a69d2d806d9c47e534f0d')

  build() {
    cd "$srcdir/$_pkgname32-$pkgver"
    ./configure \
      --prefix=/usr \
      --exec-prefix=/opt/lib32/usr \
      --disable-python
    make || return 1
  }

  package() {
    cd "$srcdir/$_pkgname32-$pkgver"

    make DESTDIR="$pkgdir" install

    rm -rf "$pkgdir"/{usr,opt/lib32/usr/{bin,lib/pkgconfig}}
  }
else
  arch=(x86_64)
  depends=(lib32-glibc)
  source=($pkgname-$pkgver-$pkgrel-i686.pkg.tar.xz)
  md5sums=('e156a24b1f74ea1e56bbf0ddaf8d834a')

  build() {
    cp -a "$srcdir/opt" "$pkgdir/opt"
  }
fi
