# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-lib
pkgname=lib32-alsa-lib
pkgver=1.2.3.2
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('lib32-glibc' "alsa-lib=${pkgver}")
provides=('libasound.so' 'libatopology.so')
source=("https://www.alsa-project.org/files/pub/lib/${_name}-$pkgver.tar.bz2")
sha512sums=('1fbc6360fda841bd9ca488739bdc9f4142c1b4a07ff767f48f1e160e3d4dff914aed422c97088e238b5e77d7e30aa79ff72569c3348a4cf4a412e1e4bce0bf2a')
b2sums=('70ba972953f71fc6ac20f6f3e91361b5aed72c8c7887252331a01334ec4cc154c1bfea02dfaf0f76f8244b8cf73426d83577dcf55aa0033f80ee303d23386df6')

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -vfi
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --without-debug \
              --disable-python
  make
}

check() {
  cd "${_name}-$pkgver"
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
