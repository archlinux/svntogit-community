# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=alsa-plugins
pkgname=lib32-$_pkgbasename
pkgver=1.0.23
pkgrel=6
pkgdesc="Extra alsa plugins (32-bit)"
arch=(x86_64)
url="http://www.alsa-project.org"
license=(GPL)
depends=(lib32-alsa-lib $_pkgbasename)
makedepends=(lib32-{pulseaudio,jack,speex,libsamplerate} gcc-multilib)
optdepends=('lib32-pulseaudio: PulseAudio plugin'
            'lib32-jack: Jack plugin'
            'lib32-libsamplerate: libsamplerate resampling plugin'
            'lib32-speex: libspeexdsp resampling plugin')
options=('!libtool')
source=(ftp://ftp.alsa-project.org/pub/plugins/$pkgname-$pkgver.tar.bz2
        Fix_invalid_buffer_pointer_return_value.patch
        Add_handle_underrun_option.patch)
md5sums=('a671f8102366c5b388133e948e1c85cb'
         '067a51a5157a42989f60078d5225b6c1'
         '56fb0e9cbef9714504ecaa1bb62d7acf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # Improve pulse underrun handling
  patch -Np1 -i "$srcdir/Fix_invalid_buffer_pointer_return_value.patch"
  patch -Np1 -i "$srcdir/Add_handle_underrun_option.patch"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-avcodec
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
}
