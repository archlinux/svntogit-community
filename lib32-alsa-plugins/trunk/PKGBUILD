# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=alsa-plugins
pkgname=lib32-$_pkgbasename
pkgver=1.0.23
pkgrel=5
pkgdesc="Extra alsa plugins (32-bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
license=('GPL')
depends=('lib32-alsa-lib' $_pkgbasename)
makedepends=(lib32-{pulseaudio,jack,speex,libsamplerate} gcc-multilib)
optdepends=(lib32-{pulseaudio,jack,speex,libsamplerate})
options=('!libtool')
source=(ftp://ftp.alsa-project.org/pub/plugins/alsa-plugins-$pkgver.tar.bz2)
md5sums=('a671f8102366c5b388133e948e1c85cb')

build() {
  cd $srcdir/alsa-plugins-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-avcodec
  make
}

package() {
  cd $srcdir/alsa-plugins-$pkgver
  make DESTDIR=$pkgdir install
}
