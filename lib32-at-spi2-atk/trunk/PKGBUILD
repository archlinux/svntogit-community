# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor:Ionut Biru <ibiru@archlinux.org> 

pkgname=lib32-at-spi2-atk
pkgver=2.30.1
pkgrel=1
pkgdesc='A GTK+ module that bridges ATK to D-Bus at-spi'
arch=(x86_64)
url=https://wiki.gnome.org/Accessibility
license=(GPL2)
depends=(
  at-spi2-atk
  lib32-at-spi2-core
  lib32-atk
  lib32-libx11
  lib32-libxml2
)
makedepends=(
  git
  meson
)
source=(git+https://gitlab.gnome.org/GNOME/at-spi2-atk.git#tag=AT_SPI2_ATK_${pkgver//./_})
sha256sums=(SKIP)

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson at-spi2-atk build \
    --libdir=/usr/lib32
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -r "${pkgdir}"/usr/{include,lib32/gnome-settings-daemon-3.0}
}

# vim: ts=2 sw=2 et:
