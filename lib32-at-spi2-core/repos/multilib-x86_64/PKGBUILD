# $Id$
# Maintainer:m Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-at-spi2-core
pkgver=2.26.1
pkgrel=1
pkgdesc='Protocol definitions and daemon for D-Bus at-spi'
arch=('x86_64')
url='https://www.gnome.org'
license=('GPL2')
depends=('at-spi2-core' 'lib32-dbus' 'lib32-glib2' 'lib32-libxtst')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool')
source=("https://download.gnome.org/sources/at-spi2-core/${pkgver%.*}/at-spi2-core-${pkgver}.tar.xz")
sha256sums=('28822d5f39387ef60e5374532dde1a0d04d39847a3d484aa7d6c634247a535e2')

prepare() {
  cd at-spi2-core-${pkgver}

  autoreconf -fi
}

build() {
  cd at-spi2-core-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/at-spi2-core' \
    --sysconfdir='/etc' \
    --disable-xevie
  make
}

package() {
  cd at-spi2-core-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim: ts=2 sw=2 et:
