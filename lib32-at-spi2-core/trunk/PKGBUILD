# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-at-spi2-core
pkgname=(lib32-at-spi2-core)
pkgver=2.46.0
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
arch=(x86_64)
license=(GPL2)
depends=(
  at-spi2-core
  lib32-dbus
  lib32-glib2
  lib32-libx11
  lib32-libxml2
  lib32-libxtst
)
makedepends=(
  dbus-broker
  git
  meson
)
options=(debug)
_commit=97d9ba04e19d0a9edd209d97db81eb6ede5fe4d6  # tags/AT_SPI2_CORE_2_46_0^0
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd at-spi2-core
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd at-spi2-core
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson at-spi2-core build \
    --libdir=/usr/lib32 \
    -D introspection=no \
    -D default_bus=dbus-broker \
    -D docs=false
  meson compile -C build
}

check() {
  # memory test fails without desktop
  # broker fails to launch without journald
  dbus-run-session meson test -C build --print-errorlogs || :
}

package_lib32-at-spi2-core() {
  provides=(
    lib32-at-spi2-atk
    lib32-atk
    libatk-1.0.so
    libatk-bridge-2.0.so
    libatspi.so
  )
  replaces=(
    'lib32-at-spi2-atk<=2.38.0-2'
    'lib32-atk<=2.38.0-1'
  )

  meson install -C build --destdir "$pkgdir"

  rm -r "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim:set sw=2 sts=-1 et:
