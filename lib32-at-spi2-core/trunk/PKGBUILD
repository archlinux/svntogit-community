# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-at-spi2-core
pkgver=2.44.1
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
url="https://gitlab.gnome.org/GNOME/at-spi2-core"
arch=(x86_64)
license=(GPL2)
depends=(
  at-spi2-core
  lib32-dbus
  lib32-glib2
  lib32-libxtst
)
makedepends=(
  git
  meson
)
options=(debug)
_commit=9a1f0aec0b7dc55360310b6d02c2b7798511268f  # tags/AT_SPI2_CORE_2_44_1^0
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-core.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd at-spi2-core
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson at-spi2-core build \
    --libdir=/usr/lib32 \
    -D introspection=no \
    -D docs=false
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim:set sw=2 et:
