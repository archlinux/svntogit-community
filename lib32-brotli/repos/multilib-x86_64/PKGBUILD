# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: TingPing <tingping@tingping.se>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=lib32-brotli
pkgver=1.0.9
pkgrel=5
pkgdesc='Brotli compression library'
arch=(x86_64)
license=(MIT)
url=https://github.com/google/brotli
depends=(
  brotli
  lib32-gcc-libs
)
makedepends=(
  cmake
  git
)
provides=(
  libbrotli{common,dec,enc}.so
)
options=(
  debug
)
_tag=e61745a6b7add50d380cfd7d3883dd6c62fc2c71
source=(git+https://github.com/google/brotli#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd brotli

  git describe --tags | sed 's/^v//'
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  cmake -S brotli -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib32
  cmake --build build -v
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -rf "${pkgdir}"/usr/{bin,include,lib32/*.a}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s brotli "${pkgdir}"/usr/share/licenses/lib32-brotli
}

# vim:set sw=2 sts=-1 et:
