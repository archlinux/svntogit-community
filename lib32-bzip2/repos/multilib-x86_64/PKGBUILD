# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: TryA <tryagainprod {at} gmail.com>

_pkgbasename=bzip2
pkgname=lib32-bzip2
pkgver=1.0.7
pkgrel=1
pkgdesc="A high-quality data compression program (32-bit)"
arch=('x86_64')
license=('custom')
url="https://sourceware.org/bzip2/"
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
source=(https://sourceware.org/pub/bzip2/$_pkgbasename-$pkgver.tar.gz{,.sig}
        bzip2-ldflags.patch)
sha512sums=('e0e19b493e6b1f7beeb0eeb0be8a6358c24202173f28acb1e902a768835be9e24f2cb966452fbc90fc3e4e692532ce0c7e86d06aef2d52c0d2a9ac16e12ec8c8'
            'SKIP'
            'e9b5e247c727279e9b8711fb6a1c2bb843470e292ea5e5b596d2803b9fa39633d4726b984b5a8c399f55f4d8d6635b2ca7b479b41a22402300f3edde2df798a4')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>

prepare() {
  cd ${_pkgbasename}-${pkgver}

  sed -i "s|CC=gcc|CC=gcc -m32|" Makefile
  sed -i "s|CC=gcc|CC=gcc -m32|" Makefile-libbz2_so

  # use our LDFLAGS
  sed -e "s|-O2|${CFLAGS}|g" -i Makefile*
  sed -e "s|LDFLAGS=|LDFLAGS=${LDFLAGS}|g" -i Makefile*
  patch -p1 -i ../bzip2-ldflags.patch
}

build() {
  cd ${_pkgbasename}-${pkgver}
 
  make -f Makefile-libbz2_so
  make libbz2.a
}

package(){
  cd ${_pkgbasename}-${pkgver}

  install -dm755 "$pkgdir"/usr/lib32
  cp -a libbz2.so* "$pkgdir"/usr/lib32
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib32/libbz2.so

  install -Dm644 libbz2.a "$pkgdir"/usr/lib32/libbz2.a

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
