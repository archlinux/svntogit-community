# Maintainer: Daniel Wallace <danielwallace@aur.archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=lib32-curl
pkgname=(lib32-curl lib32-libcurl-compat lib32-libcurl-gnutls)
pkgver=7.83.1
pkgrel=1
pkgdesc='An URL retrieval utility and library (32-bit)'
arch=('x86_64')
url='https://curl.haxx.se'
license=('MIT')
depends=('curl' 'lib32-brotli' 'lib32-libidn2' 'lib32-libssh2' 'lib32-krb5'
         'lib32-libpsl' 'lib32-zlib' 'lib32-zstd')
makedepends=('lib32-gnutls' 'lib32-openssl')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha512sums=('0b909b7ed55f9a9789584fd9d2033d5838dcf29e33adf6657258e97ebe7c91b26282007687a729c8385594fd8220bd718fd008154926b87b8da254d586fab3c7'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

_configure_options=(
    --prefix='/usr'
    --disable-dependency-tracking
    --disable-ldap
    --disable-ldaps
    --enable-ipv6
    --enable-manual
    --enable-threaded-resolver
    --with-gssapi
    --with-libssh2
    --with-openssl
    --with-random='/dev/urandom'
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'
    --libdir='/usr/lib32'
)

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  mkdir build-curl{,-compat,-gnutls}

  # build lib32-curl
  cd "${srcdir}"/build-curl

  "${srcdir}/curl-${pkgver}"/configure \
    "${_configure_options[@]}" \
    --enable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
  make libcurl.pc

  # build lib32-libcurl-compat
  cd "${srcdir}"/build-curl-compat

  "${srcdir}/curl-${pkgver}"/configure \
    "${_configure_options[@]}" \
    --disable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib

  # build lib32-libcurl-gnutls
  cd "${srcdir}"/build-curl-gnutls

  "${srcdir}/curl-${pkgver}"/configure \
    "${_configure_options[@]}" \
    --disable-versioned-symbols \
    --without-ssl \
    --with-gnutls='/usr'
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
}

package_lib32-curl() {
  depends+=('lib32-openssl')

  cd "${srcdir}"/build-curl

  make -C lib DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pkgconfigDATA

  # license
  install -d "${pkgdir}/usr/share/licenses"
  ln -s curl "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-libcurl-compat() {
  pkgdesc='An URL retrieval utility and library (32-bit, without versioned symbols)'
  depends+=('lib32-openssl')

  cd "${srcdir}"/build-curl-compat

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-compat}.so.4.8.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0 4.7.0; do
    ln -s libcurl-compat.so.4.8.0 "${pkgdir}"/usr/lib32/libcurl.so.${version}
  done

  # license
  install -d "${pkgdir}/usr/share/licenses"
  ln -s curl "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lib32-libcurl-gnutls() {
  pkgdesc='An URL retrieval utility and library (32-bit, without versioned symbols and linked against gnutls)'
  depends+=('lib32-gnutls')

  cd "${srcdir}"/build-curl-gnutls

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-gnutls}.so.4.8.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0 4.7.0; do
    ln -s libcurl-gnutls.so.4.8.0 "${pkgdir}"/usr/lib32/libcurl-gnutls.so.${version}
  done

  # license
  install -d "${pkgdir}/usr/share/licenses"
  ln -s curl "${pkgdir}/usr/share/licenses/${pkgname}"
}
