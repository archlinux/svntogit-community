# Contributor : Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-dbus
pkgver=1.12.16
pkgrel=2
pkgdesc="Freedesktop.org message bus system (32-bit)"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(GPL custom)
depends=(lib32-systemd dbus)
makedepends=(lib32-glib2 lib32-expat git autoconf-archive)
_commit=23cc709db8fab94f11fa48772bff396b20aea8b0  # tags/dbus-1.12.16^0
source=("git+https://gitlab.freedesktop.org/dbus/dbus.git#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'  # Simon McVittie <simon.mcvittie@collabora.co.uk>
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059') # Simon McVittie <simon.mcvittie@collabora.co.uk>

pkgver() {
  cd dbus
  git describe --tags | sed 's/^dbus-//;s/-/+/g'
}

prepare() {
  cd dbus
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cd dbus
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib/dbus-1.0 \
    runstatedir=/run \
    --with-console-auth-dir=/run/console/ \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-inotify \
    --enable-systemd \
    --enable-user-session \
    --disable-static \
    --disable-verbose-mode \
    --disable-asserts \
    --disable-checks \
    --without-x
  make
}

check() {
  make -C dbus check
}

package() {
  provides=(lib32-libdbus libdbus-1.so)
  conflicts=(lib32-libdbus)
  replaces=(lib32-libdbus)

  DESTDIR="$pkgdir" make -C dbus install

  rm -r "$pkgdir"/{etc,var}
  rm -r "$pkgdir"/usr/{bin,include,lib,share}

  install -d "$pkgdir/usr/share/licenses"
  ln -s dbus "$pkgdir/usr/share/licenses/$pkgname"
}
