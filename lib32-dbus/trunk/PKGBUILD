# Contributor : Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-dbus
_pkgbasename=dbus
pkgver=1.12.14
pkgrel=1
pkgdesc="Freedesktop.org message bus system (32-bit)"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(GPL custom)
provides=(lib32-libdbus)
conflicts=(lib32-libdbus)
replaces=(lib32-libdbus)
depends=(lib32-systemd dbus)
makedepends=(lib32-glib2 lib32-expat git autoconf-archive)
_commit=a330c6184fe9c7f67495f8d4563b11d51a6dccc7  # tags/dbus-1.12.14^0
source=("git+https://gitlab.freedesktop.org/dbus/dbus.git#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90'  # Simon McVittie <simon.mcvittie@collabora.co.uk>
              '3C8672A0F49637FE064AC30F52A43A1E4B77B059') # Simon McVittie <simon.mcvittie@collabora.co.uk>

pkgver() {
  cd dbus
  git describe --tags | sed 's/^dbus-//;s/-/+/g'
}

prepare() {
  cd dbus
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd dbus
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib/dbus-1.0 \
    runstatedir=/run \
    --with-console-auth-dir=/run/console/ \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-inotify \
    --enable-systemd \
    --enable-user-session \
    --disable-static \
    --disable-verbose-mode \
    --disable-asserts \
    --disable-checks \
    --without-x
  make
}

check() {
  make -C dbus check
}

package() {
  DESTDIR="$pkgdir" make -C dbus install

  rm -r "$pkgdir"/usr/{bin,include,lib,share}
  rm -r "$pkgdir"/{etc,var}

  install -d "$pkgdir/usr/share/licenses"
  ln -s dbus "$pkgdir/usr/share/licenses/$pkgname"
}
