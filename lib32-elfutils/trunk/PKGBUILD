# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_pkgbasename=elfutils
pkgname=lib32-elfutils
pkgver=0.161
pkgrel=1
pkgdesc="Collection of libraries for working with ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('lib32-bzip2' 'lib32-zlib' 'elfutils')
makedepends=('gcc-multilib')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig}
        0001-tests-Make-deleted-and-vdsosyms-testcases-work-with-.patch)
options=('staticlibs')
sha1sums=('85d48e18359c51e843c49b1894b2f54b85e88ae2'
          'SKIP'
          '86947fb8d0f51a65e19142350925f428ad0c7cb1')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A') # Mark J. Wielaard <mark@klomp.org>

prepare() {
  cd ${_pkgbasename}-${pkgver}

  # https://lists.fedorahosted.org/pipermail/elfutils-devel/2015-January/004541.html
  patch -p1 < "$srcdir"/0001-tests-Make-deleted-and-vdsosyms-testcases-work-with-.patch
}

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
	
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
