# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_pkgbasename=elfutils
pkgname=lib32-elfutils
pkgver=0.157
pkgrel=1
pkgdesc="Collection of libraries for working with ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('lib32-bzip2' 'lib32-zlib' 'elfutils')
makedepends=('gcc-multilib')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2)
sha1sums=('5e3767206c7f1b3bcfe33bce99fe09686ef165d2')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
	
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make check
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
