# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_pkgbasename=elfutils
pkgname=lib32-elfutils
pkgver=0.158
pkgrel=2
pkgdesc="Collection of libraries for working with ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('lib32-bzip2' 'lib32-zlib' 'elfutils')
makedepends=('gcc-multilib')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2
        fix-run-backtrace-native-core-test.patch
	CVE-2014-0172.patch)
sha1sums=('09adbbf0f3a35bb1bcb77c2eaa40de8d3443af4d'
          '8ecef640f3d1229cdf45ffda016a69848c18e61b'
          '3e776c07d6ca2c7604a384d266f79c3ece1fb179')

prepare() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  patch -Np1 -i ../fix-run-backtrace-native-core-test.patch
  # merged upstream
  patch -Np1 -i ../CVE-2014-0172.patch
}

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
	
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

#check() {
#  cd ${srcdir}/${_pkgbasename}-${pkgver}
#
#  make check
#}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
