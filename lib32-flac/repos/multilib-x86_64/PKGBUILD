# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=flac
pkgname=lib32-$_pkgbasename
pkgver=1.3.2
pkgrel=1
pkgdesc="Free Lossless Audio Codec (32-bit)"
arch=('x86_64')
url="http://flac.sourceforge.net/"
license=('BSD' 'GPL')
depends=('lib32-libogg' 'lib32-gcc-libs' $_pkgbasename)
makedepends=('nasm' gcc-multilib)
options=('!makeflags')
source=(http://downloads.xiph.org/releases/flac/${_pkgbasename}-${pkgver}.tar.xz)
sha1sums=('2bdbb56b128a780a5d998e230f2f4f6eb98f33ee')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --disable-sse --libdir=/usr/lib32
  make
}

package() {
  cd "${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
