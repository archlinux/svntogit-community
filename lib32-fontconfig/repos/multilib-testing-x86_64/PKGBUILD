# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-fontconfig
pkgver=2.14.1
pkgrel=2
epoch=2
pkgdesc="Library for configuring and customizing font access"
url=https://www.freedesktop.org/wiki/Software/fontconfig/
arch=(x86_64)
license=(custom)
depends=(
  fontconfig
  lib32-expat
  lib32-freetype2
)
makedepends=(
  git
  gperf
  meson
)
install=fontconfig-32.install
options=(debug)
_commit=c45e09df1ef235d653d56aef05012f6a3cc57979  # tags/2.14.1^0
source=(
  "git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#commit=$_commit"
  fontconfig-32.hook
)
b2sums=('SKIP'
        '1cba71810c9bde6ecb6fac124e458fb7260be3ea72ade82b836e0e8e1eaa7c7df31e6e92e405fa420325cec0ce14d0f19630e777308032b0c26ec96a3d668d93')

prepare() {
  cd fontconfig
}

pkgver() {
  cd fontconfig
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  local meson_options=(
    --libdir=/usr/lib32
    -D doc=disabled
  )

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson fontconfig build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libexpat.so
    libfreetype.so
  )
  provides+=(libfontconfig.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/{etc,usr/{include,share}}

  mv "$pkgdir"/usr/bin/fc-cache{,-32}
  find "$pkgdir"/usr/bin -type f -not -name '*-32' -delete

  install -Dm644 *.hook -t "$pkgdir/usr/share/libalpm/hooks"
  install -Dm644 fontconfig/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 sts=-1 et:
