# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-fontconfig
pkgver=2.13.95
pkgrel=1
epoch=2
pkgdesc='Library for configuring and customizing font access'
url=https://www.freedesktop.org/wiki/Software/fontconfig/
arch=(x86_64)
license=(custom)
depends=(
  fontconfig
  lib32-expat
  libfreetype.so
)
makedepends=(
  git
  meson
  gperf
  lib32-freetype2
)
provides=(libfontconfig.so)
install=fontconfig-32.install
_tag=8cdddb07a5c414b346b8a20c2c511e00bd9050d9
source=(
  git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#tag=${_tag}
  fontconfig-32.hook
)
b2sums=('SKIP'
        '1cba71810c9bde6ecb6fac124e458fb7260be3ea72ade82b836e0e8e1eaa7c7df31e6e92e405fa420325cec0ce14d0f19630e777308032b0c26ec96a3d668d93')

prepare() {
  cd fontconfig
}

pkgver() {
  cd fontconfig
  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config
  arch-meson fontconfig build \
    --libdir=/usr/lib32 \
    -D doc=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -r "${pkgdir}"/{etc,usr/{include,share}}
  mv "${pkgdir}"/usr/bin/fc-cache{,-32}
  find "${pkgdir}"/usr/bin -type f -not -name '*-32' -delete
  install -Dm 644 *.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s fontconfig "${pkgdir}"/usr/share/licenses/lib32-fontconfig
}

# vim:set sw=2 et:
