# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
_pkgbasename=fontconfig
pkgname=lib32-$_pkgbasename
pkgver=2.8.0
pkgrel=3
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=(x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('lib32-expat>=2.0.1' 'lib32-freetype2>=2.3.11' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://www.fontconfig.org/release/${_pkgbasename}-${pkgver}.tar.gz
        30-urw-aliases.patch)
md5sums=('77e15a92006ddc2adbb06f840d591c0e'
         '51bc6cb633b50f3c28793361738aac5b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/30-urw-aliases.patch"
  # enable Position Independent Code for prelinking
  export CFLAGS="${CFLAGS} -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir"/{etc,var}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
