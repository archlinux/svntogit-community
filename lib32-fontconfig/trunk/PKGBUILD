# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=fontconfig
pkgname=lib32-$_pkgbasename
pkgver=2.10.92
pkgrel=1
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=(x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('lib32-expat' 'lib32-freetype2' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://www.fontconfig.org/release/${_pkgbasename}-${pkgver}.tar.bz2
        git_fixes.diff)
sha256sums=('1162059927120f2321f12917189b524e6b2f09e11ac643278d0bb82634035b22'
            '7dc98137405ee0e5f98f3266a760a4da27d78e3c2bc7b7ef4db6b481df3c39f0')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # post release fixes
  patch -Np1 -i ${srcdir}/git_fixes.diff

  # make sure there's no rpath trouble and sane .so versioning - FC and Gentoo do this as well
  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
	  --sysconfdir=/etc \
	  --with-templatedir=/etc/fonts/conf.avail \
	  --with-xmldir=/etc/fonts \
	  --localstatedir=/var \
	  --disable-static \
	  --with-default-fonts=/usr/share/fonts \
	  --with-add-fonts=/usr/share/fonts \
	  --libdir=/usr/lib32

  make
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make -k check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir"/{etc,var}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
