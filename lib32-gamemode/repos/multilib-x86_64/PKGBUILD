# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ysblokje <ysblokje@gmail.com>
# Contributor: Mark Wagie <mark.wagie@tutanota.com>

pkgname=lib32-gamemode
pkgver=1.6.1
pkgrel=2
pkgdesc='A daemon/lib combo that allows games to request a set of optimisations be temporarily applied to the host OS'
arch=(x86_64)
url=https://github.com/FeralInteractive/gamemode
license=(BSD)
depends=(
  lib32-dbus
  lib32-glibc
  lib32-systemd
)
makedepends=(
  git
  meson
)
checkdepends=(appstream)
provides=(
  libgamemode.so
  libgamemodeauto.so
)
_tag=b11d2912e280acb87d9ad114d6c7cd8846c4ef02
source=(git+https://github.com/FeralInteractive/gamemode.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd gamemode

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson gamemode build \
    --libdir /usr/lib32 \
    -Dwith-examples=false \
    -Dwith-pam-group=gamemode \
    -Dwith-sd-bus-provider=no-daemon \
    -Dwith-systemd-user-unit-dir=/usr/lib/systemd/user \
    -Dwith-util=false
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -rf "${pkgdir}"/{etc,usr/include}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s gamemode "${pkgdir}"/usr/share/licenses/lib32-gamemode
}

# vim: set ts=4 sw=4 tw=0 et :
