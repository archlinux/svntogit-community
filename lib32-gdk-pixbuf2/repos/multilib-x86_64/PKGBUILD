# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-gdk-pixbuf2
pkgver=2.42.8
pkgrel=1
pkgdesc="An image loading library (32-bit)"
url="https://wiki.gnome.org/Projects/GdkPixbuf"
arch=(x86_64)
license=(LGPL)
depends=(lib32-glib2 lib32-libpng lib32-libtiff lib32-libjpeg gdk-pixbuf2)
makedepends=(git meson)
optdepends=(
  "lib32-librsvg: Load .svg, .svgz, and .svg.gz"
)
options=(debug)
_commit=bca00032ad68d0b0aa2c1f7558db931e52bd9cd2  # tags/2.42.8^0
source=(
  "git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#commit=$_commit"
  gdk-pixbuf-query-loaders-32.hook
)
sha256sums=('SKIP'
            '60f0f8c2c022f9adef0902bdb4b89881e81f3388cd2e2e4830456eea10f49bae')

pkgver() {
  cd gdk-pixbuf
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gdk-pixbuf
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson gdk-pixbuf build \
    --libdir /usr/lib32 \
    -D builtin_loaders=all \
    -D introspection=disabled \
    -D installed_tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  provides=(libgdk_pixbuf-2.0.so)
  install=lib32-gdk-pixbuf2.install

  meson install -C build --destdir "$pkgdir"

  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 et:
