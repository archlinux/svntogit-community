# $Id$

_pkgbasename=glib
pkgname=lib32-$_pkgbasename
pkgver=1.2.10
pkgrel=12
pkgdesc='Common C routines used by Gtk+ and other libs (32-bit)'
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('lib32-glibc' "${_pkgbasename}")
makedepends=('gcc-multilib')
options=('!libtool' '!docs')
source=("ftp://ftp.gtk.org/pub/gtk/v1.2/${_pkgbasename}-${pkgver}.tar.gz"
        'gcc340.patch'
        'aclocal-fixes.patch')
md5sums=('6fe30dad87c77b91b632def29dd69ef9'
         '877b3330e822a4be69a0f8a8c268cfd7'
         'e52c4b88427b9785bb8049dbdc9ff6fb')

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd ${srcdir}/${_pkgbasename}-${pkgver}
	patch -Np1 -i ../gcc340.patch
	patch -Np0 -i ../aclocal-fixes.patch

	./configure --prefix=/usr \
		--mandir=/usr/share/man \
		--libdir=/usr/lib32 \
		--infodir=/usr/share/info \
		--host=x86_64-unknown-linux-gnu \
		--target=x86_64-unknown-linux-gnu
	make
}

package() {
	cd ${srcdir}/${_pkgbasename}-${pkgver}
	make DESTDIR=${pkgdir} install
	rm -rf "${pkgdir}"/usr/{share,include,bin,lib32/glib}
}
