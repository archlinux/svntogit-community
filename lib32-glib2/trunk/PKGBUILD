# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-glib2
pkgver=2.66.6
pkgrel=2
pkgdesc="Low level core library (32-bit)"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL)
arch=(x86_64)
depends=(lib32-pcre lib32-libffi lib32-util-linux lib32-zlib glib2)
makedepends=(gettext gtk-doc shared-mime-info python lib32-libelf git util-linux
             meson lib32-dbus)
checkdepends=(desktop-file-utils lib32-glib2)
_commit=d5ec4f360fb5a2ef0df1862999f970dc67fa352c  # tags/2.66.6^0
source=("git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
        0001-giochannel-Fix-length_size-bounds-check.patch
        multilib.diff
        gio-querymodules-32.{hook,script})
sha256sums=('SKIP'
            'd2dbc00679545cedb33d0179d69a9be5c12b3f00d426e227ca07687384f3407c'
            '2611ec4c97346c3c4eea61786ee20c801207135a9d52a771f5f340b41bf69fbc'
            '2fe9543c97829300c25e6b45ed01fb63d3b699649fb2924b9a41443ccec8b946'
            '1c96fb5f09baf59987e4b97bf3af37ea8feedcc753b48c91ffc6bff080461f21')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd glib

  # https://bugs.archlinux.org/task/69569
  git apply -3 ../0001-giochannel-Fix-length_size-bounds-check.patch

  # Test suite workaround
  git apply -3 ../multilib.diff
}

build() {
  export CC="gcc -m32 -mstackrealign"
  export CXX="g++ -m32 -mstackrealign"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  arch-meson glib build \
    -D selinux=disabled \
    --libdir=/usr/lib32
  meson compile -C build
}

check() {
  meson test -C build --no-suite flaky --no-suite slow --print-errorlogs
}

package() {
  depends+=(libmount.so)
  provides+=(libgio-2.0.so libglib-2.0.so libgmodule-2.0.so libgobject-2.0.so
             libgthread-2.0.so)

  DESTDIR="$pkgdir" meson install -C build

  rm -r "$pkgdir"/usr/{share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
  install -D gio-querymodules-32.script "$pkgdir/usr/share/libalpm/scripts/gio-querymodules-32"
}

# vim:set sw=2 et:
