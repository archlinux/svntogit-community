# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: valgrind requires rebuilt with each major glibc version

_pkgbasename=glibc
pkgname=lib32-$_pkgbasename
pkgver=2.15
pkgrel=6
_glibcdate=20111227
pkgdesc="GNU C Library for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
depends=("glibc>=$pkgver")
makedepends=('gcc-multilib>=4.6')
options=('!strip' '!emptydirs')
source=(ftp://ftp.archlinux.org/other/glibc/${_pkgbasename}-${pkgver}_${_glibcdate}.tar.xz
        glibc-2.10-dont-build-timezone.patch
        glibc-2.10-bz4781.patch
        glibc-__i686.patch
        glibc-2.12.2-ignore-origin-of-privileged-program.patch
        glibc-2.14-libdl-crash.patch
        glibc-2.14-revert-4768ae77.patch
        glibc-2.14-reexport-rpc-interface.patch
        glibc-2.14-reinstall-nis-rpc-headers.patch
        glibc-2.15-regex.patch
        glibc-2.15-lddebug-scopes.patch
        glibc-2.15-revert-c5a0802a.patch
        glibc-2.15-scanf.patch
        glibc-2.15-ifunc.patch
        glibc-2.15-avx.patch
        glibc-2.15-strcasecmp-disable-avx.patch
        glibc-2.15-gb18030.patch
        glibc-2.15-revert-netlink-cache.patch
        glibc-2.15-arena.patch
        glibc-2.15-negative-result-cache.patch
        lib32-glibc.conf)
md5sums=('6ffdf5832192b92f98bdd125317c0dfc'
         '4dadb9203b69a3210d53514bb46f41c3'
         '0c5540efc51c0b93996c51b57a8540ae'
         '40cd342e21f71f5e49e32622b25acc52'
         'b042647ea7d6f22ad319e12e796bd13e'
         '6970bcfeb3bf88913436d5112d16f588'
         '7da8c554a3b591c7401d7023b1928afc'
         'c5de2a946215d647c8af5432ec4b0da0'
         '55febbb72139ac7b65757df085024b83'
         'b3526cbd5e29773560dba725db99af5a'
         '3c219ddfb619b6df903cac4cc42c611d'
         '7ae3e426251ae33e73dbad71f9c91378'
         '39353f53168f4a7509ba5fe0d9f218b8'
         '136eb969f5d6bb6f5155f72a1a7cf23e'
         '41ae047ac88e8f6f547c70b0a0bc3b72'
         'fccb89f6628f59752278e125c35941f8'
         'c4cd34f20ccd37817f6c1374bd4ee68e'
         '6771b0b2bb8aa3870a259fd2f46c424f'
         'a9ffadcfd2d357f91fee0b861fd4a7c6'
         '2c46b8e294de24c531f2253ff69aeef3'
         '6e052f1cb693d5d3203f50f9d4e8c33b')

build() {
  cd ${srcdir}/glibc

  # timezone data is in separate package (tzdata)
  patch -Np1 -i ${srcdir}/glibc-2.10-dont-build-timezone.patch

  # http://sources.redhat.com/bugzilla/show_bug.cgi?id=4781
  patch -Np1 -i ${srcdir}/glibc-2.10-bz4781.patch

  # Undefine __i686 for gcc <= 4.6
  # http://sourceware.org/ml/libc-alpha/2009-07/msg00072.html
  # fix in http://sourceware.org/git/?p=glibc.git;a=commit;h=d4a54ac6 requires additional backporting...
  patch -Np1 -i ${srcdir}/glibc-__i686.patch

  # http://www.exploit-db.com/exploits/15274/
  # http://sourceware.org/git/?p=glibc.git;a=patch;h=d14e6b09  (fedora branch)
  patch -Np1 -i ${srcdir}/glibc-2.12.2-ignore-origin-of-privileged-program.patch

  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=675155e9  (fedora branch)
  # http://sourceware.org/ml/libc-alpha/2011-06/msg00006.html
  patch -Np1 -i ${srcdir}/glibc-2.14-libdl-crash.patch

  # Revert commit causing issues with crappy DNS servers...
  # Will be removed when workaround becomes annoying to maintain - USE A BETTER DNS SERVER!
  # Note that both these patches appear not to fix the issue completely (but rebuilds may fix):
  # http://sourceware.org/bugzilla/show_bug.cgi?id=13013  (used by opensuse)
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=032c0ee3  (fedora branch)
  patch -Np1 -i ${srcdir}/glibc-2.14-revert-4768ae77.patch

  # re-export RPC interface until libtirpc is ready as a replacement
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=acee4873  (fedora branch)
  patch -Np1 -i ${srcdir}/glibc-2.14-reexport-rpc-interface.patch
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=bdd816a3  (fedora branch)
  patch -Np1 -i ${srcdir}/glibc-2.14-reinstall-nis-rpc-headers.patch

  # Fix up regcomp/regexec
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=2ba92745
  patch -Np1 -i ${srcdir}/glibc-2.15-regex.patch

  # propriety nvidia crash - https://bugzilla.redhat.com/show_bug.cgi?id=737223 
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=0c95ab64  (fedora branch)
  patch -Np1 -i ${srcdir}/glibc-2.15-lddebug-scopes.patch

  # revert commit c5a0802a - causes various hangs
  # https://bugzilla.redhat.com/show_bug.cgi?id=769421
  # Note: fedora may have actual fix (not submitted upstream yet...)
  # http://pkgs.fedoraproject.org/gitweb/?p=glibc.git;a=blob_plain;f=glibc-rh552960-2.patch
  patch -Np1 -i ${srcdir}/glibc-2.15-revert-c5a0802a.patch

  # fix realloc usage in vfscanf
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=20b38e03
  patch -Np1 -i ${srcdir}/glibc-2.15-scanf.patch

  # fix ifunc relocations
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=6ee65ed6
  patch -Np1 -i ${srcdir}/glibc-2.15-ifunc.patch
        
  # fix AVX detection
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=afc5ed09
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=08cf777f
  patch -Np1 -i ${srcdir}/glibc-2.15-avx.patch
  # and "fix" strcasecmp
  patch -Np1 -i ${srcdir}/glibc-2.15-strcasecmp-disable-avx.patch

  # fix GB18030 charmap
  # http://sourceware.org/bugzilla/show_bug.cgi?id=11837
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=2a57bd79  (fedora branch)
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=3d828a61  (fedora branch)
  patch -Np1 -i ${srcdir}/glibc-2.15-gb18030.patch

  # fix crash in __nscd_get_mapping if nscd not running
  # http://sourceware.org/bugzilla/show_bug.cgi?id=13594 (potential "fix" in comment)
  # reverts commit 3a2c0242 and other necessary following changes...
  patch -Np1 -i ${srcdir}/glibc-2.15-revert-netlink-cache.patch

  # handle ARENA_TEST correctly
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=41b81892
  patch -Np1 -i ${srcdir}/glibc-2.15-arena.patch

  # Do not cache negative results in nscd if these are transient
  # http://sourceware.org/git/?p=glibc.git;a=commit;h=3e1aa84e
  patch -Np1 -i ${srcdir}/glibc-2.15-negative-result-cache.patch


  cd ${srcdir}
  mkdir glibc-build
  cd glibc-build

  export CC="gcc -m32"

  # Hack to fix NPTL issues with Xen, only required on 32bit platforms
  export CFLAGS="${CFLAGS} -mno-tls-direct-seg-refs"

  echo "slibdir=/usr/lib32" >> configparms

  # remove hardening options from CFLAGS for building libraries
  CFLAGS=${CFLAGS/-fstack-protector/}
  CFLAGS=${CFLAGS/-D_FORTIFY_SOURCE=2/}

  ${srcdir}/glibc/configure --prefix=/usr \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
      --with-headers=/usr/include \
      --enable-add-ons=nptl,libidn \
      --enable-kernel=2.6.32 \
      --with-tls --with-__thread \
      --enable-bind-now --without-gd \
      --without-cvs --disable-profile \
      --enable-multi-arch i686-unknown-linux-gnu

  # build libraries with hardening disabled
  echo "build-programs=no" >> configparms
  make
  
  # re-enable hardening for programs
  sed -i "s#=no#=yes#" configparms
  echo "CC += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -fstack-protector -D_FORTIFY_SOURCE=2" >> configparms
  make

  # remove harding in preparation to run test-suite
  sed -i '2,4d' configparms
}

check() {
  cd ${srcdir}/glibc-build

  # some errors are expected - manually check log files
  make -k check || true
}

package() {
  cd ${srcdir}/glibc-build
  make install_root=${pkgdir} install

  rm -rf ${pkgdir}/{etc,sbin,usr/{bin,sbin,share},var}

  # We need one 32 bit specific header file
  find ${pkgdir}/usr/include -type f -not -name stubs-32.h -delete

  # Do not strip the following files for improved debugging support
  # ("improved" as in not breaking gdb and valgrind...):
  #   ld-${pkgver}.so
  #   libc-${pkgver}.so
  #   libpthread-${pkgver}.so
  #   libthread_db-1.0.so

  cd $pkgdir
  strip $STRIP_BINARIES usr/lib32/getconf/*

  strip $STRIP_STATIC usr/lib32/*.a

  strip $STRIP_SHARED usr/lib32/{libanl,libBrokenLocale,libcidn,libcrypt}-${pkgver}.so \
                      usr/lib32/libnss_{compat,db,dns,files,hesiod,nis,nisplus}-${pkgver}.so \
                      usr/lib32/{libdl,libm,libnsl,libresolv,librt,libutil}-${pkgver}.so \
                      usr/lib32/{libmemusage,libpcprofile,libSegFault}.so \
                      usr/lib32/{pt_chown,{audit,gconv}/*.so}

  # Dynamic linker
  mkdir ${pkgdir}/lib
  ln -s ../usr/lib32/ld-linux.so.2 ${pkgdir}/lib/

  # Add lib32 paths to the default library search path
  install -Dm644 "$srcdir/lib32-glibc.conf" "$pkgdir/etc/ld.so.conf.d/lib32-glibc.conf"

  # Symlink /usr/lib32/locale to /usr/lib/locale
  ln -s ../lib/locale "$pkgdir/usr/lib32/locale"
}
