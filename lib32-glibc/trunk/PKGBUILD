# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# NOTE: valgrind requires rebuild with each new glibc version

_pkgbasename=glibc
pkgname=lib32-$_pkgbasename
pkgver=2.14
pkgrel=4
_glibcdate=20110617
pkgdesc="GNU C Library for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
depends=("glibc>=$pkgver")
makedepends=('gcc-multilib>=4.4')
options=('!strip' '!emptydirs')
source=(http://mirrors.kernel.org/archlinux/other/glibc/${_pkgbasename}-${pkgver}_${_glibcdate}.tar.xz
        glibc-2.10-dont-build-timezone.patch
        glibc-2.10-bz4781.patch
        glibc-__i686.patch
        glibc-2.12.1-static-shared-getpagesize.patch
        glibc-2.12.2-ignore-origin-of-privileged-program.patch
        glibc-2.13-futex.patch
        glibc-2.14-libdl-crash.patch
        #glibc-2.14-fix-resolver-crash-typo.patch
        glibc-2.14-revert-4462fad3.patch
        glibc-2.14-reexport-rpc-interface.patch
        glibc-2.14-reinstall-nis-rpc-headers.patch
        lib32-glibc.conf)
md5sums=('e441d745609d93c907b72548ba646dad'
         '4dadb9203b69a3210d53514bb46f41c3'
         '0c5540efc51c0b93996c51b57a8540ae'
         '40cd342e21f71f5e49e32622b25acc52'
         'a3ac6f318d680347bb6e2805d42b73b2'
         'b042647ea7d6f22ad319e12e796bd13e'
         '7d0154b7e17ea218c9fa953599d24cc4'
         'cea62cc6b903d222c5f26e05a3c0e0e6'
         '46e56492cccb1c9172ed3a235cf43c6c'
         'c5de2a946215d647c8af5432ec4b0da0'
         '55febbb72139ac7b65757df085024b83'
         '6e052f1cb693d5d3203f50f9d4e8c33b')

build() {
  cd ${srcdir}/glibc

  # timezone data is in separate package (tzdata)
  patch -Np1 -i ${srcdir}/glibc-2.10-dont-build-timezone.patch

  # http://sources.redhat.com/bugzilla/show_bug.cgi?id=4781
  patch -Np1 -i ${srcdir}/glibc-2.10-bz4781.patch

  # http://sources.redhat.com/bugzilla/show_bug.cgi?id=411
  # http://sourceware.org/ml/libc-alpha/2009-07/msg00072.html
  patch -Np1 -i ${srcdir}/glibc-__i686.patch

  # http://sourceware.org/bugzilla/show_bug.cgi?id=11929
  # using Fedora "fix" as patch in that bug report causes breakages...
  patch -Np1 -i ${srcdir}/glibc-2.12.1-static-shared-getpagesize.patch

  # http://www.exploit-db.com/exploits/15274/
  # http://sourceware.org/git/?p=glibc.git;a=patch;h=d14e6b09 (only fedora branch...)
  patch -Np1 -i ${srcdir}/glibc-2.12.2-ignore-origin-of-privileged-program.patch

  # http://sourceware.org/bugzilla/show_bug.cgi?id=12403
  patch -Np1 -i ${srcdir}/glibc-2.13-futex.patch

  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=675155e9 (only fedora branch...)
  # http://sourceware.org/ml/libc-alpha/2011-06/msg00006.html
  patch -Np1 -i ${srcdir}/glibc-2.14-libdl-crash.patch

  # This fixes the main segfault but not the assert fail (FS#24615)
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=57912a71 
  #patch -Np1 -i ${srcdir}/glibc-2.14-fix-resolver-crash-typo.patch
  # resort to reverting the bad commit completely for the moment
  patch -Np1 -i ${srcdir}/glibc-2.14-revert-4462fad3.patch

  # re-export RPC interface until libtirpc is ready as a replacement
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=acee4873 (only fedora branch...)
  patch -Np1 -i ${srcdir}/glibc-2.14-reexport-rpc-interface.patch
  # http://sourceware.org/git/?p=glibc.git;a=commitdiff;h=bdd816a3 (only fedora branch...)
  patch -Np1 -i ${srcdir}/glibc-2.14-reinstall-nis-rpc-headers.patch

  cd ${srcdir}
  mkdir glibc-build
  cd glibc-build

  # Hack to fix NPTL issues with Xen, only required on 32bit platforms
  export CFLAGS="${CFLAGS} -mno-tls-direct-seg-refs"

  echo "slibdir=/usr/lib32" >> configparms

  export CC="gcc -m32"

  ${srcdir}/glibc/configure --prefix=/usr \
      --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
      --with-headers=/usr/include \
      --enable-add-ons=nptl,libidn \
      --enable-kernel=2.6.27 \
      --with-tls --with-__thread \
      --enable-bind-now --without-gd \
      --without-cvs --disable-profile \
      --disable-multi-arch i686-unknown-linux-gnu
        
  make
}

check() {
  cd ${srcdir}/glibc-build

  # some errors are expected - manually check log files
  make -k check || true
}

package() {
  cd ${srcdir}/glibc-build
  make install_root=${pkgdir} install

  rm -rf ${pkgdir}/{etc,sbin,usr/{bin,sbin,share}}

  # We need one 32 bit specific header file
  find ${pkgdir}/usr/include -type f -not -name stubs-32.h -delete

  # manually strip files as stripping libpthread-*.so and libthread_db.so
  # with the default $STRIP_SHARED breaks gdb and stripping ld-*.so breaks
  # valgrind on x86_64

  cd $pkgdir
  strip $STRIP_BINARIES usr/lib32/getconf/*

  strip $STRIP_STATIC usr/lib32/*.a \
                      usr/lib32/{{ld,libpthread}-${pkgver},libthread_db-1.0}.so

  strip $STRIP_SHARED usr/lib32/{libanl,libBrokenLocale,libc,libcidn,libcrypt}-${pkgver}.so \
                      usr/lib32/libnss_{compat,dns,files,hesiod,nis,nisplus}-${pkgver}.so \
                      usr/lib32/{libdl,libm,libnsl,libresolv,librt,libutil}-${pkgver}.so \
                      usr/lib32/{libmemusage,libpcprofile,libSegFault}.so \
                      usr/lib32/{pt_chown,{audit,gconv}/*.so}

  # Dynamic linker
  install -d -m755 ${pkgdir}/lib
  ln -s ../usr/lib32/ld-linux.so.2 ${pkgdir}/lib/

  # Add /usr/lib32 to the default library search path
  install -Dm644 "$srcdir/lib32-glibc.conf" "$pkgdir/etc/ld.so.conf.d/lib32-glibc.conf"

  # Symlink /usr/lib32/locale to /usr/lib/locale
  ln -s ../lib/locale "$pkgdir/usr/lib32/locale"
}
