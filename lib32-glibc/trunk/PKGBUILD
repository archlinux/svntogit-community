# $Id$
# Maintainer: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Clement Siuchung Cheung <clement.cheung@umich.edu>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
_pkgsourcename=glibc
pkgname=lib32-$_pkgsourcename
pkgver=2.12
pkgrel=5
_pkgrel32=4
arch=('x86_64')
groups=('lib32')
pkgdesc="GNU C Library (32 Bit)"
url="http://www.gnu.org/software/libc"
license=('GPL' 'LGPL')
install=$pkgname.install
provides=("lib32-glibc-devel=$pkgver-$pkgrel" "lib32-glibc-devel-il=$pkgver-$pkgrel")
conflicts=(lib32-glibc-devel lib32-glibc-devel-il)
replaces=(lib32-glibc-devel lib32-glibc-devel-il)
source=(http://mir.archlinux.fr/core/os/i686/$_pkgsourcename-$pkgver-$_pkgrel32-i686.pkg.tar.xz)

build() {
	cd "$srcdir"

	mkdir -p "$pkgdir/opt/lib32/lib"
	mkdir -p "$pkgdir/opt/lib32/usr/lib/gconv"

	cp -dp lib/*.so* "$pkgdir/opt/lib32/lib"
	cp -dp usr/lib/*.so* usr/lib/*.o usr/lib/*.a "$pkgdir/opt/lib32/usr/lib"
	cp -dp usr/lib/gconv/* "$pkgdir/opt/lib32/usr/lib/gconv"

        ln -s /usr/include "$pkgdir/opt/lib32/usr/include"
        install -Dm644 {,"$pkgdir/"}usr/include/gnu/stubs-32.h

	cat /usr/bin/ldd | sed 's/\/lib\/ld-linux-x86-64.so.2/\/lib\/ld-linux-x86-32.so.2/' > "$srcdir/ldd32"
	install -D -m755 "$srcdir/ldd32" "$pkgdir/usr/bin/ldd32"

	mkdir -p "$pkgdir/lib"
	ln -s /opt/lib32/lib/ld-linux.so.2 "$pkgdir/lib/ld-linux-x86-32.so.2"

	install -d -m755 "$pkgdir/etc/ld.so.conf.d/"
	echo '/opt/lib32/lib' > "$pkgdir/etc/ld.so.conf.d/lib32-glibc.conf"
	echo '/opt/lib32/usr/lib' >> "$pkgdir/etc/ld.so.conf.d/lib32-glibc.conf"
}

md5sums=('0cc5649017481616caa370a8a07d0fb1')
