# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail at cvigano dot de>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=3.1.7
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="http://gnutls.org/"
options=('!libtool')
depends=('lib32-zlib' 'lib32-nettle>=2.4' 'lib32-p11-kit>=0.12' 'lib32-libtasn1' $_pkgbasename)
makedepends=('gcc-multilib' 'lib32-libidn')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.1/${_pkgbasename}-${pkgver}.tar.xz{,.sig})
md5sums=('95c96dfaba0e82e9e6bcb67eab5abf49'
         'ea9d8c24c1ac2654ae9fe87f69b45e17')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  # build fails without --disable-hardware-acceleration because of assembler errors
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-zlib \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests --disable-hardware-acceleration
  make
}
check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  #make -k check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  find $pkgdir

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
