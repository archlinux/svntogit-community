# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail at cvigano dot de>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=3.4.1
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL2.1')
url="http://gnutls.org/"
depends=('lib32-zlib' 'lib32-nettle' 'lib32-p11-kit' 'lib32-libtasn1' $_pkgbasename)
makedepends=('gcc-multilib')
source=(ftp://ftp.gnutls.org/gcrypt/gnutls/v3.4/${_pkgbasename}-${pkgver}.tar.xz{,.sig}
        test_do_not_perform_internationalized_name_checks_without_libidn.diff)
md5sums=('2d04f34fa25b45f9dcb9104c0394e12e'
         'SKIP'
        'cc584698a4558642129b6398d1e666d6')
validpgpkeys=(1F42418905D8206AA754CCDC29EE58B996865171)

prepare() {
  cd ${_pkgbasename}-${pkgver}
  # fix build in testsuite without libidn
  # https://gitlab.com/gnutls/gnutls/commit/78fcb17c74e1e625f89e71e0641083c85a268414
  patch -Np1 -i $srcdir/test_do_not_perform_internationalized_name_checks_without_libidn.diff
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  # disable libidn for security reasons:
  # http://lists.gnupg.org/pipermail/gnutls-devel/2015-May/007582.html

  # build fails without --disable-hardware-acceleration because of assembler errors
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-zlib \
    --disable-static \
	--without-idn \
    --disable-guile \
    --disable-valgrind-tests --disable-hardware-acceleration
  make
}

check() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  #make -k check
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  find $pkgdir

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
