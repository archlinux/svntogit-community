# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_basename=gst-plugins-good
pkgname=lib32-gst-plugins-good
pkgver=1.8.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('lib32-libpulse' 'lib32-jack' 'lib32-libsoup' 'lib32-gst-plugins-base-libs' 'lib32-wavpack'
         'lib32-aalib' 'lib32-taglib' 'lib32-libdv' 'lib32-libshout' 'lib32-libvpx' 'lib32-gdk-pixbuf2'
         'lib32-libcaca' 'lib32-libavc1394' 'lib32-libiec61883' 'lib32-libxdamage' 'lib32-v4l-utils'
         'lib32-cairo' 'lib32-libgudev' 'gst-plugins-good')
makedepends=('lib32-gstreamer' 'lib32-speex' 'lib32-flac' 'lib32-libraw1394' 'python')
options=(!emptydirs)
source=(${url}/src/$_basename/$_basename-$pkgver.tar.xz)
sha256sums=('c20c134d47dbc238d921707a3b66da709c2b4dd89f9d267cec13d1ddf16e9f4d')

build() {
  cd $_basename-$pkgver

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --build=i686-pc-linux-gnu --libdir=/usr/lib32 \
    --disable-static --enable-experimental --disable-gtk-doc \
    --with-package-name="GStreamer Good Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

# Disabled for now: souphttpsrc times out, and takes a damn long time to do so
#check() {
#  cd $_basename-$pkgver
#  make check
#}

package() {
  cd $_basename-$pkgver
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir"/usr/share
}
