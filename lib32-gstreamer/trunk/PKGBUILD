# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Kitching <chriskitching@linux.com>

pkgname=lib32-gstreamer
_pkgbasename=gstreamer
pkgver=1.18.5
pkgrel=1
pkgdesc="GStreamer open-source multimedia framework core library (32-bit)"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-libxml2 lib32-glib2 lib32-libunwind lib32-gtk3 lib32-libelf gstreamer)
makedepends=(meson git wayland-protocols)
_commit=71c213f99ad5c8fc8f36a26238483f2bdf7d846a
source=("git+https://gitlab.freedesktop.org/gstreamer/gstreamer.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgbasename
  git describe --tags | sed 's/-/+/g'
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson $_pkgbasename build \
    --libdir=lib32 \
    --libexecdir=lib32 \
    -D doc=disabled \
    -D ptp-helper-permissions=capabilities \
    -D dbghelp=disabled \
    -D gobject-cast-checks=disabled \
    -D bash-completion=disabled \
    -D introspection=disabled \
    -D package-name="GStreamer (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  rm -R "${pkgdir}"/usr/{share,include}

  for _i in "${pkgdir}"/usr/bin/*; do
    mv "${_i}" "${_i}-32"
  done
}
