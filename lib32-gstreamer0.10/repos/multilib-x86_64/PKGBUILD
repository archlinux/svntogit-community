# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-gstreamer0.10
pkgver=0.10.36
pkgrel=2
pkgdesc="GStreamer Multimedia Framework"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=("${pkgname#*-}" 'lib32-glib2' 'lib32-libxml2')
makedepends=('gcc-multilib' 'intltool')
options=('!libtool')
source=("http://gstreamer.freedesktop.org/src/gstreamer/gstreamer-${pkgver}.tar.xz")
sha256sums=('9151aa108c177054387885763fa0e433e76780f7c5655c70a5390f2a6c6871da')

build() {
  cd "${srcdir}"/gstreamer-${pkgver}

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libdir=/usr/lib32 --libexecdir=/usr/lib32 --with-package-{name="GStreamer (Archlinux Multilib)",origin="http://www.archlinux.org/"} --disable-{examples,static,tests}
  make
}

package() {
  cd "${srcdir}"/gstreamer-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
