# $Id$

_pkgbasename=gtk
pkgname=lib32-$_pkgbasename
pkgver=1.2.10
pkgrel=13
pkgdesc="The GTK+ toolkit (32-bit)"
arch=('x86_64')
url="http://www.gtk.org/"
license=('LGPL')
depends=('lib32-libxi' 'lib32-glib' "${_pkgbasename}")
makedepends=('gcc-multilib' 'lib32-libxt')
options=('!libtool')
source=("ftp://ftp.gtk.org/pub/gtk/v1.2/gtk+-${pkgver}.tar.gz"
        'aclocal-fixes.patch')
md5sums=('4d5cb2fc7fb7830e4af9747a36bfce20'
         'c59d4906602d99a7468f7334b6fc3b4e')

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd ${srcdir}/gtk+-${pkgver}
	patch -p0 -i ${srcdir}/aclocal-fixes.patch

	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--sysconfdir=/etc \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		--host=x86_64-unknown-linux-gnu \
		--target=x86_64-unknown-linux-gnu \
		--with-xinput=xfree
	make
}

package() {
	cd ${srcdir}/gtk+-${pkgver}
	make DESTDIR=${pkgdir} install
	rm -rf "${pkgdir}/"{usr/{share,include,bin},etc}
}
