# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.24.34+r156+g812b3930d0
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit'
arch=(x86_64)
license=(LGPL)
url=https://www.gtk.org/
depends=(
  gtk3
  lib32-at-spi2-atk
  lib32-colord
  lib32-dbus
  lib32-mesa
  lib32-gdk-pixbuf2
  lib32-libcups
  lib32-libepoxy
  lib32-librsvg
  lib32-libxcomposite
  lib32-libxcursor
  lib32-libxinerama
  lib32-libxkbcommon
  lib32-libxrandr
  lib32-pango
  lib32-libcanberra
  lib32-wayland
  lib32-libxdamage
)
makedepends=(
  gtk-doc
  git
  glib2-docs
  meson
  ninja
  sassc
  wayland-protocols
)
install=lib32-gtk3.install
options=(debug)
_commit=812b3930d0bb11d67d4a2c159d037c8c0d440c50  # gtk-3-24
source=(
  git+https://gitlab.gnome.org/GNOME/gtk.git#commit=${_commit}
  gtk-query-immodules-3.0-32.hook
)
sha256sums=('SKIP'
            '04255db75cc626e122df4710b350fb656778d01968d56e598b496b71db335989')

pkgver() {
  cd gtk
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtk

  # https://bugs.archlinux.org/task/76018
  # https://gitlab.gnome.org/GNOME/gtk/-/merge_requests/4381#note_1465559
  git revert -n da4066774b926880631af099469d308714b5606c
}

build() {
  CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  local meson_options=(
    --libdir=/usr/lib32
    -D introspection=false
    -D broadway_backend=true
    -D cloudproviders=false
    -D tracker3=false
    -D colord=yes
    -D gtk_doc=false
    -D man=false
  )

  export CC='gcc -m32'
  export CXX=/bin/false
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson gtk build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "${pkgdir}"/{etc,usr/{include,share}}
  find "${pkgdir}"/usr/bin -type f -not -name gtk-query-immodules-3.0 -delete
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  install -Dm 644 gtk-query-immodules-3.0-32.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
}

# vim:set sw=2 et:
