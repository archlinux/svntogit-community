# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgbase=lib32-harfbuzz
pkgname=(lib32-harfbuzz lib32-harfbuzz-icu)
pkgver=2.6.5
pkgrel=2
pkgdesc="OpenType text shaping engine (32-bit)"
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(lib32-glib2 lib32-freetype2 lib32-cairo lib32-icu ragel git python
             harfbuzz)
checkdepends=(python-fonttools python-setuptools)
_commit=f9bc373381ddf8553f943b774596ae5a53bf2641  # tags/2.6.5^0
source=("git+https://github.com/harfbuzz/harfbuzz#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd harfbuzz
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd harfbuzz
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cd harfbuzz
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --with-cairo \
    --with-freetype \
    --with-glib \
    --with-gobject \
    --without-graphite2 \
    --with-icu \
    --disable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd harfbuzz
  TMPDIR="$srcdir" make check
  rm -rf "$srcdir"/tmp*
}

package_lib32-harfbuzz() {
  depends=(lib32-glib2 lib32-freetype2 libglib-2.0.so libfreetype.so
           libgobject-2.0.so harfbuzz)
  provides=(libharfbuzz.so libharfbuzz-subset.so libharfbuzz-gobject.so)

  cd harfbuzz
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/lib32/pkgconfig; cd ../hb-icu
  mv "$pkgdir"/usr/lib32/libharfbuzz-icu* ./usr/lib32
  mv "$pkgdir"/usr/lib32/pkgconfig/harfbuzz-icu.pc ./usr/lib32/pkgconfig
}

package_lib32-harfbuzz-icu() {
  pkgdesc="$pkgdesc (ICU integration)"
  depends=("lib32-harfbuzz=$pkgver-$pkgrel" lib32-icu libharfbuzz.so harfbuzz-icu)
  provides=(libharfbuzz-icu.so)

  mv hb-icu/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}
