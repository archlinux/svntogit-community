# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgbase=lib32-harfbuzz
pkgname=(lib32-harfbuzz lib32-harfbuzz-icu)
pkgver=3.1.2
pkgrel=1
pkgdesc="OpenType text shaping engine - 32-bit"
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(lib32-glib2 lib32-freetype2 lib32-icu ragel git python meson
             harfbuzz)
checkdepends=(python-fonttools python-setuptools)
_commit=8aed5c21a31eece6a9f3cd775fda8facb6c28b9b  # tags/3.1.2^0
source=("git+https://github.com/harfbuzz/harfbuzz#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd harfbuzz
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd harfbuzz
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson harfbuzz build \
    --libdir=/usr/lib32 \
    -D cairo=disabled \
    -D chafa=disabled \
    -D introspection=disabled \
    -D docs=disabled
  meson compile -C build
}

check() {
  mkdir -p tmp
  TMPDIR="$PWD/tmp" meson test -C build --print-errorlogs
  rm -r tmp
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_lib32-harfbuzz() {
  depends=(lib32-glib2 lib32-freetype2 libglib-2.0.so libfreetype.so
           libgobject-2.0.so harfbuzz)
  provides=(libharfbuzz.so libharfbuzz-subset.so libharfbuzz-gobject.so)

  meson install -C build --destdir "$pkgdir"

  ( cd "$pkgdir"
    rm -r usr/{bin,include}

    _pick hb-icu usr/lib32/libharfbuzz-icu*
    _pick hb-icu usr/lib32/pkgconfig/harfbuzz-icu.pc
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

package_lib32-harfbuzz-icu() {
  pkgdesc+=" - ICU integration"
  depends=("lib32-harfbuzz=$pkgver-$pkgrel" libicuuc.so libharfbuzz.so harfbuzz-icu)
  provides=(libharfbuzz-icu.so)

  mv hb-icu/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}
