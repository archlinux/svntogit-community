# $Id$
# Maintainer: tobias <tobias@archlinux.net>
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Robert Emil Berge <robert@rebi.no>

_pkgbasename=jack
pkgname=lib32-$_pkgbasename
_longname=jack-audio-connection-kit
pkgver=0.124.1
pkgrel=1
pkgdesc="A low-latency audio server (32-bit)"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=("$_pkgbasename=$pkgver" 'lib32-db' 'lib32-libsamplerate' 'lib32-readline')
makedepends=('gcc-multilib')
url="http://jackaudio.org/"
source=("http://jackaudio.org/downloads/${_longname}-${pkgver}.tar.gz")
md5sums=('d64e90121be8a54860b870a726fb5b5d')

build() {
  cd "${srcdir}/${_longname}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make -C libjack
}

package() {
  cd ${srcdir}/${_longname}-${pkgver}

  make DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C libjack DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
