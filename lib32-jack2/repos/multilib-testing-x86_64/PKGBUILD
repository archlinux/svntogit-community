# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lib32-jack2
pkgver=1.9.12
pkgrel=2
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL')
depends=('lib32-celt' 'lib32-gcc-libs' 'lib32-opus' 'jack2')
makedepends=('celt' 'doxygen' 'gcc-multilib' 'lib32-celt' 'lib32-gcc-libs' 'lib32-opus' 'libffado' 'libsamplerate' 'opus' 'python')
provides=('lib32-jack')
conflicts=('lib32-jack' 'jack2-multilib' 'jack2-dbus-multilib')
replaces=('jack2-multilib' 'jack2-dbus-multilib')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jackaudio/${pkgname#lib32-}/archive/v${pkgver}.tar.gz")
sha512sums=('f0271dfc8f8e2f2489ca52f431ad4fa420665816d6c67a01a76da1d4b5ae91f6dad8c4e3309ec5e0c159c9d312ed56021ab323d74bce828ace26f1b8d477ddfa')

build() {
  cd "${pkgname#lib32-}-${pkgver}"

  python waf configure --prefix=/usr \
                        --libdir=/usr/lib \
                        --libdir32=/usr/lib32 \
                        --mixed
  python waf build
}

package() {
  cd "${pkgname#lib32-}-${pkgver}"

  python waf install --destdir="${pkgdir}"
  # remove all non-32bit and those provided by jack2{,-dbus}
  rm -rf "${pkgdir}"/usr/{bin,lib,include,share}
}

# vim:set ts=2 sw=2 et:
