# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=lib32-jack2
pkgver=1.9.10
pkgrel=3
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL')
makedepends=('python2' 'lib32-gcc-libs' 'gcc-multilib' 'libsamplerate')
provides=('lib32-jack')
conflicts=('lib32-jack' 'jack2-multilib' 'jack2-dbus-multilib')
replaces=('jack2-multilib' 'jack2-dbus-multilib')
source=("https://dl.dropboxusercontent.com/u/28869550/jack-$pkgver.tar.bz2")
md5sums=('4aeb91d7ae0cabce98355436ed4f217a')

build() {
  cd "$srcdir/jack-$pkgver"

  python2 waf configure --prefix=/usr \
                        --libdir=/usr/lib \
                        --libdir32=/usr/lib32 \
                        --mixed
  python2 waf build $MAKEFLAGS
}

package() {
  depends=('lib32-gcc-libs' 'jack2')
  cd "$srcdir/jack-$pkgver"

  python2 waf install --destdir="$pkgdir"

  rm -rf "$pkgdir"/usr/{bin,lib,include,share}

}

# vim:set ts=2 sw=2 et:
