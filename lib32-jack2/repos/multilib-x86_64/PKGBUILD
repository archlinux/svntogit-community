# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lib32-jack2
pkgver=1.9.13
pkgrel=1
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL2' 'LGPL2.1')
depends=('lib32-db' 'lib32-celt' 'lib32-gcc-libs' 'lib32-glibc' 'lib32-opus' 'jack2')
makedepends=('db' 'celt' 'lib32-dbus' 'lib32-expat' 'lib32-libsamplerate' 'libsamplerate' 'opus' 'waf')
provides=('lib32-jack')
conflicts=('lib32-jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jackaudio/${pkgname#lib32-}/archive/v${pkgver}.tar.gz")
sha512sums=('8c578df723f2c0c91e854abf73a35249784c833264c42c340f0e56744bf931bda75ebd941532c80cecc67e2d469becc27498e546b73cc4581379b8ceee1f49c1')

prepare() {
  cd "${pkgname#lib32-}-${pkgver}"
  # copying relevant custom waf scripts and loading them specifically using
  # wscript
  touch __init__.py
  mkdir -vp tools
  cp -v waflib/extras/xcode*.py tools
  rm -rv waflib
  sed -e "s/load('xcode'/load('xcode', tooldir='tools'/g" \
      -e "s/load('xcode6'/load('xcode6', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname#lib32-}-${pkgver}"
  # mixed mode is broken: https://github.com/jackaudio/jack2/issues/508
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CXXFLAGS+=' -m32'
  export CFLAGS+=' -m32'
  waf configure --prefix=/usr \
                --libdir=/usr/lib32 \
                --classic \
                --dbus
  waf build
}

package() {
  cd "${pkgname#lib32-}-${pkgver}"
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  waf install --destdir="${pkgdir}"
  # remove all non-32bit and those provided by jack2
  rm -rf "${pkgdir}"/usr/{bin,lib,include,share}
  rm -rf "${pkgdir}/usr/lib32/jack/"
  rm -rf "${pkgdir}/usr/lib32/libjack"{net,server}*
}
# vim:set ts=2 sw=2 et:
