# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

_name=jack2
pkgname=lib32-jack2
pkgver=1.9.21
pkgrel=1
pkgdesc="The JACK low-latency audio server (32 bit)"
arch=(x86_64)
url="https://github.com/jackaudio/jack2"
license=(GPL2 LGPL2.1)
depends=(lib32-gcc-libs lib32-glibc lib32-opus jack2=$pkgver)
makedepends=(git lib32-alsa-lib lib32-dbus lib32-expat lib32-libsamplerate waf)
conflicts=(lib32-jack)
provides=(lib32-jack libjack.so libjackserver.so libjacknet.so)
options=(debug)
source=(git+https://github.com/jackaudio/$_name.git#tag=v$pkgver?signed)
sha512sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0') # falkTX <falktx@falktx.com>

prepare() {
  cd $_name
  touch __init__.py
  # remove custom waflib, as we are using system provided waf
  rm -rv waflib
}

build() {
  cd $_name
  export LINKFLAGS="$LDFLAGS"
  export PYTHONPATH="$PWD:$PYTHONPATH"
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  export CXXFLAGS+=' -m32'
  export CFLAGS+=' -m32'
  waf configure --prefix=/usr \
                --libdir=/usr/lib32 \
                --classic \
                --dbus
  waf build
}

package() {
  depends+=(libasound.so libsamplerate.so)

  cd $_name
  export PYTHONPATH="$PWD:$PYTHONPATH"
  waf install --destdir="$pkgdir"
  # remove all files provided by jack2
  rm -rf "$pkgdir/usr/"{bin,include,share}
}
# vim:set ts=2 sw=2 et:
