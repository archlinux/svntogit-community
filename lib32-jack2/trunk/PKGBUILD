# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

## Replaces jack2-multilib, which had its own rationale:
# http://mailman.archlinux.org/pipermail/arch-multilib/2011-December/000251.html

## New lib32-jack2 package description (replaces jack2{-dbus,}-multilib
#
# The vanilla mixed mode build is broken since a long time, so we build
# and provide just the 32 bit jack client library using some dynamic
# patching that seems stable enough to last on future build system changes.

pkgname=lib32-jack2
pkgver=1.9.10
pkgrel=1
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64' 'i686')
url="http://jackaudio.org/"
license=('GPL')
depends=('lib32-gcc-libs' 'jack2')
makedepends=('python2' 'gcc-multilib')
provides=('lib32-jack')
conflicts=('lib32-jack' 'jack2-multilib' 'jack2-dbus-multilib')
replaces=('jack2-multilib' 'jack2-dbus-multilib')
source=("http://jackaudio.org/downloads/jack-$pkgver.tar.bz2")
md5sums=('4aeb91d7ae0cabce98355436ed4f217a')

prepare() {
  cd "$srcdir/jack-$pkgver"

  # ugly dynamic patch to build just the 32 bit jack client lib
  # (compatible with previous releases and hoping with the next)
  sed -e '/serverlib/,/clientlib/{/clientlib/!d}' \
      -e '/clientlib32bit/i\\tclientlib.posted = True' \
      -e '1,/clientlib32bit/!d' \
      -i common/wscript

  # celt and opus are not required to build
  sed -i 's/, "CELT", "OPUS"//' common/wscript
}

build() {
  cd "$srcdir/jack-$pkgver"
  python2 waf configure --prefix=/usr \
                        --libdir=/usr/lib32 \
                        --mixed
  cd common
  python2 ../waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/jack-$pkgver/build/common"

  # jack client 32 bit lib
  install -Dm755 libjack.so "$pkgdir/usr/lib32/libjack.so.0.1.0"
  ln -s libjack.so.0.1.0 "$pkgdir/usr/lib32/libjack.so.0"
  ln -s libjack.so.0.1.0 "$pkgdir/usr/lib32/libjack.so"
}

# vim:set ts=2 sw=2 et:
