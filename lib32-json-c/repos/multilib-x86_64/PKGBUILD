# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-json-c
pkgver=0.14
pkgrel=1
pkgdesc="A JSON implementation in C (32-bit)"
url="https://github.com/json-c/json-c/wiki"
license=(MIT)
arch=(x86_64)
depends=(lib32-glibc json-c)
makedepends=(git cmake)
provides=(libjson-c.so)
_commit=bae2f10c436eaf0d95746cbc5f1c1f0ecb866a8e  # tags/json-c-0.14-20200419^0
source=("git+https://github.com/json-c/json-c#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd json-c
  local tag="$(git describe --tags --abbrev=0)"
  local ver="$(git describe --tags)"
  echo "${tag%-*}${ver#$tag}" | sed 's/^json-c-//;s/-/+/g'
}

prepare() {
  cd json-c
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="i686-pc-linux-gnu-pkg-config"

  cmake -Hjson-c -Bbuild \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
    -DENABLE_THREADING=ON \
    -DENABLE_RDRAND=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  rm -r "$pkgdir/usr/include"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 json-c/COPYING
}

# vim:set sw=2 et:
