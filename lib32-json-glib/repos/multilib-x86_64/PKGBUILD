# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-json-glib
pkgver=1.4.2
pkgrel=1
pkgdesc='JSON library built on GLib'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/JsonGlib'
license=('GPL')
depends=('json-glib' 'lib32-glib2')
makedepends=('gobject-introspection' 'meson')
source=("https://ftp.gnome.org/pub/GNOME/sources/json-glib/${pkgver%.*}/json-glib-${pkgver}.tar.xz")
sha256sums=('2d7709a44749c7318599a6829322e081915bdc73f5be5045882ed120bb686dc8')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build(){
  cd build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  export LANG='en_US.UTF-8'
  export LANGUAGE='en_US'

  meson ../json-glib-${pkgver} \
    --buildtype='release' \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32'
  ninja
}

package(){
  cd build

  DESTDIR="${pkgdir}" ninja install
  rm -rf "${pkgdir}"/usr/{bin,include,lib32/installed-tests,share}
}

# vim: ts=2 sw=2 et:
