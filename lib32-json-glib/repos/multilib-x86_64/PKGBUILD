# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-json-glib
pkgver=1.0.4
pkgrel=1
pkgdesc='JSON library built on GLib'
arch=('x86_64')
url='http://live.gnome.org/JsonGlib'
license=('GPL')
depends=('json-glib' 'lib32-glib2')
makedepends=('gobject-introspection' 'python2')
source=("http://ftp.gnome.org/pub/GNOME/sources/json-glib/${pkgver%.*}/json-glib-${pkgver}.tar.xz")
sha256sums=('80f3593cb6bd13f1465828e46a9f740e2e9bd3cd2257889442b3e62bd6de05cd')

build(){
  cd json-glib-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make
}

package(){
  cd json-glib-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
