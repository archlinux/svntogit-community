# $Id: PKGBUILD 122855 2011-05-06 19:36:38Z stephane $
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_pkgbasename=krb5
pkgname=lib32-$_pkgbasename
pkgver=1.9.2
pkgrel=1
pkgdesc="The Kerberos network authentication system (32-bit)"
arch=('x86_64')
url="http://web.mit.edu/kerberos/"
license=('custom')
depends=('lib32-e2fsprogs' 'lib32-libldap' lib32-keyutils $_pkgbasename)
makedepends=('perl' gcc-multilib)
provides=('lib32-heimdal')
replaces=('lib32-heimdal')
conflicts=('lib32-heimdal')
source=(http://web.mit.edu/kerberos/dist/${_pkgbasename}/1.9/${_pkgbasename}-${pkgver}-signed.tar
krb5-1.9.1-2011-007.patch)
sha1sums=('aa06f778ee1f9791cd4c5cf4c9e9465769ffec92'
          '31a130542e92b70c807b2dbe6f9b182dc14f5e9f')
options=('!emptydirs')

build() {
   tar zxvf ${_pkgbasename}-${pkgver}.tar.gz
   cd "${srcdir}/${_pkgbasename}-${pkgver}/src"

   # Apply upstream patch to fix a null pointer dereference when processing TGS requests
   # CVE-2011-1530
   # see http://web.mit.edu/kerberos/advisories/MITKRB5-SA-2011-007.txt
   patch -Np2 -i ${srcdir}/krb5-1.9.1-2011-007.patch

   export CC="gcc -m32"
   export CXX="g++ -m32"
   export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

   export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
   export CPPFLAGS+=" -I/usr/include/et"
   ./configure --prefix=/usr \
               --sysconfdir=/etc/krb5 \
               --mandir=/usr/share/man \
               --localstatedir=/var/lia \
               --libdir=/usr/lib32 \
               --enable-shared \
               --with-system-et \
               --with-system-ss \
               --disable-rpath \
               --without-tcl \
               --enable-dns-for-realm \
               --with-ldap

   make
}

check() {
   # We can't do this in the build directory.
   cd "${srcdir}/${_pkgbasename}-${pkgver}"
   make -C src check
}

package() {
   cd "${srcdir}/${_pkgbasename}-${pkgver}/src"
   make DESTDIR="${pkgdir}" install

   rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
   mkdir -p "$pkgdir/usr/share/licenses"
   ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
