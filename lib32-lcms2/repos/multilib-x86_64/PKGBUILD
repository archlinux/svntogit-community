# Maintainer: Laurent Carlier <lordheavym@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=lib32-lcms2
pkgver=2.13.1
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (32-bit)"
arch=('x86_64')
license=('MIT')
depends=('lib32-libtiff' 'lcms2')
provides=('liblcms2.so')
makedepends=('gcc-multilib')
url="https://www.littlecms.com"
source=("https://github.com/mm2/Little-CMS/releases/download/lcms${pkgver}/lcms2-${pkgver}.tar.gz")
sha512sums=('214ec63fa086b580a6507d493a54ccf5faf02c40e149d71e41f9fc8510efdb16554621c96d91cc886f09682c9631b10aa194b4b67eb6ffcc871d5d4666b05617')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd lcms2-${pkgver}

  ./configure --prefix=/usr \
    --libdir=/usr/lib32
  make
}

check() {
  cd lcms2-${pkgver}

  make check
}

package() {
  cd lcms2-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s lcms2 "${pkgdir}/usr/share/licenses/${pkgname}"
}
