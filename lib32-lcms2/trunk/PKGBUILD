# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

_pkgbasename=lcms2
pkgname=lib32-lcms2
pkgver=2.12
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2 (32-bit)"
arch=('x86_64')
license=('MIT')
depends=('lib32-libtiff' ${_pkgbasename})
provides=('liblcms2.so')
makedepends=('gcc-multilib')
url="https://www.littlecms.com"
source=("https://github.com/mm2/Little-CMS/archive/${pkgver}/${_pkgbasename}-${pkgver}.tar.gz")
sha512sums=('4a4f3727905b1e1d514679bd83da1ead30bc5cf40cd63c3b43663fa6c5d582dc7e3540d5301393d9f01f7d2cde934975cabdcbea718cfd48c390f838d9149abb')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd Little-CMS-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd Little-CMS-${pkgver}

  make check
}

package() {
  cd Little-CMS-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
