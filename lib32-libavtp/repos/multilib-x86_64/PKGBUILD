# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libavtp
pkgname=lib32-libavtp
pkgver=0.1.0
pkgrel=1
pkgdesc="Open source implementation of Audio Video Transport Protocol"
arch=('x86_64')
url="https://github.com/Avnu/libavtp"
license=('BSD')
depends=('lib32-glibc' 'libavtp')
makedepends=('meson')
checkdepends=('lib32-cmocka')
provides=('libavtp.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Avnu/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('b08d177bbf2e9059489b9d11869e8c8c46e3abce4d3c4a3cabc0aedaae41a591280a9f79295a5c17bcba3beb52e10061c062bceffed8e1a26589d5726f2c5428')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  arch-meson --prefix=/usr \
             --libdir=/usr/lib32 \
             build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CONTRIBUTING,HACKING,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -rvf "${pkgdir}/usr/include"
}
