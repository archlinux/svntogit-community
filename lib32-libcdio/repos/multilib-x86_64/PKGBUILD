# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>
# Contributor: Kazuo Teramoto <kaz.rag at gmail dot com>
# Contributor: damir <damir@archlinux.org>

pkgname=lib32-libcdio
_pkgname=libcdio
pkgver=0.94
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library (32-bit)"
arch=("x86_64")
license=('GPL3')
url="https://www.gnu.org/software/libcdio/"
depends=('lib32-gcc-libs' "libcdio")
source=(https://ftp.gnu.org/gnu/libcdio/${_pkgname}-${pkgver}.tar.gz{,.sig}
        libcdio-0.83-linking.patch)
sha256sums=('96e2c903f866ae96f9f5b9048fa32db0921464a2286f5b586c0f02699710025a'
            'SKIP'
            'd88186b2a11726fe5ef10853e0245996b55793552b14230d1f7ba5ee3c54f3ed')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    patch -Np1 -i $srcdir/libcdio-0.83-linking.patch

    # Unbreak symbol versioning
    # https://savannah.gnu.org/bugs/?49907
    sed -i '/AM_INIT_AUTOMAKE/s/subdir-objects//' configure.ac

    autoreconf -fi
}	

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd "${srcdir}/${_pkgname}-${pkgver}"
    ./configure --prefix=/usr --disable-vcd-info --enable-cpp-progs \
        --libdir=/usr/lib32 --without-cd-drive --without-cd-info \
        --without-cdda-player --without-cd-read --without-iso-info \
        --without-iso-read --disable-cddb
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make -j1 DESTDIR="${pkgdir}" install

    rm -rf ${pkgdir}/usr/bin ${pkgdir}/usr/include ${pkgdir}/usr/share
}
