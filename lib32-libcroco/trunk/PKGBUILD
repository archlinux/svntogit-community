# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: GordonGR <ntheo1979@gmail.com>
# Contributor: speps <speps.aur@archlinux.org>

pkgname=lib32-libcroco
pkgver=0.6.11
pkgrel=1
pkgdesc='GNOME CSS2 parsing and manipulation toolkit'
arch=('x86_64')
url="http://www.gnome.org/"
license=('LGPL')
depends=('lib32-glib2' 'lib32-libxml2' 'libcroco')
makedepends=('gcc-multilib')
source=("https://download.gnome.org/sources/libcroco/0.6/libcroco-${pkgver}.tar.xz")
md5sums=('dabc1911dfbfa85f8e6859ca47863168')

build() {
  cd libcroco-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  make
}

package() {
  cd libcroco-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
