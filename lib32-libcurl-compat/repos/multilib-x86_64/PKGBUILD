# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Piotr Balcerowski <piotr@balcerowski.org>

pkgname=lib32-libcurl-compat
pkgver=7.52.1
pkgrel=1
pkgdesc='An URL retrieval library (without versioned symbols)'
arch=('x86_64')
url='https://curl.haxx.se'
license=('MIT')
depends=('lib32-glibc' 'lib32-krb5' 'lib32-libssh2' 'lib32-libpsl'
         'lib32-openssl' 'lib32-zlib' 'libcurl-compat')
options=('strip')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc}
        0001-vtls-s-SSLEAY-OPENSSL.patch)
sha256sums=('a8984e8b20880b621f61a62d95ff3c0763a3152093a9f9ce4287cfd614add6ae'
            'SKIP'
            'a850c761e5fd1c312328a0168c9ce2eefbf9a96f960c5db9e6e4812693379b49')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

prepare() {
  cd curl-${pkgver}
  patch -p1 < "${srcdir}/0001-vtls-s-SSLEAY-OPENSSL.patch" # FS#52287
}

build() {
  cd curl-${pkgver}

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-versioned-symbols \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --with-gssapi \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-compat}.so.4.4.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0; do
    ln -s libcurl-compat.so.4.4.0 "${pkgdir}"/usr/lib32/libcurl.so.${version}
  done

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libcurl-compat "${pkgdir}"/usr/share/licenses/lib32-libcurl-compat
}

# vim: ts=2 sw=2 et:
