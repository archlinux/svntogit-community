# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lizao (Larry) Li <lzlarryli@gmail.com>

pkgname=lib32-libcurl-gnutls
pkgver=7.75.0
pkgrel=1
pkgdesc='An URL retrieval library (linked against gnutls)'
arch=('x86_64')
url='https://curl.haxx.se'
license=('MIT')
depends=('lib32-curl' 'lib32-glibc' 'lib32-gnutls' 'lib32-libidn2'
         'lib32-libpsl' 'lib32-nettle' 'lib32-libssh2' 'lib32-zlib'
         'lib32-zstd' 'libcurl-gnutls')
makedepends=('gcc-multilib')
options=('strip')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha512sums=('ebef01be48e05f46f7d0605966d4c66391d5a51f6d102265647522f8f5df61b5440c930bd1b043d56968769869275123122826a9ec7fb3148c72a28ee32ce696'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

build() {
  cd curl-${pkgver}

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --disable-versioned-symbols \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --without-gssapi \
    --with-libssh2 \
    --without-libidn \
    --without-ssl \
    --with-gnutls='/usr' \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib32/libcurl{,-gnutls}.so.4.7.0
  rm "${pkgdir}"/usr/lib32/libcurl.{a,so}*
  for version in 3 4 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0; do
    ln -s libcurl-gnutls.so.4.7.0 "${pkgdir}"/usr/lib32/libcurl-gnutls.so.${version}
  done

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libcurl-gnutls "${pkgdir}"/usr/share/licenses/lib32-libcurl-gnutls
}

# vim: ts=2 sw=2 et:
