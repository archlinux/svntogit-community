# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_name=elfutils
pkgname=lib32-libelf
pkgver=0.186
pkgrel=3
pkgdesc="Handle ELF object files and DWARF debugging information (libraries) (32-bit)"
arch=(x86_64)
url="https://sourceware.org/elfutils/"
license=(LGPL3 GPL3)
depends=(lib32-curl lib32-bzip2 lib32-gcc-libs lib32-xz lib32-zlib
lib32-zstd "libelf=${pkgver}")
# NOTE: the shared objects can not be added to provides as they are not versioned
options=(debug)
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2"{,.sig})
options=(staticlibs)
sha512sums=('c9180b27ec62935f18b9431268d176f6023d1bb938731d2af6e7626ae460af6608a70ba68483aa1ec7e6cb0fa0528b661ca8b68bc4f58ea8e18af527c5950c78'
            'SKIP')
b2sums=('49573d0a1f3519eab81d1ba3e94354cbc0935a36a94a3fdb22fe223a47b78cca8fd6e322870b0e335a809529fa6f54180b13c67936dec0242123c54ba20c9fc9'
        'SKIP')
validpgpkeys=(
  '47CC0331081B8BC6D0FD4DA08370665B57816A6A' # Mark J. Wielaard <mark@klomp.org>
  'EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A' # Mark Wielaard <mjw@gnu.org>
)

prepare() {
  (
  cd "${_name}-${pkgver}"
  autoreconf -fiv
  )

  cp -av "${_name}-${pkgver}" "${_name}-test-${pkgver}"
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  (
  # fat-lto-objects is required for non-mangled .a files in libelf
  CFLAGS+=" -ffat-lto-objects"
  cd "${_name}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --disable-debuginfod
  make
  )
  (
  # debugging information is required for test-suite
  CFLAGS+=" -g"
  # fat-lto-objects is required for non-mangled .a files in libelf
  CFLAGS+=" -ffat-lto-objects"
  cd "${_name}-test-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --disable-debuginfod
  make
  )
}

check() {
  cd "${_name}-test-${pkgver}"
  make check || printf "Fails in restrictive build environments: https://gitlab.archlinux.org/archlinux/infrastructure/-/issues/424\n"
}

package() {
  cd "${_name}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  # remove anything that would conflict with elfutils and debuginfod
  rm -rf "${pkgdir}/"{etc,usr/{bin,include,share}}
}
