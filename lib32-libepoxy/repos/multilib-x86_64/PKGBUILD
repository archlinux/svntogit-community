# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-libepoxy
pkgver=1.5.8
pkgrel=1
pkgdesc='Library handling OpenGL function pointer management'
url='https://github.com/anholt/libepoxy'
arch=('x86_64')
license=('MIT')
depends=('lib32-glibc')
makedepends=('git' 'lib32-libgl' 'lib32-systemd' 'meson')
source=("git+https://github.com/anholt/libepoxy?signed#tag=${pkgver}")
sha256sums=('SKIP')
validpgpkeys=('53EF3DC3B63E2899271BD26322E8091EEA11BBB7') # Emmanuele Bassi <ebassi@gnome.org>

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson libepoxy build \
    --libdir='/usr/lib32'
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir="${pkgdir}"
  rm -rf "${pkgdir}"/usr/include
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libepoxy "${pkgdir}"/usr/share/licenses/lib32-libepoxy
}

# vim: ts=2 sw=2 et:
