# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libffi
pkgver=3.2.1
pkgrel=1
pkgdesc="A portable, high level programming interface to various calling conventions (32-bit)"
arch=('x86_64')
license=('MIT')
url="http://sourceware.org/libffi/"
depends=('lib32-glibc')
checkdepends=('dejagnu')
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
sha1sums=('280c265b789e041c02e5c97815793dfc283fb1e6')

build() {
  cd libffi-${pkgver}

  export CC="gcc -m32"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
    --disable-static --enable-pax_emutramp

  make
}

check() {
  make -C libffi-${pkgver} check
}

package() {
  cd libffi-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -r "${pkgdir}"/usr/share/{info,man}
}
