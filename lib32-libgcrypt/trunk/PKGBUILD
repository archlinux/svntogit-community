# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgbasename=libgcrypt
pkgname=lib32-$_pkgbasename
pkgver=1.9.3
pkgrel=1
pkgdesc="General purpose cryptographic library based on the code from GnuPG (32-bit)"
arch=(x86_64)
url="https://www.gnupg.org"
license=('LGPL')
depends=('lib32-libgpg-error' $_pkgbasename)
makedepends=(gcc-multilib libtool-multilib)
source=(https://gnupg.org/ftp/gcrypt/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2{,.sig})
sha1sums=('6b18f453fee677078586279d96fb88e5df7b3f35'
          'SKIP')
validpgpkeys=('6DAA6E64A76D2840571B4902528897B826403ADA') # "Werner Koch (dist signing 2020)"

prepare() {
  cd ${_pkgbasename}-${pkgver}

  # Use 32-bit assembler
  sed 's:i586\*-\*-\*:x86_64-*-*:' -i mpi/config.links
  sed 's:x86_64-\*-\*:ignore:;s:i?86-\*-\*:x86_64-*-*:' -i configure.ac

  autoreconf -fi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr --disable-static --disable-padlock-support \
              --libdir=/usr/lib32 --enable-shared
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}
}
