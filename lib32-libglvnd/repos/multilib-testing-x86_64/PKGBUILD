# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-libglvnd
pkgver=0.1.0.20160329
_commit=af2aeb0b4293e085ad61eae08be34e52f9d7b389
pkgrel=2
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
depends=('lib32-libxext' 'libglvnd')
makedepends=('xorg-server-devel' 'git' 'python2')
source=("git+https://github.com/NVIDIA/libglvnd.git#commit=$_commit")
sha1sums=('SKIP')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd libglvnd
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib32/ --build=i686-unknown-linux-gnu
  make
}

package() {
  cd libglvnd
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir/usr/include"

  # For compatibility with older nvidia drivers for bumblebee
  # Symlinks to /usr/lib will be in nvidia-libgl
  mkdir "$pkgdir/usr/lib32/nvidia"
  for _lib in libGL.so libGLESv1_CM.so libGLESv2.so; do
    mv "$pkgdir"/usr/lib32/$_lib* "$pkgdir"/usr/lib32/nvidia/
  done

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s libglvnd "$pkgdir/usr/share/licenses/$pkgname"
}
