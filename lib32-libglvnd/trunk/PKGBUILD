# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lib32-libglvnd
pkgver=1.0.0
pkgrel=1
pkgdesc="The GL Vendor-Neutral Dispatch library"
arch=('x86_64')
url="https://github.com/NVIDIA/libglvnd"
license=('custom:BSD-like')
makedepends=('lib32-libx11' 'lib32-libxext' 'glproto' 'python')
provides=('lib32-libgl' 'lib32-libegl' 'lib32-libgles')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NVIDIA/libglvnd/archive/v$pkgver.tar.gz")
sha512sums=('c6040b0a508a6b74d7113f8e375fcaf93ec6f2875504953f30e2882e58e22f954bd4027a7d9ab8ac41abef266039e5834fdaf4ce2a69d5d98acf3bbdd54f04b6')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd libglvnd-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib32/ --build=i686-unknown-linux-gnu
  make
}

package() {
  # lib32-libglvnd needs lib32-mesa for indirect rendering
  depends=('lib32-libxext' 'libglvnd' 'lib32-mesa' 'lib32-opengl-driver')

  cd libglvnd-$pkgver
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/include

  mkdir -p "$pkgdir"/usr/share/licenses
  ln -s libglvnd "$pkgdir"/usr/share/licenses/$pkgname
}
