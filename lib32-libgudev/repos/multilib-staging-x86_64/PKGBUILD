# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=lib32-libgudev
pkgver=237
pkgrel=2
pkgdesc='GObject bindings for libudev'
arch=(x86_64)
url=https://wiki.gnome.org/Projects/libgudev
license=(LGPL2.1)
depends=(
  lib32-glib2
  lib32-systemd
  libgudev
)
makedepends=(
  git
  meson
  python
)
provides=(libgudev-1.0.so)
_tag=dff7a794e55d6ad7a10e8edacd73aa047593e74c
source=(git+https://gitlab.gnome.org/GNOME/libgudev.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd libgudev
  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config
  arch-meson libgudev build \
    --libdir=/usr/lib32 \
    -Dintrospection=disabled \
    -Dgtk_doc=false \
    -Dtests=disabled \
    -Dvapi=disabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
