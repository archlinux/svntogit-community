# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=lib32-libgudev
pkgver=230
pkgrel=2
pkgdesc='GObject bindings for libudev'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/libgudev'
license=('LGPL2.1')
depends=('lib32-glib2' 'lib32-systemd' 'libgudev')
makedepends=('gcc-multilib')
conflicts=('lib32-systemd<221')
source=("https://download.gnome.org/sources/libgudev/${pkgver}/libgudev-${pkgver}.tar.xz")
sha256sums=('a2e77faced0c66d7498403adefcc0707105e03db71a2b2abd620025b86347c18')

build() {
  cd libgudev-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --disable-introspection
  make
}

package() {
  cd libgudev-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/{include,share}
}

# vim: ts=2 sw=2 et:
