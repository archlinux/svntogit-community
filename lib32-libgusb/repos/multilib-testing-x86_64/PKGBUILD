# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libgusb
pkgver=0.3.10
pkgrel=2
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64)
license=(LGPL2.1)
depends=(
  lib32-glib2
  lib32-libusb
  libgusb
)
makedepends=(
  git
  meson
)
_tag=332d5b987ffecb824426e88518e05547faf2b520
source=(git+https://github.com/hughsie/libgusb.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd libgusb

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson libgusb build \
    --libdir='/usr/lib32' \
    -Ddocs=false \
    -Dintrospection=false \
    -Dvapi=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
