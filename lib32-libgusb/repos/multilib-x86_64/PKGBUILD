# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libgusb
pkgver=0.4.2
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64)
license=(LGPL2.1)
depends=(
  lib32-glib2
  lib32-json-glib
  lib32-libusb
  libgusb
)
makedepends=(
  git
  meson
)
_tag=dd5a500ed97be6d9ed2d033b8147b7878b86005a
source=(git+https://github.com/hughsie/libgusb.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd libgusb
  git describe --tags
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config
  arch-meson libgusb build \
    --libdir=/usr/lib32 \
    -Ddocs=false \
    -Dintrospection=false \
    -Dumockdev=disabled \
    -Dvapi=false
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
