# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libgusb
pkgver=0.3.1
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
arch=(x86_64)
url=https://github.com/hughsie/libgusb
license=(LGPL2.1)
depends=(
  lib32-glib2
  lib32-libusb
  libgusb
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  python2
  vala
)
source=(git+https://github.com/hughsie/libgusb#tag=7f2afe5dc3bcb6ce0d702856c9c0973efdf6ad1d)
sha256sums=(SKIP)

pkgver() {
  cd libgusb

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson libgusb build \
    --libdir='/usr/lib32'
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
