# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libgusb
pkgver=0.2.11
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
arch=('x86_64')
url='https://github.com/hughsie/libgusb'
license=('LGPL2.1')
depends=('lib32-glib2' 'lib32-glibc' 'lib32-libusb' 'libgusb')
makedepends=('gcc-multilib' 'git' 'gobject-introspection' 'gtk-doc' 'python2')
_commit='732b49b528cc69bf4c8999e088835ecab9d5c05c'
source=("git+https://github.com/hughsie/libgusb#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libgusb

  git describe --tags | sed 's/^gusb_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd libgusb

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libgusb

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-gtk-doc \
    --disable-static
  make
}

package() {
  cd libgusb

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
