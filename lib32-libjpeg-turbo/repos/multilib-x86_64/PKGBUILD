# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_name=libjpeg-turbo
pkgname=lib32-${_name}
pkgver=2.1.2
pkgrel=1
pkgdesc='JPEG image codec with accelerated baseline compression and decompression'
arch=(x86_64)
url="https://libjpeg-turbo.org/"
license=(BSD)
depends=(lib32-glibc "${_name}=${pkgver}")
makedepends=(nasm cmake)
provides=(lib32-libjpeg libjpeg.so libturbojpeg.so)
source=("https://sourceforge.net/projects/${_name}/files/${pkgver}/${_name}-${pkgver}.tar.gz"{,.sig})
sha512sums=('f0377b58d00310b2e644da31f807ac6c3425823f362ea88bcbed32991dddbea4e7a478676a7c1c4874a34bc578f262bf1d32c925e2f14fa517a50fd911ba8dd5'
            'SKIP')
b2sums=('ae8b7db9fd634b3b29a916098e2b54a8e045cb7a9d883de1f2e8a34542154a724e5d7788fb4b4d816c36d6503fc2a7427165dc3af9d47c77bfb7c51a42bc1d51'
        'SKIP')
validpgpkeys=('0338C8D8D9FDA62CF9C421BD7EC2DBB6F4DBF434') # The libjpeg-turbo Project (Signing key for official binaries) <information@libjpeg-turbo.org>

build() {
  cd "${_name}-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
        -DCMAKE_BUILD_TYPE=None \
        -DWITH_JPEG8=ON \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${_name}-$pkgver"
  make test -C build
}

package() {
  cd "${_name}-$pkgver"

  make VERBOSE=1 DESTDIR="${pkgdir}" \
       docdir="/usr/share/doc/${_name}" \
       exampledir="/usr/share/doc/${_name}" \
       install -C build
  # remove everything that is provided by libjpeg-turbo
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  # license
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
