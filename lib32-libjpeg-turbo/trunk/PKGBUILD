# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_pkgbasename=libjpeg-turbo
pkgname=lib32-$_pkgbasename
pkgver=1.5.3
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (32-bit)"
arch=('x86_64')
url="https://libjpeg-turbo.org/"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('nasm')
provides=('lib32-libjpeg=8.1.2' 'lib32-turbojpeg')
conflicts=('lib32-libjpeg' 'lib32-turbojpeg')
replaces=('lib32-libjpeg' 'lib32-turbojpeg')
options=('!libtool')
validpgpkeys=('7D6293CC6378786E1B5C496885C7044E033FDE16')
source=(https://sourceforge.net/projects/$_pkgbasename/files/$pkgver/$_pkgbasename-$pkgver.tar.gz{,.sig})
sha512sums=('b611b1cc3d1ddedddad871854b42449d053a5f910ed1bdfa45c98e0270f4ecc110fde3a10111d2b876d847a826fa634f09c0bb8c357056c9c3a91c9065eb5202'
            'SKIP')

build() {
  cd "$_pkgbasename-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --libdir=/usr/lib32 \
    --with-jpeg8 \
    --without-simd \
    --with-build-date="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y%m%d)" \

  make
}

check() {
  cd "$_pkgbasename-$pkgver"
  make test
}

package() {
  cd "$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" docdir=/usr/share/doc/libjpeg-turbo \
    exampledir=/usr/share/doc/libjpeg-turbo install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 ChangeLog.md -t"${pkgdir}/usr/share/doc/${pkgname}"
}
