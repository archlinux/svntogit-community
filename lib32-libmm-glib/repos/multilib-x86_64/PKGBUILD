# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libmm-glib
pkgver=1.6.10
pkgrel=1
pkgdesc='ModemManager library'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/ModemManager/'
license=('GPL2' 'LGPL2.1')
depends=('lib32-glib2' 'libmm-glib')
makedepends=('gcc-multilib' 'intltool' 'lib32-libmbim' 'lib32-libqmi'
             'lib32-polkit' 'lib32-systemd' 'python' 'vala')
source=("https://www.freedesktop.org/software/ModemManager/ModemManager-${pkgver}.tar.xz")
sha256sums=('bc74326fa69ae8012f806e235f3d296144922669b952d4d4987dd0af645d5f68')

build() {
  cd ModemManager-${pkgver}

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --disable-gtk-doc-html \
    --disable-static \
    --with-dbus-sys-dir='/usr/share/dbus-1/system.d' \
    --with-polkit='permissive' \
    --with-suspend-resume='systemd' \
    --with-udev-base-dir='/usr/lib32/udev'
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C libmm-glib
  make -C data
}

package() {
  cd ModemManager-${pkgver}

  make DESTDIR="${pkgdir}" -C libmm-glib install
  make DESTDIR="${pkgdir}" -C data install
  rm -rf "${pkgdir}"/{etc,usr/{include,lib,share}}
}

# vim: ts=2 sw=2 et:
