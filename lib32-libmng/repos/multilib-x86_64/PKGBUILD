# $Id$
# Maintainer: Florian Pritz <flo@xssn.at>
_pkgbasename=libmng
pkgname=lib32-$_pkgbasename
pkgver=1.0.10
pkgrel=5
pkgdesc="A collection of routines used to create and manipulate MNG format graphics files (32-bit)"
arch=('x86_64')
url="http://www.libmng.com/"
license=('custom')
depends=('lib32-zlib' 'lib32-libjpeg>=8' $_pkgbasename)
makedepends=(gcc-multilib)
options=(!libtool)
source=(http://downloads.sourceforge.net/sourceforge/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz)
md5sums=('a464ae7d679781beebdf7440d144b7bd')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/$_pkgbasename-$pkgver
  ln -s makefiles/configure.in .
  ln -s makefiles/Makefile.am .
  autoreconf --force --install
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $srcdir/$_pkgbasename-$pkgver

  make DESTDIR=$pkgdir install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
