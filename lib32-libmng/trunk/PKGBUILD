# $Id$
# Maintainer: Florian Pritz <flo@xssn.at>

_pkgbasename=libmng
pkgname=lib32-$_pkgbasename
pkgver=2.0.2
pkgrel=1
pkgdesc="A collection of routines used to create and manipulate MNG format graphics files (32-bit)"
arch=('x86_64')
url="http://www.libmng.com/"
license=('custom')
depends=('lib32-lcms2' $_pkgbasename)
makedepends=(gcc-multilib)
options=(!libtool)
source=(http://downloads.sourceforge.net/sourceforge/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.xz{,.asc})
validpgpkeys=(8048643BA2C840F4F92A195FF54984BFA16C640F)
md5sums=('3804bf2523af9b4e0670b5982b3bf984'
         'SKIP')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $_pkgbasename-$pkgver

  make DESTDIR=$pkgdir install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
