# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>

pkgbase=lib32-libnm
pkgname=('lib32-libnm' 'lib32-libnm-glib')
pkgver=1.10.2
pkgrel=1
arch=('x86_64')
url='https://wiki.gnome.org/Projects/NetworkManager'
license=('GPL2' 'LGPL2.1')
makedepends=('dhclient' 'gcc-multilib' 'git' 'gtk-doc' 'intltool' 'iproute2'
             'iptables' 'lib32-dbus' 'lib32-dbus-glib' 'lib32-jansson'
             'lib32-libgudev' 'lib32-libmm-glib' 'lib32-libndp' 'lib32-libnewt'
             'lib32-libnl' 'lib32-libpsl' 'lib32-libsoup' 'lib32-nspr'
             'lib32-nss' 'lib32-polkit' 'lib32-util-linux' 'modemmanager'
             'perl-yaml' 'python-gobject' 'systemd' 'wpa_supplicant')
_commit='7ebc9258452623679b9f1c27aee94c528c14b273'
source=("git+https://anongit.freedesktop.org/git/NetworkManager/NetworkManager#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd NetworkManager

  git describe | sed 's/-dev/dev/;s/-/+/g'
}

prepare() {
  cd NetworkManager

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd NetworkManager

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/NetworkManager' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --enable-json-validation \
    --enable-ld-gc \
    --enable-modify-system \
    --enable-polkit \
    --enable-polkit-agent \
    --enable-wifi \
    --disable-bluez5-dun \
    --disable-concheck \
    --disable-gtk-doc-html \
    --disable-introspection \
    --disable-ifcfg-rh \
    --disable-ifcfg-suse \
    --disable-ifnet \
    --disable-ifupdown \
    --disable-introspection \
    --disable-lto \
    --disable-more-warnings \
    --disable-ppp \
    --disable-static \
    --disable-teamdctl \
    --with-config-dhcp-default='dhclient' \
    --with-config-dns-rc-manager-default='resolvconf' \
    --with-config-logging-backend-default='journal' \
    --with-config-plugins-default='keyfile' \
    --with-crypto='nss' \
    --with-dbus-sys-dir='/usr/share/dbus-1/system.d' \
    --with-dhclient='/usr/bin/dhclient' \
    --with-dist-version="${pkgver}-${pkgrel}, Arch Linux" \
    --with-dnsmasq='/usr/bin/dnsmasq' \
    --with-dnssec-trigger='/usr/lib/dnssec-trigger/dnssec-trigger-script' \
    --with-hostname-persist='default' \
    --with-iptables='/usr/bin/iptables' \
    --with-modem-manager-1 \
    --with-kernel-firmware-dir='/usr/lib/firmware' \
    --with-libnm-glib \
    --with-libsoup \
    --with-modem-manager-1 \
    --with-resolvconf='/usr/bin/resolvconf' \
    --with-session-tracking='systemd' \
    --with-suspend-resume='systemd' \
    --with-system-ca-path='/etc/ssl/certs' \
    --with-systemd-journal \
    --with-systemd-logind \
    --with-systemdsystemunitdir='/usr/lib/systemd/system' \
    --with-udev-dir='/usr/lib/udev' \
    --with-wext \
    --without-consolekit \
    --without-dhcpcd \
    --without-libaudit \
    --without-netconfig \
    --without-nmcli \
    --without-nmtui \
    --without-ofono \
    --without-selinux

  sed -e 's/ -shared / -Wl,-O1,--as-needed\0/g' -i libtool

  make DESTDIR="${srcdir}"/install install
}

package_lib32-libnm() {
  pkgdesc='NetworkManager client library'
  depends=('lib32-glib2' 'lib32-glibc' 'lib32-jansson' 'lib32-libgudev'
           'lib32-nspr' 'lib32-nss' 'lib32-util-linux' 'libnm')

  install -dm 755 "${pkgdir}"/usr/lib32/pkgconfig
  cp -dr --no-preserve='ownership' install/usr/lib32/libnm.so* "${pkgdir}"/usr/lib32/
  cp -dr --no-preserve='ownership' install/usr/lib32/pkgconfig/libnm.pc "${pkgdir}"/usr/lib32/pkgconfig/

}

package_lib32-libnm-glib() {
  pkgdesc='NetworkManager client library (legacy)'
  depends=('lib32-dbus' 'lib32-dbus-glib' 'lib32-glib2' 'lib32-glibc'
           'lib32-libgudev' 'lib32-nspr' 'lib32-nss' 'lib32-util-linux'
           'libnm-glib')

  install -dm 755 "${pkgdir}"/usr/lib32/pkgconfig
  cp -dr --no-preserve='ownership' install/usr/lib32/libnm-glib.so* "${pkgdir}"/usr/lib32/
  cp -dr --no-preserve='ownership' install/usr/lib32/pkgconfig/libnm-glib.pc "${pkgdir}"/usr/lib32/pkgconfig/
  cp -dr --no-preserve='ownership' install/usr/lib32/libnm-glib-vpn.so* "${pkgdir}"/usr/lib32/
  cp -dr --no-preserve='ownership' install/usr/lib32/pkgconfig/libnm-glib-vpn.pc "${pkgdir}"/usr/lib32/pkgconfig/
  cp -dr --no-preserve='ownership' install/usr/lib32/libnm-util.so* "${pkgdir}"/usr/lib32/
  cp -dr --no-preserve='ownership' install/usr/lib32/pkgconfig/libnm-util.pc "${pkgdir}"/usr/lib32/pkgconfig/
}

# vim: ts=2 sw=2 et:
