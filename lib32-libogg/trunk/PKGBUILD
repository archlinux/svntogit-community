# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libogg
pkgver=1.3.3
pkgrel=2
pkgdesc='Ogg bitstream and framing library (32-bit)'
arch=('x86_64')
url='http://www.xiph.org/ogg/'
license=('BSD')
depends=(lib32-glibc libogg)
makedepends=(gcc-multilib)
source=("https://downloads.xiph.org/releases/ogg/libogg-${pkgver}.tar.xz")
sha256sums=('4f3fc6178a533d392064f14776b23c397ed4b9f48f5de297aba73b643f955c08')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd libogg-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd libogg-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/usr/{share,include,bin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s libogg "$pkgdir/usr/share/licenses/$pkgname"
}
