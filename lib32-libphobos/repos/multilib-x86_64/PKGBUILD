# $Id: PKGBUILD 72006 2012-06-05 14:34:01Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=('lib32-libphobos')
_pkgname=dmd
pkgver=2.059
pkgrel=1
pkgdesc="The phobos D standard library for DMD (32-bit)"
arch=('x86_64')
url="http://www.digitalmars.com/d/2.0/"
source=(http://ftp.digitalmars.com/$_pkgname.$pkgver.zip)
makedepends=('dmd')
md5sums=('803b182e71c4b021dfd1811066201140')
license=('custom')

build() {
    cd $srcdir/dmd2/src/

    cd druntime
    make -f posix.mak MODEL=32

    cd ../phobos
    make -f posix.mak MODEL=32
}

package() {
  install -Dm644 $srcdir/dmd2/src/druntime/lib/libdruntime-linux32.a $pkgdir/usr/lib32/libdruntime.a
  install -Dm644 $srcdir/dmd2/src/phobos/generated/linux/release/32/libphobos2.a $pkgdir/usr/lib32/libphobos2.a

  install -Dm644 $srcdir/dmd2/src/druntime/LICENSE_1_0.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE-druntime
  install -Dm644 $srcdir/dmd2/src/phobos/phoboslicense.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE-phobos
}
