# Maintainer: Mihails Strasuns <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=('lib32-libphobos' 'lib32-libphobos-devel')
groups=('dlang' 'dlang-dmd')
pkgver=2.071.0
pkgrel=2
pkgdesc="Libraries for cross-compiling 32-bit D apps from 64-bit system"
arch=('x86_64')
groups=('dlang' 'dlang-dmd')
url="http://www.dlang.org"
source=("git+http://github.com/D-Programming-Language/phobos.git#tag=v$pkgver"
        "git+http://github.com/D-Programming-Language/druntime.git#tag=v$pkgver")
sha1sums=('SKIP'
          'SKIP')
makedepends=('dmd' 'git')
depends=('lib32-gcc-libs')
license=('custom')

build() {
    cd $srcdir/druntime
    make -f posix.mak MODEL=32 RELEASE=1 DMD=dmd

    cd $srcdir/phobos
    make -f posix.mak MODEL=32 RELEASE=1 DMD=dmd
}

package_lib32-libphobos-devel() {
    pkgdesc="The Phobos standard library for D programming language. Modules and static library (32-bit)"
    depends=("dmd" "gcc-multilib")
    options=("staticlibs")
    provides=("d-runtime=$pkgver" "d-stdlib=$pkgver")

    install -Dm644 $srcdir/phobos/generated/linux/release/32/libphobos2.a $pkgdir/usr/lib32/libphobos2.a

    install -Dm644 $srcdir/druntime/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE-druntime
    install -Dm644 $srcdir/phobos/LICENSE_1_0.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE-phobos
}

package_lib32-libphobos() {
    pkgdesc="The Phobos standard library for D programming language (32-bit)"

    install -Dm644 $srcdir/phobos/generated/linux/release/32/libphobos2.so $pkgdir/usr/lib32/libphobos2.so

    install -Dm644 $srcdir/druntime/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE-druntime
    install -Dm644 $srcdir/phobos/LICENSE_1_0.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE-phobos
}
