# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lib32-libpsl
pkgver=0.20.1
pkgrel=2
pkgdesc='Public Suffix List library (32-bit)'
url='https://github.com/rockdaboot/libpsl'
arch=('x86_64')
license=('MIT')
depends=('libpsl' 'lib32-glibc' 'lib32-libidn2' 'lib32-libunistring')
makedepends=('publicsuffix-list' 'python')
provides=('libpsl.so')
source=(https://github.com/rockdaboot/libpsl/releases/download/libpsl-${pkgver}/libpsl-${pkgver}.tar.gz)
sha512sums=('5a6c54c3a0aafef3eaa309bf3147179d41393fb63d7e6276a3e02705ae5c4005c61b3f8b13636eac0570ac20033c1c9b1a51937a06b56a30a4461ff9b1f955be')

prepare() {
  cd libpsl-${pkgver}
  rm -frv list
  autoreconf -fiv
}

build() {
  cd libpsl-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static \
    --disable-dependency-tracking \
    --enable-{builtin,runtime}=libidn2 \
    --with-psl-file='/usr/share/publicsuffix/effective_tld_names.dat' \
    --with-psl-testfile='/usr/share/publicsuffix/test_psl.txt'
  LC_CTYPE=en_US.UTF-8 make
}

check() {
  cd libpsl-${pkgver}
  make check
}

package() {
  cd libpsl-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libpsl "${pkgdir}"/usr/share/licenses/lib32-libpsl
}

# vim: ts=2 sw=2 et:
