# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

_pkgbasename=libpulse
pkgname=lib32-$_pkgbasename
pkgdesc="A featureful, general-purpose sound server (32-bit client libraries)"
pkgver=1.0
pkgrel=2
arch=(x86_64)
url="http://pulseaudio.org/"
license=(GPL LGPL)
depends=(lib32-dbus-core lib32-xcb-util lib32-json-c 
         lib32-libasyncns lib32-libcap lib32-libxtst lib32-libsm lib32-libsamplerate
         $_pkgbasename)
makedepends=(gcc-multilib libtool-multilib intltool lib32-glib2 lib32-speex)
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=$pkgver")
conflicts=(lib32-pulseaudio)
replaces=(lib32-pulseaudio)
options=(!emptydirs !libtool !makeflags)
source=("http://freedesktop.org/software/pulseaudio/releases/pulseaudio-$pkgver.tar.xz"
        version-number.patch)
sha256sums=('e63464f8a0975446e2b4cfe768b615a66735c8d092b2366d2bdc7a2a04a249be'
            '6b067ae9e8da703510fb24eae8ee53f3a7b6a90bcf88941ee50997ad679a4530')

_libs="libpulsecommon-$pkgver.la libpulse.la libpulse-simple.la libpulse-mainloop-glib.la libpulsedsp.la"

build() {
  cd "$srcdir/pulseaudio-$pkgver"

  patch -Np1 -i "$srcdir/version-number.patch"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-hal \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests

  make -C src $_libs
}

package() {
  cd "$srcdir/pulseaudio-$pkgver"
  make -C src lib_LTLIBRARIES="$_libs" DESTDIR="$pkgdir" install-libLTLIBRARIES
  make DESTDIR="$pkgdir" install-pkgconfigDATA
}
