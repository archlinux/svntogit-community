# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

_pkgbasename=libpulse
pkgname=lib32-$_pkgbasename
pkgdesc="A featureful, general-purpose sound server (32-bit client libraries)"
pkgver=3.99.1
pkgrel=1
arch=(x86_64)
url="http://pulseaudio.org/"
license=(GPL LGPL)
depends=(lib32-dbus-core lib32-json-c
         lib32-libasyncns lib32-libcap lib32-libxtst lib32-libsm lib32-libsndfile
         $_pkgbasename)
makedepends=(gcc-multilib libtool-multilib intltool lib32-glib2 lib32-speex lib32-gtk2)
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=$pkgver")
conflicts=(lib32-pulseaudio)
replaces=(lib32-pulseaudio)
options=(!emptydirs !libtool)
source=("http://freedesktop.org/software/pulseaudio/releases/pulseaudio-$pkgver.tar.xz")
sha256sums=('01266830d73316364075ac81a2d67e9098332533fdf9273057e6324c7bbef9c5')

_libs="libpulse.la libpulse-simple.la libpulse-mainloop-glib.la"
_pkglibs="libpulsecommon-${pkgver%.*}.la libpulsedsp.la"

build() {
  cd pulseaudio-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -C src $_libs $_pkglibs
}

package() {
  cd pulseaudio-$pkgver
  make -j1 -C src DESTDIR="$pkgdir" \
    lib_LTLIBRARIES="$_libs" pkglib_LTLIBRARIES="$_pkglibs" \
    install-libLTLIBRARIES install-pkglibLTLIBRARIES
  make -j1 DESTDIR="$pkgdir" install-pkgconfigDATA
}
