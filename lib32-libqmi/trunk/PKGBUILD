# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libqmi
pkgver=1.16.0
pkgrel=1
pkgdesc='QMI modem protocol helper library'
arch=('x86_64')
url='http://www.freedesktop.org/wiki/Software/libqmi/'
license=('GPL2')
depends=('lib32-glib2' 'lib32-glibc' 'lib32-libmbim' 'libqmi')
makedepends=('gcc-multilib' 'python')
source=("http://www.freedesktop.org/software/libqmi/libqmi-${pkgver}.tar.xz")
sha256sums=('7ab6bb47fd23bf4d3fa17424e40ea5552d08b19e5ee4f125f21f316c8086ba2a')

build() {
  cd libqmi-${pkgver}

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --disable-gtk-doc-html \
    --disable-static \
    --without-python
  make
}

package() {
  cd libqmi-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
