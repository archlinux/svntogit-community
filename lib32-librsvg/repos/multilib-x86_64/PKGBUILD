# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-librsvg
pkgver=2.40.18
pkgrel=1
pkgdesc='A SVG viewing library (32-bit)'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/LibRsvg'
license=('LGPL')
depends=('lib32-gdk-pixbuf2' 'lib32-libcroco' 'lib32-pango' 'librsvg')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python2' 'vala')
source=("https://download.gnome.org/sources/librsvg/${pkgver:0:4}/librsvg-${pkgver}.tar.xz")
sha512sums=('faf503d0d881801ce129715e7ae979ee1675bd32beac9170b1889c94aac64caf486968bc86f3480463b84e94f28c1d805551ffdcb7068ecadd0688146e0fb182')

build() {
  cd librsvg-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --enable-vala \
    --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool

  make
}

check() {
  cd librsvg-$pkgver
  make check
}

package() {
  cd librsvg-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

# vim: ts=2 sw=2 et:
