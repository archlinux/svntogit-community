# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-librsvg
pkgver=2.42.3
pkgrel=2
pkgdesc="SVG rendering library (32-bit)"
url="https://wiki.gnome.org/Projects/LibRsvg"
arch=(x86_64)
license=(LGPL)
depends=(lib32-gdk-pixbuf2 lib32-pango lib32-libcroco librsvg)
makedepends=(intltool gobject-introspection vala python2 gtk-doc git lib32-rust)
_commit=ca640d1185745665c61689b71c627e1d020a80b7  # tags/2.42.3^0
source=("git+https://gitlab.gnome.org/GNOME/librsvg.git#commit=$_commit"
        multilib.diff)
sha256sums=('SKIP'
            '1f61c67a7527dbf95d47e3782b0824b6ea5c0ce27bcd8b86de07b616a9b0a33e')

pkgver() {
  cd librsvg
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd librsvg

  # Hack to cross-compile rust only
  patch -Np1 -i ../multilib.diff

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd librsvg
  ./configure --prefix=/usr --disable-static --enable-vala --disable-gtk-doc --libdir=/usr/lib32
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

check() {
  cd librsvg
  make check
}

package() {
  cd librsvg
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
