# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libsm
pkgname=lib32-$_pkgbasename
pkgver=1.1.1
pkgrel=3
pkgdesc="X11 Session Management library (32-bit)"
arch=(x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libice' 'lib32-util-linux-ng>=2.16' $_pkgbasename)
makedepends=('pkgconfig' 'xtrans>=1.2.4' 'gcc-multilib')
options=('!libtool')
source=(${url}/releases/individual/lib/libSM-${pkgver}.tar.bz2)
md5sums=('6889a455496aaaa65b1fa05fc518d179')
sha1sums=('dc535af7328dee9a6121b85c3f8041656681a195')

build() {
  cd "${srcdir}/libSM-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libSM-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
