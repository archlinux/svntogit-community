# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libsndfile
pkgname=lib32-$_name
pkgver=1.2.0
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled audio data (32-bit)"
arch=(x86_64)
url="https://libsndfile.github.io/libsndfile/"
license=(LGPL2.1)
depends=(lib32-opus $_name=$pkgver)
makedepends=(cmake lib32-alsa-lib lib32-flac lib32-libogg lib32-libvorbis
lib32-sqlite python)
provides=(libsndfile.so)
options=(debug)
source=(https://github.com/$_name/$_name/releases/download/$pkgver/$_name-$pkgver.tar.xz{,.asc})
sha512sums=('c0dee6b33cd3c619aa13ec2854a5480e826640ca98a438758346194e83752af576a39f9de118824f0d0c61288632d87b489dc5793c025296e0f15564d87ca12a'
            'SKIP')
b2sums=('cb13fde9b9f44c6279a9ee7741457824eb41fc17261bf9c669d757e9e506eba2c3d4ad259e4625b8a11c82308188cc19f7399e9d1fc0299a3a11794e4b653e47'
        'SKIP')
validpgpkeys=(
  '6A91A5CF22C24C99A35E013FCFDCF91FB242ACED'  # Erik de Castro Lopo <erikd@mega-nerd.com>
  '9B1CFD2E92239C4B288E025F9D0D1F1CCB35FF8C'  # evpobr evpobr@gmail.com
  '31D95CAB6D80D262244A1750A47620E801E47E95'  # David Seifert soap@gentoo.org
)

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # tests can only be built with static libs
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_LIBDIR=lib32 \
        -DENABLE_MPEG=OFF \
        -Wno-dev \
        -B build-test \
        -S $_name-$pkgver
  make VERBOSE=1 -C build-test

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DBUILD_SHARED_LIBS=ON \
        -DCMAKE_INSTALL_LIBDIR=lib32 \
        -DENABLE_MPEG=OFF \
        -Wno-dev \
        -B build \
        -S $_name-$pkgver
  make VERBOSE=1 -C build
}

check() {
  make test -C build-test
}

package() {
  depends+=(
    lib32-flac libFLAC.so
    lib32-libogg libogg.so
    lib32-libvorbis libvorbis.so libvorbisenc.so
  )

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $_name-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"

  # remove everything that is provided by libsndfile
  rm -rf "$pkgdir/usr/"{bin,include,share/{man,doc/$_name}}
}
