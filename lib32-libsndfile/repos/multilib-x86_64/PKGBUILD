# $Id$

_pkgbasename=libsndfile
pkgname=lib32-$_pkgbasename
pkgver=1.0.21
pkgrel=4
pkgdesc="A C library for reading and writing files containing sampled sound (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('lib32-alsa-lib' 'lib32-flac>=1.2.1' 'lib32-libvorbis' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://www.mega-nerd.com/libsndfile/files/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('880a40ec636ab2185b97f8927299b292')
sha1sums=('136845a8bb5679e033f8f53fb98ddeb5ee8f1d97')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64"

  ./configure --prefix=/usr --disable-sqlite --libdir=/usr/lib32
  make -C src
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -C src DESTDIR="${pkgdir}" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA

  rm -rf "$pkgdir/usr/include"
}
