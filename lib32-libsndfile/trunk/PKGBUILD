# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libsndfile
pkgname=lib32-$_name
pkgver=1.1.0
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled audio data (32-bit)"
arch=(x86_64)
url="https://libsndfile.github.io/libsndfile/"
license=(LGPL2.1)
depends=(lib32-opus $_name=$pkgver)
makedepends=(cmake lib32-alsa-lib lib32-flac lib32-libogg lib32-libvorbis
lib32-sqlite python)
provides=(libsndfile.so)
options=(debug)
source=(https://github.com/$_name/$_name/releases/download/$pkgver/$_name-$pkgver.tar.xz{,.asc})
sha512sums=('d01696a8a88a4444e5eb91a137cf7b26b55b12c1fe3b648653f7e78674bbdf61870066216c9ff2f6a1e63bdf7b558af9a759480cf6523b607d29347b12762006'
            'SKIP')
b2sums=('ff2d27bff460c4d3c549bdf70c84c6f17a2c3e10674a44216ed83e657e603ddfbc33afe0827676fc4f5dc7745a9fb6aa866023fe589d0c00714477e3a246e5f2'
        'SKIP')
validpgpkeys=(
  '6A91A5CF22C24C99A35E013FCFDCF91FB242ACED'  # Erik de Castro Lopo <erikd@mega-nerd.com>
  '9B1CFD2E92239C4B288E025F9D0D1F1CCB35FF8C'  # evpobr evpobr@gmail.com
  '31D95CAB6D80D262244A1750A47620E801E47E95'  # David Seifert soap@gentoo.org
)

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # tests can only be built with static libs
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_LIBDIR=lib32 \
        -Wno-dev \
        -B build-test \
        -S $_name-$pkgver
  make VERBOSE=1 -C build-test

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_SHARED_LIBS=ON \
        -DCMAKE_INSTALL_LIBDIR=lib32 \
        -Wno-dev \
        -B build \
        -S $_name-$pkgver
  make VERBOSE=1 -C build
}

check() {
  make test -C build-test
}

package() {
  depends+=(libFLAC.so libogg.so libvorbis.so libvorbisenc.so)

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $_name-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname"

  # remove everything that is provided by libsndfile
  rm -rf "$pkgdir/usr/"{bin,include,share/{man,doc/$_name}}
}
