# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maximilian Stein <theoddbird@posteo.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libsoup
pkgver=2.52.1
pkgrel=1
pkgdesc='GNOME HTTP Library'
arch=('x86_64')
url='http://www.gnome.org'
license=('LGPL')
depends=('lib32-glib-networking' 'lib32-libxml2' 'lib32-sqlite' 'libsoup')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python'
             'vala')
conflicts=('lib32-libsoup-gnome')
replaces=('lib32-libsoup-gnome')
options=('!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/libsoup/${pkgver%.*}/libsoup-${pkgver}.tar.xz")
sha256sums=('0e19bca047ad50b28e8ed7663840f9e45a94909148822ca44dcb3e8cafb5cc48')

build() {
  cd libsoup-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  make
}

package() {
  cd libsoup-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
