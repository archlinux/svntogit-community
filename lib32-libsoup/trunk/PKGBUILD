# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maximilian Stein <theoddbird@posteo.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libsoup
pkgver=2.74.0
pkgrel=2
pkgdesc='HTTP client/server library for GNOME'
arch=(x86_64)
url=https://wiki.gnome.org/Projects/libsoup
license=(LGPL)
depends=(
  lib32-brotli
  lib32-glib-networking
  lib32-krb5
  lib32-libxml2
  lib32-sqlite
  lib32-libpsl
  libsoup
)
makedepends=(
  git
  meson
  samba
)
checkdepends=(
  apache
  php-apache
)
optdepends=('samba: Windows Domain SSO')
_tag=590611dac35a02ff16a556f97de9eea249e75d06
source=(git+https://gitlab.gnome.org/GNOME/libsoup.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd libsoup

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  arch-meson libsoup build \
    --libdir=/usr/lib32 \
    -D introspection=disabled \
    -D sysprof=disabled \
    -D vapi=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(
    libgssapi_krb5.so
    libg{lib,object,io}-2.0.so
    libpsl.so
  )
  provides+=(libsoup{,-gnome}-2.4.so)

  meson install -C build --destdir "${pkgdir}"
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
