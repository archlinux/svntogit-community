# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Nicky726 <nicky726@gmail.com>
# Contributor: Tom Killian <tom@archlinux.org>
# Contributor: RÃ©my Oudompheng <remyoudompheng@gmail.com>

pkgname=lib32-libtheora
pkgver=1.1.1
pkgrel=8
pkgdesc="An open video codec developed by the Xiph.org"
arch=('x86_64')
url="http://www.xiph.org"
license=('BSD')
depends=('lib32-libogg' "${pkgname#*-}")
makedepends=('gcc-multilib' 'lib32-libvorbis')
options=('!libtool')
source=("http://downloads.xiph.org/releases/theora/${pkgname#*-}-${pkgver}.tar.bz2")
sha256sums=('b6ae1ee2fa3d42ac489287d3ec34c5885730b1296f0801ae577a35193d3affbc')

build() {
  cd "${srcdir}"/${pkgname#*-}-${pkgver}

# Patch
  sed -i 's|AC_DEFINE(\[OC_X86_64_ASM\]|#AC_DEFINE(\[OC_X86_64_ASM\]|
          s|AM_CONFIG_HEADER|AC_CONFIG_HEADERS|' configure.ac

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./autogen.sh --prefix=/usr --libdir=/usr/lib32 --disable-{examples,static} --with-pic
  make
}

package() {
  cd "${srcdir}"/${pkgname#*-}-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
