# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: arjan <arjan@archlinux.org>

pkgname=lib32-libusb-compat
pkgver=0.1.3
pkgrel=2
pkgdesc='Library to enable user space application programs to communicate with USB devices'
arch=('x86_64')
depends=('lib32-libusb' 'libusb-compat')
makedepends=('gcc-multilib')
url="http://libusb.sourceforge.net/"
license=('LGPL')
source=(http://downloads.sourceforge.net/libusb/libusb-compat-0.1/libusb-compat-$pkgver/libusb-compat-${pkgver}.tar.bz2)
options=('!libtool')
md5sums=('570ac2ea085b80d1f74ddc7c6a93c0eb')

build() {
  cd ${srcdir}/libusb-compat-${pkgver}

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/libusb-compat-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include}
}
