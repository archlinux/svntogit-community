# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: kokoko3k <kokoko3k at gmail dot com>
# Contributor: Ionut Biru <ibiru at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski at archlinux dot org>

pkgname=lib32-libva
pkgver=2.15.0
pkgrel=6
pkgdesc='Video Acceleration (VA) API for Linux (32-bit)'
arch=(x86_64)
url=https://01.org/linuxmedia/vaapi
license=(MIT)
depends=(
  lib32-libdrm
  lib32-libgl
  lib32-libxext
  lib32-libxfixes
  lib32-wayland
  libva
)
makedepends=(
  git
  lib32-libglvnd
  lib32-mesa
  meson
)
optdepends=(
  'lib32-libva-intel-driver: backend for Intel GPUs (<= Haswell)'
  'lib32-libva-mesa-driver: backend for AMD and Nvidia GPUs'
)
provides=(
  libva-drm.so
  libva-glx.so
  libva-wayland.so
  libva-x11.so
  libva.so
)
options=(debug)
_tag=b095d10bf355110352e75c22e581018a7ea7de5a
source=(git+https://github.com/intel/libva.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd libva
  git cherry-pick -n c49f7ea61116b030c9df251f111fffbc3235d224 # https://bugs.archlinux.org/task/75312
}

pkgver() {
  cd libva
  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  CFLAGS+=" -DENABLE_VA_MESSAGING"  # Option missing
  arch-meson libva build --libdir=/usr/lib32
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -r "${pkgdir}/usr/include"
  install -Dm 644 libva/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim:set sw=2 sts=-1 et:
