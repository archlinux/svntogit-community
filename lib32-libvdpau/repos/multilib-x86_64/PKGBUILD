# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libvdpau
pkgver=1.1.1
pkgrel=3
pkgdesc='Nvidia VDPAU library'
arch=('x86_64')
url='https://cgit.freedesktop.org/~aplattner/libvdpau'
license=('custom')
depends=('lib32-gcc-libs' 'lib32-libxext' 'libvdpau')
makedepends=('dri2proto')
source=("https://people.freedesktop.org/~aplattner/vdpau/libvdpau-${pkgver}.tar.bz2"{,.sig})
validpgpkeys=('BD68A042C603DDAD9AA354B0F56ACC8F09BA9635') # Aaron Plattner
sha256sums=('857a01932609225b9a3a5bf222b85e39b55c08787d0ad427dbd9ec033d58d736'
            'SKIP')

build() {
  cd libvdpau-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --sysconfdir='/etc'
  make
}

package() {
  cd libvdpau-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libvdpau "${pkgdir}"/usr/share/licenses/lib32-libvdpau
}

# vim: ts=2 sw=2 et:
