# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: TryA <tryagainprod {at} gmail.com>

_pkgbasename=libvdpau
pkgname=lib32-libvdpau
pkgver=0.4.1
pkgrel=3
pkgdesc="Nvidia VDPAU library (32-bit)"
arch=('x86_64')
url=http://cgit.freedesktop.org/~aplattner/libvdpau
depends=(${_pkgbasename})
options=('!libtool')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${_pkgbasename}-${pkgver}.tar.gz)
makedepends=('lib32-libx11')
sha256sums=('061b6f86c64912f79851bfefcac4f77b401e9a939dbbb7b1ec795bc7d5aaf59b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/include

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
