# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libx11
pkgname=lib32-$_pkgbasename
pkgver=1.3.5
pkgrel=2
pkgdesc="X11 client-side library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
depends=('lib32-libxcb>=1.7' 'kbproto>=1.0.4' $_pkgbasename)
makedepends=('xproto>=7.0.17' 'xextproto>=7.1.1' 'xf86bigfontproto>=1.2.0' 'xtrans>=1.2.5' 'inputproto>=2.0' 'xorg-util-macros>=1.8.0' 'gcc-multilib')
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2)
sha1sums=('c5dda2b6279f3e3267646576138fcd091a7f3c54')

build() {
  cd "${srcdir}/libX11-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --with-xcb --disable-static --disable-man-pages --disable-specs
  make
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
