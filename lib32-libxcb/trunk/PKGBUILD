# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
_pkgbasename=libxcb
pkgname=lib32-$_pkgbasename
pkgver=1.7
pkgrel=2
pkgdesc="X11 client-side library (32-bit)"
arch=(x86_64)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.6' 'lib32-libxdmcp' 'lib32-libxau'
         $_pkgbasename)
makedepends=('pkgconfig' 'libxslt' 'python' 'gcc-multilib'
             'autoconf')
conflicts=('libx11<1.1.99.2')
options=('!libtool')
license=('custom')
source=(${url}/dist/${_pkgbasename}-${pkgver}.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch)
sha1sums=('7540f0587907bce421914f0ddb813810cb2f36f8'
          '3455e84642283bc91c8313af319002a20bbcbdf4')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -i "${srcdir}/libxcb-1.1-no-pthread-stubs.patch"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
  ./configure --prefix=/usr --enable-xinput --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
