# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxfixes
pkgname=lib32-$_pkgbasename
pkgver=4.0.5
pkgrel=3
pkgdesc="X11 miscellaneous 'fixes' extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11>=1.3.4' $_pkgbasename)
makedepends=('pkg-config' gcc-multilib "fixesproto>=4.1.1")
options=('!libtool')
source=(${url}/releases/individual/lib/libXfixes-${pkgver}.tar.bz2)
sha1sums=('255dfb9a8c50d795c60711ec4764f76cd0620cbf')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXfixes-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXfixes-${pkgver}"
  make DESTDIR="${pkgdir}" install
 
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
