# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxfixes
pkgname=lib32-$_pkgbasename
pkgver=5.0.1
pkgrel=1
pkgdesc="X11 miscellaneous 'fixes' extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11' $_pkgbasename)
makedepends=('pkg-config' gcc-multilib )
options=('!libtool')
source=(${url}/releases/individual/lib/libXfixes-${pkgver}.tar.bz2)
sha256sums=('63bec085084fa3caaee5180490dd871f1eb2020ba9e9b39a30f93693ffc34767')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXfixes-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXfixes-${pkgver}"
  make DESTDIR="${pkgdir}" install
 
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
