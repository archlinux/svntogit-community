#Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxfixes
pkgname=lib32-$_pkgbasename
pkgver=6.0.0
pkgrel=1
pkgdesc="X11 miscellaneous 'fixes' extension library (32-bit)"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11' $_pkgbasename)
makedepends=('pkg-config' gcc-multilib )
source=(${url}/releases/individual/lib/libXfixes-${pkgver}.tar.bz2{,.sig})
sha256sums=('a7c1a24da53e0b46cac5aea79094b4b2257321c621b258729bc3139149245b4c'
            'SKIP')

validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>
validpgpkeys+=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd libXfixes-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd libXfixes-${pkgver}
  make DESTDIR="${pkgdir}" install
 
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
