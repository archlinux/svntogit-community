# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxi
pkgname=lib32-$_pkgbasename
pkgver=1.7.3
pkgrel=1
pkgdesc="X11 Input extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org"
depends=('lib32-libxext' 'lib32-libxfixes' 'inputproto' $_pkgbasename)
makedepends=('pkgconfig' 'xorg-util-macros' gcc-multilib automake)
options=(!libtool)
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha256sums=('763db879f57b44c6e2cc96ae4bb756cf276170c979ef8a51b4db6e04c969953c')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXi-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32

  make
}

package() {
  cd "${srcdir}/libXi-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
