# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=lib32-libxkbcommon
pkgname=(lib32-libxkbcommon lib32-libxkbcommon-x11)
pkgver=0.4.3
pkgrel=1
pkgdesc='Keyboard handling library using XKB data (32-bit)'
arch=('x86_64')
url='http://xkbcommon.org/'
license=('custom')
depends=('lib32-glibc' "${pkgbase#*-}")
makedepends=('lib32-libxcb' 'xorg-util-macros' 'gcc-multilib')
source=("http://xkbcommon.org/download/${pkgbase#*-}-${pkgver}.tar.xz")
sha256sums=('9a52d5d0419e76c49c0ece86208c205ffacb1cf0ff8ffbaba98d3d4dd40c4e41')

prepare() {
  mkdir -p x11/usr/lib32/pkgconfig
}

build() {
  cd ${pkgbase#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-{docs,static}
  make
}

package_lib32-libxkbcommon() {
  cd ${pkgbase#*-}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon

### Split libxkbcommon-x11

  mv "$pkgdir"/usr/lib32/*x11* "$srcdir/x11/usr/lib32"
  mv "$pkgdir"/usr/lib32/pkgconfig/*x11* "$srcdir/x11/usr/lib32/pkgconfig"
}

package_lib32-libxkbcommon-x11() {
  pkgdesc="Keyboard handling library using XKB data for X11 XCB clients (32-bit)"
  depends=(lib32-libxkbcommon lib32-libxcb libxkbcommon-x11)

  mv x11/* "$pkgdir"

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon-x11 "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon-x11
}

# vim: ts=2 sw=2 et:
