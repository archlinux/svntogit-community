# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=lib32-libxkbcommon
pkgname=('lib32-libxkbcommon' 'lib32-libxkbcommon-x11')
pkgver=0.7.2
pkgrel=1
arch=('x86_64')
url='https://xkbcommon.org/'
license=('custom')
makedepends=('gcc-multilib' 'lib32-libxcb' 'lib32-wayland' 'meson'
             'wayland-protocols' 'xorg-util-macros')
source=("https://xkbcommon.org/download/libxkbcommon-${pkgver}.tar.xz")
sha256sums=('28a4dc2735863bec2dba238de07fcdff28c5dd2300ae9dfdb47282206cd9b9d8')

prepare() {
  mkdir -p build x11/usr/lib32/pkgconfig
}

build() {
  cd build

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  meson ../libxkbcommon-${pkgver} \
    --buildtype='release' \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    -Denable-docs='false'
  ninja
}

package_lib32-libxkbcommon() {
  pkgdesc='Keymap handling library for toolkits and window systems'
  depends=('lib32-glibc' 'libxkbcommon')

  cd build

  DESTDIR="${pkgdir}" ninja install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon

  mv "${pkgdir}"/usr/lib32/*x11* ../x11/usr/lib32
  mv "${pkgdir}"/usr/lib32/pkgconfig/*x11* ../x11/usr/lib32/pkgconfig
}

package_lib32-libxkbcommon-x11() {
  pkgdesc='Keyboard handling library using XKB data for X11 XCB clients'
  depends=('lib32-libxcb' 'lib32-libxkbcommon' 'libxkbcommon-x11')

  mv x11/* "${pkgdir}"/
  find "${pkgdir}" -type d -exec chmod 755 {} +

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon-x11 "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon-x11
}

# vim: ts=2 sw=2 et:
