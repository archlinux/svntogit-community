# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxrender
pkgname=lib32-$_pkgbasename
pkgver=0.9.10
pkgrel=1
pkgdesc="X Rendering Extension client library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11>=1.3.4' $_pkgbasename)
makedepends=('pkgconfig' 'gcc-multilib' renderproto)
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2{,.sig})
sha256sums=('c06d5979f86e64cabbde57c223938db0b939dff49fdb5a793a1d3d0396650949'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys+=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXrender-${pkgver}"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXrender-${pkgver}"

  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
