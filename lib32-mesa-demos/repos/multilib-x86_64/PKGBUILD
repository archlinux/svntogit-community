# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgbase=lib32-mesa-demos
pkgname=('lib32-mesa-demos' 'lib32-mesa-utils')
pkgver=8.4.0
pkgrel=8
arch=(x86_64)
url="http://mesa3d.sourceforge.net"
license=('custom')
makedepends=('lib32-mesa' 'lib32-freetype2' 'lib32-glew' 'lib32-freeglut')
options=('debug')
source=(https://mesa.freedesktop.org/archive/demos/mesa-demos-${pkgver}.tar.bz2{,.sig}
        'LICENSE')
sha512sums=('b72d03cad36e0535ff18dcfb222ec4200064b9264f6da51a6e5f03b0dd912abe188bc1d600b6698de3ce6f63b28d2ce01565886ca8e7079edc4967fbf2fb0957'
            'SKIP'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2')
validpgpkeys=('E390B9700582FAEA959ACAD41EEF53D38A3A9C67') # "Andreas Boll <andreas.boll.dev@gmail.com>"

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cd mesa-demos-${pkgver}
  ./configure --prefix=/usr \
    --disable-gles1 \
    --with-system-data-files=/usr/share/mesa-demos

  make
}

package_lib32-mesa-demos() {
  pkgdesc="Mesa demos (32-bit)"
  depends=('lib32-libgl' 'lib32-glew' 'lib32-freeglut' 'mesa-demos')
  install=mesa-demos.install

  cd mesa-demos-${pkgver}
  make DESTDIR="${pkgdir}" install

  # remove utils
  rm "${pkgdir}"/usr/bin/{glxinfo,glxgears,eglinfo}

  local i
  for i in "$pkgdir/usr/bin/"*; do
    mv "$i" "$i"32
  done

  rm -r "$pkgdir/usr/share/mesa-demos"

  install -Dm 0644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-mesa-utils() {
  pkgdesc="Essential Mesa utilities (32-bit)"
  depends=('lib32-libgl')
  provides=('glxinfo32' 'glxgears32' 'eglinfo32')

  cd mesa-demos-${pkgver}
  install -Dm 0755 src/egl/opengl/eglinfo -t "${pkgdir}/usr/bin/"
  install -Dm 0755 src/xdemos/{glxinfo,glxgears} -t "${pkgdir}/usr/bin/"

  local i
  for i in "$pkgdir/usr/bin/"*; do
    mv "$i" "$i"32
  done

  install -Dm 0644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
