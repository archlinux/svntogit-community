# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=lib32-nouveau-dri
pkgver=8.0.3
pkgrel=3.2
arch=('x86_64')
makedepends=('glproto>=1.4.15' 'lib32-libdrm-old' 'lib32-libdrm-nouveau1' 'lib32-libxxf86vm>=1.1.1' 'lib32-libxdamage>=1.1.3' 'lib32-expat>=2.0.1' 'lib32-libx11>=1.4.99.1'
  'lib32-libxt>=1.1.1' 'lib32-gcc-libs>=4.6.1' 'dri2proto>=2.6' 'python2' 'libxml2' 'gcc-multilib' 'imake' 'lib32-llvm' 'namcap')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=(LICENSE
        mesa-8.0.3-llvm-3.1-fixes.patch
        ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2
)
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a'
         'c452ed3392468170726c004c2f4e02ca'
         'cc5ee15e306b8c15da6a478923797171')

build() {
  cd ${srcdir}/?esa-*

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # for our llvm-config for 32 bit
  export LLVM_CONFIG=/usr/bin/llvm-config32

  patch -Np1 -i "${srcdir}/mesa-8.0.3-llvm-3.1-fixes.patch"


   autoreconf -vfi
  ./configure --prefix=/usr \
  --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
  --with-gallium-drivers=nouveau \
  --with-dri-drivers=nouveau \
  --enable-gallium-llvm \
  --enable-gallium-egl \
  --enable-shared-glapi \
  --enable-gbm \
  --enable-glx-tls \
  --enable-dri \
  --enable-glx \
  --enable-osmesa \
  --enable-gles1 \
  --enable-gles2 \
  --disable-egl \
  --enable-texture-float \
  --enable-xa \
  --enable-shared-dricore \
  --enable-32-bit \
  --libdir=/usr/lib32

  make
}

package() {
  depends=("lib32-libgl=${pkgver}" 'lib32-libdrm-nouveau1')
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau (32-bit)"

  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/nouveau DESTDIR="${pkgdir}" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install
}
