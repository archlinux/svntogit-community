#$Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

_pkgbasename=nspr
pkgname=lib32-${_pkgbasename}
pkgver=4.9.5
pkgrel=1
pkgdesc="Netscape Portable Runtime (32-bit)"
arch=('x86_64')
url="http://www.mozilla.org/projects/nspr/"
#download_url=ftp://ftp.mozilla.org/pub/mozilla.org/nspr/releases/
license=('MPL' 'GPL')
depends=('lib32-glibc' ${_pkgbasename})
makedepends=('zip' 'gcc-multilib')
options=(!emptydirs)
source=(http://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${_pkgbasename}-${pkgver}.tar.gz
        nspr.pc.in)

md5sums=('b6ccfa8fcbbeb17ebeb19a3edff612bd'
         'bce1611f3117b53fc904cab549c09967')

build() {
  cd "${srcdir}"/${_pkgbasename}-${pkgver}

  sed -e 's/\$(MKSHLIB) \$(OBJS)/\$(MKSHLIB) \$(LDFLAGS) \$(OBJS)/g' \
    -i mozilla/nsprpub/config/rules.mk

  ./mozilla/nsprpub/configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug ${confflags}
  make

  NSPR_LIBS=`./config/nspr-config --libs`
  NSPR_CFLAGS=`./config/nspr-config --cflags`
  NSPR_VERSION=`./config/nspr-config --version`
  install -m755 -d "${pkgdir}"/usr/lib/pkgconfig
  sed "${srcdir}"/nspr.pc.in -e "s,%libdir%,/usr/lib32," \
  	-e "s,%prefix%,/usr," \
	-e "s,%exec_prefix%,/usr/bin," \
	-e "s,%includedir%,/usr/include/nspr," \
	-e "s,%NSPR_VERSION%,${NSPR_VERSION}," \
	-e "s,%FULL_NSPR_LIBS%,${NSPR_LIBS}," \
	-e "s,%FULL_NSPR_CFLAGS%,${NSPR_CFLAGS}," > "${srcdir}"/nspr.pc
}

package() {
  cd "${srcdir}"/${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -D "${srcdir}"/nspr.pc "${pkgdir}"/usr/lib32/pkgconfig/nspr.pc
  chmod 644 "${pkgdir}"/usr/lib32/pkgconfig/nspr.pc
  ln -sf nspr.pc "${pkgdir}"/usr/lib32/pkgconfig/mozilla-nspr.pc
  chmod 644 "${pkgdir}"/usr/lib32/*.a

  rm -rf "${pkgdir}"/usr/{bin,share,include}
}
