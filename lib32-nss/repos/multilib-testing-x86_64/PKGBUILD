# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

pkgname=lib32-nss
pkgver=3.84
pkgrel=1
pkgdesc="Network Security Services (32-bit)"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
arch=(x86_64)
license=(MPL GPL)
depends=('lib32-nspr>=4.35' lib32-sqlite lib32-zlib 'lib32-p11-kit>=0.23.19' nss)
makedepends=(perl python gyp mercurial)
options=(debug)
_revision=89975de900203c7a8aaf8457311b8c1ebfda7b22
source=("hg+https://hg.mozilla.org/projects/nss#revision=$_revision"
        0001-Hack-mpi_x64.s-to-work-with-fno-plt.patch)
sha256sums=('SKIP'
            'e95b3fea802aba8ae1cfff45fa7881ec0e8d092d58a10f098caae4d5f878305e')

pkgver() {
  cd nss
  hg id -t -r. | sed 's/^NSS_//;s/_RTM$//;s/_/./g'
}

prepare() {
  cd nss

  # https://bugzilla.mozilla.org/show_bug.cgi?id=1382942
  patch -Np1 -i ../0001-Hack-mpi_x64.s-to-work-with-fno-plt.patch
}

build() {
  cd nss
  ./build.sh \
    --target ia32 \
    --opt \
    --system-sqlite \
    --system-nspr \
    --enable-libpkix \
    --disable-tests
}

package() {
  local nsprver="$(i686-pc-linux-gnu-pkg-config --modversion nspr)"
  local libdir=/usr/lib32

  sed nss/pkg/pkg-config/nss.pc.in \
    -e "s,%libdir%,$libdir,g" \
    -e "s,%prefix%,/usr,g" \
    -e "s,%exec_prefix%,/usr/bin,g" \
    -e "s,%includedir%,/usr/include/nss,g" \
    -e "s,%NSPR_VERSION%,$nsprver,g" \
    -e "s,%NSS_VERSION%,$pkgver,g" |
    install -Dm644 /dev/stdin "$pkgdir$libdir/pkgconfig/nss.pc"

  ln -s nss.pc "$pkgdir$libdir/pkgconfig/mozilla-nss.pc"

  install -Dt "$pkgdir$libdir" dist/Release/lib/*.so

  # Replace built-in trust with p11-kit connection
  ln -s pkcs11/p11-kit-trust.so "$pkgdir$libdir/p11-kit-trust.so"
  ln -sf p11-kit-trust.so "$pkgdir$libdir/libnssckbi.so"
}

# vim:set sw=2 et:
