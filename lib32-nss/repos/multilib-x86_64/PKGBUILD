# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

_pkgbasename=nss
pkgname=lib32-${_pkgbasename}
pkgver=3.14.3
pkgrel=1
pkgdesc="Mozilla Network Security Services (32-bit)"
arch=('x86_64')
url="http://www.mozilla.org/projects/security/pki/nss/"

#download_url=ftp://ftp.mozilla.org/pub/security/nss/releases/
#alternative download link
#ftp://ftp.mozilla.org/pub/mozilla.org/security/nss/releases/NSS_${pkgver//./_}_RTM/src/${_pkgbasename}-${pkgver}.tar.gz

license=('MPL' 'GPL')
_nsprver=4.9.5
depends=("lib32-nspr>=${_nsprver}" 'lib32-sqlite>=3.6.17' "${_pkgbasename}" 'lib32-zlib')
makedepends=('gcc-multilib' 'perl')
options=('!strip')
source=(ftp://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/${_pkgbasename}-${pkgver}.tar.gz
        nss.pc.in
        ssl-renegotiate-transitional.patch
        add_spi+cacert_ca_certs.patch)
sha1sums=('94d8781d1fa29cfbd37453dda3e9488709b82c4c'
          'aa5b2c0aa38d3c1066d511336cf28d1333e3aebd'
          '8a964a744ba098711b80c0d279a2993524e8eb92'
          'be6ef0f124f188ebb76f4d83e13466bdcfb27dd3')

build() {
  cd "${srcdir}"/${_pkgbasename}-${pkgver}/mozilla
  
  # Add CAcert and spi-inc.org CA certificates.
  # See http://wiki.cacert.org/NSSLib for how to generate
  patch -Np0 -i "${srcdir}/add_spi+cacert_ca_certs.patch"

  # Adds transitional SSL renegotiate support - patch from Debian
  patch -Np2 -i "${srcdir}/ssl-renegotiate-transitional.patch"

  # Respect LDFLAGS
  sed -e 's/\$(MKSHLIB) -o/\$(MKSHLIB) \$(LDFLAGS) -o/g' \
      -i security/coreconf/rules.mk

  # see https://bugzilla.mozilla.org/show_bug.cgi?id=683266 - this step can be dropped now
  # CaCert Wiki page should be updated to reflect these changes how to add the certificates
  # for now the first patch should still do it

  # Generate certdata.c from certdata.txt
  #cd security/nss/lib/ckfw/builtins
  #make generate
 
  cd "${srcdir}"/${_pkgbasename}-${pkgver}

  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  export BUILD_OPT=1
  export PKG_CONFIG_ALLOW_SYSTEM_LIBS=1
  export PKG_CONFIG_ALLOW_SYSTEM_CFLAGS=1
  export NSS_USE_SYSTEM_SQLITE=1
  export NSPR_INCLUDE_DIR=`pkg-config --cflags-only-I nspr | sed 's/-I//'`
  export NSPR_LIB_DIR=`pkg-config --libs-only-L nspr | sed 's/-L.//'`
  export XCFLAGS="${CFLAGS}"

  make -j 1 -C mozilla/security/coreconf
  make -j 1 -C mozilla/security/dbm
  make -j 1 -C mozilla/security/nss
}

package() {
  cd "${srcdir}"/${_pkgbasename}-${pkgver}
  install -m755 -d "${pkgdir}"/usr/lib32/pkgconfig
  #install -m755 -d "${pkgdir}"/usr/bin

  sed "${srcdir}"/nss.pc.in -e "s,%libdir%,/usr/lib32,g" \
  				-e "s,%prefix%,/usr,g" \
				-e "s,%exec_prefix%,/usr/bin,g" \
				-e "s,%includedir%,/usr/include/nss,g" \
				-e "s,%NSPR_VERSION%,${_pkgver},g" \
				-e "s,%NSS_VERSION%,${_pkgver},g" > \
			"${pkgdir}"/usr/lib32/pkgconfig/nss.pc
  ln -sf nss.pc "${pkgdir}"/usr/lib32/pkgconfig/mozilla-nss.pc
  chmod 644 "${pkgdir}"/usr/lib32/pkgconfig/*.pc
  
  #sed "${srcdir}/nss-config.in" -e "s,@libdir@,/usr/lib32,g" \
  #				    -e "s,@prefix@,/usr/bin,g" \
  #	 			    -e "s,@exec_prefix@,/usr/bin,g" \
  #			   		-e "s,@includedir@,/usr/include/nss,g" \
  #				    -e "s,@MOD_MAJOR_VERSION@,${NSS_VMAJOR},g" \
  #				    -e "s,@MOD_MINOR_VERSION@,${NSS_VMINOR},g" \
  #				    -e "s,@MOD_PATCH_VERSION@,${NSS_VPATCH},g" \
  #			    > "${pkgdir}/usr/bin/nss-config"
  #  chmod 755 "${pkgdir}/usr/bin/nss-config"

  for file in libsoftokn3.so libfreebl3.so libnss3.so libnssutil3.so \
              libssl3.so libsmime3.so libnssckbi.so libnssdbm3.so
  do
    install -m755 mozilla/dist/*.OBJ/lib/${file} "${pkgdir}"/usr/lib32/
  done

  install -m644 mozilla/dist/*.OBJ/lib/libcrmf.a "${pkgdir}"/usr/lib32/
  install -m644 mozilla/dist/*.OBJ/lib/*.chk "${pkgdir}"/usr/lib32/
}
