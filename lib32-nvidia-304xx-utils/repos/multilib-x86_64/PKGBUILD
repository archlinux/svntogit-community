# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jason Graham <jason@the-graham.com>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>

_pkgbasename=nvidia-304xx-utils
pkgbase=lib32-$_pkgbasename
pkgname=('lib32-nvidia-304xx-utils' 'lib32-nvidia-304xx-libgl' 'lib32-opencl-nvidia-304xx')
pkgver=304.121
pkgrel=2
pkgdesc="NVIDIA drivers utilities and libraries. (32-bit)"
arch=('x86_64')
url="http://www.nvidia.com/"
license=('custom')
makedepends=('lib32-gcc-libs' 'lib32-libcl' 'lib32-libxvmc' 'lib32-zlib' 
             'lib32-libxext' 'nvidia-304xx-utils' 'opencl-nvidia-304xx')
options=('!strip')

_arch='x86'
_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
source=("http://us.download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
md5sums=('b8c950f0a30a6e1f503630fbd0ca8ab9')

build() {
  sh ${_pkg}.run --extract-only
}

package_lib32-opencl-nvidia-304xx() {
  pkgdesc="OpenCL implemention for NVIDIA (32-bit)"
  depends=('lib32-libcl' 'lib32-zlib' 'lib32-gcc-libs' 'opencl-nvidia-304xx')
  optdepends=('opencl-headers: headers necessary for OpenCL development')
  conflicts=('lib32-opencl-nvidia')

  cd ${_pkg}

  # OpenCL
  install -D -m755 "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so.${pkgver}"
  ln -s "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so.1"
  ln -s "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-compiler.so"

  install -D -m755 "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so.${pkgver}" 
  ln -s "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so.1"
  ln -s "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-opencl.so"
}

package_lib32-nvidia-304xx-libgl() {
  pkgdesc="NVIDIA drivers libraries symlinks (32-bit)"
  depends=('lib32-nvidia-304xx-utils' 'lib32-mesa')
  conflicts=('lib32-libgl')
  provides=('lib32-libgl')

  cd ${_pkg}

  mkdir -p "${pkgdir}/usr/lib32"
  ln -s nvidia/libGL.so.${pkgver} "${pkgdir}/usr/lib32/libGL.so.${pkgver}"
  ln -s libGL.so.${pkgver} "${pkgdir}/usr/lib32/libGL.so.1"
  ln -s libGL.so.${pkgver} "${pkgdir}/usr/lib32/libGL.so"

  # We have to provide symlinks to mesa, as nvidia 304xx doesn't ship them
  ln -s mesa/libEGL.so.1.0.0 "${pkgdir}/usr/lib32/libEGL.so.1.0.0"
  ln -s libEGL.so.1.0.0      "${pkgdir}/usr/lib32/libEGL.so.1"
  ln -s libEGL.so.1.0.0      "${pkgdir}/usr/lib32/libEGL.so"

  ln -s mesa/libGLESv1_CM.so.1.1.0 "${pkgdir}/usr/lib32/libGLESv1_CM.so.1.1.0"
  ln -s libGLESv1_CM.so.1.1.0      "${pkgdir}/usr/lib32/libGLESv1_CM.so.1"
  ln -s libGLESv1_CM.so.1.1.0      "${pkgdir}/usr/lib32/libGLESv1_CM.so"

  ln -s mesa/libGLESv2.so.2.0.0 "${pkgdir}/usr/lib32/libGLESv2.so.2.0.0"
  ln -s libGLESv2.so.2.0.0      "${pkgdir}/usr/lib32/libGLESv2.so.2"
  ln -s libGLESv2.so.2.0.0      "${pkgdir}/usr/lib32/libGLESv2.so"

  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s $_pkgbasename "${pkgdir}/usr/share/licenses/lib32-nvidia-libgl"
}

package_lib32-nvidia-304xx-utils() {
  pkgdesc="NVIDIA drivers utilities (32-bit)"
  depends=('lib32-libxvmc' 'lib32-zlib' 'lib32-gcc-libs' 'lib32-libxext')
  optdepends=('lib32-opencl-nvidia-304xx')
  conflicts=('lib32-nvidia-utils')

  cd ${_pkg}

  # OpenGL libraries
  install -D -m755 "libGL.so.${pkgver}" "${pkgdir}/usr/lib32/nvidia/libGL.so.${pkgver}"

  # OpenGL core library
  install -D -m755 "libnvidia-glcore.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-glcore.so.${pkgver}"

  # XvMC
  install -D -m755 "libXvMCNVIDIA.so.${pkgver}" "${pkgdir}/usr/lib32/libXvMCNVIDIA.so.${pkgver}"

  # VDPAU
  install -D -m755 "libvdpau_nvidia.so.${pkgver}" "${pkgdir}/usr/lib32/vdpau/libvdpau_nvidia.so.${pkgver}"

  # nvidia-tls library
  install -D -m755 "tls/libnvidia-tls.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-tls.so.${pkgver}"
  install -D -m755 "libnvidia-cfg.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-cfg.so.${pkgver}"
  install -D -m755 "libnvidia-ml.so.${pkgver}" "${pkgdir}/usr/lib32/libnvidia-ml.so.${pkgver}"
  
  # CUDA
  install -D -m755 "libcuda.so.${pkgver}" "${pkgdir}/usr/lib32/libcuda.so.${pkgver}"
  install -D -m755 "libnvcuvid.so.${pkgver}" "${pkgdir}/usr/lib32/libnvcuvid.so.${pkgver}"

  # DEBUG
  install -D -m755 nvidia-debugdump "${pkgdir}/usr/bin/nvidia-debugdump"

  # create soname links
  for _lib in $(find "${pkgdir}" -name '*.so*'); do
    _soname="$(dirname ${_lib})/$(LC_ALL=C readelf -d "$_lib" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
    if [ ! -e "${_soname}" ]; then
      ln -s "$(basename ${_lib})" "${_soname}"
      ln -s "$(basename ${_soname})" "${_soname/.[0-9]*/}"
    fi
  done

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s $_pkgbasename "${pkgdir}/usr/share/licenses/${pkgname}"
}

