# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

_pkgbasename=openssl
pkgname=lib32-$_pkgbasename
_ver=1.0.0a
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=6
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (32-bit)'
arch=('x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=(lib32-zlib $_pkgbasename)
optdepends=('ca-certificates' perl gcc-multilib)
options=('!makeflags')
source=("https://www.openssl.org/source/${_pkgbasename}-${_ver}.tar.gz"
        'no-rpath.patch'
        'ca-dir.patch'
        'fix-double-free.patch')
md5sums=('e3873edfffc783624cfbdb65e2249cbd'
         'dc78d3d06baffc16217519242ce92478'
         '3bf51be3a1bbd262be46dc619f92aa90'
         'ccb896e23a44b89a3c93ac094c592c1f')

# keep an upgrade path for older installations

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/$_pkgbasename-$_ver

  # remove rpath: http://bugs.archlinux.org/task/14367
  patch -p0 -i $srcdir/no-rpath.patch
  # set ca dir to /etc/ssl by default
  patch -p0 -i $srcdir/ca-dir.patch
  # fix double free
  # see http://marc.info/?l=openssl-dev&m=128118163216952&w=2
  # and http://marc.info/?l=openssl-dev&m=128128256314328&w=2
  patch -p0 -i $srcdir/fix-double-free.patch
  # mark stack as non-executable: http://bugs.archlinux.org/task/12434
  ./Configure linux-elf --prefix=/usr --openssldir=/etc/ssl --libdir=lib32 \
    shared zlib enable-md2 -Wa,--noexecstack

  make

  # the test fails due to missing write permissions in /etc/ssl
  # revert this patch for make test
  #patch -p0 -R -i $srcdir/ca-dir.patch
  #make test
  #patch -p0 -i $srcdir/ca-dir.patch
}

package() {
  cd $srcdir/$_pkgbasename-$_ver
  make INSTALL_PREFIX=$pkgdir install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/etc"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
