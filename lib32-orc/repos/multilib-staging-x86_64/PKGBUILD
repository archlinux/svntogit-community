# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lib32-orc
pkgver=0.4.32
pkgrel=2
pkgdesc='The Oild Runtime Compiler'
arch=(x86_64)
url=https://gstreamer.freedesktop.org/modules/orc.html
license=(custom)
depends=(
  lib32-glibc
  orc
)
makedepends=(
  git
  gtk-doc
  meson
  valgrind-multilib
)
_tag=629864f073ae003e63c026c1de2407fec713cb53
source=(git+https://gitlab.freedesktop.org/gstreamer/orc.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd orc

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson orc build \
    --libdir=/usr/lib32
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s orc "${pkgdir}"/usr/share/licenses/lib32-orc
}

# vim: ts=2 sw=2 et:
