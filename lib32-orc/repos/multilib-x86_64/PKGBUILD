# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-orc
pkgver=0.4.17
pkgrel=2
pkgdesc="The Oild Runtime Compiler"
arch=('x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('lib32-glibc' "${pkgname#*-}")
options=('!libtool')
source=("http://code.entropywave.com/download/${pkgname#*-}/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('4fc7cca48c59fff23afee78fb642cdbde001f56401c8f47b95a16578d1d5d7e8')

build () {
  cd "${srcdir}"/${pkgname#*-}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package() {
  cd "${srcdir}"/${pkgname#*-}-${pkgver}

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
