# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lib32-orc
pkgver=0.4.33
pkgrel=1
pkgdesc='The Oild Runtime Compiler'
arch=(x86_64)
url=https://gstreamer.freedesktop.org/modules/orc.html
license=(custom)
depends=(
  lib32-glibc
  orc
)
makedepends=(
  git
  gtk-doc
  meson
  valgrind-multilib
)
_tag=be26150a7ab2e19a0f9384109b97b6ed9c805cf6
source=(git+https://gitlab.freedesktop.org/gstreamer/orc.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd orc

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson orc build \
    --libdir=/usr/lib32
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s orc "${pkgdir}"/usr/share/licenses/lib32-orc
}

# vim: ts=2 sw=2 et:
