# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-orc
pkgver=0.4.18
pkgrel=1
pkgdesc="The Oild Runtime Compiler"
arch=('x86_64')
license=('custom')
url="http://code.entropywave.com/projects/orc/"
depends=('lib32-glibc' "${pkgname#*-}")
makedepends=('valgrind')
options=('!libtool')
source=("http://code.entropywave.com/download/${pkgname#*-}/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('34d3c8a7259f42bed4dcfbc106c5e3d0cc8e17eeaa73128d1c5eaf92883a6a24')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-static
  make
}

check() {
  cd ${pkgname#*-}-${pkgver}

  make check
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
