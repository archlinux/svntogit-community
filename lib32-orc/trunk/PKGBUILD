# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-orc
pkgver=0.4.19
pkgrel=1
pkgdesc='The Oild Runtime Compiler'
arch=('x86_64')
license=('custom')
url='http://code.entropywave.com/projects/orc/'
depends=('lib32-glibc' "${pkgname#*-}")
makedepends=('valgrind')
options=('!libtool')
source=("http://gstreamer.freedesktop.org/src/orc/orc-${pkgver}.tar.gz")
sha256sums=('e21f50321a21c33178bbe41ea8bd27c49ae60aeb8c22902a215f7141297fb59e')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-static
  make
}

check() {
  cd ${pkgname#*-}-${pkgver}

  make check
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
