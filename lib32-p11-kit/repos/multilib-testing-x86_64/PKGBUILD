# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.23.9
pkgrel=1
pkgdesc="Provides a way to load and enumerate PKCS#11 modules (32-bit)"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
license=(BSD)
depends=(lib32-glibc lib32-libtasn1 lib32-libffi $_pkgbasename)
makedepends=(git)
_commit=34cb047e20af02ef3d820a295e9e29f618b83bab  # tags/0.23.9^0
source=("git+https://github.com/p11-glue/p11-kit#commit=$_commit"
        libnssckbi-compat.patch)
sha256sums=('SKIP'
            '8f763cdbc6c0ca6c5a7898f9fd6f3018b7ac5b1aca36f67c6c813343c2962962')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF')

pkgver() {
  cd ${_pkgbasename}
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ${_pkgbasename}

  # Build and install an additional library (libnssckbi-p11-kit.so) which
  # is a copy of p11-kit-trust.so but uses the same label for root certs as
  # libnssckbi.so ("Builtin Object Token" instead of "Default Trust")
  # https://bugs.freedesktop.org/show_bug.cgi?id=66161
  patch -Np1 -i ../libnssckbi-compat.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${_pkgbasename}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --disable-doc \
    --with-module-path=/usr/lib32/pkcs11 \
    --with-trust-paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  make
}

check() {
  cd ${_pkgbasename}
  make check
}

package() {
  cd ${_pkgbasename}
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/etc "${pkgdir}"/usr/{bin,include,lib,share}

  install -d "$pkgdir/usr/share/licenses"
  ln -s ${_pkgbasename} "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
