# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.23.2
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules (32-bit)"
arch=(x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=(lib32-glibc lib32-libtasn1 lib32-libffi $_pkgbasename)
options=(!libtool)
source=($url/releases/$_pkgbasename-$pkgver.tar.gz{,.sig}
        libnssckbi-compat.patch)
validpgpkeys=(C0F67099B808FB063E2C81117BFB1108D92765AF)
sha256sums=('ba726ea8303c97467a33fca50ee79b7b35212964be808ecf9b145e9042fdfaf0'
            'SKIP'
            '8f763cdbc6c0ca6c5a7898f9fd6f3018b7ac5b1aca36f67c6c813343c2962962')

prepare() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  # Build and install an additional library (libnssckbi-p11-kit.so) which
  # is a copy of p11-kit-trust.so but uses the same label for root certs as
  # libnssckbi.so ("Builtin Object Token" instead of "Default Trust")
  # https://bugs.freedesktop.org/show_bug.cgi?id=66161
  patch -Np1 -i ../libnssckbi-compat.patch

  autoreconf -vi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cd "$srcdir/$_pkgbasename-$pkgver"

  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
    --with-module-path=/usr/lib32/pkcs11 \
    --with-trust-paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  make
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make check
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING

  ln -srf "$pkgdir/usr/bin/update-ca-trust" "$pkgdir/usr/lib32/p11-kit/trust-extract-compat"
}

# vim:set ts=2 sw=2 et:
