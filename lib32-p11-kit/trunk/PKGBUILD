# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.22.1
pkgrel=3
pkgdesc="Library to work with PKCS#11 modules (32-bit)"
arch=(x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=(lib32-glibc lib32-libtasn1 lib32-libffi $_pkgbasename)
options=(!libtool)
source=($url/releases/$_pkgbasename-$pkgver.tar.gz{,.sig})
validpgpkeys=(C0F67099B808FB063E2C81117BFB1108D92765AF)
md5sums=('4e9bea1106628ffb820bdad24a819fac'
         'SKIP')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cd "$srcdir/$_pkgbasename-$pkgver"

  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
    --with-module-path=/usr/lib32/pkcs11 \
    --with-trust-paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  make
}

check() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make check
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING

  ln -srf "$pkgdir/usr/bin/update-ca-trust" "$pkgdir/usr/lib32/p11-kit/trust-extract-compat"
}

# vim:set ts=2 sw=2 et:
