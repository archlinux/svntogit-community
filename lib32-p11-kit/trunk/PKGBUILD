# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.13
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules (32-bit)"
arch=(i686 x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=(lib32-glibc $_pkgbasename)
options=(!libtool)
source=($url/releases/$_pkgbasename-$pkgver.tar.gz)
md5sums=('3892bc07ff54b1b391aad4628911801d')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cd "$srcdir/$_pkgbasename-$pkgver"

  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
    --with-module-path=/usr/lib32/pkcs11
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
