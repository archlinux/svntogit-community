# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-pango
pkgver=1.48.6
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-libthai lib32-cairo lib32-libxft lib32-harfbuzz lib32-fribidi pango)
makedepends=(git meson)
checkdepends=(ttf-dejavu cantarell-fonts noto-fonts noto-fonts-emoji)
provides=(libpango{,cairo,ft2,xft}-1.0.so)
_commit=61763238b548d717d0772f31467ad7e9b6a557ae  # tags/1.48.6^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
        fix-test.diff)
sha256sums=('SKIP'
            'd74df5b325007d02c3054a9e2a623327d0d195adf18d0ee41689672c27657cdb')

pkgver() {
  cd pango
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pango

  # Test assumes no bitmap fonts?
  git apply -3 ../fix-test.diff
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson pango build \
    --libdir=/usr/lib32 \
    -D introspection=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/usr/{bin,include}
}

# vim:set sw=2 et:
