# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Maintainer: Biru Ionut <ionut@archlinux.ro>
_pkgbasename=pango
pkgname=lib32-$_pkgbasename
pkgver=1.29.4
pkgrel=1
pkgdesc="A library for layout and rendering of text (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2>=2.25.15' 'lib32-cairo>=1.10.0' 'lib32-libxft>=2.1.14'
         'lib32-freetype2>=2.4.2' $_pkgbasename)
makedepends=("gcc-multilib")
options=('!libtool' '!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/1.29/${_pkgbasename}-${pkgver}.tar.xz
        pango-modules-conffile.patch)
url="http://www.pango.org/"
sha256sums=('7ae8d1953e6098a2706df58c1f84555c06ace7006bb34c0e54ab9acd98c1127f'
            '4a178b60dd420ae53baeabbecfaaeca4070a4b777b2b3f36d137cd70b5a270c3')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -p0 < ${srcdir}/pango-modules-conffile.patch
  # No libthai support yet
  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
      --localstatedir=/var --with-included-modules=basic-fc \
      --with-dynamic-modules=arabic-fc,arabic-lang,basic-fc,basic-win32,basic-x,basic-atsui,hangul-fc,hebrew-fc,indic-fc,indic-lang,khmer-fc,syriac-fc,tibetan-fc \
      --disable-introspection
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/etc
  rm -rf "$pkgdir"/usr/{bin/pango-view,share,include}
  mv "$pkgdir"/usr/bin/pango-querymodules "$pkgdir"/usr/bin/pango-querymodules-32
}
