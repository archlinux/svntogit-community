# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-pango
pkgver=1.50.0
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-libthai lib32-cairo lib32-libxft lib32-harfbuzz lib32-fribidi pango)
makedepends=(git meson)
checkdepends=(ttf-dejavu cantarell-fonts)
provides=(libpango{,cairo,ft2,xft}-1.0.so)
_commit=e80e224baabc7bf22971d0a97c8b31d32fa8ca1c  # tags/1.50.0^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
        fix-test.diff)
sha256sums=('SKIP'
            'e1c0638305c0f46653bd60b5dee0b3445dd6a6b9aff8cee70be7759c21ba1037')

pkgver() {
  cd pango
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pango

  # Fix tests... seems to be a rounding error
  git apply -3 ../fix-test.diff
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson pango build \
    --libdir=/usr/lib32 \
    -D introspection=disabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir"/usr/{bin,include}
}

# vim:set sw=2 et:
