
pkgname=lib32-pcre2
_pkgname=pcre2
pkgver=10.40
pkgrel=3
pkgdesc='A library that implements Perl 5-style regular expressions. 2nd version (32-bit)'
arch=('x86_64')
url='https://www.pcre.org/'
license=('BSD')
depends=('lib32-gcc-libs' "$_pkgname")
provides=(libpcre2-{8,16,32,posix}.so)
options=(debug)
source=("https://github.com/PhilipHazel/pcre2/releases/download/$_pkgname-$pkgver/$_pkgname-$pkgver.tar.bz2"{,.sig})
sha512sums=('00e7b48a6554b9127cb6fe24c5cacf72783416a9754ec88f62f73c52f46ed72c86c1869e62c91a31b2ff2cbafbbedabca44b3f1eb7670bc92f49d8401c7374e8'
            'SKIP')
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8')  # Philip Hazel <ph10@hermes.cam.ac.uk>

build() {
  cd $_pkgname-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-jit \
    --disable-pcre2grep-libz \
    --disable-pcre2grep-libbz2 \
    --disable-pcre2test-libreadline
  make
}

check() {
  cd $_pkgname-$pkgver
  make -j1 check
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,share,include}

  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set sw=2 sts=-1 et:
