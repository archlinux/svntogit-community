# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexander Monakov <amonakov@gmail.com>

pkgname=lib32-primus
pkgver=20131226
pkgrel=1
pkgdesc="Faster OpenGL offloading for Bumblebee (32-bit)"
arch=('i686' 'x86_64')
url="https://github.com/amonakov/primus"
license=('custom:ISC')
depends=('bumblebee' 'lib32-mesa-libgl')
makedepends=('git' 'gcc-multilib')
source=("git://github.com/amonakov/primus.git#commit=074817614c")
sha1sums=('SKIP')

build() {
  cd primus

  export CC="g++ -m32"
  export CXX="g++ -m32"

  LIBDIR=lib32 make
}

package() {
  cd primus

  install -D "lib32/libGL.so.1" "$pkgdir/usr/lib32/primus/libGL.so.1"

  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
