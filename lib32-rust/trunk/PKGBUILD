# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=lib32-rust
pkgver=1.34.1
pkgrel=1

pkgdesc='32-bit libraries for rust'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')

makedepends=('llvm' 'libffi' 'perl' 'python' 'curl' 'cmake')
depends=('lib32-gcc-libs' "rust=1:$pkgver-1")

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        config.toml)

sha256sums=('b0c785264d17e1dac4598627c248a2d5e07dd39b6666d1881fcfc8e2cf4c40a7'
            'SKIP'
            'eebaab90793d0213fc04be9c6a682715dce7959f180e51d3633ff450ef64310d')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python ./x.py build
}

package() {
  cd "rustc-$pkgver-src"

  target="$pkgdir"/"$(rustc --print sysroot)"/lib/rustlib
  install -d "$target"
  cp -r \
    build/x86_64-unknown-linux-gnu/stage2/lib/rustlib/i686-unknown-linux-gnu \
    "$target"

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

# vim:set ts=2 sw=2 et:
