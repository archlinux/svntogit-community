# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: J0k3r <moebius282@gmail.com>

pkgname=lib32-sdl2
pkgver=2.0.18
pkgrel=1
pkgdesc='A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard'
arch=(x86_64)
url=https://www.libsdl.org
license=(MIT)
depends=(
  lib32-glibc
  lib32-libxext
  lib32-libxrender
  lib32-libx11
  lib32-libgl
  lib32-libxcursor
  sdl2
)
makedepends=(
  cmake
  git
  jack
  lib32-alsa-lib
  lib32-mesa
  lib32-libpulse
  lib32-libxrandr
  lib32-libxinerama
  lib32-wayland
  lib32-libxkbcommon
  lib32-libxss
  wayland-protocols
)
optdepends=(
  'lib32-alsa-lib: ALSA audio driver'
  'lib32-libpulse: PulseAudio audio driver'
  'lib32-jack: JACK audio driver'
)
source=("https://www.libsdl.org/release/SDL2-${pkgver}.tar.gz"{,.sig}
         sdl2-wayland1.20.patch::https://github.com/libsdl-org/SDL/pull/5092.patch)
sha512sums=('8688b855ce15d574fb0726dd2a44109bb7b34d81c652c51025a32bf7eb7015ceb685834cf9fc3f97a2f5f5a3203f548ce6845420fafae7e5dc6bb9d11ce1740d'
            'SKIP'
            'efa2d01a5861f99597eac0cff7e67efc03bd3877d8ad53d711b1ab54a59f93ef5bc6ffe776b9ac7e3103688a56f79e7d0e4155852dedd0d1982a1e334a2f8d37')
validpgpkeys=('1528635D8053A57F77D1E08630A59377A7763BE6') # Sam Lantinga

prepare() {
  # fix libdir
  sed -i 's|lib/cmake|lib32/cmake|' SDL2-${pkgver}/CMakeLists.txt
  # Don't try to link against ibus
  sed -i '/pkg_search_module.*ibus-1.0/d' SDL2-${pkgver}/CMakeLists.txt
  # Fix build with wayland 1.20
  patch -d SDL2-${pkgver} -p1 < sdl2-wayland1.20.patch
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cmake -S SDL2-${pkgver} -B build \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib32 \
      -DSDL_STATIC=OFF \
      -DSDL_DLOPEN=ON \
      -DARTS=OFF \
      -DESD=OFF \
      -DNAS=OFF \
      -DALSA=ON \
      -DPULSEAUDIO_SHARED=ON \
      -DVIDEO_WAYLAND=ON \
      -DRPATH=OFF \
      -DCLOCK_GETTIME=ON \
      -DJACK_SHARED=ON
  make -C build
}

package() {
  make DESTDIR="${pkgdir}" -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  sed -i "s/libSDL2\.a/libSDL2main.a/g" "$pkgdir"/usr/lib32/cmake/SDL2/SDL2Targets-noconfig.cmake

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s sdl2 "${pkgdir}"/usr/share/licenses/lib32-sdl2
}

# vim: ts=2 sw=2 et:
