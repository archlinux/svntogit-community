# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: carstene1ns <arch.carsten@teibes.de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=lib32-smpeg2
pkgver=2.0.0
pkgrel=1
pkgdesc='SDL2 MPEG Player Library'
arch=('x86_64')
url='http://icculus.org/smpeg/'
license=('LGPL')
depends=('bash' 'lib32-gcc-libs' 'lib32-glibc' 'lib32-sdl2' 'smpeg2')
makedepends=('subversion')
source=("smpeg2::svn://svn.icculus.org/smpeg/tags/release_${pkgver//./_}")
sha256sums=('SKIP')

build() {
  cd smpeg2

  export CC='gcc -m32'
  export CXX='g++ -m32 -std=gnu++03'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  make
}

package() {
  cd smpeg2

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/smpeg2-config{,-32}
  rm -rf "${pkgdir}"/usr/{bin/plaympeg,share,include}
}

# vim: ts=2 sw=2 et:
