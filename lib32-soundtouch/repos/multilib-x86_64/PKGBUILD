# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-soundtouch
pkgver=1.7.1
pkgrel=2
pkgdesc="An audio processing library"
arch=('x86_64')
url="http://www.surina.net/soundtouch/"
license=('LGPL')
depends=('lib32-gcc-libs' "${pkgname#*-}")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://www.surina.net/soundtouch/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('385eafa438a9d31ddf84b8d2f713097a3f1fc93d7abdb2fc54c484b777ee0267')

build() {
  cd "${srcdir}"/${pkgname#*-}

# Patch
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./bootstrap
  ./configure --prefix=/usr --libdir=/usr/lib32 --enable-shared --with-pic
  make
}

package() {
  cd "${srcdir}"/${pkgname#*-}

# Install
  make DESTDIR="${pkgdir}" install

# Multilib
  rm -rf "${pkgdir}"/usr/{bin,doc,include,share}
}

# vim: ts=2 sw=2 et:
