# $Id$
# Maintainer: Biru Ionut <ionut@archlinux.ro>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Contributor: Kaos < gianlucaatlas dot gmail dot com >

_pkgbasename=sqlite
pkgname=lib32-sqlite
_amalgamationver=3081002
_docver=${_amalgamationver}
#_docver=3080401
pkgver=3.8.10.2
pkgrel=1
pkgdesc="A C library that implements an SQL database engine (32-bit)"
arch=('x86_64')
license=('custom')
url="http://www.sqlite.org/"
depends=(lib32-glibc $_pkgbasename)
makedepends=('tcl' 'gcc-multilib' 'lib32-readline')
source=(http://www.sqlite.org/2015/sqlite-autoconf-${_amalgamationver}.tar.gz)
sha1sums=('c2f2c17d3dc4c4e179d35cc04e4420636d48a152')
options=('!makeflags')
provides=("lib32-sqlite3=$pkgver")
replaces=("lib32-sqlite3")
conflicts=("lib32-sqlite3")

build() {
  cd ${srcdir}/sqlite-autoconf-${_amalgamationver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export LTLINK_EXTRAS="-ldl"
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY -DSQLITE_SECURE_DELETE"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-static

  make
}


package() {
  cd ${srcdir}/sqlite-autoconf-${_amalgamationver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
