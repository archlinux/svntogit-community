# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>

pkgname=lib32-systemd
_pkgbasename=systemd
pkgver=208
pkgrel=2
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('lib32-glib2' 'lib32-libgcrypt' 'lib32-xz' 'systemd')
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-libcap' 'lib32-acl' 
             'intltool' 'gperf')
options=('!libtool')
source=("http://www.freedesktop.org/software/$_pkgbasename/$_pkgbasename-$pkgver.tar.xz")
md5sums=('df64550d92afbffb4f67a434193ee165')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  ./configure \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-audit \
    --disable-ima \
    --disable-pam \
    --disable-kmod \
    --disable-blkid \
    --without-python \
    --disable-libcryptsetup \
    --with-sysvinit-path= \
    --with-sysvrcnd-path= \
    --with-firmware-path="/usr/lib/firmware/updates:/usr/lib/firmware"

  make
}

package() {
  cd "${srcdir}/systemd-${pkgver}"
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
  rm -rf "${pkgdir}"/usr/lib32/libnss*

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s systemd "$pkgdir/usr/share/licenses/lib32-systemd"
}
