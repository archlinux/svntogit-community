# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>

pkgname=lib32-systemd
_pkgbasename=systemd
pkgver=215
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('lib32-glib2' 'lib32-libgcrypt' 'lib32-xz' 'systemd')
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-libcap' 'lib32-acl' 
             'intltool' 'gperf')
source=("http://www.freedesktop.org/software/$_pkgbasename/$_pkgbasename-$pkgver.tar.xz"
	'0001-networkd-properly-track-addresses-when-first-added.patch')
md5sums=('d2603e9fffd8b18d242543e36f2e7d31'
	 '2d237a277a12b3801c88d159d64a7413')

prepare() {
  cd "${srcdir}/systemd-${pkgver}"

  patch -Np1 <"$srcdir"/0001-networkd-properly-track-addresses-when-first-added.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  # LTO currently breaks the build because of libtool failures
  CFLAGS+=' -fno-lto'

  ./configure \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --enable-compat-libs \
    --disable-audit \
    --disable-tests \
    --disable-ima \
    --disable-kdbus \
    --disable-seccomp \
    --disable-pam \
    --disable-kmod \
    --disable-networkd \
    --disable-blkid \
    --without-python \
    --disable-libcryptsetup \
    --with-sysvinit-path= \
    --with-sysvrcnd-path= \
    --with-firmware-path="/usr/lib/firmware/updates:/usr/lib/firmware"

  make
}

package() {
  cd "${srcdir}/systemd-${pkgver}"
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
  rm -rf "${pkgdir}"/usr/lib32/libnss*

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s systemd "$pkgdir/usr/share/licenses/lib32-systemd"
}
