# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

_pkgbasename=talloc
pkgname=lib32-$_pkgbasename
pkgver=2.0.1
pkgrel=1
pkgdesc="talloc is a hierarchical pool based memory allocator with destructors (32-bit)"
arch=(x86_64)
license=('GPL3')
url="http://talloc.samba.org/"
source=(http://samba.org/ftp/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
md5sums=('c6e736540145ca58cb3dcb42f91cf57b')

build() {
  export CC="gcc -m32"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr \
  --sysconfdir=/etc/samba \
  --localstatedir=/var \
  --enable-talloc-compat1 \
  --enable-largefile \
  --libdir=/usr/lib32

  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=$pkgdir/ install

  # fix permission
  chmod 644 $pkgdir/usr/lib32/libtalloc.a
  rm -rf $pkgdir/usr/include
}

