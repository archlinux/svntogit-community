# $Id: PKGBUILD 86506 2010-08-02 14:22:19Z tpowa $
# Maintainer: judd <jvinet@zeroflux.org>
_pkgbasename=util-linux-ng
pkgname=lib32-$_pkgbasename
pkgver=2.18
pkgrel=3
pkgdesc="Miscellaneous system utilities for Linux (32-bit)"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('x86_64')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
license=('GPL2')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.kernel.org/pub/linux/utils/${_pkgbasename}/v2.18/${_pkgbasename}-${pkgver}.tar.bz2
        fix-findmnt.patch
        util-linux-ng-nilfs2.patch)
md5sums=('2f5f71e6af969d041d73ab778c141a77'
         '7346673932b165faadde2fa2a9c1cd3a'
         'aa1f210aa22363605363b7b14b8f7a78')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c
  mkdir -p "${pkgdir}/var/lib/hwclock"
  # fix findmnt
  patch -p1 -i "${srcdir}/fix-findmnt.patch"
  # add nilfs2 support, included in next upstream release
  patch -Np1 -i "${srcdir}/util-linux-ng-nilfs2.patch"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  autoreconf
  automake
  ./configure --without-ncurses --libdir=/usr/lib32

  make -C shlibs
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make -C shlibs DESTDIR="${pkgdir}" install

  # remove files
  rm -rf "${pkgdir}"/usr/{include,share}
}
