# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgbasename=util-linux
pkgname=lib32-$_pkgbasename
pkgver=2.21.2
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux (32-bit)"
url='http://www.kernel.org/pub/linux/utils/util-linux/'
arch=('x86_64')
depends=('lib32-glibc' "$_pkgbasename")
makedepends=('gcc-multilib')
provides=('lib32-util-linux-ng')
conflicts=('lib32-util-linux-ng')
replaces=('lib32-util-linux-ng')
license=('GPL2')
options=('!libtool' '!emptydirs')
source=("ftp://ftp.kernel.org/pub/linux/utils/util-linux/v2.21/util-linux-$pkgver.tar.xz")
md5sums=('54ba880f1d66782c2287ee2c898520e9')

shopt -s extglob

build() {
  cd "$_pkgbasename-$pkgver"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  #./autogen.sh
  ./configure --without-ncurses --libdir=/usr/lib32

  for lib in lib{mount,blkid,uuid}; do
    make -C "$lib"
  done
}

package() {
  make -C "$_pkgbasename-$pkgver" DESTDIR="$pkgdir" install

  # remove everything but libs
  rm -rf "$pkgdir"/!(usr) "$pkgdir"/usr/!(lib32)
}
