# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: FigoDaPaura <ffigoDaPaura>

pkgname=lib32-virtualgl
pkgver=2.3.2
pkgrel=1
pkgdesc="32-bit serverside components for 64-bit VirtualGL servers"
arch=('x86_64')
url="http://virtualgl.org"
license=('LGPL')
makedepends=('lib32-libxv' 'lib32-glu' 'virtualgl' 'lib32-turbojpeg' 'cmake' 'gcc-multilib' 'lib32-mesa' 'lib32-mesa-libgl')
md5sums=('29aaf0607a1031fa326444ff0321bcec')
source=("http://downloads.sourceforge.net/project/virtualgl/VirtualGL/$pkgver/VirtualGL-$pkgver.tar.gz")
 
build() {
  cd "$srcdir/VirtualGL-$pkgver"
 
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
 
  mkdir -p build && cd build
 
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr/share \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib32/libturbojpeg.so \
      -DVGL_LIBDIR=/usr/lib32 \
      -DVGL_BINDIR=/usr/bin \
      -DX11_X11_LIB=/usr/lib32/libX11.so \
      -DX11_Xext_LIB=/usr/lib32/libXext.so \
      -DOPENGL_gl_LIBRARY=/usr/lib32/libGL.so
  make
}
 
package() {
  depends=('virtualgl' 'lib32-libxv' 'lib32-glu' 'lib32-turbojpeg')
  cd "$srcdir/VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"
 
  cd "$pkgdir/usr"
  mv bin/glxspheres glxspheres-32
  rm -rf bin
  mkdir -p bin
  mv glxspheres-32 bin/
 
  cd "$pkgdir/usr/share"
  rm -rf doc
  rm -rf include
}
