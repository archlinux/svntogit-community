# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-vulkan-icd-loader
pkgver=1.2.199
pkgrel=1
arch=(x86_64)
pkgdesc="Vulkan Installable Client Driver (ICD) Loader (32-bit)"
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=('cmake' 'python' 'lib32-libx11' 'lib32-libxrandr' 'lib32-wayland' 'vulkan-headers')
depends=('lib32-glibc')
optdepends=('lib32-vulkan-driver: packaged vulkan driver')
provides=('libvulkan.so')
source=("vulkan-icd-loader-${pkgver}.tar.gz::https://github.com/KhronosGroup/Vulkan-Loader/archive/v${pkgver}.tar.gz")
sha256sums=('cc496f6725c7e088510d1a5e7c6a97b61e356b147dcc3d697233ca775dd768ef')

build() {
  export ASFLAGS=--32
  export CFLAGS=-m32
  export CXXFLAGS=-m32                                                                                                                                                                                                            
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig" 

  cd "${srcdir}"/Vulkan-Loader*

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DVULKAN_HEADERS_INSTALL_DIR=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}"/Vulkan-Loader*/build
  
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
