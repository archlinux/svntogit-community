# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=lib32-vulkan-loader-layers
pkgname=(lib32-vulkan-icd-loader lib32-vulkan-validation-layers)
_pkgname=Vulkan-LoaderAndValidationLayers
pkgver=1.1.73.0
pkgrel=1
arch=(x86_64)
url="https://www.khronos.org/vulkan/"
license=('custom')
depends=()
makedepends=(git cmake python-lxml lib32-libx11 lib32-libxrandr lib32-wayland)
source=("git+https://github.com/KhronosGroup/${_pkgname}#tag=sdk-${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}"
  ln -s ${_pkgname} "${_pkgname}-sdk-${pkgver}"
}

build() {
  export ASFLAGS=--32
  export CC="gcc -m32"
  export CXX="g++ -m32"                                                                                                                                                                                                            
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig" 

  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"

  ./update_external_sources.sh

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DBUILD_WSI_MIR_SUPPORT=Off \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package_lib32-vulkan-icd-loader() {
  pkgdesc="Vulkan Installable Client Driver (ICD) Loader (32-bit)"
  depends=(lib32-glibc)
  optdepends=('lib32-vulkan-driver: packaged vulkan driver')
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}/build"
  
  make DESTDIR="${pkgdir}" install

  rm -r ${pkgdir}/etc
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/include
  rm ${pkgdir}/usr/lib32/libVkLayer*.so

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_lib32-vulkan-validation-layers() {
  pkgdesc="Vulkan Software Development Kit (32-bit)"
  depends=(lib32-gcc-libs lib32-vulkan-icd-loader vulkan-headers)
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}/build"
  
  make DESTDIR="${pkgdir}" install

  rm -r ${pkgdir}/etc
  rm -r ${pkgdir}/usr/bin
  rm -r ${pkgdir}/usr/include
  rm -r ${pkgdir}/usr/lib32/pkgconfig
  rm ${pkgdir}/usr/lib32/libvulkan.so*

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

