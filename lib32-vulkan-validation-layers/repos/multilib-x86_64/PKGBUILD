# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-vulkan-validation-layers
pkgver=1.1.100
pkgrel=1
arch=(x86_64)
pkgdesc="Vulkan Validation Layers (32-bit)"
url="https://www.khronos.org/vulkan/"
license=('custom')
depends=(lib32-gcc-libs lib32-vulkan-icd-loader vulkan-headers)
makedepends=(cmake python lib32-libx11 lib32-libxrandr lib32-wayland git)
source=(https://github.com/KhronosGroup/Vulkan-ValidationLayers/archive/v${pkgver}.tar.gz)
#source=("git+https://github.com/KhronosGroup/Vulkan-ValidationLayers.git#commit=35d091dad71304dab79758fdae4bda12f692d735")
sha256sums=('3b6cfe4b38dda176ce56a93a9dd2f3a4d8193d384637f5ab83999330f20233ae')

#pkgver() {
#  cd "${srcdir}"/Vulkan-ValidationLayers*
#
#  echo 1.1.97.0+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
#}

build() {
  export ASFLAGS=--32
  export CC="gcc -m32"
  export CXX="g++ -m32"                                                                                                                                                                                                            
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig" 

  cd "${srcdir}"/Vulkan-ValidationLayers*

  rm -rf build ; mkdir build ; cd build
  ../scripts/update_deps.py
  cmake -C helper.cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}"/Vulkan-ValidationLayers*/build
  
  make DESTDIR="${pkgdir}" install

  rm -r ${pkgdir}/usr/include
  rm -r ${pkgdir}/usr/share/vulkan

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

