# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Sébastien Luttringer <sеblu@аrchlіnux․org>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.22.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=(x86_64)
url=https://wayland.freedesktop.org
license=(MIT)
depends=(
  lib32-glibc
  lib32-libffi
  lib32-expat
  lib32-libxml2
  wayland
)
makedepends=(meson)
provides=(libwayland-{client,cursor,egl,server}.so)
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611'  # Bryce Harrington
              'C0066D7DB8E9AC6844D728715E54498E697F11D7'  # Derek Foreman
              '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # Simon Ser <contact@emersion.fr>
source=("https://gitlab.freedesktop.org/wayland/wayland/-/releases/$pkgver/downloads/wayland-$pkgver.tar.xz"{,.sig})
b2sums=('b0bb366e7f3494813d1574228ae5dd48eccde035120d6dd8709e3a24738681ab9dd9ba6a13924b6a35982d331fa8911fa7346911da9f618be9c9109da0288629'
        'SKIP')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson build wayland-${pkgver} \
    --libdir=/usr/lib32 \
    -Ddocumentation=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wayland "${pkgdir}"/usr/share/licenses/lib32-wayland
}

# vim: ts=2 sw=2 et:
