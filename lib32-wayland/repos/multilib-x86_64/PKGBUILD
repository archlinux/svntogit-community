# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Sébastien Luttringer <sеblu@аrchlіnux․org>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.12.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='https://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-glibc' 'lib32-libffi' 'lib32-expat' 'lib32-libxml2' 'wayland')
source=("https://wayland.freedesktop.org/releases/wayland-${pkgver}.tar.xz"{,.sig})
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611') # Bryce Harrington
sha256sums=('d6b4135cba0188abcb7275513c72dede751d6194f6edc5b82183a3ba8b821ab1'
            'SKIP')

build() {
  cd wayland-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-documentation \
    --disable-static
  make
}

package() {
  cd wayland-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wayland "${pkgdir}"/usr/share/licenses/lib32-wayland
}

# vim: ts=2 sw=2 et:
