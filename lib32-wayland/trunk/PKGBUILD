# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Sébastien Luttringer <sеblu@аrchlіnux․org>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.20.0
pkgrel=2
pkgdesc='A computer display server protocol'
arch=(x86_64)
url=https://wayland.freedesktop.org
license=(MIT)
depends=(
  lib32-glibc
  lib32-libffi
  lib32-expat
  lib32-libxml2
  wayland
)
makedepends=(meson)
provides=(libwayland-{client,cursor,egl,server}.so)
options=(debug)
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611'  # Bryce Harrington
              'C0066D7DB8E9AC6844D728715E54498E697F11D7'  # Derek Foreman
              '34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # emersion <contact@emersion.fr>
source=("https://wayland.freedesktop.org/releases/wayland-$pkgver.tar.xz"{,.sig})
sha256sums=('b8a034154c7059772e0fdbd27dbfcda6c732df29cae56a82274f6ec5d7cd8725'
            'SKIP')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson build wayland-$pkgver \
    --libdir=/usr/lib32 \
    -D documentation=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wayland "${pkgdir}"/usr/share/licenses/lib32-wayland
}

# vim:set ts=2 sw=2 et:
