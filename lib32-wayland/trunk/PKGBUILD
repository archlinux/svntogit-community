# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.4.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-libffi' 'lib32-expat' "${pkgname#*-}")
source=("http://wayland.freedesktop.org/releases/wayland-${pkgver}.tar.xz")
sha256sums=('18f33b9f15b4c8b662fb1968e7636e7926b419dfc48de8a164b3a3d7095c5a58')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-{documentation,static}
  make
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
