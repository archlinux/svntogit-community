# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Limao Luo <luolimao@gmail.com>
# Contributor: Devin Cofer <ranguvar@archlinux.us>
# Contributor: Yggdrasil <tetzank@web.de>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Bob Finch <w9ya@arrl.net>
# Contributor: J. Santiago "Angelus" Hirschfeld <jsantiagoh@yahoo.com.ar>

pkgname=lib32-wxgtk
pkgver=2.8.12.1
pkgrel=4
pkgdesc="GTK+ implementation of wxWidgets API for GUI (lib32)"
arch=('x86_64')
url="http://wxwidgets.org/"
license=('custom:wxWindows')
makedepends=('lib32-gstreamer0.10-base-plugins' 'lib32-gtk2' 'lib32-mesa' 'lib32-glu' 'lib32-libxt' 'lib32-libsm' 'lib32-sdl' 'lib32-mesa-libgl')
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2")
sha256sums=('1f3f153d9f1504c6ce2d2c4b23e940b8f58b81f4cba35cda1a5bb31142243cd0')

build() {
  cd "${srcdir}"/wxPython-src-${pkgver}

# Multilib
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

# Build
  ./configure --prefix=/usr --libdir=/usr/lib32 --with-{gtk=2,libjpeg=sys,libpng=sys,libtiff=sys,libxpm=sys,opengl,regex=builtin,sdl} --enable-{graphics_ctx,unicode} --disable-{mediactrl,optimize,precomp-headers}
  make
  make -C locale allmo
  make -C contrib/src
}

package() {
  depends=('lib32-gtk2' 'lib32-gstreamer0.10-base' 'lib32-libsm' 'lib32-sdl' "${pkgname#*-}")

  cd "${srcdir}"/wxPython-src-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin/wxrc}
  mv "${pkgdir}"/usr/bin/wx-config{,32}
  mv "${pkgdir}"/usr/bin/wxrc{,32}-2.8

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
