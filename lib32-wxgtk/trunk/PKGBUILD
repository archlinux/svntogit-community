# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=lib32-wxgtk
pkgver=3.0.2
pkgrel=5
pkgdesc='GTK+ implementation of wxWidgets API for GUI'
arch=('x86_64')
url='http://wxwidgets.org'
license=('custom:wxWindows')
depends=('lib32-gtk2' 'lib32-libsm' "wxgtk=${pkgver}")
makedepends=('gcc-multilib' 'lib32-glu')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2"
        'make-abicheck-non-fatal.patch'
        'wxGTK-collision.patch')
sha256sums=('346879dc554f3ab8d6da2704f651ecb504a22e9d31c17ef5449b129ed711585d'
            'd4c2d070a06eb63f0a018c8cf687589e5ffdec601225b4d16a268ffe390fb58b'
            'c73c51f4b65a779462a4e0923a7e3bc7fe28457258fc8bfb5d843d87df119364')

prepare() {
  cd wx*-${pkgver}

  patch -Np1 -i ../make-abicheck-non-fatal.patch
  patch -Np1 -i ../wxGTK-collision.patch
}


build() {
  cd wxWidgets-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  export CFLAGS="-I/usr/include/libtiff32 $CFLAGS"
  export CXXFLAGS="-I/usr/include/libtiff32 $CXXFLAGS"

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --enable-graphics_ctx \
    --enable-unicode \
    --disable-{mediactrl,webview} \
    --disable-precomp-headers \
    --with-gtk='2' \
    --with-lib{jpeg,png,tiff,xpm}='sys' \
    --with-opengl \
    --with-regex='builtin' \
    --without-libnotify
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wxgtk "${pkgdir}"/usr/share/licenses/lib32-wxgtk
}

# vim: ts=2 sw=2 et:
