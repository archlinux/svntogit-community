# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=lib32-wxgtk2.8
pkgver=2.8.12.1
pkgrel=1
pkgdesc='GTK+ implementation of wxWidgets API for GUI'
arch=('x86_64')
url="http://wxwidgets.org/"
license=('custom:wxWindows')
makedepends=('lib32-glu' 'lib32-gstreamer0.10-base-plugins' 'lib32-gtk2' 'lib32-sdl')
source=("http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2"
        'wxGTK-collision.patch')
sha256sums=('1f3f153d9f1504c6ce2d2c4b23e940b8f58b81f4cba35cda1a5bb31142243cd0'
            '582eaed73d94eba2cfe7390c59b5143daef7d1c61554c4543261fe444d46e7d8')

prepare() {
  cd wx*-${pkgver}

  patch -Np1 -i ../wxGTK-collision.patch
}

build() {
  cd wx*-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --with-{gtk='2',{libjpeg,libpng,libtifflibxpm}='sys',opengl,regex='builtin',sdl} --enable-{graphics_ctx,unicode} --disable-{mediactrl,optimize,precomp-headers}
  make
  make -C locale allmo
  make -C contrib/src
}

package() {
  depends=('lib32-gstreamer0.10-base' 'lib32-gtk2' 'lib32-sdl' "${pkgname#*-}")

  cd wx*-${pkgver}

  make DESTDIR="${pkgdir}" install
  make -C contrib/src DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
