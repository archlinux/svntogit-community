# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=lib32-wxgtk2
pkgver=3.0.3.1
pkgrel=1
pkgdesc='GTK+ implementation of wxWidgets API for GUI'
arch=('x86_64')
url='http://wxwidgets.org'
license=('custom:wxWindows')
depends=('lib32-gtk2' 'lib32-libsm' 'wxgtk2')
makedepends=('gcc-multilib' 'lib32-glu')
conflicts=('lib32-wxgtk')
provides=('lib32-wxgtk')
replaces=('lib32-wxgtk')
source=("https://github.com/wxWidgets/wxWidgets/releases/download/v${pkgver}/wxWidgets-${pkgver}.tar.bz2"
        'make-abicheck-non-fatal.patch')
sha256sums=('3164ad6bc5f61c48d2185b39065ddbe44283eb834a5f62beb13f1d0923e366e4'
            'd4c2d070a06eb63f0a018c8cf687589e5ffdec601225b4d16a268ffe390fb58b')

prepare() {
  cd wxWidgets-${pkgver}

  patch -Np1 -i ../make-abicheck-non-fatal.patch
}

build() {
  cd wxWidgets-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  export CFLAGS="-I/usr/include/libtiff32 $CFLAGS"
  export CXXFLAGS="-I/usr/include/libtiff32 $CXXFLAGS"

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --enable-graphics_ctx \
    --enable-unicode \
    --disable-mediactrl \
    --disable-precomp-headers \
    --disable-webview \
    --with-gtk='2' \
    --with-lib{jpeg,png,tiff,xpm}='sys' \
    --with-opengl \
    --with-regex='builtin' \
    --without-libnotify
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin/{wx-config,wxrc},include,share}
  mv "${pkgdir}"/usr/bin/wxrc{,32}-3.0
  ln -s /usr/bin/wxrc32-3.0 "${pkgdir}"/usr/bin/wxrc32
  ln -s /usr/lib32/wx/config/gtk2-unicode-3.0 "${pkgdir}"/usr/bin/wx-config32

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wxgtk2 "${pkgdir}"/usr/share/licenses/lib32-wxgtk2
}

# vim: ts=2 sw=2 et:
