# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=lib32-xz
_pkgbasename=xz
pkgver=5.0.5
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files (32-bit)'
arch=('x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('lib32-glibc' 'xz')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://tukaani.org/xz/${_pkgbasename}-${pkgver}.tar.gz"
        "http://tukaani.org/xz/${_pkgbasename}-${pkgver}.tar.gz.sig")
md5sums=('19d924e066b6fff0bc9d1981b4e53196'
         'SKIP')

build() {
	cd ${srcdir}/xz-${pkgver}

	export CC="gcc -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	
	./configure --prefix=/usr \
		--libdir=/usr/lib32 \
		--disable-rpath \
		--enable-werror
	make
}

check() {
	cd ${srcdir}/xz-${pkgver}

	make check
}

package() {
	cd ${srcdir}/xz-${pkgver}
	
	make DESTDIR=${pkgdir} install

	rm -rf "${pkgdir}"/usr/{bin,include,share}
	install -d -m755 "${pkgdir}"/usr/share/licenses/xz/
	ln -s xz "$pkgdir/usr/share/licenses/xz/lib32-xz"
}
