# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

_pkgbasename=zlib
pkgname=lib32-$_pkgbasename
pkgver=1.2.12
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP (32-bit)'
arch=('x86_64')
license=('custom')
url="https://www.zlib.net/"
depends=('lib32-glibc' "$_pkgbasename")
provides=('libz.so')
source=("https://zlib.net/zlib-${pkgver}.tar.gz"{,.asc}
        $_pkgbasename-handle-incorrect-crc-inputs.patch::https://github.com/madler/zlib/commit/ec3df00224d4.patch
        $pkgname-fix-cross-compilation.patch::https://github.com/madler/zlib/commit/05796d3d8d55.patch)
sha256sums=('91844808532e5ce316b3c010929493c0244f3d37593afd6de04f71821d5136d9'
            'SKIP'
            '00e023c3ccb7b895ebb3421970b1b77f8a527b40190f35050b79fd0e817a7b0c'
            '0d38234384870bfd34dfcb738a9083952656f0c766a0f5990b1893076b084b76')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA') # Mark Adler <madler@alumni.caltech.edu>

prepare() {
	cd ${srcdir}/zlib-$pkgver

	# https://github.com/madler/zlib/issues/613
	patch -Np1 -i ../$_pkgbasename-handle-incorrect-crc-inputs.patch

	# https://github.com/madler/zlib/pull/607
	patch -Np1 -i ../$pkgname-fix-cross-compilation.patch
}

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

	cd ${srcdir}/zlib-$pkgver
	./configure --prefix=/usr \
		--libdir=/usr/lib32
	make
}

check() {
	cd zlib-$pkgver
	make test
}

package() {
	cd zlib-$pkgver
	make install DESTDIR="${pkgdir}"

	rm -rf "${pkgdir}"/usr/{include,share,bin}
	mkdir -p "$pkgdir/usr/share/licenses"
	ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
