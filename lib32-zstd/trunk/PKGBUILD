# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

_pkgname=zstd
pkgname=lib32-zstd
pkgver=1.5.0
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm (32-bit)'
url='https://facebook.github.io/zstd/'
arch=('x86_64')
license=('BSD' 'GPL2')
depends=('zstd' 'lib32-glibc')
makedepends=('gtest')
provides=('libzstd.so')
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.gz)
sha256sums=('5194fbfa781fcf45b98c5e849651aa7b3b0a008c6b72d4a0db760f3002291e94')
sha512sums=('b322fc1b89a556827b7fece2fb0f34c83bf65bb85b2468c791d6d9178a65c81e21f4171b7533cbf12bc1dfb2fd323d3e8c34d86167b157645c27f65186eec659')
b2sums=('3e365f9a9ffd684062c7af055d8e61a1b8ab91c2c294bd194f1684a4a1c97cc9884a523356cb0a3412e81bf6ec8ea26ecd41bf394dd3badea917981943865d16')

build() {
  cd ${_pkgname}-${pkgver}
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  make
  make zstdmt
  make -C contrib/pzstd
}

check() {
  cd ${_pkgname}-${pkgver}
  make check
  make -C contrib/pzstd test
}

package() {
  cd ${_pkgname}-${pkgver}
  make \
    PREFIX=/usr \
    LIBDIR=/usr/lib32 \
    PKGCONFIGDIR=/usr/lib32/pkgconfig \
    DESTDIR="${pkgdir}" \
    install
  rm -rf "${pkgdir}"/usr/{share,include,bin}
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
