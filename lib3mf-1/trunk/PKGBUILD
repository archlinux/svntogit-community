# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=lib3mf-1
pkgver=1.8.1
pkgrel=4
pkgdesc='Implementation of the 3D Manufacturing Format file standard (legacy version)'
arch=('x86_64')
url='https://3mf.io/'
license=('BSD')
depends=('libzip' 'zlib') 
makedepends=('cmake')
checkdepends=('gtest')
provides=('lib3MF.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/3MFConsortium/lib3mf/archive/v$pkgver.tar.gz"
        'lib3mf-1_use-system-gtest.patch::https://github.com/3MFConsortium/lib3mf/commit/cf679cc3736d4b22ce5711b5e81d28b7b6f4f4b0.patch'
        'lib3mf-1_link-libzip-and-zlib.patch::https://github.com/3MFConsortium/lib3mf/commit/093f2d95045dac25844936a72d5d8134a6265b20.patch')
sha512sums=('f7fd75bcb6472de1595a018e5add516d0d78ab0aee92462b686b77f8d2bef05270e7b737cb7e1d31fc5850815056e753874c2f9ec456a455e8461c4010fe914a'
            '84da1e659ed52582aca3c6f9ce2b234aad000e96bb657ff2aa7d118a613d5bca9b3f05f117f5db0a91440a9d51494730a637ed844ca4adb4887f357c15394663'
            '9fbe47bf940ccee60c759c47432b596eb8882c2bcf393853f1ff951bb8e662dc0f525768b4b9e02412e96a8814d6f55f61f1a90e74cbb61e331c31984ef6427e')

prepare() {
	cd "${pkgname%-1}-$pkgver"
	patch --strip=1 --input="$srcdir/lib3mf-1_use-system-gtest.patch"
	patch --strip=1 --input="$srcdir/lib3mf-1_link-libzip-and-zlib.patch"
}

build() {
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_INCLUDEDIR="include/$pkgname" \
	      -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS}" \
	      -DUSE_INCLUDED_LIBZIP=OFF -DUSE_INCLUDED_ZLIB=OFF -DUSE_INCLUDED_GTEST=OFF \
	      $( ((CHECKFUNC)) || echo -DLIB3MF_TESTS=OFF) \
	      -B build -S "$srcdir/${pkgname%-1}-$pkgver"
	make -C build
}

check() {
	cd build
	ctest
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/${pkgname%-1}-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
