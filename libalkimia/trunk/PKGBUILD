# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: gumper <gumper1034@gmail.com>

pkgname=libalkimia
pkgver=8.1.1
pkgrel=1
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC"
arch=('x86_64')
url="https://community.kde.org/Alkimia/libalkimia"
license=('LGPL')
depends=('qt5-webengine' 'knewstuff' 'hicolor-icon-theme' 'perl')
makedepends=('cmake' 'doxygen' 'extra-cmake-modules' 'plasma-framework' 'kpackage' 'kdoctools')
source=(https://download.kde.org/stable/alkimia/$pkgver/alkimia-$pkgver.tar.xz{,.sig})
validpgpkeys=('2060FF0BBF0E77C4E56DECE0B3DA98B373A0D6FA'
              '8C13BC14185A65215B592A2F1243132CD9321771'
              '91F17B048CF4668CD27FE9FBD3694D8B346FCA80') # Ralf Habacker <ralf.habacker@freenet.de>
sha256sums=('957adc63c0be54dd433e3268a37323be5456e5ab84ece27f73a1616a92db86d5'
            'SKIP')

build() {
  mkdir build
  cd build

  cmake -DSHARE_INSTALL_DIR=/usr/share ../alkimia-${pkgver}
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
