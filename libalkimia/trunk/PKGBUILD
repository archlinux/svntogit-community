# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: gumper <gumper1034@gmail.com>

pkgname=libalkimia
pkgver=8.1.0
pkgrel=1
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC"
arch=('x86_64')
url="https://community.kde.org/Alkimia/libalkimia"
license=('LGPL')
depends=('qt5-webengine' 'knewstuff' 'kdelibs4support' 'hicolor-icon-theme' 'perl')
makedepends=('cmake' 'doxygen' 'extra-cmake-modules' 'kdesignerplugin' 'plasma-framework' 'kpackage' 'kdoctools' 'kinit')
source=(https://download.kde.org/stable/alkimia/$pkgver/alkimia-$pkgver.tar.xz{,.sig})
validpgpkeys=('2060FF0BBF0E77C4E56DECE0B3DA98B373A0D6FA'
              '8C13BC14185A65215B592A2F1243132CD9321771') # Ralf Habacker <ralf.habacker@freenet.de>
sha256sums=('916807352707b0490cdd9ca65682eff73b00ca523029bda6fe7a2749a1bc927c'
            'SKIP')

build() {
  mkdir build
  cd build

  cmake -DSHARE_INSTALL_DIR=/usr/share ../alkimia-${pkgver} \
    -DBUILD_WITH_WEBENGINE=ON
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
