# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=libao-pulse
_pkgbasename=libao
pkgver=1.0.0
pkgrel=1
pkgdesc="A libao driver for the PulseAudio sound server"
url="http://www.xiph.org/ao"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libao' 'pulseaudio')
backup=('etc/libao.conf')
options=('!libtool')
source=("http://downloads.xiph.org/releases/ao/$_pkgbasename-$pkgver.tar.gz"
        "https://trac.xiph.org/raw-attachment/ticket/1689/ao-pulse-fixes.patch")

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  patch -Np0 -i "$srcdir/ao-pulse-fixes.patch" || return 1

  ./configure --prefix=/usr || return 1

  cd src/plugins/pulse
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver/src/plugins/pulse"
  make DESTDIR="$pkgdir" install
}

md5sums=('08283fbe1f587619053a156254afecec'
         '58eaa3e00e605264ad286f6b161b07e6')
