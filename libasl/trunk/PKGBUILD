# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Andrejs Mivre≈Üiks <gim at fastmail dot fm>

pkgname=libasl
pkgver=0.1.7
pkgrel=4
pkgdesc='Free and open source hardware accelerated multiphysics simulation platform (Advanced Simulation Library)'
arch=('i686' 'x86_64')
url='http://asl.org.il/'
license=('AGPL3')
depends=('boost-libs' 'opencl-icd-loader' 'libmatio' 'vtk' 'libjpeg' 'libpng' 'libtiff'
         'freetype2' 'libgl')
makedepends=('cmake' 'opencl-headers' 'boost' 'mesa-libgl')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/AvtechScientific/ASL/archive/v${pkgver}.tar.gz"
        'suppress-deprecated-warnings.patch')
md5sums=('a339c515690f929c2a870666dc6d2233'
         '972d7ed8037a35d32125773ae70f6360')

prepare() {
  rm -rf "build"
  mkdir "build"

  # patch to suppress 'deprecated' warning messages
  # caused by 'opencl-headers' since update to 2.0
  cd "ASL-${pkgver}"
  patch -p1 -i "${srcdir}/suppress-deprecated-warnings.patch"
}

build() {
  cd "build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DBUILD_SHARED_LIBS=ON \
        -DWITH_EXAMPLES=ON \
        -DWITH_MATIO=ON \
        -DWITH_API_DOC=OFF \
        -DWITH_TESTS=OFF \
        "../ASL-${pkgver}"
  make
}

package() {
  cd "build"
  make DESTDIR="${pkgdir}" install
}

