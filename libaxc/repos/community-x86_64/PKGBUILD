# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=libaxc
pkgver=0.3.6
pkgrel=1
pkgdesc='Client lib for libsignal-c aka libaxolotl'
arch=('x86_64')
url='https://github.com/gkdr/axc'
license=('GPL3')
depends=('libsignal-protocol-c' 'sqlite' 'libgcrypt' 'glib2')
provides=('libaxc.so')
source=("https://github.com/gkdr/axc/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('e59d73e0011b11c0e594297d15353f0e7c378c5ce29c2f747f0a10f7ac302abbe8a0e6f3cad37a652a5a4763d15f393e2cbd7272c4249883c0c99640d18ab518')

build() {
  cd ${pkgname##lib}-${pkgver}
  make PREFIX="/usr"
}

package() {
  cd ${pkgname##lib}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  chmod a+x "${pkgdir}"/usr/lib/libaxc.so.${pkgver}
}
