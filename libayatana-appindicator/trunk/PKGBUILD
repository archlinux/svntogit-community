# Maintainer:
# Contributor: Robert Tari <robert at tari dot in>

pkgname=libayatana-appindicator
pkgver=0.5.91
pkgrel=2
pkgdesc='Ayatana Application Indicators shared library'
arch=(x86_64)
url='https://github.com/AyatanaIndicators/libayatana-appindicator'
license=(LGPL2.1 LGPL3)
makedepends=(cmake gobject-introspection vala)
depends=(libdbusmenu-gtk3 libayatana-indicator)
source=(https://github.com/AyatanaIndicators/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('52eb5d0c0de07177833e50fbaee592dcb3939e96c6b789921e2a8caf40a1ed26')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_BINDINGS_MONO=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
