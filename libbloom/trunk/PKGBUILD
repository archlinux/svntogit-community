# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libbloom
pkgver=1.4
pkgrel=1
pkgdesc="A simple and small bloom filter implementation in plain C"
arch=('x86_64')
url="https://github.com/jvirkki/libbloom"
license=('BSD')
depends=('glibc')
makedepends=('git')
source=("git+https://github.com/jvirkki/libbloom.git#tag=v$pkgver")
sha256sums=('SKIP')

build() {
  cd libbloom
  MM=" " make
}

check() {
  cd libbloom
  MM=" " make test
}

package() {
  cd libbloom
  install -Dm644 bloom.h "$pkgdir"/usr/include/bloom.h
  install -Dm755 build/libbloom.so.$pkgver "$pkgdir"/usr/lib/libbloom.so.$pkgver
  ln -s libbloom.so.$pkgver "$pkgdir"/usr/lib/libbloom.so

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
