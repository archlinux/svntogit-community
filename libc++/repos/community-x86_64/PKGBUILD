# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: MThinkCpp <mtc.maintainer[at]outlook.com>
pkgname=libc++
pkgver=3.7.1
pkgrel=1
pkgdesc='A new implementation of the C++ standard library, targeting C++11.'
url='http://libcxx.llvm.org'
license=('custom:University of Illinois/NCSA Open Source License')
arch=('i686' 'x86_64')
# gcc-libs needed for libgcc_s, because Arch doesn't use a pure compiler-rt clang
depends=('glibc' 'libc++abi' 'gcc-libs')
makedepends=('clang' 'subversion' 'cmake')
source=("http://www.llvm.org/releases/$pkgver/libcxx-${pkgver}.src.tar.xz"
        "http://www.llvm.org/releases/$pkgver/libcxx-${pkgver}.src.tar.xz.sig")
md5sums=('f9c43fa552a10e14ff53b94d04bea140'
         'SKIP')
install=${pkgname}.install
validpgpkeys=(
              # Bill Wendling <void@llvm.org>
              54E3BDE33185D9F69664D22455F5CD70BB5A0569
              # Tom Stellard <tom@stellard.net>
              11E521D646982372EB577A1F8F0871F202119294
              # Hans Wennborg <hans@chromium.org>
              B6C8F98282B944E3B0D5C2530FC3042E345AD05D
)

build() {
  mkdir -p libcxx-${pkgver}.src/build
  cd libcxx-${pkgver}.src/build
  CC=clang CXX=clang++ cmake -G "Unix Makefiles" -DLIBCXX_CXX_ABI=libcxxabi \
    -DLIBCXX_CXX_ABI_INCLUDE_PATHS=/usr/include/cxxabi \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr "$srcdir/libcxx-${pkgver}.src"
  make
}
package() {
  install -Dm644 libcxx-${pkgver}.src/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  cd libcxx-${pkgver}.src/build
  make DESTDIR="$pkgdir" install
}
