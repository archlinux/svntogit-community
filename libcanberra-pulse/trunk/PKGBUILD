# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libcanberra
pkgname=$_pkgbasename-pulse
pkgver=0.25
pkgrel=1
pkgdesc="A implementation of the XDG Sound Theme Specification, with PulseAudio support"
arch=(i686 x86_64)
license=('LGPL')
depends=('libvorbis>=1.2.1rc1' 'gstreamer0.10>=0.10.24' 'libtool>=2.2.6a'
         'gtk2>=2.18.2' 'alsa-lib>=1.0.20' 'tdb' 'pulseaudio')
makedepends=('gtk-doc')
provides=("$_pkgbasename=$pkgver-$pkgrel")
conflicts=("$_pkgbasename")
install=libcanberra.install
options=(!emptydirs)
url=http://0pointer.de/lennart/projects/libcanberra
source=("$url/$_pkgbasename-$pkgver.tar.gz" libcanberra-gtk-module.sh)
md5sums=('d7a158d22d0b44fb2a67aadae74e28ab' 'a54799e624aac814b9343ab05f25c38b')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static --disable-oss \
      --enable-alsa --enable-null --enable-gstreamer --enable-pulse \
      --with-builtin=dso
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/libcanberra-gtk.la"
  rm -f "${pkgdir}/usr/lib/gtk-2.0/modules/"*.la
  
  install -m755 -d "${pkgdir}/usr/share/gconf" 
  mv "${pkgdir}/etc/gconf/schemas" "${pkgdir}/usr/share/gconf/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/libcanberra-gtk-module.sh" \
    "${pkgdir}/etc/X11/xinit/xinitrc.d/40-libcanberra-gtk-module"
}
