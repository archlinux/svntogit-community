# Maintainer:
# Contributor: Daniel LÃ¶bl <dloebl.2000@gmail.com>

pkgname=libcgif
pkgver=0.3.0
pkgrel=1
pkgdesc='A fast and lightweight GIF encoding library'
arch=(x86_64)
url='https://github.com/dloebl/cgif'
license=(MIT)
depends=(glibc)
makedepends=(meson)
source=(https://github.com/dloebl/cgif/archive/V$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('c4f70bbae4c6afee3a524e65be31ae495201fd26687cb8429d7aded8be96306a')

build() {
  meson build cgif-$pkgver \
    --prefix=/usr
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
 #mkdir -p "$pkgdir/usr/lib/" "$pkgdir/usr/include/" "$pkgdir/usr/share/licenses/$pkgname/"
  meson install -C build \
    --destdir "$pkgdir"
  install -Dm644 cgif-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
