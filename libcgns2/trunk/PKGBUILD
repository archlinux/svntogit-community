# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Klimov Max <cleemmi@gmail.com>

pkgname=libcgns2
_basever=2.5
_relver=5
pkgver=${_basever}.${_relver}
pkgrel=3
pkgdesc='General purpose library for the storage and retrieval of computational fluid dynamics analysis data by CGNS standard'
arch=('i686' 'x86_64')
url='http://www.cgns.org'
license=('custom')
conflicts=('libcgns')
depends=('glibc')
makedepends=('chrpath')
source=("http://downloads.sourceforge.net/project/cgns/cgnslib_${_basever}/Release%205/cgnslib_${_basever}-${_relver}.tar.gz")
md5sums=('ae2a2e79b99d41c63e5ed5f661f70fd9')

[[ "$CARCH" == "x86_64" ]] && _64bits="--enable-64bit"

build() {
  cd cgnslib_${_basever}

  ./configure \
    --prefix=/usr \
    --enable-shared \
    ${_64bits}

  make
}

package() {
  cd cgnslib_${_basever}

  install -d ${pkgdir}/usr/include
  install -d ${pkgdir}/usr/lib

  make install LIBDIR=${pkgdir}/usr/lib INCLUDEDIR=${pkgdir}/usr/include

  # install license
  install -d ${pkgdir}/usr/share/licenses/libcgns2
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/libcgns2

  chrpath --delete ${pkgdir}/usr/lib/libcgns.so
}
