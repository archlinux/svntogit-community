# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libcontentaction
pkgver=0.3.18
pkgrel=1
pkgdesc="Library for associating content with actions"
arch=('x86_64')
url="https://git.merproject.org/mer-core/libcontentaction"
license=('LGPL')
depends=('qt5-systems' 'mlite')
makedepends=('python' 'qt5-tools')
source=("https://git.merproject.org/mer-core/libcontentaction/-/archive/$pkgver/libcontentaction-$pkgver.tar.bz2")
sha512sums=('a62ff4c800513e47a81e45b1dab8f1b8b9155257e7c81c72b43d9779bcb5801377d770acc81a7c13e71bfbaf1cdb88755d1a89f3faa8b96011039437bf8cd283')

build() {
  cd libcontentaction-$pkgver

  qmake-qt5
  make
}

package() {
  cd libcontentaction-$pkgver

  make INSTALL_ROOT="$pkgdir" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
