# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: X0rg

pkgname=libcpuid
pkgver=0.6.0
pkgrel=1
pkgdesc='A small C library for x86 CPU detection and feature extraction'
arch=('x86_64')
url='http://libcpuid.sourceforge.net'
license=('BSD')
depends=('glibc')
makedepends=('git' 'cmake' 'ninja' 'doxygen' 'python' 'graphviz')
options=('debug')
_commit='3bf569faf5d606bc29d7122984eb888232fae8bd'
source=("$pkgname::git+https://github.com/anrieff/libcpuid#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
	cmake \
    -S "$pkgname" \
    -B build \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBCPUID_TESTS:BOOL=ON

	cmake --build build
}

check() {
  cd build
  ninja consistency test-old
}

package() {
	DESTDIR="$pkgdir" cmake --install build

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/COPYING"
}
