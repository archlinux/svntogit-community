# $Id$
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>

pkgname=libcuefile
pkgver=475
pkgrel=1
pkgdesc='Library to work with CUE files'
arch=('i686' 'x86_64')
url="http://www.musepack.net/index.php?pg=src"
license=('GPL2')
depends=('glibc')
makedepends=('cmake')
source=(http://files.musepack.net/source/${pkgname}_r${pkgver}.tar.gz)
md5sums=('1a6ac52e1080fd54f0f59372345f1e4e')

build() {
  mkdir "${srcdir}/${pkgname}_r${pkgver}/build"
  cd "${srcdir}/${pkgname}_r${pkgver}/build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "${srcdir}/${pkgname}_r${pkgver}/build"
  make DESTDIR="${pkgdir}/" install

  mkdir -p "${pkgdir}/usr/include/cuetools"
  install -Dm644 "${srcdir}"/${pkgname}_r${pkgver}/include/cuetools/{cd,cdtext,cuefile}.h \
    "${pkgdir}/usr/include/cuetools"
}

# vim:set ts=2 sw=2 et:
