# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libdesktop-agnostic
pkgver=0.3.92
pkgrel=5
pkgdesc="Provides an extensible config API, a unified virtual fs API, and a desktop item editor for GLib-based projects"
arch=('i686' 'x86_64')
url="https://launchpad.net/libdesktop-agnostic"
license=('GPL' 'LGPL')
depends=('pygtk')
makedepends=('intltool' 'vala' 'gconf' 'gobject-introspection')
optdepends=('gconf: gconf configuration backend')
options=('!libtool')
backup=(etc/xdg/libdesktop-agnostic/desktop-agnostic.ini)
source=(https://launchpad.net/$pkgname/0.4/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('42374d226a21d57637f97173f6b105a1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=/usr/bin/python2

  python2 ./waf configure --prefix=/usr --sysconfdir=/etc \
                  --config-backends=gconf,keyfile \
                  --vfs-backends=gio \
                  --desktop-entry-backends=gio,glib
  python2 ./waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 ./waf install --destdir="$pkgdir"
}
