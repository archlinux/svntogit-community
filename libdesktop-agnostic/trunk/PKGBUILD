# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libdesktop-agnostic
pkgver=0.3.90
pkgrel=3
pkgdesc="Provides an extensible config API, a unified virtual fs API, and a desktop item editor for GLib-based projects."
arch=('i686' 'x86_64')
url="https://launchpad.net/libdesktop-agnostic"
license=('GPL')
depends=('glib2' 'gtk2' 'pygtk' 'pygobject' 'gconf')
makedepends=('gobject-introspection' 'vala' 'intltool')
options=('!libtool')
source=(https://launchpad.net/${pkgname}/0.4/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz
        various-fixes.patch)
backup=(etc/xdg/libdesktop-agnostic/desktop-agnostic.ini)
md5sums=('67141440b7fa77e2d704e7368c067212'
         'f60fb02433a79b71041db3f85254b9f4')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

        # Diff from various-fixes branch (revision 406)
        # https://code.launchpad.net/~awn-core/libdesktop-agnostic/various-fixes
        patch -Np1 -i "$srcdir/various-fixes.patch"

        # Force source regeneration
        rm -r gen_src

        # Optimization causes segfault in lda-desktop-entry-editor
        CFLAGS+=" -O0"

	# fix for python 2.7
	sed -i 's:^#!.*bin/env python:#!/usr/bin/env python2:' waf

	./waf configure --config-backends=gconf,keyfile --vfs-backends=gio \
	    --desktop-entry-backends=glib --prefix=/usr --sysconfdir=/etc
	./waf
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./waf install --destdir=${pkgdir} --sysconfdir=${pkgdir}/etc
}
