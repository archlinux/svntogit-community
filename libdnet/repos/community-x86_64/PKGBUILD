# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: James Fryman <jfryman@gmail.com>

pkgname=libdnet
pkgver=1.12
pkgrel=10
pkgdesc='A simplified, portable interface to several low-level networking routines'
arch=(x86_64)
makedepends=('python2' 'pyrex')
optdepends=('python2')
license=('BSD')
url='https://github.com/dugsong/libdnet'
source=("https://github.com/dugsong/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz"
	'libdnet-1.12.pyrex2.7.patch')
sha256sums=('b6360659c93fa2e3cde9e0a1fc9c07bc4111f3448c5de856e095eb98315dd424'
            '136141d0bde459a4c7acf6fdca6aa52d2a377b9fdc07116ddc85c6a776752ef8')

prepare() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver

  patch -p0 <"$srcdir"/libdnet-1.12.pyrex2.7.patch
  rm -f python/dnet.c
}

build() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver

  autoreconf -I config --force --install

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin
  pyrexc python/dnet.pyx
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  cd python && \
  python2 setup.py install --root="$pkgdir"

  install -D -m0644 ../LICENSE "$pkgdir"/usr/share/licenses/libdnet/license
}
