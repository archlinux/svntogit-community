# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libdsme
pkgver=0.64.1
pkgrel=1
pkgdesc="DSME dsmesock dynamic library"
arch=('x86_64')
url="https://git.merproject.org/mer-core/libdsme"
license=('LGPL')
depends=('glib2')
makedepends=('git' 'check')
source=("git+https://git.merproject.org/mer-core/libdsme.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd libdsme
  ./verify_version
  make
}

package() {
  cd libdsme
  make DESTDIR="${pkgdir}" install_main install_devel

  # Fix symlinks
  for name in libdsme libdsme_dbus_if libthermalmanager_dbus_if; do
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so.0
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so
  done
}
