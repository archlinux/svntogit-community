# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libdsme
pkgver=0.66.6
pkgrel=1
pkgdesc="DSME dsmesock dynamic library"
arch=('x86_64')
url="https://git.merproject.org/mer-core/libdsme"
license=('LGPL')
depends=('glib2')
makedepends=('check')
source=(https://github.com/sailfishos/libdsme/archive/$pkgver/$pkgname-$pkgver.tar.gz libdsme-uio.patch)
sha512sums=('29fe4f3b9accc200392be8450c93825cbc1aaca9558a7419f51fe0d4bdc8f7b3f9c1b8c7da1cd3f9e66cced4a7c26dd25faed9b8b990755bd4f618035e1f01f2'
            'aba3ffda5cab681133bc2df12f91c6b572e5b11692f9485558be36a80bfbf43a6438df2bf3c003c4f6e3dc73afc345435885eedf7d6b17c0083f054008a84a33')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../libdsme-uio.patch # Fix build
}

build() {
  cd $pkgname-$pkgver
  ./verify_version
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install_main install_devel

  # Fix symlinks
  for name in libdsme libdsme_dbus_if libthermalmanager_dbus_if; do
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so.0
    ln -sf $name.so.0.3.0 "$pkgdir"/usr/lib/$name.so
  done
}
