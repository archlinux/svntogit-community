# $Id$
# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=libextractor
pkgver=1.0.1
pkgrel=4
pkgdesc="A library used to extract meta-data from files of arbitrary type"
arch=("i686" "x86_64")
license=('GPL')
url="http://gnunet.org/libextractor/"
depends=('libltdl')
makedepends=('flac' 'poppler' 'exiv2' 'qt4' 'libvorbis' 'gtk2' 'libgsf' 'libmpeg2')
optdepends=('flac: flac support'
	    'poppler: pdf support'
	    'exiv2: metadata support'
	    'qt: the Qt GUI interface/thumbnails'
	    'gtk2: gtk2 GUI interface/thumbnails'
	    'libgsf: (optional) for ODF support'
	    'libmpeg2: mpeg support'
	    'libvorbis: ogg/vorbis support')
options=('!libtool' '!makeflags')
install=libextractor.install
source=("http://ftp.gnu.org/gnu/libextractor/${pkgname}-${pkgver}.tar.gz"{,.sig})
md5sums=('593c40670fd7fc8c3ae7adc3df557f64'
         'ef9bfeb3ade063dc9a5b29b2afd0732e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/share/info/dir
}
