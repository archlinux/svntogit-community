# $Id$
# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=libextractor
pkgver=1.6
pkgrel=2
pkgdesc="A library used to extract meta-data from files of arbitrary type"
arch=("x86_64")
license=('GPL')
url="http://gnunet.org/libextractor/"
depends=('libltdl')
makedepends=('exiv2' 'ffmpeg' 'file' 'flac' 'gst-plugins-base-libs' 'libgsf' 'libmpeg2' 'libvorbis' 'poppler')
optdepends=('exiv2: metadata support'
            'ffmpeg: previewopus/thumbnailffmpeg support'
            'file: mime support'
            'flac: flac support'
            'gst-plugins-base-libs: gstreamer support'
            'libarchive: archive support'
    	    'libgsf: (optional) for ODF support'
    	    'libmpeg2: mpeg support'
    	    'libvorbis: ogg/vorbis support'
            'poppler: pdf support')
validpgpkeys=('19647543F7861D3BF4E64FF7BF60708B48426C7E'
	      'D8423BCB326C7907033929C7939E6BE1E29FC3CC')
source=("https://ftp.gnu.org/gnu/libextractor/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('26d4adca2e381d2a0c8b3037ec85e094ac5d40485623794466cfc176f5bbf69d'
            'SKIP')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/share/info/dir
}
