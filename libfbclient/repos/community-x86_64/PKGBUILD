# $Id$
# Maintainer: Carlier Laurent <lordheavym@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=libfbclient
pkgver=2.5.0.26074
pkgrel=3
pkgdesc="Client library for Firebird."
arch=('i686' 'x86_64')
url="http://www.firebirdsql.org/"
license=('custom')
depends=('gcc-libs' 'icu')
source=(http://downloads.sourceforge.net/firebird/Firebird-$pkgver-0.tar.bz2
        LICENSE)
md5sums=('780f162ee71f087fc277adf09f966529'
         '0d61798861e159c85dbdf0f30fe2f2ce')

build() {
  cd $srcdir/Firebird-$pkgver-0

  ./configure --prefix=/usr --with-system-icu --without-fbudf \
    --without-fbsbin --without-fbconf --without-fbdoc --without-fbsample \
    --without-fbsample-db --without-fbintl --without-fbmisc --without-fbhelp \
    --without-fbsecure-db --with-fbmsg=/usr/share/firebird --without-fblog \
    --without-fbglock --without-fbplugins

  make
}

package() {
  cd $srcdir/Firebird-$pkgver-0

  mkdir -p $pkgdir/usr/{bin,share/firebird}
  cp -R gen/firebird/{lib,include} $pkgdir/usr
  rm -f $pkgdir/usr/lib/libedit.a
  install -m644 gen/firebird/*.msg $pkgdir/usr/share/firebird
  install -m755 gen/firebird/bin/fb_config $pkgdir/usr/bin
  mkdir -p $srcdir/usr/share/licenses/$pkgname/
  install -D -m644 ${srcdir}/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
