# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=libfdk-aac
pkgver=0.1.4
pkgrel=2
pkgdesc='Fraunhofer FDK AAC codec library'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/opencore-amr/'
license=('custom')
depends=('glibc')
provides=('libfdk-aac.so')
source=("http://downloads.sourceforge.net/opencore-amr/fdk-aac-${pkgver}.tar.gz")
sha256sums=('5910fe788677ca13532e3f47b7afaa01d72334d46a2d5e1d1f080f1173ff15ab')

build() {
  cd fdk-aac-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-static
  make
}

package () {
  cd fdk-aac-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/libfdk-aac
  install -m 644 NOTICE "${pkgdir}"/usr/share/licenses/libfdk-aac/
}

# vim: ts=2 sw=2 et:
