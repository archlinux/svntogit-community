# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.6
pkgrel=1
pkgdesc="Driver for FireWire audio devices"
arch=(x86_64)
url="http://www.ffado.org/"
license=(GPL3)
depends=(gcc-libs glibc glibmm hicolor-icon-theme libavc1394
libiec61883 libxml++ python-dbus)
makedepends=(alsa-lib dbus-c++ jack libconfig python-pyqt5 scons)
optdepends=('python-pyqt5: ffado-mixer')
provides=(libffado.so)
options=(debug)
source=(http://www.ffado.org/files/$pkgname-$pkgver.tgz)
sha512sums=('dac651233ea94c19834e810c64230bcbd5516de9e694ced1b6497ad927d734c527b3a556281676393674d33da768cb2431e7da1a0fdfc6ceb268e897029aa584')
b2sums=('1e86e7f9179e94c00a74514dbd7c0c299de6d1870f22c4d9ba14cb6b105b41c9dda62d19d0d660e84eee7e0f02c0bf7c44fbe58e24634ccf9cc9e41f1d38b5c2')

prepare() {
  cd $pkgname-$pkgver
  # fix icon name, so it's coherent with XDG desktop file name
  sed -e 's/hi64-apps-ffado/ffado-mixer/g' -i support/xdg/ffado.org-ffadomixer.desktop
  # fix id, so it's coherent with XDG desktop file name
  sed -e 's/ffado.org-ffadomixer.desktop/ffado-mixer.desktop/g' -i support/xdg/ffado-mixer.appdata.xml
}

build() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd $pkgname-$pkgver
  scons PREFIX=/usr \
        DEBUG=no \
        CUSTOM_ENV=True \
        MANDIR=/usr/share/man \
        PYTHON_INTERPRETER=/usr/bin/python3 \
        PYPKGDIR=/usr/lib/python$python_version/site-packages \
        UDEVDIR=/usr/lib/udev/rules.d
}

package() {
  depends+=(libasound.so libconfig++.so libdbus-c++-1.so)

  cd $pkgname-$pkgver
  scons DESTDIR="$pkgdir" WILL_DEAL_WITH_XDG_MYSELF=True install
  install -vDm 644 support/xdg/ffado.org-ffadomixer.desktop "$pkgdir/usr/share/applications/ffado-mixer.desktop"
  install -vDm 644 support/xdg/hi64-apps-ffado.png "$pkgdir/usr/share/icons/hicolor/64x64/apps/ffado-mixer.png"
  install -vDm 644 {AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
