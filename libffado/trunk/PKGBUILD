# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.1
pkgrel=4
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('alsa-lib' 'dbus-c++' 'libavc1394' 'libconfig' 'libiec61883' 'libxml++' 'python-dbus')
makedepends=('jack' 'python-pyqt5' 'scons')
optdepends=('python-pyqt5: ffado-mixer')
provides=('libffado.so')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        "${pkgname}-2.4.1-fix-python3-pyqt5.diff"
        "${pkgname}-2.4.1-fix-scons.diff")
sha512sums=('0859f17b87c68108bbde726d1cf8ab0b99f6f7f02cbe36acd8b451e112688a4d1edc21134eb4a2edc8c92dcf5f5da52efeabb2ad0dc07804ed1aa664a3b38d74'
            '39beb2399377d7ee13a43ddd7a80907eb94a2f8f146c5a94a4ba0e1471b3369d2b1519cb8b15c3e2352d9854048882cc037058a1992e6e87270fe9e17fdba91e'
            '69d0771a1abf5e40473e4b46fa53fdeead44de78108b3901841b68649f203c64ac6afe4f925c57f1126792def3be246a3600d9032f1a558bb536c0b28fe61f68')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "../${pkgname}-2.4.1-fix-python3-pyqt5.diff"
  patch -Np1 -i "../${pkgname}-2.4.1-fix-scons.diff"
  # fix icon name, so it's coherent with XDG desktop file name
  sed -e 's/hi64-apps-ffado/ffado-mixer/g' \
      -i support/xdg/ffado.org-ffadomixer.desktop
}

build() {
  cd "${pkgname}-${pkgver}"
  scons PREFIX=/usr \
        DEBUG=no \
        CUSTOM_ENV=True \
        MANDIR='/usr/share/man' \
        PYTHON_INTERPRETER='/usr/bin/python3' \
        PYPKGDIR='/usr/lib/python3.7/site-packages' \
        UDEVDIR='/usr/lib/udev/rules.d'
}

package() {
  cd "${pkgname}-${pkgver}"
  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # XDG
  install -vDm 644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffado-mixer.desktop"
  install -vDm 644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/icons/64x64/apps/ffado-mixer.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
