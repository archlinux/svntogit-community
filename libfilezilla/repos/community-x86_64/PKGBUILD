# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

pkgname=libfilezilla
epoch=1
pkgver=0.39.1
pkgrel=1
pkgdesc='Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs'
url='https://lib.filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'gcc-libs' 'nettle' 'gnutls' 'libnettle.so' 'libhogweed.so')
makedepends=(clang)
checkdepends=('cppunit')
provides=('libfilezilla.so')
source=(https://download.filezilla-project.org/libfilezilla/libfilezilla-${pkgver}.tar.bz2)
sha512sums=('b5c4dcaef146ea3e249a4972e66aa441187693bea148f0ead0c08568ecf1856a78c7455cbc70737d82b9c33c2af5d6abe79dd4cb2dfbb65bdcefd6765016dfb6')
b2sums=('2845f0309f8a9036f42930203b89d503ffe5897dbb819cc9249b3179deb24f5c562843471712702d7a302df0b3c7dead1bc108de9dd5dd4b1cb1501cd3ef9ba1')

build() {
  cd ${pkgname}-${pkgver}
  export CXX=clang++
  export CC=clang
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
