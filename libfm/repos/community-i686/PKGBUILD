# $Id$
# Maintainer:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=libfm
pkgver=1.2.2.1
pkgrel=2
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'menu-cache' 'desktop-file-utils' 'libexif' 'dbus-glib')
makedepends=('udisks' 'intltool' 'gtk-doc')
replaces=('lxshortcut')
provides=('lxshortcut')
conflicts=('lxshortcut')
install=libfm.install
source=(http://downloads.sourceforge.net/pcmanfm/libfm-$pkgver.tar.xz
        0001-Fix-default-app-detection-in-App-chooser-combo-box.patch)
md5sums=('f898c480b142b56471377ef3a2810f2d'
         '10ece6d3a98f1244a82929b7c7ceba2a')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i ../0001-Fix-default-app-detection-in-App-chooser-combo-box.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-udisks \
    --with-gnu-ld
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Temporary fix to FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm
}
