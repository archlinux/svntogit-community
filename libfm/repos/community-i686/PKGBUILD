# $Id$
# Maintainer:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgbase=libfm
pkgname=(libfm libfm-extra)
pkgver=1.2.2.1
pkgrel=3
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'menu-cache' 'desktop-file-utils' 'libexif' 'dbus-glib')
makedepends=('udisks' 'intltool' 'gtk-doc')
replaces=('lxshortcut')
provides=('lxshortcut')
conflicts=('lxshortcut')
source=(http://downloads.sourceforge.net/pcmanfm/libfm-$pkgver.tar.xz
        0001-Fix-default-app-detection-in-App-chooser-combo-box.patch)
md5sums=('f898c480b142b56471377ef3a2810f2d'
         '10ece6d3a98f1244a82929b7c7ceba2a')

prepare() {
  cd $pkgbase-$pkgver

  patch -Np1 -i ../0001-Fix-default-app-detection-in-App-chooser-combo-box.patch
}

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-udisks \
    --with-gnu-ld
  make
}

package_libfm() {
  depends+=('libfm-extra')
  install=libfm.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  # Temporary fix to FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm

  # Split libfm-extra
  [[ -d $srcdir/libfm-extra ]] && rm -r "$srcdir/libfm-extra/"
  mkdir "$srcdir"/libfm-extra
  mv "$pkgdir"/usr/include/libfm/fm-{extra,version,xml-file}.h \
     "$pkgdir"/usr/lib/libfm-extra.so* \
     "$pkgdir"/usr/lib/pkgconfig/libfm-extra.pc \
     "$srcdir/libfm-extra/"
}

package_libfm-extra() {
  pkgdesc="Extra library for file management" 
  depends=('glib2')
  replaces=()
  provides=()
  conflicts=()

  cd "$srcdir"/libfm-extra
  mkdir -p "$pkgdir"/usr/{include/libfm,lib/pkgconfig}
  mv *.h "$pkgdir/usr/include/libfm"
  mv libfm-extra.so* "$pkgdir/usr/lib"
  mv libfm-extra.pc "$pkgdir/usr/lib/pkgconfig"
}
