# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=libfm
pkgver=1.1.4
pkgrel=1
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
groups=('lxde')
depends=('gtk2' 'menu-cache' 'udisks' 'desktop-file-utils' 'libexif')
makedepends=('intltool')
install=libfm.install
source=(http://downloads.sourceforge.net/pcmanfm/libfm-$pkgver.tar.xz)
md5sums=('fd3ff9174e097ab3fd619e4a10fd822a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-udisks \
    --with-gnu-ld
  make LDFLAGS+=' -lgobject-2.0 -lglib-2.0'
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Temporary fix to FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm
}
