# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=libftdi
pkgver=1.1
pkgrel=4
pkgdesc="A library to talk to FTDI chips, optional python bindings."
arch=('i686' 'x86_64')
url="http://www.intra2net.com/en/developer/libftdi/download.php"
license=('GPL2' 'LGPL2.1')
depends=('libusbx')
optdepends=('python: library bindings')
makedepends=('boost' 'cmake' 'python' 'swig')
# doxygen for docs, confuse for eeprom
source=(http://www.intra2net.com/en/developer/libftdi/download/${pkgname}1-$pkgver.tar.bz2{,.sig})
sha1sums=('f05ade5614aa31e64f91a30ce3782f7ca3704d18'
          'SKIP')

prepare() {
  cd "${pkgname}1-$pkgver"
  sed -i 's|LIB_SUFFIX 64|LIB_SUFFIX ""|' CMakeLists.txt
}

build() {
  cd "$srcdir/${pkgname}1-$pkgver"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_SKIP_BUILD_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release -DEXAMPLES=OFF -DFTDI_EEPROM=OFF
  make
}

# needs yet unknown dependencies
#check() {
#  cd "$srcdir/${pkgname}1-$pkgver/build"
#  make check
#}

package() {
  cd "${pkgname}1-$pkgver/build"
  make DESTDIR="$pkgdir" install
}

