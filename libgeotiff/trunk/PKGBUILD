# $Id$
# Maintainer: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libgeotiff
pkgver=1.2.5
pkgrel=3
pkgdesc="A TIFF based interchange format for georeferenced raster imagery"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.remotesensing.org/geotiff"
depends=('libtiff' 'proj' 'libjpeg>=7')
options=('!makeflags')
source=(ftp://ftp.remotesensing.org/pub/geotiff/libgeotiff/$pkgname-$pkgver.tar.gz)
md5sums=('000f247a88510f1b38d4b314d1e47048')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Use GCC to link the library (ld doesn't like the -Wl options we pass to it)
  # This shouldn't be required with libgeotiff 1.3 which uses Makefile.am
  sed -i 's/@LD_SHARED@/@CC@ -shared @CFLAGS@/g' Makefile.in || return 1

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/libgeotiff/LICENSE"
}
