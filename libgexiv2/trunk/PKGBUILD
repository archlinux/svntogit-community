# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.7.0
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
arch=('x86_64' 'i686')
# http://yorba.org/download/gexiv2/
url='http://redmine.yorba.org/projects/gexiv2/roadmap'
license=('GPL2')
depends=('exiv2' 'glib2')
makedepends=('python-gobject' 'python2-gobject')
source=("http://yorba.org/download/gexiv2/${pkgver%.0}/$pkgname-$pkgver.tar.xz")
sha256sums=('44ae5e74ec82a6b19e366cf6877ce34693ace85c5cb519a7158bdcfcb7e28e64')

build() {
  cd "$pkgname-$pkgver"

  # In order for the build process to include any python-related files,
  # 'import gi; print(gi._overridesdir)' must work with either python or
  # python2. The gi module is provided by python-gobject and python2-gobject.
  ./configure --prefix=/usr --enable-introspection
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
