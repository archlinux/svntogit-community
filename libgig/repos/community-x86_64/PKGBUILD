# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=libgig
pkgver=4.1.0
pkgrel=1
pkgdesc="C++ library for loading Gigasampler and DLS files"
arch=('x86_64')
url="https://www.linuxsampler.org/libgig/"
license=('GPL')
depends=('util-linux' 'libsndfile')
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('e88500f5d60d597b383093f904a2356aba7505a3a22b4852fe3fbbe71723fddbfc81029a82edf3c18e46ade519343cdbe87c06c652f982ffd4cfb4c35d5a9a0a')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # add libgig library path to ld.so.conf.d (thanks to libgig-svn for the heads-up)
  install -d "${pkgdir}/etc/ld.so.conf.d"
  echo "/usr/lib/${pkgname}" > "${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf"
}

# vim:set ts=2 sw=2 et:
