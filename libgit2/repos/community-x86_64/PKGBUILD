# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2
pkgver=0.19.0
pkgrel=1
pkgdesc="A linkable library for Git"
arch=('i686' 'x86_64')
url="http://libgit2.github.com/"
depends=(zlib openssl libssh2)
makedepends=(cmake python)
license=('GPL2')
source=("src-$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/tarball/v$pkgver")
md5sums=('df1c1e058b2d0f46b1b73dc78d551ea1')

build() {
  cd $pkgname-*
  export LANG=en_US.UTF-8
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DTHREADSAFE=ON
  make
}

check() {
  cd $pkgname-*
  make test
}

package() {
  cd $pkgname-*
  make DESTDIR="$pkgdir" install
}
