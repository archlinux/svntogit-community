# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgovirt
epoch=2
pkgver=0.3.8
pkgrel=1
pkgdesc='GObject-based library to access oVirt REST API'
arch=(x86_64)
url='https://gitlab.gnome.org/GNOME/libgovirt'
license=(LGPL)
depends=(rest)
makedepends=(gobject-introspection meson)
source=("https://gitlab.gnome.org/GNOME/libgovirt/-/archive/v${pkgver}/libgovirt-v${pkgver}.tar.bz2")
b2sums=('5b80bdcdade855695bc875f0d0b3203186856ffcebb2dc9c53161ba1ed9bc0ed48ffc30b6f3d951cfda73d398e60213ee4ccee711c64cb29cef5bca0ab53604a')

prepare() {
  cd ${pkgname}-v${pkgver}
  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
  cd ${pkgname}-v${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}-v${pkgver}
  make check
}

package() {
  cd ${pkgname}-v${pkgver}
  make DESTDIR="${pkgdir}" install
}

# use with >= 0.3.9
#
# build() {
#   arch-meson ${pkgname}-v${pkgver} build
#   meson -C build compile
# }
# 
# check() {
#   meson -C build test
# }
# 
# package() {
#   DESTDIR="${pkgdir}" meson -C build install
# }
