# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libhandy
pkgver=0.0.11
pkgrel=1
pkgdesc="Library full of GTK+ widgets for mobile phones"
arch=(x86_64)
url="https://source.puri.sm/Librem5/libhandy"
license=(LGPL2.1)
depends=(gtk3)
makedepends=(git glade gobject-introspection meson vala)
_commit=f5909a897f70143bdd2f89f47a63c1bf848330ce  # tags/v0.0.11^0
source=("git+https://source.puri.sm/Librem5/libhandy.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build -Dgtk_doc=true -Dexamples=false
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
