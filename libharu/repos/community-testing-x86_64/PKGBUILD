# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : SpepS <dreamspepser at yahoo dot it>
# Contributor: Auguste <auguste@gmail.com>
# Contributor: Jose Negron <josenj.arch@mailnull.com>

pkgname=libharu
pkgver=2.3.0
pkgrel=3
pkgdesc="C library for generating PDF documents"
url="https://github.com/libharu/libharu"
arch=('x86_64')
license=('custom:ZLIB')
depends=('libpng' 'gcc-libs')
source=("https://github.com/libharu/libharu/archive/RELEASE_${pkgver//./_}.tar.gz")
sha512sums=('ebcabf1e8488e21185a231e97b4c16f9de742ae0ac2ebc7535b72b6b8e2045c2619bc6a94a820f7f923b0dfeceb217408f4e4c613ef3602104373cdf000d3f41')

build() {
  cd "$srcdir/$pkgname-RELEASE_${pkgver//./_}"
  touch include/config.h.in
  aclocal
  libtoolize
  automake --add-missing
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-RELEASE_${pkgver//./_}"
  make DESTDIR="$pkgdir/" install
  # license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '102,120p' README > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
