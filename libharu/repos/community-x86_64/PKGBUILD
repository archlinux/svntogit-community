# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Auguste <auguste@gmail.com>
# Contributor: Jose Negron <josenj.arch@mailnull.com>

pkgname=libharu
pkgver=2.4.0
pkgrel=1
pkgdesc="C library for generating PDF documents"
arch=(x86_64)
url="https://github.com/libharu/libharu"
license=(custom:ZLIB)
depends=(libpng zlib)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('96521b6ee65c256d8d2322dc4abe8f6a93746cd3bb6ed9a1cdeb9c365acf28069f6f09dd7104196ea1cd0cc69eafbe6a6c922590e53fff7ea8e4c7d56dbc5552')

prepare() {
  sed -i "s#add_library(haru#add_library(hpdf#" ${pkgname}-${pkgver}/src/CMakeLists.txt
  sed -i "s#target_link_libraries (haru#target_link_libraries (hpdf#" ${pkgname}-${pkgver}/src/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBHPDF_STATIC=OFF
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm755 build/src/libhpdf.so "${pkgdir}"/usr/lib/libhpdf.so
  # License
  install -Dm644 ${pkgname}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  # Remove some uneeded wrongly installed files
  rm "${pkgdir}"/usr/{CHANGES,INSTALL,README.md}
  rm -r "${pkgdir}"/usr/bindings
}
