# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Auguste <auguste@gmail.com>
# Contributor: Jose Negron <josenj.arch@mailnull.com>

pkgname=libharu
pkgver=2.4.2
pkgrel=1
pkgdesc="C library for generating PDF documents"
arch=('x86_64')
url="https://github.com/libharu/libharu"
license=(custom:ZLIB)
depends=('libpng' 'zlib')
makedepends=('cmake')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('faa5c0390f22ae8bbe3dbc2b49e49b475257a70c5772e456cc34df3f98b26e39b65b17b0f982dd844b9774a4217a621337a1a8a810d7be09569a7bffe4ea1f15')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBHPDF_STATIC=OFF
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm755 build/src/libhpdf.so "${pkgdir}"/usr/lib/libhpdf.so
  install -Dm644 ${pkgname}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
