# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libhubbub
pkgver=0.3.4
pkgrel=1
pkgdesc='HTML5 compliant parsing library'
arch=('x86_64')
url='http://www.netsurf-browser.org/projects/hubbub/' # no https available
license=('MIT')
provides=('hubbub')
replaces=('hubbub')
conflicts=('hubbub')
depends=('libparserutils>=0.2.3-2')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz") # no https available
sha256sums=('e3f7e5cac0a14bf7de87782f3f6579d7e226be85de38e190ffc9e686362411ea')

prepare() {
  sed -i 's:-Werror::' "$pkgname-$pkgver/Makefile"
}

build() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE='lib-shared'
}

package() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE='lib-shared' \
    install

  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
