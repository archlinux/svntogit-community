# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=libime
pkgver=0.0.0.20180817
_commit=d612e0722a3fed5ede2138195c8b54d61dca4798
_kenlmcommit=2ad7cb56924cd3c6811c604973f592cb5ef604eb
_lm_sc_ver=20140820
_dict_ver=20170423
pkgrel=2
pkgdesc="A library to support generic input method implementation"
arch=('x86_64')
url="https://gitlab.com/fcitx/libime"
license=('GPL')
depends=('boost-libs' 'fcitx5')
makedepends=('boost' 'extra-cmake-modules' 'python')
source=(https://gitlab.com/fcitx/libime/-/archive/$_commit/libime-$_commit.tar.bz2
        kenlm-$_kenlmcommit.tar.gz::https://github.com/kpu/kenlm/archive/$_kenlmcommit.tar.gz
        https://download.fcitx-im.org/data/lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
        https://download.fcitx-im.org/data/dict.utf8-$_dict_ver.tar.xz
        https://download.fcitx-im.org/data/table.tar.gz
        fix-duplicate-cmake-target.patch::"https://gitlab.com/fcitx/libime/commit/32541593.patch")
noextract=(lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
           dict.utf8-$_dict_ver.tar.xz
           table.tar.gz)
sha512sums=('fb9564595bc05e200b054704a3ac99a4798b3d82965c3093458a034234bddb41f3e64e4e72fb4626e0a22a8e343e05e6be859d32969654d13532398e7bc508c9'
            '1fb7607ec0e0bdf7392f32cad223ceca0f89aea628654ee3cb666b961573846b72a6f4ce4449fffe0bdd6718b5fa82c2ce29604fa573d5b58eacf4e7982c717e'
            'ea3ede62eb993c4d21ca7cf339635f4686493a0cefd57df4a31f00a001e67502a13ff9612ff667b941b82b47de00be9f3c9689677d04195a7ef8716bac087519'
            '99dc0a42925e9dc341b238d3114ec9a996a4135c2795c0f58e4ba61902d3856da8cbb86449a63030a72bc2894c0ed88f22dcc64275f82137cf90ae4fb3964e05'
            '99e1d69c679355fcf3e117a1ef39b131cbd3af841387d2335757247d840b90ff6b7323e16fca98cf7a043033faf250d2e9c6d47b96845d18c278a0cf6f30fec9'
            '53c3e8e38081667ff5aa6428d29baea7bfe0c8915517e4e5dcb07fb55d7a70e0f6c150fb2442c7921b417c2826b8055705ec20a573909d40bfcd72b677da366a')

prepare() {
  cd $pkgname-$_commit/src/libime/core
  rmdir kenlm
  ln -s "$srcdir"/kenlm-$_kenlmcommit ./kenlm

  patch -p4 -i "$srcdir"/fix-duplicate-cmake-target.patch # Fix duplicate cmake target errors

  cd "$srcdir"/$pkgname-$_commit/data
  ln -s "$srcdir"/lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
  ln -s "$srcdir"/dict.utf8-$_dict_ver.tar.xz
  ln -s "$srcdir"/table.tar.gz
}

build(){
  cd $pkgname-$_commit
 
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
