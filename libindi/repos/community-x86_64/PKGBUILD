# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.9.9
pkgrel=1
pkgdesc='A distributed control protocol designed to operate astronomical instrumentation'
url='https://www.indilib.org/index.php?title=Main_Page'
license=(GPL2)
arch=(x86_64)
depends=(libnova cfitsio libjpeg gsl libtheora fftw libev rtl-sdr)
makedepends=(cmake boost qt5-base)
source=(https://github.com/indilib/indi/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('9859b22b67a368075552aef2f9cb2de9bef6da243bbcc570b3926d871d02a1bc')

prepare() {
  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i indi-$pkgver/drivers/*/*.rules
}

build() {
  cmake -B build -S indi-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINDI_BUILD_QT5_CLIENT=ON \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects -Wp,-U_GLIBCXX_ASSERTIONS"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
