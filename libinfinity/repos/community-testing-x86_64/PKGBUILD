# $Id$
# Maintainer: Baptiste Jonglez <arch at bitsofnetworks dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gregory Haynes <greg@greghaynes.net>

pkgname=libinfinity
pkgver=0.7.1
pkgrel=1
pkgdesc="A library to build collaborative text editors. Includes the infinoted server"
arch=('x86_64')
url="http://gobby.github.io/"
license=('GPL')
depends=('gnutls' 'gsasl' 'glib2' 'libxml2' 'gobject-introspection-runtime')
makedepends=('gtk-doc' 'gtk3' 'avahi' 'gobject-introspection')
optdepends=('avahi: zeroconf support'
            'gtk3: gtk support')
source=(http://releases.0x539.de/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('626ee0841bfe24f471580cd17d906dd83b973cf4f10019574adfdfc5327482cb'
            'SKIP')
validpgpkeys=('728834F3B8D552ED25CC1B1FB1C71544BF1D92C7') # Armin Burgmeier <armin@arbur.net>

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --localstatedir=/var
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "$pkgdir"/var
}
