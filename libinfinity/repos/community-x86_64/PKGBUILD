# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gregory Haynes <greg@greghaynes.net>

pkgname=libinfinity
pkgver=0.6.4
pkgrel=1
pkgdesc="An implementation of the Infininote protocol written in GObject-based C"
arch=('i686' 'x86_64')
url="http://gobby.github.io/"
license=('GPL')
depends=('gnutls>=1.7.2' 'gsasl>=0.2.21' 'glib2>=2.16' 'libxml2>=2.0' 'gtk2')
makedepends=('gtk-doc' 'pkgconfig')
optdepends=('avahi: zeroconf support'
            'gtk2: gtk support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gobby/libinfinity/archive/${pkgver}.tar.gz")
md5sums=('4a7bdac64c24ea4fe003fe43ffd4cf6d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --localstatedir=/var
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf $pkgdir/var
}
