# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libiodbc
pkgver=3.52.15
pkgrel=1
pkgdesc='Independent Open DataBase Connectivity for Linux'
arch=('x86_64')
url='http://www.iodbc.org/dataspace/doc/iodbc/wiki/iodbcWiki/'
license=('LGPL')
depends=('bash')
makedepends=('chrpath')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/openlink/iODBC/archive/v${pkgver}.tar.gz")
sha256sums=('f6b376b6dffb4807343d6d612ed527089f99869ed91bab0bbbb47fdea5ed6ace')

build() {
	cd "iODBC-${pkgver}"

	./autogen.sh
	./configure \
		--prefix=/usr \
		--disable-static \
		--includedir=/usr/include/libiodbc \
		--disable-gui \
		--disable-libodbc
	make
}

package() {
	cd "iODBC-${pkgver}"

	make DESTDIR="${pkgdir}" install

	chrpath -d "${pkgdir}"/usr/bin/iodbctest{,w}
	rm -rf "${pkgdir}"/usr/share/libiodbc/
}
