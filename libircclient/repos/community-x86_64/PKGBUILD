# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Maintainer:  SpepS <dreamspepser at yahoo dot it>
# Contributor: Marcel Wysocki <maci@satgnu.net>
# Contributor: coolkehon <coolkehon at g m a i l>

pkgname=libircclient
pkgver=1.7
pkgrel=1
pkgdesc='Small but powerful library, which implements client-server IRC protocol'
arch=('i686' 'x86_64')
url='http://www.ulduzsoft.com/libircclient/'
depends=('glibc')
makedepends=('python2-sphinx' 'python2-rst2pdf') 
license=('GPL')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('968370276f7cf21302f504f9bce1fc99')

build() {
  cd $pkgname-$pkgver

  # add fPIC flag for x86_64
  [[ "$CARCH" = "x86_64" ]] && export CFLAGS="$CFLAGS -fPIC"

  sed -e "/install/s/lib/all/" \
      -e "/DESTDIR/s/\.a/\.so/g" \
      -e "s/@\/include/&\/$pkgname/" \
      -i src/Makefile.in

  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-shared
  cd src && make

  cd ../doc
  sed -i 's/sphinx-build/sphinx-build2/g' Makefile 
  make singlehtml man
}

package() {
  cd $pkgname-$pkgver/src
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -a ../doc/_build/singlehtml/* "$pkgdir"/usr/share/doc/$pkgname
  cp -a ../doc/_build/man "$pkgdir"/usr/share

  install -d "$pkgdir"/usr/share/$pkgname/examples
  install -Dm644 ../examples/* \
    "$pkgdir"/usr/share/$pkgname/examples
}
