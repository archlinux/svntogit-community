# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Marcel Wysocki <maci@satgnu.net>
# Contributor: coolkehon <coolkehon at g m a i l>

pkgname=libircclient
pkgver=1.9
pkgrel=3
pkgdesc='Small but powerful library, which implements client-server IRC protocol'
arch=('x86_64')
url='https://www.ulduzsoft.com/linux/libircclient/'
license=('GPL')
depends=('openssl')
makedepends=('python2-sphinx' 'python2-rst2pdf') 
source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6f424791fed94ecacdd5d0b2c04472b0ddba09da08a8efb76c115a2a6abedecdf0aab3a86632bdf0fbca78781256fb591d8c94ed8eb776c5f30557d5c426bd1c')

prepare() {
  cd "${pkgname}-${pkgver}"

  # fix include dir path
  sed -i "s/@\/include/&\/${pkgname}/" src/Makefile.in

  # support openssl 1.1
  sed -e 's|SSL_library_init|OPENSSL_init_ssl|' -i configure.in
  autoreconf -vi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib \
              --enable-shared \
              --enable-openssl \
              --enable-ipv6
  cd src
  make

  cd ../doc
  make SPHINXBUILD=sphinx-build2 singlehtml man
}

package() {
  cd "${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install

  # docs
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a ../doc/_build/singlehtml/* "${pkgdir}/usr/share/doc/${pkgname}"

  # man
  install -Dm644 "../doc/_build/man/${pkgname}.1" \
    "${pkgdir}/usr/share/man/man1/${pkgname}.1"

  # examples
  install -t "${pkgdir}/usr/share/${pkgname}/examples" -Dm644 ../examples/*
}
