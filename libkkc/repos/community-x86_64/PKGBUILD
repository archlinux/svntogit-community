# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname=libkkc
pkgver=0.3.5
pkgrel=2
pkgdesc="Japanese Kana Kanji conversion library"
arch=('x86_64')
url="https://github.com/ueno/libkkc"
license=('GPL')
depends=('marisa' 'json-glib' 'libgee')
makedepends=('vala' 'gobject-introspection' 'intltool' 'python2-marisa')
optdepends=('sh: to use kkc-package-data')
source=(https://github.com/ueno/libkkc/releases/download/v$pkgver/libkkc-$pkgver.tar.gz)
sha512sums=('f611c3104a22ad38af29746870849d8937b6af2a7fee0952cc25f36ef845b5617a192df7ca63ff2a507384f7a6a13c9c1b4fa3b9a34f3f041bee8793d04b9236')

build() {
  cd "${pkgname}-${pkgver}"

  PYTHON=/usr/bin/python2 \
  ./configure --prefix=/usr --disable-static --disable-silent-rules
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m 644 README data/rules/README.rules COPYING \
    "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"  
}
