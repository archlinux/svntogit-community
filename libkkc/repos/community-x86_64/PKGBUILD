# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname=libkkc
pkgver=0.3.3
pkgrel=1
pkgdesc="Japanese Kana Kanji conversion library"
arch=('i686' 'x86_64')
url="https://bitbucket.org/libkkc"
license=('GPL')
depends=('marisa' 'json-glib' 'libgee06')
makedepends=('vala' 'gobject-introspection' 'intltool' 'python2-marisa')
optdepends=('sh: to use kkc-package-data')
source=(https://bitbucket.org/libkkc/libkkc/downloads/${pkgname}-${pkgver}.tar.gz)

build() {
  cd "${pkgname}-${pkgver}"

  PYTHON=/usr/bin/python2 \
  ./configure --prefix=/usr --disable-static --disable-silent-rules
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m 644 README data/rules/README.rules COPYING \
    "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"  
}

sha512sums=('6e3463f9a14739b2bfa87de1339a00d9a617fdbc17f412765feadc5a4bd4b9b70cacc03b5f7b656868059656ed517c8ce8786c2051117541b8a8653234e29cba')
