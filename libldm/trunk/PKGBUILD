# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chris Severance <aur.severach@spamgourmet.com>
# Contributor: Neal Buchanan <neal.buchanan@gmx.com>

pkgname=libldm
pkgver=0.2.4.r20.a856552
pkgrel=1
pkgdesc='A tool and library for managing Microsoft Windows Dynamic Disks'
arch=(x86_64)
url=https://github.com/mdbooth/libldm
license=(
  GPL3
  LGPL3
)
depends=(
  device-mapper
  glib2
  json-glib
  libreadline.so
  libuuid.so
  zlib
)
makedepends=(
  git
  gtk-doc
)
provides=(
  ldmtool
  libldm-1.0.so
)
conflicts=(ldmtool)
source=(
  git+https://github.com/mdbooth/libldm.git#commit=a856552670944b4dbb28740aa42e58ffa8434803
  ldmtool.service
)
sha256sums=(
  SKIP
  bc191523e702b9f8813d93f9e37693e6b97d9adf7eada5463c29fbf1b46c6f2a
)

pkgver() {
  cd libldm

  git describe --tags | sed 's/^libldm-//; s/-/.r/; s/-g/./'
}

build() {
  cd libldm

  ./autogen.sh \
    --prefix=/usr
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libldm install
  install -Dm 644 ldmtool.service -t "${pkgdir}"/usr/lib/systemd/system/
}

# vim: ts=2 sw=2 et:
