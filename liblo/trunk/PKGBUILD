# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=liblo
pkgver=0.31
pkgrel=2
epoch=1
pkgdesc="A lightweight OSC (Open Sound Control) implementation"
arch=(x86_64)
url="http://liblo.sourceforge.net/"
license=(LGPL2.1)
depends=(glibc)
makedepends=(doxygen)
provides=(liblo.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/radarsat1/$pkgname/archive/$pkgver.tar.gz)
sha512sums=('328d413a6608e5e99d41bfc5a153ef10510e79589cb4d8df4511c8187b8baa6fd8f8c2fd3fe54de7736855f699619d4ebc088a17acf8700085b59ee38ade7d21')
b2sums=('0dcec43175a960fbb56a0082bed64dd55dd2f40b0a1b0356222cc25ea6212109ca6626656ce6030625a3bc02b9383a8691ea163fc4e401f2c4003dbf34bc5837')

prepare() {
  cd $pkgname-$pkgver
  # fixing bug in autotools setup:
  # https://github.com/radarsat1/liblo/issues/96
  ln -svf README.md README
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # delete broken man pages
  rm -f doc/man/man3/_*.3

  # man pages
  install -vDm 644 doc/man/man3/*.3 -t "$pkgdir/usr/share/man/man3"
  # examples
  install -vDm 644 examples/*.{cpp,c} -t "$pkgdir/usr/share/$pkgname/examples/"
  # docs
  install -vDm 644 doc/html/* -t "$pkgdir/usr/share/doc/$pkgname/html/"
  install -vDm 644 doc/latex/* -t "$pkgdir/usr/share/doc/$pkgname/latex/"
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
