# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=0.9.3
pkgrel=1
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=('x86_64')
url="https://www.linuxsampler.org/"
license=('LGPL2.1')
depends=('glibc')
provides=('liblscp.so')
source=("https://download.linuxsampler.org/packages/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e776b0d48a9728e69b1a198d44ab88862366ecece293d71f6552ab395fd33bfdc395dbea6f56000c2ef9897ba00e0e287b0bc042fd6b6bdc1b4864b72b20753e')
b2sums=('a3cddd07f55522291be12f2952eafd0b9761ac8c3d38c05010d9ca6593ef9db62d9bdd7a1e8f31d74fd9cbd29cbe3ade71f3b51ffa330780a0b7feb18ff71d32')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

