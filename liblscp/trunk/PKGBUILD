# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=liblscp
pkgver=0.9.7
pkgrel=1
pkgdesc="LinuxSampler Control Protocol C++ library"
arch=(x86_64)
url="https://github.com/rncbc/liblscp"
license=(LGPL2.1)
depends=(glibc)
makedepends=(cmake)
provides=(liblscp.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/rncbc/liblscp/archive/refs/tags/${pkgname}_${pkgver//./_}.tar.gz)
sha512sums=('ac68492d6097c00254e5593a66e693e57cab8f5c2c1b4b00648c9698d950f5bd67fe53e9da41097cf28eb4d6e61596d6cfaad2b4b47ba0bc6f1f3a5851f51556')
b2sums=('c1cafb37c939ac5794142a6214c74f8f6f5552d9f9b75732d9ac6b87d9c810bf40ac1ba89a1c81f9833e86d2afc0fe614e1af46d9e85384d1c16f2d860287d09')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-${pkgname}_${pkgver//./_}
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-${pkgname}_${pkgver//./_}/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}

