# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=liblxqt
pkgver=0.10.0
pkgrel=2
pkgdesc="Common base library for LXQt components."
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("kwindowsystem" "libqtxdg")
makedepends=("cmake" "qt5-tools")
source=(
        "http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
        "http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
        "442e6dc70b57b2362b96d859752c9a7098e37dc894b0d8cb2331e38fd2868e17"
        "SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
