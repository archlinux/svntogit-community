# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Stefan Husmann <stefan-husmann at t-online dot de>
# Contributor: Alexander RÃ¸dseth <rodseth at gmail dot com>
# Contributor: William Rea <sillywilly at gmail dot com>

pkgname=libmatio
pkgver=1.5.13
pkgrel=1
pkgdesc='C library with a fortran 90/95 module interface for reading/writing MATLAB MAT-files'
arch=('x86_64')
license=('custom:BSD')
url='http://sourceforge.net/projects/matio'
depends=('zlib' 'hdf5')
options=('!emptydirs')
source=("https://downloads.sourceforge.net/matio/matio-${pkgver}.tar.gz")
changelog=ChangeLog
sha512sums=('2db430dd47b8ceed850aaadf36c0fd3b405ba32db3eb2d5be320d007754983e2538ab99338d3ae8d69e8a531818c3bde0807c1f4a505eff7dd25d4a77934b737')

build() {
  cd "matio-${pkgver}"
  ./configure --prefix=/usr --enable-shared --with-hdf5
  make
}

package() {
  cd "matio-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

