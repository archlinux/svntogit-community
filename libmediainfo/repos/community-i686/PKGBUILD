# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  hydro <hydro@freenet.de>

pkgname=libmediainfo
pkgver=0.7.51
pkgrel=1
pkgdesc="shared library for mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('GPL')
depends=( 'curl' 'libmms>=0.5-2' 'libzen>=0.4.15')
makedepends=('libtool' 'automake' 'autoconf')
options=(!libtool)
source=(http://downloads.sourceforge.net/mediainfo/${pkgname}_${pkgver}.tar.bz2
	libmediainfo-0.7.50-libmms.patch)
md5sums=('eb377eebb40d64bf980c857f36cc5b51'
         '2f4b21cc250f663d52b497dedb963e3b')

build() {
  cd $srcdir/MediaInfoLib/Project/GNU/Library
  sh ./autogen
  ./configure --prefix=/usr --enable-shared --disable-static --with-libcurl --with-libmms
  make clean
  (cd ../../.. && patch -p1 <$srcdir/libmediainfo-0.7.50-libmms.patch)
  make
}

package() {
  cd $srcdir/MediaInfoLib/Project/GNU/Library
  make DESTDIR=$pkgdir install
  for i in MediaInfo MediaInfoDLL; do
    install -dm 755 $pkgdir/usr/include/$i
    install -m 644 $srcdir/MediaInfoLib/Source/$i/*.h $pkgdir/usr/include/$i
  done
  install -dm 755 $pkgdir/usr/lib/pkgconfig
  install -m 644 $srcdir/MediaInfoLib/Project/GNU/Library/libmediainfo.pc $pkgdir/usr/lib/pkgconfig
  sed -i -e 's|Version: |Version: '$pkgver'|g' $pkgdir/usr/lib/pkgconfig/libmediainfo.pc
}
