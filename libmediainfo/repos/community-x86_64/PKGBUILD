# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libmediainfo
pkgver=21.03
pkgrel=1

pkgdesc="shared library for mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('curl' 'glib2' 'libmms' 'libzen' 'tinyxml2')

source=(
  https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz
)

sha256sums=('56b7e9abf80cba48032165cd7a46fd8d43dd63e3af35765f66c3f134caaca4ca')

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-shared \
              --disable-static \
              --with-libcurl \
              --with-libmms \
              --with-libtinyxml2

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
