# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libmediainfo
pkgver=21.09
pkgrel=1

pkgdesc="shared library for mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('curl' 'glib2' 'libmms' 'libzen' 'tinyxml2' 'graphviz')

source=(
  "https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz"
  'fix_unresolved_symbols.patch::https://patch-diff.githubusercontent.com/raw/MediaArea/MediaInfoLib/pull/1434.patch'
)

sha512sums=('a0b5ea9c6fce3f5551176d411b8c227927f579c3f09442316f6c380926acc545ea346e044f808b66403b1f0aac6f88ed4413c8231e792256d3cd7676133ef3be'
            'c374e1307ffb95ea9c23f803ddcd9be55a47600a60491a71f367c07c178225deaa83165b14f7eb2b2a6d3e5ebc923f527fec1f018537b9504943a55a29a79845')
b2sums=('ac6b32a23a82f0c5f0901ef9b6a22b850a59efab80952fcc7cbcbd28951a43d0abb7065ca19adde4715e06f1798ac2992e3f3e82c16c9cf152ff95c42f15b27d'
        '3d97e1024b6a1d5732d81ac2c9ca5d251f214a855a338c476baa2aeb98e4ebf1b932081a67fc0454a0ffacb7ab63f08deeaa0a894f5864c9eb81e332516ac411')

prepare() {
  cd MediaInfoLib

  patch -p1 -i ../fix_unresolved_symbols.patch
}

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms \
    --with-libtinyxml2 \
    --with-graphviz

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
