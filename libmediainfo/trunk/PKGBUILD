# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=libmediainfo
pkgver=22.06
pkgrel=1

pkgdesc="shared library for mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('curl' 'glib2' 'libmms' 'libzen' 'tinyxml2' 'graphviz')

source=("https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz")

sha512sums=('ae00ea32732e4a4086714d678fdd51b43bde2c478ad21b74c70e992eb70589efb48ba13cf59e53f04a1ad5b2b9cc37c7578425af1f44e9536f1b455f37139779')
b2sums=('24bbe3caadf71ff99ce51946eb8a364f6ba017be543c2d03b4f7e0d382fd14c72d12970f3e78b51376f70d0814d41add530c84d1d43c43665b1734ac11fc50ed')

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --enable-shared \
    --disable-static \
    --with-libcurl \
    --with-libmms \
    --with-libtinyxml2 \
    --with-graphviz

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
