# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=libmemcached-awesome
pkgver=1.1.3
pkgrel=1
pkgdesc="C/C++ client library and tools for the memcached server"
arch=('x86_64')
url="https://awesomized.github.io/libmemcached/"
license=('BSD')
depends=('gcc-libs' 'libsasl' 'libevent')
makedepends=('cmake' 'memcached' 'python-sphinx' 'python-sphinx_rtd_theme')
provides=('libmemcached')
conflicts=('libmemcached')
replaces=('libmemcached')
source=(https://github.com/awesomized/libmemcached/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f8aafdcd70f5912615894765645de4e2af1b057d9c3d59f3f492d8a5fc478d64')

build() {
  cmake -B build -S libmemcached-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOCS=ON \
    -DBUILD_DOCS_MANGZ=OFF \
    -DBUILD_TESTING=ON \
    -DENABLE_SASL=ON \
    -DENABLE_HASH_HSIEH=ON \
    -DENABLE_HASH_FNV64=ON \
    -DENABLE_HASH_MURMUR=ON \
    -DENABLE_MEMASLAP=ON \
    -DENABLE_OPENSSL_CRYPTO=ON
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 libmemcached-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
