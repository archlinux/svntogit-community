# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=libmemcached
pkgver=1.0.17
pkgrel=2
pkgdesc="C and C++ client library to the memcached server"
arch=('i686' 'x86_64')
url="http://libmemcached.org/"
license=('GPL')
depends=('glibc' 'libsasl' 'libevent')
makedepends=('perl' 'memcached' 'python-sphinx')
source=(http://launchpad.net/$pkgname/1.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        gcc-4.8.patch)
sha256sums=('7bb27b2589400f918df1cf5002cb01ef7ccac6e23f818604e2386de62c80bba5'
            '4a6b5d70cf7905fbd9e58a4646a6943da5f77b5e0a87b1518c4688f437963194')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build with GCC 4.8 (patch from Debian)
  patch -Np1 -i "$srcdir/gcc-4.8.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
