# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Norberto Lopes <shelika@mail.telepac.pt>
# Contributor: Kao Dome <kaodome@gmail.com>
# Contributor: Dmitrij D. Czarkoff <czarkoff@gmail.com>
# Contributor: Mathias Rohnstock <linksoft@gmx.de>

pkgname=libmicrohttpd
pkgver=0.9.12
pkgrel=2
pkgdesc="a small C library that is supposed to make it easy to run an HTTP server as part of another application."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/libmicrohttpd/"
license=('LGPL')
options=('!libtool')
depends=('gnutls')
install=libmicrohttpd.install
source=(ftp://ftp.gnu.org/gnu/libmicrohttpd/$pkgname-$pkgver.tar.gz)
md5sums=('35b000cd7d201ace2b071d53a02b2c0d')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-dependency-tracking --enable-largefile \
	--enable-curl --enable-messages --with-pic
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/include/platform.h \
	${pkgdir}/usr/include/$pkgname/platform.h
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/src/include/plibc/plibc.h \
	${pkgdir}/usr/include/$pkgname/plibc.h
  sed -i 's#Cflags: -I${includedir}#Cflags: -I${includedir} -I{includedir}/libmicrohttpd#' \
	$pkgdir/usr/lib/pkgconfig/libmicrohttpd.pc
}
