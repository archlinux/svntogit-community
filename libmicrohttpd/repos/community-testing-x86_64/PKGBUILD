# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Norberto Lopes <shelika@mail.telepac.pt>
# Contributor: Kao Dome <kaodome@gmail.com>
# Contributor: Dmitrij D. Czarkoff <czarkoff@gmail.com>
# Contributor: Mathias Rohnstock <linksoft@gmx.de>

pkgname=libmicrohttpd
pkgver=0.9.58
pkgrel=1
pkgdesc="a small C library that is supposed to make it easy to run an HTTP server as part of another application."
arch=('x86_64')
url="http://www.gnu.org/software/libmicrohttpd/"
license=('LGPL')
depends=('gnutls')
checkdepends=('curl')
validpgpkeys=('289FE99E138CF6D473A3F0CFBF7AC4A5EAC2BAF4') # Evgeny Grin (Karlson2k) <k2k@yandex.ru>
source=("https://ftp.gnu.org/gnu/libmicrohttpd/$pkgname-$pkgver.tar.gz"{,.sig})
sha256sums=('7a11e1376c62ff95bd6d2dfe6799d57ac7cdbcb32f70bfbd5e47c71f373e01f3'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-dependency-tracking \
    --disable-examples \
    --enable-curl \
    --enable-https \
    --enable-largefile \
    --enable-messages \
    --with-pic
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
