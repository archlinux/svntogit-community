# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mutantmonkey <mutantmonkey@gmail.com>

pkgname=libmilter
_pkgname=sendmail
pkgver=8.16.1
pkgrel=1
pkgdesc='Implementation of the sendmail Mail Filter API'
url='https://www.proofpoint.com/us/sendmail-open-source'
arch=('x86_64')
options=('staticlibs')
license=('custom:Sendmail')
validpgpkeys=('ADFDB709FE1EA682E5855971D583210EF51471A7')

source=("https://ftp.sendmail.org/${_pkgname}.${pkgver}.tar.gz"{,.sig}
        'fd-passing-libmilter.patch'
        'site.config.m4')
sha256sums=('7886d5dc4b436b86175f32b5b9c7305c80787749847e2909bf99123ecc4e64ba'
            'SKIP'
            '3692cb43987a3162edead6eda233e9636f86bce98e6bccf5334e1bc209359287'
            '3c8cb1448f560ba4ca51c4a93e5a8f68920ba69981109551d7e386e0e04a9b52')


prepare() {
  cd "${_pkgname}-${pkgver}"
  patch -p1 -i ../fd-passing-libmilter.patch # FS#49421
  cp ../site.config.m4 devtools/Site
}

build() {
  cd "${_pkgname}-${pkgver}/${pkgname}"
  ./Build
}

package() {
  cd "${_pkgname}-${pkgver}/${pkgname}"
  install -d "${pkgdir}/usr/lib"
  ./Build DESTDIR="${pkgdir}" install
  install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  chown -R root:root "${pkgdir}"
}
