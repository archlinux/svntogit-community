# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=libmirage
pkgver=2.1.1
pkgrel=1
pkgdesc="CD-ROM image (B6T/C2D/CCD/CDI/CIF/CUE/ISO/MDS/MDX/NRG/TOC) access library"
arch=(i686 x86_64)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(glib2 libsndfile libsamplerate zlib bzip2 xz shared-mime-info)
makedepends=(gtk-doc gobject-introspection cmake)
install=libmirage.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2
        missing-xml.patch)
sha256sums=('f606391095200df510b923201881bfbadb6ab032035ce9a67aa24fcae3145108'
            'c20e5d8d0695e3832971646eef2d735f1d12a4da767da1ff4f9c9af92ad7ad02')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np2 -i ../missing-xml.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPOST_INSTALL_HOOKS:BOOL=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
