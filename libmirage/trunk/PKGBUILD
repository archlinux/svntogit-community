# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=libmirage
pkgver=3.1.0
pkgrel=1
pkgdesc="CD-ROM image (B6T/C2D/CCD/CDI/CIF/CUE/ISO/MDS/MDX/NRG/TOC) access library"
arch=(i686 x86_64)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(glib2 libsndfile libsamplerate zlib bzip2 xz shared-mime-info)
makedepends=(gtk-doc gobject-introspection cmake intltool)
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2)
sha256sums=('b67ecc1056cf1986321d637f1a52cb36b0f5bec4fac08fd9c71075dcb7dd7363')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPOST_INSTALL_HOOKS:BOOL=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
