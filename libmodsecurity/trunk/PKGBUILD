# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libmodsecurity
pkgver=3.0.5
pkgrel=2
epoch=1

pkgdesc='ModSecurity v3 library component'
arch=('x86_64')
depends=('yajl' 'curl' 'libxml2' 'pcre' 'geoip' 'libmaxminddb' 'ssdeep' 'luajit')
makedepends=('doxygen')
url="https://github.com/SpiderLabs/ModSecurity/tree/v3/master"
license=('APACHE')
source=("https://github.com/SpiderLabs/ModSecurity/releases/download/v$pkgver/modsecurity-v$pkgver.tar.gz"{,.asc})
sha256sums=('751bf95a7a8d39c440d0c26ec1f73961550ca2eb2ac9e2e7a56dce2dd7b959e9'
            'SKIP')
validpgpkeys=('190EFACCA1E9FA466A8ECD9CE6DFB08CE8B11277') # Felipe Zimmerle da Nobrega Costa <felipe@zimmerle.org>

build() {
	cd "$srcdir"/modsecurity-v$pkgver
	./build.sh
	./configure \
		--prefix=/usr \
		--disable-examples
	make
}

package() {
	cd "$srcdir"/modsecurity-v$pkgver
	make DESTDIR="$pkgdir" install
}
