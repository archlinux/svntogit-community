# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.5.3
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('i686' 'x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl')
makedepends=('git')
checkdepends=('python-pifpaf')
checkdepends_x86_64=('mongodb')
source=("git+https://github.com/mongodb/mongo-c-driver.git#tag=$pkgver")
sha512sums=('SKIP')

build() {
    cd "$srcdir/mongo-c-driver"
    PTHREAD_LIBS=-pthread ./autogen.sh --prefix=/usr
    make
}

check() {
    cd "$srcdir/mongo-c-driver"
    pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
    cd "$srcdir/mongo-c-driver"
    make DESTDIR="$pkgdir/" install
}
