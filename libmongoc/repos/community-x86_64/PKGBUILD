# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.8.1
pkgrel=2
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl' 'snappy' 'zlib')
checkdepends=('python-pifpaf' 'mongodb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('4bc6d864d35d77cf6175a9092c0e77fcd08353fedccef1494c0ee0add4632c6055b674aa19659cabeb0454848c2654902bb213d08caebf96637d2d70023ac345')

prepare() {
  cd mongo-c-driver-$pkgver
  # Remove check for bundled libbson
  sed -i 's|src/libbson/autogen.sh|/usr/bin/true|' autogen.sh
}

build() {
  cd mongo-c-driver-$pkgver
  PTHREAD_LIBS=-pthread ./autogen.sh --prefix=/usr
  make
}

check() {
  cd mongo-c-driver-$pkgver
  pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd mongo-c-driver-$pkgver
  make DESTDIR="$pkgdir/" install
}
