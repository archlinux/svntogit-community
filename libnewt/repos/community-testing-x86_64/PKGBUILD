# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnewt
pkgver=0.52.12
pkgrel=3
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=('i686' 'x86_64')
url="http://www.redhat.com"
license=('GPL')
depends=('slang' 'popt' 'gpm')
makedepends=('tcl' 'python2')
optdepends=('tcl: whiptcl support' \
            'python2: libnewt api through _snack module')
options=('!makeflags')
source=(https://fedorahosted.org/releases/n/e/newt/newt-${pkgver}.tar.gz)
md5sums=('51b04128d9e1bf000fa769c417b74486')

build() {
	cd ${srcdir}/newt-${pkgver}

	sed -i "s:tcl8.4:tcl8.5:" Makefile.in
	sed -i "s:#!/usr/bin/python:#!/usr/bin/python2:" *.py
	sed -i "s:/usr/bin/install:/bin/install:" po/Makefile

	./configure --prefix=/usr --with-gpm-support
	make
}

package() {
	cd ${srcdir}/newt-${pkgver}

	make prefix=${pkgdir}/usr install
}
