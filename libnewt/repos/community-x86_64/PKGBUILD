# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnewt
pkgver=0.52.22
pkgrel=1
_tclver=8.6
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=(x86_64)
url='https://pagure.io/newt'
license=(GPL)
depends=(gpm libxcrypt popt slang)
makedepends=(python "tcl>=$_tclver")
optdepends=('python: libnewt support with the _snack module'
            'tcl: whiptcl support')
options=(!makeflags)
source=("https://releases.pagure.org/newt/newt-$pkgver.tar.gz")
b2sums=('f4678d306e3d6761a8ca4f26d049ca27aec5ac2a3bc2dc9127e3d9ef4703e4e9a1feb363240bc60bd87167ad213c6745c99a8db4e8e70eddc645b573fff18c45')

prepare() {
  cd newt-$pkgver
  echo '#define USE_INTERP_RESULT 1' >> config.h
  sed -i "s:tcl8.4:tcl$_tclver:" Makefile.in
}

build() {
  cd newt-$pkgver
  ./configure --prefix=/usr --with-gpm-support
  make
}

package() {
  make -C newt-$pkgver prefix="$pkgdir/usr" install
}
