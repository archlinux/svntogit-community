# $Id$
# Maintainer:
# Contributor: Essien Ita Essien <essiene@datavibe.net>
# Contributor: damir <damir@archlinux.org>

pkgname=libnids
pkgver=1.24
pkgrel=4
pkgdesc="An implementation of an E-component of Network Intrusion Detection System."
arch=('x86_64')
url="http://libnids.sourceforge.net/"
license=('GPL')
depends=('libnet' 'libpcap' 'glib2' 'libnsl')
makedepends=('pkgconfig')
validpgpkeys=('67E00C8AE6DEE3486468F6C6E20D29536F5C037F')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('314b4793e0902fbf1fdb7fb659af37a3c1306ed1aad5d1c84de6c931b351d359'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./configure --prefix=/usr --man=/usr/share/man --enable-shared
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make install_prefix="$pkgdir" install
  ln -s libnids.so.1.24 "$pkgdir"/usr/lib/libnids.so.1
}
