# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnsbmp
pkgver=0.1.0
pkgrel=1
pkgdesc='Decoding library for BMP and ICO image file formats'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsbmp/'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('fb576af6bd4d02d3626d5c2092bc06c80b2a80089a14decf40c813d9ec80ddc0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX=/usr COMPONENT_TYPE="lib-shared" 
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"   
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
