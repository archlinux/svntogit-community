# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnsbmp
pkgver=0.1.3
pkgrel=1
pkgdesc='Decoding library for BMP and ICO image file formats'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsbmp/'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('deff0a0497fc011356f901b3f75ee612721133c91b256dda28e540c803ffbb3e')

build() {
  cd "$pkgname-$pkgver"

  make INCLUDEDIR=include LIBDIR=lib PREFIX=/usr COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname-$pkgver"

  make install INCLUDEDIR=include LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
