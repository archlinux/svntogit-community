# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnsbmp
pkgver=0.1.5
pkgrel=1
pkgdesc='Decoding library for BMP and ICO image file formats'
arch=('x86_64')
url='http://www.netsurf-browser.org/projects/libnsbmp/'
license=('MIT')
makedepends=('netsurf-buildsystem>=1.7')
# no https available
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('65cc0d81727e2ece417b7997ba2451309f07a4e5932e52019a20867640152b52')

build() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname-$pkgver"

  make install \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
