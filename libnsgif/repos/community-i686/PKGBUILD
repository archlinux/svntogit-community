# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libnsgif
pkgver=0.1.3
pkgrel=1
pkgdesc='Decoding library for the GIF image file format'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsgif/'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('d3d2c9c008047eab41fc0a4ce7c280456c55c479d191e1968f843b005f219fa8')

build() {
  cd "$pkgname-$pkgver"

  CFLAGS="$CFLAGS -fno-strict-aliasing"
  make INCLUDEDIR=include LIBDIR=lib PREFIX=/usr COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname-$pkgver"

  make install INCLUDEDIR=include LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
