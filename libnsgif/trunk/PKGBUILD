# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libnsgif
pkgver=0.2.0
pkgrel=1
pkgdesc='Decoding library for the GIF image file format'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsgif/' # invalid https
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('0a8dd99f2260c645c940f15eb6527ec8116a1524813b93c069d654222a701cde')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing -w"

  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname-$pkgver"

  make install \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: netsurf-browser.org/projects/libnsgif/
# vim: ts=2 sw=2 et:
