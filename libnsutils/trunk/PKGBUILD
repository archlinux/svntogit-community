# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=libnsutils
pkgver=0.0.3
_netsurf=3.6
pkgrel=1
pkgdesc='NetSurf Utilities'
arch=('x86_64')
url='http://source.netsurf-browser.org/libnsutils.git/'
license=('MIT')
makedepends=('netsurf-buildsystem')
depends=('glibc')
source=("http://download.netsurf-browser.org/netsurf/releases/source-full/netsurf-all-$_netsurf.tar.gz")
sha256sums=('fdde93ab180222bda8e2135dbb52d7cc1b63c66eaa5da74782f1ce6d894df8b1')

build() {
  make -C "netsurf-all-$_netsurf/$pkgname" PREFIX=/usr INCLUDEDIR=include \
    LIBDIR=lib COMPONENT_TYPE="lib-shared"
}

package() {
  cd "netsurf-all-$_netsurf/$pkgname"

  make PREFIX=/usr INCLUDEDIR=include LIBDIR=lib COMPONENT_TYPE="lib-shared" \
    DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libnsutils.git
# vim:set ts=2 sw=2 et:
