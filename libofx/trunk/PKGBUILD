# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgbase=libofx
pkgname=('libofx' 'libofx-doc')
pkgver=0.9.11
pkgrel=1
pkgdesc="API for the OFX banking standard"
arch=('x86_64')
url="http://libofx.sourceforge.net"
license=('GPL')
depends=('opensp' 'curl' 'libxml++2.6')
checkdepends=('gnupg')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('b0115c876bee5df84002a96f58cca8d73ef7b75d799d6a3a44905c34a8cfe94f')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr
   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
   make CXXFLAGS+=-std=c++11
}

check() {
  cd $pkgbase-$pkgver
  make check
}

package_libofx() {
  options=('!docs')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
}

package_libofx-doc() {
  pkgdesc="Documention of the OFX banking standard API"
  depends=()

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install-docDATA
  make -C doc DESTDIR="$pkgdir" install
}
