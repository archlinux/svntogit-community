# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Maarten de Vries <maarten@de-vri.es>

pkgname=libopensmtpd
pkgver=0.6
pkgrel=2
pkgdesc="Library for creating opensmtd filters"
license=(BSD)
url="https://imperialat.at/dev/libopensmtpd/"
arch=(x86_64)
depends=(libevent)
makedepends=(gzip)
source=(https://distfiles.sigtrap.nl/${pkgname}-${pkgver}.tar.gz
        LICENSE)
sha512sums=('2204f1b35558342a214310338fb0c8d9f574697412162270c0a92a9a0a02cd5d317c63e630959525ff4ae3f8d1d3d5664dcd38442292256182926dcc8ce613f8'
            '24ffeb515e767416728adf4f02aac9b6305447ff4ce8acdfb1ecb8321f76e261e5a536b20df9acec90542e3c3f30e259c0d11cd99d401eb154fd5faf89a6e125')

build() {
  cd ${pkgname}-${pkgver}
  make -f Makefile.gnu
  gzip -kf osmtpd_run.3
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 libopensmtpd.so -t "${pkgdir}"/usr/lib 
  install -Dm644 osmtpd_run.3.gz -t "${pkgdir}"/usr/share/man/man3/
  install -Dm644 ../LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

  (
    shopt -s globstar nullglob
    for header in **/*.h; do
      dir=$(dirname ${header})
      install -Dm644 ${header} -t "${pkgdir}"/usr/include/libopensmtpd/${dir}
    done
  )
}
