# $Id$
# Maintainer: Sergej Pupykin
# Contributor: Hauke Wesselmann <hauke@h-dawg.de>
# Contributor: Laurie Clark-Michalek <bluepeppers@archlinux.us>

pkgname=libopensync
pkgver=0.39
pkgrel=4
pkgdesc='Stable version of the opensync synchronisation framework'
url='http://www.opensync.org'
license=('LGPL')
arch=('i686' 'x86_64')
makedepends=('python2' 'cmake' 'chrpath')
depends=('glib2' 'libxml2' 'sqlite' 'libxslt')
options=('!libtool')
source=(http://www.opensync.org/download/releases/$pkgver/$pkgname-$pkgver.tar.bz2)
md5sums=('733211e82b61e2aa575d149dda17d475')

build() {
  cd $srcdir/$pkgname-$pkgver

  #disable treating of warnings as errors
  find /$(pwd) -type f -exec sed -i 's/-Werror//g' '{}' \;

  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr $srcdir/$pkgname-$pkgver
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/build
  make DESTDIR=$pkgdir install
  find $pkgdir -name \*.so  -type f -exec chrpath -d {} \;
  find $pkgdir -perm /ugo+x -type f -exec chrpath -d {} \;
}
