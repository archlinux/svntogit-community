# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=liboping
pkgver=1.7.0
pkgrel=1
pkgdesc='C library to generate ICMP echo requests, better known as "ping packets"'
url='http://verplant.org/liboping/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libcap')
makedepends=('ncurses')
optdepends=('perl: perl bindings'
            'ncurses: noping CLI tool')
source=("${url}files/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('60089a544522abced4aecbb554f494632968371e5ade7bea27015cd6aa598db3')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# setting capabilities/setuid is futile in fakeroot
	# (we do that in the install script)
	sed -i 's/ install-exec-hook//g' src/Makefile.in

	./configure --prefix=/usr --with-perl-bindings='INSTALLDIRS=vendor'
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
