# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=liboping
pkgver=1.10.0
pkgrel=3
pkgdesc='C library to generate ICMP echo requests, better known as "ping packets"'
url='https://noping.cc/'
arch=('x86_64')
license=('GPL')
depends=('libcap')
makedepends=('ncurses')
optdepends=('perl: perl bindings'
            'ncurses: noping CLI tool')
source=("${url}files/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('eb38aa93f93e8ab282d97e2582fbaea88b3f889a08cbc9dbf20059c3779d5cd8')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# setting capabilities/setuid is futile in fakeroot
	# (we do that in the install script)
	sed -i 's/ install-exec-hook//g' src/Makefile.in

	./configure --prefix=/usr --with-perl-bindings='INSTALLDIRS=vendor'
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
