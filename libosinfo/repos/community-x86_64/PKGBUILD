# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libosinfo
pkgver=0.2.10
pkgrel=2
pkgdesc="GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support"
arch=('i686' 'x86_64')
url="http://libosinfo.org/"
license=('GPL' 'LGPL')
depends=('glib2' 'libxslt')
makedepends=('check' 'gobject-introspection' 'intltool' 'libsoup' 'vala')
options=('!libtool')
source=(https://fedorahosted.org/releases/l/i/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('564bd487a39dc09a10917c1d7a95f739ee7701d9cd0fbabcacea64f615e20a2d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-udev \
              --with-udev-rulesdir=/usr/lib/udev/rules.d \
              --with-usb-ids-path=/usr/share/hwdata/usb.ids \
              --with-pci-ids-path=/usr/share/hwdata/pci.ids
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
