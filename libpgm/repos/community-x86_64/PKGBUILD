# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Vladimir Kirillov <proger@wilab.org.ua>

pkgname=libpgm
pkgver=5.2.122
pkgrel=5
pkgdesc="OpenPGM: implementation of the Pragmatic General Multicast (PGM, RFC3208)"
arch=('x86_64')
url='https://www.freshports.org/net/openpgm/'
license=('LGPL2.1')
depends=('glibc')
makedepends=('python')
options=(!strip)
#source=("http://openpgm.googlecode.com/files/$pkgname-$pkgver.tar.gz")
source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/openpgm/libpgm-$pkgver.tar.gz"
        libpgm-fix-pkgconfig.patch::"https://github.com/steve-o/openpgm/commit/ee25ff3d13f2639b4c3a42125e79f77f921c3320.patch")
sha256sums=('6b895f550b95284dcde7189b01e04a9a1c1f94579af31b1eebd32c2207a1ba2c'
            '4a9fc7fbb6e73e325639a895cd19c1ac6918b575f715c057caa01f826de40114')


prepare() {
  cd "$srcdir/$pkgname-$pkgver/openpgm/pgm"
  patch -p3 -i "$srcdir"/libpgm-fix-pkgconfig.patch # Fix wrong include dir in pkgconfig file
  2to3 -wn version_generator.py
}

build() {
  cd "$srcdir/$pkgname-$pkgver/openpgm/pgm"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/openpgm/pgm"
  make prefix="$pkgdir/usr" install
}
