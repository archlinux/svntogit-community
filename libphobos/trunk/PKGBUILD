# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Dawid Ciezarkiewicz <dawid.ciezarkiewicz@jabster.pl>

pkgname=libphobos
pkgver=1.055
pkgrel=1
pkgdesc="Runtime library for the D programming language"
arch=('i686')
url="http://www.digitalmars.com/d/1.0/"
source=(http://ftp.digitalmars.com/dmd.$pkgver.zip dmd.conf)
md5sums=('24cfee48b4ad0be4ae0e6643b18ed4d0'
         'e93f0ccb1e5c00cd222af5a9be3f599a')
depends=(dmd=$pkgver)
license=('custom')
conflicts=('libtango')

build() {
  install -d "$pkgdir/usr/include/d" || return 1
  cd "$srcdir/dmd/src/phobos" || return 1
  cp -Rf std "$pkgdir/usr/include/d" || return 1
  cp -Rf etc "$pkgdir/usr/include/d" || return 1
  cp -Rf internal "$pkgdir/usr/include/d" || return 1
  cp -f {crc32,object,gcstats}.d "$pkgdir/usr/include/d" || return 1

  install -Dm644 "$srcdir/dmd/linux/lib/libphobos.a" "$pkgdir/usr/lib/libphobos.a" || return 1
  install -Dm644 "$srcdir/dmd.conf" "$pkgdir/etc/dmd.conf" || return 1

  # Get rid of this subdirectory; it's just an unpacked zlib source
  # distribution.
  rm -rf "${pkgdir}/usr/include/d/etc/c/zlib"
  # Insure that files and directories under /usr/include/d have
  # correct permissions.
  find "${pkgdir}/usr/include/d" -type d -print0 |xargs -0 chmod 755 || return 1
  find "${pkgdir}/usr/include/d" -type f -print0 |xargs -0 chmod 644 || return 1
  install -Dm644 phoboslicense.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}

