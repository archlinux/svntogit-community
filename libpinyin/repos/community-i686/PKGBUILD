# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yangtse <yangtsesu@gmail.com> 
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=1.9.91
_modelver=14
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
# Switching to kyotocabinet will break all existing installation. Requesting a migration tool to prevent data loss:
# https://github.com/libpinyin/libpinyin/issues/44
depends=('db' 'glib2') # 'kyotocabinet'
makedepends=('git')
source=("git+https://github.com/$pkgname/${pkgname}.git#tag=$pkgver"
        "http://downloads.sourceforge.net/libpinyin/model${_modelver}.text.tar.gz")
noextract=("model${_modelver}.text.tar.gz")
sha512sums=('SKIP'
            '792d3cf7ab7fdea701737cea66d18f697015f19d18b6d26273db2da0ae28291f38b3d8a5717f595da63fbe573921c552bdce10c4bec9a0834947dfec92265da9')

prepare() {
  cd ${pkgname}

  cp ../model${_modelver}.text.tar.gz data/
  sed -i "/wget.*model${_modelver}\.text\.tar\.gz/ d" data/Makefile.am
}

build() {
  cd ${pkgname}
  aclocal && libtoolize --force && autoheader && automake -a && autoconf

  # :/
  sed -i 's/AX_CXX_COMPILE_STDCXX(11)/AX_CXX_COMPILE_STDCXX_11()/' configure

  ./configure --prefix=/usr # --with-dbm=KyotoCabinet
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}
