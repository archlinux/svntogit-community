# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libqtxdg
pkgver=1.2.0
pkgrel=2
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt."
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("libsm" "libxkbcommon-x11" "qt5-base")
makedepends=("cmake")
source=("http://downloads.lxqt.org/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=("88c9b01b56fea205226b53642833963e4f27471de3b90e93e9b650ca28d098d9")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DUSE_QT5=true
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
