# $Id$
# Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>
# Maintainer: Mateusz Herych   <heniekk@gmail.com>

pkgname=libquicktime
pkgver=1.2.4
pkgrel=17
pkgdesc="A library for reading and writing quicktime files"
arch=('x86_64')
license=('GPL')
url="http://libquicktime.sourceforge.net/"
depends=('alsa-lib' 'faac' 'faad2' 'gtk2' 'lame' 'libjpeg-turbo' 'libxaw'
         'libxv' 'schroedinger'
         'libavcodec.so' 'libswscale.so' 'libvorbisenc.so' 'libvorbisfile.so'
         'libvorbis.so' 'libx264.so')
makedepends=('mesa-libgl')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        ffmpeg2.0.patch libquicktime-ffmpeg3.patch)
sha256sums=('1c53359c33b31347b4d7b00d3611463fe5e942cae3ec0fefe0d2fd413fd47368'
            'a1c8415e8e7bc04c81b946b9e3746f7d5ef24819135962280c3a7ddb3f2baa2b'
            '5513f628866de253031de2761d27c2be3965e90b70d10a494446a96d851c0729')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../ffmpeg2.0.patch
# Fix build with ffmpeg 3.0 (Gentoo)
  patch -p1 -i ../libquicktime-ffmpeg3.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --with-ffmpeg \
    --with-x264 \
    --without-doxygen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
