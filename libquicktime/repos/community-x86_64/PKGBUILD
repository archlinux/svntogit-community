# Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>
# Maintainer: Mateusz Herych   <heniekk@gmail.com>

pkgname=libquicktime
pkgver=1.2.4
pkgrel=20
pkgdesc="A library for reading and writing quicktime files"
arch=('x86_64')
license=('GPL')
url="http://libquicktime.sourceforge.net/"
depends=('alsa-lib' 'faac' 'faad2' 'gtk2' 'lame' 'libjpeg-turbo' 'libxaw'
         'libxv' 'schroedinger'
         'libavcodec.so' 'libswscale.so' 'libvorbisenc.so' 'libvorbisfile.so'
         'libvorbis.so' 'libx264.so')
makedepends=('mesa-libgl')
source=("https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
		CVE-2016-2399.patch
        libquicktime-1.2.4-ffmpeg2.patch
		libquicktime-1.2.4-ffmpeg29.patch
		libquicktime-1.2.4-ffmpeg4.patch)
sha256sums=('1c53359c33b31347b4d7b00d3611463fe5e942cae3ec0fefe0d2fd413fd47368'
            '006c6be761075ca25217fe510e1ec7cfbb6262c3d758c1bd629719072ddb0935'
            '2c7bb532ea8ac3b0e7a9a76ca6ad41127810529e7f7c2faac6f1e2f7c5c4bb93'
            'c3a8dfb6bfb69c2b54aeafe3306a8af1fff5a3a06fd05b28725d3ea7688ce2cd'
            'd4582dc2ed389acd01080690a95643277e43955adcb4058132506c5b437d044f')

prepare() {
  cd $pkgname-$pkgver

  # apply patches from gentoo https://gitweb.gentoo.org/repo/gentoo.git/tree/media-libs/libquicktime/files
  patch -p1 -i "$srcdir/CVE-2016-2399.patch"
  patch -p1 -i "$srcdir/libquicktime-1.2.4-ffmpeg2.patch"
  patch -p1 -i "$srcdir/libquicktime-1.2.4-ffmpeg29.patch"
  patch -p1 -i "$srcdir/libquicktime-1.2.4-ffmpeg4.patch"
  for FILE in lqt_ffmpeg.c video.c audio.c ; do
	  sed -i -e "s:CODEC_ID_:AV_&:g" "plugins/ffmpeg/${FILE}"
  done
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-gpl \
    --with-ffmpeg \
    --with-x264 \
    --without-doxygen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
