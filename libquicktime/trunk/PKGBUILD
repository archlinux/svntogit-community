# $Id$
# Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>
# Maintainer: Mateusz Herych   <heniekk@gmail.com>

pkgname=libquicktime
pkgver=1.2.4
pkgrel=9
pkgdesc="A library for reading and writing quicktime files"
arch=('i686' 'x86_64')
license=('GPL')
url="http://libquicktime.sourceforge.net/"
depends=('gtk2' 'ffmpeg' 'libxv' 'libxaw' 'faad2' 'faac')
makedepends=('mesa-libgl' 'x264-dev')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        ffmpeg2.0.patch)
sha256sums=('1c53359c33b31347b4d7b00d3611463fe5e942cae3ec0fefe0d2fd413fd47368'
            'a1c8415e8e7bc04c81b946b9e3746f7d5ef24819135962280c3a7ddb3f2baa2b')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../ffmpeg2.0.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
  	--enable-gpl \
	--with-ffmpeg \
	--with-x264 \
	--without-doxygen
	make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
