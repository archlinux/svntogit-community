# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexey Andreyev <aa13q@ya.ru>
# Contributor Sefa Eyeoglu <contact@scrumplex.net>

pkgname=libquotient
pkgver=0.6.7
pkgrel=1
pkgdesc='A Qt library to write cross-platform clients for Matrix'
license=(LGPL)
arch=(x86_64)
url='https://matrix.org/docs/projects/sdk/quotient'
depends=(qt5-multimedia)
makedepends=(cmake)
source=(https://github.com/quotient-im/libQuotient/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('29143c56efb24525d168281fdb751531d69529f05e7e7d1b1121a1d64a504510')

build() {
  cmake -B build -S libQuotient-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
