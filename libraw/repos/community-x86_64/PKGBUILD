# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
pkgname=libraw
_pkgbasename=LibRaw
pkgver=0.10.0
pkgrel=1
pkgdesc="A library for reading RAW files obtained from digital photo cameras (CRW/CR2, NEF, RAF, DNG, and others)"
arch=('i686' 'x86_64')
url="http://www.libraw.org/"
license=('CDDL' 'LGPL' 'custom')
depends=('gcc-libs')
source=(http://www.libraw.org/data/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('248456748c8310c99c3593bfa6bf71be')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  install -m755 -d ${pkgdir}/usr/include/
  cp -R libraw ${pkgdir}/usr/include/
  install -m755 -d ${pkgdir}/usr/lib/
  cp lib/libraw.a lib/libraw_r.a ${pkgdir}/usr/lib/
  install -m755 -d ${pkgdir}/usr/bin/
  cp bin/[a-z]* ${pkgdir}/usr/bin/
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}/
  cp COPYRIGHT LICENSE* ${pkgdir}/usr/share/licenses/${pkgname}/
}
