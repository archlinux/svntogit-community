# Maintainer: Anatol Pomozov
# Contributor: Andrew Sun <adsun701 at gmail dot com>
# Contributor: Joel Teichroeb <joel at teichroeb dot net>
# Contributor: Alim Gokkaya <alimgokkaya at gmail dot com>

pkgname=librdkafka
pkgver=1.3.0
pkgrel=1
pkgdesc='The Apache Kafka C/C++ library'
arch=(x86_64)
url='https://github.com/edenhill/librdkafka'
license=(BSD)
depends=(libsasl zstd)
makedepends=(python openssl lz4 rapidjson)
source=(librdkafka-$pkgver.tar.gz::https://github.com/edenhill/librdkafka/archive/v$pkgver.tar.gz)
sha256sums=('465cab533ebc5b9ca8d97c90ab69e0093460665ebaf38623209cf343653c76d2')

build() {
  cd librdkafka-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd librdkafka-$pkgver
  make install DESTDIR="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
