# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=libretro-beetle-psx
pkgname=('libretro-beetle-psx' 'libretro-beetle-psx-hw')
pkgver=1845
pkgrel=2
pkgdesc='Sony PlayStation core'
arch=('x86_64')
url='https://github.com/libretro/beetle-psx-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libretro-core-info')
makedepends=('git' 'libgl' 'mesa' 'vulkan-icd-loader')
_commit='a28b1bb18b46fbf28f0d1800ca9fa4c074e381dc'
source=("libretro-beetle-psx::git+https://github.com/libretro/beetle-psx-libretro.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd libretro-beetle-psx

  git rev-list --count HEAD
}

prepare() {
  cp -r libretro-beetle-psx{,-hw}
}

build() {
  make -C libretro-beetle-psx
  make HAVE_HW=1 -C libretro-beetle-psx-hw
}

package_libretro-beetle-psx() {
  cd libretro-beetle-psx

  install -Dm 644 mednafen_psx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

package_libretro-beetle-psx-hw() {
  depends+=('libgl')

  cd libretro-beetle-psx-hw

  install -Dm 644 mednafen_psx_hw_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
