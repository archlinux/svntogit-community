# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-bsnes
pkgver=1226
pkgrel=2
epoch=1
pkgdesc='Super Nintendo Entertainment System cores'
arch=(x86_64)
url=https://github.com/libretro/bsnes
license=(GPL3)
groups=(libretro)
depends=(
  gcc-libs
  libretro-core-info
)
makedepends=(git)
_commit=4ea6208ad05de7698c321db6fffea9273efc7dee
source=(
  libretro-bsnes::git+https://github.com/libretro/bsnes.git#commit=${_commit}
  libretro-bsnes-flags.patch
)
sha256sums=(
  SKIP
  3e1704ba3e7175330a0e291fbeb1e0bee18518ac29a2008d984585fb7a9b887c
)

pkgver() {
  cd libretro-bsnes

  git rev-list --count HEAD
}

prepare() {
  cd libretro-bsnes

  patch -Np1 -i ../libretro-bsnes-flags.patch
}

build() {
  make -C libretro-bsnes/bsnes target=libretro binary=library local=false platform=linux
}

package() {
  install -Dm 644 libretro-bsnes/bsnes/out/bsnes_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
