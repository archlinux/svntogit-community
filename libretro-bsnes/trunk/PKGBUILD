# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-bsnes
pkgver=499
pkgrel=1
pkgdesc='Super Nintendo Entertainment System cores'
arch=('x86_64')
url='https://github.com/libretro/bsnes-libretro'
license=('GPL3')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
_commit='026fc306b232d0987e618a39ed4ad0a50c7abcc5'
source=("libretro-bsnes::git+https://github.com/libretro/bsnes-libretro#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/bsnes_accuracy_libretro.info'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/bsnes_balanced_libretro.info'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/bsnes_performance_libretro.info')
sha256sums=('SKIP'
            '07d87c4de75611811ff1c0970ddbffcb3bd89452cc1fe80b9f777f5ed526ba2c'
            'b5917bd92bf4aeb7c6f30cf1ab31f693556319ddcb3c5c9b2d5d14e9eef34fc7'
            'e0d46da3132d1ae770115bba4e4f826a6990d01c2706bf02da7c7d5088a2077a')

pkgver() {
  cd libretro-bsnes

  git rev-list --count HEAD
}

build() {
  cd libretro-bsnes

  for p in accuracy balanced performance; do
    make profile=${p}
  done
}

package() {
  cd libretro-bsnes

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  for p in accuracy balanced performance; do
    install -m 644 out/bsnes_${p}_libretro.so "${pkgdir}"/usr/lib/libretro/
    install -m 644 ../bsnes_${p}_libretro.info "${pkgdir}"/usr/share/libretro/info/
  done
}

# vim: ts=2 sw=2 et:
