# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-citra
pkgver=6966
pkgrel=1
pkgdesc='Nintendo 3DS core'
arch=(x86_64)
url=https://github.com/libretro/citra
license=(GPL2)
groups=(libretro)
depends=(
  libretro-core-info
  sdl2
)
makedepends=(
  cmake
  git
)
_commit=81484690da4cae20feedff053a4ed537aef519de
source=(
  libretro-citra::git+https://github.com/libretro/citra.git#commit=${_commit}
  citra-boost::git+https://github.com/citra-emu/ext-boost.git
  git+https://github.com/philsquared/Catch.git
  git+https://github.com/zeromq/cppzmq.git
  git+https://github.com/kinetiknz/cubeb.git
  git+https://github.com/discordapp/discord-rpc.git
  git+https://github.com/weidai11/cryptopp.git
  git+https://github.com/MerryMage/dynarmic.git
  git+https://github.com/lsalzman/enet.git
  git+https://github.com/fmtlib/fmt.git
  git+https://github.com/svn2github/inih.git
  citra-libressl::git+https://github.com/citra-emu/ext-libressl-portable.git
  git+https://github.com/zeromq/libzmq.git
  git+https://github.com/neobrain/nihstro.git
  citra-soundtouch::git+https://github.com/citra-emu/ext-soundtouch.git
  git+https://github.com/herumi/xbyak.git
)
sha256sums=(
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
)

pkgver() {
  cd libretro-citra

  git rev-list --count HEAD
}

prepare() {
  cd libretro-citra

  for submodule in externals/{catch,cppzmq,cryptopp/cryptopp,cubeb,discord-rpc,dynarmic,enet,fmt,inih/inih,libressl,libzmq,nihstro,xbyak}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule##*/}
    git submodule update
  done
  for submodule in externals/{boost,soundtouch}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../citra-${submodule##*/}
    git submodule update
  done
}

build() {
  cmake -S libretro-citra -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_LIBRETRO=ON \
    -DENABLE_QT=OFF \
    -DENABLE_SDL2=ON \
    -DENABLE_WEB_SERVICE=OFF
  make -C build
}

package() {
  install -Dm 644 build/src/citra_libretro/citra_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
