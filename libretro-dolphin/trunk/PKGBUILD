# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-dolphin
pkgver=32984
pkgrel=1
pkgdesc='Nintendo GC/Wii core'
arch=(x86_64)
url=https://github.com/libretro/dolphin
license=(GPL2)
groups=(libretro)
depends=(
  bluez-libs
  bzip2
  enet
  gcc-libs
  glibc
  libevdev
  libfmt.so
  libgl
  libglvnd
  libhidapi-hidraw.so
  libmbedcrypto.so
  libmbedtls.so
  libmbedx509.so
  libpng
  libretro-core-info
  libudev.so
  libusb-1.0.so
  libx11
  libxi
  libxrandr
  lzo
  sfml
  xz
  zlib
  zstd
)
makedepends=(
  cmake
  git
  mesa
  python
)
_commit=71bdce82feb6e3ae4d94fed6bf5d6e2828826c1d
source=(libretro-dolphin::git+https://github.com/libretro/dolphin.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-dolphin

  git rev-list --count HEAD
}

build() {
  cmake -S libretro-dolphin -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_LTO=ON \
    -DENABLE_NOGUI=OFF \
    -DENABLE_QT=OFF \
    -DENABLE_TESTS=OFF \
    -DLIBRETRO=ON \
    -DUSE_SHARED_ENET=ON \
    -Wno-dev
  make -C build
}

package() {
  install -Dm 644 build/dolphin_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
