# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-flycast
pkgver=4087
pkgrel=1
pkgdesc='Sega Dreamcast core'
arch=(x86_64)
url=https://github.com/libretro/flycast
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
)
makedepends=(
  git
  mesa
)
source=(
  libretro-flycast::git+https://github.com/libretro/flycast.git#commit=
  libretro-flycast-flags.patch
)
sha256sums=(
  SKIP
  051f6fda81d816a8f8c58748565ba4db73a18d0d788e520dc343839471606709
)

pkgver() {
  cd libretro-flycast

  git rev-list --count HEAD
}

prepare() {
  cd libretro-flycast

  patch -Np1 -i ../libretro-flycast-flags.patch
}

build() {
  make HAVE_OIT=1 -C libretro-flycast
}

package() {
  install -Dm 644 libretro-flycast/flycast_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
