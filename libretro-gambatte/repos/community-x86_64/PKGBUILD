# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>

pkgname=libretro-gambatte
pkgver=797
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=(x86_64)
url='https://github.com/libretro/gambatte-libretro'
license=(GPL2)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
source=(libretro-gambatte::git+https://github.com/libretro/gambatte-libretro.git#commit=a67455ea3f4245b569d2b3c8ca23fe0fe24377c2)
sha256sums=(SKIP)

pkgver() {
  cd libretro-gambatte

  git rev-list --count HEAD
}

build() {
  cd libretro-gambatte

  make -f Makefile.libretro
}

package() {
  cd libretro-gambatte

  install -Dm 644 gambatte_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
