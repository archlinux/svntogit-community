# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>

pkgname=libretro-gambatte
pkgver=635
pkgrel=1
pkgdesc='Nintendo Game Boy/Game Boy Color core'
arch=('x86_64')
url='https://github.com/libretro/gambatte-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
_commit='b1af7340b3a2fdf427fc607672026da07ddf8c5b'
source=("libretro-gambatte::git+https://github.com/libretro/gambatte-libretro.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/gambatte_libretro.info')
sha256sums=('SKIP'
            '4e8d6d21f9baec7df47a5057781eaa7f14c020463b6efb10d1ef9d496a22419b')

pkgver() {
  cd libretro-gambatte

  git rev-list --count HEAD
}

build() {
  cd libretro-gambatte

  make -f Makefile.libretro
}

package() {
  cd libretro-gambatte

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 gambatte_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../gambatte_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
