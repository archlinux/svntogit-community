# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-kronos
pkgver=7016
pkgrel=2
pkgdesc='Sega Saturn core'
arch=(x86_64)
url=https://github.com/libretro/yabause
license=(GPL2)
groups=(libretro)
depends=(
  glibc
  libgl
  libretro-core-info
)
makedepends=(
  git
  libglvnd
)
_commit=146f4295eb7f5f76a2e6e6c84518c9bdf6a8398f
source=(libretro-yabause::git+https://github.com/libretro/yabause.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-yabause

  git rev-list --count HEAD
}

build() {
  make -C libretro-yabause/yabause/src/libretro
}

package() {
  install -Dm 644 libretro-yabause/yabause/src/libretro/kronos_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
