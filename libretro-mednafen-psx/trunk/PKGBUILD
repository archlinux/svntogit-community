# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-mednafen-psx
pkgver=1011
pkgrel=1
pkgdesc='Sony PlayStation core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/mednafen-psx-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
source=('libretro-mednafen-psx::git+https://github.com/libretro/beetle-psx-libretro.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mednafen_psx_libretro.info')
sha256sums=('SKIP'
            '8517d3ddd709d6ffec1b41b2d05d91d0f60ba434ac2b029d834db405960e0a7d')

pkgver() {
  cd libretro-mednafen-psx

  git rev-list --count HEAD
}

build() {
  cd libretro-mednafen-psx

  make
}

package() {
  cd libretro-mednafen-psx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mednafen_psx_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mednafen_psx_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
