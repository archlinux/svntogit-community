# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-melonds
pkgver=2002
pkgrel=1
pkgdesc='Nintendo DS core'
arch=(x86_64)
url=https://github.com/libretro/melonDS
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
)
makedepends=(
  git
  libglvnd
)
_commit=5e52c245fb38cabe881fbfa6513280ee44fc5bd8
source=(libretro-melonds::git+https://github.com/libretro/melonDS.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-melonds

  git rev-list --count HEAD
}

build() {
  export LDFLAGS="$LDFLAGS -lpthread"

  make -C libretro-melonds
}

package() {
  install -Dm 644 libretro-melonds/melonds_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
