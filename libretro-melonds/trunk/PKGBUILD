# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-melonds
pkgver=1994
pkgrel=1
pkgdesc='Nintendo DS core'
arch=(x86_64)
url=https://github.com/libretro/melonDS
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
)
makedepends=(
  git
  libglvnd
)
_commit=490a66a5834e23304addc9b16a2f95da6db9f061
source=(libretro-melonds::git+https://github.com/libretro/melonDS.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-melonds

  git rev-list --count HEAD
}

build() {
  export LDFLAGS="$LDFLAGS -lpthread"

  make -C libretro-melonds
}

package() {
  install -Dm 644 libretro-melonds/melonds_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
