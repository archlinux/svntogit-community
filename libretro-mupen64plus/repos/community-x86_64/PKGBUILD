# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-mupen64plus
pkgver=4029
pkgrel=1
pkgdesc='Nintendo 64 core'
arch=('x86_64')
url='https://github.com/libretro/mupen64plus-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libgl')
makedepends=('git' 'mesa')
source=('libretro-mupen64plus::git+https://github.com/libretro/mupen64plus-libretro.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mupen64plus_libretro.info')
sha256sums=('SKIP'
            '94f2a7c177e315d330c206b93e9575e9a6b53f3eed933fac2c6b22df23276f60')

pkgver() {
  cd libretro-mupen64plus

  git rev-list --count HEAD
}

build() {
  cd libretro-mupen64plus

  [[ $CARCH = 'i686' ]] && make WITH_DYNAREC='x86'
  [[ $CARCH = 'x86_64' ]] && make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-mupen64plus

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mupen64plus_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mupen64plus_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
