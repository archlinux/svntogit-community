# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-parallel-n64
pkgver=5142
pkgrel=1
pkgdesc='Nintendo 64 core'
arch=(x86_64)
url=https://github.com/libretro/parallel-n64
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
)
makedepends=(
  git
  mesa
)
source=(libretro-parallel-n64::git+https://github.com/libretro/parallel-n64.git#commit=f8f35c70dd33d529a528bc573c036c4b9400693d)
sha256sums=(SKIP)

pkgver() {
  cd libretro-parallel-n64

  git rev-list --count HEAD
}

build() {
  make -C libretro-parallel-n64 WITH_DYNAREC=x86_64 HAVE_PARALLEL_RSP=1 HAVE_THR_AL=1
}

package() {
  install -Dm 644 libretro-parallel-n64/parallel_n64_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
