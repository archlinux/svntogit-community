# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-play
pkgver=5484
pkgrel=1
pkgdesc='Sony PlayStation 2 core'
arch=(x86_64)
url=https://github.com/jpd002/Play-
license=(MIT)
groups=(libretro)
depends=(
  bzip2
  glew
  glibc
  icu
  libgl
  libretro-core-info
  zlib
)
makedepends=(
  cmake
  git
  libglvnd
)
source=(
  libretro-play::git+https://github.com/jpd002/Play-.git#commit=d2245933a899b9c66f4ee6ded288f34165ab753e
  play-CodeGen::git+https://github.com/jpd002/Play--CodeGen.git
  play-Dependencies::git+https://github.com/jpd002/Play-Dependencies.git
  play-Framework::git+https://github.com/jpd002/Play--Framework.git
  play-Nuanceur::git+https://github.com/jpd002/Nuanceur.git
  git+https://github.com/SDWebImage/SDWebImage.git
  play-boost-cmake::git+https://github.com/jpd002/boost-cmake.git
  git+https://github.com/gulrak/filesystem.git
)
sha256sums=(
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
  SKIP
)

pkgver() {
  cd libretro-play

  git rev-list --count HEAD
}

prepare() {
  cd libretro-play

  for submodule in deps/{CodeGen,Dependencies,Framework,Nuanceur}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../play-${submodule#*/}
    git submodule update ${submodule}
  done

  cd deps/Dependencies

  git submodule init SDWebImage
  git config submodule.SDWebImage.url ../../../SDWebImage
  git submodule update SDWebImage

  git submodule init boost-cmake
  git config submodule.boost-cmake.url ../../../play-boost-cmake
  git submodule update boost-cmake

  git submodule init ghc_filesystem
  git config submodule.ghc_filesystem.url ../../../filesystem
  git submodule update ghc_filesystem
}

build() {
  cmake -S libretro-play -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_LIBRETRO_CORE=ON \
    -DBUILD_PLAY=OFF \
    -DBUILD_TESTS=OFF \
    -DENABLE_AMAZON_S3=OFF
  make -C build
}

package() {
  install -Dm 644 build/Source/ui_libretro/play_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-play/License.txt -t "${pkgdir}"/usr/share/licenses/libretro-play/
}

# vim: ts=2 sw=2 et:
