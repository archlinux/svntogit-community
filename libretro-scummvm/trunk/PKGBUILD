# $Id$
# Maintainer: Maxime Gauduin <alucryd@Ã¦rchlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-scummvm
pkgver=78548
pkgrel=1
pkgdesc='ScummVM core'
arch=('x86_64')
url='https://github.com/libretro/scummvm'
license=('GPL')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
_commit='011f40ad0aa475c149d2fa76c86dcc248a8642ba'
source=("libretro-scummvm::git+https://github.com/libretro/scummvm.git#commit=${_commit}"
        'https://raw.github.com/libretro/libretro-super/master/dist/info/scummvm_libretro.info')
sha256sums=('SKIP'
            '677aa9aa4a0cadb4e1d8dba21889b8b96266530a6ea9aa55c64e6b1d77bb0e90')

pkgver() {
  cd libretro-scummvm

  git rev-list --count HEAD
}

build() {
  cd libretro-scummvm/backends/platform/libretro/build

  make
}

package() {
  cd libretro-scummvm/backends/platform/libretro/build

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 scummvm_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 "${srcdir}"/scummvm_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
