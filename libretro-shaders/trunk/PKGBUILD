# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-shaders
pkgver=587
pkgrel=1
pkgdesc='Collection of shaders for libretro'
arch=('any')
url='http://www.libretro.com/'
license=('GPL')
groups=('libretro')
makedepends=('git' 'nvidia-cg-toolkit' 'python' 'retroarch')
source=('libretro-shaders::git+https://github.com/libretro/common-shaders.git')
sha256sums=('SKIP')

pkgver() {
  cd libretro-shaders

  git rev-list --count HEAD
}

build() {
  retroarch-cg2glsl libretro-shaders shaders
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/libretro
  cp -dr --no-preserve='ownership' shaders "${pkgdir}"/usr/share/libretro/
}

# vim: ts=2 sw=2 et:
