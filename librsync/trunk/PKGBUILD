# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Christoph 'delmonico' Neuroth <delmonico@gmx.net>

pkgname=librsync
pkgver=0.9.7
pkgrel=6
pkgdesc='Free software library that implements the rsync remote-delta algorithm (rdiff)'
arch=('i686' 'x86_64')
url="http://librsync.sourceforge.net/"
license=('GPL')
depends=('popt' 'zlib' 'bzip2')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/librsync/$pkgname-$pkgver.tar.gz
        lfs-overflow.patch)
sha256sums=('6633e4605662763a03bb6388529cbdfd3b11a9ec55b8845351c1bd9a92bc41d6'
            'bda94f0aa550498673a459326656798b9f327c8687fa924415cf7b08e8fd2f38')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Patch for large files (https://bugzilla.redhat.com/show_bug.cgi?id=207940)
  patch -Np1 -i "$srcdir/lfs-overflow.patch"

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-shared
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
