# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Christoph 'delmonico' Neuroth <delmonico@gmx.net>

pkgname=librsync
pkgver=2.3.2
pkgrel=1
epoch=1
pkgdesc="Library for calculating and applying network deltas"
arch=('x86_64')
url="http://librsync.sourcefrog.net/"
license=('LGPL')
depends=('popt')
makedepends=('cmake')
provides=('librsync.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('ef8ce23df38d5076d25510baa2cabedffbe0af460d887d86c2413a1c2b0c676f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver/build"

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    ..
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
