# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=libseat
pkgver=0.5.0
pkgrel=2
pkgdesc="A minimal seat management daemon, and a universal seat management library"
arch=(x86_64)
url="https://sr.ht/~kennylevinsen/seatd/"
license=('MIT')
makedepends=(
    'meson'
    'ninja'
    'scdoc'
    'systemd'
)
depends=(
    'systemd-libs'
)
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~kennylevinsen/seatd/archive/$pkgver.tar.gz")
sha256sums=('274b56324fc81ca6002bc1cdd387668dee34a6e1063e5f3896805c3770948988')

build() {
    mkdir build
    meson --prefix=/usr \
          --buildtype=plain \
          -Dexamples="disabled" \
          -Dlogind="enabled" \
          -Dman-pages="enabled" \
          "seatd-$pkgver" \
          build
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "seatd-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
