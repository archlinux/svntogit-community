# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=2.2.1
pkgrel=1
pkgdesc='C++ library for semigroups and monoids'
url='https://libsemigroups.github.io'
arch=(x86_64)
license=(GPL3)
depends=(fmt)
makedepends=(eigen)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ba914af778c93ccf2ab2ce24954a45904da8bbfa345e3d6aea984cf3a773f203')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-fmt \
              --with-external-fmt \
              --with-external-eigen
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
