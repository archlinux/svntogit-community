# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libsieve
pkgver=2.3.1
pkgrel=3
pkgdesc="sieve interpretator lib"
arch=(x86_64)
url="http://libsieve.sourceforge.net/"
license=('GPL')
source=("https://github.com/downloads/sodabrew/libsieve/libsieve-$pkgver.tar.gz"
	"glibc-regex.patch"
	"crash-fix.patch")
md5sums=('bb48eecf877cc7fdb2b0dfe757b48d7a'
         '0d98d9e8f0583857616b2ba98f1833fa'
         '05ce47217ced05fd32a3ae41dc37f07e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  rm -rf src/sv_regex
  rm -f src/sv_parser/{addr,header,sieve}.{c,h}
  rm -f src/sv_parser/{addr,header,sieve}-lex.{c,h}
  patch -p1 <"$srcdir"/glibc-regex.patch
  patch -p1 <"$srcdir"/crash-fix.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  aclocal
  automake
  autoconf
#  export CFLAGS="-g -O2"
#  export LDFLAGS="-g -O2"
  ./configure --prefix=/usr
  (cd src/sv_parser && for i in `ls -1 *.l | sed 's|\.l||'`; do flex --header-file=$i.h -o $i.c $i.l; done)
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver/src/sv_test"
  export LD_LIBRARY_PATH="$srcdir"/libsieve-$pkgver/src/.libs:$LD_LIBRARY_PATH
  ls -1 script* | while read script; do
    ls -1 message* | while read msg; do
      if [ "$script" == "script22.sv" ]; then
        # script22.sv: syntax errors check
        msg "checking !$script $msg"
        ./example $script $msg && false || true
      else
        if [ "$script" == "script23.sv" ]; then
          # script23.sv: enotify looks unsupported or I don't know how to use it - skip it
          msg "skipping $script $msg"
          true
        else
          msg "checking $script $msg"
          ./example $script $msg
        fi
      fi
    done
  done
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
