# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: namelessjon <jonathan.stott@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=libsodium
pkgver=0.6.0
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library"
arch=('i686' 'x86_64')
url="https://github.com/jedisct1/libsodium"
license=('custom:ISC')
depends=('glibc')
makedepends=('clang')
source=(http://download.dnscrypt.org/$pkgname/releases/$pkgname-${pkgver}.tar.gz)

build() {
  cd "$pkgname-$pkgver"
  export CC=clang
  CFLAGS=${CFLAGS/-fstack-protector-strong/}

  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -d -m 755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
sha512sums=('3a47468d3db1447fcb223de330ee3ffd11394ecd31440bfd2d89999003bdf01f85c0644b6075d73d670bf94ab9ea2162417a0dcd290ad2a9a46ae76cd467fc85')
