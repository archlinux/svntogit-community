# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: namelessjon <jonathan.stott@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=libsodium
pkgver=0.7.0
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library"
arch=('i686' 'x86_64')
url="https://github.com/jedisct1/libsodium"
license=('custom:ISC')
depends=('glibc')
#makedepends=('clang')
source=("http://download.dnscrypt.org/$pkgname/releases/$pkgname-${pkgver}.tar.gz")
sha512sums=('3b1a976ac83e2a740cc462f0589abcfe85604357e601f976b3d7f9d2aec7fca4705871f0fb937d6f9321102a5cc80b0cee6ec9c7864ce9d2c4735e20885b39ff')

build() {
  cd "$pkgname-$pkgver"
  #export CC=clang
  #CFLAGS=${CFLAGS/-fstack-protector-strong/}

  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -d -m 755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
