# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: namelessjon <jonathan.stott@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=libsodium
pkgver=0.5.0
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl-based crypto library"
arch=('i686' 'x86_64')
url="https://github.com/jedisct1/libsodium"
license=('custom:ISC')
depends=('glibc')
makedepends=('clang')
source=(http://download.dnscrypt.org/$pkgname/releases/$pkgname-${pkgver}.tar.gz)

build() {
  cd "$pkgname-$pkgver"
  export CC=clang
  CFLAGS=${CFLAGS/-fstack-protector-strong/}

  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -d -m 755 "$pkgdir/usr/share/licenses/$pkgname"
  install -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
sha512sums=('6a483a4699daaa9f047fd362b405b21eb058c44e240f0f910fbd2167239f51bc092e807f875fc971a1e70decc6fa861b6904bef86156b27790960aba88ce253a')
