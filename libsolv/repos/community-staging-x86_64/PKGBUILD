# Contributor: larchunix
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=libsolv
pkgver=0.7.23
pkgrel=2
pkgdesc="Library for solving packages and reading repositories"
arch=('x86_64')
url="https://github.com/openSUSE/libsolv"
license=('custom:BSD')
depends=('bzip2' 'expat' 'rpm-tools' 'xz' 'zlib' 'zstd')
makedepends=('cmake' 'perl' 'python' 'ruby' 'swig')
optdepends=('perl: for perl bindings'
            'python: for python bindings'
            'ruby: for ruby bindings')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('0286155964373c6cc3802d025750786c3ee79608d5cb884598e110e3918bb2fe')

build() {
  cd "$pkgname-$pkgver"

  cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DUSE_VENDORDIRS=ON \
        -DFEDORA=1 \
        -DENABLE_APPDATA=ON \
        -DENABLE_ARCHREPO=ON \
        -DENABLE_BZIP2_COMPRESSION=ON \
        -DENABLE_COMPLEX_DEPS=1 \
        -DENABLE_COMPS=ON \
        -DENABLE_CONDA=ON \
        -DENABLE_CUDFREPO=ON \
        -DENABLE_DEBIAN=ON \
        -DENABLE_HAIKU=OFF \
        -DENABLE_HELIXREPO=ON \
        -DENABLE_LZMA_COMPRESSION=ON \
        -DENABLE_MDKREPO=ON \
        -DENABLE_PERL=ON \
        -DENABLE_PUBKEY=ON \
        -DENABLE_PYTHON=ON \
        -DENABLE_RPMDB=ON \
        -DENABLE_RPMDB_BYRPMHEADER=ON \
        -DENABLE_RPMDB_LIBRPM=ON \
        -DENABLE_RPMMD=ON \
        -DENABLE_RPMPKG=ON \
        -DENABLE_RUBY=ON \
        -DENABLE_SUSEREPO=ON \
        -DENABLE_TCL=OFF \
        -DENABLE_ZSTD_COMPRESSION=ON \
        -DMULTI_SEMANTICS=ON

  make -C build
}

check() {
  cd "$pkgname-$pkgver"

  make -C build ARGS="-V" test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install

  install -Dp -m644 LICENSE.BSD "$pkgdir/usr/share/licenses/$pkgname/LICENSE.BSD"
  install -Dp -m644 README      "$pkgdir/usr/share/doc/$pkgname/README"
}
