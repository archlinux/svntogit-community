# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Brian Galey <bkgaley at gmail dot com>
# Contributor: Pietro Zambelli <peter.zamb at gmail dot com>

pkgname=libspatialite
pkgver=5.0.1
pkgrel=1
pkgdesc="SQLite extension to support spatial data types and operations"
arch=('x86_64')
url="https://www.gaia-gis.it/fossil/libspatialite"
license=('MPL' 'GPL' 'LGPL')
depends=('geos' 'libfreexl' 'librttopo' 'libxml2' 'minizip' 'proj' 'sqlite')
makedepends=('libtiff')
source=(https://www.gaia-gis.it/gaia-sins/$pkgname-$pkgver.tar.gz)
sha256sums=('eecbc94311c78012d059ebc0fae86ea5ef6eecb13303e6e82b3753c1b3409e98')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
