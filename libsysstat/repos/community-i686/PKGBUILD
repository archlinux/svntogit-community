# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libsysstat
pkgver=0.3.1
pkgrel=3
pkgdesc="Library to query system statistics (net, resource usage, ...)"
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("qt5-base")
makedepends=("cmake" "qt5-tools")
source=(
	"https://downloads.lxqt.org/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://downloads.lxqt.org/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"88e1031db659427d6549723d13317708e210cc833042c205ebfd447183b31648"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
