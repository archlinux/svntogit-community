# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=libtango
pkgver=0.99.9
pkgrel=1
pkgdesc="Runtime library for the D programming language"
arch=('i686')
url="http://dsource.org/projects/tango"
source=(http://downloads.dsource.org/projects/tango/$pkgver/tango-$pkgver-src.tar.gz
        tango-headers.patch
        dmd.conf)
depends=('dmd')
license=('custom')
conflicts=('libphobos')
options=('!makeflags')

build() {
  cd "$srcdir/tango-$pkgver"

  # This patch brings one of our header files up-to-date with the one
  # included in the dmd + tango binary bundle distributed by upstream.
  # That bundle was built after this source was released.
  patch -p1 < "$srcdir/tango-headers.patch" || return 1

  cd build/script

  export MAKEFLAGS=-j1
  ./bob.sh --runtime --user dmd || return 1

  # I'm leary of that script.  The README.txt file in build/script
  # warns that the contents of this directory are for internal use only.
  # But the library builds successfully.  I tested several of the
  # example programs, and they're fine.

  install -dm755 "$pkgdir/usr/lib" "$pkgdir/usr/include/d" || return 1
  install -m644 "$srcdir/tango-$pkgver/libtango.a" "$pkgdir/usr/lib/libtango-dmd.a" || return 1
  install -m644 "$srcdir/tango-$pkgver/object.di" "$pkgdir/usr/include/d/object.di" || return 1
  cp -Rf "$srcdir/tango-$pkgver/tango" "$pkgdir/usr/include/d" || return 1
  # This subdirectory isn't part of the binary distribution,
  # so we don't need it.
  rm -rf "$pkgdir/usr/include/d/tango/core/rt"

  install -Dm644 "$srcdir/dmd.conf" "$pkgdir/etc/dmd.conf" || return 1
  install -Dm644 "$srcdir/tango-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt" || return 1
}
md5sums=('3be02f042c59a8b1e2c55394945d2ce1'
         '7c03558a8a6975198c4d73375bc8f105'
         '99eaa2affc06ec0867bcd9535587e1a6')
