# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: suasageandeggs <s_stoakley@hotmail.co.uk>
# Contributor: Michael Fellinger <manveru@www.weez-int.com>

pkgname=libtommath
pkgver=0.42.0
pkgrel=4
pkgdesc="Highly optimized and portable routines for integer based number theoretic applications"
url="http://www.libtom.org/"
license=('custom')
arch=('i686' 'x86_64')
depends=('glibc')
options=(staticlibs)
source=("http://libtom.org/files/ltm-${pkgver}.tar.bz2"
        'libtommath-0.42.0-makefile.patch')
md5sums=('7380da904b020301be7045cb3a89039b'
         'ff127effd4418073a16ffb5df45a1bcb')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np1 -i ${srcdir}/libtommath-0.42.0-makefile.patch

  make -f makefile.shared IGNORE_SPEED=1
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make -f makefile.shared DESTDIR=$pkgdir INSTALL_GROUP=root install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  chmod 644 ${pkgdir}/usr/lib/libtommath.a
}
