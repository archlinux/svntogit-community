# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuild with each new gcc version

pkgname=libtool-multilib
pkgver=2.4.4
_gccver=4.9.2
pkgrel=2
pkgdesc="A generic library support script for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
groups=('multilib-devel')
depends=('sh' 'tar' "gcc-multilib=${_gccver}")
makedepends=("gcc-multilib=${_gccver}")
provides=("lib32-libltdl" "lib32-libtool" "libltdl=$pkgver" "libtool=$pkgver-$pkgrel")
conflicts=(lib32-libltdl lib32-libtool libltdl libtool)
replaces=('lib32-libltdl' 'lib32-libtool')
install=libtool.install
source=(ftp://ftp.gnu.org/pub/gnu/libtool/libtool-${pkgver}.tar.xz{,.sig})
md5sums=('51bf400de3354687d68dfa2392506b7e'
         'SKIP')
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')

prepare() {
  mv libtool-$pkgver libtool-64
  cp -a libtool-64 libtool-32
}

build() {
  ( msg2 "Building libtool-64..."
    cd libtool-64
    ./configure --prefix=/usr
    make
  )

  ( msg2 "Building libtool-32..."
    export CC="gcc -m32"
    export CXX="g++ -m32"

    cd libtool-32
    ./configure --prefix=/usr --libdir=/usr/lib32
    make
  )
}

check() {
  cd libtool-64
  make -k check
  cd ../libtool-32
  make -k check
}

package() {
  cd libtool-64
  make DESTDIR=${pkgdir} install
  cd ../libtool-32
  make DESTDIR=${pkgdir} install-libLTLIBRARIES
}
