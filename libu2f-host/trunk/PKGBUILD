# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=libu2f-host
pkgver=1.1.10
pkgrel=2
pkgdesc="Yubico Universal 2nd Factor (U2F) Host C Library"
arch=('x86_64')
url='https://github.com/Yubico/libu2f-host'
license=('BSD')
depends=('json-c' 'hidapi')
makedepends=('git' 'help2man' 'gengetopt')
validpgpkeys=('0A3B0262BCA1705307D5FF06BCA00FD4B2168C0A') # Klas Lindfors <klas@yubico.com>
source=("https://developers.yubico.com/${pkgname}/Releases/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('4265789ec59555a1f383ea2d75da085f78ee4cf1cd7c44a2b38662de02dd316f'
            'SKIP')

prepare() {
	cd "${pkgname}-${pkgver}"

	sed -i 's/, GROUP="plugdev", MODE="0660"//' 70-u2f.rules
}

build() {
	cd "${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--disable-gtk-doc \
		--with-udevrulesdir=/usr/lib/udev/rules.d/
	make
}


check() {
	cd "${pkgname}-${pkgver}"

	make check
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" install

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

