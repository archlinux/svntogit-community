# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=libuhd
pkgver=3.5.4
_verstring=003_005_004
pkgrel=3
pkgdesc="Universal Software Radio Peripheral (USRP) userspace driver"
arch=('x86_64' 'i686')
url="http://code.ettus.com/redmine/ettus/projects/uhd/wiki"
license=('GPL')
depends=('boost-libs' 'orc' 'libusbx')
makedepends=('cmake' 'boost' 'python2-cheetah')

source=("libuhd-$pkgver.tar.gz::https://codeload.github.com/EttusResearch/uhd/tar.gz/release_$_verstring")
md5sums=('e90c95483a52b75cacc85f4fb0ef63ed')

build() {
  cd "$srcdir/uhd-release_$_verstring/host"
  # fix for py2
  find -name "*.py" | xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr/ \
           -DPYTHON_EXECUTABLE=/usr/bin/python2 \
           -DENABLE_EXAMPLES=OFF \
           -DENABLE_UTILS=ON \
           -DENABLE_TESTS=OFF \
           -DENABLE_E100=ON
  # e100 is disabled by default for now, check that on future releases
  make
}

check() {
  cd "$srcdir/uhd-release_$_verstring/host/build"
  make test
}

package() {
  cd "$srcdir/uhd-release_$_verstring/host/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$pkgdir/usr/share/uhd/utils/uhd-usrp.rules" "$pkgdir/usr/lib/udev/rules.d/10-uhd-usrp.rules"
} 
