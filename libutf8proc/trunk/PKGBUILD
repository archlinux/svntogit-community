# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=libutf8proc
pkgver=2.5.0
_sover=2.3.2
pkgrel=2
pkgdesc='C library for processing UTF-8 encoded Unicode strings'
arch=(x86_64)
url='https://github.com/JuliaStrings/utf8proc'
license=(custom)
makedepends=(cmake git ninja)
# tag: v2.5.0
source=("git+$url#commit=0890a538bf8238cded9be0c81171f57e43f2c755"
        libutf8proc.pc.in)
sha256sums=('SKIP'
            '0fc49af11c4e77675d307bcee442c3582db805f7c17c32284d0620131b1007d1')

prepare() {
  sed "s#@VERSION@#$pkgver#" libutf8proc.pc.in > libutf8proc.pc
}

build() {
  mkdir -p build
  cd build
  cmake ../utf8proc \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -G Ninja
  ninja
}

package() {
  # The install command does not work for libutf8proc
  #DESTDIR="$pkgdir" ninja -C $pkgname-$pkgver/build install

  cd utf8proc
  install -Dm644 utf8proc.h "$pkgdir/usr/include/utf8proc.h"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  install -Dm644 "$srcdir/libutf8proc.pc" \
    "$pkgdir/usr/lib/pkgconfig/libutf8proc.pc"
  install -Dm644 ../build/libutf8proc.so.$_sover \
    "$pkgdir/usr/lib/libutf8proc.so.$_sover"
  ln -s /usr/lib/libutf8proc.so.$_sover "$pkgdir/usr/lib/libutf8proc.so"
  ldconfig -n "$pkgdir/usr/lib"
}

# vim: ts=2 sw=2 et:
