# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=1.9.0
pkgrel=1
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('i686' 'x86_64')
url="https://github.com/libuv/libuv"
license=('custom')
depends=('glibc')
makedepends=('python-sphinx')
source=("https://github.com/libuv/libuv/archive/v$pkgver/$pkgname-$pkgver.tar.gz")

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./autogen.sh
	./configure --prefix=/usr
	make
	make man -C docs

	# skip failing get_passwd test
	sed '/pwd\.homedir/d' -i test/test-get-passwd.c
}

check() {
	cd "$srcdir"/$pkgname-$pkgver
	make check
}

package() {
	cd "$srcdir"/$pkgname-$pkgver

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
	install -Dm644 AUTHORS \
		"$pkgdir"/usr/share/doc/$pkgname/AUTHORS
	install -Dm644 README.md \
		"$pkgdir"/usr/share/doc/$pkgname/README.md
	install -Dm644 ChangeLog \
		"$pkgdir"/usr/share/doc/$pkgname/ChangeLog
	install -Dm644 docs/build/man/libuv.1 \
		"$pkgdir"/usr/share/man/man1/libuv.1
}

sha256sums=('f8b8272a0d80138b709d38fad2baf771899eed61e7f9578d17898b07a1a2a5eb')
