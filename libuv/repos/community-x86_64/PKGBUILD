# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=0.11.25
pkgrel=1
pkgdesc="A new platform layer for Node.JS"
arch=('i686' 'x86_64')
url="https://github.com/joyent/libuv"
license=('custom')
depends=('glibc')
source=("https://github.com/joyent/libuv/archive/v$pkgver.tar.gz"
        "inet_pton6.patch::https://github.com/joyent/libuv/compare/6d56e6e132717df13c7ed76e6cc3ee53105759be...4fac9427e4a652cdebf241803ce7443dd59a4451.patch")

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i "$srcdir/inet_pton6.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./autogen.sh
	./configure --prefix=/usr
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 AUTHORS \
		"$pkgdir/usr/share/doc/$pkgname/AUTHORS"
	install -Dm644 README.md \
		"$pkgdir/usr/share/doc/$pkgname/README.md"
	install -Dm644 ChangeLog \
		"$pkgdir/usr/share/doc/$pkgname/ChangeLog"
}

sha256sums=('b95a3e5eca9554998b7423ee786205e52731e82e2d24cbbd4f65af763de542fb'
            '655c98f10daf171ce736cea594b710a3f8d72f4da02613588eb9856e4bb36561')
