# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv
pkgver=0.11.27
pkgrel=1
pkgdesc="A new platform layer for Node.JS"
arch=('i686' 'x86_64')
url="https://github.com/joyent/libuv"
license=('custom')
depends=('glibc')
source=("https://github.com/joyent/libuv/archive/v$pkgver.tar.gz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./autogen.sh
	./configure --prefix=/usr
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgver"
	make check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install

	install -Dm644 LICENSE \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 AUTHORS \
		"$pkgdir/usr/share/doc/$pkgname/AUTHORS"
	install -Dm644 README.md \
		"$pkgdir/usr/share/doc/$pkgname/README.md"
	install -Dm644 ChangeLog \
		"$pkgdir/usr/share/doc/$pkgname/ChangeLog"
}

sha256sums=('68ffa82ef2b894df5d6e530cc70735fa3633857b69f078923b6580b182dc1cd3')
