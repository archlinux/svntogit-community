# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Sean V Kelley <seanvk@posteo.de>

pkgname=libva-utils
pkgver=2.3.0
pkgrel=1
pkgdesc="Intel VA-API Media Applications and Scripts for libva"
arch=('x86_64')
url="https://github.com/intel/libva-utils"
license=('custom')
depends=('libva')
makedepends=('mesa')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('f338497b867bbc9bf008e4892eaebda08955785dc7eb2005855bba5f1a20b037')

build() {
    cd ${pkgname}-${pkgver}
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/${pkgname}/COPYING
}
