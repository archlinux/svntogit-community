# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libvips
pkgver=8.13.0
pkgrel=1
pkgdesc="A fast image processing library with low memory needs"
arch=('x86_64')
license=('LGPL')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'libexif' 'libgsf' 'libimagequant' 'librsvg' 'libwebp' 'libxml2' 'openexr'
         'orc' 'pango' 'libcgif')
# Adding 'highway' here temporarily for FS#73931
makedepends=('gobject-introspection' 'libheif' 'libjxl' 'highway' 'imagemagick' 'openslide'
             'poppler-glib')
optdepends=('libheif: for heif module'
            'imagemagick: for magick module'
            'openslide: for openslide module'
            'poppler-glib: for poppler module'
            'libjxl: for jxl module')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.gz")
sha512sums=('9ef10edd44e726997e9d5da304560a90102b629134423853f6f0f6af4181dd946ef1b79207eb17d8e8fae0a03c223ecaca9faa8fee95516f03d459e4895e58e4')

build() {
  cd vips-$pkgver
  ./configure --prefix=/usr --disable-static
# Fix overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd vips-$pkgver
  make DESTDIR="$pkgdir" install
}
