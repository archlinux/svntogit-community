# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libvips
pkgver=8.12.2
pkgrel=1
pkgdesc="A fast image processing library with low memory needs"
arch=('x86_64')
license=('LGPL')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'imagemagick' 'libexif' 'libgsf' 'libheif' 'libimagequant' 'librsvg'
         'libwebp' 'libxml2' 'openexr' 'orc' 'pango' 'poppler-glib' 'libcgif')
makedepends=('gobject-introspection')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.gz")
sha512sums=('9df337b01234199c00e5007db9d4711dac81377a521a3fd65246778be6907f54c2fd7c1f23bde1c37644d08e5e5034d65f6095adc072e29fc9185a607a97fe61')

build() {
  cd vips-$pkgver
  ./configure --prefix=/usr
# Fix overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd vips-$pkgver
  make DESTDIR="$pkgdir" install
}
