# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgname=('libvirt-python')
epoch=1
pkgver=8.8.0
pkgrel=1
pkgdesc="libvirt python binding"
arch=('x86_64')
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL')
makedepends=('python' 'python-setuptools' 'libvirt')
depends=('python' 'libvirt')
options=('emptydirs')
source=("https://libvirt.org/sources/python/libvirt-python-${pkgver}.tar.gz"{,.asc})
sha512sums=('bcfe69b1279d8e2fc1343959f2c17ff29ef957c1d7f98b233c8bb32cffe50285138d1be11b0c327f6b18e2395b6353c3552cecfd9bbfcb5cc6bc97981f9d6f26'
            'SKIP')
validpgpkeys=('453B65310595562855471199CA68BE8010084C9C') # Jiří Denemark <jdenemar@redhat.com>

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
}
