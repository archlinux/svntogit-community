# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=libvirt-python
pkgname=('libvirt-python2' 'libvirt-python')
pkgver=5.7.0
pkgrel=1
pkgdesc="libvirt python binding"
arch=('x86_64')
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL')
makedepends=('python' 'python2' 'libvirt')
options=('emptydirs')
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
source=("https://libvirt.org/sources/python/libvirt-python-${pkgver}.tar.gz")
sha512sums=('578e54778dfb4a9d0ac6d7330146976c765d888c3ad30c7990e52cf9815b19b2a6581eb7a3d0e21e9e80496f5a002b9c529cf2d3a8ef0f41c7931a0cb24aa732')

package_libvirt-python2() {
  depends=('python2' 'libvirt')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py clean
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

package_libvirt-python() {
  depends=('python' 'libvirt')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py clean
  python setup.py install --root="${pkgdir}" --optimize=1
}
