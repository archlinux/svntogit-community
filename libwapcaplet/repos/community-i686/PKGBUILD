# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=libwapcaplet
pkgver=0.1.0
pkgrel=2
pkgdesc="A string internment library with rapid string comparison."
arch=('i686' 'x86_64')
url="http://www.netsurf-browser.org/projects/libwapcaplet/"
license=('MIT')
source=(http://www.netsurf-browser.org/projects/releases/${pkgname}-${pkgver}-src.tar.gz)
md5sums=('c21975b81bcdafa60ad6b97b95925efe')

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}-src

	make PREFIX=/usr COMPONENT_TYPE="lib-shared" 
	make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}-src

	make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-shared" 
	make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-static"
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
