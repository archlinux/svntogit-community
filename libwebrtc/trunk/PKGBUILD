# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=libwebrtc
_commit=18721df
pkgver=83.git1.${_commit}
pkgrel=1
pkgdesc='WebRTC library - static linked'
arch=('x86_64')
url='https://webrtc.org/?hl=de'
license=('custom:BSD')
makedepends=('git' 'gn' 'ninja' 'unzip' 'python2' 'python' 'libxrandr' 'libxcomposite' 'openssl' 'glibc' 'ffmpeg' 'libva')
options=('staticlibs')
source=("webrtc::git+https://github.com/open-webrtc-toolkit/owt-deps-webrtc#commit=${_commit}"
        'https://storage.googleapis.com/chrome-infra/depot_tools.zip'
        gclient-conf
        args.gn
        src.diff
        build.diff
        third_party.diff
        libsrtp.diff)
noextract=('depot_tools.zip')
b2sums=('SKIP'
        'c931652e11cc81db2cf7b8f547c72dbea2cc1fe1a11b01eae0af677fab91a48ba0339265eda110a4f3f53f859d1bdb520576a534600a582d5dc93979acf3f606'
        '25ab3e116e2f241219fe31e96166e009a233813417c6901ed3ff0cafcdc7b6bff17ae284167164028203e4a5e94548deff3e4bad00aff6c8286bd7d63cb9cdb2'
        '235560d3bf6c1023398eb53051f2fde522501c939105d100d0091a832909f0ffe7e4e98068a4ec3f2ee790994e904be7e35c12fdfe9020862fb2a0c8f32209e1'
        '923a626e69c819e550e1681257e1fb0c5c2a64160b13bd5d5a322125cba58020ba84330c8056d94a23175090055686916860d083adea08f8c8bff9e5e0ebbe01'
        'f8af81b249989296402e8ab9fa999559b8c9e18ac78c64766554db44289fe16778795a7ce4cd902b13f6ee9aa4866e8ae112af617a32f069674eec659c84a54a'
        'c8cbe2395066cdb639fd9cc8242ae345e4d4e2dabb3803397f162b26a0dfd170ddb888474e85c65fd2142a3462c984164fac0bc061f163128aad92d350be26ab'
        'fb6d9de8c49667e3726f255aabe8840a60e73d0d3a2ea5fd539580cc7aa734a2486267b00a236e0114adfd852a0710833b729c153761097668fdf7f6d755de94')

prepare() {
  mv webrtc src
  unzip depot_tools.zip -d depot_tools
  mv gclient-conf .gclient
  DEPOT_TOOLS_UPDATE=0 "${srcdir}"/depot_tools/gclient sync --no-history
  cd src
  patch -Np1 < "${srcdir}"/src.diff
  cd build
  patch -Np1 < "${srcdir}"/build.diff
  cd ../third_party
  patch -Np1 < "${srcdir}"/third_party.diff
  cd libsrtp
  patch -Np1 < "${srcdir}"/libsrtp.diff
  cd ../..
  mkdir out/Release
  cp "${srcdir}"/args.gn out/Release/args.gn

  gn gen out/Release
}

build() {
  cd src
  ninja -C out/Release webrtc
}

package() {
  install -Dm644 -t "${pkgdir}"/usr/lib/ "${srcdir}"/src/out/Release/obj/${pkgname}.a
  install -Dm644 -t "${pkgdir}"/usr/share/licenses/libwebrtc/ "${srcdir}"/src/LICENSE
}
