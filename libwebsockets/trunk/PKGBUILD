# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=libwebsockets
pkgver=4.1.3
pkgrel=1
pkgdesc="C library for websocket clients and servers"
arch=('x86_64')
url="https://libwebsockets.org"
license=('MIT')
depends=('glibc' 'openssl' 'libuv' 'libev' 'zlib')
makedepends=('cmake' 'glib2' 'libcap')
provides=('libwebsockets.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/warmcat/libwebsockets/archive/v$pkgver.tar.gz")
sha512sums=('33b23aaecb85c2bf87dce04fc16c6d8b08520d6273d94debfd9e9a7660501254d9dacd2a570d160dfa8bbadf28ccee43938be15ed7309f733a4f4720c1d3a41b')
b2sums=('31913cb1f4d77618c9f9686e7355dbcc67bed56e8d5ea145ac948bc28e3a99ba4907f428260e2b9221f9cbefd46ceb9fd37aae5e5797ecd0137e4c63dcd255c9')

build() {
  cd "$pkgname-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_BUILD_TYPE='None' \
        -D LWS_WITH_HTTP2=ON \
        -D LWS_IPV6=ON \
        -D LWS_WITH_GLIB=ON \
        -D LWS_WITH_ZIP_FOPS=ON \
        -D LWS_WITH_SOCKS5=ON \
        -D LWS_WITH_RANGES=ON \
        -D LWS_WITH_ACME=ON \
        -D LWS_WITH_LIBUV=ON \
        -D LWS_WITH_LIBEV=ON \
        -D LWS_WITH_LIBEVENT=OFF \
        -D LWS_WITH_FTS=ON \
        -D LWS_WITH_THREADPOOL=ON \
        -D LWS_UNIX_SOCK=ON \
        -D LWS_WITH_HTTP_PROXY=ON \
        -D LWS_WITH_DISKCACHE=ON \
        -D LWS_WITH_LWSAC=ON \
        -D LWS_LINK_TESTAPPS_DYNAMIC=ON \
        -D LWS_WITHOUT_BUILTIN_GETIFADDRS=ON \
        -D LWS_WITHOUT_BUILTIN_SHA1=ON \
        -D LWS_WITH_STATIC=OFF \
        -D LWS_WITHOUT_CLIENT=OFF \
        -D LWS_WITHOUT_SERVER=OFF \
        -D LWS_WITHOUT_TESTAPPS=ON \
        -D LWS_WITHOUT_TEST_SERVER=OFF \
        -D LWS_WITHOUT_TEST_SERVER_EXTPOLL=ON \
        -D LWS_WITHOUT_TEST_PING=ON \
        -D LWS_WITHOUT_TEST_CLIENT=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libcap.so' 'libglib-2.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
