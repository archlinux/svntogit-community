# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ondrej Martinak <omartinak@gmail.com>

pkgname=libxdg-basedir
pkgver=1.2.0
pkgrel=4
pkgdesc="An implementation of the XDG Base Directory specifications."
arch=('x86_64')
#url="http://n.ethz.ch/student/nevillm/download/libxdg-basedir"
url="https://github.com/devnev/$pkgname"
license=('MIT')
depends=('glibc')
source=(https://github.com/devnev/$pkgname/archive/$pkgname-$pkgver.tar.gz
        'xdgGetRelativeHome-overflow.patch'
        'LICENSE')
sha512sums=('8584405d45e0b57e570666b6eab6d99d70411d00f88965826c9ed0292372385668f190157a10ff536f3a5a59fd0031b332ecbb8a38ac64eda1b04a0603997406'
            '0e7b7a1d2e3dd1362201d67c6a24e276c43c50effd360248ebc1a33d82dba348d3389ce753d12a2a1d16ff384243faf208e6c345d90489a00db23e00492ff9c7'
            '772afb3c9aefe6ed6b941faf582ea74582c3e7042237701f7e02d4f237d3e4ec528ebc7caf5adb2c1a0adc398fae52e172c009ad962de0620b0376123d76b2d6')

prepare() {
   cd "${srcdir}/$pkgname-$pkgname-$pkgver"

#   FS#39490: Fix buffer overflow
   patch -Np0 -i "${srcdir}/xdgGetRelativeHome-overflow.patch"
}

build() {
   cd "${srcdir}/$pkgname-$pkgname-$pkgver"
   ./autogen.sh --prefix=/usr
   make
}

package() {
   cd "${srcdir}/$pkgname-$pkgname-$pkgver"
   make DESTDIR=${pkgdir} install

   # Install MIT license
   install -Dm644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

