# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=libxml-perl
pkgver=0.08
pkgrel=3
pkgdesc="Perl library for working with XML"
arch=('any')
url="http://search.cpan.org/~KMACLEOD/libxml-perl"
license=('GPL' 'PerlArtistic')
depends=('perlxml>=2.19')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/K/KM/KMACLEOD/libxml-perl-$pkgver.tar.gz) 
md5sums=('0ed5fbdda53d1301ddaed88db10503bb')

build() {
  cd  "${srcdir}"/$pkgname-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  find "${pkgdir}" -name '.packlist' -delete
  find "${pkgdir}" -name '*.pod' -delete
}
