# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libxmlb
pkgver=0.1.5
pkgrel=1
pkgdesc="A library to help create and query binary XML blobs"
arch=('x86_64')
url="https://github.com/hughsie/libxmlb"
license=('LGPL')
depends=('glib2' 'libstemmer')
makedepends=('meson' 'gobject-introspection' 'gtk-doc')
checkdepends=('shared-mime-info')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('88eb4d760d99f0cea17fcef4a148397474fd17cec1d3a9de507f6f2216b82b27' 'SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

build() {
    cd ${pkgname}-${pkgver}
    arch-meson ../build
    ninja -v -C ../build
}

check() {
    cd build
    meson test
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
}
