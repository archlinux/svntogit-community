# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libxmlb
pkgver=0.1.14
pkgrel=1
pkgdesc="A library to help create and query binary XML blobs"
arch=(x86_64)
url="https://github.com/hughsie/libxmlb"
license=(LGPL)
depends=(glib2 libstemmer)
makedepends=(meson gobject-introspection gtk-doc)
checkdepends=(shared-mime-info)
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('9128735d08d0326f48526fd11c4a44659f7995fae458bd7dbdf27460b842cc80' 'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
    arch-meson ${pkgname}-${pkgver} build -D stemmer=true
    ninja -v -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
}
