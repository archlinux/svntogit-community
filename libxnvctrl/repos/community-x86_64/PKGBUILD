# $Id$
# Maintainer:  Martin Wimpress <code@flexion.org>

pkgname=libxnvctrl
pkgver=331.49
pkgrel=1
pkgdesc="NVIDIA X Ctrl library"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('GPL2')
depends=('libxext')
source=("http://download.nvidia.com/XFree86/nvidia-settings/nvidia-settings-${pkgver}.tar.bz2"
        "10_libxnvctrl_so_0.diff")
sha1sums=('9c6b3445fdc800ebf01703527a0610365dd6c0f3'
          '84146494a08d4b691e2cff84d1551210eee584dd')

prepare() {
    cd "nvidia-settings-$pkgver"
    patch -Np1 -i "${srcdir}/10_libxnvctrl_so_0.diff"
}

build() {
    cd "nvidia-settings-$pkgver/src/libXNVCtrl"
    make clean
    make
}

package() {
    cd "nvidia-settings-$pkgver/src/libXNVCtrl"
    # install libXNVCtrl and headers
    install -d -m 755 "$pkgdir"/usr/{include/NVCtrl,lib}
    install -m 644 nv_control.h NVCtrl{,Lib}.h "$pkgdir"/usr/include/NVCtrl/
    install -m 644 libXNVCtrl.so.0.0.0 "$pkgdir"/usr/lib/
    cd "$pkgdir"/usr/lib/
    ln -s libXNVCtrl.so.0.0.0 libXNVCtrl.so.0
    ln -s libXNVCtrl.so.0 libXNVCtrl.so
}
