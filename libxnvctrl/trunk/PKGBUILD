# $Id$
# Maintainer:  Martin Wimpress <code@flexion.org>

pkgname=libxnvctrl
pkgver=343.22
pkgrel=1
pkgdesc="NVIDIA X Ctrl library"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('GPL2')
makedepends=('libxext')
options=('staticlibs')
source=("http://download.nvidia.com/XFree86/nvidia-settings/nvidia-settings-${pkgver}.tar.bz2")
sha256sums=('979936ba474ab8f14deb5f0d74fc486e862216270b4d870a4386131696ac80e1')

build() {
    cd "nvidia-settings-$pkgver/src/libXNVCtrl"
    make clean
    make
}

package() {
    cd "nvidia-settings-$pkgver/src/libXNVCtrl"
    # install libXNVCtrl and headers
    install -d -m 755 "$pkgdir"/usr/{include/NVCtrl,lib}
    install -m 644 nv_control.h NVCtrl{,Lib}.h "$pkgdir"/usr/include/NVCtrl/
    install -m 644 libXNVCtrl.a "$pkgdir"/usr/lib/libXNVCtrl.a
}
