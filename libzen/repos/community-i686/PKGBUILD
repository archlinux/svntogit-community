# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: hydro <hydro@freenet.de>

pkgname=libzen
pkgver=0.4.32
pkgrel=2
pkgdesc="shared library for libmediainfo and mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('BSD')
depends=('gcc-libs')
makedepends=('libtool' 'automake' 'autoconf')
source=(http://mediaarea.net/download/source/libzen/${pkgver}/${pkgname}_${pkgver}.tar.bz2)
md5sums=('ed9cae405b204919fe40531adf888a43')

build() {
  cd $srcdir/ZenLib/Project/GNU/Library
  [ -x configure ] || sh ./autogen.sh
  [ -f Makefile ] || ./configure --prefix=/usr --enable-shared
  make clean
  make
}

package() {
  cd $srcdir/ZenLib/Project/GNU/Library
  make DESTDIR=$pkgdir install
  install -dm 755 $pkgdir/usr/include/ZenLib
  install -m 644 $srcdir/ZenLib/Source/ZenLib/*.h $pkgdir/usr/include/ZenLib
  for i in HTTP_Client Format/Html Format/Http; do
    install -dm0755 $pkgdir/usr/include/ZenLib/$i
    install -m0644 $srcdir/ZenLib/Source/ZenLib/$i/*.h $pkgdir/usr/include/ZenLib/$i
  done
  install -dm 755 $pkgdir/usr/lib/pkgconfig
  install -m 644 $srcdir/ZenLib/Project/GNU/Library/libzen.pc $pkgdir/usr/lib/pkgconfig
  sed -i -e 's|Version: |Version: '$pkgver'|g' $pkgdir/usr/lib/pkgconfig/libzen.pc
  install -D -m 644 $srcdir/ZenLib/License.txt $pkgdir/usr/share/licenses/libzen/License.txt
}
