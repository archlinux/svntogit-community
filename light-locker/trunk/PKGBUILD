# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=light-locker
pkgver=1.6.0
pkgrel=3
pkgdesc='A simple session locker for LightDM'
arch=('i686' 'x86_64')
url='https://github.com/the-cavalry/light-locker'
license=('GPL2')
depends=('dbus-glib' 'gtk3' 'libxxf86misc' 'lightdm' 'libxss')
makedepends=('gnome-common' 'intltool')
source=("https://github.com/the-cavalry/light-locker/releases/download/v${pkgver}/light-locker-${pkgver}.tar.bz2"
        'light-locker-libsystemd-login.patch')
sha256sums=('63e0cbcc7ceb8ad695b4bbe7eb9ba5fee32f47790ccde048748d8a47edd32ad0'
            'd5a4e9a97b491c17aa059863020ed5cbfaaf0c79e05130d36a58959438c85cf3')

prepare() {
  cd light-locker-${pkgver}

  patch -Np1 -i ../light-locker-libsystemd-login.patch
}

build () {
  cd light-locker-${pkgver}

  ./configure \
    --prefix='/usr' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --with-x \
    --with-dpms-ext \
    --with-mit-ext \
    --with-xf86gamma-ext \
    --with-systemd \
    --without-console-kit \
    --without-upower \
    --disable-schemas-compile \
    --disable-static
  make
}

package() {
  cd light-locker-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
