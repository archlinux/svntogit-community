# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>
# Contributor: andrew.p <pan.pav.7c5@gmail.com>

pkgname=lightdm-gtk-greeter-settings
pkgver=1.2.0
pkgrel=4
pkgdesc='Settings editor for the LightDM GTK+ Greeter'
arch=('any')
url='https://launchpad.net/lightdm-gtk-greeter-settings'
license=('GPL3')
depends=('gtk-update-icon-cache' 'lightdm-gtk-greeter' 'python-gobject')
makedepends=('python-distutils-extra')
source=("https://launchpad.net/lightdm-gtk-greeter-settings/${pkgver%.*}/${pkgver}/+download/lightdm-gtk-greeter-settings-${pkgver}.tar.gz")
sha256sums=('0450202639b947434695d5ff60b4c4b655d7399f52e5b56db4593241c8080a55')

build() {
  cd lightdm-gtk-greeter-settings-${pkgver}

  python setup.py build
}

package() {
  cd lightdm-gtk-greeter-settings-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
