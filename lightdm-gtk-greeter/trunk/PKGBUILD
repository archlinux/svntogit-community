# $Id$
# Maintainer: Maxime Gauduin <alucrydarchlinux.org>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgbase=lightdm-gtk-greeter
pkgname=('lightdm-gtk2-greeter' 'lightdm-gtk3-greeter')
pkgver=1.8.6
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
makedepends=('bzr' 'exo' 'gnome-common' 'gnome-doc-utils'
             'gobject-introspection' 'gtk2' 'gtk3' 'intltool' 'lightdm')
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install='lightdm-gtk-greeter.install'
source=('bzr+lp:lightdm-gtk-greeter#revision=299')
sha256sums=('SKIP')

prepare() {
  cp -r lightdm-gtk-greeter lightdm-gtk2-greeter
  mv lightdm-gtk-greeter lightdm-gtk3-greeter
}

build() {
  cd lightdm-gtk2-greeter

  ./autogen.sh --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' \
               --libexecdir='/usr/lib/lightdm' --disable-static --with-gtk2 \
               --with-libxklavier
    make

  cd ../lightdm-gtk3-greeter

  ./autogen.sh --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' \
               --libexecdir='/usr/lib/lightdm' --disable-static \
               --with-libxklavier
    make
}

package_lightdm-gtk2-greeter() {
  depends=('gtk2' 'lightdm')
  provides=('lightdm-gtk-greeter')
  conflicts=('lightdm-gtk-greeter')

  cd lightdm-gtk2-greeter

  make DESTDIR="${pkgdir}" install
}

package_lightdm-gtk3-greeter() {
  depends=('gtk3' 'lightdm')
  provides=('lightdm-gtk-greeter')
  conflicts=('lightdm-gtk-greeter')

  cd lightdm-gtk3-greeter

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
