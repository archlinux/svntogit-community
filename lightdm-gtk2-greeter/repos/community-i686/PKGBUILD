# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk2-greeter
pkgver=1.6.1
pkgrel=1
epoch=1
pkgdesc="GTK+ greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3' 'LGPL3')
depends=('gtk2' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/2/}")
conflicts=("${pkgname/2/3}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/${pkgname/2/}/1.6/${pkgver}/+download/${pkgname/2/}-${pkgver}.tar.gz")
sha256sums=('1f0286ab093d50151f0efc46496aef3f8ed8c7306bbc81807a3fdcbcb2a468d9')

build() {
  cd ${pkgname/2/}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --libexecdir='/usr/lib/lightdm' --disable-static --with-gtk2
  make
}

package() {
  cd ${pkgname/2/}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
