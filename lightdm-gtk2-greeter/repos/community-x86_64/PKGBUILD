# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk2-greeter
pkgver=1.8.3
pkgrel=1
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk2' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/2/}")
conflicts=("${pkgname/2/3}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install="${pkgname/2/}.install"
source=("https://launchpad.net/${pkgname/2/}/${pkgver%.?}/${pkgver}/+download/${pkgname/2/}-${pkgver}.tar.gz")
sha256sums=('5c4676a7aa85f1507d7169df8b2cc026db208811a8945d615adb53c50d80eb03')

build() {
  cd ${pkgname/2/}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --libexecdir='/usr/lib/lightdm' --disable-static --with-gtk2
  make
}

package() {
  cd ${pkgname/2/}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
