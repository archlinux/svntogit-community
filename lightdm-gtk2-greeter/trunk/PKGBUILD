# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk2-greeter
pkgver=1.8.4
pkgrel=2
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=('i686' 'x86_64')
url='https://launchpad.net/lightdm-gtk-greeter'
license=('GPL3' 'LGPL3')
depends=('gtk2' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/2/}")
conflicts=("${pkgname/2/3}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
install="${pkgname/2/}.install"
source=("https://launchpad.net/${pkgname/2/}/${pkgver%.?}/${pkgver}/+download/${pkgname/2/}-${pkgver}.tar.gz"
        'tooltip-focus.patch')
sha256sums=('cb3e93bfcb8fbaf109e01080dc75ec9a6803afcb68acbf9eef3799be927a2c6c'
            '2dcf615181fc57cfcc02601d4d9845ef23af60a0a322a5208989849b279deec7')

prepare() {
  cd ${pkgname/2/}-${pkgver}

  patch -Np1 -i ../tooltip-focus.patch
}

build() {
  cd ${pkgname/2/}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --sysconfdir='/etc' --libexecdir='/usr/lib/lightdm' --disable-static --with-gtk2
  make
}

package() {
  cd ${pkgname/2/}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
