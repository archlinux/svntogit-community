# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=6.1.0
pkgrel=2
pkgdesc='Pantheon greeter for LightDM'
arch=(x86_64)
url=https://github.com/elementary/greeter
license=(GPL)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
options=(debug)
install=lightdm-pantheon-greeter.install
_tag=b4a35f592ab88c859cae1ec57fd56de48479bc52
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags
}

prepare() {
  cd lightdm-pantheon-greeter

  # Mutter 43
  git cherry-pick -n 11dadd1315da0e62e6e95f64081c60317efa1e7f \
                     6318ee489e8aead4ec02c3da965ac2be278e82b1 \
                     770bb572ed5de5038ab50f9106b4726f9b61926e \
                     4972c1b7a07e0141eceeae3c24cc1baba921a83e
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
