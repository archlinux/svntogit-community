# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lilv
pkgver=0.24.2
pkgrel=1
pkgdesc="A C library interface to the LV2 plug-in standard"
arch=('x86_64')
url="https://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('sratom' 'jack')
makedepends=('swig' 'python')
optdepends=('bash-completion: completion for bash')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('b5d04b6bb1fb4d3a9a58d93b233db502dc2ded75672766a31d252abca657cbf2d170fa590c0c56159b2bac68c7af95d8787a512e318e3a0f30855504bb034659'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # don't run local ldconfig
  sed -i "/ldconfig/d" wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python waf configure --prefix=/usr \
                       --no-bash-completion \
                       --dyn-manifest
  python waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"
  # license
  install -vDm644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # bash completion
  install -vDm644 utils/lilv.bash_completion\
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}

# vim:set ts=2 sw=2 et:
