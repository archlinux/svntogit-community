# $Id$
# Maintainer: Antonio Rojas < nqn76sw@gmail.com >

pkgname=linbox
pkgver=1.4.2
pkgrel=9
pkgdesc="A template library for exact, high-performance linear algebra computation with dense, sparse, and structured matrices over the integers and over finite fields"
arch=(i686 x86_64)
url="http://linalg.org/"
license=(LGPL)
depends=(m4rie flint fplll iml fflas-ffpack) 
source=("https://github.com/linbox-team/linbox/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
        linbox-fplll5.patch::"https://github.com/fingolfin/linbox/commit/95355873.patch")
sha256sums=('47f025fe7aab204fb276307f32a562de357c887c6ef589b9e4f7a57a3b58ecaf'
            '44ca7b3be35d0724dc0261726cffe31939c20bb61aaeb015da3e543953a2a887')

prepare() {
  cd $pkgname-$pkgver
# Fix build with fplll 5.0
  patch -p1 -i ../linbox-fplll5.patch
  sed -e 's|fplll/dpe.h|fplll/nr/dpe.h|g' -i linbox/algorithms/short-vector.h
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-sage --enable-optimization --with-fplll=/usr
  make
}

check() {
  cd $pkgname-$pkgver
# make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
