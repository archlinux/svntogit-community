# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=linphone
pkgver=3.11.1
pkgrel=3
pkgdesc="A Voice-over-IP phone"
arch=('i686' 'x86_64')
url="http://www.linphone.org"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'gtk2' 'speex' 'libv4l' 'libglade' 'v4l-utils'
	 'libpulse' 'libxv' 'mediastreamer' 'ortp' 'bzrtp' 'bcg729' 'libsoup'
	 'belle-sip' 'libnotify' 'libldap')
makedepends=('pkg-config' 'perl-xml-parser' 'intltool' 'sgml-common' 'bcunit')
optdepends=('pulseaudio')
options=('!emptydirs')
install=linphone.install
source=("https://www.linphone.org/releases/sources/linphone/linphone-$pkgver.tar.gz"
	"0001-Improve-linphone-config-handling-to-prevent-useless.patch::https://bugs.archlinux.org/task/53591?getfile=15156")
sha256sums=('dc88ef3d311495b9db6041f2ed7b99f6b00a91339c1612e63b4071a4f0a0404e'
            '4ba66e7b87b931ceeb6614b3b181eb043456627b2a9d8326f844192572c547d9')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/0001-Improve-linphone-config-handling-to-prevent-useless.patch
}

build() {
  cd $pkgname-$pkgver
#  export PKG_CONFIG=/usr/bin/pkg-config
#  unset SGML_CATALOG_FILES
#  export CFLAGS="$CFLAGS -Dbctbx_list_remove_link=bctbx_list_unlink -Dbctbx_list_delete_link=bctbx_list_erase_link"
  ./autogen.sh
  ./configure --prefix=/usr --disable-static --enable-ipv6 --enable-video \
	--enable-alsa --enable-pulseaudio --disable-artsc --disable-strict \
	--libexecdir=/usr/lib/$pkgname \
	--enable-external-mediastreamer \
	--enable-external-ortp \
	--enable-zrtp \
	--enable-ldap
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
