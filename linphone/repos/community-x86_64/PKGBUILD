# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=linphone
pkgver=3.10.2
pkgrel=1
pkgdesc="A Voice-over-IP phone"
arch=('i686' 'x86_64')
url="http://www.linphone.org"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'gtk2' 'speex' 'libv4l' 'libglade' 'v4l-utils'
	 'libpulse' 'libxv' 'mediastreamer' 'ortp' 'bzrtp' 'bcg729' 'libsoup'
	 'belle-sip' 'libnotify')
makedepends=('pkg-config' 'perl-xml-parser' 'intltool' 'sgmltools-lite' 'cunit')
optdepends=('pulseaudio')
options=('!emptydirs')
source=("http://www.linphone.org/releases/sources/linphone/linphone-$pkgver.tar.gz")
md5sums=('0b080e4f46bf944f4dc7fc11a76f13a1')

build() {
  cd $pkgname-$pkgver
  export PKG_CONFIG=/usr/bin/pkg-config
  unset SGML_CATALOG_FILES
  ./configure --prefix=/usr --disable-static --enable-ipv6 --enable-video \
	--enable-alsa --enable-pulseaudio --disable-artsc --disable-strict \
	--libexecdir=/usr/lib/$pkgname \
	--enable-external-mediastreamer \
	--enable-external-ortp \
	--enable-zrtp
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
