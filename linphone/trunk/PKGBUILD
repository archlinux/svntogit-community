# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=linphone
pkgver=3.6.1
pkgrel=1
pkgdesc="A Voice-over-IP phone"
arch=('i686' 'x86_64')
url="http://www.linphone.org/index.php/eng"
license=('GPL')
depends=('alsa-lib' 'ffmpeg' 'gtk2' 'libexosip2' 'speex' 'libv4l' 'libglade' 'v4l-utils'
	 'libpulse' 'libxv' 'mediastreamer' 'ortp' 'libzrtpcpp' 'bcg729' 'libsoup')
makedepends=('pkgconfig' 'perlxml' 'intltool' 'sgmltools-lite')
optdepends=('pulseaudio')
options=('!emptydirs')
source=("http://download-mirror.savannah.gnu.org/releases/${pkgname}/3.6.x/sources/${pkgname}-$pkgver.tar.gz"
	"port-to-exosip-4.patch")
md5sums=('f59b99ec2501ebbb02969c885be4c4c5'
         '45178a854c9321c58296dead77fbc5dc')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/port-to-exosip-4.patch
}

build() {
  cd $pkgname-$pkgver
  export PKG_CONFIG=/usr/bin/pkg-config
  unset SGML_CATALOG_FILES
  ./configure --prefix=/usr --disable-static --enable-ipv6 --enable-video \
	--enable-alsa --enable-pulseaudio --disable-artsc --disable-strict \
	--libexecdir=/usr/lib/$pkgname \
	--enable-external-mediastreamer \
	--enable-external-ortp \
	--enable-zrtp
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
