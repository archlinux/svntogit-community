# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Ell <david at bytesizebits dot net>

pkgname=linuxdoc-tools
pkgver=0.9.72
pkgrel=2
pkgdesc="Convert LinuxDoc SGML source into other formats"
arch=('x86_64')
url="http://packages.debian.org/sid/linuxdoc-tools"
license=('GPL')
depends=('jade')
makedepends=('texlive-bin' 'texlive-core')
optdepends=('groff' 'texlive-bin')
source=(https://ftp.de.debian.org/debian/pool/main/l/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz)
sha256sums=('98edf1acff4be68e04c10371eb17e12a613003ac2a5ab1adab006768c35de349')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i '/extern int yyleng;/d' rtf-fix/rtf2rtf.l
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-texdir=/usr/share/texmf-dist/tex/linuxdoc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
