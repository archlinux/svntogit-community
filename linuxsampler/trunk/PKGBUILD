# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=linuxsampler
pkgver=2.0.0
pkgrel=4
pkgdesc="Professional-grade audio sampler alternative to Gigasampler"
arch=('i686' 'x86_64')
url="http://www.linuxsampler.org/"
license=('GPL' 'custom:exception')
depends=('libgig>=4' 'jack' 'sqlite')
makedepends=('ladspa' 'dssi' 'lv2') # ladspa is a header dep of dssi
options=('libtool') # do NOT slay libtool; required by gigedit
source=("http://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.bz2"
        'license.txt')
md5sums=('ddf4208c9b23f8832ad15c2adf974140'
         '746c1421eb9f6812b5fa411bfb923c75')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # install GPL exception statement
  install -Dm644 "$srcdir/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/GPL-EXCEPTION"

  # lscp files conflict with nilfs-utils
  # see https://bugs.archlinux.org/task/45827
  mv "$pkgdir/usr/bin/lscp" "$pkgdir/usr/bin/lscp-linuxsampler"
  mv "$pkgdir/usr/share/man/man1/lscp.1" \
    "$pkgdir/usr/share/man/man1/lscp-linuxsampler.1"
}

# vim:set ts=2 sw=2 et:
