# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=liquidctl
pkgver=1.4.2
pkgrel=3
arch=('any')
url='https://github.com/jonasmalacofilho/liquidctl'
license=('GPL3')
pkgdesc='Cross-platform CLI and Python drivers for AIO liquid coolers and other devices'
depends=('python' 'python-docopt' 'python-pyusb' 'python-hidapi')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('617510153ee84ce94bc143fa4b4a8af1d13478cfe4cdb838a9c72dd97d57a924f36a5aedf9a9eb96cc240b3d088651bc5db11ea9298e980386e7f398863705e5')

build() {
  cd $pkgname-$pkgver

  DIST_NAME='Arch Linux'
  DIST_PACKAGE="$pkgver-$pkgrel"

  python setup.py build

  gzip liquidctl.8
}

check() {
  cd $pkgname-$pkgver

  mkdir runtime
  XDG_RUNTIME_DIR=runtime python -m pytest
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root "$pkgdir" --skip-build -O1

  install -Dm 644 extra/linux/71-liquidctl.rules "$pkgdir"/usr/lib/udev/rules.d/71-liquidctl.rules

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership docs/* "$pkgdir"/usr/share/doc/$pkgname
  install -Dm 644 liquidctl.8.gz "$pkgdir"/usr/share/man/man8/liquidctl.8.gz
}

