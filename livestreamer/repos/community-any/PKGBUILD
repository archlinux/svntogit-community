# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
pkgname=livestreamer
pkgver=1.10.1
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player'
arch=('any')
url='https://github.com/chrippa/livestreamer'
license=('BSD')
depends=('python-requests' 'rtmpdump' 'python-setuptools')
makedepends=('python-sphinx')
provides=('livestreamer')
conflicts=('livestreamer-git')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/l/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('f0ea8620b5f5364a74c3943b812910469e1219dd1573221a471475a36e2a14b1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py build_sphinx -b man
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 build/sphinx/man/livestreamer.1 \
    "$pkgdir/usr/share/man/man1/livestreamer.1"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
