# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
pkgname=livestreamer
pkgver=1.11.1
pkgrel=2
pkgdesc='CLI program that launches streams from various streaming services in a custom video player'
arch=('any')
url='https://github.com/chrippa/livestreamer'
license=('BSD')
depends=('python-requests' 'rtmpdump' 'python-setuptools')
makedepends=('python-sphinx')
source=(https://pypi.python.org/packages/source/l/$pkgname/$pkgname-$pkgver.tar.gz
        plugins-hitbox-Refactor-to-use-the-StreamMapper.patch)
sha256sums=('84dd83d301518ffcf96f30cbffc0e0598e0756e7ab8e7498d11285d42fe17f9c'
            '1a305add9db98912f0cdb16862d1296588b2ed299587de26d93c5be53f9fd572')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://github.com/chrippa/livestreamer/issues/648
  patch -Np1 -i ../plugins-hitbox-Refactor-to-use-the-StreamMapper.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py build_sphinx -b man
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 build/sphinx/man/livestreamer.1 \
    "$pkgdir/usr/share/man/man1/livestreamer.1"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
