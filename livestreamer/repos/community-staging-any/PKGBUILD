# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
pkgname=livestreamer
pkgver=1.12.2
pkgrel=3
pkgdesc='CLI program that launches streams from various streaming services in a custom video player'
arch=('any')
url='https://github.com/chrippa/livestreamer'
license=('BSD')
depends=('python-requests' 'rtmpdump' 'python-setuptools')
makedepends=('python-sphinx')
source=(https://pypi.python.org/packages/source/l/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('ef3e743d0cabc27d8ad906c356e74370799e25ba46c94d3b8d585af77a258de0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py build_sphinx -b man
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 build/sphinx/man/livestreamer.1 \
    "$pkgdir/usr/share/man/man1/livestreamer.1"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
