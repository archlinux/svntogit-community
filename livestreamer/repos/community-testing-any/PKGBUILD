# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
pkgname=livestreamer
pkgver=1.7.5
pkgrel=4
pkgdesc='CLI program that launches streams from various streaming services in a custom video player'
arch=('any')
url='https://github.com/chrippa/livestreamer'
license=('BSD')
depends=('python-requests' 'rtmpdump' 'python-setuptools')
provides=('livestreamer')
conflicts=('livestreamer-git')
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/l/$pkgname/$pkgname-$pkgver.tar.gz
        # https://github.com/chrippa/livestreamer/issues/255
        ca-certs.diff)
sha256sums=('7f8308a3efdb7c951eda2f5d1b3e738f2fe280e145d4c624b7cd70b1071967a4'
            'b8f3008444c74b844be3368c662628f9cc048589abf5387d2f0116bf8f6f6c22')

prepare(){
  patch -Np1 -d "$srcdir/$pkgname-$pkgver/" -i "$srcdir/ca-certs.diff"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
