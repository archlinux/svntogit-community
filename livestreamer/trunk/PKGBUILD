# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Christopher Rosell <chrippa@tanuki.se>
pkgname=livestreamer
pkgver=1.11.0
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player'
arch=('any')
url='https://github.com/chrippa/livestreamer'
license=('BSD')
depends=('python-requests' 'rtmpdump' 'python-setuptools')
makedepends=('python-sphinx')
provides=('livestreamer')
conflicts=('livestreamer-git')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/l/$pkgname/$pkgname-$pkgver.tar.gz
        0001-plugins.twitch-Update-for-API-change.patch)
sha256sums=('5af677ec93ebce229ac235ea6c23aec915deb4aacc8bdb8418c8488fbdd96920'
            '2dd33dca00befedb0754700c3aeb7921361b2c87c78eb55de397c18376d94952')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../0001-plugins.twitch-Update-for-API-change.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py build_sphinx -b man
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 build/sphinx/man/livestreamer.1 \
    "$pkgdir/usr/share/man/man1/livestreamer.1"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
