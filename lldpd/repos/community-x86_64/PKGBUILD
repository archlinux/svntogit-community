# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=lldpd
pkgver=1.0.1
pkgrel=1
pkgdesc='802.1ab implementation (LLDP) to help you locate neighbors'
arch=('x86_64')
url='https://vincentbernat.github.io/lldpd/'
license=('custom:ISC' 'GPL')
depends=('glibc' 'libevent' 'libbsd' 'zlib' 'openssl' 'pciutils' 'perl'
         'libxml2' 'net-snmp' 'jansson')
backup=('etc/lldpd.conf')
source=("https://media.luffy.cx/files/lldpd/lldpd-$pkgver.tar.gz"
        "lldpd-$pkgver.tar.gz.sig::https://media.luffy.cx/files/lldpd/lldpd-$pkgver.tar.gz.gpg"
        'LICENSE'
        'lldpd.service'
        'lldpd.sysusers'
        'lldpd.tmpfiles')
validpgpkeys=('AEF2348766F371C689A7360095A42FE8353525F9') # Vincent Bernat <bernat@luffy.cx>
md5sums=('91de961bfccfa8790e0514a5dc6eafb3'
         '368a231641559d4d16db0b7b699d5896'
         '8ae98663bac55afe5d989919d296f28a'
         'fc660a94c6dda3146ea7932685434bc8'
         '8c9fee704ce84afe51930fc73484ab66'
         '00a82f466404aec01b074503633d12ba')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-snmp \
    --with-xml \
    --with-readline \
    --with-privsep-user=lldpd \
    --with-privsep-group=lldpd \
    --with-privsep-chroot=/run/lldpd/chroot \
    --with-lldpd-ctl-socket=/run/lldpd/socket \
    --with-lldpd-pid-file=/run/lldpd/pid
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  # config stuff
  install -D -m 644 /dev/null "$pkgdir/etc/lldpd.conf"
  install -d -m 755  "$pkgdir/etc/lldpd.d"
  # systemd stuff
  install -D -m 644 lldpd.service "$pkgdir/usr/lib/systemd/system/lldpd.service"
  install -D -m 644 lldpd.sysusers "$pkgdir/usr/lib/sysusers.d/lldpd.conf"
  install -D -m 644 lldpd.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/lldpd.conf"
  # license
  install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
