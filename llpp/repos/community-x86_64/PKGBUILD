# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: earnestly

pkgname=llpp
pkgver=34
pkgrel=1
pkgdesc='opengl accelerated pdf viewer based on mupdf'
arch=('x86_64')
url="https://github.com/moosotc/${pkgname}"
license=('GPL3')
depends=('libgl' 'openjpeg2' 'jbig2dec' 'libjpeg' 'freetype2' 'gumbo-parser')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion'
            'inotify-tools: for the llpp.inotify autoreload wrapper')
makedepends=('git' 'asciidoc' 'ocaml' 'ocaml-lablgl' 'libmupdf')
options=('!strip')
source=("git+${url}#tag=v${pkgver}"
        'system-makedeps.patch')
sha256sums=('SKIP'
            'c4e3f3fb6b74ffe7e9dccb7eff2cccda8177ba7e4574f894a06e3fc3a820e6db')

prepare() {
    cd "${srcdir}"/${pkgname}

    # apparently the desktop file went "stale"
    git revert --no-commit aad4b1e65e581ff7a096a3c3901b222a9c127a1c

    patch --forward --strip=1 --input=../system-makedeps.patch
}

build() {
    cd "${srcdir}"/${pkgname}

    bash ./build.bash build
    bash ./build.bash build doc
}

package() {
    cd "${srcdir}"/${pkgname}

    install -Dt "${pkgdir}"/usr/bin build/llpp misc/llpp{.inotify,ac}
    install -Dm644 -t "${pkgdir}"/usr/share/applications misc/llpp.desktop
    install -Dm644 -t "${pkgdir}"/usr/share/man/man1 build/doc/*.1
}
