# $Id$
# Maintainer: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

pkgname=lmms
pkgver=0.4.7
pkgrel=1
pkgdesc="The Linux MultiMedia Studio"
arch=('i686' 'x86_64')
url="http://lmms.sourceforge.net/"
license=('GPL')
depends=('sdl' 'glib2' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'qt' 'sdl_sound' 'libsamplerate' 'shared-mime-info')
optdepends=('wine: VST support (experimental)'
	    'fftw: SpectrumAnalyzer plugin' )
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2')
provides=('lmms-extras')
conflicts=('lmms-extras')
options=('!libtool' '!makeflags')
install=lmms.install
source=(http://downloads.sourceforge.net/sourceforge/lmms/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e1e747a7c19da6fe2f8ca18d76ec0d75')
sha1sums=('9c9fc806307c5e00e7b2b94ba4e18d28c7ac7501')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

