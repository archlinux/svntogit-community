# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=('loki' 'loki-canary' 'promtail' 'logcli')
pkgver=2.1.0
pkgrel=2
pkgdesc='like Prometheus, but for logs'
url='https://github.com/grafana/loki'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go' 'systemd')
source=($pkgname-$pkgver.tar.gz::https://github.com/grafana/loki/archive/v$pkgver.tar.gz
        promtail.sysusers promtail.service promtail.tmpfiles
        loki.sysusers loki.service loki.tmpfiles)
sha512sums=('e488e393bf123b1f822e1dc304c3938f67c3872e9fc33b0caf377927d97cecb0a191c4ff42bf4e7d573a87d03adbef170dbcfbadf206022a5c29205dc36c12ce'
            '2b6c44b18ea3c9f955a7450222180d0b20b5fc551d0b7e5d0d8949e40adc847c4166829146260f87a75732cc5473eab0347dd56fc2125517698bac0652738c74'
            'b63ee3f5c3948f50f1ea46247c5e828f6f0b48294e3ccfe190c13516d24dcecd17ecb985e53048654f843f953625de6a0a8c1edd8adbe1b15edb877e6aa63af4'
            '598042c40673a7914c5a1eeccfb78f832379a61f4360212c5d86f667343cf2fc78e98d9025f9717ea64f3e16e0a28f08cd7709706d811656722019f6167dd788'
            'f00b3cb64b71d3ca5a422a2bdff1f81a3e2707c4f73bcb27e24338eabff039b96125c847aaae43c9b22ccc47f89585118c1d0e0b29c4eb7b6f9260c68f8a8324'
            '41726c7c0a13f672ee0ab58e8decc5c3bd7558d7efdc219dc16f567895c9fdfc44eb730c7ea426edc9e778bbe45c58570986f7141946f3b3f78f563441a86901'
            'de40d1e6752edbf8c21317ce1ee10f98dfc869e569c07092c613f7144e261e9438683a145a2e0e37e2a9fc758c5c2f02e1d0ac1c60347e98b147e4a550ec6040')

build() {
  cd loki-$pkgver

  for cmd in loki loki-canary promtail logcli; do
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags " \
         -X github.com/grafana/loki/pkg/build.Version=$pkgver
         -X github.com/grafana/loki/pkg/build.BuildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH})
         -linkmode external
         -extldflags \"${LDFLAGS}\"" \
      ./cmd/"$cmd"
  done
}

check() {
  cd loki-$pkgver

  go test -v ./...
}

package_loki() {
  backup=('etc/loki/loki.yaml')

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin loki

  install -Dm644 cmd/loki/loki-local-config.yaml $pkgdir/etc/loki/loki.yaml

  install -Dm644 ${srcdir}/loki.sysusers "$pkgdir/usr/lib/sysusers.d/loki.conf"
  install -Dm644 ${srcdir}/loki.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/loki.conf"
  install -Dm644 ${srcdir}/loki.service "$pkgdir/usr/lib/systemd/system/loki.service"
}

package_loki-canary() {
  pkgdesc="A standalone app that audits the log capturing performance of Loki"

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin loki-canary
}

package_promtail() {
  pkgdesc="An agent which ships the contents of local logs to a private Loki instance or Grafana Cloud"
  backup=('etc/loki/promtail.yaml')

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin promtail

  install -Dm644 cmd/promtail/promtail-local-config.yaml $pkgdir/etc/loki/promtail.yaml

  install -Dm644 ${srcdir}/promtail.sysusers "$pkgdir/usr/lib/sysusers.d/promtail.conf"
  install -Dm644 ${srcdir}/promtail.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/promtail.conf"
  install -Dm644 ${srcdir}/promtail.service "$pkgdir/usr/lib/systemd/system/promtail.service"
}

package_logcli() {
  pkgdesc="A command-line for loki"

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin logcli
}
