# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=100.0.0
pkgrel=3
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-optimist' 'ruby-manpages')
options=('!emptydirs')
source=(https://github.com/busyloop/lolcat/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('5a155a0ec68715306a90384c1d624b7fd80a1286fa92938040fdd781c7053e52')
sha512sums=('59ed3480386f0d7eb0a2a94f5cfd684f3cd30a74504ba3281caa0356db43158353d4fb9b306842d89ec63ddca661bf90856c2593aaa99124d665f33d9698dbce')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i lolcat.gemspec
  sed 's|git ls-files -- bin/\*|find bin -type f|' -i lolcat.gemspec
  sed 's|git ls-files -- {test,spec,features}/*|echo|' -i lolcat.gemspec
  sed 's|git ls-files|find -type f|' -i lolcat.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build lolcat.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem

  install -Dm 644 "${pkgdir}/${_gemdir}/gems/lolcat-${pkgver}/man/lolcat.6" \
    -t "${pkgdir}/usr/share/man/man6"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
