# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=42.1.43
pkgrel=2
pkgdesc="Okay, no unicorns. But rainbows!!"
url="https://github.com/busyloop/lolcat"
arch=('any')
license=('custom:WTF')
depends=('ruby' 'ruby-paint' 'ruby-trollop')
options=('!emptydirs')
source=(https://rubygems.org/downloads/${pkgname}-${pkgver}.gem)
noextract=(${pkgname}-${pkgver}.gem)
sha512sums=('eaea5637655bb2571025a9b33fc40c13aa1e7a4e782f6b9cd217bfd19f19e497228cea23988913183968a23564cae8f07e488ac413f8c5b27e5b50ce9a58e0ce')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${pkgname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README"
  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
