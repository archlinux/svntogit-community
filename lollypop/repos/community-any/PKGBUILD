# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.70
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://github.com/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'python')
optdepends=('libsecret: Last.FM support'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support')
install='lollypop.install'
source=("https://github.com/gnumdk/lollypop/releases/download/${pkgver}/lollypop-${pkgver}.tar.xz")
sha256sums=('fea79f50083dab195da5f88a5272201451cc82fa2eb98ab3396b70482f511c41')

build() {
  cd lollypop-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd lollypop-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
