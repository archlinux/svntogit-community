# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=1.2.2
pkgrel=2
pkgdesc='Music player for GNOME'
arch=(any)
url=https://gitlab.gnome.org/World/lollypop
license=(GPL)
depends=(
  appstream-glib
  gst-plugins-base-libs
  gtk3
  python-beautifulsoup4
  python-cairo
  python-gobject
  python-pillow
  totem-plparser
)
makedepends=(
  git
  gobject-introspection
  intltool
  itstool
  meson
)
optdepends=(
  'easytag: Modify tags'
  'gst-libav: FFmpeg plugin for GStreamer'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'kid3-qt: Store covers in tags'
  'libsecret: Last.FM support'
  'python-pylast: Last.FM support'
  'youtube-dl: Youtube support'
)
source=(git+https://gitlab.gnome.org/World/lollypop.git#tag=e537b9953e4e992519fd4306d978037aff797a09)
sha256sums=(SKIP)

pkgver() {
  cd lollypop

  git describe --tags
}

build() {
  arch-meson lollypop build \
    --libexecdir=lib/lollypop
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
