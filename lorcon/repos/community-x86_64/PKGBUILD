# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: fnord0 [fnord0 <AT> riseup <DOT> net]
# Contributor: dninja <dninja@gmail.com>
# Contributor: Jon Gjengset <jon@thesquareplanet.com>

pkgname=lorcon
# Version: raw.githubusercontent.com/lunixbochs/lorcon/master/Makefile.in
pkgver=2.0.0.20091101
pkgrel=3
epoch=1
pkgdesc='Generic library for injecting 802.11 frames'
url='http://802.11ninja.net/'
license=('GPL')
arch=('x86_64')
depends=('libpcap')
makedepends=('git')
source=('git://github.com/lunixbochs/lorcon#commit=10d5c825aa')
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"

  major=$(cat Makefile.in | grep -E "^MAJOR " | sed 's/^.*= *//')
  minor=$(cat Makefile.in | grep -E "^MINOR " | sed 's/^.*= *//')
  tiny=$(cat Makefile.in | grep -E "^TINY " | sed 's/^.*= *//')
  version=$(cat Makefile.in | grep -E "^VERSION " | sed 's/^.*= *//')
  echo "$major.$minor.$tiny.$version"
}

prepare() {
  cd "$pkgname"

  ./configure --prefix=/usr --mandir=/usr/share/man
}

build() {
  make -C "$pkgname"
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et: 
