# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: catwell <catwell@archlinux.us>

pkgname=lout
pkgver=3.42
pkgrel=1
pkgdesc="A lightweight document formatting system"
arch=('x86_64')
url="https://github.com/william8000/lout"
license=('GPL')
depends=('glibc' 'zlib')
options=('!emptydirs')
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  'use-cflags-ldflags.patch'
  'add-destdir.patch'
)
sha512sums=('2c6e496bbacbce68f1c0b6a8c4b18eb275264c04eae14afc9f59fdc7578457da001c7f0fe4c0c1a1603618a1e4528ba41e53539c3c5e8e747221abaf9d1df8f2'
            'ae9797c1fa889075c7ac71d53b307a235112781e2b3e935b4992fd254e1bc9e9ad6672b9df51af751420c524db330d3c38a49f8046efb67f51c512e4de759455'
            '87a8b2e45536680a7247509aef881006e4b2b6483aef4c6bc2204b341e1a1c211d654e1f87bf4155d0e2ddce59bb2ceb458bb2399584f02270fdf4add599226c')
b2sums=('2352de7ab6b1133892b93a2a74d84fb57c6e5f3f4f56bb711ec9a8ea16e51e1e34e8524a296d4490b871a8d8d31916a1a24c0c9f5c1f68cad8d222f915fe7cfc'
        'd85b9c455c0cabf7e420a46ae8e423bd842ae36563944dfebc12c557b0077d6160813206be36adad637459947b695e8831f11d6de35dbb8a73eb0891d0fefb26'
        '7b5fe13b9ff1e6522af6949874dfce4d3db1143f36bec3f9dc22edc12d2f0a6c923e80583f0db8b69621eef868f751e0b9908b107f421f7f0d12b9e1f2c2f869')

prepare() {
  cd "$pkgname-$pkgver"

  # ensure CFLAGS/LDFLAGS are passed
  patch -p1 -i ../use-cflags-ldflags.patch

  # add $DESTDIR variable to Makefile
  patch -p1 -i ../add-destdir.patch

  # simplify paths
  sed -i \
    -e 's:^MANDIR.*:MANDIR  = $(PREFIX)/share/man/man1:' \
    -e 's:^LIBDIR.*:LIBDIR = $(PREFIX)/lib/lout:' \
    -e 's:^LOUTLIBDIR.*:LOUTLIBDIR = $(LIBDIR):' \
    makefile

  # TODO generate documentation
  #-e 's:^LOUTDOCDIR.*:LOUTDOCDIR = $(PREFIX)/share/doc/lout:' \
}

build() {
  cd "$pkgname-$pkgver"

  make PDF_COMPRESSION=1 ZLIB=-lz PREFIX=/usr all
}

package() {
  cd "$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install installman
}

