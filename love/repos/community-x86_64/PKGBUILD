# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Linus Sj√∂gren <thelinx@unreliablepollution.net>
# Contributor: Eric Forgeot < http://anamnese.online.fr >, dreeze
pkgname=love
pkgver=11.4
pkgrel=1
pkgdesc="An open-source 2D game engine which uses the versatile Lua scripting language to create dynamic gaming experiences"
arch=(x86_64)
url="http://love2d.org/"
license=('zlib')
depends=('luajit' 'freetype2' 'mpg123' 'openal' 'libvorbis' 'libmodplug' 'sdl2' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils' 'libtheora')
source=("https://github.com/love2d/love/releases/download/${pkgver}/love-${pkgver}-linux-src.tar.gz")
sha512sums=('a1669e353c216b0f8de862eef877a178618499c38b5ad91143ee64e543affea8fccb13af4ea4957d8698c65dd703e45095ae1069601c4d0ff98d34d71d70ffd3')

build() {
  cd "$srcdir"/love-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/love-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm0644 "license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
