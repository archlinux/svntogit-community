# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Linus Sj√∂gren <thelinx@unreliablepollution.net>
# Contributor: Eric Forgeot < http://anamnese.online.fr >, dreeze
pkgname=love
pkgver=0.8.0
pkgrel=2
pkgdesc='Open-source 2D game engine which uses the versatile Lua scripting language to create dynamic gaming experiences'
arch=(i686 x86_64)
url="http://love2d.org/"
license=('zlib')
depends=('luajit' 'physfs' 'freetype2' 'devil' 'mpg123' 'openal' 'libvorbis' 'libmodplug' 'sdl' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils')
install=love.install
source=("https://bitbucket.org/rude/love/downloads/love-${pkgver}-linux-src.tar.gz"
        "https://bitbucket.org/rude/love/raw/$pkgver/license.txt"
        "https://bitbucket.org/rude/love/raw/$pkgver/platform/unix/app.svg"
        "https://bitbucket.org/rude/love/raw/$pkgver/platform/unix/game.svg"
        "https://bitbucket.org/rude/love/raw/$pkgver/platform/unix/love.desktop"
        "https://bitbucket.org/rude/love/raw/$pkgver/platform/unix/love.xml")
md5sums=('9db9c32585fc7c7da3eba7e438783099'
         'a4890908149d91bc042b30d00c121c58'
         'a1e19f91420cc519a683af360f5b1120'
         '16f2ecc899c9ffc8b7b7c807f8967861'
         '971bec1bffe4f424972eef2a26d27bec'
         'b4f00fb1cb80057a0a371a994100d418')

build() {
  cd "$srcdir"/love-$pkgver
  #./platform/unix/automagic
  ./configure --prefix=/usr --with-luajit #LDFLAGS="$LDFLAGS -Wl,--no-as-needed"
  make
}

package() {
  cd "$srcdir"/love-$pkgver

  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  mkdir -p "$pkgdir/usr/share/icons/hicolor/scalable/mimetypes"
  mkdir -p "$pkgdir/usr/share/icons/hicolor/scalable/apps"
  mkdir -p "$pkgdir/usr/share/applications"
  mkdir -p "$pkgdir/usr/share/mime/packages"

  install -Dm0644 "$srcdir/license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm0644 "$srcdir/app.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/love.svg"
	install -Dm0644 "$srcdir/game.svg" "$pkgdir/usr/share/icons/hicolor/scalable/mimetypes/gnome-mime-application-x-love-game.svg"
  install -Dm0644 "$srcdir/love.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm0644 "$srcdir/love.xml" "$pkgdir/usr/share/mime/packages/love.xml"
}

# vim:set ts=2 sw=2 et:
