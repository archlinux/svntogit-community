# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrcalc
pkgver=1.2
pkgrel=2
pkgdesc="Littlewood-Richardson calculator"
arch=('x86_64')
url="http://math.rutgers.edu/~asbuch/lrcalc/"
license=('GPL2')
depends=('glibc')
source=("https://math.rutgers.edu/~asbuch/lrcalc/$pkgname-$pkgver.tar.gz"
	fix-include-syntax.patch::"https://bitbucket.org/asbuch/lrcalc/commits/226981a0/raw/")
md5sums=('6bba16c0cca9debccd0af847bd3d4a23'
         '9dd3a3b8323db94bd6ab6c31d1a8636d')

prepare() {
  cd $pkgname-$pkgver
# Fix include syntax
  patch -p1 -i ../fix-include-syntax.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
