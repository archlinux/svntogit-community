# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrs
pkgver=061
pkgrel=1
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(i686 x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$pkgver.tar.gz")
md5sums=('0b3687c8693cd7d1f234a3f65e147551')

build() {
  cd lrslib-$pkgver
  make
}

package() {
  cd lrslib-$pkgver

  mkdir -p "$pkgdir"/usr/{lib,bin,include/lrslib}
  cp lrs redund 2nash lrsnash "$pkgdir"/usr/bin/
  install -m644 liblrsgmp.a "$pkgdir"/usr/lib
  install -m644 *.h "$pkgdir"/usr/include/lrslib
# Compatibility symlink
  ln -s /usr/bin/lrsnash "$pkgdir"/usr/bin/nash
}

