# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrs
pkgver=062
pkgrel=3
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(i686 x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$pkgver.tar.gz")
sha256sums=('adf92f9c7e70c001340b9c28f414208d49c581df46b550f56ab9a360348e4f09')

prepare() {
  cd lrslib-$pkgver
# Don't hardcode cflags
  sed -e '/CFLAGS=/d' -i makefile
}

build() {
  cd lrslib-$pkgver
  CFLAGS+=" -fPIC" make
}

package() {
  cd lrslib-$pkgver

  mkdir -p "$pkgdir"/usr/{lib,bin,include/lrslib}
  cp lrs redund 2nash lrsnash nashdemo "$pkgdir"/usr/bin/
  install -m644 liblrsgmp.a "$pkgdir"/usr/lib
  install -m644 *.h "$pkgdir"/usr/include/lrslib
}

