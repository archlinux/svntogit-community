# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=lsp-plugins
pkgname=(lsp-plugins lsp-plugins-docs)
pkgver=1.1.31
pkgrel=1
pkgdesc="Collection of free plugins compatible with LADSPA, LV2 and LinuxVST"
arch=(x86_64)
url="https://lsp-plug.in"
license=(LGPL3)
makedepends=(cairo gcc-libs glibc hicolor-icon-theme libglvnd libx11 jack ladspa libsndfile lv2 php)
checkdepends=(lv2lint)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/sadko4u/${pkgname}/archive/${pkgver}/${pkgver}.tar.gz"
  "$pkgbase-1.1.30-shared_object_ldflags.patch"
)
sha512sums=('6fb56ae40f25c66d770c495377ac43f6bfed1c74d9a1a49a0daee5393a0c2799cbd431597abd571a1766243823fb431eb31ca13cc7090604c9e316e5723caa94'
            '4ffd405bccf5d86e9c88a8de86da804914fe955ecbfac56b6b323f2ed9a34e9a65bb27357ec9fa13fc3d00c5439efa44d4d60fbf212675aecc4282e5ec920b5c')
b2sums=('194b1aa48083d66b2a84e9a6776e59cc1e34d2059b16efe0dc484dfb9583ea4d6aebe073412981ece09249986b43ebe83cce8f79816042b03612c76d48349f8d'
        'c63f14733a8c2ff31fc35a532c5572046d002a6ad812988694dc528c77d727a902d8022060b13a4ac93fbe3cbc144c89379c4496f9aa5840b4a3dfc27e9102ac')

prepare() {
  cd "$pkgbase-$pkgver"
  patch -Np1 -i ../"$pkgbase-1.1.30-shared_object_ldflags.patch"
}

build() {
  make -C "$pkgbase-$pkgver"
  make -C "$pkgbase-$pkgver" test
}

check() {
  local _plugin
  local _lv2_uri="http://lsp-plug.in/plugins/lv2/"
  local _lv2_plugins=('art_delay_mono' 'art_delay_stereo' 'comp_delay_mono'
  'comp_delay_stereo' 'comp_delay_x2_stereo' 'compressor_lr' 'compressor_mono'
  'compressor_ms' 'compressor_stereo' 'crossover_lr' 'crossover_mono'
  'crossover_ms' 'crossover_stereo' 'dyna_processor_lr' 'dyna_processor_mono'
  'dyna_processor_ms' 'dyna_processor_stereo' 'expander_lr' 'expander_mono'
  'expander_ms' 'expander_stereo' 'gate_lr' 'gate_mono' 'gate_ms' 'gate_stereo'
  'graph_equalizer_x16_lr' 'graph_equalizer_x16_mono' 'graph_equalizer_x16_ms'
  'graph_equalizer_x16_stereo' 'graph_equalizer_x32_lr'
  'graph_equalizer_x32_mono' 'graph_equalizer_x32_ms'
  'graph_equalizer_x32_stereo' 'impulse_responses_mono'
  'impulse_responses_stereo' 'impulse_reverb_mono' 'impulse_reverb_stereo'
  'latency_meter' 'limiter_mono' 'limiter_stereo' 'loud_comp_mono'
  'loud_comp_stereo' 'mb_compressor_lr' 'mb_compressor_mono' 'mb_compressor_ms'
  'mb_compressor_stereo' 'mb_expander_lr' 'mb_expander_mono' 'mb_expander_ms'
  'mb_expander_stereo' 'mb_gate_lr' 'mb_gate_mono' 'mb_gate_ms'
  'mb_gate_stereo' 'multisampler_x12' 'multisampler_x12_do' 'multisampler_x24'
  'multisampler_x24_do' 'multisampler_x48' 'multisampler_x48_do'
  'oscillator_mono' 'oscilloscope_x1' 'oscilloscope_x2' 'oscilloscope_x4'
  'para_equalizer_x16_lr' 'para_equalizer_x16_mono' 'para_equalizer_x16_ms'
  'para_equalizer_x16_stereo' 'para_equalizer_x32_lr' 'para_equalizer_x32_mono'
  'para_equalizer_x32_ms' 'para_equalizer_x32_stereo' 'phase_detector'
  'profiler_mono' 'profiler_stereo' 'room_builder_mono' 'room_builder_stereo'
  'sampler_mono' 'sampler_stereo' 'sc_compressor_lr' 'sc_compressor_mono'
  'sc_compressor_ms' 'sc_compressor_stereo' 'sc_dyna_processor_lr'
  'sc_dyna_processor_mono' 'sc_dyna_processor_ms' 'sc_dyna_processor_stereo'
  'sc_expander_lr' 'sc_expander_mono' 'sc_expander_ms' 'sc_expander_stereo'
  'sc_gate_lr' 'sc_gate_mono' 'sc_gate_ms' 'sc_gate_stereo' 'sc_limiter_mono'
  'sc_limiter_stereo' 'sc_mb_compressor_lr' 'sc_mb_compressor_mono'
  'sc_mb_compressor_ms' 'sc_mb_compressor_stereo' 'sc_mb_expander_lr'
  'sc_mb_expander_mono' 'sc_mb_expander_ms' 'sc_mb_expander_stereo'
  'sc_mb_gate_lr' 'sc_mb_gate_mono' 'sc_mb_gate_ms' 'sc_mb_gate_stereo'
  'slap_delay_mono' 'slap_delay_stereo' 'spectrum_analyzer_x1'
  'spectrum_analyzer_x12' 'spectrum_analyzer_x16' 'spectrum_analyzer_x2'
  'spectrum_analyzer_x4' 'spectrum_analyzer_x8' 'surge_filter_mono'
  'surge_filter_stereo' 'trigger_midi_mono' 'trigger_midi_stereo'
  'trigger_mono' 'trigger_stereo')

  (
    cd "$pkgbase-$pkgver"
    ./.test/lsp-plugins-test utest --nofork --debug --verbose || echo "Known to fail: https://github.com/sadko4u/lsp-plugins/issues/218"
  )

  mkdir -vp "$pkgbase-$pkgver/test/usr/bin"
  make -C "$pkgbase-$pkgver" PREFIX='/usr' DESTDIR="test" install_lv2
  for _plugin in "${_lv2_plugins[@]}"; do
    lv2lint -Mpack -I "$pkgbase-$pkgver/test/usr/lib/lv2/${pkgname}.lv2/" "${_lv2_uri}${_plugin}"
  done
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_lsp-plugins() {
  groups=(ladspa-plugins lv2-plugins pro-audio vst-plugins)
  depends=(cairo gcc-libs glibc hicolor-icon-theme libglvnd libsndfile.so libx11)
  optdepends=(
    'jack: for standalone applications'
    'ladspa-host: for LADSPA plugins'
    'lsp-plugins-docs: for documentation'
    'lv2-host: for LV2 plugins'
    'vst-host: for VST plugins'
  )

  make -C "$pkgbase-$pkgver" PREFIX='/usr' DESTDIR="$pkgdir/" install
  make -C "$pkgbase-$pkgver" PREFIX='/usr' DESTDIR="$pkgdir/" install_xdg
  _pick docs "${pkgdir}/usr/share/doc"
}

package_lsp-plugins-docs() {
  pkgdesc+=" (documentation)"

  mv -v docs/* "$pkgdir"
}
