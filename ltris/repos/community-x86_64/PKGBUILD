# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: SmackleFunky <smacklefunky@optusnet.com.au>

pkgname=ltris
pkgver=1.2.5
pkgrel=1
pkgdesc="A tetris clone where you have a bowl with blocks falling down"
arch=('x86_64')
url="http://lgames.sourceforge.net/index.php?project=LTris"
license=('GPL')
depends=('sdl_mixer')
backup=('var/games/ltris.hscr')
install=$pkgname.install
source=(https://downloads.sourceforge.net/lgames/$pkgname-$pkgver.tar.gz)
sha512sums=('07e4165c026d62847a50daaaeeacb493cf7fe62fcf7e2f6ec42b80b6549f2ca36a716d8c6242a9fa7bb0b1240877af745134e01f0812eacfd54b7b68575e001a')
b2sums=('1771f47cca79884a292ac1af9781b29c3d17e0bfc8ed90bbc137896880391c7bb0b76c0e52ae364031ba835251cd9e7a4e88e81a88ca38100041446062fbb457')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|-Wno-format||' -i configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --localstatedir=/var/games
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/pixmaps
  install -m644 icons/ltris{16,32,48}.xpm "${pkgdir}"/usr/share/pixmaps
  #FS#37951 fix
  chmod 775 "${pkgdir}"/var/games
}
