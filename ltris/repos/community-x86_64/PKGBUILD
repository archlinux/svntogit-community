# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: SmackleFunky <smacklefunky@optusnet.com.au>

pkgname=ltris
pkgver=1.2.2
pkgrel=1
pkgdesc="A tetris clone where you have a bowl with blocks falling down"
arch=('x86_64')
url="http://lgames.sourceforge.net/index.php?project=LTris"
license=('GPL')
depends=('sdl_mixer')
backup=('var/games/ltris.hscr')
install=$pkgname.install
changelog=$pkgname.changelog
source=(https://downloads.sourceforge.net/lgames/$pkgname-$pkgver.tar.gz)
sha512sums=('9d4ba4f2fb5c50a20f1f3a12ea86ffbf3c4d329588769f16c3bc90580eb79c18d15f8faa937b44ef8e30629b7221fee4b8ee5efdda6aed789161f2845e28c659')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --localstatedir=/var/games
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/pixmaps
  install -m644 icons/ltris{16,32,48}.xpm "${pkgdir}"/usr/share/pixmaps
#FS#37951 fix  
  chmod 775 "${pkgdir}"/var/games  
}
