# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: SmackleFunky <smacklefunky@optusnet.com.au>

pkgname=ltris
pkgver=1.2.4
pkgrel=1
pkgdesc="A tetris clone where you have a bowl with blocks falling down"
arch=('x86_64')
url="http://lgames.sourceforge.net/index.php?project=LTris"
license=('GPL')
depends=('sdl_mixer')
backup=('var/games/ltris.hscr')
install=$pkgname.install
source=(https://downloads.sourceforge.net/lgames/$pkgname-$pkgver.tar.gz)
sha512sums=('58781aff51d212c1fcaa58f07aea72f51311ab9ccc96e01adce226bdefb1c988898b87018c29bd240cac51627322564bf20358327b633c763785869267468076')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|-Wno-format||' -i configure
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --localstatedir=/var/games
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/pixmaps
  install -m644 icons/ltris{16,32,48}.xpm "${pkgdir}"/usr/share/pixmaps
#FS#37951 fix  
  chmod 775 "${pkgdir}"/var/games  
}
