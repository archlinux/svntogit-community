# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Philippe Proulx <pproulx@efficios.com>
# Contributor: Manuel Mendez <mmendez534 at gmail dot com>

pkgbase='lttng-ust'
pkgname=('lttng-ust' 'python-lttngust')
pkgver=2.12.0
pkgrel=3
arch=('x86_64')
url="https://lttng.org/"
license=('LGPL2.1' 'GPL2' 'MIT')
makedepends=('asciidoc' 'xmlto' 'libxml2' 'python' 'liburcu>=0.7.2' 'util-linux' 'numactl')
source=("https://lttng.org/files/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('d69772170f57b9669b99cea372ca537777151a31b82002e1e5ec80da3ceb302c23f0f85d63a2c95c90a375b879e266aa5297e7d6444050dd40feb1253d863801'
            'SKIP')
validpgpkeys=('2A0B4ED915F2D3FA45F5B16217280A9781186ACF')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # build lttng-ust
    PYTHON="/usr/bin/python" ./configure \
        --prefix='/usr' \
        --enable-man-pages
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}

package_lttng-ust() {
    pkgdesc="LTTng user space tracing libraries for LTTng"
    depends=('liburcu>=0.7.2' 'util-linux' 'python' 'numactl')
    optdepends=('babeltrace2: trace viewer')
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-lttngust() {
    pkgdesc='LTTng-UST Python Bindings'
    depends=('lttng-ust' 'python')
    cd "${srcdir}/${pkgbase}-${pkgver}/python-lttngust"
    make DESTDIR="${pkgdir}" PYTHON="/usr/bin/python" install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
