# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: Lucas Hermann Negri <kkndrox@gmail.com>

pkgbase=lua-bitop
pkgname=(lua51-bitop lua52-bitop)
pkgver=1.0.2
pkgrel=8
pkgdesc='Bitwise operations on numbers'
arch=('x86_64' 'i686')
license=('MIT')
url='https://bitop.luajit.org/'
makedepends=('lua51' 'lua52')
source=("https://bitop.luajit.org/download/LuaBitOp-$pkgver.tar.gz")
sha256sums=('1207c9293dcd52eb9dca6538d1b87352bd510f4e760938f5048433f7f272ce99')

prepare() {
  cp -a "LuaBitOp-$pkgver" "LuaBitOp51-$pkgver"
  cp -a "LuaBitOp-$pkgver" "LuaBitOp52-$pkgver"
}

build() {
  make -C "LuaBitOp51-$pkgver" INCLUDES=' -I/usr/include/lua5.1' LUA='lua5.1'
  make -C "LuaBitOp52-$pkgver" INCLUDES=' -I/usr/include/lua5.2' LUA='lua5.2'
}

package_lua-bitop() {
  depends=('lua')

  cd "LuaBitOp-$pkgver"
  install -Dm 644 bit.so "$pkgdir/usr/lib/lua/5.3/bit.so"
  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -s '../lua/LICENSE' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-bitop() {
  depends=('lua51')
  conflicts=('luabitop')
  replaces=('luabitop')

  cd "LuaBitOp51-$pkgver"
  install -Dm 644 bit.so "$pkgdir/usr/lib/lua/5.1/bit.so"
  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -s '../lua51/COPYRIGHT' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-bitop() {
  depends=('lua52')
  conflicts=('lua-bitop')
  replaces=('lua-bitop')

  cd "LuaBitOp52-$pkgver"
  install -Dm 644 bit.so "$pkgdir/usr/lib/lua/5.2/bit.so"
  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -s '../lua52/LICENSE' "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:ts=2 sw=2 et:
