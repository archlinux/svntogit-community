# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=lua-lut
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
pkgver=1.2.1
_rockrel=1
pkgrel=1
pkgdesc='Lubyk utility module'
arch=(any)
url="https://lubyk.github.io/lubyk/$_rockname.html"
license=(MIT)
_luadeps=(filesystem
          lub)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
checkdepends=("${_luadeps[@]/#/lua-}")
_archive="$_rockname-REL-$pkgver"
source=("https://github.com/lubyk/$_rockname/archive/REL-$pkgver/$_archive.tar.gz")
sha256sums=('6f7fbe620995b49d59127cf354d9100c218744b91327345c2fa7777fb9871413')

check() {
	cd "$_archive"
	lua test/all.lua
}

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}" "${_luadeps[@]/#/${pkgname%-*}-}")
	luarocks --lua-version="$1" --tree="$pkgdir/usr/" \
		make --deps-mode=none --no-manifest "$_rockname-$pkgver-$_rockrel.rockspec"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua-lut() {
	_package 5.4
}

package_lua53-lut() {
	_package 5.3
}

package_lua52-lut() {
	_package 5.2
}

package_lua51-lut() {
	_package 5.1
}
