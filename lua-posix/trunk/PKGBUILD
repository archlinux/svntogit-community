# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Sébastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgname=lua-posix
pkgver=32
pkgrel=1
pkgdesc='POSIX library for Lua'
arch=('x86_64' 'i686')
url='https://github.com/luaposix/luaposix'
license=('GPL' 'LGPL')
depends=('lua')
makedepends=('lua' 'git' 'help2man')
options=('!makeflags')
source=("$pkgname::git://github.com/luaposix/luaposix.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  ./bootstrap
}

build() {
  cd "$pkgname"

  ./configure \
      LUA=/usr/bin/lua \
      --prefix=/usr \
      --libdir=/usr/lib/lua/5.2 \
      --datadir=/usr/share/lua/5.2 \
      --docdir=/usr/share/doc/lua-posix
  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
