# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgbase=lua-term
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
pkgver=0.7
_pkgver=0.07
_rockrel=1
pkgrel=7
pkgdesc='Terminal functions for Lua'
arch=(x86_64)
url="https://github.com/hoelzro/$pkgbase"
license=(MIT)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$pkgbase-$_pkgver"
source=("$_archive.tgz::$url/archive/$_pkgver.tar.gz")
sha256sums=('c1a1d0c57107147ea02878a50b768d1c3c13aca2769b026c5bb7a84119607f30')

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}")
	luarocks --lua-version="$1" --tree="$pkgdir/usr/" \
		CFLAGS="$CPPFLAGS $CFLAGS -fPIC" \
		LIBFLAG="$LDFLAGS -shared" \
		make --deps-mode=none --no-manifest "$pkgbase-$pkgver-$_rockrel.rockspec"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" COPYING
}

package_lua-term() {
	_package 5.4
}

package_lua51-term() {
	_package 5.1
}

package_lua52-term() {
	_package 5.2
}

package_lua53-term() {
	_package 5.3
}
