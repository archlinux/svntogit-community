# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Robert Knauer <robert@privatdemail.net>

pkgname=lua51-dbi
pkgver=0.5
pkgrel=3
pkgdesc="Database interface library for Lua (5.1)"
arch=('x86_64')
url="https://code.google.com/archive/p/luadbi/"
license=('MIT')
makedepends=('sqlite3' 'libmariadbclient' 'postgresql')
depends=('lua51')
optdepends=('sqlite3: sqlite3 Support'
            'postgresql-libs: postgresql Support'
            'libmariadbclient: mysql Support')
source=("https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/luadbi/luadbi.$pkgver.tar.gz")
sha1sums=('c8c650c168599e3ac97634a5ffb28aeaaa4fb635')

prepare() {
  sed -i 's|#!/usr/bin/lua|#!/usr/bin/lua5.1|g' DBI.lua
  sed -i 's|-I /usr/include/postgresql/|-I /usr/include/postgresql/server/|g' Makefile
}

build() {
  make free
}

package() {
  install -Dm644 DBI.lua "${pkgdir}/usr/share/lua/5.1/DBI.lua"
  install -dm755 "${pkgdir}/usr/lib/lua/5.1/"
  install -Dm644 *.so "${pkgdir}/usr/lib/lua/5.1/"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
