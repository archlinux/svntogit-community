# $Id$
# Maintainer: Jakob Gruber <jakob.gruber@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: rayte <rabyte.at.gmail.dot.com>

pkgname=luabind
pkgver=0.9.1
pkgrel=2
pkgdesc='Library that helps you create bindings between C++ and Lua'
arch=('i686' 'x86_64')
url="http://www.rasterbar.com/products/luabind.html"
license=('MIT')
makedepends=('unzip' 'boost' 'boost-build')
depends=('lua' 'gcc-libs')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "luabind_boost.patch")

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 -i ${srcdir}/luabind_boost.patch

  # build luabind
  bjam release --prefix=${pkgdir}/usr/ link=static,shared
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  bjam release --prefix=${pkgdir}/usr/ link=static,shared install

  # copy license
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE \
      ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

md5sums=('8a323fb51d07dad5cfdb12c432793d55'
         'd62ae6fc635300bc659df9dbb587ac53')
