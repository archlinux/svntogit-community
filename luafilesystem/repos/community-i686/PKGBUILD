# $Id$
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=luafilesystem
pkgname=(lua-filesystem lua51-filesystem)
pkgver=1.6.3
pkgrel=1
pkgdesc='File System Library for the Lua Programming Language'
arch=('i686' 'x86_64')
url='http://www.keplerproject.org/luafilesystem/'
license=('MIT')
makedepends=(lua lua51)
#source=(http://github.com/downloads/keplerproject/$pkgbase/$pkgbase-$pkgver.tar.gz
source=(https://github.com/keplerproject/luafilesystem/archive/v_${pkgver//./_}.tar.gz
        LICENSE)
md5sums=('d0552c7e5a082f5bb2865af63fb9dc95'
         'beb2b58c4b5c2782304a5b0267a5651b')

build() {
  msg2 "Building $pkgbase with Lua 5.1"
  cp -a $pkgbase-v_${pkgver//./_} $pkgbase-v_${pkgver//./_}-51
  cd $pkgbase-v_${pkgver//./_}-51
  make LUA_INC=/usr/include/lua5.1

  msg2 "Building $pkgbase with Lua 5.2"
  cd "$srcdir"/$pkgbase-v_${pkgver//./_}
  make
}

package_lua-filesystem() {
  depends=('lua')

  cd $pkgbase-v_${pkgver//./_}
  make PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.2' install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_lua51-filesystem() {
  depends=('lua51')
  conflicts=('luafilesystem')
  replaces=('luafilesystem')

  cd $pkgbase-v_${pkgver//./_}-51
  make PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.1' install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
