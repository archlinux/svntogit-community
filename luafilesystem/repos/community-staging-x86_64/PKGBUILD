# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=luafilesystem
pkgname=(lua-filesystem lua51-filesystem)
pkgver=1.6.2
pkgrel=1
pkgdesc='File System Library for the Lua Programming Language'
arch=('i686' 'x86_64')
url='http://www.keplerproject.org/luafilesystem/'
license=('MIT')
replaces=(luafilesystem)
conflicts=(luafilesystem)
makedepends=(lua lua51)
source=(http://github.com/downloads/keplerproject/$pkgbase/$pkgbase-$pkgver.tar.gz
        LICENSE)
md5sums=('4e7ec93678c760c4e42cea7d28aafa13'
         'beb2b58c4b5c2782304a5b0267a5651b')

build() {
  msg2 "Building $pkgbase with lua 5.1"
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-51
  cd $pkgbase-$pkgver-51
  make LUA_INC=/usr/include/lua5.1
  msg2 "Building $pkgbase with lua 5.2"
  cd "$srcdir/$pkgbase-$pkgver"
  make
}

package_lua-filesystem() {
  depends=('lua')

  cd $pkgbase-$pkgver
  make PREFIX=$pkgdir/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.2' install
  install -Dm644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

package_lua51-filesystem() {
  depends=('lua51')

  cd $pkgbase-$pkgver-51
  make PREFIX=$pkgdir/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.1' install
  install -Dm644 $srcdir/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
