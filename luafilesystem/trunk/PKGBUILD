# $Id$
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=luafilesystem
pkgname=(lua-filesystem lua51-filesystem lua52-filesystem)
pkgver=1.7.0.2
_tag=${pkgver//./_}
pkgrel=1
pkgdesc='File System Library for the Lua Programming Language'
arch=('i686' 'x86_64')
url='http://keplerproject.github.io/luafilesystem/'
license=('MIT')
makedepends=(lua lua51 lua52)
#source=(http://github.com/downloads/keplerproject/$pkgbase/$pkgbase-$pkgver.tar.gz
source=(luafilesystem-${_tag}.tar.gz::https://github.com/keplerproject/luafilesystem/archive/v${_tag}.tar.gz
        LICENSE)
sha256sums=('23b4883aeb4fb90b2d0f338659f33a631f9df7a7e67c54115775a77d4ac3cc59'
            'bab7b7a48ca7cb322a35c8975cd2c08d051ce6e20e8db56c2e6017726acdf9ae')

build() {
  cd "$srcdir"
  msg2 "Building $pkgbase with Lua 5.1"
  cp -a $pkgbase-${_tag} $pkgbase-${_tag}-51
  cd $pkgbase-${_tag}-51
  make LUA_INC=/usr/include/lua5.1

  msg2 "Building $pkgbase with Lua 5.2"
  cd "$srcdir"
  cp -a $pkgbase-${_tag} $pkgbase-${_tag}-52
  cd $pkgbase-${_tag}-52
  make LUA_INC=/usr/include/lua5.2

  msg2 "Building $pkgbase with Lua 5.3"
  cd "$srcdir"/$pkgbase-${_tag}
  make
}

package_lua-filesystem() {
  depends=('lua')

  cd $pkgbase-${_tag}
  make PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.3' install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_lua51-filesystem() {
  depends=('lua51')
  conflicts=('luafilesystem')
  replaces=('luafilesystem')

  cd $pkgbase-${_tag}-51
  make PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.1' install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_lua52-filesystem() {
  depends=('lua52')

  cd $pkgbase-${_tag}-52
  make PREFIX="$pkgdir"/usr LUA_LIBDIR='$(PREFIX)/lib/lua/5.2' install
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
