# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: J. W. Birdsong <jwbirdsong AT gmail DOT com>

pkgname=luakit
pkgver=2012.09.13.r1
_pkgver=2012.09.13-r1
pkgrel=7
pkgdesc='Fast, small, Webkit based browser framework extensible by Lua'
arch=('i686' 'x86_64')
url='http://mason-larobina.github.com/luakit/'
license=('GPL3')
depends=('webkitgtk2' 'lua51-filesystem' 'libunique' 'desktop-file-utils' 'luajit')
makedepends=('help2man')
backup=(etc/xdg/luakit/binds.lua etc/xdg/luakit/globals.lua
        etc/xdg/luakit/modes.lua etc/xdg/luakit/rc.lua etc/xdg/luakit/theme.lua
        etc/xdg/luakit/webview.lua etc/xdg/luakit/window.lua)
options=(!makeflags)
install=luakit.install
source=($pkgname-$pkgver.tar.gz::https://github.com/mason-larobina/luakit/tarball/$_pkgver)
md5sums=('77b02e6d19ba021409f1778fddd94a42')

prepare() {
  cd mason-larobina-luakit-*
  sed -i '1s,lua,luajit,' build-utils/gentokens.lua
}

build() {
  cd mason-larobina-luakit-*
  make USE_LUAJIT=1 PREFIX=/usr all
}

package() {
  cd mason-larobina-luakit-*
  make PREFIX=/usr DESTDIR="$pkgdir" install

  chmod -x "$pkgdir"/usr/share/pixmaps/luakit.png # FS#32938
}
