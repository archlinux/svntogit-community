# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgbase=luaposix
pkgname=('lua-posix' 'lua51-posix')
pkgver=31
pkgrel=1
pkgdesc='A POSIX library for Lua programming language'
arch=('i686' 'x86_64')
url='https://github.com/luaposix/luaposix'
license=('GPL' 'LGPL')
makedepends=('lua' 'lua51' 'lua51-bitop' 'git' 'help2man')
options=('!makeflags')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/$pkgbase/$pkgbase/archive/v$pkgver.tar.gz"
        'git+git://git.sv.gnu.org/gnulib'
        'git+git://github.com/gvvaughan/slingshot')
md5sums=('7b17b9a3f25d9658de967c6bfd50c138'
         'SKIP'
         'SKIP')

prepare() {
  # replace git submodules by source one.
  # this allow us to cache and avoid clone each time
  for _rep in gnulib slingshot; do
    rmdir $pkgbase-$pkgver/$_rep
    ln -s "$srcdir/$_rep" $pkgbase-$pkgver/$_rep
  done
  # bootstraping
  (cd $pkgbase-$pkgver && ./bootstrap)
  # copy source for lua 5.1 build
  cp -r $pkgbase-$pkgver $pkgbase-$pkgver-51
}

build() {
  msg2 'Build with lua 5.2'
  cd $pkgbase-$pkgver
  ./configure \
      LUA=/usr/bin/lua \
      --prefix=/usr \
      --libdir=/usr/lib/lua/5.2 \
      --datadir=/usr/share/lua/5.2 \
      --docdir=/usr/share/doc/lua-posix
  make
  msg2 'Build with lua 5.1'
  cd "$srcdir/$pkgbase-$pkgver-51"
  ./configure \
      LUA=/usr/bin/lua5.1 \
      LUA_INCLUDE=-I/usr/include/lua5.1 \
      --prefix=/usr \
      --libdir=/usr/lib/lua/5.1 \
      --datadir=/usr/share/lua/5.1 \
      --docdir=/usr/share/doc/lua51-posix
  make
}

package_lua-posix() {
  depends=('lua')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir/" install
}

package_lua51-posix() {
  depends=('lua51' 'lua51-bitop')
  conflicts=('luaposix')
  replaces=('luaposix')

  cd $pkgbase-$pkgver-51
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
