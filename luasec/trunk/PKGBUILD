# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Dwayne Bent <dbb.1@liqd.org>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>

pkgbase=luasec
pkgname=(lua-sec lua51-sec lua52-sec)
epoch=2
pkgver=0.6
pkgrel=2
pkgdesc='Lua bindings for OpenSSL library to provide TLS/SSL communication.'
arch=('i686' 'x86_64')
url='https://github.com/brunoos/luasec/wiki'
license=('MIT')
makedepends=('openssl' 'lua' 'lua-socket' 'lua51' 'lua51-socket' 'lua52' 'lua52-socket')
options=('!buildflags')
source=("https://github.com/brunoos/luasec/archive/luasec-$pkgver.tar.gz"
        luasec-openssl-1.1.patch)
md5sums=('14e1aef6d2aae96bbf98afc6b6634af2'
         '729e64adcab9b4bd9f3d68521706ef0f')

prepare() {
  # openssl 1.1 compatibility
  cd luasec-luasec-$pkgver
  patch -p1 -i ../luasec-openssl-1.1.patch
  cd ..

  cp -a luasec-luasec-$pkgver luasec-luasec-$pkgver-51
  cp -a luasec-luasec-$pkgver luasec-luasec-$pkgver-52
}

build() {
  msg2 'Building with lua 5.3'
  cd "$srcdir"/luasec-luasec-$pkgver
  make linux DEFS="-DWITH_LUASOCKET -DOPENSSL_NO_SSL3"

  msg2 'Building with lua 5.1'
  cd "$srcdir"/luasec-luasec-$pkgver-51
  find . -type f -name \*.[ch] -exec sed -i \
    -e 's|include .lua.h.|include "lua5.1/lua.h"|g' \
    -e 's|include .lualib.h.|include "lua5.1/lualib.h"|g' \
    -e 's|include .luaconf.h.|include "lua5.1/luaconf.h"|g' \
    -e 's|include .lauxlib.h.|include "lua5.1/lauxlib.h"|g' \
    {} \;
  make linux DEFS="-DWITH_LUASOCKET -DOPENSSL_NO_SSL3"

  msg2 'Building with lua 5.2'
  cd "$srcdir"/luasec-luasec-$pkgver-52
  find . -type f -name \*.[ch] -exec sed -i \
    -e 's|include .lua.h.|include "lua5.2/lua.h"|g' \
    -e 's|include .lualib.h.|include "lua5.2/lualib.h"|g' \
    -e 's|include .luaconf.h.|include "lua5.2/luaconf.h"|g' \
    -e 's|include .lauxlib.h.|include "lua5.2/lauxlib.h"|g' \
    {} \;
  make linux DEFS="-DWITH_LUASOCKET -DOPENSSL_NO_SSL3"
}

package_lua-sec() {
  depends=('openssl' 'lua' 'lua-socket')

  install -dm755 "$pkgdir/usr/lib/lua/5.3"
  cd luasec-luasec-$pkgver
  make LUACPATH="$pkgdir/usr/lib/lua/5.3" LUAPATH="$pkgdir/usr/share/lua/5.3" install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-sec() {
  depends=('openssl' 'lua51' 'lua51-socket')
  conflicts=('luasec')
  replaces=('luasec')

  install -dm755 "$pkgdir/usr/lib/lua/5.1"
  cd luasec-luasec-$pkgver-51
  make LUACPATH="$pkgdir/usr/lib/lua/5.1" LUAPATH="$pkgdir/usr/share/lua/5.1" install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-sec() {
  depends=('openssl' 'lua52' 'lua52-socket')

  install -dm755 "$pkgdir/usr/lib/lua/5.2"
  cd luasec-luasec-$pkgver-52
  make LUACPATH="$pkgdir/usr/lib/lua/5.2" LUAPATH="$pkgdir/usr/share/lua/5.2" install
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
