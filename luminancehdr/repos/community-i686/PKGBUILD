# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.1.0
pkgrel=2
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'hicolor-icon-theme' 'libraw' 'libtiff' 'openexr' 'qt')
optdepends=('hugin: align image stack functionality')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/qtpfsgui/luminance-hdr-${pkgver}.tar.gz)
sha256sums=('0b0f124ada6da62c379d6e4bd6fd150643115cb46f05c527c29113f475a5eeac')

build() {
  cd "${srcdir}/luminance-hdr-${pkgver}"

  if [ ${CARCH} = i686 ]; then
    sed -i 's/ -msse2//' project.pro
  fi

  lrelease project.pro
  qmake PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/luminance-hdr-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" install
}
