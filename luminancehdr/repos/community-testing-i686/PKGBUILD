# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.3.0
pkgrel=5
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'lcms2' 'libraw' 'openexr' 'qtwebkit')
makedepends=('cmake')
optdepends=('hugin: align image stack functionality')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://sourceforge.net/projects/qtpfsgui/files/luminance/${pkgver}/luminance-hdr-${pkgver}.tar.bz2)
sha256sums=('dada0795b58843912bcb57ce61dcd615bcb9f9437b610a37ff813ad2c899c676')

build() {
  cd "${srcdir}/luminance-hdr-${pkgver}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/luminance-hdr-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
