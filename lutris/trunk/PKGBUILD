# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lutris
pkgver=0.4.21.1
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'gtk3' 'glib2' 'psmisc' 'cabextract' 'unrar' 'unzip' 'p7zip' 'curl' 'xorg-xrandr')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'xterm' 'python-nose-cover3' 'wine')
optdepends=('wine: Run windows games'
            'wine-staging: Run windows games - Staging patches')
makedepends=('python-setuptools')
source=("$url/releases/${pkgname}_$pkgver.tar.xz")
sha512sums=('4fda42bec6beeee77fe0ba9c8d60b3ff1c8163d02a06405e78f233a23072deaeb9702ff689639d7611f7c6cd49fc9558ad74fc5728de82f236f83cc9a967d97f')

build() {
  cd $pkgname

  python setup.py build
}

check() {
  cd $pkgname

  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

