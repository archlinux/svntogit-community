# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lutris
pkgver=0.4.20
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'gtk3' 'glib2' 'psmisc' 'cabextract' 'unrar' 'unzip' 'p7zip' 'curl')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'xterm' 'xorg-xrandr' 'python-nose-cover3' 'wine')
optdepends=('wine: Run windows games'
            'wine-staging: Run windows games - Staging patches')
makedepends=('python-setuptools')
source=("$url/releases/${pkgname}_$pkgver.tar.xz")
sha512sums=('bdf5568fed1d8eafc2e3c8221b903907e3a4855bb1d880c761e190421f0bfbea9cdc5bd2e6b3e2525f1d1a5a659a520a93d67df1594bfaa575c84f8ceddd3bb5')

build() {
  cd $pkgname

  python setup.py build
}

check() {
  cd $pkgname

  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

