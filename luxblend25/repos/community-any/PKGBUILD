# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxblend25
pkgver=1.6
_pkgver=885740dce55a
_blender=2.78
pkgrel=2
pkgdesc="A LuxRender exporter for Blender."
arch=('any')
url="http://www.luxrender.net/"
license=('GPL')
depends=('blender' 'luxrender' 'luxrays')
source=($pkgname-$pkgver.tar.bz2::https://bitbucket.org/luxrender/luxblend25/get/v${pkgver/./}.tar.bz2 \
        system_config.diff pyluxcore_path.diff luxrender.cfg)
md5sums=('dc7b8776ad579a0c6c6a155fec5cffc1'
         'f42e7bc5476b144cca60bf3e6363158a'
         '3ed96ce1e0ff5338789da698ec11c0fc'
         'ec6b7028e48f73650d9315d47cd14f1d')

prepare() {
  cd "$srcdir"/luxrender-luxblend25-$_pkgver

  # enables loading of the configuration from a system-wide config file
  patch -Np1 < "$srcdir/system_config.diff"
  # change the search path for pylux* so the pylux* can be found in its new location
  #find "$pkgdir/usr/share/blender/$_blender/scripts/addons/luxrender" -name "*.py" \
  #  -exec sed -i 's|from.*import pylux|import pylux|' '{}' ';'
  patch -Np1 < "$srcdir/pyluxcore_path.diff"
}

package() {
  install -d -m755 "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/luxrender-luxblend25-$_pkgver/src/luxrender \
    "$pkgdir"/usr/share/blender/$_blender/scripts/addons

  # install luxrender config file so the luxblend find luxrender automatically
  install -D -m644 luxrender.cfg \
    "$pkgdir"/usr/share/blender/$_blender/scripts/luxrender.cfg
}

# vim:set ts=2 sw=2 et:
