# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxblend25
pkgver=1.5.1
_pkgver=63e44ef60fb4
_blender=2.77
pkgrel=2
pkgdesc="A LuxRender exporter for Blender."
arch=('any')
url="http://www.luxrender.net/"
license=('GPL')
depends=('blender' 'luxrender' 'luxrays')
source=($pkgname-${pkgver}.tar.bz2::https://bitbucket.org/luxrender/luxblend25/get/${_pkgver}.tar.bz2 \
        system_config.diff luxrender.cfg)
md5sums=('1bc83f7b6de2320a371fc44fe9daa4ff'
         'f42e7bc5476b144cca60bf3e6363158a'
         'ec6b7028e48f73650d9315d47cd14f1d')

prepare() {
  cd "$srcdir"/luxrender-luxblend25-$_pkgver

  # enables loading of the configuration from a system-wide config file
  patch -Np1 < "$srcdir/system_config.diff" || true
}

package() {
  install -d -m755 "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/luxrender-luxblend25-$_pkgver/src/luxrender \
    "$pkgdir"/usr/share/blender/$_blender/scripts/addons

  # install luxrender config file so the luxblend find luxrender automatically
  install -D -m644 luxrender.cfg \
    "$pkgdir"/usr/share/blender/$_blender/scripts/luxrender.cfg

  # change the search path for pylux* so the pylux* can be found in its new location
  find "$pkgdir/usr/share/blender/$_blender/scripts/addons/luxrender" -name "*.py" \
    -exec sed -i 's|from.*import pylux|import pylux|' '{}' ';'
}

# vim:set ts=2 sw=2 et:
