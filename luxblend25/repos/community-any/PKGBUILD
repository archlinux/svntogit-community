# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxblend25
pkgver=1.4
_pkgver=a249c861c2f3
_blender=2.75
pkgrel=3
pkgdesc="A LuxRender exporter for Blender."
arch=('any')
url="http://www.luxrender.net/"
license=('GPL')
depends=('blender' 'luxrender')
source=(https://bitbucket.org/luxrender/luxblend25/get/$_pkgver.tar.bz2 \
        system_config.diff luxrender.cfg)
md5sums=('a9ca48c9d091df8dd55ba1b90d377ac2'
         'f42e7bc5476b144cca60bf3e6363158a'
         'ec6b7028e48f73650d9315d47cd14f1d')

prepare() {
  cd "$srcdir"/luxrender-luxblend25-$_pkgver

  # enables loading of the configuration from a system-wide config file
  patch -Np1 < "$srcdir/system_config.diff" || true
}

package() {
  install -d -m755 "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/luxrender-luxblend25-$_pkgver/src/luxrender \
    "$pkgdir"/usr/share/blender/$_blender/scripts/addons

  # install luxrender config file so the luxblend find luxrender automatically
  install -D -m644 luxrender.cfg \
    "$pkgdir"/usr/share/blender/$_blender/scripts/luxrender.cfg

  # change the search path for pylux* so the pylux* can be found in its new location
  find "$pkgdir/usr/share/blender/$_blender/scripts/addons/luxrender" -name "*.py" \
    -exec sed -i 's|from.*import pylux|import pylux|' '{}' ';'
}

# vim:set ts=2 sw=2 et:
