# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=luxblend25
pkgver=1.3.1
_pkgver=ad6536b0dda8
_blender=2.70
pkgrel=1
pkgdesc="A LuxRender exporter for Blender."
arch=('any')
url="http://www.luxrender.net/"
license=('GPL')
depends=('blender' 'luxrender')
source=(https://bitbucket.org/luxrender/luxblend25/get/$_pkgver.tar.bz2 \
        luxrender_path.diff)
md5sums=('389991810953df26f1d849db1c65a129'
         'cea2a59336c28b84258e795f66301918')

prepare() {
  cd "$srcdir/luxrender-luxblend25-$_pkgver"

  # set the default path to luxrender instalation, so the users doesn't have
  # to do that everytime they enable luxrender
  patch -Np1 < "$srcdir/luxrender_path.diff" || true
}

package() {
  install -d -m755 "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/luxrender-luxblend25-$_pkgver/src/luxrender \
    "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  # change the search path in exporter so it finds pylux in its new location
  sed -i 's|from.*import pylux|import pylux|' "$pkgdir/usr/share/blender/$_blender/scripts/addons/luxrender/outputs/pure_api.py"
}

# vim:set ts=2 sw=2 et:
