# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname='luxrays'
pkgver=1.4
_pkgver=7c7127ee1fa4
pkgrel=4
pkgdesc="Accelerate the ray intersection process by using GPUs"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('freetype2' 'libcl' 'libgl' 'libpng' 'openimageio')
makedepends=('cmake' 'boost' 'mesa' 'opencl-headers' 'glew' 'freeglut')
optdepends=('opencl-nvidia: OpenCL support for nVidia GPUs' \
            'amdapp-sdk: OpenCL support for AMD GPUs' \
            'intel-opencl-runtime: OpenCL support for Intel CPUs' \
            'glew: demos (SmallLuxGPU etc.)' 'freeglut: demos (SmallLuxGPU etc.)' \
            'boost-libs: demos (SmallLuxGPU etc.)')
options=('staticlibs')
source=($pkgname-$pkgver.tar.bz2::https://bitbucket.org/luxrender/luxrays/get/${_pkgver}.tar.bz2 \
        force_python3.diff)
md5sums=('2dda3316fb2ae9be6e58b5fa3a35de62'
         '3bfb5594a709d1ea772866d66904b8fd')

prepare() {
  cd "$srcdir"/luxrender-luxrays-$_pkgver

  patch -Np1 < "$srcdir/force_python3.diff" || true
}

build() {
  cd "$srcdir/luxrender-luxrays-$_pkgver"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DLUXRAYS_DISABLE_OPENCL=OFF \
    .
  make
}

package() {
  cd "$srcdir/luxrender-luxrays-$_pkgver"

  install -d -m755 "$pkgdir"/usr/{bin,include,lib}
  install -m755 bin/* "$pkgdir"/usr/bin
  install -m644 lib/* "$pkgdir"/usr/lib
  cp -a include "$pkgdir"/usr

  # install pyluxcore to the Python search path
  install -d -m755 "$pkgdir"/usr/lib/python3.4/
  mv "$pkgdir"/usr/lib/pyluxcore.so "$pkgdir"/usr/lib/python3.4/
}

# vim:set ts=2 sw=2 et:
