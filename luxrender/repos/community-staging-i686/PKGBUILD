# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Initial contributor: flixie <69one@gmx.net>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
pkgname=luxrender
pkgver=0.8
_pkgver=45d3e13eb94c
_luxblend=9cb3fcee0be8
_blender=2.58
pkgrel=7
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'freeimage' 'openexr')
optdepends=('blender: Blender exporter' 'qt: Qt GUI')
makedepends=('cmake' 'boost' 'qt' 'luxrays' 'python')
source=(http://ftp.archlinux.org/other/community/luxrender/lux-"$pkgver".tar.bz2 \
        http://ftp.archlinux.org/other/community/luxrender/luxblend-"$pkgver".tar.bz2)
md5sums=('0f2d856385db72131f51e44a7ee527fa'
         '0c3c8d9efba76133f8c687b5dd2bca8a')

build() {
  cd "$srcdir"/luxrender-lux-$_pkgver

  sed -i 's/FIND_PACKAGE(Boost 1.43 COMPONENTS python REQUIRED)/FIND_PACKAGE(Boost 1.43 COMPONENTS python3 REQUIRED)/' CMakeLists.txt

  export CXXFLAGS="$CXXFLAGS -lpthread"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLUXRAYS_DISABLE_OPENCL=ON \
    -DPYTHON_CUSTOM=ON \
    -DPYTHON_LIBRARIES=/usr/lib/libpython3.2mu.so \
    -DPYTHON_INCLUDE_PATH=/usr/include/python3.2mu/ \
    .
  make
}

package() {
  cd "$srcdir"/luxrender-lux-$_pkgver
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  # install blender exporter
  install -d -m755 "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/luxrender-luxblend25-$_luxblend/src/luxrender \
    "$pkgdir"/usr/share/blender/$_blender/scripts/addons
  cp -a "$srcdir"/luxrender-lux-$_pkgver/pylux.so "$pkgdir"/usr/share/blender/$_blender/scripts/addons/luxrender/
}

# vim:set ts=2 sw=2 et:
