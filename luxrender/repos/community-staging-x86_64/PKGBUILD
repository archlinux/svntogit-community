# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Initial contributor: flixie <69one@gmx.net>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
pkgname=luxrender
pkgver=0.7.1
_pkgver=9206b3ba7011
_luxblend=f93faf8189c1
pkgrel=2
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('i686' 'x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'freeimage')
optdepends=('python2: luxblend' 'qt: Qt GUI' 'wxgtk: wxGTK GUI')
makedepends=('cmake' 'boost' 'mesa' 'wxgtk' 'qt' 'libpng' 'openexr')
source=(http://src.luxrender.net/lux/archive/$_pkgver.tar.bz2
        http://src.luxrender.net/luxblend/archive/$_luxblend.tar.bz2
        libpng.patch)
md5sums=('44b819d37c2cccd8b1a890e6c67061a8'
         'daf70af98123d31303c2c037244c527c'
         '4bbd82a31aa3f6ee2644a712bf2bfc05')
build() {
  cd "$srcdir"/lux-$_pkgver

  patch -Np1 -i "${srcdir}/libpng.patch" || true

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir"/lux-$_pkgver
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  # install blender exporter
  install -d -m755 "$pkgdir"/usr/share/blender/scripts
  install -m644 "$srcdir"/luxblend-$_luxblend/*.py "$pkgdir"/usr/share/blender/scripts
}

# vim:set ts=2 sw=2 et:
