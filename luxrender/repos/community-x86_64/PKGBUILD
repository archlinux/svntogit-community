# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: flixie <69one@gmx.net>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
pkgname=luxrender
pkgver=1.6
_pkgver=b3f85cf7742f
pkgrel=7
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'embree' 'freeimage' 'openexr' 'openimageio' 'libpng' 'opencl-icd-loader' 'libgl' 'fftw')
optdepends=('luxblend25: Blender exporter' 'qt4: Qt GUI' \
            'python: pylux Python interface' \
            'amdapp-sdk: OpenCL support for AMD GPUs' \
            'intel-opencl-runtime: OpenCL support for Intel CPUs')
makedepends=('cmake' 'boost' 'mesa' 'qt4' "luxrays=$pkgver" 'python' 'opencl-headers')
source=($pkgname-$pkgver.tar.bz2::https://bitbucket.org/luxrender/lux/get/v${pkgver/./}.tar.bz2 \
        force_python3.diff)
md5sums=('e40812e4e5a2bb1a58bcbd2a298ba9ee'
         '42692e65eabc5828693e2682e94b7c64')

prepare() {
  cd "$srcdir"/luxrender-lux-$_pkgver

  patch -Np1 < "$srcdir/force_python3.diff"
}

build() {
  cd "$srcdir"/luxrender-lux-$_pkgver


  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLUXRAYS_DISABLE_OPENCL=OFF \
    -DPYTHON_CUSTOM=ON \
    -DPYTHON_LIBRARIES=/usr/lib/libpython3.6m.so \
    -DPYTHON_INCLUDE_PATH=/usr/include/python3.6m/ \
    -DCMAKE_EXE_LINKER_FLAGS=-lpthread
  make
}

package() {
  cd "$srcdir"/luxrender-lux-$_pkgver
  make DESTDIR="$pkgdir" install

  # fix library path on x86_64
  [ "$CARCH" = "x86_64" ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib

  # install pylux
  install -D -m644 pylux.so "$pkgdir"/usr/lib/python3.6/pylux.so
}

# vim:set ts=2 sw=2 et:
