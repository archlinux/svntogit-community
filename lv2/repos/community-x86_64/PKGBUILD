# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=lv2
pkgver=1.14.0
pkgrel=3
pkgdesc="Plugin standard for audio systems"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=('x86_64')
makedepends=('asciidoc' 'doxygen' 'gtk2' 'libsndfile' 'pygmentize'
'python2-pygments' 'python2-rdflib' 'python2-six')
optdepends=('libsndfile: Example sampler'
            'gtk2: Example sampler'
            'python2-pygments: Documentation generator script'
            'python2-rdflib: Documentation generator script')
provides=('lv2core')
conflicts=('lv2core')
replaces=('lv2core')
source=("http://lv2plug.in/spec/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('6991d848ed9f04a48ca3070efc15af83431f680aa8a8559a02b1666f7b8d43af8e089f78c3b14ad3345dac32fd1c8ad0faf3a3f56dddc07c6f0e0aeef4077498'
            'SKIP') # David Robillard
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare(){
  cd "${pkgname}-${pkgver}"
  # fix python2 shebang
  sed -e 's/python/python2/' -i lv2specgen/lv2specgen.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python2 waf configure --prefix=/usr \
    --libdir=/usr/lib \
    --docs \
    --test
  python2 waf build $MAKEFLAGS
}

check() {
  cd "${pkgname}-${pkgver}"
  python2 waf test
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
  # license
  install -vDm644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -vDm644 {NEWS,README.md}\
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
