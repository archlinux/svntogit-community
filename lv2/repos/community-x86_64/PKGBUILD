# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=lv2
pkgver=1.18.4
pkgrel=2
pkgdesc="Plugin standard for audio systems"
arch=(x86_64)
url="https://lv2plug.in/"
license=(ISC)
makedepends=(asciidoc doxygen gtk2 libsndfile pygmentize
python-markdown python-pygments python-rdflib waf)
optdepends=(
  'sord: for lv2_validate'
  'libsndfile: for eg-sampler.lv2'
  'atk: for eg-sampler.lv2 and eg-scope.lv2'
  'cairo: for eg-sampler.lv2 and eg-scope.lv2'
  'fontconfig: for eg-sampler.lv2 and eg-scope.lv2'
  'freetype2: for eg-sampler.lv2 and eg-scope.lv2'
  'glib2: for eg-sampler.lv2 and eg-scope.lv2'
  'gtk2: for eg-sampler.lv2 and eg-scope.lv2'
  'harfbuzz: for eg-sampler.lv2 and eg-scope.lv2'
  'pango: for eg-sampler.lv2 and eg-scope.lv2'
  'python-pygments: for lv2specgen.py'
  'python-rdflib: for lv2specgen.py'
)
options=(debug)
source=(https://lv2plug.in/spec/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('8095a1f3dcf70cf8cefff18c40b52c1dffeab0fe1f6bfb083c9f0e48a9617d3c8647b3f4db567ac1f5f33564399ffe1023774ad9bacb1bb0133f182422f22d6a'
            'SKIP')
b2sums=('84bcb5578110ff933b37b398029ed062c9393e17f17ddaa6329638f3c71e2b17b424f84987c00bc2c5699ffc15649362b98d5b238521e593c578c67450ecfeec'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

prepare() {
  cd $pkgname-$pkgver
  # let wscript(s) find the custom waf scripts
  mkdir -pv tools
  touch __init__.py
  cp -v waflib/extras/{autowaf,lv2}.py tools/
  mkdir -pv plugins/tools/
  cp -v waflib/extras/{autowaf,lv2}.py plugins/tools/
  rm -rv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/g" \
      -i {,plugins/,plugins/*/}wscript
}

build() {
  export LINKFLAGS="$LDFLAGS"

  cd $pkgname-$pkgver
  waf configure --prefix=/usr \
                --libdir=/usr/lib \
                --docs \
                --test
  waf -v build $MAKEFLAGS
}

check() {
  cd $pkgname-$pkgver
  waf test
}

package() {
  cd $pkgname-$pkgver
  waf install --destdir="$pkgdir"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
