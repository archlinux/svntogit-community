# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=lv2
pkgver=1.18.8
pkgrel=1
pkgdesc="Plugin standard for audio systems"
arch=(x86_64)
url="https://lv2plug.in/"
license=(ISC)
makedepends=(
  asciidoc
  codespell
  doxygen
  flake8
  libsamplerate
  libsndfile
  meson
  pygmentize
  python-black
  python-lxml
  python-markdown
  python-pygments
  python-pylint
  python-rdflib
  serd
  sord
)
optdepends=(
  'sord: for lv2_validate'
  'libsamplerate: for eg-sampler.lv2'
  'libsndfile: for eg-sampler.lv2'
  'python-lxml: for lv2specgen.py'
  'python-markdown: for lv2specgen.py'
  'python-pygments: for lv2specgen.py'
  'python-rdflib: for lv2specgen.py'
)
options=(debug)
source=(https://lv2plug.in/spec/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('9925290818bd683200bc738e3e9342b946fe82a04d983e89c240ae70b9053a7a59a8d505c522fe7af5b804adeba00fdf94403bd34844ce11e26cc1ff62c2f254'
            'SKIP')
b2sums=('b9d6480286f0709d3e7ddd9444c092e4e1e108ed384a8cdc7991ec870e8374869bab21e09ebebad4c59445ec3a90ddd05bf2c461a1c9aeb986f68d84a937bbbd'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
