# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>

pkgname=lv2
pkgver=1.18.6
pkgrel=1
pkgdesc="Plugin standard for audio systems"
arch=(x86_64)
url="https://lv2plug.in/"
license=(ISC)
makedepends=(
  asciidoc
  codespell
  doxygen
  flake8
  libsndfile
  meson
  pygmentize
  python-black
  python-lxml
  python-markdown
  python-pygments
  python-pylint
  python-rdflib
  serd
  sord
)
optdepends=(
  'sord: for lv2_validate'
  'libsndfile: for eg-sampler.lv2'
  'python-lxml: for lv2specgen.py'
  'python-markdown: for lv2specgen.py'
  'python-pygments: for lv2specgen.py'
  'python-rdflib: for lv2specgen.py'
)
options=(debug)
source=(https://lv2plug.in/spec/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('baecef70abe8354bca056d67085657e2174e39d2030a5173a226ee194c96662d6c3351df4500b4631e08798765dfed6d758b6be16a2ea78c4f29abb53c5d786f'
            'SKIP')
b2sums=('27c9eb3e15e4515a63f93552c8924c502f8bc585fbf62a62a1fca4ff7fd2e9940827cda4b5c7cbb6858324e1dba5bcad2ad75239da9f860db56e6839224ae186'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
