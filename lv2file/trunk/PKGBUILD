# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=lv2file
pkgver=0.93
pkgrel=2
pkgdesc="A simple program which you can use to apply effects to your audio files"
arch=(x86_64)
url="https://github.com/jeremysalwen/lv2file"
license=(GPL3)
groups=(lv2-plugins pro-audio)
depends=(argtable glibc)
makedepends=(libsndfile lilv lv2)
# upstream didn't add the manpage to the release tarball:
# https://github.com/jeremysalwen/lv2file/issues/4
source=("$pkgname-$pkgver.tar.gz::https://github.com/jeremysalwen/$pkgname/archive/upstream/$pkgver.tar.gz"
        "https://github.com/jeremysalwen/$pkgname/blob/9c6fa09f829afa4e4dd47841ec57959c958ddce3/debian/$pkgname.1")
sha512sums=('4f4b610b44c34920671eaae0735a3f71a2bdc8ac874e61e408d49518e19ca59c30f989dc1cd7542e733d799db1fa5ad1242fd290978b07304b88848095ce0128'
            '0129ca83c510a1cabe6e41c6064f40b08d7bfc0204e9f6cc5afd2b42c39e62c3ee248fab5069eae21549b731625469746b4332fdf4bb4135d5c06e23ad4802af')
b2sums=('1fc1f2440f7c90cc967c1decc820b4222253968da691b89db582d46b5deb942296e405c2403ca13c5c8e0ec507cbf2da175d75cb79b1921dbe1b7c54a4d975c3'
        'd14d6c3a2f4aa27329fbfeb927798040e357d004c4987ca7fa633da6f2bd4150cfe7d5feecba97444dea4a9a934f870437ea07de8e654bb54bf5af1d7a94da8d')

build() {
  make all -C $pkgname-upstream-$pkgver
}

package() {
  depends+=(liblilv-0.so libsndfile.so)

  make DESTDIR="$pkgdir" install -C $pkgname-upstream-$pkgver
  install -vDm 644 $pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 $pkgname-upstream-$pkgver/README -t "$pkgdir/usr/share/doc/$pkgname/"
}
