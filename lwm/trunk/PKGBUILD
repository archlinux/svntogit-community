# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jeff Mickey <j@codemac.net>
# Contributor: Ben Mazer <blm@groknil.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lwm
pkgver=1.2.2
pkgrel=3
pkgdesc='Very light weight window manager'
arch=('i686' 'x86_64')
url="http://www.jfc.org.uk/software/lwm.html"
license=('GPL')
depends=('xorg-server' 'libxext' 'libsm')
makedepends=('imake')
source=(http://www.jfc.org.uk/files/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('1748722a293e03d632b615275ef84498')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	xmkmf
	sed -i 's/^LOCAL_LIBRARIES.*/& $(ICELIB)/' Makefile
	make
	strip lwm
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	install -Dm755 lwm "$pkgdir/usr/bin/lwm"
	install -Dm644 lwm.man "$pkgdir/usr/share/man/man1/lwm.1"
}
