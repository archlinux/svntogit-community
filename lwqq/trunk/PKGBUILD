# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=lwqq
pkgver=0.4.1
_gitver="tag=v$pkgver"
#_gitver='commit=472881c1b122bad32089c1488a43414e1f85e077'
pkgrel=2
pkgdesc="A library for webqq protocol"
arch=('i686' 'x86_64')
url="https://github.com/xiehuc/lwqq"
license=('GPL3')
depends=('curl' 'libev' 'js17' 'sqlite')
makedepends=('git' 'cmake')
source=("git://github.com/xiehuc/lwqq.git#$_gitver"
        pt_tea.patch)
sha512sums=('SKIP'
            '277a08329f353924deca3266489e8710f56149f2e645ac9998023b999ef901baf11a0ce3b851bfad1e6f1d76f7536dc3433846c1d83aa8ba52c820755072576a')

prepare() {
  cd $pkgname
  patch -p1 -i ../pt_tea.patch
}

build() {
  cd $pkgname
  
  sed -e 's/set(LIBSUFFIX "64")/set(LIBSUFFIX "")/' \
      -i CMakeLists.txt
      
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
