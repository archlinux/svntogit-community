# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>

pkgname=lxcfs
pkgver=0.13
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(i686 x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'cgmanager>=0.35-1' 'libnih' 'fuse' 'dbus-glib')
makedepends=('help2man')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"
	"lxcfs.service")
md5sums=('b4b4f847f94b4e784e32bff54d6bb2c6'
         '29a6bf8868ce09871e6447db4070a923')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --localstatedir=/var
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -dm0755 $pkgdir/var/lib/lxcfs
    install -Dm0644 $srcdir/lxcfs.service $pkgdir/usr/lib/systemd/system/lxcfs.service
}
