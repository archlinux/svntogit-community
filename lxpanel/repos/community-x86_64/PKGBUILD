# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxpanel
pkgver=0.7.1
pkgrel=3
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2' 'alsa-lib' 'menu-cache' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools')
optdepends=('wireless_tools: netstat plugin')
source=(http://downloads.sourceforge.net/sourceforge/lxde/lxpanel-$pkgver.tar.xz
        lxpanel-0.7.1-SF-679-X11-library-uses-unsigned-long-as-cardinal-32.patch
		lxpanel-0.7.1-SF-680-Fix-left-click-on-Move-to-Workspace-menu.patch)
sha256sums=('45a9a511287ceb6177d0688f828b0974ebeae8655e05e2a52dddbe0bae30b026'
            '01a46dc9dd949ef93f27bfd59514e5a88068f618cc0399176a99ecef4aab6aaf'
            '2428aaed87e6d4d3930524b6fb1422e7fbdfb86e5ea4e78dd0dba9b18959f11e')

prepare() {
  cd $pkgname-$pkgver

  # http://sourceforge.net/p/lxde/bugs/679/
  # https://bugs.archlinux.org/task/42178
  patch -Np1 -i ../lxpanel-0.7.1-SF-679-X11-library-uses-unsigned-long-as-cardinal-32.patch

  # https://sourceforge.net/p/lxde/bugs/680/
  # https://bugs.archlinux.org/task/42226
  patch -Np1 -i ../lxpanel-0.7.1-SF-680-Fix-left-click-on-Move-to-Workspace-menu.patch
}

build() {
  cd $pkgname-$pkgver
  CFLAGS+=' -lgmodule-2.0' ./configure \
    --sysconfdir=/etc \
    --prefix=/usr \
    --enable-man
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
