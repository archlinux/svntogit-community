# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=lxpolkit
pkgver=0.1.0
pkgrel=3
pkgdesc="Simple polkit authentication agent for LXDE"
arch=('i686' 'x86_64')
url="http://blog.lxde.org/?p=674"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'polkit')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.gz)
md5sums=('2597b00035fe1d695219e0f9bfa8c26f')

prepare() {
  cd $pkgname-$pkgver
  # Don't conflict with MATE and Razor-qt (they have their own polkit agents)
  sed -i '/^NotShowIn/ s/GNOME;KDE;/GNOME;KDE;MATE;Razor;/' data/lxpolkit.desktop.in.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
