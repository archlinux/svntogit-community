# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-common
pkgver=0.11.2
pkgrel=1
pkgdesc="Common data to run a LXQt session."
arch=("any")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("qt5-svg" "hicolor-icon-theme" "xdg-utils" "ttf-font")
makedepends=("cmake" "qt5-tools")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"1c0520208a582b557d38926a92cc9d321a6402e77f40cfd47c727da9aa70f1d6"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
