# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-config
pkgver=0.14.0
pkgrel=1
pkgdesc="LXQt system configuration."
arch=("x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("lxqt-themes" "liblxqt" "libkscreen" "libxcursor" "xf86-input-libinput" "qt5-svg")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=('88ee2ff42f69f25053c38138391131a376f9e713af9725c1284ed8ff2a565551'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
