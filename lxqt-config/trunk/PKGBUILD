# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-config
pkgver=0.10.0
pkgrel=5
pkgdesc="LXQt system configuration."
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt" "libkscreen" "libxcursor" "qt5-svg")
makedepends=("cmake" "qt5-tools")
source=(
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"4567be4653f2f1f9ff156126250a08d7b480f627fa1410484d49f65ba5dab9b4"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build
	# Build with LC_ALL=en_US.UTF-8 to work around lxqt#957 (FS#48754)
	# Remove in 0.11
	LC_ALL=en_US.UTF-8 cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	LC_ALL=en_US.UTF-8 make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
