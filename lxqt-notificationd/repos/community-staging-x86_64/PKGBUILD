# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-notificationd
pkgver=0.15.0
pkgrel=1
pkgdesc="LXQt notification daemon and library."
arch=("x86_64")
groups=("lxqt")
url="https://lxqt.org"
license=("LGPL2.1")
depends=("qt5-base" "liblxqt" "kwindowsystem" "libQt5Xdg.so")
makedepends=("cmake" "lxqt-build-tools")
provides=("notification-daemon")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
)
sha512sums=('0febe3ff61caaf4303e3853df6bc494ce8eb4a931e6d8600a2e38c07317ecadbe3d7060ac234d139326a726c43ac555153dde6b2d4d05e95ab357873457c9632'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
