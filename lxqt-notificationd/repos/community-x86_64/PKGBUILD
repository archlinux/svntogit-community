# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-notificationd
pkgver=0.10.0
pkgrel=3
pkgdesc="LXQt notification daemon and library."
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("lxqt-common" "liblxqt")
makedepends=("cmake" "qt5-tools")
source=(
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"5c0859157ff6a20d613ea4aca24e786faf79ab6d69afec1e0b3671e3bbda53d5"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
