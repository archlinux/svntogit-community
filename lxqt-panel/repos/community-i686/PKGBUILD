# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-panel
pkgver=0.9.0
pkgrel=1
pkgdesc="The LXQt desktop panel"
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=(
	"libxcomposite" "kguiaddons" "lxmenu-data"
	"lxqt-common" "liblxqt-mount" "lxqt-globalkeys"
)
optdepends=(
	"alsa-lib: Alsa support in volume control plugin"
	"libpulse: PulseAudio support in volume control plugin"
	"lm_sensors: Battery plugin"
	"libstatgrab: Network and CPU monitor plugin"
	"libsysstat: Network/CPU monitor plugin"
	"menu-cache: Improved performance for main menu plugin"
)
makedepends=("cmake" "qt5-tools" "alsa-lib" "libpulse" "lm_sensors" "libstatgrab" "libsysstat")
source=("http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=("96b2c2f1b7de7605ac79f094936e6fcf8f0034342e11d84624a033f2450f6bf1")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
