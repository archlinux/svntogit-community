# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-panel
pkgver=0.14.0
pkgrel=1
pkgdesc="The LXQt desktop panel"
arch=("x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=(
	"libdbusmenu-qt5" "libxcomposite" "lxmenu-data"
	"lxqt-globalkeys" "solid" "libxcb"
)
optdepends=(
	"libpulse: Volume control plugin"
	"libstatgrab: CPU monitor and Network monitor plugins"
	"libsysstat: System Statistics plugin"
	"lm_sensors: Sensors plugin"
)
makedepends=(
	"lxqt-build-tools" "liblxqt" "libpulse" "libstatgrab" "libsysstat"
	"lm_sensors" "libxdamage"
)
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=('e090b970827957c21f1d9ae822fb32e86e6bf9a0ad848b68f6ba338927a31859'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	}
