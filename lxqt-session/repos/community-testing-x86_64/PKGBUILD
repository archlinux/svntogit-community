# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-session
pkgver=1.0.0
pkgrel=1
pkgdesc="The LXQt session manager"
arch=("x86_64")
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
license=("LGPL2.1")
depends=("liblxqt" "xdg-user-dirs"  "libx11" "udev" "kwindowsystem" "libQt5Xdg.so" "procps")
makedepends=("cmake" "lxqt-build-tools")
optdepends=(
	"breeze-icons: Default icon theme"
	"oxygen-icons: Alternative icon theme"
	"sddm: The recommended display manager"
)
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
)
sha256sums=('5ad5f810907db7ecfc98f3c562d6df1b04d1d532fc7f8fa953e7b057bc442ac5'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
	"19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
