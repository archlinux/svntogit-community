# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-themes
pkgver=0.12.0
pkgrel=2
pkgdesc="LXQt themes, graphics and icons."
arch=("any")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("hicolor-icon-theme")
makedepends=("lxqt-build-tools")
replaces=("lxqt-common")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"bfc94e8bd3cf0b509d46e0a382b2564b44e82ca4da962d1d4cdd771889d49eb9"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
