# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxsession
pkgver=0.5.0
pkgrel=1
epoch=1
pkgdesc='Lightweight X11 session manager'
arch=('i686' 'x86_64')
url="http://lxde.org/"
license=('GPL2')
groups=('lxde')
depends=('gtk2' 'dbus-glib' 'polkit')
makedepends=('intltool' 'docbook-xsl' 'vala')
replaces=('lxpolkit' 'lxsession-lite' 'lxde-settings-daemon')
conflicts=('lxpolkit' 'lxsession-lite' 'lxde-settings-daemon')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.xz
        fix-lxsession-db.patch
        fix-lxsession-edit.patch
        fix-userswitch.patch
        fix-lxpolkit.patch)
md5sums=('6ef8b1487e60376e2381b2dec9c94734'
         '289040c2789f429e73af946295e4d3f5'
         '3dd128815be533b406353af0fec6081c'
         'f2d40d5c9ff1ea740463a2cecc6bd9a0'
         '71adf77e5b9ba82a29ba448a4f5aa1d6')

prepare() {
  cd $pkgname-$pkgver

  # lxsession-db: Don't hang when MIME folders are not available.
  # https://sourceforge.net/p/lxde/patches/479/
  patch -Np1 -i ../fix-lxsession-db.patch

  # lxsession-edit: Fix segfault on exit when kf is not set.
  # https://sourceforge.net/p/lxde/patches/480/
  patch -Np1 -i ../fix-lxsession-edit.patch

  # lxsession-logout: Fix user switch with LightDM when GDM is installed.
  # https://sourceforge.net/p/lxde/patches/481/
  patch -Np1 -i ../fix-userswitch.patch

  # Set lxpolkit by default, so lxsession will autostart it on login.
  # https://sourceforge.net/p/lxde/patches/478/
  patch -Np1 -i ../fix-lxpolkit.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install 
}
