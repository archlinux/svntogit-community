# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgbase=lxsession
pkgname=(lxsession lxsession-gtk3)
pkgver=0.5.3
pkgrel=1
epoch=1
pkgdesc='Lightweight X11 session manager'
arch=('x86_64')
url="https://lxde.org/"
license=('GPL2')
depends=('gtk2' 'gtk3' 'polkit')
makedepends=('intltool' 'docbook-xsl' 'vala')
conflicts=('lxpolkit')
source=(https://downloads.sourceforge.net/lxde/$pkgbase-$pkgver.tar.xz
        0001-Use-GApplication-for-unique-app-handling.patch)
sha256sums=('90bf42a45421f45f2c745d60f2c730176e1472642451c06c2ab07dcebdaf1fa8'
            '8017e586a6e43939cfef2053edd81d4a0171f4105af59316bc42bda96506d323')

prepare() {
  cd $pkgbase-$pkgver

  # Use GApplication for unique app handling
  # https://sourceforge.net/p/lxde/patches/539/
  patch -Np1 -i ../0001-Use-GApplication-for-unique-app-handling.patch

  # Regenerate C sources from Vala code
  rm *.stamp
  autoreconf -fi
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib --enable-gtk3
  make
}

package_lxsession() {
  groups=('lxde')
  depends=('gtk2' 'polkit')
  replaces=('lxpolkit')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxsession-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3' 'polkit')
  conflicts+=('lxsession')

  cd gtk3
  make DESTDIR="$pkgdir" install
}

package() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install 
}
