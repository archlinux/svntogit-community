# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=lxtask
pkgname=(lxtask lxtask-gtk3)
pkgver=0.1.7
pkgrel=1
pkgdesc='Task manager of the LXDE Desktop'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
depends=('gtk2' 'gtk3')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/lxde/$pkgbase-$pkgver.tar.xz)
md5sums=('eb2ad374e97c317e2f74d47b1e582303')

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --sysconfdir=/etc --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package_lxtask() {
  groups=('lxde')
  depends=('gtk2')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxtask-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3')
  conflicts=('lxtask')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
