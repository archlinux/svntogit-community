# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=lychee
pkgname=lychee-link-checker
pkgver=0.7.0
pkgrel=1
pkgdesc='Fast, async, resource-friendly link checker written in Rust'
arch=('x86_64')
url=https://github.com/lycheeverse/lychee
license=('Apache' 'MIT')
replaces=('lychee-rs')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
b2sums=('cdc8396cab5373a81f6d9be0f53edd0a5cb3a29ae33c2e35552ef9711dcfff6c0b15b9fbee16efd4984a331a96b368ffc5037c75b4f360411b890a46e40a47cd')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked
}

check() {
  cd $_name-$pkgver
  cargo test --release --locked
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
