# $Id$
# Maintainer: SÃ©bastien Luttringer

# Contacted by mail, author use svn commit as release version.
# He only push tested release code into svn and support using revision as version

pkgname=lz4
pkgver=111
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('i686' 'x86_64')
url='https://code.google.com/p/lz4/'
license=('GPL2')
makedepends=('svn')
depends=('glibc')
source=("$pkgname::svn+http://lz4.googlecode.com/svn/trunk/#revision=$pkgver"
        '01-fix-include.patch')
md5sums=('SKIP'
         '522618c085bb30924f540c3b2c18a7e4')

prepare() {
  patch -d $pkgname -p 1 < 01-fix-include.patch
}

build() {
  cd $pkgname
  make
  cd programs
  make
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
