# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=6.14
_pkgver=614
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('gcc-libs')
makedepends=('curl')
source=("https://monkeysaudio.com/files/MAC_SDK_${_pkgver}.zip")
sha256sums=('73cd7eb59a854de8e84c22c2b6a692a402af976345aa03792994ab41d858bd12')

prepare() {
  cd "${srcdir}"
  curl --remote-name "https://www.monkeysaudio.com/license.html"
  ln -s "${srcdir}/Source/Projects/NonWindows/Makefile" "Makefile"
}

build() {
  cd "${srcdir}"
  make
}

package() {
  cd "${srcdir}"
  make DESTDIR="${pkgdir}" prefix="/usr" install
  install -Dm644 license.html \
      "${pkgdir}"/usr/share/licenses/${pkgname}/license.htm
}
