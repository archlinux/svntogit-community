# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=7.22
_pkgver=722
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('gcc-libs')
makedepends=('curl')
source=(https://monkeysaudio.com/files/MAC_SDK_${_pkgver}.zip
        https://www.monkeysaudio.com/license.html)
sha256sums=('2dae19002a744715254dbc61ebb3db88cabeb612dbdfed207dc2c921addff921'
            '7bcca2ec9f7c45ebdc983d105d6608f6ef4883883d16bf9fa79d0832757294fd')

build() {
  make -C Source/Projects/NonWindows
}

package() {
  make DESTDIR="${pkgdir}" prefix=/usr install -C Source/Projects/NonWindows
  install -Dm644 license.html -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
