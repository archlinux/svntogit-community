# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=8.81
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('gcc-libs')
makedepends=('python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('3f02c09a7432bd81c36c149d88033aad4782257963ad88a5982602396f4bc104bfc7bb95e385770f3978d7f6680b15f54deff46a2f1cbbb70c1c74f1ddb18cb2'
            'ae842550b52e6dd7281acf254c9b4457a69a33bf7e8229780df990915048c44a91172b59c47e5f2b5e51dfae9f0dbc1916161d89689badfc59378c22a2a2347b')
b2sums=('87d083a29fc5e746bd28dc6f137bd019db153713b3f9714db088460cd67e8b4b93b6394f6a3a48a18001138b2ea3b3218ae488ddccf987eb66cf424025ed4495'
        'eb2ff25d3114cc18005816a8a8a3809fd28ecb9be0823d9ce6f1bf58120830d4623fa87ba71a38d7f25d354daf7575f819385bacb679f307e5373d4c4ea8973e')

build() {
  make -C Source/Projects/NonWindows

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  make DESTDIR="$pkgdir" prefix=/usr install -C Source/Projects/NonWindows

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
