# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Hugo Ideler <hugoideler@dse.nl>
# Contributor: Zsolt Varadi <sysop_xxl@fibermail.hu>

pkgname=madman
pkgver=0.94beta1.20060611
pkgrel=3
pkgdesc="I-tunes like music player"
arch=('i686' 'x86_64')
url="http://madman.sourceforge.net/"
license=('GPL')
depends=('qt3' 'xmms' 'gtk' 'taglib')
makedepends=('python2' 'scons')
source=(ftp://ftp.archlinux.org/other/community/madman/madman_$pkgver.orig.tar.gz
	ftp://ftp.archlinux.org/other/community/madman/madman_$pkgver-2.diff.gz
	ftp://ftp.archlinux.org/other/community/madman/madman.desktop
	ftp://ftp.archlinux.org/other/community/madman/madman_logo.png
	ftp://ftp.archlinux.org/other/community/madman/madman_logo_mini.png
	ftp://ftp.archlinux.org/other/community/madman/madman_logo_large.png
	fake-g++.sh)
md5sums=('36e7a4619ebe83ed939c7ae09c16d535'
         '26de942ae2465fb805748d3bea9a8a17'
         '591354b69235d5a4b5a09591d04db266'
         'd4546f77857a6ccdabb2036eb6a0da07'
         '2d54bb8eceed283edc88a3e4553d2483'
         '3c2da1b1800fb372e225ae38fa1398e8'
         '685a6fb13c861b82950dd6a77fd02120')

build() {
  cd $srcdir/$pkgname-$pkgver.orig

  patch -p1 -i $srcdir/madman_0.94beta1.20060611-2.diff
  mkdir -p ,build/release/designer
  touch ,build/release/designer/tsonglistview.h \
	,build/release/designer/tsetlistview.h \
	,build/release/designer/tclickablelabel.h \
	,build/release/designer/tacceleratorlineedit.h \
	,build/release/designer/tsonglistview.h \
	,build/release/designer/tsetlistview.h
  sed -i '26,1i#include <memory>' utility/base.h
  sed -i 's|python|python2|' designer/make_imagedata httpd/make_webdata doc/update_format_tag
  install -m755 $srcdir/fake-g++.sh ./g++
  export PATH=.:$PATH

  scons qt_directory=/usr/lib/qt3 add_c_include_dirs=/usr/include/qt3
}

package() {
  cd $srcdir/$pkgname-$pkgver.orig

  scons qt_directory=/usr/lib/qt3 add_c_include_dirs=/usr/include/qt3 \
    install_to=$pkgdir/usr prefix=/usr install

  install -Dm644 $srcdir/madman_logo.png $pkgdir/usr/share/icons/madman_logo.png
  install -Dm644 $srcdir/madman_logo_mini.png $pkgdir/usr/share/icons/mini/madman_logo_mini.png
  install -Dm644 $srcdir/madman_logo_large.png $pkgdir/usr/share/icons/large/madman_logo_large.png
  install -Dm644 $srcdir/madman.desktop $pkgdir/usr/share/applications/madman.desktop
}
