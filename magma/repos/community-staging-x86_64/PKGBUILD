# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
# Contributor: pingplug <pingplug@foxmail.com>
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=magma
pkgver=2.5.3
pkgrel=4
pkgdesc="Matrix Algebra on GPU and Multicore Architectures"
arch=('x86_64')
url="https://icl.cs.utk.edu/magma/"
license=('custom')
depends=('blas' 'lapack' 'cuda')
makedepends=('gcc-fortran' 'cmake' 'ninja')
optdepends=('python: for examples and tests'
            'gcc-fortran: Fortran interface')
source=("${pkgname}-${pkgver}.tar.gz::http://icl.cs.utk.edu/projectsfiles/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz"
        'cuda11.1.patch'
        'disable_magma_sparse.patch')
sha256sums=('c602d269a9f9a3df28f6a4f593be819abb12ed3fa413bba1ff8183de721c5ef6'
            '4d6e7e844a5aa5a771480a1fa61daaf85981f52d852ee444d7ffda63ac9a2db2'
            '259c38004b26395278cdaf431591974e0589dedc0f8bfc14413f854c2a10424f')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/cuda11.1.patch"

  # Temporarily disable magma-sparse until it's fixed by magma upstream:
  # https://bugs.archlinux.org/task/67094
  patch -Np1 -i "${srcdir}/disable_magma_sparse.patch"
}

build() {
  cd ${pkgname}-${pkgver}

  CC=/usr/bin/gcc \
  CXX=/usr/bin/g++ \
  FC=/usr/bin/gfortran \
  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DGPU_TARGET="sm_52 sm_53 sm_60 sm_61 sm_62 sm_70 sm_72 sm_75 sm_80 sm_86"
  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -Cbuild install

  install -d "${pkgdir}"/usr/share/magma/example
  cp -r "${srcdir}"/magma-${pkgver}/example/* "${pkgdir}"/usr/share/magma/example/
  install -d "${pkgdir}"/usr/share/magma/testing
  cp -r "${srcdir}"/magma-${pkgver}/testing/* "${pkgdir}"/usr/share/magma/testing/
  install -Dm644 "${srcdir}"/magma-${pkgver}/COPYRIGHT "${pkgdir}"/usr/share/licenses/magma/LICENSE
}

# vim:set ts=2 sw=2 et:
