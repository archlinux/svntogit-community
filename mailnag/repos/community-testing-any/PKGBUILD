# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Patrick Ulbrich <zulu99 at gmx . net>
# Contributor: rasmus <rasmus . pank at gmail . com>

pkgname=mailnag
pkgver=2.0.0
pkgrel=4
pkgdesc="Extensible mail notification daemon"
arch=('any')
url="https://github.com/pulb/mailnag"
license=('GPL')
depends=('gst-plugins-base' 'gtk3' 'libnotify' 'libsecret' 'python-dbus' 'python-gobject' 'python-xdg')
makedepends=(git)
optdepends=('mailnag-goa-plugin: GNOME Online Accounts integration'
            'mailnag-gnome-shell: GNOME Shell integration')
_commit=d3c1f7a43626484e02bcdf507a3562bf6869327c  # tags/v2.0.0
source=("git+https://github.com/pulb/mailnag.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  # Add categories to desktop file
  echo 'Categories=Network;Email;' >> data/mailnag.desktop
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1
}
