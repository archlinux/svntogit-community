# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=man-pages-de
pkgver=0.9
pkgrel=1
pkgdesc='German Linux man pages'
arch=('any')
url="https://alioth.debian.org/projects/manpages-de/"
license=('custom')
depends=('man-db')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://manpages-de.alioth.debian.org/downloads/manpages-de-$pkgver.tar.bz2)
sha256sums=('c69ebb685150995e77f32ad0742cd23291d046ebb54d518ee8db06ab022a2d63')

package() {
  cd ${srcdir}/manpages-de-$pkgver

  eval install -d ${pkgdir}/usr/share/man/de/man{$(seq -s, 1 8)}
  find man?/ -name '*.?' -exec iconv -f latin1 -t utf8 -o ${pkgdir}/usr/share/man/de/{} {} \;

  install -D -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/$pkgname/COPYRIGHT
}
