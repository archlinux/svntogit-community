# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: dickeny@gmail.com
# Contributor: cuihao <cuihao dot leo at gmail dot com>

pkgbase=man-pages-zh
pkgname=(man-pages-zh_cn man-pages-zh_tw)
pkgver=1.6.1
pkgrel=1
arch=('any')
url='https://github.com/man-pages-zh/manpages-zh'
license=('GPL2')
depends=('man-db')
makedepends=('opencc' 'python' 'git')
source=("git+https://github.com/man-pages-zh/manpages-zh.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
    cd "${srcdir}/manpages-zh"
    autoreconf -i
    ./configure --prefix=/usr
    make
}

package_man-pages-zh_cn() {
    pkgdesc='Simplified Chinese Linux man pages'

    cd "${srcdir}/manpages-zh"
    make install DESTDIR="${pkgdir}"

    rm -r "${pkgdir}/usr/share/man/zh_TW"

    # remove conflicted files with shadow
    cd "${pkgdir}/usr/share/man/zh_CN"
    rm -f ./man1/groups*
}

package_man-pages-zh_tw() {
    pkgdesc='Traditional Chinese Linux man pages'

    cd "${srcdir}/manpages-zh"
    make install DESTDIR="${pkgdir}"

    rm -r "${pkgdir}/usr/share/man/zh_CN"

    # remove conflicted files with shadow
    cd "${pkgdir}/usr/share/man/zh_TW"
    rm -f ./man1/groups*
}
