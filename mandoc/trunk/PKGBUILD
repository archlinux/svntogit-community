# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Jakub Klinkovsk√Ω <j.l.k@gmx.com>
# Contributor: Matheus de Alcantara <matheus.de.alcantara@gmail.com>

pkgname=mandoc
pkgver=1.14.6
pkgrel=1
pkgdesc='A suite of tools compiling mdoc from the OpenBSD project'
arch=('x86_64')
url='https://mdocml.bsd.lv/'
license=('ISC')
depends=('zlib')
provides=('man')
conflicts=('man')
source=("${url}/snapshots/${pkgname}-${pkgver}.tar.gz"
        'configure.local'
        'mandoc.service'
        'mandoc.timer')
sha256sums=('8bf0d570f01e70a6e124884088870cbed7537f36328d512909eb10cd53179d9c'
            '383772d5dd7c51319588ac4b63d4e7c452490568f6a3e905d118fdd6c4aa8a68'
            '2091411d5f87a3c371a5ba74b4773d1e454046446fa2cb045485979e52419bb6'
            '74d6a02b97a17fffddcc0a3dc830e811348b1f6c6b84f867882c776d50f00ea4')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    cp ../configure.local .
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # apply CFLAGS and LDFLAGS from makepkg.conf
    {
        echo "CFLAGS=${CFLAGS@Q}"
        echo "LDFLAGS=${LDFLAGS@Q}"
    } >> configure.local

    ./configure
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    DESTDIR="${pkgdir}" make install
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

    install -Dm644 "${srcdir}"/mandoc.timer "${pkgdir}"/usr/lib/systemd/system/mandoc.timer
    install -Dm644 "${srcdir}"/mandoc.service "${pkgdir}"/usr/lib/systemd/system/mandoc.service
    install -dm755 "${pkgdir}"/usr/lib/systemd/system/multi-user.target.wants
    ln -s ../mandoc.timer "${pkgdir}"/usr/lib/systemd/system/multi-user.target.wants/mandoc.timer
}
