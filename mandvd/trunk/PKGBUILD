# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>
# Contributor: Jesus Lazaro Plaza <jesuslazaro84@gmail.com>

pkgname=mandvd
pkgver=2.6
pkgrel=2
pkgdesc='Program to simply create DVD Video'
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php/ManDVD?content=83906"
license=('GPL')
depends=('dvd+rw-tools>=5.21.4' 'dvd-slideshow>=0.7.5' 'mjpegtools>=1.8.0' 'mplayer' 'qt3' 'xine-ui>=0.99.4')
changelog=$pkgname.changelog
source=(http://ftp.sh.cvut.cz/MIRRORS/vector/veclinux-7.0/source/abs/xap/$pkgname/$pkgname-$pkgver-1.fc12.tar.gz)
sha256sums=('967fd66da31f0619001b3aaea494ab41579ab7ffdc6a1b0a51a4a4add012eb9c')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  source /etc/profile.d/qt3.sh
  ${QTDIR}/bin/qmake
  sed -i "s|O2|O1|" ./Makefile
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  install -D -m755 $pkgname ${pkgdir}/usr/bin/$pkgname

# Creating menu item
  install -D -m644 mandvdico.png ${pkgdir}/usr/share/pixmaps/$pkgname.png
  install -D -m644 $pkgname.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop
}
