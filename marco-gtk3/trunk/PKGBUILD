# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=marco
pkgname=(${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="A window manager for MATE  (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt'
         'mate-desktop-gtk3>=1.10' 'startup-notification' 'zenity')
makedepends=('mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-gtk3')
conflicts=("${pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('57636adeddbaa1520dda76f7c383ab58944d5ed5')
install=${pkgbase}.install

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-startup-notification \
        --disable-static
    make
}

package_marco-gtk3() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
