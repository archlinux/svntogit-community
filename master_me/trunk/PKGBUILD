# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=master_me
pkgname=(master_me master_me-jack master_me-lv2 master_me-vst master_me-vst3)
pkgver=1.1.0
pkgrel=4
pkgdesc="Automatic mastering plugin for live streaming, podcasts and internet radio"
arch=(x86_64)
url="https://github.com/trummerschlunk/master_me"
license=(GPL3)
makedepends=(faustpp gcc-libs gendesk glibc libglvnd libx11 libxext libxrandr)
source=(https://github.com/trummerschlunk/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha512sums=('196db9214b555f5323b712627c220f0f5f6c14e9e264cc0a90eee4ab16f9ef695ebce0ecf7300d141452f5556db984ddc4c56ef8501d3029fe992171f7a1962c')
b2sums=('981e2de5a8962c509ae3c35636b4296ea1e16eaf8d6f0763b80a881e4163aab0ab758c7aae2cfb2d537f9a786b4dc3d471ee2f60f0ee60f1e13d036b10e38bec')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  gendesk -n -f \
          --pkgname "com.github.Trummerschlunk.Master_me" \
          --name "master_me" \
          --exec "master_me" \
          --icon "master_me" \
          --genericname "Automatic mastering plugin"
}

build() {
  make -C $pkgname-$pkgver
}

package_master_me() {
  depends=(master_me-jack master_me-lv2 master_me-vst master_me-vst3)

  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname-$pkgver

  (
    cd "$pkgdir"
    _pick master_me-jack usr/bin/
    _pick master_me-lv2 usr/lib/lv2/
    _pick master_me-vst usr/lib/vst/
    _pick master_me-vst3 usr/lib/vst3/
  )
}

package_master_me-jack() {
  pkgdesc+=" - standalone"
  depends=(gcc-libs glibc hicolor-icon-theme libglvnd libGL.so libx11 libxext libxrandr)

  mv -v $pkgname/* "$pkgdir"

  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 $pkgbase-$pkgver/artwork/LogoAssets/signet_master_me_black.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/master_me.svg"
}

package_master_me-lv2() {
  pkgdesc+=" - LV2 plugin"
  depends=(gcc-libs glibc libglvnd libGL.so libx11 libxext libxrandr lv2-host)
  groups=(lv2-plugins)

  mv -v $pkgname/* "$pkgdir"
}

package_master_me-vst() {
  pkgdesc+=" - VST plugin"
  depends=(gcc-libs glibc libglvnd libGL.so libx11 libxext libxrandr vst-host)
  groups=(vst-plugins)

  mv -v $pkgname/* "$pkgdir"
}

package_master_me-vst3() {
  pkgdesc+=" - VST3 plugin"
  depends=(gcc-libs glibc libglvnd libGL.so libx11 libxext libxrandr vst3-host)
  groups=(vst3-plugins)

  mv -v $pkgname/* "$pkgdir"
}
