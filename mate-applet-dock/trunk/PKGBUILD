# $Id$
# Contriburor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mate-applet-dock
_pkgname=mate-dock-applet
pkgver=0.81
pkgrel=1
pkgdesc="Application dock for the MATE panel"
arch=('any')
url="https://github.com/robint99/dock-applet"
license=('GPL3')
depends=('libnotify' 'mate-panel' 'python-cairo' 'python-dbus' 'python-gobject' 'python-pillow' 'python-xdg' 'python-xlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/robint99/$_pkgname/archive/V$pkgver.tar.gz")
sha256sums=('80a3e54b04ebcd61451c27ae72b2513815ef6521b1693c9ddbc2e8c07de37445')

prepare() {
  cd $_pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --with-gtk3
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
