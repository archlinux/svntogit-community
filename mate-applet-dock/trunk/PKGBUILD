# $Id$
# Contriburor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mate-applet-dock
_pkgname=mate-dock-applet
pkgver=0.80
pkgrel=1
pkgdesc="Application dock for the MATE panel"
arch=('any')
url="https://github.com/robint99/dock-applet"
license=('GPL3')
depends=('libnotify' 'mate-panel' 'python-cairo' 'python-dbus' 'python-gobject' 'python-pillow' 'python-xdg' 'python-xlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/robint99/$_pkgname/archive/V$pkgver.tar.gz")
sha256sums=('72d74d34e33427faa77c6c13346b39ac7cb70e8dfdf218b1d10aea5178611680')

prepare() {
  cd $_pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --with-gtk3
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
