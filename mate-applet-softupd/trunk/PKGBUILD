# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-applet-softupd
pkgver=0.2.10
pkgrel=2
pkgdesc="A MATE panel applet to notify when software updates become available."
url="http://www.zavedil.com/mate-software-updates-applet/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-packagekit' 'gtk2' 'libnotify' 'mate-panel')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
source=("http://www.zavedil.com/wp-content/uploads/2013/10/${pkgname}-${pkgver}.tar.gz")
md5sums=('f15f61c05a6701d8f987179f61c7bc0f')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname}
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/glib-2.0/schemas/gschemas.compiled"
}
