# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-applet-streamer
pkgver=0.1.2
pkgrel=1
pkgdesc="A MATE panel applet to let you play your favourite online radio station with a single click."
url="http://www.zavedil.com/online-radio-applet/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gst-plugins-base' 'gtk2' 'libnotify' 'mate-panel' 'sqlite')
makedepends=('mate-common')
optdepends=('gst-plugins-bad: for MP3 decoding')
source=("http://www.zavedil.com/wp-content/uploads/2014/11/${pkgname}-${pkgver}.tar.gz")
sha1sums=('c6b584a87d653a29a7e9496fd71e503314ec1315')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname}
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/glib-2.0/schemas/gschemas.compiled"
}
