# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-applets
pkgver=1.6.2
pkgrel=1
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64')
url="http://mate-desktop.org"
license=('GPL')
depends=('mate-character-map' 'mate-panel' 'python2-dbus' 'python2-gobject'
         'upower')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'mate-doc-utils'
             'networkmanager' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        "https://github.com/mate-desktop/mate-applets/commit/98dfd949e9209eefc0722fb232761f01b4fe2d7e.diff"
        "https://github.com/mate-desktop/mate-applets/commit/6fca38a74cc9e6b4ce107496f2e0e7d203c43d2b.diff")
sha1sums=('2978918a0b995b3f3dd98ad7ca159b0d7bcd245a'
          'c4d7f2d7952b53d8ac616ced55a4cf3da25c0fc4'
          '36c4507e54b21fbcff49732077912dbc5283807a')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    #upower: fix upower version
    patch -Np1 -i "${srcdir}/98dfd949e9209eefc0722fb232761f01b4fe2d7e.diff"
    #battstat: use g_signal notify for upower-1.0
    patch -Np1 -i "${srcdir}/6fca38a74cc9e6b4ce107496f2e0e7d203c43d2b.diff"
    # Use python2
    sed -i 's/env python/env python2/' invest-applet/invest/{chart.py,invest-applet.py,mate-invest-chart}
    sed -i 's/env python/env python2/' timer-applet/src/timer-applet
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-networkmanager \
        --enable-ipv6 \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
