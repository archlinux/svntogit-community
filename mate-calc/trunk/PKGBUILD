# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-calc
pkgver=1.24.2
pkgrel=1
pkgdesc="Calculator for the MATE desktop environment"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('gtk3' 'gettext')
makedepends=('itstool')
groups=('mate-extra')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('d4a4c8ff51c4611b32a129b7cad33c3bb8a1ec6b9d5f209aa4ee0c8f1b90e8f9')

build() {
    	cd "${pkgname}-${pkgver}"
   	./configure \
        	--prefix=/usr
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
