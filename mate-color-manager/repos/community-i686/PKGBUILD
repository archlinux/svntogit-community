# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-color-manager
pkgver=1.6.1
pkgrel=3
pkgdesc="Color management application for MATE."
url="https://github.com/NiceandGently/${pkgname}"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'exiv2' 'gtk2' 'lcms2' 'libcanberra' 'libcups' 'libexif' 'libnotify' 'libtiff'
         'libunique' 'mate-desktop' 'sane' 'vte')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs')
source=("https://github.com/NiceandGently/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/NiceandGently/mate-color-manager/commit/cb80f549af83a703243eae4afad841d564743afb.diff")
sha1sums=('6a8d18a3a7e3fb22e03cd319556575b9482c6f04'
          '283c767591956206f5c7229ee1eff86faea253c0')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Get udevrulesdir from pkg-config 
    patch -Np1 -i  "${srcdir}/cb80f549af83a703243eae4afad841d564743afb.diff"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static \
        --disable-scrollkeeper

    #Work around a problem where the .sgml fail to parse.
    sed -e 's:@HAVE_DOCBOOK2MAN_TRUE@.*::' -i man/Makefile.in
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
