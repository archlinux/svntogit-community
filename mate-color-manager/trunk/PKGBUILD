# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-color-manager
pkgver=1.8.0
pkgrel=2
pkgdesc="Color management application for MATE."
url="https://github.com/NiceandGently/${pkgname}"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'exiv2' 'gtk2' 'lcms2' 'libcanberra' 'libcups' 'libexif'
         'libgudev' 'libnotify' 'libtiff' 'libunique' 'mate-desktop>=1.10' 'sane' 'vte')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/NiceandGently/${pkgname}/archive/v${pkgver}.tar.gz"
        "adapt-materr.diff")
sha1sums=('9727991077ca740eb050eb7a64cc7a6f5dd0e3ae'
          'f4b910418866c90530ff990564d992c802d9ba05')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # https://github.com/NiceandGently/mate-color-manager/commit/2512d5c1cf10b3a3903045c5ca5a8d411ea0ef32
    patch -p1 < "${srcdir}/adapt-materr.diff"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
