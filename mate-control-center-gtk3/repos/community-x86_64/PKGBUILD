# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=mate-control-center
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The Control Center for MATE (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-menus' 'mate-settings-daemon-gtk3' 'marco-gtk3' 'libunique3' 'libxss')
makedepends=('intltool' 'itstool')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('49f65c992fdc27e0c5c57cd57e7143813b67d76b')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-update-mimedb

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
