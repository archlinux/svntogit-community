# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-control-center
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.0
pkgrel=3
pkgdesc="The Control Center for MATE (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'desktop-file-utils' 'gsettings-desktop-schemas' 'gtk3'
         'mate-desktop-gtk3>=1.10' 'mate-icon-theme' 'mate-menus>=1.10' 'mate-settings-daemon-gtk3>=1.10'
         'marco-gtk3>=1.10' 'libgtop' 'libmatekbd-gtk3>=1.10' 'libunique3' 'librsvg' 'libxss'
         'libxt' 'startup-notification')
makedepends=('docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz"
        "0001_fix_keyboard_and_mouse_properties_windows.diff"
        "0002_adjust_mate-about-me_dialog_window.diff")
sha1sums=('b720314054e8c417f32eeac8385b82d3849dace0'
          'd0fae2cd19ef20cb8eaae42ea7e0b9860dcf9ffe'
          'bf845bd8f0cda2fad6a3726b7cee40b31321e066')
install=${_pkgbase}.install

prepare() {
    # UI fixes.
    #  - https://github.com/mate-desktop/mate-control-center/pull/157
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    patch -Np1 -i "${srcdir}/0001_fix_keyboard_and_mouse_properties_windows.diff"
    patch -Np1 -i "${srcdir}/0002_adjust_mate-about-me_dialog_window.diff"
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
