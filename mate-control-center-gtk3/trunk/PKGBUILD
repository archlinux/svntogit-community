# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=mate-control-center
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The Control Center for MATE (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'desktop-file-utils' 'gsettings-desktop-schemas' 'gtk3'
         'mate-desktop-gtk3>=1.12' 'mate-icon-theme' 'mate-menus>=1.12' 'mate-settings-daemon-gtk3>=1.12'
         'marco-gtk3>=1.12' 'libgtop' 'libmatekbd-gtk3>=1.12' 'libunique3' 'librsvg' 'libxss'
         'libxt' 'startup-notification')
makedepends=('docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('7e69f411c44f15d9dce4bd85b7f88344f39a383b')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
