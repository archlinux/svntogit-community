# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-control-center
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The Control Center for MATE (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.10' 'dbus-glib' 'dconf' 'desktop-file-utils' 'gsettings-desktop-schemas'
         'gtk2' 'mate-desktop>=1.10' 'mate-icon-theme' 'mate-menus>=1.10' 'mate-settings-daemon>=1.10'
         'marco>=1.10' 'libgtop' 'libmatekbd>=1.10' 'libunique' 'librsvg' 'libxss'
         'libxt' 'startup-notification')
makedepends=('docbook2x' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('b720314054e8c417f32eeac8385b82d3849dace0')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-static \
        --disable-update-mimedb
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/applications/mimeinfo.cache"
}
