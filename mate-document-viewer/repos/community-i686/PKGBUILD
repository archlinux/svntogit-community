# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-document-viewer
pkgver=1.6.1
pkgrel=5
pkgdesc="Simply a document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus' 'desktop-file-utils' 'gtk2' 'libmatekeyring' 'libsm'
         'libspectre' 'mate-icon-theme' 'poppler-glib' 'zlib')
makedepends=('djvulibre' 'gobject-introspection' 'libgxps' 'mate-common'
             'mate-doc-utils' 'mate-file-manager' 'perl-xml-parser' 'texlive-bin')
optdepends=('djvulibre: DjVu support'
            'libgxps: XPS support'
            'mate-file-manager: Document Properties extension'
            'p7zip: cbz and cb7 compressed comic books'
            'texlive-bin: DVI support'
            'unrar: cbr compressed comic books')
options=('!emptydirs')
groups=('mate-extra')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('5b3d1643b7945d56214ec13dc38cb641dfb688e1')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-introspection \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    ln -s atril "${pkgdir}/usr/bin/mate-document-viewer"
}
