# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgname=mate-icon-theme-faenza
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Faenza icon theme for MATE"
url="http://mate-desktop.org"
arch=('any')
license=('LGPL')
depends=('gtk-update-icon-cache')
makedepends=('git' 'icon-naming-utils' 'mate-common')
options=(!strip)
groups=('mate-extra' 'mate-extra-gtk3')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('155c6e3c5b6fd2d1b5dfcdb725598c0ba0121d9e')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    rm -f "${pkgdir}/usr/share/icons/matefaenza/icon-theme.cache"
}
