# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-keyring
pkgver=1.6.1
pkgrel=3
pkgdesc="MATE Password Management daemon"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dbus' 'dconf' 'gtk2' 'libtasn1' 'pam')
makedepends=('docbook-xml' 'mate-common' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('7b2e0201d4f4eb0f728dbf1626d5aa05b2d05675')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Prevent gck conflict with gnome gcr lib
    sed -e 's/DOC_MODULE=gck/DOC_MODULE=mate-gck/' -i docs/reference/gck/Makefile.am
    autoreconf -fi
    intltoolize -c -f
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-pam-dir=/usr/lib/security \
        --with-root-certs=/etc/ssl/certs \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
