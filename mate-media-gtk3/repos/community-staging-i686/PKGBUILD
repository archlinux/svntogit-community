# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=mate-media
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
url="http://mate-desktop.org"
pkgdesc="MATE Media Tools (GTK3 version)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libcanberra' 'libmatemixer' 'libunique3' 'mate-desktop-gtk3')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
sha1sums=('c9937f35fa3eb4209e2493f5d9838f5a5fb69886')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${_pkgbase} \
        --localstatedir=/var \
        --with-gtk=3.0
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
