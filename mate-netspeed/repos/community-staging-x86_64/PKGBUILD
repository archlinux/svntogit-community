# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=mate-netspeed
pkgname=(${_pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="A MATE applet that shows how much traffic occurs on a specified network device. (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libgtop' 'mate-panel>=1.12' 'wireless_tools')
makedepends=('mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('b661c3118b16017ce3d0df84e322904a4c54da8a')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2.0 \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
