# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-nettool
pkgver=1.8.1
pkgrel=7
pkgdesc="MATE interface for various networking tools."
url="https://github.com/NiceandGently/mate-nettool"
arch=('i686' 'x86_64')
license=('GPL')
depends=('coreutils' 'gtk3' 'libgtop' 'net-tools')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('dnsutils: Required for DNS queries'
            'iputils: Required for ping, traceroute and finger'
            'nmap: Required for network discovery and security audits'
            'whois: Required for WHOIS queries'
            'yelp: for reading help documents')
options=('!emptydirs')
source=("https://github.com/NiceandGently/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('c05c525fee4d70c03ebad7e9bb9b7e4c')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --disable-static \
        --disable-schemas-compile
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
