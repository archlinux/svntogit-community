# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
_pkgbase=mate-panel
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.0
pkgrel=4
pkgdesc="The MATE Panel (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'gtk3' 'libwnck3' 'libcanberra' 'libice'
         'libmateweather-gtk3>=1.10' 'librsvg' 'libsm' 'libsoup' 'libxau'
         'mate-menus>=1.10' 'mate-desktop-gtk3>=1.10')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz"
        fix-force-quit.patch)
groups=('mate-gtk3')
conflicts=("${_pkgbase}")
sha1sums=('632d7127fba8c5fa76f5dd951db89325c6ca3f89'
          '4c89df9a32decc1be4181154a1fa8ba6282abd86')
install=${_pkgbase}.install

prepare() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"

    # Fix Force-Quit panel applet to avoid hang with GTK3
    # https://github.com/mate-desktop/mate-panel/pull/318
    patch -Np1 -i ../fix-force-quit.patch
}

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
