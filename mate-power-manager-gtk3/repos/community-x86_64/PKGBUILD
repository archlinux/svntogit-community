# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=mate-power-manager
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=2
pkgdesc="Power management tool for the MATE desktop (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gtk3' 'libcanberra' 'libgnome-keyring' 'libnotify' 'upower')
makedepends=('intltool' 'itstool' 'mate-panel-gtk3')
optdepends=('mate-panel-gtk3: Set brightness and inhibit power management from the panel')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
sha1sums=('a7089947d4ac2151e2b8182f4d130aba08d804d2')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --with-gtk=3.0 \
        --disable-strict
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
