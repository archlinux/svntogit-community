# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.14
_pkgbase=mate-power-manager
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Power management tool for the MATE desktop (GTK3 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libcanberra' 'libgnome-keyring' 'libnotify' 'libunique3' 'mate-desktop-gtk3' 'upower')
makedepends=('intltool' 'itstool' 'mate-panel-gtk3')
optdepends=('mate-panel-gtk3: Set brightness and inhibit power management from the panel')
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
sha1sums=('286ac97658d18ea1fffa6e0a692a877727a44e41')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --with-gtk=3.0 \
        --disable-strict
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
