# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=mate-screensaver
pkgname=(${_pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=2
pkgdesc='Screensaver for MATE (GTK3 version [EXPERIMENTAL])'
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libmatekbd-gtk3>=1.12' 'libnotify' 'libxss' 'mate-desktop-gtk3>=1.12'
         'mate-menus>=1.12' 'mate-session-manager-gtk3>=1.12')
makedepends=('mate-common' 'xmlto')
optdepends=('mate-power-manager-gtk3: Add power management support.'
            'mate-screensaver-hacks: Additional screensavers.'
            'rss-glx: Really slick screensavers.')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('6930459af0d0f116861696265faf5f0807285bcf')
install=${_pkgbase}.install

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --with-xscreensaverdir=/usr/share/xscreensaver/config \
        --with-xscreensaverhackdir=/usr/lib/xscreensaver \
        --with-mit-ext \
        --with-libnotify \
        --enable-locking \
        --with-gtk=3.0 \
        --without-console-kit \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
