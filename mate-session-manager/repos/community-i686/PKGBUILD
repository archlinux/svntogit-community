# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-session-manager
pkgver=1.6.1
pkgrel=9
pkgdesc="The MATE Session Handler"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('dbus-glib' 'gtk2' 'libsm' 'libxtst' 'mate-desktop' 'mate-polkit'
         'mate-settings-daemon')
makedepends=('mate-common' 'perl-xml-parser' 'xmlto')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz"
        f0b5b1ab78879d64f80117415aacf029359afea5.diff
        e934e91226e9a93956121c9c84ecf3664fcd68be.diff)
sha1sums=('46188f6c02cc5978688af9d5db4be09f1b9199a5'
          '20fb99c29731d05191cd0029d0573db784b06ccf'
          '4312495bcd12ca5a20f7d5442b7517902605534c')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # fix systemd-login1 support for hibernate/suspend
    # https://github.com/mate-desktop/mate-session-manager/commit/f0b5b1ab78879d64f80117415aacf029359afea5
    patch -Np1 -i "${srcdir}/f0b5b1ab78879d64f80117415aacf029359afea5.diff"

    # Make upower optional
    # https://github.com/mate-desktop/mate-session-manager/commit/e934e91226e9a93956121c9c84ecf3664fcd68be
    patch -Np1 -i "${srcdir}/e934e91226e9a93956121c9c84ecf3664fcd68be.diff"

    # Recreate the build system because configure.ac was patched.
    autoreconf -fi
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-upower
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
