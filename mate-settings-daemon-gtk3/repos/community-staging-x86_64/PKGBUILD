# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=mate-settings-daemon
pkgname=(${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Settings daemon (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'dconf' 'fontconfig' 'gtk3' 'libcanberra-pulse'
         'libmatekbd-gtk3>=1.10' 'libmatemixer' 'libnotify' 'libxt'
         'mate-desktop-gtk3>=1.10' 'nss' 'polkit')
makedepends=('mate-common')
groups=('mate-gtk3')
conflicts=("${pkgbase}" "${pkgbase}-gstreamer" "${pkgbase}-pulseaudio")
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('ca2895f20c660f178317ef5e1b480bfddf9c34fc')
install=${pkgbase}.install

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=3.0 \
        --enable-polkit \
        --disable-static
    make
}

package_mate-settings-daemon-gtk3() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
