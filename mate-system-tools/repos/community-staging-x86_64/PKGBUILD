# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=mate-system-tools
pkgver=1.6.0
pkgrel=9
pkgdesc="Suite of tools for MATE to simplify configuring workstations."
url="http://mate-desktop.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'gtk2' 'liboobs' 'mate-polkit' 'system-tools-backends'
         'wireless_tools')
makedepends=('mate-common' 'mate-doc-utils' 'mate-file-manager' 'perl-xml-parser')
optdepends=('ntp: Automatic time synchronisation.'
            'mate-file-manager: provides Caja Shares extension')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('89d53c22171f6181ad4fb89387d4fa3300bb27b2')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Modify the groups for Arch
    #  - https://wiki.archlinux.org/index.php/Users_and_Groups
    #  - https://wiki.archlinux.org/index.php/Systemd#Supplementary_information
    #  - https://bbs.archlinux.org/viewtopic.php?pid=1183332
    # Desktop user
    sed -i 's/cdrom,floppy,dialout,tape,dip,adm,plugdev,fax,audio,scanner,fuse,video/adm,log,sys,users,uucp/' src/users/user-profiles.conf.in
    # Administrator
    sed -i 's/cdrom,floppy,dialout,tape,dip,adm,plugdev,fax,audio,scanner,fuse,admin,sambashare,lpadmin,video/adm,disk,locate,log,sys,users,uucp,wheel/' src/users/user-profiles.conf.in
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-scrollkeeper \
        LIBS="-lm" # Required for MATE 1.6 but can be removed in MATE 1.7/1.8
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
