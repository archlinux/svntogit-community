# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Piotr Gorski <sir_lucjan@bridgelinux.pl>

pkgname=mate-themes
pkgver=1.7.1
pkgrel=2
pkgdesc="Default themes for the MATE desktop"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('gtk2' 'gtk-engines' 'gtk-engine-murrine' 'librsvg' 'mate-icon-theme')
makedepends=('mate-common' 'icon-naming-utils' 'perl-xml-parser')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.7/${pkgname}-${pkgver}.tar.xz")
sha1sums=('d30744009b3090d1ce7e8cfba8293c58994a5a85')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --enable-all-themes \
        --enable-test-themes \
        --disable-placeholders
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
