# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kevin Dodd <jesin00@gmail.com>
# Contributor: Tianjiao Yin <ytj000@gmail.com>

pkgname=mathjax
pkgver=2.5.3
pkgrel=1
pkgdesc='An open source JavaScript display engine for mathematics that works in all modern browsers'
url='https://www.mathjax.org/'
arch=(any)
license=(Apache)
depends=(xorg-mkfontdir fontconfig)
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/mathjax/MathJax/archive/$pkgver.tar.gz")
sha256sums=('d24fcd6e66db4d79177513fbd25be9b75bff30f2a379199e8a05d5051fed5df3')

prepare() {
  cd MathJax-$pkgver

# don't warn about missing PNG fonts
  sed -e 's|imageFont: "TeX",|imageFont: null,|' -i config/default.js

# Remove unneeded stuff, see https://github.com/mathjax/MathJax-docs/wiki/Guide%3A-reducing-size-of-a-mathjax-installation
  rm -r docs test unpacked
  rm -r fonts/HTML-CSS/TeX/png
  for _format in eot otf svg ; do
   find . -type d -name "$_format" -prune -exec rm -rf {} \;
  done
}

package() {
  mkdir -p "$pkgdir"/usr/share/{fonts,licenses/mathjax}
  cp -a MathJax-$pkgver "$pkgdir"/usr/share/mathjax
  mv "$pkgdir"/usr/share/mathjax/fonts "$pkgdir"/usr/share/fonts/mathjax
  ln -s /usr/share/fonts/mathjax "$pkgdir"/usr/share/mathjax/fonts
  mv "$pkgdir"/usr/share/mathjax/LICENSE "$pkgdir"/usr/share/licenses/mathjax/LICENSE
}
