# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer:  Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=mathomatic
pkgver=15.6.3
pkgrel=1
pkgdesc="General purpose Computer Algebra System written in C"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://mathomatic.orgserve.de/math/"
depends=('readline')
makedepends=('time')
optdepends=('python2: for running /usr/share/doc/mathomatic/factorial/factorial'
            'bash: for running a test script' 
            'gnuplot: for plotting')
source=(http://mathomatic.org/$pkgname-$pkgver.tar.bz2)
md5sums=('afe923c5164b2d8746ef8dc87b868ba0')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # python2 fix
  for file in primes/{primorial,matho-sum,matho-mult} examples/factorial; do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  make READLINE=1
  make test
  cd "$srcdir"/$pkgname-$pkgver/primes
  make
  make test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make prefix="$pkgdir"/usr install
  cd "$srcdir"/$pkgname-$pkgver/primes
  make prefix="$pkgdir"/usr install
}

