# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Maintainer:  Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=mathomatic
pkgver=16.0.5
pkgrel=8
pkgdesc='General purpose Computer Algebra System written in C'
arch=('x86_64')
license=('LGPL')
url="http://mathomatic.orgserve.de/math/"
depends=('readline')
makedepends=('time' 'python')
optdepends=('python: for running /usr/share/doc/mathomatic/factorial/factorial'
            'bash: for running a test script'
            'm4: for complex operations using the matho script'
            'rlwrap: for readline editing'
            'gnuplot: for plotting')
# The original author is dead and the projects site no longer exists. Uploaded a backup copy.
source=('https://github.com/Atsutane/mathomatic/raw/master/mathomatic-16.0.5.tar.bz2')
md5sums=('dd04913a98a5073b56f3bc78a01820f3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  2to3 -wn examples/factorial primes/{matho-mult,matho-sum,primorial}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make READLINE=1
  cd primes
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test
  cd primes
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir"/usr m4install
  sed -i 's+/build/pkg++' "$pkgdir"/usr/bin/matho

  cd primes
  make prefix="$pkgdir"/usr install
  sed -i "s+$pkgdir++" "$pkgdir/usr/bin/matho"
}

# vim:set ts=2 sw=2 et:
