# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=1.2.1
pkgrel=1

pkgdesc="Matrix reference homeserver"
url="https://github.com/matrix-org/synapse"
arch=('any')
license=('Apache')

depends=('python-jsonschema' 'python-twisted' 'python-service-identity'
         'python-pyopenssl' 'python-yaml' 'python-pyasn1' 'python-pynacl'
         'python-daemonize' 'python-bcrypt' 'python-frozendict'
         'python-pillow' 'python-ujson'
         'python-pysaml2' 'python-setuptools'
         'python-systemd' 'python-unpaddedbase64' 'python-canonicaljson'
         'python-signedjson' 'python-pymacaroons'
         'python-service-identity' 'python-msgpack'
         'python-phonenumbers' 'python-prometheus_client'
         'python-attrs' 'python-netaddr' 'python-sortedcontainers'
         'python-treq' 'python-psutil' 'python-idna' 'python-jinja'
         'python-bleach' 'systemd')
checkdepends=('python-lxml' 'python-mock' 'python-parameterized')
optdepends=('python-psycopg2: PostgreSQL support'
            'python-lxml: URL previewing'
            'python-psutil: metrics'
            'python-pyjwt: jwt')

source=("synapse-$pkgver.tar.gz::https://github.com/matrix-org/synapse/archive/v$pkgver.tar.gz"
        'synapse.service'
        'sysusers-synapse.conf')

md5sums=('e6e005ec360ccd1fa27ff33e15eb78ab'
         '276a99050f40601089255ea168bb7620'
         'ecd9f66fb57fe1a2e1e2df07a460a35b')

backup=('etc/synapse/log_config.yaml')
install=synapse.install

build() {
	cd synapse-$pkgver
	python setup.py build
}

check() {
	cd synapse-$pkgver
#	PYTHONPATH=. trial3 tests
}

package() {
	install -Dm644 synapse.service "$pkgdir"/usr/lib/systemd/system/synapse.service

	cd synapse-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build

	install -dm755 -o 198 -g 198 "$pkgdir"/etc/synapse
	install -dm700 -o 198 -g 198 "$pkgdir"/var/lib/synapse/
	cp -a synapse/res/ "$pkgdir"/var/lib/synapse/
	chown -R 198:198 "$pkgdir"/var/lib/synapse/res

	install -Dm644 contrib/systemd/log_config.yaml "$pkgdir"/etc/synapse/log_config.yaml
	install -Dm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
}
