# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=0.22.1
pkgrel=2

pkgdesc="Matrix reference homeserver"
url="https://github.com/matrix-org/synapse"
arch=('any')
license=('Apache')

depends=('python2-jsonschema' 'python2-twisted' 'python2-service-identity'
         'python2-pyopenssl' 'python2-yaml' 'python2-pyasn1' 'python2-pynacl'
         'python2-daemonize' 'python2-bcrypt' 'python2-frozendict'
         'python2-pillow' 'python2-pydenticon' 'python2-ujson' 'python2-blist'
         'python2-pysaml2' 'python2-setuptools'
         'python2-systemd' 'python2-unpaddedbase64' 'python2-canonicaljson'
         'python2-signedjson' 'python2-pymacaroons-pynacl'
         'python2-service-identity' 'python2-msgpack'
         'python2-phonenumbers'
         'systemd')
makedepends=('python2-mock')
optdepends=('python2-psycopg2: PostgreSQL support'
            'python2-netaddr: URL previewing'
            'python2-jinja: e-mail notifications'
            'python2-bleach: e-mail notifications'
            'python2-matrix-synapse-ldap3: LDAP support'
            'python2-psutil: metrics'
            'python2-matrix-angular-sdk: built-in web client')

source=("synapse-$pkgver.tar.gz::https://github.com/matrix-org/synapse/archive/v$pkgver.tar.gz"
        'sysusers-synapse.conf'
        'deps-relax-checks.patch')

md5sums=('91119edc813d7305708df89d833d73de'
         'ecd9f66fb57fe1a2e1e2df07a460a35b'
         '74d3d018e588d70ff0a22863d3d7aa4e')

backup=('etc/synapse/log_config.yaml')
install=synapse.install

prepare() {
	cd synapse-$pkgver
	patch -p1 <"$srcdir/deps-relax-checks.patch"
}

build() {
	cd synapse-$pkgver
	python2 setup.py build
}

package() {
	cd synapse-$pkgver
	python2 setup.py install --root "$pkgdir" --optimize=1 --skip-build

	install -dm755 -o 198 -g 198 "$pkgdir"/etc/synapse
	install -Dm644 contrib/systemd/log_config.yaml "$pkgdir"/etc/synapse/log_config.yaml
	install -Dm644 contrib/systemd/synapse.service "$pkgdir"/usr/lib/systemd/system/synapse.service
	install -Dm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
}
