# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: <kaptoxic _at_ yahoo _dot_ com >
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=maude
pkgver=3.0
pkgrel=3
pkgdesc="High-level Specification Language"
arch=('x86_64')
url="http://maude.cs.illinois.edu"
license=('GPL')
depends=('buddy' 'cvc4' 'libtecla' 'gmp' 'libsigsegv')
makedepends=('flex' 'bison')
source=("http://maude.cs.illinois.edu/w/images/9/92/Maude-$pkgver.tar.gz"
        "http://maude.cs.illinois.edu/w/images/0/04/Full-Maude-$pkgver.zip"
        maude.sh)
sha512sums=('9f27a045da931c38536e27e7f046a518714fe80b4cb37749c9a4e6a5c5c7b94d111398037c03a50953294762360219d23e96f34e0e6c904bf0e5aff3b3473324'
            '10b9b8085ac2f40f2ce9a1430ff93d2e4454e82c1fec8857ca10108da4705929b17da557db9ea3afbfd88c05a9f7bd89a79490988fa03c132aa79e4f5dc8f431'
            'f62e1709d035e996c9f9a887840492527415f157990cc2d52355d8a7fe9382a757c06bed95f97e298f8eea0dd2a846bf7efb8fb84987784a4a797820807be12e')

prepare() {
  cd "$srcdir/maude-$pkgver"

  # Always enabled in CVC4 1.8: https://github.com/CVC4/CVC4/pull/4519
  sed -i '/rewrite-divk/d' src/Mixfix/cvc4_Bindings.cc

  autoreconf -i
}

build() {
  cd "$srcdir/maude-$pkgver"
  ./configure --with-cvc4=yes --with-yices2=no --prefix=/usr --datadir=/usr/share/maude
  make -j1 all CVC4_LIB="-lcvc4 -lcln"
}

check() {
  cd "$srcdir/maude-$pkgver"
  make check
}

package() {
  cd "$srcdir/maude-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m 444 "$srcdir/full-maude3.maude" \
    "$pkgdir/usr/share/maude/full-maude.maude"

  install -D -m 555 "$srcdir/maude.sh" "$pkgdir/etc/profile.d/maude.sh"
}
