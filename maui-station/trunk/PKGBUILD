# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maui-station
_pkgname=station
pkgver=1.2.0
pkgrel=1
pkgdesc="Convergent terminal emulator"
url="https://mauikit.org/"
arch=(x86_64)
license=(GPL3)
depends=(ki18n kcoreaddons qt5-quickcontrols2 qt5-svg)
makedepends=(extra-cmake-modules knotifications)
conflicts=(qmltermwidget)
groups=(maui)
source=(https://invent.kde.org/maui/$_pkgname/-/archive/v$pkgver/$_pkgname-v$pkgver.tar.gz
        https://github.com/Swordfish90/qmltermwidget/archive/v0.1.0/qmltermwidget-0.1.0.tar.gz)
sha256sums=('459fadacc64bdf972cab457fc8b97af60cf449cf4e0576b1ad44e1ac202199ac'
            '127f529a96cf1c96ff201c79458cff67f497b1820b04b0b30cb473423ccb30f9')

build() {
# Bundle qmltermwidget 0.1, see https://gitlab.com/nitrux/mauikit/maui-bug-tracker/-/issues/2
  (
  cd qmltermwidget-0.1.0
  qmake
  make
  )

  cmake -B build -S $_pkgname-v$pkgver
  cmake --build build
}

package() {
  INSTALL_ROOT="$pkgdir" make install -C qmltermwidget-0.1.0
  DESTDIR="$pkgdir" cmake --install build
}
