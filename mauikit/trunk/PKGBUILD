# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mauikit
pkgver=1.2.2
pkgrel=3
pkgdesc="Templated convergent controls and multi-platform utilities for Maui applications"
url="https://mauikit.org/"
arch=(x86_64)
license=(LGPL)
depends=(kconfig kcoreaddons ki18n kirigami2 qt5-svg)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        mauikit-appletdecoration.patch)
sha256sums=('3af24f8f8ac85a3215092d6014e60e324f400bbd96d8135ee273cf1d86dedef6'
            '5ac1b69177039b5335304a39fdb20a5dd2cd00170c9a17f7a6dc8422328be949')

prepare() {
  patch -d $pkgname-$pkgver -p1 < mauikit-appletdecoration.patch # Remove spurious appletdecoration import
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
