# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=mayavi
pkgver=4.7.4
pkgrel=1
pkgdesc="A 3-dimensional visualizer of scientific data"
arch=('x86_64')
url="https://github.com/enthought/mayavi"
license=('BSD')
depends=('vtk' 'python-envisage' 'python-traitsui' 'python-configobj' 'python-pyqt5' 'pdal' 'unixodbc' 'pugixml' 'openmpi' 'ffmpeg' 'python-numpy'
         'fmt' 'ospray' 'openvr' 'liblas' 'cgns' 'adios2' 'libharu' 'gl2ps')
makedepends=('python-setuptools' 'python-sphinx' 'python-twisted' 'xorg-server-xvfb' 'qt5-svg' 'glew' 'proj')
replaces=('python-mayavi')
provides=('python-mayavi')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/mayavi/archive/${pkgver}.tar.gz"
        "disable-openvdb-and-openvr.patch"
        "mayavi.sh" "mayavi.csh")
sha256sums=('44f144a5646e81b7eb0668a8b1ac383a847e634f6f4c306b4629e32587080467'
            'fc3eb33dde9541986a37aa91b7d80aca5754a39a0370688c1f0a5d06d3364853'
            'abb34e78105abf15197bdd05c02ba3b080755d46aa81e4cfaeaba4994d05effe'
            '034b36bf9d279d926c39c858bf18992e34de880725d778f8435b293259f4775d')

prepare() {
  cd "$srcdir"/mayavi-$pkgver

  # skip vtk 9.1.0 classes which cause segfaults
  patch -Np1 -i ../disable-openvdb-and-openvr.patch

  # fix wrong-file-end-of-line-encoding
  for file in *.txt examples/mayavi/data/room_vis.wrl examples/tvtk/dscene.py \
    examples/mayavi/interactive/wx_mayavi_embed*.py ; do
    sed "s|\r||g" $file > $file.new && \
    touch -r $file $file.new && \
    mv $file.new $file
  done

  # file-not-utf8
  for file in *.txt docs/*.txt; do
    iconv -f ISO-8859-1 -t UTF-8 -o $file.new $file && \
    touch -r $file $file.new && \
    mv $file.new $file
  done

  # remove exec permission
  find examples -type f -exec chmod 0644 {} ";"
  chmod 0644 mayavi/tests/data/cellsnd.ascii.inp

  # set path so autodoc can find just built modules
  echo "import sys" >> docs/source/mayavi/conf.py
  echo "sys.path.append('$srcdir/mayavi-$pkgver/build/lib.linux-$CARCH-3.10/')" >> docs/source/mayavi/conf.py
}

build() {
  cd "$srcdir"/mayavi-$pkgver

  rm -rf build
  mkdir build
  mkdir -p build/lib.linux-$CARCH-3.10/{tvtk/plugins/scene,mayavi/preferences,mayavi/core}
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/tvtk/tvtk_classes.zip \
    build/lib.linux-$CARCH-3.10/tvtk/tvtk_classes.zip
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/mayavi/preferences/preferences.ini \
    build/lib.linux-$CARCH-3.10/mayavi/preferences/preferences.ini
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/tvtk/plugins/scene/preferences.ini \
    build/lib.linux-$CARCH-3.10/tvtk/plugins/scene/preferences.ini

  export ETS_TOOLKIT="qt"
  # now this is ugly, but docs do not build without X. We setup X according to:
  # http://docs.enthought.com/mayavi/mayavi/tips.html#rendering-using-the-virtual-framebuffer
  xvfb-run python setup.py build

  rm build/lib.linux-$CARCH-3.10/tvtk/tvtk_classes.zip
  rm build/lib.linux-$CARCH-3.10/mayavi/preferences/preferences.ini
  rm build/lib.linux-$CARCH-3.10/tvtk/plugins/scene/preferences.ini
}

package() {
  cd "$srcdir"/mayavi-$pkgver

  python setup.py install --skip-build --root="$pkgdir"/ --optimize=1

  # remove useless files
  rm -f "${pkgdir}"/usr/lib/python3.10/site-packages/tvtk/setup.py*
  find "${pkgdir}" -name \.buildinfo -type f -print | xargs rm -f -

  # non-executable-script
  chmod +x "${pkgdir}"/usr/lib/python3.10/site-packages/mayavi/tests/runtests.py

  # install manpage
  mkdir -p "${pkgdir}"/usr/share/man/man1
  cp -p docs/mayavi2.man "${pkgdir}"/usr/share/man/man1/mayavi2.1

  # Force Qt
  install -d "${pkgdir}"/etc/profile.d
  install -Dm644 "${srcdir}"/mayavi.sh "${pkgdir}"/etc/profile.d/mayavi.sh
  install -Dm644 "${srcdir}"/mayavi.csh "${pkgdir}"/etc/profile.d/mayavi.csh

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Cannot find vtk
  sed -i '/vtk/d' "$pkgdir"/usr/lib/python3.10/site-packages/mayavi-${pkgver}-py3.10.egg-info/requires.txt
}
