# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Jingbei Li <i@jingbei.li>

pkgname=mayavi
pkgver=4.7.3
pkgrel=1
pkgdesc="A 3-dimensional visualizer of scientific data"
arch=('x86_64')
url="https://github.com/enthought/mayavi"
license=('BSD')
depends=('vtk' 'python-envisage' 'python-traitsui' 'python-configobj' 'python-pyqt5' 'pdal' 'unixodbc' 'pugixml' 'openmpi' 'ffmpeg' 'python-numpy')
makedepends=('python-setuptools' 'python-sphinx' 'python-twisted' 'xorg-server-xvfb' 'qt5-svg' 'glew' 'proj')
replaces=('python-mayavi')
provides=('python-mayavi')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/mayavi/archive/${pkgver}.tar.gz"
        "mayavi.sh" "mayavi.csh")
sha256sums=('5bfc3f852748b3343dc9beef19bff5f7865124130a44ab191a0f7ceb396efaee'
            'abb34e78105abf15197bdd05c02ba3b080755d46aa81e4cfaeaba4994d05effe'
            '034b36bf9d279d926c39c858bf18992e34de880725d778f8435b293259f4775d')

prepare() {
  cd "$srcdir"/mayavi-$pkgver

  # fix wrong-file-end-of-line-encoding
  for file in *.txt examples/mayavi/data/room_vis.wrl examples/tvtk/dscene.py \
    examples/mayavi/interactive/wx_mayavi_embed*.py ; do
    sed "s|\r||g" $file > $file.new && \
    touch -r $file $file.new && \
    mv $file.new $file
  done

  # file-not-utf8
  for file in *.txt docs/*.txt; do
    iconv -f ISO-8859-1 -t UTF-8 -o $file.new $file && \
    touch -r $file $file.new && \
    mv $file.new $file
  done

  # remove exec permission
  find examples -type f -exec chmod 0644 {} ";"
  chmod 0644 mayavi/tests/data/cellsnd.ascii.inp

  # set path so autodoc can find just built modules
  echo "import sys" >> docs/source/mayavi/conf.py
  echo "sys.path.append('$srcdir/mayavi-$pkgver/build/lib.linux-$CARCH-3.9/')" >> docs/source/mayavi/conf.py
}

build() {
  cd "$srcdir"/mayavi-$pkgver

  rm -rf build
  mkdir build
  mkdir -p build/lib.linux-$CARCH-3.9/{tvtk/plugins/scene,mayavi/preferences,mayavi/core}
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/tvtk/tvtk_classes.zip \
    build/lib.linux-$CARCH-3.9/tvtk/tvtk_classes.zip
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/mayavi/preferences/preferences.ini \
    build/lib.linux-$CARCH-3.9/mayavi/preferences/preferences.ini
  ln -s "$srcdir"/enthought-mayavi-${_githubtag}/tvtk/plugins/scene/preferences.ini \
    build/lib.linux-$CARCH-3.9/tvtk/plugins/scene/preferences.ini

  export ETS_TOOLKIT="qt"
  # now this is ugly, but docs do not build without X. We setup X according to:
  # http://docs.enthought.com/mayavi/mayavi/tips.html#rendering-using-the-virtual-framebuffer
  xvfb-run python setup.py build

  rm build/lib.linux-$CARCH-3.9/tvtk/tvtk_classes.zip
  rm build/lib.linux-$CARCH-3.9/mayavi/preferences/preferences.ini
  rm build/lib.linux-$CARCH-3.9/tvtk/plugins/scene/preferences.ini
}

package() {
  cd "$srcdir"/mayavi-$pkgver

  python setup.py install --skip-build --root="$pkgdir"/ --optimize=1

  # remove useless files
  rm -f "${pkgdir}"/usr/lib/python3.9/site-packages/tvtk/setup.py*
  find "${pkgdir}" -name \.buildinfo -type f -print | xargs rm -f -

  # non-executable-script
  chmod +x "${pkgdir}"/usr/lib/python3.9/site-packages/mayavi/tests/runtests.py

  # install manpage
  mkdir -p "${pkgdir}"/usr/share/man/man1
  cp -p docs/mayavi2.man "${pkgdir}"/usr/share/man/man1/mayavi2.1

  # Force Qt
  install -d "${pkgdir}"/etc/profile.d
  install -Dm644 "${srcdir}"/mayavi.sh "${pkgdir}"/etc/profile.d/mayavi.sh
  install -Dm644 "${srcdir}"/mayavi.csh "${pkgdir}"/etc/profile.d/mayavi.csh

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Cannot find vtk
  sed -i '/vtk/d' "$pkgdir"/usr/lib/python3.9/site-packages/mayavi-${pkgver}-py3.9.egg-info/requires.txt
}
