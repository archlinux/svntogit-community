# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=mbedtls
pkgver=2.28.1
pkgrel=1
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('x86_64')
url="https://tls.mbed.org"
license=('Apache')
depends=('glibc' 'sh')
checkdepends=('python')
makedepends=('cmake')
provides=('polarssl' 'libmbedcrypto.so' 'libmbedtls.so' 'libmbedx509.so')
replaces=('polarssl')
conflicts=('polarssl')
options=('staticlibs')
source=(https://github.com/ARMmbed/mbedtls/archive/${pkgname}-${pkgver}.tar.gz)
sha256sums=('82ff5fda18ecbdee9053bdbeed6059c89e487f3024227131657d4c4536735ed1')
b2sums=('85969895b50b63537438e0b67e1dec97a0ee4ac188c55ebcb109d5e0cc8f4a3f66f016b2ac5f920d7b10f094c9626029d19ab86be0cb5941224a7f3eed2dcc06')

prepare() {
  cd "$pkgname-$pkgname-$pkgver"
  # enable flags for non-embedded systems
  sed -i 's|//\(#define MBEDTLS_THREADING_C\)|\1|' include/mbedtls/config.h
  sed -i 's|//\(#define MBEDTLS_THREADING_PTHREAD\)|\1|' include/mbedtls/config.h
}

build() {
  cd "$pkgname-$pkgname-$pkgver"
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="${CFLAGS} -ffat-lto-objects" \
    -DMBEDTLS_FATAL_WARNINGS=OFF \
    -DCMAKE_VERBOSE_MAKEFILE=ON \
    -DUSE_SHARED_MBEDTLS_LIBRARY=ON \
    -DCMAKE_INSTALL_RPATH=
  make -C build
}

check() {
  cd "$pkgname-$pkgname-$pkgver"
  make -C build test
  # make check?
}

package() {
  cd "$pkgname-$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir" install

  # rename generic utils
  local _prog _baseprog
  for _prog in "$pkgdir"/usr/bin/*; do
	_baseprog=$(basename "$_prog")
    mv -v "$_prog" "${_prog//$_baseprog/mbedtls_$_baseprog}"
  done

  # fixup static lib permissions
  chmod 644 "$pkgdir"/usr/lib/*.a
}
