# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor:  3ED <kas1987@o2.pl>

pkgname=mcabber
pkgver=1.1.0
pkgrel=2
pkgdesc="A small Jabber console client, includes features: SSL, PGP, MUC, UTF8"
arch=('x86_64')
url="https://www.lilotux.net/~mikael/mcabber/"
license=('GPL')
depends=('ncurses' 'glib2' 'loudmouth' 'gpgme' 'libotr' 'aspell')
makedepends=('pkg-config')
validpgpkeys=('EACADFF156849BC89653139E3C2900DEACB7FC95')
source=(https://www.lilotux.net/~mikael/mcabber/files/$pkgname-${pkgver/_/-}.tar.bz2{,.asc})
sha256sums=('04fc2c22c36da75cf4b761b5deccd074a19836368f38ab9d03c1e5708b41f0bd'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-${pkgver/_/-}
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --prefix=/usr --enable-hgcset \
      --mandir=/usr/share/man \
      --enable-aspell --enable-otr
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver/_/-}

  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/usr/share/mcabber/example
  cp -a "${srcdir}"/$pkgname-${pkgver/_/-}/mcabberrc.example \
	"${pkgdir}"/usr/share/mcabber/example/mcabberrc
}

