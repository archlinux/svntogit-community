#Maintainer: jakob gruber <jakob.gruber@gmail.com>
#Contributor: gborzi
#Previous Contributor: Vinzenz Vietzke <vinz@archlinux.us>
#Previous Contributor: Heiko Baums <heiko@baums-on-web.de>
#Previous Contributor: Jeff Bailes <thepizzaking@gmail.com>

pkgname=me-tv
pkgver=2.0.1
pkgrel=3
pkgdesc="A GTK desktop application for watching digital television services that use the DVB standard"
arch=('i686' 'x86_64')
url="https://launchpad.net/me-tv"
license=('GPL')
depends=('glibmm' 'gtkmm' 'gconfmm' 'glib2' 'xine-lib' 'sqlite3' 'linuxtv-dvb-apps' 'libunique' 'libxml++' 'vlc' 'gstreamer0.10-base')
makedepends=('intltool')
install='me-tv.install'
source=("http://launchpad.net/${pkgname}/${pkgver:0:3}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
options=('!emptydirs')
md5sums=('7cda820b795f2fea522ba3de5e312483')

build() {

	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure CXXFLAGS="$CXXFLAGS -D__KERNEL_STRICT_NAMES" --prefix=/usr
	make

}

package() {

	cd "${srcdir}/${pkgname}-${pkgver}"

	make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
	gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
        --domain me-tv "${pkgdir}/usr/etc/gconf/schemas/${pkgname}.schemas"

	rm -rf "${pkgdir}/usr/etc/"

}
