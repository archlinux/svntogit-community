#Maintainer: jakob gruber <jakob.gruber@gmail.com>
#Contributor: gborzi
#Previous Contributor: Vinzenz Vietzke <vinz@archlinux.us>
#Previous Maintainer: Vinzenz Vietzke <vinz@archlinux.us>
#Previous Contributor: Heiko Baums <heiko@baums-on-web.de>
#Previous Contributor: Jeff Bailes <thepizzaking@gmail.com>

pkgname=me-tv
pkgver=1.3.2
pkgrel=1
pkgdesc="A GTK desktop application for watching digital television services that use the DVB standard"
arch=('i686' 'x86_64')
url="https://launchpad.net/me-tv"
license=('GPL')
depends=('glibmm' 'gtkmm' 'gconfmm' 'glib2' 'xine-lib' 'sqlite3' 'linuxtv-dvb-apps' 'libunique')
makedepends=('intltool')
install=me-tv.install
source=(http://launchpad.net/${pkgname}/1.3/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
options=('!emptydirs')
md5sums=('15a63faa09e751e4a345de3a38a31c52')

build() {

	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure CXXFLAGS="$CXXFLAGS -D__KERNEL_STRICT_NAMES" --prefix=/usr
	make

}

package() {

	cd "${srcdir}/${pkgname}-${pkgver}"

	make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

	install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
	gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain me-tv "${pkgdir}/usr/etc/gconf/schemas/${pkgname}.schemas"

	rm -rf "${pkgdir}/usr/etc/"

}
