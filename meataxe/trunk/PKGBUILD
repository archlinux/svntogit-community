# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=meataxe
pkgver=2.4.24
pkgrel=6
pkgdesc="A set of programs for working with matrix representations over finite fields"
arch=(x86_64)
url="http://www.math.rwth-aachen.de/~MTX/"
license=(GPL)
depends=(glibc)
#source=("http://www.math.rwth-aachen.de/~MTX/$pkgname-$pkgver.tar.gz")
source=("http://mirrors.mit.edu/sage/spkg/upstream/meataxe/meataxe-$pkgver.tar.gz"
	"https://git.sagemath.org/sage.git/plain/build/pkgs/meataxe/patches/IO_fixes.patch"
	"https://git.sagemath.org/sage.git/plain/build/pkgs/meataxe/patches/StrassenWinogradImplementation.patch"
	"https://git.sagemath.org/sage.git/plain/build/pkgs/meataxe/patches/StrassenWinogradUsage.patch"
	"https://git.sagemath.org/sage.git/plain/build/pkgs/meataxe/patches/TweakEchelon.patch"
	"https://git.sagemath.org/sage.git/plain/build/pkgs/meataxe/patches/UseErrorPropagation.patch"
        meataxe-shared.patch)
sha256sums=('c3b131218d293c6a68620cafe5da589241f157cd07887e491e7720ba4b469dd1'
            'a0c2141d7741326a304c15a15d621215ec7275d84954cde53e34eac873e56eaa'
            'b49c32eb1836f13a3c348cf56bff790f78a73e349e70e2172f60a949f17c28df'
            '4c7538a55bb0f29b7d95601f6fae408b1742db5aa9954cab22cdd01d8042a272'
            'a36b4e966fcbe6ac63216538038ef45391d44029894f386803d858f8fe32a39c'
            '4c4d2cbec389ed561af10a5558eea87b6f301f8db96e44c06a8ca54a7709f6fd'
            'aa05e4b235c078ce1d7807ae7320986f56fa6e38033c21328ac298a6e44df411')

prepare() {
  cd $pkgname-$pkgver
  for _patch in IO_fixes StrassenWinogradImplementation StrassenWinogradUsage TweakEchelon UseErrorPropagation; do
   patch -p1 -i ../$_patch.patch
  done
  patch -p0 -i ../meataxe-shared.patch # build shared lib (Fedora)
}

build() {
  cd $pkgname-$pkgver

  export ZZZ=0
  export MTXLIB="$HOME/.local/share/meataxe"
  export MTXBIN=/usr/bin
  touch Makefile.conf
  make LFLAGS1="$LDFLAGS" CFLAGS1="$CFLAGS"
}

package() {
  cd $pkgname-$pkgver
  
  mkdir -p "$pkgdir"/usr/{include,bin,lib,share/doc/meataxe}
  install -m755 bin/* "$pkgdir"/usr/bin
  install -m644 tmp/libmtx.so "$pkgdir"/usr/lib
  install -m644 src/meataxe.h "$pkgdir"/usr/include
  install -m644 doc/* "$pkgdir"/usr/share/doc/meataxe
}
