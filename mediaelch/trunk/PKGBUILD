# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.8.12
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=('LGPL3')
depends=('glibc' 'gcc-libs' 'libmediainfo' 'qt5-declarative' 'qt5-multimedia'
         'qt5-script' 'qt5-svg' 'quazip')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c3f30fb18ad496057984ea7ea2593710ec4069eda1f4324ba10dffb66a5c3a75')
b2sums=('f0c8afa6b12988621d339b5c6b56d7dd4354d61598a2cb26a2f4a75784d197aad5e2d425d59032b174efb57b079ec9a9fd948553e74f5e1bcea8b49a494dc047')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip5/|QuaZip-Qt5-1.1/quazip/|g' -i src/*/*.cpp src/*/*.h
  sed 's|quazip5|quazip1-qt5|g' -i */CMakeLists.txt */*/CMakeLists.txt MediaElch.pro
}

build() {
  cd MediaElch-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DDISABLE_UPDATER=ON \
    -DUSE_EXTERN_QUAZIP=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd MediaElch-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
