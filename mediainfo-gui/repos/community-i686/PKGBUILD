# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: hydro <hydro@freenet.de>

pkgname=mediainfo-gui
pkgver=0.7.65
pkgrel=6
pkgdesc="GUI for mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('GPL')
depends=('libmediainfo' 'wxgtk' 'libsm' 'desktop-file-utils')
install=mediainfo-gui.install
source=(http://downloads.sourceforge.net/mediainfo/mediainfo_${pkgver}.tar.bz2)
md5sums=('19d7992a7d0cba0f5f731f993e8c537c')

build() {
  cd MediaInfo/Project/GNU/GUI
  sh ./autogen
  ./configure --prefix=/usr
  make
}

package() {
  cd MediaInfo/Project/GNU/GUI
  make DESTDIR="$pkgdir" install
  install -D -m 644 "$srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png" \
                    "$pkgdir/usr/share/icons/hicolor/128x128/apps/mediainfo.png"
  install -D -m 644 "$srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png" \
                    "$pkgdir/usr/share/pixmaps/mediainfo-gui.png"
  install -D -m 644 "$srcdir/MediaInfo/Project/GNU/GUI/mediainfo-gui.desktop" \
                    "$pkgdir/usr/share/applications/mediainfo-gui.desktop"
}
