# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: hydro <hydro@freenet.de>

pkgname=mediainfo-gui
pkgver=0.7.62
pkgrel=1
pkgdesc="GUI for mediainfo"
arch=('i686' 'x86_64')
url="http://mediainfo.sourceforge.net"
license=('GPL')
depends=('libmediainfo' 'wxgtk')
makedepends=('libtool' 'automake' 'autoconf')
install=mediainfo-gui.install
source=(http://downloads.sourceforge.net/mediainfo/mediainfo_${pkgver}.tar.bz2)
md5sums=('a9a8bc398cc5d2ca8723fae024f6e891')

build() {
  cd $srcdir/MediaInfo/Project/GNU/GUI
  sh ./autogen
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/MediaInfo/Project/GNU/GUI
  make DESTDIR=$pkgdir install
  install -D -m 644 $srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png \
                    $pkgdir/usr/share/icons/hicolor/128x128/apps/mediainfo.png
  install -D -m 644 $srcdir/MediaInfo/Source/Resource/Image/MediaInfo.png \
                    $pkgdir/usr/share/pixmaps/mediainfo-gui.png
  install -D -m 644 $srcdir/MediaInfo/Project/GNU/GUI/mediainfo-gui.desktop \
                    $pkgdir/usr/share/applications/mediainfo-gui.desktop
}
