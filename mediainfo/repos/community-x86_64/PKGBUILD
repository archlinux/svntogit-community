# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: hydro <hydro@freenet.de>

pkgbase=mediainfo
pkgname=(mediainfo mediainfo-gui)
pkgver=22.09
pkgrel=1

pkgdesc="Supplies technical and tag information about a video or audio file"
url="https://mediaarea.net/"
arch=('x86_64')
license=('BSD')

depends=('libmediainfo')
makedepends=('wxwidgets-gtk3')

source=(https://mediaarea.net/download/source/mediainfo/$pkgver/mediainfo_$pkgver.tar.xz)

sha512sums=('acdeef13153fd74c29d54b9bdf4e983dd81525ace47550977d99cb5950ab72579cbbfc1ae39d81d89e03c491ea559177a16853d9db20d83a995eff300b864d4d')
b2sums=('c4b1766c4d49be6e90d11c736e4572e24299ae96efdfbf92ef9d32a418d7777cad2ae60ac7e6234cfa7c3a11996e46fb178996c536b9ce13dd19a0449ae8bf86')

build() {
  cd MediaInfo/Project/GNU/CLI
  ./autogen.sh
  ./configure --prefix=/usr
  make

  cd "$srcdir/MediaInfo/Project/GNU/GUI"
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package_mediainfo() {
  pkgdesc+=" (CLI interface)"

  cd MediaInfo/Project/GNU/CLI
  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfo/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mediainfo-gui() {
  pkgdesc+=" (GUI interface)"
  depends+=('wxwidgets-gtk3' 'hicolor-icon-theme')

  cd MediaInfo/Project/GNU/GUI
  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfo/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
