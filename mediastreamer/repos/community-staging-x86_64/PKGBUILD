# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=mediastreamer
pkgver=4.3.1
pkgrel=2
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=(x86_64)
url="https://github.com/BelledonneCommunications/mediastreamer2"
license=(GPL)
depends=(ortp ffmpeg libupnp bzrtp glew libsrtp)
makedepends=(cmake python bcunit doxygen)
source=("$pkgname-$pkgver.tar.gz::https://github.com/BelledonneCommunications/mediastreamer2/archive/$pkgver.tar.gz")
sha256sums=('0b33ca111fbc6202ad55decee4c185b2ee42699ab0694b6281dee1180fdef4ba')
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}2-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
