# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=mediastreamer
pkgver=4.5.22
pkgrel=1
pkgdesc='A library written in C that allows you to create and run audio and video streams'
arch=(x86_64)
url='https://github.com/BelledonneCommunications/mediastreamer2'
license=(GPL)
depends=(ortp ffmpeg bzrtp glew libsrtp)
makedepends=(cmake python bcunit doxygen)
source=(https://github.com/BelledonneCommunications/mediastreamer2/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8043e42bf0a017d97500f8817e89d7ba4c1e5222365b502bbbfeb9e30fa7b1a9')
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')

build() {
  cmake -B build -S ${pkgname}2-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF \
    -DENABLE_STRICT=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
