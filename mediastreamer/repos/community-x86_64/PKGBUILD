# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=mediastreamer
pkgver=5.0.11
pkgrel=1
pkgdesc='A library written in C that allows you to create and run audio and video streams'
arch=(x86_64)
url='https://github.com/BelledonneCommunications/mediastreamer2'
license=(GPL)
depends=(ortp ffmpeg bzrtp glew libsrtp)
makedepends=(cmake python bcunit doxygen)
source=(https://github.com/BelledonneCommunications/mediastreamer2/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a07b6928037fd59fe0b2b2823990166f7b57c421c69ac98cbeda97653073d41e')
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')

build() {
  cmake -B build -S ${pkgname}2-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF \
    -DENABLE_STRICT=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/include/OpenGL
}
