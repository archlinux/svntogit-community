# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=mediastreamer
pkgver=2.14.0
pkgrel=4
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL')
depends=('ortp' 'ffmpeg' 'libxv' 'libupnp' 'bzrtp' 'glew' 'libsrtp' 'mbedtls' 'bctoolbox')
# xxd from Vim is needed to build
makedepends=('intltool' 'vim' 'cunit')
#source=("http://download-mirror.savannah.gnu.org/releases/linphone/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
source=("$pkgname-$pkgver.tar.gz::https://github.com/BelledonneCommunications/mediastreamer2/archive/$pkgver.tar.gz")
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')
md5sums=('ba7c44433b10e924036c01fdbee44040')

prepare() {
  cd ${pkgname}2-$pkgver
  patch src/crypto/dtls_srtp.c <<EOF
diff -r mediastreamer2-2.14.0/src/crypto/dtls_srtp.c mediastreamer2-2.14.0.q/src/crypto/dtls_srtp.c
708c708
< 	bctbx_ssl_config_set_ca_chain(dtlsContext->ssl_config, dtlsContext->crt, NULL);
---
> 	bctbx_ssl_config_set_ca_chain(dtlsContext->ssl_config, dtlsContext->crt);
EOF
}

build() {
  cd ${pkgname}2-$pkgver
  export CFLAGS="$CFLAGS -Dbctbx_list_remove_link=bctbx_list_unlink -Dbctbx_list_delete_link=bctbx_list_erase_link"
  [ -x ./configure ] || ./autogen.sh
  ./configure --prefix=/usr \
    --enable-glx --enable-xv --enable-zrtp --enable-external-ortp \
    --disable-strict
  make
}

package() {
  cd ${pkgname}2-$pkgver
  make DESTDIR="${pkgdir}" install
}
