# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=mediastreamer
pkgver=5.0.6
pkgrel=1
pkgdesc='A library written in C that allows you to create and run audio and video streams'
arch=(x86_64)
url='https://github.com/BelledonneCommunications/mediastreamer2'
license=(GPL)
depends=(ortp ffmpeg bzrtp glew libsrtp)
makedepends=(cmake python bcunit doxygen)
source=(https://github.com/BelledonneCommunications/mediastreamer2/archive/$pkgver/$pkgname-$pkgver.tar.gz
        fix-build.patch)
sha256sums=('c52986230b219d91d5590cf6a6c2a01262e5c9e056644021a0554383999b2c9a'
            '6e421e02a302c408975e8e01580ae72f35f9e9d7dc043542eade38961cb8e4f9')
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')

prepare() {
  patch -d ${pkgname}2-$pkgver -p1 < fix-build.patch
}

build() {
  cmake -B build -S ${pkgname}2-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF \
    -DENABLE_STRICT=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/include/OpenGL
}
