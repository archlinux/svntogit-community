# $Id$
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Nikhil Bysani <nikron@gmail.com>
# Contributor: Mika HynnÃ¤ <igheax@gmail.com>
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

pkgname=mediatomb
pkgver=0.12.1
pkgrel=5
pkgdesc="Free UPnP/DLNA media server"
arch=('i686' 'x86_64')
url="http://mediatomb.cc/"
license=('GPL')
depends=('curl' 'ffmpegthumbnailer' 'js' 'libexif' 'libmp4v2' 'sqlite3' 'taglib')
backup=('etc/conf.d/mediatomb')
install=mediatomb.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'mediatomb.rc'
        'mediatomb.conf'
        'gcc46.patch'
	'tonewjs.patch')
md5sums=('e927dd5dc52d3cfcebd8ca1af6f0d3c2'
         'aa1191ec508d8bd4b3b9a5fe48efc079'
         'bec297e4178332a26b42bbde873b94cd'
         '0ae34c0d73b76e3d215887834c3c08cf'
	 'd9e02a9956eecf5ff645bddf6dac0331')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/gcc46.patch
  patch -Np1 -i $srcdir/tonewjs.patch

  ./configure --prefix=/usr \
              --disable-mysql \
              --enable-libmagic \
              --enable-libjs \
              --enable-ffmpeg
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -D -m0755 "$srcdir/mediatomb.rc" "$pkgdir/etc/rc.d/mediatomb"
  install -D -m0755 "$srcdir/mediatomb.conf" "$pkgdir/etc/conf.d/mediatomb"
  install -d "$pkgdir/var/lib/mediatomb"
}
