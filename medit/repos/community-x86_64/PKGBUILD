# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=medit
pkgver=1.2.0
pkgrel=3
pkgdesc="A GTK text editor"
arch=('i686' 'x86_64')
url="http://mooedit.sourceforge.net"
options=('!emptydirs')
license=('GPL')
depends=('pygtk' 'libxml2' 'pcre' 'libsm' 'python2' 'gtk2' 'gcc-libs' 'gamin')
optdepends=('ctags' 'vte: terminal emulator')
makedepends=(pkg-config perl-xml-parser intltool gcc-objc)
source=(http://downloads.sourceforge.net/mooedit/medit-$pkgver.tar.bz2)
md5sums=('8388a8e275d0e1fa6cd19f7bb081279a')

build() {
  cd $srcdir/medit-$pkgver
  export PYTHON=/usr/bin/python2
  [ -f Makefile ] || ./configure --prefix=/usr --with-fam
  make
}

package() {
  cd $srcdir/medit-$pkgver
  make DESTDIR=${pkgdir} install
  cd ${pkgdir}/usr/share/medit-1/language-specs
  sed -i "s|*.sh|*.sh;PKGBUILD|" sh.lang
  rm -rf $pkgdir/usr/share/mime
  rm -rf $pkgdir/usr/share/icons/hicolor/icon-theme.cache
}
