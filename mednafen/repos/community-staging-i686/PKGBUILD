# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: kagan <juanynie@mi.madritel.es>
# Contributor: Hans-Kristian Arntzen <maister@archlinux.us>

pkgname=mednafen
pkgver=0.9.38.7
pkgrel=2
pkgdesc="Command-line multi-system gaming emulator"
url="http://mednafen.fobby.net"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libsndfile' 'sdl' 'zlib')
makedepends=('mesa' 'glu')
source=(http://mednafen.fobby.net/releases/files/$pkgname-$pkgver.tar.bz2)
md5sums=('e49c6b4c0c8278bc01c63cba9b54498c')

build() {
  cd $pkgname

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  for i in ABOUT-NLS AUTHORS ChangeLog COPYING INSTALL NEWS \
           README{,.DOS,.PORTING} TODO; do
    install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
  done
}
