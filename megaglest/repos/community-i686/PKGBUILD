# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>
 
pkgname=megaglest
pkgver=3.5.2.4
pkgrel=1
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/megaglest/"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl' 'libvorbis' 'openal' 'mesa' 'lua' 'icu')
makedepends=('ftjam' 'p7zip' 'wxgtk' 'cmake')
source=("http://downloads.sourceforge.net/project/${pkgname}/current_release/${pkgname}-source-${pkgver}.tar.xz")
md5sums=('0f8a3857d41fce8a726eebcc4826ad60')

build() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"/

  [[ -d build ]] && rm -r build
  mkdir build && cd build
  cmake .. \
   -DWANT_SVN_STAMP=OFF \
 	 -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release # set =Debug for debugging version
  make
}

package() {
  cd "${srcdir}"/"${pkgname}"-"${pkgver}"/

  cd build
  make DESTDIR="$pkgdir" install
}

# vim: sw=2:ts=2 et:
