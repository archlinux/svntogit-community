# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=megaglest
pkgver=3.11.1
pkgrel=5
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/megaglest/"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl' 'libvorbis' 'openal' 'libgl' 'lua51' 'icu' 'ftgl' 'glew'
'libircclient' 'miniupnpc' 'wxgtk2.8' 'glu' 'libx11')
makedepends=('ftjam' 'cmake' 'mesa' 'git')
source=("git+https://github.com/MegaGlest/megaglest-source.git#tag=${pkgver}"
        "miniupnpc14.patch")
md5sums=('SKIP'
         '9774a0ab4daa635d3ea7bb6d3ec9834c')

prepare() {
  cd megaglest-source
  patch -Np1 -i ../miniupnpc14.patch
}

build() {
  cd megaglest-source

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-lX11" \
    -DWANT_GIT_STAMP=0 \
    -DwxWidgets_CONFIG_EXECUTABLE="/usr/bin/wx-config-2.8" \
    -DwxWidgets_wxrc_EXECUTABLE="/usr/bin/wxrc-2.8"
  make VERBOSE=1
}

package() {
  cd megaglest-source/build

  make DESTDIR="$pkgdir" install
}

# vim: sw=2:ts=2 et:
