# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_pkgname=MeiliSearch
pkgname=meilisearch
pkgver=0.25.2
pkgrel=1
pkgdesc="Lightning Fast, Ultra Relevant, and Typo-Tolerant Search Engine"
arch=(x86_64)
url="https://docs.meilisearch.com/"
license=('MIT')
depends=(gcc-libs)
makedepends=(rust)
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/MeiliSearch/archive/v${pkgver}.tar.gz
        ${pkgname}.service ${pkgname}.tmpfiles ${pkgname}.sysusers ${pkgname}.conf)
sha512sums=('a9b800b6f3869105354a8bde20970d180d8108ead8064326d82063d1d9b3951fbd374efcd739a6c05aea0662e32c5a98ee91d858780e1c310e91d9e59929fdb7'
            '9531963580e1d20cc87c7062d727367b628a02b3e599311df7272e59cc14b0b7d16218b793436587b4a64d7fe009f877a86f5b18bdd42783afd8f42af9be0af8'
            '76284bf4c436a6b3365edd6bd12ffcb186b721d284b29933e77ddc841d333be9eeb2701c89fba1567a8eedda52787be8d3c5f55809a051488b7295fbb6842bce'
            'b83518bd61cb587baac784b1b9bbe4a6a12be91c0a5b6cc0b3b631dde9cee1d59d6e43800a0d32b909fb2d90fe3f64e1ab63c3c8028428be3333d39f6c05d60c'
            '9d63a9cca6cafb248fffcd4a6e5bac0905d4b87f3eb9793576003d3965e35e217c4c1d6b3206b25ef47143e7d538e71c746bc0352bcdf248ed19e12f58aedc12')
validpgpkeys=()

build() {
  cd "$_pkgname-$pkgver"
  cargo build --release --locked --all-features
}

check() {
  cd "$_pkgname-$pkgver"
  ulimit -n 3000
  cargo test --release --locked
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

  install -D -m644 "${srcdir}/meilisearch.service"  "${pkgdir}/usr/lib/systemd/system/meilisearch.service"
  install -D -m644 "${srcdir}/meilisearch.sysusers" "${pkgdir}/usr/lib/sysusers.d/meilisearch.conf"
  install -D -m644 "${srcdir}/meilisearch.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/meilisearch.conf"
  install -D -m644 "${srcdir}/meilisearch.conf" "${pkgdir}/etc//meilisearch.conf"
}
