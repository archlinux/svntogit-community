# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=menu-cache
pkgver=1.1.0
pkgrel=1
pkgdesc='Caching mechanism for freedesktop.org compliant menus'
arch=('x86_64')
license=('GPL2')
url='http://lxde.org/'
depends=('libfm-extra')
makedepends=('gtk-doc')
source=(https://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.xz)
sha256sums=('ed02eb459dcb398f69b9fa5bf4dd813020405afc84331115469cdf7be9273ec7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --enable-gtk-doc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
