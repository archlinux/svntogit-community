# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mephisto.lv2
pkgver=0.16.0
pkgrel=1
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libvterm' 'libx11' 'lv2-host' 'ttf-fira-code' 'ttf-fira-sans')
makedepends=('faust' 'fontconfig' 'glew' 'glu' 'lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('d5b954303f36c5351bf301b499b555556dc141b13f612be9ecbbfea24c62ecc2afdfc5de868795c635ed5806efe189b3b174b76e01e3764ab4616a0d82ed2657'
            'SKIP')
b2sums=('71bf6ecdbdaacf6bb100d93b8f36be68b3b09798955fd786e06b21bc7c49332aa84fd21cdcb412debf83312073db36773495ab55f9fe598c0651c2e616ed8fdb'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  depends+=('libGLEW.so' 'libfaust.so' 'libfontconfig.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" ninja -C build install
  # devendoring ttf-fira-code
  rm -vf "${pkgdir}/usr/lib/lv2/${pkgname}/"*.ttf
  for font_type in {Bold,Light,Medium,Regular}; do
    ln -svf "/usr/share/fonts/TTF/FiraCode-${font_type}.ttf" \
      "${pkgdir}/usr/lib/lv2/${pkgname}/"
  done
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
