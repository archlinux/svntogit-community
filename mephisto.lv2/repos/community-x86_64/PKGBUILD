# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mephisto.lv2
pkgver=0.18.2
pkgrel=1
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=(x86_64)
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=(Artistic2.0)
groups=(lv2-plugins pro-audio)
depends=(glibc libglvnd libvterm libx11 lv2-host ttf-fira-code ttf-fira-sans)
makedepends=(faust fontconfig glew glu lv2 meson)
checkdepends=(lv2lint)
options=(debug)
source=(https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz{,.asc})
sha512sums=('6136dcc32c41bd494f156e84be0fb417efbcb42fbddfaff7f54d3e11dc4bba29957cf919b25ada27b8d0e796abbfbe3c7bd0e0fba3698d4871b166b38ba5fa2d'
            'SKIP')
b2sums=('5ef07088b8ba006dcfc511c7b34a4fabb5482b4a3bd7287ecafbdb0414a10d7f7058a21ea66e94e78c35c3b32dc0e9911e40b920f1dddcd8f377e5596b521c7d'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  arch-meson build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(libGLEW.so libfaust.so libfontconfig.so)

  DESTDIR="$pkgdir" ninja -C build install
  # devendor ttf-fira-code
  rm -vf "$pkgdir/usr/lib/lv2/$pkgname/"*.ttf
  for font_type in {Bold,Light,Medium,Regular}; do
    ln -svf /usr/share/fonts/TTF/FiraCode-$font_type.ttf "$pkgdir/usr/lib/lv2/$pkgname/"
  done
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
