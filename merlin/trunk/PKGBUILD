# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=merlin
pkgver=3.2.2
pkgrel=1
pkgdesc="Context sensitive completion for OCaml in Vim and Emacs"
arch=('x86_64')
url="https://github.com/ocaml/merlin"
license=('MIT')
options=('!strip')
depends=('glibc')
makedepends=('ocaml-findlib' 'ocaml-yojson' 'dune')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('689ae38e2f50e7b02d257d502cb55e26b425b4ecb2108af9df7d769632694b17')

build() {
    cd ${pkgname}-${pkgver}
    make build
}

check() {
    cd ${pkgname}-${pkgver}
    make test || warning "Tests failed"
}

package() {
    cd ${pkgname}-${pkgver}
    # Work around install command
    dune install --prefix="${pkgdir}"/usr/ --libdir="${pkgdir}$(ocamlfind printconf destdir)"
    install -Dm644 LICENSE_MIT.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
    # Fix path
    install -d "${pkgdir}"/usr/share/vim
    mv "${pkgdir}"/usr/share/{merlin/vim,vim/vimfiles}
    rmdir "${pkgdir}"/usr/share/merlin
    # Remove useless files
    rm -r "${pkgdir}"/usr/doc
}
