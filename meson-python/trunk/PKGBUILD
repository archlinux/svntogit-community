# Maintainer:

pkgname=meson-python
pkgver=0.10.0
pkgrel=1
pkgdesc='Meson PEP 517 Python build backend'
arch=(any)
url='https://github.com/FFY00/meson-python'
license=(MIT)
depends=(meson patchelf python-wheel python-pyproject-metadata)
makedepends=(python-build python-installer ninja)
checkdepends=(python-pytest python-gitpython python-pytest-mock)
source=(https://github.com/FFY00/meson-python/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e3e79e592b8ad79e22292ab76b7fcde2af3aeebc4bd76f687fb19e5672908a40')

prepare() {
  cd $pkgname-$pkgver
  sed -e '/ninja/d' -i pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
