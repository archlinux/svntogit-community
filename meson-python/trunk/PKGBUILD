# Maintainer:

pkgname=meson-python
pkgver=0.12.1
pkgrel=2
pkgdesc='Meson PEP 517 Python build backend'
arch=(any)
url='https://github.com/mesonbuild/meson-python'
license=(MIT)
depends=(meson patchelf python-wheel python-pyproject-metadata)
makedepends=(python-build python-installer ninja)
checkdepends=(python-pytest python-gitpython python-pytest-mock cython)
source=(https://github.com/mesonbuild/meson-python/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8e9c726aeba817c21209fc89eac38ca069e79b5238ac023f83a0be507a160804')

prepare() {
  cd $pkgname-$pkgver
  sed -e '/ninja/d' -i pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
