# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.16.1
pkgrel=2
pkgdesc="Legacy GNOME window manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'gsettings-desktop-schemas' 'gtk3' 'startup-notification' 'zenity')
makedepends=('intltool' 'itstool')
url="https://wiki.gnome.org/Projects/Metacity"
install=metacity.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        fix-crasher.patch)
sha256sums=('7a2e4ab7e19bfa5e6fc75d80d66e79f7965b5a51435c9675b0e895179d256f86'
            'eb2a6043ff1a2fc0207a65c2bee18ada509108354379ddc12ef04566cfd7a524')

prepare() {
  cd $pkgname-$pkgver

  # Fix crasher
  patch -Np1 -i ../fix-crasher.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
