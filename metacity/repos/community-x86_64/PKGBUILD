# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.40.0
pkgrel=1
pkgdesc='Window manager of GNOME Flashback'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/Metacity'
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'libxpresent' 'libxres'
         'gsettings-desktop-schemas' 'gtk3' 'startup-notification' 'zenity')
makedepends=('intltool' 'vulkan-headers')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('224c1f65487eac21f0c1d9856152343768ee726c48b1a8a2835a46a8ad9015b5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
