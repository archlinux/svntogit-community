# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Daniel Isenmann <daniel.isenmann [at] gmx [dot] de>

pkgname=metapixel
pkgver=1.0.2
pkgrel=6
pkgdesc='A program for generating photomosaics.'
arch=('i686' 'x86_64')
url='http://www.complang.tuwien.ac.at/schani/metapixel/'
license=('GPL')
depends=('giflib' 'libjpeg' 'libpng' 'perl' 'glib2')
makedepends=('git' 'docbook-xsl')
source=("git://github.com/schuay/metapixel.git#commit=58c9607b02e4290f47d9cf06af2a3e443b8819fd")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"

  git submodule init
  git submodule update

  # Build librwimg first, make dependencies seem to be bugged.
  make librwimg
  make MANPAGE_XSL="/usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl"
}

package() {
  cd "${srcdir}/${pkgname}"
  
  install -dm0755 "${pkgdir}/usr/share/man/man1"

  make PREFIX="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man/man1" install
}
