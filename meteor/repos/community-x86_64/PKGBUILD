# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Parth Buch <parthbuch115 at gmail dot com>
# Contributor: Tom Vincent <http://tlvince.com/contact/>
# Contributor: Valentin Haloiu <vially.ichb+aur@gmail.com>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>

pkgname=meteor
pkgver=1.0.3.1
pkgrel=2
pkgdesc='Platform for rapidly developing mobile and web applications'
arch=('x86_64' 'i686')
url='https://www.meteor.com/'
license=('MIT')
depends=('nodejs' 'mongodb')
makedepends=('git' 'scons' 'mongodb' 'nodejs' 'python2') # 'kexec-tools'
options=('!strip')
source=("git://github.com/meteor/meteor.git#tag=release/METEOR@$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"
  PYTHON=/usr/bin/python2 scripts/generate-dev-bundle.sh
}

package() {
  install -d "$pkgdir/"{opt,usr/bin}
  cd "$pkgdir/opt"
  tar zxf "$srcdir/$pkgname/dev_bundle"*".tar.gz"
  cd "$srcdir/$pkgname"
  install -Dm755 meteor "$pkgdir/opt/meteor/meteor"
  ln -s /opt/meteor/meteor "$pkgdir/usr/bin/meteor"
}

# vim:set ts=2 sw=2 et:
