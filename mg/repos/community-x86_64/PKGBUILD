# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hannes Rist <hrist@phreeknet.org>

pkgname=mg
pkgver=20210314
pkgrel=2
pkgdesc="Micro GNU/emacs, this is a portable version of the mg maintained by the OpenBSD team."
arch=('x86_64')
url="https://github.com/hboetes/mg"
depends=('ncurses' 'libbsd')
license=('custom')
#source=("https://homepage.boetes.org/software/mg/mg-$pkgver.tar.gz")
source=("mg-$pkgver.tgz::https://github.com/hboetes/mg/archive/$pkgver.tar.gz")
sha256sums=('6820e76a0e27d9a744624d421b3b30b0030140c2a817df1310b25c3abe397106')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make prefix=/usr mandir=/usr/share/man
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  make install INSTALL=install prefix=/usr mandir=/usr/share/man DESTDIR="$pkgdir" BINDIR=/usr/bin

  mkdir -p "$pkgdir/usr/share/"
  install -D -m0644 README "$pkgdir/usr/share/licenses/$pkgname/README"

  mkdir -p "$pkgdir/usr/share/doc"
  install -D -m0644 tutorial "$pkgdir/usr/share/doc/$pkgname/tutorial"
}
