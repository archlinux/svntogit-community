# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hannes Rist <hrist@phreeknet.org>

pkgname=mg
pkgver=20180824
pkgrel=1
pkgdesc="Micro GNU/emacs, this is a portable version of the mg maintained by the OpenBSD team."
arch=('x86_64')
url="https://homepage.boetes.org/software/mg/"
depends=('ncurses' 'libbsd')
license=('custom')
#source=("https://homepage.boetes.org/software/mg/mg-$pkgver.tar.gz")
source=("mg-$pkgver.tgz::https://github.com/hboetes/mg/archive/$pkgver.tar.gz")
sha256sums=('a51a6ee77a1601cb3003504609a881ee9da8743efd3ddc0a8cf1371b0f93cb9c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make prefix=/usr mandir=/usr/share/man
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/bin"
  make install INSTALL=install prefix=/usr mandir=/usr/share/man DESTDIR="$pkgdir" BINDIR=/usr/bin

  mkdir -p "$pkgdir/usr/share/"
  install -D -m0644 README "$pkgdir/usr/share/licenses/$pkgname/README"

  mkdir -p "$pkgdir/usr/share/doc"
  install -D -m0644 tutorial "$pkgdir/usr/share/doc/$pkgname/tutorial"
}
