# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgbase=mgba
pkgname=('libmgba' 'mgba-sdl' 'mgba-qt')
pkgver=0.1.0
pkgrel=5
pkgdesc='A Nintendo Gameboy Advance Emulator focusing on both speed and accuracy'
arch=('i686' 'x86_64')
url="https://endrift.com/mgba/"
license=('custom:MPL2')
makedepends=('cmake' 'qt5-base' 'sdl2' 'zlib' 'libpng' 'libzip' 'libedit'
             'ffmpeg' 'imagemagick' 'desktop-file-utils')
source=(https://github.com/mgba-emu/mgba/archive/$pkgver.tar.gz
        mgba.desktop
        component.patch)
sha1sums=('a3cefcc31453904a01c5d537b49ed6a62cf6a474'
          '0f6f43ed45b702f6571c254d4717fbc1fe15e845'
          'a58b27e29f2ffe023859ebfbd0d007c822f79073')

prepare() {
  [[ ! -d build ]] && mkdir build || rm -rf build

  cd mgba-$pkgver
  patch -p1 -i ../component.patch

  cd res
  convert mgba-1024.png -resize 256x256 mgba-256.png
}

build() {
  cd build
  cmake "$srcdir"/mgba-$pkgver -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_libmgba() {
  depends=('zlib' 'libpng' 'libzip' 'libedit' 'ffmpeg' 'imagemagick')

  cmake -DCOMPONENT=libmgba mgba-$pkgver -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake
  install -Dm644 mgba-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mgba-sdl() {
  depends=('libmgba' 'sdl2')

  cmake -DCOMPONENT=mgba-sdl mgba-$pkgver -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/libmgba/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mgba-qt() {
  depends=('libmgba' 'qt5-base' 'sdl2')

  cmake -DCOMPONENT=mgba-qt mgba-$pkgver -DCMAKE_INSTALL_PREFIX="$pkgdir/usr" \
    -P build/cmake_install.cmake

  desktop-file-install mgba.desktop --dir "$pkgdir"/usr/share/applications/
  install -Dm644 mgba-$pkgver/res/mgba-256.png "$pkgdir"/usr/share/pixmaps/mgba.png

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/libmgba/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
