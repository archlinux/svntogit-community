# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: loqs
# Contributor: Fredy García <frealgagu at gmail dot com>
# Contributor: Youngbin Han <sukso96100@gmail.com>
# Contributor: blainester <theblainestory@gmail.com>
# Contributor: mar77i <mar77i at protonmail dot ch>
pkgname=micro
pkgver=2.0.10
pkgrel=4
pkgdesc='Modern and intuitive terminal-based text editor'
arch=(x86_64)
url='https://micro-editor.github.io/'
license=(MIT)
makedepends=(git go)
optdepends=('wl-clipboard: for copying/pasting text using Wayland'
            'xclip: for copying/pasting text using X')
source=("git+https://github.com/zyedidia/micro#commit=b97638566ea8431712f0faafe23661da2db0e8ec") # tag: v2.0.10
b2sums=(SKIP)

build() {
  cd $pkgname
  local version=$(go run tools/build-version.go)
  local hash=$(git rev-parse --short HEAD)
  local date=$(go run tools/build-date.go)
  local govars="-X github.com/zyedidia/micro/v2/internal/util.Version=$version -X github.com/zyedidia/micro/v2/internal/util.CommitHash=$hash -X 'github.com/zyedidia/micro/v2/internal/util.CompileDate=$date'"
  go build -v -buildmode=pie -trimpath -ldflags="-s -w ${govars} -linkmode=external -extldflags \"${LDFLAGS}\"" ./cmd/micro
}

check() {
  make -C $pkgname test
}

package() {
  cd $pkgname
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 LICENSE-THIRD-PARTY "$pkgdir/usr/share/licenses/$pkgname/LICENSE-THIRD-PARTY"
  install -Dm644 assets/packaging/micro.1 -t "$pkgdir/usr/share/man/man1"
  install -Dm644 assets/packaging/micro.desktop -t "$pkgdir/usr/share/applications"
  install -Dm644 assets/micro-logo-mark.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"
}
