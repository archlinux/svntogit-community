# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Aleksandar TrifunoviÄ‡ <akstrfn at gmail dot com>

_pkgname=GSL
pkgname=microsoft-gsl
pkgver=2.1.0
pkgrel=1
pkgdesc="C++ Core Guidelines Support Library"
arch=('any')
url="https://github.com/Microsoft/GSL"
license=('MIT')
makedepends=('cmake')
source=("https://github.com/Microsoft/GSL/archive/v${pkgver}.tar.gz")
sha256sums=('ef73814657b073e1be86c8f7353718771bf4149b482b6cb54f99e79b23ff899d')

prepare() {
    mkdir "$_pkgname-$pkgver"/build
}

build() {
    cd "$_pkgname-$pkgver"/build
    cmake .. \
        -DGSL_TEST=OFF \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
        -DCMAKE_EXE_LINKER_FLAGS="${LDFLAGS}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
# TODO Re-enable tests in next release

    make
}

# check() {
# TODO Re-enable tests in next release
# }

package() {
    cd "$_pkgname-$pkgver"/build

    make DESTDIR="$pkgdir/" install

    cd ..
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
