# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=midi_matrix.lv2
pkgver=0.26.0
pkgrel=1
pkgdesc="A plugin for MIDI channel filtering, multiplication and rerouting"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/midi_matrix/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11')
makedepends=('glu' 'lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('b2d7418de8d158562cc9b2dc6a7c37f1490a947e40861555d7f9baf68457eac67d7d9201cd42589b6f6b0d39e402611e5d241fe8899fb2eb04ea596abb20e78f'
            'SKIP')
b2sums=('d90b1c31499e3e388639abefffdb63a610ce2de5e073d59e2bca98b50e23038c93b4b555c8c565dade60dd3b3d16ed206a4fd53ec65a604317b3b67445421c71'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libdir=lib \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
  lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/midi_matrix#channel_filter"
}


package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
