# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: rabyte <rabyte.at.gmail.dot.com>
# Contributor: Johannes Krampf <wuischke.at.amule.dot.org>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>

pkgname=midori-gtk3
pkgver=0.5.6
pkgrel=1
pkgdesc='Lightweight web browser (GTK3)'
arch=('x86_64' 'i686')
url='http://www.midori-browser.org/'
license=('LGPL2.1')
install='midori.install'
conflicts=('midori')
provides=('midori')
depends=('libzeitgeist' 'webkitgtk3' 'libnotify' 'libxss' 'hicolor-icon-theme' 'desktop-file-utils' 'libunique3' 'gcr')
makedepends=('pkg-config' 'bzr' 'python2' 'libxml2' 'gtk3' 'intltool' 'python2-docutils' 'libsoup' 'vala' 'librsvg' 'cmake')
optdepends=('gst-plugins-base: HTML5 OGG videos support'
            'gst-plugins-good: HTML5 H264 and WebM videos support'
            'gst-libav: HTML5 H264 videos support'
            'aria2: download utility')
options=('!emptydirs')
source=("http://www.midori-browser.org/downloads/${pkgname/-gtk3}_${pkgver}_all_.tar.bz2")
sha256sums=('c5be1b5d9933776f09c5cab7945a073a820d65555e51fda9ff55fd063843f24a')

build() {
  cd "${pkgname/-gtk3}-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-gtk3
  make
}

package() {
  make -C "${pkgname/-gtk3}-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
