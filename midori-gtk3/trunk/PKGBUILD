# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: rabyte <rabyte.at.gmail.dot.com>
# Contributor: Johannes Krampf <wuischke.at.amule.dot.org>
# Contributor: Bartłomiej Piotrowski <barthalion@gmail.com>

pkgname=midori-gtk3
pkgver=0.5.5
pkgrel=1
pkgdesc='Lightweight web browser (GTK3)'
arch=('x86_64' 'i686')
url='http://www.midori-browser.org/'
license=('LGPL2.1')
install='midori.install'
conflicts=('midori')
provides=('midori')
depends=('libzeitgeist' 'webkitgtk' 'libnotify' 'libxss' 'hicolor-icon-theme' 'desktop-file-utils' 'libunique3' 'gcr')
makedepends=('bzr' 'python2' 'intltool' 'vala' 'librsvg')
optdepends=('gst-plugins-base: HTML5 OGG videos support'
            'gst-plugins-good: HTML5 H264 and WebM videos support'
            'gst-libav: HTML5 H264 videos support'
            'aria2: download utility')
options=('!emptydirs')
source=("http://www.midori-browser.org/downloads/${pkgname/-gtk3}_${pkgver}_all_.tar.bz2")
sha256sums=('ca69382a285222a86028abebd73fed1976735883027ff0adc094b627789bbd62')

build() {
  cd "$srcdir/${pkgname/-gtk3}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --enable-gtk3 \
    --disable-granite
  make
}

package() {
  cd "$srcdir/${pkgname/-gtk3}-${pkgver}"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
