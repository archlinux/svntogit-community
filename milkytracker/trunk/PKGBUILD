# $Id: PKGBUILD 61589 2012-01-03 23:37:49Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

pkgname=milkytracker
pkgver=0.90.85
pkgrel=7
pkgdesc="Fast Tracker II inspired music tracker"
arch=('x86_64' 'i686')
url="http://www.milkytracker.org/"
license=('GPL3')
depends=('sdl' 'alsa-lib' 'zlib' 'gcc-libs' 'gendesk')
makedepends=('jack-audio-connection-kit')
optdepends=('jack-audio-connection-kit: JACK audio support')
options=('docs' '!strip')
install=$pkgname.install
source=("http://milkytracker.org/files/$pkgname-$pkgver.tar.bz2"
        'arch.patch')
sha256sums=('59a0d294d63752be74b157e07b0c70252c9198c312d9b1ad0ca460129fe5bd80'
            'c0cf7adc0db78519644a64321c319b04a6ee6c26de1f0620b581137791f596c3')
_name=('MilkyTracker')
_categories=('AudioVideo;Audio;AudioVideoEditing;')

build() {
  cd "$srcdir"

  gendesk -n
  cd "$pkgname-$pkgver"
  patch -p1 -i "$srcdir/arch.patch"
  ./configure --prefix=/usr --with-alsa --with-jack
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/applications" \
    "$pkgdir/usr/share/doc/milkytracker"
  install -Dm644 resources/pictures/carton.png \
    "$pkgdir/usr/share/pixmaps/milkytracker.png"
  install -m644 "$srcdir/milkytracker.desktop" \
    "$pkgdir/usr/share/applications"
  install -m644 docs/ChangeLog.html "$pkgdir/usr/share/doc/milkytracker"
  install -m644 docs/MilkyTracker.html "$pkgdir/usr/share/doc/milkytracker"
  install -m644 docs/readme_unix "$pkgdir/usr/share/doc/milkytracker"
  install -m644 docs/FAQ.html "$pkgdir/usr/share/doc/milkytracker"
}

# vim:set ts=2 sw=2 et:
