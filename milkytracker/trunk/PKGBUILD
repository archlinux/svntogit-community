# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

_name=MilkyTracker
pkgname=milkytracker
pkgver=1.03.00
pkgrel=2
pkgdesc='Music tracker inspired by Fast Tracker II'
arch=(x86_64)
url='https://milkytracker.titandemo.org/'
license=(GPL3)
depends=(alsa-lib lhasa rtmidi sdl2 zlib zziplib)
makedepends=(cmake jack ninja)
optdepends=('jack: JACK audio support')
# Disable stripping
# https://bugs.archlinux.org/task/30018
options=(!strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/milkytracker/MilkyTracker/archive/v$pkgver.tar.gz")
sha256sums=('72d5357e303380b52383b66b51f944a77cd77e2b3bfeb227d87cc0e72ab292f7')

prepare() {
  mv -v $_name-$pkgver $pkgname-$pkgver
  # remove explicit use of C++98 as dependencies use C++11
  sed -e '/CMAKE_CXX_STANDARD/d' -e '/CMAKE_CXX_EXTENSIONS/d' -i $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -W nodev \
    -B build \
    -G Ninja \
    $pkgname-$pkgver
  ninja -C build
}

package() {
  depends+=(libasound.so librtmidi.so)

  DESTDIR="$pkgdir" ninja install -C build
  cd $pkgname-$pkgver/resources
  install -vDm 644 pictures/carton.png "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 ${pkgname}.desktop -t "$pkgdir/usr/share/applications/"
  # yes, this file extension is wrong
  install -vDm 644 ${pkgname}.appdata "$pkgdir/usr/share/appdata/${pkgname}.appdata.xml"
}
