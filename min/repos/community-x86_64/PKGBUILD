# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=min
pkgver=1.22.1
_commit=3141886212317056e762adae632c1d41661df3ad
pkgrel=1
pkgdesc='A fast, minimal browser that protects your privacy'
arch=('x86_64')
url='https://minbrowser.org/'
license=('Apache')
depends=('electron15' 'libsecret')
makedepends=('git' 'npm')
options=(!emptydirs)
source=("git+https://github.com/minbrowser/min.git#commit=${_commit}"
        'min.desktop'
        'min.js'
        'icon.patch')
sha256sums=('SKIP'
            'a069caac07638ca2bafde5f96a4db646ec7484741ff9b44788c2e159b5142650'
            '9e3aa1c32351da818f575eddc699818f900511e4218a4e8a83a6b53220812b84'
            'ac24fe55be98edf115602533b8e892ebc5c0841a4edb412ef4ed29a07d9d89dd')

prepare() {
    cd ${pkgname}

    patch -Np1 -i "${srcdir}"/icon.patch
}

build() {
    cd ${pkgname}

    npm install
    npm run build
    rm -r node_modules

    npm install --production --no-optional
}

package() {
    cd ${pkgname}

    appdir=/usr/lib/${pkgname}

    install -dm755 "${pkgdir}"${appdir}
    cp -r * "${pkgdir}"${appdir}

    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/256x256/apps
    mv icons/icon256.png \
        "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/${pkgname}.png

    install -Dm755 "${srcdir}"/${pkgname}.js "${pkgdir}"/usr/bin/${pkgname}
    install -Dm644 "${srcdir}"/${pkgname}.desktop \
            "${pkgdir}"/usr/share/applications/${pkgname}.desktop

    # Clean up
    rm "${pkgdir}"${appdir}/dist/build.js
    rm -r "${pkgdir}"${appdir}/icons
    rm -r "${pkgdir}"${appdir}/localization
    rm -r "${pkgdir}"${appdir}/main
    rm -r "${pkgdir}"${appdir}/scripts
    find "${pkgdir}"${appdir} \
        -name "package.json" \
            -exec sed -e "s|${srcdir}/${pkgname}|${appdir}|" \
                -i {} \; \
        -or -name ".*" -prune -exec rm -r '{}' \; \
        -or -name "*.Makefile" -exec rm '{}' \; \
        -or -name "*.h" -exec rm '{}' \; \
        -or -name "*.c" -exec rm '{}' \; \
        -or -name "*.cc" -exec rm '{}' \; \
        -or -name "*.gypi" -exec rm '{}' \; \
        -or -name "*.mk" -exec rm '{}' \; \
        -or -name "Gruntfile.js" -exec rm '{}' \; \
        -or -name "Makefile" -exec rm '{}' \; \
        -or -name "bin" -prune -exec rm -r '{}' \; \
        -or -name "bin.js" -exec rm '{}' \; \
        -or -name "bower.json" -exec rm '{}' \; \
        -or -name "cli.js" -exec rm '{}' \; \
        -or -name "cmd.js" -exec rm '{}' \; \
        -or -name "coffee" -prune -exec rm -r '{}' \; \
        -or -name "example" -prune -exec rm -r '{}' \; \
        -or -name "examples" -prune -exec rm -r '{}' \; \
        -or -name "gulpfile.js" -exec rm '{}' \; \
        -or -name "man" -prune -exec rm -r '{}' \; \
        -or -name "obj.target" -prune -exec rm -r '{}' \; \
        -or -name "scripts" -prune -exec rm -r '{}' \; \
        -or -name "test" -prune -exec rm -r '{}' \; \
        -or -name "tests" -prune -exec rm -r '{}' \;
}
