# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=min
pkgver=1.6.3
pkgrel=1
pkgdesc='A smarter, faster web browser'
arch=('any')
url='https://minbrowser.github.io/min'
license=('Apache')
depends=('electron')
makedepends=('nodejs-grunt-cli' 'npm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/minbrowser/min/archive/v${pkgver}.tar.gz"
        'min.desktop'
        'min.js'
        'icon.patch')
sha256sums=('cdc3f7e218c9010a1e915185262bdab622156481a978794857fd53e178235e1c'
            'a779a8daff1d684779ebc6d13ea4c2f09e838df018de41fa19fa3f25aa12e32f'
            'c22324184b72b3fad5a0aadb4e18182414e0294c5596c26426adc204fd473258'
            'f73d138d68f90e11a60128099b57eecb84a7d067e7f6883ff98924c5f4a61ea8')

prepare() {
    cd ${pkgname}-${pkgver}

    patch -Np1 -i "${srcdir}"/icon.patch
}

build() {
    cd ${pkgname}-${pkgver}

    npm install decomment grunt grunt-contrib-concat grunt-contrib-uglify
    mkdir dist
    npm run buildTranslations
    grunt
    rm -r node_modules

    npm install --production --no-optional
}

package() {
    cd ${pkgname}-${pkgver}

    appdir=/usr/lib/${pkgname}

    install -dm755 "${pkgdir}"${appdir}
    cp -r * "${pkgdir}"${appdir}

    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/256x256/apps
    mv icons/icon256.png \
        "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/${pkgname}.png

    install -Dm755 "${srcdir}"/${pkgname}.js "${pkgdir}"/usr/bin/${pkgname}
    install -Dm644 "${srcdir}"/${pkgname}.desktop \
            "${pkgdir}"/usr/share/applications/${pkgname}.desktop

    # Clean up
    rm "${pkgdir}"${appdir}/dist/{build,webview}.js
    rm -r "${pkgdir}"${appdir}/icons
    rm -r "${pkgdir}"${appdir}/localization
    rm -r "${pkgdir}"${appdir}/main
    rm -r "${pkgdir}"${appdir}/scripts
    find "${pkgdir}"${appdir} \
        -name "package.json" \
            -exec sed -e "s|${srcdir}/${pkgname}-${pkgver}|${appdir}|" \
                -i {} \; \
        -or -name ".*" -prune -exec rm -r '{}' \; \
        -or -name "Gruntfile.js" -exec rm '{}' \; \
        -or -name "Makefile" -exec rm '{}' \; \
        -or -name "bower.json" -exec rm '{}' \; \
        -or -name "cmd.js" -exec rm '{}' \; \
        -or -name "coffee" -prune -exec rm -r '{}' \; \
        -or -name "example" -prune -exec rm -r '{}' \; \
        -or -name "gulpfile.js" -exec rm '{}' \; \
        -or -name "test" -prune -exec rm -r '{}' \; \
        -or -name "tests" -prune -exec rm -r '{}' \;
}
