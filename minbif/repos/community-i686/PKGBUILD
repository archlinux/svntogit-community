# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: AkiraYB <brunoyb!yahoo,com,br>

pkgname=minbif
pkgver=1.0.5
pkgrel=5
pkgdesc='An IRC gateway to IM networks that uses libpurple.'
arch=('i686' 'x86_64')
url='http://minbif.im/'
license=('GPL2')
depends=('gnutls' 'libcaca' 'libpurple')
makedepends=('cmake')
optdepends=('xinetd: to run minbif through xinetd')
backup=('etc/minbif/minbif.conf'
        'etc/minbif/minbif.motd')
install='minbif.install'
source=("http://minbif.im/attachments/download/148/${pkgname}-${pkgver}.tar.gz"
        'minbif-gcc-4.7.patch'
        'minbif-imlib.patch'
        'minbif.service'
        'minbif.tmpfiles.conf')
md5sums=('00c5c1abbe2b8a9d900961832c7909a8'
         '2235c45d5bdb01e9ef4b11d2a41d49bb'
         'c94ea3152b7d279da25947e0d1deff14'
         'b34876e339a37dfb1dc6cd0b9d8edf9d'
         '3360f5d1bf9bc69fd9b7c9326f321dc3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # build fixes
  patch -p1 -i ../minbif-gcc-4.7.patch
  patch -p1 -i ../minbif-imlib.patch

  make PREFIX=/usr CONF_PREFIX=/etc/minbif ENABLE_MINBIF=ON ENABLE_IMLIB=ON ENABLE_CACA=ON \
       ENABLE_VIDEO=OFF ENABLE_PLUGIN=OFF ENABLE_PAM=ON ENABLE_TLS=ON DEBUG=OFF
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm0755 "${pkgdir}/var/lib"
  install -dm0770 -o67 -g67 "${pkgdir}/var/lib/minbif"
  install -dm0770 -o67 -g67 "${pkgdir}/var/lib/minbif/users"

  make DESTDIR="${pkgdir}" install

  install -Dm0644 "${srcdir}/minbif.service" "${pkgdir}/usr/lib/systemd/system/minbif.service"
  install -Dm0644 "${srcdir}/minbif.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/minbif.conf"
}
