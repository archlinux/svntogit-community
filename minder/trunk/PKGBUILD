# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>

pkgname=minder
pkgver=1.12.4
pkgrel=1
pkgdesc='Mind-mapping application designed for Pantheon'
arch=('x86_64')
url='https://github.com/phase1geo/Minder'
license=('GPL3')
depends=('discount' 'granite' 'gtksourceview3' 'libarchive')
makedepends=('meson' 'vala')
source=("https://github.com/phase1geo/Minder/archive/$pkgver/Minder-$pkgver.tar.gz")
sha256sums=('02f69cde491fc14ee54088005abd20ba6b39dca5c7b97dca72a9017cc081b0c2')

build() {
  arch-meson Minder-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  ln -s com.github.phase1geo.minder "$pkgdir/usr/bin/$pkgname"
}
