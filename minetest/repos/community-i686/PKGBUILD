# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname=minetest
pkgver=0.4.7
pkgrel=1
pkgdesc='An Infiniminer/Minecraft inspired game'
arch=('i686' 'x86_64')
url='http://minetest.net/'
license=('GPL')
depends=('sqlite' 'libgl' 'libxxf86vm' 'openal' 'libvorbis' 'curl' 'hicolor-icon-theme')
makedepends=('cmake' 'irrlicht' 'mesa')
install=minetest.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}/tarball/${pkgver}
	${pkgname}_game-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}_game/tarball/${pkgver})
md5sums=('232b87ef5c4300ddad7bc883f8f1a1c5'
         '30a9965667470ebc785979de665d4df4')

build() {
  cd "$srcdir"/minetest-minetest-*

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"/minetest-minetest-*

  make DESTDIR="$pkgdir" install

  mv -v "$srcdir"/minetest-minetest_game-* "$pkgdir"/usr/share/minetest/games/minetest
  
  # small cleanup
  rm "$pkgdir"/usr/share/minetest/games/minetest/.gitignore
}
