# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname=('minetest' 'minetest-server' 'minetest-common')
pkgver=0.4.10
pkgrel=3
pkgdesc='An Infiniminer/Minecraft inspired game'
arch=('i686' 'x86_64')
url='http://minetest.net/'
license=('GPL')
makedepends=('sqlite' 'freetype2' 'leveldb' 'openal' 'libvorbis' 'curl' 'irrlicht'
         'hicolor-icon-theme' 'luajit' 'cmake')
source=(https://github.com/minetest/minetest/archive/$pkgver.tar.gz
        data-$pkgver.tar.gz::https://github.com/minetest/minetest_game/archive/$pkgver.tar.gz)
md5sums=('61bb35c9d5521f1b072bc3c3e634c863'
         '398d83431c44fb85f02bd902b016338c')

prepare() {
  install -d build-{client,server}
}

build() {
  cd "$srcdir"/build-client
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SERVER=0 \
    -DENABLE_GETTEXT=1 \
    -DENABLE_FREETYPE=1 \
    -DENABLE_LEVELDB=0 \
    -DENABLE_REDIS=0 \
    -DLUA_INCLUDE_DIR=/usr/include/luajit-2.0 \
    -DLUA_LIBRARY=//usr/lib/libluajit-5.1.so.2
  make

  cd "$srcdir"/build-server
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_CLIENT=0 \
    -DENABLE_LEVELDB=1 \
    -DENABLE_REDIS=1 \
    -DLUA_INCLUDE_DIR=/usr/include/luajit-2.0 \
    -DLUA_LIBRARY=//usr/lib/libluajit-5.1.so.2
  make
}

package_minetest() {
  pkgdesc='Multiplayer infinite-world block sandbox game'
  depends=('minetest-common' 'irrlicht' 'curl' 'luajit' 'libvorbis' 'sqlite'
           'openal' 'hicolor-icon-theme' 'desktop-file-utils' 'xdg-utils')
  install=minetest.install

  cd build-client
  make DESTDIR="$pkgdir" install
  cp -r locale "$pkgdir"/usr/share/

  rm -rf "$pkgdir"/usr/share/minetest
  rm "$pkgdir"/usr/share/man/man6/minetestserver.6
}

package_minetest-server() {
  pkgdesc='Server of infinite-world block sandbox game'
  depends=('minetest-common' 'leveldb' 'luajit' 'curl' 'sqlite')

  cd build-server
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/share/{minetest,appdata,applications,icons,doc}
  rm "$pkgdir"/usr/share/man/man6/minetest.6
}

package_minetest-common() {
  pkgdesc='Common data files for minetest and minetest-server'

  cd $pkgbase-$pkgver
  install -d "$pkgdir"/usr/share/minetest

  cp -r games builtin client fonts textures "$pkgdir"/usr/share/minetest/
  cp -r "$srcdir"/minetest_game-$pkgver "$pkgdir"/usr/share/minetest/games/minetest
  rm "$pkgdir"/usr/share/minetest/games/minetest/.gitignore
}
