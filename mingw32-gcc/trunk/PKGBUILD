# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Ondrej Jirman <megous@megous.com>

pkgname=mingw32-gcc
pkgver=4.5.0
pkgrel=3
arch=(i686 x86_64)
pkgdesc="A C and C++ cross-compilers for building Windows executables on Linux"
depends=(mingw32-runtime mingw32-binutils mingw32-w32api)
replaces=(mingw32-gcc-base)
provides=(mingw32-gcc-base)
options=(!strip force)
url="http://mingw.sf.net"
license=(GPL LGPL)
source=(gcc-$pkgver-1-mingw32-src.tar.xz::http://downloads.sourceforge.net/project/mingw/GCC%20Version%204/gcc-$pkgver-1/gcc-$pkgver-1-mingw32-src.tar.lzma)
md5sums=('f279430018dc379b8184c0a18b280298')

build()
{
  [ $NOEXTRACT -eq 1 ] || tar xJf gcc-$pkgver-1-mingw32-src.tar.xz
  [ $NOEXTRACT -eq 1 ] || tar xjf gcc-$pkgver.tar.bz2

  mkdir -p $srcdir/build
  cd $srcdir/build

  unset CFLAGS CXXFLAGS

  chmod ugo+x $srcdir/gcc-$pkgver/configure
  chmod ugo+x $srcdir/gcc-$pkgver/move-if-change

  [ $NOEXTRACT -eq 1 ] || $srcdir/gcc-$pkgver/configure \
     --target=i486-mingw32 \
     --host=$CHOST \
     --build=$CHOST \
     --prefix=/usr \
     --enable-languages=c,c++ \
     --enable-shared \
     --enable-sjlj-exceptions \
     --enable-hash-synchronization \
     --disable-nls \
     --disable-libssp \
    || return 1

  make || return 1
  make install DESTDIR=$pkgdir || return 1

  make -j1 -C i486-mingw32/libgcc DESTDIR=${pkgdir} libgcc_eh.a install || return 1
  make -j1 -C i486-mingw32/libstdc++-v3 DESTDIR=${pkgdir} install || return 1

  cd $pkgdir
  rm -rf usr/bin/i486-mingw32-{gcov,gccbug,gcc-*} \
    usr/{include,lib/libiberty.a} \
    usr/share/{info,man} \
    usr/share/gcc-$pkgver/python

  strip usr/bin/*
  strip usr/libexec/gcc/i486-mingw32/$pkgver/{cc1*,collect2}
  i486-mingw32-strip -g usr/lib/gcc/i486-mingw32/$pkgver/*.a
}
