# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=minikube
pkgver=1.9.0
_minikube_commit=48fefd43444d2f8852f527c78f0141b377b1e42a
pkgrel=1
pkgdesc="Minikube is a tool that makes it easy to run Kubernetes locally"
url="https://github.com/kubernetes/minikube"
license=('Apache')
arch=('x86_64')
optdepends=(
  'kubectl: to manage the cluster'
  'virtualbox: to use --vm-driver=virtualbox'
)
makedepends=('git' 'go-pie' 'go-bindata')
depends=('glibc')
source=("git+https://github.com/kubernetes/minikube.git#commit=${_minikube_commit}")
md5sums=('SKIP')
changelog=CHANGELOG.md

prepare() {
  rm -rf "$srcdir/src"
  mkdir -p "$srcdir/src/k8s.io"
  mv -T "$srcdir/minikube" "$srcdir/src/k8s.io/minikube"
}

build() {
  export GOPATH="$srcdir"
  cd "$GOPATH/src/k8s.io/minikube"
  make
}

package() {
  install -Dm755 "$srcdir/src/k8s.io/minikube/out/minikube" -t "$pkgdir/usr/bin"

  "$pkgdir/usr/bin/minikube" completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/minikube"
  "$pkgdir/usr/bin/minikube" completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_minikube"
}
