# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=miredo
pkgver=1.2.6
pkgrel=1
pkgdesc="Teredo client and server."
arch=('i686' 'x86_64')
url="http://www.remlab.net/miredo/"
license=('GPL')
depends=('judy' 'iproute2' 'libcap')
backup=('etc/miredo/miredo.conf'
        'etc/miredo/client-hook')
options=('!libtool')
source=(http://www.remlab.net/files/${pkgname}/${pkgname}-${pkgver}.tar.xz
	miredo.install
	miredo.service)
md5sums=('5bcdbaced54f40b6cb1e9c9a8dbac411'
         'd1b655d7a851cdb46c91c3418ed1962f'
         'fd6d5b355b2356426eeefd6047f6bb7c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib \
	--sbindir=/usr/bin \
	--with-Judy
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  sed -i 's#/sbin/ip#/usr/sbin/ip#' $pkgdir/etc/miredo/client-hook

  # avoid conflict with filesystem>=2012.06
  rmdir "$pkgdir/var/run" "$pkgdir/var"

  install -Dm644 "$srcdir/miredo.service" "$pkgdir/usr/lib/systemd/system/miredo.service"
}
