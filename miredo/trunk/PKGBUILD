# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=miredo
pkgver=1.2.6
pkgrel=4
pkgdesc="Teredo client and server."
arch=('i686' 'x86_64')
url="http://www.remlab.net/miredo/"
license=('GPL')
depends=('judy' 'iproute2' 'libcap')
backup=('etc/miredo/miredo.conf'
        'etc/miredo/client-hook')
source=(http://www.remlab.net/files/${pkgname}/${pkgname}-${pkgver}.tar.xz
	miredo.service)
md5sums=('5bcdbaced54f40b6cb1e9c9a8dbac411'
         'f8a8ebbd5531e074816e009e50a2fb68')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
	--prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib \
	--sbindir=/usr/bin \
	--with-Judy \
	--disable-assert
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  sed -i 's#/sbin/ip#/usr/bin/ip#' $pkgdir/etc/miredo/client-hook
  rmdir "$pkgdir/var/run" "$pkgdir/var"
  install -Dm644 "$srcdir/miredo.service" "$pkgdir/usr/lib/systemd/system/miredo.service"
}
