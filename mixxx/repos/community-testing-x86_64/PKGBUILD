# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.2.4
pkgrel=2
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url="https://www.mixxx.org"
license=('GPL2')
groups=('pro-audio')
depends=('chromaprint' 'gcc-libs' 'glibc' 'hidapi' 'libglvnd' 'libmad'
'libmodplug' 'libmp4v2' 'libshout' 'libx11' 'openssl' 'opusfile' 'qt5-base'
'qt5-script' 'qt5-svg' 'qt5-x11extras' 'qtkeychain' 'soundtouch' 'speex'
'sqlite' 'taglib' 'upower' 'wavpack')
makedepends=('faad2' 'ffmpeg' 'flac' 'glib2' 'glu' 'gperftools' 'libid3tag'
'libogg' 'libsndfile' 'libusb' 'libvorbis' 'lilv' 'lv2' 'qt5-tools' 'portaudio'
'portmidi' 'protobuf' 'rubberband' 'scons' 'vamp-plugin-sdk')
checkdepends=('xorg-server-xvfb')
source=("https://github.com/${pkgname}dj/${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('34f67088ea2271cb13b75d001d3814237b1b6c2052646d670a36e341fb78e24c34d361a3eb03b0dbfb2d94b0026fecc9125713d7533aac7e99161c27090115fa')

prepare() {
  mv -v "${pkgname}-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  scons prefix=/usr \
        build=release \
        faad=1 \
        ffmpeg=1 \
        modplug=1 \
        perftools=1 \
        perftools_profiler=1 \
        qt_sqlite_plugin=0 \
        qtkeychain=1 \
        test=1 \
        target=linux \
        virtualize=0 \
        wv=1
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run lin64_build/${pkgname}-test
}

package() {
  depends+=('libFLAC.so' 'libfaad.so' 'libavcodec.so' 'libavformat.so'
  'libavutil.so' 'libglib-2.0.so' 'libid3tag.so' 'libgobject-2.0.so'
  'liblilv-0.so' 'libogg.so' 'libportaudio.so' 'libportmidi.so'
  'libprotobuf-lite.so' 'librubberband.so' 'libsndfile.so' 'libtcmalloc.so'
  'libusb-1.0.so' 'libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so')
  cd "${pkgname}-${pkgver}"
  scons prefix=/usr \
        install_root="${pkgdir}/usr" \
        install
  # removing the test executable
  rm -v "${pkgdir}/usr/bin/${pkgname}-test"
}
