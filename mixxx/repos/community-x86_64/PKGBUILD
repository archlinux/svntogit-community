# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.3.0
pkgrel=2
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url="https://www.mixxx.org"
license=('GPL2')
groups=('pro-audio')
# TODO: package libshout-idjc
depends=('gcc-libs' 'glibc' 'hidapi' 'lame' 'libglvnd' 'libmad' 'libmodplug'
'libx11' 'openssl' 'opusfile' 'qt5-base' 'qt5-script' 'qt5-svg' 'qt5-x11extras'
'qtkeychain' 'soundtouch' 'speex' 'sqlite' 'taglib' 'upower' 'wavpack')
makedepends=('chromaprint' 'cmake' 'faad2' 'ffmpeg' 'fftw' 'flac' 'glib2' 'glu'
'gperftools' 'libid3tag' 'libogg' 'libsndfile' 'libusb' 'libvorbis' 'lilv'
'lv2' 'qt5-tools' 'portaudio' 'portmidi' 'protobuf' 'rubberband'
'vamp-plugin-sdk')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}dj/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('9ebb735b4f5d85b5f2b1ea6b8976ccfc0b1cd39516c5d5d6eb9038761836bb640583ea5c941f3b0d37e1882fa14dbc971c99f69498de764218d2815f58fc00f1')
b2sums=('346bef3feecf1671729b2b07156be281d9f7879aaf3bb5764ec7bdfa1004ef1630c9279df46467ab3696ea7debf3c2a6b68202db9f5a288879d8a34ee00df738')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='Release' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run build/${pkgname}-test
}

package() {
  depends+=('libFLAC.so' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
  'libchromaprint.so' 'libfftw3.so' 'libglib-2.0.so' 'libid3tag.so'
  'libgobject-2.0.so' 'liblilv-0.so' 'libogg.so' 'libportaudio.so'
  'libportmidi.so' 'libprotobuf-lite.so' 'librubberband.so' 'libsndfile.so'
  'libusb-1.0.so' 'libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so')

  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
}
