# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.3.2
pkgrel=3
pkgdesc="Free, open source software for digital DJ'ing."
arch=(x86_64)
url="https://www.mixxx.org"
license=(GPL2)
groups=(pro-audio)
# TODO: package libshout-idjc
depends=(gcc-libs glibc hidapi lame libglvnd libmad libmodplug libx11 openssl
opusfile qt5-base qt5-script qt5-svg qt5-x11extras qtkeychain soundtouch speex
sqlite taglib upower wavpack)
makedepends=(chromaprint cmake faad2 ffmpeg fftw flac glib2 glu gperftools
libid3tag libogg libsndfile libusb libvorbis lilv lv2 qt5-tools portaudio
portmidi protobuf rubberband vamp-plugin-sdk)
checkdepends=(xorg-server-xvfb)
options=(debug)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}dj/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('81282d6c587914157b9ef4a7ca2f0e886cb97a847e215e2b0496671938d392e3fc8f941071577bc69db517e677dcd96b72e4b53d6fd42d2224c8a4e62d2acbcd')
b2sums=('9ec1d871d3051f081152f07fdafd6c5e2bb41224e723dc260afe27755fdcd87befd2cb5ecaa2d0fb6d7ee9fb1b97c12db4b8d0a29e695a3ff1a7fb51b94ebfa4')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run ../build/${pkgname}-test
}

package() {
  depends+=(libFLAC.so libavcodec.so libavformat.so libavutil.so
  libchromaprint.so libfftw3.so libglib-2.0.so libid3tag.so libgobject-2.0.so
  liblilv-0.so libogg.so libportaudio.so libportmidi.so libprotobuf-lite.so
  librubberband.so libsndfile.so libusb-1.0.so libvorbis.so libvorbisenc.so
  libvorbisfile.so)

  make DESTDIR="${pkgdir}" install -C build
}
