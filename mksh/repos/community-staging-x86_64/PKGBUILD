# Maintainer: Alad Wenter <alad@archlinux.org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Daniel Hommel <dhommel@gmail.com>

pkgname=mksh
_pkgver=R57
pkgver=57
pkgrel=3
pkgdesc='MirBSD Korn Shell'
url='https://www.mirbsd.org/mksh.htm'
license=('custom')
arch=('x86_64')
depends=('glibc')
checkdepends=('ed')
install=mksh.install
source=("https://www.mirbsd.org/MirOS/dist/mir/mksh/$pkgname-$_pkgver.tgz")
sha256sums=('3d101154182d52ae54ef26e1360c95bc89c929d28859d378cc1c84f3439dbe75')

check() {
  cd "$pkgname"
  PERL=/usr/bin/perl ./test.sh
}

build() {
  cd "$pkgname"
  sh Build.sh -r
}

package() {
  cd "$pkgname"
  install -D -m 755 mksh "$pkgdir/usr/bin/mksh"
  install -D -m 644 mksh.1 "$pkgdir/usr/share/man/man1/mksh.1"
  install -D -m 644 dot.mkshrc "$pkgdir/etc/skel/.mkshrc"

  mkdir -p "$pkgdir/usr/share/licenses/mksh/"
  sed -n '/Copyright/,/\*\//p' main.c > "$pkgdir/usr/share/licenses/mksh/LICENSE"
}

# vim:set ts=2 sw=2 et:
