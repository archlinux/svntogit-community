# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Daniel Hommel <dhommel@gmail.com>

pkgname=mksh
_pkgver=R51
pkgver=51
pkgrel=1
pkgdesc='The MirBSD Korn Shell - an enhanced version of the public domain ksh'
url='https://www.mirbsd.org/mksh.htm'
license=('custom')
arch=('i686' 'x86_64')
depends=('gcc-libs')
install=mksh.install
source=("https://www.mirbsd.org/MirOS/dist/mir/mksh/$pkgname-$_pkgver.tgz"
        'https://www.mirbsd.org/TaC-mksh.txt')
md5sums=('8e522475da795c60f35a6f8035481baf'
         '56add717e01667833e5c8148afddabb6')

check() {
  cd "$srcdir/$pkgname"
  ./test.sh
}

build() {
  cd "$srcdir/$pkgname"
  sh Build.sh -r -c lto
}

package() {
  cd "$srcdir/$pkgname"
  install -D -m 755 mksh "$pkgdir/usr/bin/mksh"
  install -D -m 644 mksh.1 "$pkgdir/usr/share/man/man1/mksh.1"
  install -D -m 644 dot.mkshrc "$pkgdir/etc/skel/.mkshrc"
  install -D -m 644 "$srcdir/TaC-mksh.txt" "$pkgdir/usr/share/licenses/mksh/TaC-mksh.txt"
}

# vim:set ts=2 sw=2 et:
