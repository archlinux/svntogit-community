# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>   

pkgname=mldonkey
pkgver=3.1.0
pkgrel=1
pkgdesc="A multi-network P2P client"
arch=('i686' 'x86_64')
url="http://mldonkey.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'file' 'gd' 'bzip2')
makedepends=('lablgtk2' 'librsvg' 'ocaml')
optdepends=('librsvg: GUI support'
            'libx11: GUI support')
backup=('etc/conf.d/mldonkey')
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkeyd
        mldonkey.conf
        mldonkey.desktop)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-gui=newgui2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/$pkgname-$pkgver/icons/rsvg/type_source_normal.svg" \
    "$pkgdir/usr/share/icons/mldonkey.svg"
  install -Dm644 "$srcdir/mldonkey.desktop" \
    "$pkgdir/usr/share/applications/mldonkey.desktop"

  install -Dm755 "$srcdir/mldonkeyd" "$pkgdir/etc/rc.d/mldonkey"
  install -Dm644 "$srcdir/mldonkey.conf" "$pkgdir/etc/conf.d/mldonkey"
}
md5sums=('072726d158ba1e936c554be341e7ceff'
         '75e4b6c7fb282f7de15a8d5478f3bd70'
         '1f86d92db51c62cded0e6f3833057dee'
         '1de3266caa49f82bee73ba891eea12cf')
