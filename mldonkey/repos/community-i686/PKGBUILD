# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>   

pkgname=mldonkey
pkgver=3.0.2
pkgrel=1
pkgdesc="A multi-network P2P client"
arch=('i686' 'x86_64')
url="http://mldonkey.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'file' 'gd' 'bzip2')
makedepends=('lablgtk2' 'librsvg' 'ocaml')
optdepends=('librsvg: GUI support'
            'libx11: GUI support')
backup=('etc/conf.d/mldonkey' 'etc/mldonkey.conf')
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/${pkgname}-${pkgver}.tar.bz2
        mldonkeyd mldonkey.conf mldonkey.desktop)
md5sums=('bed733181ec3634cdf0073085eed0110'
         '7ad908c0386a4b7a2a612690e0565b7c'
         '190cbbc30b510981e0d8b0ed8bae87a4'
         '1de3266caa49f82bee73ba891eea12cf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-gui=newgui2 || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/icons/rsvg/type_source_normal.svg" \
    "${pkgdir}/usr/share/icons/mldonkey.svg" || return 1
  install -Dm644 "${srcdir}/mldonkey.desktop" \
    "${pkgdir}/usr/share/applications/mldonkey.desktop" || return 1

  install -Dm755 "${srcdir}/mldonkeyd" "${pkgdir}/etc/rc.d/mldonkey" || return 1
  install -Dm644 "${srcdir}/mldonkey.conf" "${pkgdir}/etc/conf.d/mldonkey" \
    || return 1
}

# vim:set ts=2 sw=2 et:
