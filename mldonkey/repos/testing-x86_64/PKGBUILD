# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mldonkey
pkgver=3.1.4
pkgrel=1
pkgdesc='A multi-network P2P client'
arch=(i686 x86_64)
url='http://mldonkey.sourceforge.net/'
license=(GPL)
depends=(file gd)
makedepends=(lablgtk2 librsvg ocaml)
optdepends=('librsvg: GUI support'
            'gtk2: GUI support')
backup=(etc/conf.d/mldonkey)
install=mldonkey.install
source=(http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkey.conf
        mldonkey.service
        mldonkey.tmpfiles
        fix_compile_error.patch)
sha256sums=('1e454be51fec346d650a72049e4dc4471036d129a488443a67436af62ef375fe'
            'f1d9401cefd591662d49011c53fdb2788755a6f745a963e46d8037b990edeb6a'
            '778cebe8edcffd63db3594054c2daa62ce571644a96ad235b8c95470b55c0415'
            '45f45d2a3c064d5bbc0b6b9b70fa3497ed47cfecdd1ec52e175311f48eaa89dc'
            '172d1b129a068caafcb1e48c861b4b78b129b4ba50bb5e692b61cf00dcc66215')

prepare() {
  cd mldonkey-$pkgver

  # http://mldonkey.sourceforge.net/phpBB2/viewtopic.php?p=32462#32462
  patch -p1 < "$srcdir/fix_compile_error.patch"

  rm config/configure  # remove this file to force its regeneration, we've just changed ./config/configure.in
}

build() {
  cd mldonkey-$pkgver
  ./configure --prefix=/usr --enable-gui=newgui2
  make
}

package() {
  cd mldonkey-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 icons/rsvg/type_source_normal.svg "$pkgdir"/usr/share/icons/mldonkey.svg
  install -Dm644 distrib/mldonkey.desktop "$pkgdir"/usr/share/applications/mldonkey.desktop

  install -Dm644 "$srcdir"/mldonkey.conf "$pkgdir"/etc/conf.d/mldonkey
  install -Dm644 "$srcdir"/mldonkey.service "$pkgdir"/usr/lib/systemd/system/mldonkey.service
  install -Dm644 "$srcdir"/mldonkey.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/mldonkey.conf
}
