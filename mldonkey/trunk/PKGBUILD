# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=mldonkey
pkgver=3.1.6
pkgrel=2
pkgdesc='A multi-network P2P client'
arch=(x86_64)
url='http://mldonkey.sourceforge.net/'
license=(GPL)
depends=(file gd miniupnpc libnatpmp libminiupnpc.so)
makedepends=(gtk2 librsvg) # we build our own version of ocaml and lablgtk2
optdepends=('librsvg: GUI support'
            'gtk2: GUI support')
backup=(etc/conf.d/mldonkey)
source=(https://github.com/ygrek/mldonkey/releases/download/release-${pkgver//./-}/mldonkey-$pkgver.tar.bz2{,.asc}
http://downloads.sourceforge.net/sourceforge/mldonkey/$pkgname-$pkgver.tar.bz2
        mldonkey.conf
        mldonkey.service
        mldonkey.tmpfiles
        mldonkey.sysusers
        http://caml.inria.fr/distrib/ocaml-3.12/ocaml-3.12.0.tar.gz
        https://forge.ocamlcore.org/frs/download.php/561/lablgtk-2.14.2.tar.gz)
sha256sums=('1b36b57c05a83c2e363c085bf8e80630884c6c92ecdeffc1ad5e1c39a98e043d'
            'SKIP'
            '1b36b57c05a83c2e363c085bf8e80630884c6c92ecdeffc1ad5e1c39a98e043d'
            'f1d9401cefd591662d49011c53fdb2788755a6f745a963e46d8037b990edeb6a'
            '778cebe8edcffd63db3594054c2daa62ce571644a96ad235b8c95470b55c0415'
            '9c78fbfbba4f8286e2c2299e4da6f76d0f34f33fde26964922707c34fb75157b'
            '24d7ef8f6af93a8d87a82842b0ed796e35ce2f88d81734d9275eede8f4e10fba'
            'd964b385e110c1517c9de1c6331476d5e7a3585cc692b6b6b8973f3a9be7da4b'
            '4981abedabdc462303f345104042c88af227ccd50fd30a9bf48fd353ab02d0ba')
validpgpkeys=(A34C49DD3DB8B78DFAEBE0FA6346B945708D5A0C)

build() {
  cd mldonkey-$pkgver
  # mldonkey hardcodes its dependencies to some specific version
  ln -s "$srcdir"/ocaml-3.12.0.tar.gz "$srcdir"/lablgtk-2.14.2.tar.gz patches/
  ./configure --prefix=/usr --enable-gui=newgui2 --enable-upnp-natpmp --enable-batch
  make
}

package() {
  cd mldonkey-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 icons/rsvg/type_source_normal.svg "$pkgdir"/usr/share/icons/mldonkey.svg
  install -Dm644 distrib/mldonkey.desktop "$pkgdir"/usr/share/applications/mldonkey.desktop

  install -Dm644 "$srcdir"/mldonkey.conf "$pkgdir"/etc/conf.d/mldonkey
  install -Dm644 "$srcdir"/mldonkey.service "$pkgdir"/usr/lib/systemd/system/mldonkey.service
  install -Dm644 "$srcdir"/mldonkey.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/mldonkey.conf
  install -Dm644 "$srcdir"/mldonkey.sysusers "$pkgdir"/usr/lib/sysusers.d/mldonkey.conf
}
