# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jes√∫s Espino <jespinog@gmail.com>

pkgname=mmctl
pkgver=7.9.1
pkgrel=1
pkgdesc='A remote CLI tool for Mattermost'
arch=(x86_64)
url="https://github.com/mattermost/$pkgname"
license=(Apache)
depends=(gcc-libs)
makedepends=(go
             git)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('c594abb0f33e8e284eca6d005b97f8894dedd821679b89ad52455353b92bb6f3')

prepare() {
	cd "$_archive"
	go mod vendor
}

build() {
	local _commit=$(zcat ${source[0]##*/} | git get-tar-commit-id)
	cd "$_archive"
	export CGO_LDFLAGS="$LDFLAGS"
	local _config=${url##https://}/commands
	go build \
		-trimpath \
		-buildmode=pie \
		-mod=readonly \
		-modcacherw \
		-ldflags "-linkmode external -X \"$_config.gitCommit=$_commit\" -X \"$_config.gitTreeState=clean\" -X \"$_config.buildDate=$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +"%Y-%m-%d %H:%M:%S")\"" \
		.
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "$pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
