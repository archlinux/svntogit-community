# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ryan Coyner <rcoyner@gmail.com>

pkgname=mod_wsgi2
pkgver=3.4
pkgrel=3
pkgdesc='Python2 WSGI adapter module for Apache'
arch=('x86_64' 'i686')
url='http://www.modwsgi.org/'
license=('APACHE')
depends=('apache' 'python2')
conflicts=('mod_wsgi')
install="$pkgname.install"
source=("http://modwsgi.googlecode.com/files/mod_wsgi-$pkgver.tar.gz")
sha256sums=('ae85c98e9e146840ab3c3e4490e6774f9bef0f99b9f679fca786b2adb5b4b6e8')

build() {
  cd "$srcdir/mod_wsgi-$pkgver"

  ./configure \
    --prefix=/usr \
    --with-apxs=/usr/sbin/apxs \
    --with-python=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/mod_wsgi-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
