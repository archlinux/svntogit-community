# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-docker
pkgver=1.1.0
pkgrel=1
pkgdesc="Molecule Docker Driver"
arch=('any')
url="https://github.com/ansible-community/molecule-docker"
license=('MIT')
depends=('molecule' 'python-ansible-compat' 'python-docker' 'python-selinux')
makedepends=('python-setuptools-scm' 'python-setuptools-scm-git-archive')
checkdepends=('python-pytest' 'python-pytest-helpers-namespace')
optdepends=('docker: for using a local docker setup')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1133883f7f46f08d92051c8387bca048959a06d742dba94d4e6784587194619407744f3e5bea0d883cf78d2919448f4572cdf62f7c537dd4bd0845cec8e3f7b6')
b2sums=('b2dc53d49bc97ff11a3372f39351e37418439033e4647c02ca0c84e712d6911d65e29266738e372978867d255ae3da0b9758365cb5a3e56dc39284299921f398')

prepare() {
  cd "$pkgname-$pkgver"
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  sed -e '/pre-commit/d' \
      -e '/addopts/d' \
      -e '/error/d' \
      -i setup.cfg

  printf "from setuptools import setup\nsetup()\n" > setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  pytest -v --ignore "src/molecule_docker/test/test_func.py"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
