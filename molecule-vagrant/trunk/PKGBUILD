# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Ike Devolder <ike DOT devolder AT gmail DOT com>
# Contributor: Marc Straube <email@marcstraube.de>

pkgname=molecule-vagrant
pkgver=1.0.0
pkgrel=1
pkgdesc="Molecule Vangrant Driver"
arch=(any)
url="https://github.com/ansible-community/molecule-vagrant"
license=(MIT)
depends=(molecule python-jinja python-selinux python-yaml python-vagrant)
makedepends=(python-build python-installer python-setuptools
python-setuptools-scm python-setuptools-scm-git-archive python-toml python-wheel)
checkdepends=(ansible python-filelock python-pytest)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('de55cea15c5df876b21f91f7f27b6de88896ea301a9e7c240a82abfaaea44d09a6ea85941a5ff3a84cb28991c77e344f9c2aa27f852f652c98e70f51787c8ca5')
b2sums=('6267554e55717ecde97b062867f4a72691ffd912155fe05d7f50f61682b46ed955c26789819af1e3c386dea4f89e606810c19a65212859ac6d75a491c3ef927d')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="${PWD}/test_dir"

  # install to test location
  mkdir -vp "${_test_dir}"
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}"
  # ignore tests that require running molecule
  pytest -v "${_test_dir}/${_site_packages}/molecule_vagrant/test/" --ignore "${_test_dir}/${_site_packages}/molecule_vagrant/test/functional/" -c /dev/null
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
