# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.3.0
pkgrel=2
pkgdesc="Aids in the development and testing of Ansible roles"
arch=('any')
url="https://github.com/ansible-community/molecule"
license=('MIT')
depends=('ansible' 'python-cerberus' 'python-click' 'python-click-completion'
'python-click-help-colors' 'python-cookiecutter' 'python-enrich' 'python-jinja'
'python-packaging' 'python-paramiko' 'python-pluggy' 'python-rich'
'python-selinux' 'python-setuptools' 'python-subprocess-tee' 'python-yaml'
'yamllint' 'ansible-lint')
makedepends=('python-setuptools-scm' 'python-simplejson' 'python-toml')
checkdepends=('python-ansi2html' 'python-mock' 'python-pexpect'
'python-pytest' 'python-pytest-helpers-namespace' 'python-pytest-html'
'python-pytest-mock' 'python-pytest-testinfra'
'python-pytest-verbose-parametrize' 'python-pytest-xdist')
optdepends=('molecule-docker: for the docker driver'
            'molecule-podman: for the podman driver'
            'molecule-vagrant: for the vagrant driver'
            'python-pywinrm: for Windows support'
            'python-pytest-testinfra: for the testinfra verifier')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        molecule-click-8.0.patch)
sha512sums=('2d72ad4da9c654adc62a9263593ed17c2ddb21ee5dfbe6479eaa5a71c52329098b2c5eec3a8995ce1016cf62c39cd48116d45c66eb2288622dbf24b1d9ba0423'
            '5858631ebc5ca86fe3109abb4ca38d78431e2e81f565df7f3e7a3d99d9f982d4bde14497079a6461401a50f2fdca7b5252da30f05ad0302104cc0c11ffda6a9e')
b2sums=('fa94d9637a5262c307539e1ea2c46238effe388d50eca13eff22e71e40c537e872b75e0c75695c17358d4493360213ee4c15b395df226d5ae187bb6595fdc5a1'
        'e628017797a9b11a27bfffb0f6a2913eb28efd44275911f812de1796979ea57044cf41f741598c7d2b65fbca242fcb3d939eacb9b1628fc9380c4e24552e2e1a')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 < ../molecule-click-8.0.patch
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  sed -e '/addopts/d' \
      -e '/testpaths/d' \
      -e '/error/d' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  local _test_dir='test_dir'

  # install into test location
  mkdir -vp "${_test_dir}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${_test_dir}"

  export PYTHONPATH="${PWD}/${_test_dir}/usr/lib/python${_python_version}/site-packages/:${PYTHONPATH}"
  export PATH="${PWD}/test_dir/usr/bin:${PATH}"

  pytest -v "${_test_dir}/usr/lib/python${_python_version}/site-packages/molecule/test/unit/"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
