# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=4.0.2
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=(any)
url="https://github.com/ansible-community/molecule"
license=(MIT)
depends=(python-ansible-compat python-click
python-click-help-colors python-cookiecutter python-enrich python-jinja
python-jsonschema python-packaging python-pluggy python-pyyaml python-rich)
makedepends=(python-build python-setuptools-scm python-setuptools-scm-git-archive python-installer)
checkdepends=(ansible python-ansi2html python-filelock python-pexpect
python-pytest python-pytest-mock python-pytest-testinfra python-pytest-xdist
yamllint)
optdepends=(
  'ansible: for the ansible verifier'
  'molecule-docker: for the docker driver'
  'molecule-podman: for the podman driver'
  'molecule-vagrant: for the vagrant driver'
  'python-pywinrm: for Windows support'
  'python-pytest-testinfra: for the testinfra verifier'
)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('9922785c8669540428af60cb6b1c8cdd53782a9679e53e4c456c2b3afbb41fe478d605dadcd55d4b98ae452fb018be21e2e0ccc1f127ae0acd82d2a6ad48c947')
b2sums=('276a3a6c5d0c4c61addafddddae447c1dcfdba811e1002712b0ab469aad19284567d217bd410c0ecc24e1684d0969a96500c7638e1e51756c046a0d5266f472d')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="$PWD/test_dir"

  # install into test location
  mkdir -vp "$_test_dir"
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$_test_dir/usr/bin:$PATH"
  pytest -v "$_test_dir/$_site_packages/molecule/test/unit/" -c /dev/null
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"

  # remove tests
  rm -rvf "$pkgdir/$_site_packages/molecule/test/"
}
