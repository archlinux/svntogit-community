# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.6.1
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=(any)
url="https://github.com/ansible-community/molecule"
license=(MIT)
depends=(python-ansible-compat python-cerberus python-click
python-click-help-colors python-cookiecutter python-enrich python-jinja
python-packaging python-paramiko python-pluggy python-rich python-yaml)
makedepends=(python-build python-setuptools-scm python-setuptools-scm-git-archive python-installer)
checkdepends=(ansible python-ansi2html python-filelock python-pexpect
python-pytest python-pytest-html python-pytest-mock python-pytest-testinfra
python-pytest-xdist yamllint)
optdepends=(
  'ansible: for local ansible support'
  'molecule-docker: for the docker driver'
  'molecule-podman: for the podman driver'
  'molecule-vagrant: for the vagrant driver'
  'python-pywinrm: for Windows support'
  'python-pytest-testinfra: for the testinfra verifier'
)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('229f709e2d8f68013ada9f022e4d65e9ededa9b6ffb72fd804fef941ce5c548d582767fc12b86f02b179241891848a2ed3ebe292e69422940eaac1d8a2470dd4')
b2sums=('17417ef21ec5cbb72174271556eb3b3fa3c87409ff4af950dfa620a3c399c8fc3893f4616f19f4171ab8c515f9a4e808e46095bf27a8913e422053448ec2e9d6')

prepare() {
  cd "$pkgname-$pkgver"
  # allegedly cerberus is not compatible with py39, but we don't mind:
  # https://github.com/pyeve/cerberus/issues/568
  sed -e '/cerberus/ s/,.*//' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$pkgname-$pkgver"
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="${PWD}/test_dir"

  # install into test location
  mkdir -vp "${_test_dir}"
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}"
  export PATH="${_test_dir}/usr/bin:${PATH}"
  pytest -v "${_test_dir}/${_site_packages}/molecule/test/unit/" -c /dev/null
}

package() {
  cd "$pkgname-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
