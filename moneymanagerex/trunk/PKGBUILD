# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=moneymanagerex
pkgver=1.3.3
pkgrel=2
pkgdesc="An easy-to-use personal finance suite"
arch=('x86_64')
url="http://www.moneymanagerex.org/"
license=('GPL')
depends=('wxgtk3' 'webkit2gtk')
makedepends=('git' 'boost')
_commit=('2a90484293b5020556d835054285fd77a3ee1150')
source=(git+https://github.com/moneymanagerex/moneymanagerex.git#commit=${_commit})
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}"/$pkgname

  git submodule update --init
}

build() {
  cd "${srcdir}"/$pkgname

  ./configure --prefix=/usr --disable-shared --enable-unicode --with-wx-config=/usr/bin/wx-config-gtk3
  make
  make po
}

package() {
  cd "${srcdir}"/$pkgname

  make DESTDIR="${pkgdir}" install
}
