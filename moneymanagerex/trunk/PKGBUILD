# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=moneymanagerex
pkgver=1.3.3
pkgrel=4
pkgdesc="An easy-to-use personal finance suite"
arch=('x86_64')
url="https://www.moneymanagerex.org/"
license=('GPL')
depends=('wxgtk3' 'webkit2gtk')
makedepends=('git' 'boost')
_commit=('2a90484293b5020556d835054285fd77a3ee1150')
source=(git+https://github.com/moneymanagerex/moneymanagerex.git#commit=${_commit})
sha256sums=('SKIP')

prepare() {
  cd "${srcdir}"/$pkgname

  git submodule update --init

  # Fix build with wxgtk 3.1
  git cherry-pick -n 47acc00d5355aacb1f15654c4c4ebb7080a6710f
  git cherry-pick -n f23f4b027f839f3b8a9a3cd21034d2e4d9c6b7d9
}

build() {
  cd "${srcdir}"/$pkgname

  export LDFLAGS+=" -lwx_gtk3u_webview-3.1" # Fix build with wxgtk 3.1
  ./configure --prefix=/usr --disable-shared --enable-unicode --with-wx-config=/usr/bin/wx-config-gtk3
  make
  make po
}

package() {
  cd "${srcdir}"/$pkgname

  make DESTDIR="${pkgdir}" install
}
