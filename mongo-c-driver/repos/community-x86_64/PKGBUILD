# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongo-c-driver
pkgver=1.15.0
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='https://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libsasl' 'icu' 'snappy' 'zlib' 'zstd')
makedepends=('cmake')
provides=('libbson' 'libmongoc')
conflicts=('libbson' 'libmongoc')
replaces=('libbson' 'libmongoc')
source=("https://github.com/mongodb/mongo-c-driver/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b3a24116787e2e76eaeb86d26e812a6afe4fbd065ae12ed5c2730d14c78101b9c7a7cf60d549bd64c278e57581118af065e97ac09db7a08b7008b3f607538b09')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../mongo-c-driver-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_STATIC=OFF -DENABLE_TESTS=OFF
    # Test profiles are not found even with ENABLE_TESTS=ON
  make
}

check() {
  cd build
  # No test profile found
  # pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
