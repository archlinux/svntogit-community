# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongodb-tools
pkgver=3.2.0
pkgrel=1
pkgdesc="The MongoDB tools provide import, export, and diagnostic capabilities."
arch=('i686' 'x86_64')
url="https://github.com/mongodb/mongo-tools"
license=('Apache')
depends=('libsasl' 'gcc-go')
makedepends=('git')
source=("git+https://github.com/mongodb/mongo-tools.git#tag=r$pkgver")
md5sums=('SKIP')

prepare() {
  cd mongo-tools
  . ./set_gopath.sh
  mkdir bin
}

build() {
  cd mongo-tools
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog; do
    go build -o bin/$i -tags "ssl sasl" -gccgoflags "$(pkg-config --libs openssl)" $i/main/$i.go
  done
}

package() {
  cd mongo-tools
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog; do
    install -Dm755 bin/$i "$pkgdir/usr/bin/$i"
  done
}
