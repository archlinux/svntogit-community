# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongodb-tools
pkgver=3.1.0
pkgrel=2
pkgdesc="The MongoDB tools provide import, export, and diagnostic capabilities."
arch=('i686' 'x86_64')
url="https://github.com/mongodb/mongo-tools"
license=('Apache')
depends=('libsasl')
makedepends=('git' 'go')
source=("git+https://github.com/mongodb/mongo-tools.git#tag=r$pkgver"
        i686.patch)
md5sums=('SKIP'
         '1e77521c9040575ab1e7ed91ac6e410c')

prepare() {
  cd mongo-tools
  # https://github.com/mongodb/mongo-tools/issues/13
  patch -p0 -i ../i686.patch
}

build() {
  cd mongo-tools
  . ./set_gopath.sh

  mkdir bin
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog; do
    go build -o bin/$i -tags "ssl sasl" $i/main/$i.go
  done
}

package() {
  cd mongo-tools
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog; do
    install -Dm755 bin/$i "$pkgdir/usr/bin/$i"
  done
}
