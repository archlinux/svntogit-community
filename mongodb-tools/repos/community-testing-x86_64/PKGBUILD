# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongodb-tools
pkgver=3.2.5
pkgrel=1
pkgdesc="The MongoDB tools provide import, export, and diagnostic capabilities."
arch=('i686' 'x86_64')
url="https://github.com/mongodb/mongo-tools"
license=('Apache')
depends=('libsasl' 'gcc-libs>=5.3.0-3')
makedepends=('git' 'gcc-go')
source=("git+https://github.com/mongodb/mongo-tools.git#tag=r$pkgver"
        "disable-sslv3.patch")
md5sums=('SKIP'
         '883ddbb1a936d988c2d0c6d78644dce9')

prepare() {
  cd mongo-tools
  patch -Np1 -i ../disable-sslv3.patch
  . ./set_gopath.sh
  mkdir bin
}

build() {
  cd mongo-tools
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog; do
    go build -o bin/$i -tags "ssl sasl" -gccgoflags "$(pkg-config --libs openssl) $CFLAGS $LDFLAGS" $i/main/$i.go
  done
}

package() {
  cd mongo-tools
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog; do
    install -Dm755 bin/$i "$pkgdir/usr/bin/$i"
  done
}
