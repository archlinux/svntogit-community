# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb
pkgver=1.8.2
pkgrel=6
pkgdesc='A high-performance, open source, schema-free document-oriented database.'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('boost-libs' 'js' 'pcre')
makedepends=('scons' 'boost')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install="mongodb.install"
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.rc'
        'mongodb.conf'
        'mongodb-1.8.0-spidermonkey-1.8.5-support.patch'
        'add-js185-support-to-SConstruct.diff')
md5sums=('951fb1a75d90fc822cf4528585a970cf'
         '85eaa28e349fdc6250f883624e624cca'
         '4839fe1d638187ca3226e8267b947318'
         '2e6409732fba887c9cfe81257b5260ad'
         'ca7e62be31389d951bfd8848b1675c1b')

build() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  # js185 support https://jira.mongodb.org/browse/SERVER-2887
  patch -Np1 -i ${srcdir}/mongodb-1.8.0-spidermonkey-1.8.5-support.patch
  patch -Np0 -i ${srcdir}/add-js185-support-to-SConstruct.diff

  scons \
    all \
    --full
}

package() {
  cd mongodb-src-r${pkgver}

  scons \
    install \
    --full \
    --prefix=${pkgdir}/usr

  install -D -m755 ${srcdir}/mongodb.rc \
    ${pkgdir}/etc/rc.d/mongodb
  install -D -m644 ${srcdir}/mongodb.conf \
    ${pkgdir}/etc/mongodb.conf
  install -d -m700 ${pkgdir}/var/lib/mongodb
  install -d -m755 ${pkgdir}/var/log/mongodb

  if [ -d ${pkgdir}/usr/lib64 ]; then
    mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib
  fi
}
