# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb
pkgver=2.6.6
pkgrel=1
pkgdesc='A high-performance, open source, schema-free document-oriented database'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('pcre' 'snappy' 'openssl' 'gperftools' 'libsasl' 'boost-libs') # 'libyaml' 'yaml-cpp' 'v8')
makedepends=('scons' 'readline' 'ncurses' 'libpcap' 'boost' 'clang')
checkdepends=('python2-pymongo')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install=mongodb.install
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.conf' 'mongodb.service')

prepare() {
  cd mongodb-src-r${pkgver}

  # Add missing include required for std::swap()
  # https://github.com/mongodb/mongo/commit/2d5d662
  if grep -q '#include <algorithm>' src/mongo/shell/linenoise_utf8.h; then
    error '<algorithm> include has been fixed upstream; remove this local fix.'
    return 1
  fi
  sed -i '1i #include <algorithm>' src/mongo/shell/linenoise_utf8.h
}

build() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons all \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --use-system-boost \
    --ssl \
    --disable-warnings-as-errors \
    --cc=clang --cxx=clang++
    # --use-system-v8
    # --use-system-yaml
}

check() {
  # Tests not passing
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons smokeAll \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --use-system-boost \
    --ssl \
    --disable-warnings-as-errors \
    --cc=clang --cxx=clang++ \
    --smokedbprefix="$srcdir" || warning "Tests failed"
}

package() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd mongodb-src-r${pkgver}

  scons install \
    --use-system-pcre \
    --use-system-snappy \
    --use-system-tcmalloc \
    --use-sasl-client \
    --use-system-boost \
    --ssl \
    --disable-warnings-as-errors \
    --prefix="$pkgdir/usr" \
    --cc=clang --cxx=clang++
    # --use-system-yaml
    # --use-system-v8

  install -Dm644 "$srcdir/mongodb.conf" "$pkgdir/etc/mongodb.conf"
  install -Dm644 "$srcdir/mongodb.service" "$pkgdir/usr/lib/systemd/system/mongodb.service"
  install -dm700 "$pkgdir/var/lib/mongodb"
  install -dm755 "$pkgdir/var/log/mongodb"
}

sha512sums=('0f5fdce6ad3aa64e3636099cee9b7099b74c5f1d5887c89543bcdf900e523a855164f497071c8c4251c8e87361841eaec969bd7b7e0efcff5504cf5f1d6b9f05'
            '05dead727d3ea5fe8af1a3c3888693f6b3e2b8cb7f197a5d793352e10d2c524e96c9a5c55ad2e88c1114643a9612ec0b26a2574b48a5260a9b51ec8941461f1c'
            '177251404b2e818ae2b546fe8b13cb76e348c99e85c7bef22a04b0f07b600fd515a309ede50214f4198594388a6d2b31f46e945b9dae84aabb4dfa13b1123bb9')
