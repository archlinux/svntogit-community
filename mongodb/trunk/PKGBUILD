# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mathias Stearn <mathias@10gen.com>
# Contributor: Alec Thomas

pkgname=mongodb
pkgver=1.8.1
pkgrel=2
pkgdesc='A high-performance, open source, schema-free document-oriented database.'
arch=('i686' 'x86_64')
url='http://www.mongodb.org'
license=('AGPL3')
depends=('boost-libs' 'spidermonkey' 'pcre')
makedepends=('scons' 'boost')
optdepends=('libpcap: needed for mongosniff')
backup=('etc/mongodb.conf')
install="mongodb.install"
source=("http://downloads.mongodb.org/src/mongodb-src-r${pkgver}.tar.gz"
        'mongodb.rc'
        'mongodb.conf'
        'gcc46fixes.diff'
        'gcc46fixes2.diff')
md5sums=('e75a5cae641a53760df8cb866ad5d929'
         '859f8f9bb32ef2bd21fec55ae9a87d0a'
         '9e0ea3f96732bb7811f0b64dace56440'
         'e90c78350e25df2d24f98e4767677d4b'
         '5097de6ce2c347c3703ab8cf5a611052')

build() {
  export SCONSFLAGS="$MAKEFLAGS"

  cd ${pkgname}-src-r${pkgver}

  patch -Np1 -i ${srcdir}/gcc46fixes.diff
  patch -Np1 -i ${srcdir}/gcc46fixes2.diff

  # scons is "special"
  sed -i 's/-Wall -Wsign-compare/& -DBOOST_FILESYSTEM_VERSION=2/' SConstruct

  scons \
    all \
    --full
}

package() {
  cd ${pkgname}-src-r${pkgver}

  scons \
    install \
    --full \
    --prefix=${pkgdir}/usr

  install -D -m755 ${srcdir}/mongodb.rc \
    ${pkgdir}/etc/rc.d/mongodb
  install -D -m644 ${srcdir}/mongodb.conf \
    ${pkgdir}/etc/mongodb.conf
  install -d -m700 ${pkgdir}/var/state/mongodb

  if [ -d ${pkgdir}/usr/lib64 ]; then
    mv ${pkgdir}/usr/lib64 ${pkgdir}/usr/lib
  fi
}
