# $Id$
# Maintainer: Alexander F RÃ¸dseth <rodseth@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=monica
pkgver=3.7
pkgrel=5
pkgdesc='Monitor calibration tool'
arch=('x86_64' 'i686')
url='https://sources.archlinux.org/other/monica/'
license=('BSD')
depends=('fltk' 'xorg-xgamma')
makedepends=('gendesk')
source=("https://sources.archlinux.org/other/monica/$pkgname-$pkgver.tar.bz2"
        'monica.png')
sha256sums=('da0d7762f5a2eed482e8e2f87762d3ac1f013d5c99bd99b34083b69f03492036'
            '4c9852c7462f747eeede72f252e2578d827c12f09ffd83808f695a64c67bee69')

prepare() {
  cd "$pkgname-$pkgver"

  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --categories 'Application;Settings;System'
}

build() {
  make -C "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 licence "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
