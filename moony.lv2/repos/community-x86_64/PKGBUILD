# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=moony.lv2
pkgver=0.40.0
pkgrel=1
pkgdesc="Easily add realtime programmable logic glue in LV2 plugin graphs"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/moony/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11' 'lv2-host')
makedepends=('glew' 'glu' 'libvterm' 'lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('f602f15fcf0a5b5b78ad7fd14a91b22384cc34d6f98192fc807b8578fd81d5dd50d5441351d281889133b35e519467ae044fba786a16b16129af0af33215e57c'
            'SKIP')
b2sums=('7e6d43cd4c9a5987657644db5ec333ea8cea9fa527b6c65d54ce43db583641910f1e416ee4d6fdb59e643b1c21592250d690f8fe05cd7fd7c34745ccd7254902'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  depends+=('libGLEW.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
