# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=moony.lv2
pkgver=0.38.0
pkgrel=1
pkgdesc="Easily add realtime programmable logic glue in LV2 plugin graphs"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/moony/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11' 'lv2-host')
makedepends=('glew' 'glu' 'libvterm' 'lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('bde2d89119e15da2484547e6c6470174db4ed9474ee4089d0d0aeafcc6165f9aff6933a6f1038a80af6ed72570e2b806548e6d21086b8a89e13191dd6bc13740'
            'SKIP')
b2sums=('28e8fff4bbd36e06718a0239759c4d287ab1950ba7c3475332ec901c81d473e3427882d61e21270f45c2278c89b157e7fc4c864f06aa324f4cb286f2ada261e4'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  depends+=('libGLEW.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
