# $Id: PKGBUILD 133367 2015-05-13 12:31:10Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=moosefs
pkgver=3.0.84_1
pkgrel=1
pkgdesc='High performing and fault tolerant distributed file system'
url='http://moosefs.com/'
replaces=('mfs-master' 'mfs-chunkserver' 'mfs-client')
conflicts=('mfs-master' 'mfs-chunkserver' 'mfs-client')
arch=('x86_64' 'i686')
license=('GPL2')
depends=('fuse' 'python2' 'bash' 'xfsprogs')
install="$pkgname.install"
source=("http://ppa.moosefs.com/src/moosefs-${pkgver/_/-}.tar.gz")
sha256sums=('384153d6411633122f6a3b9b9b30fc47be2e881f45c9733f112f4c9b9530f328')

build() {
  cd "moosefs-${pkgver%_*}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --with-default-user=mfs \
    --with-default-group=mfs \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  make DESTDIR="$pkgdir" -C "moosefs-${pkgver%_*}" install

  # Move mount.moosefs from sbin to usr/bin
  mv "$pkgdir/sbin/"* "$pkgdir/usr/bin/"
  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/sbin"
  rmdir "$pkgdir/usr/sbin"
}

# getver: moosefs.com/download/sources.html
# vim:set ts=2 sw=2 et:
