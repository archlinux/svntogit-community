# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexandre Petitjean <alpetitjean at gmail dot com>

pkgname=mopidy
pkgver=2.1.0
pkgrel=3
pkgdesc="An extensible music server written in Python"
arch=('any')
url="http://www.mopidy.com"
license=('APACHE')
depends=('python2' 'python2-pykka>=1.1' 'python2-setuptools' 'python2-requests' 'gstreamer'
         'gst-python2' 'gst-plugins-good' 'gst-plugins-ugly' 'python2-tornado>=2.3')
makedepends=('python2-sphinx')
backup=('etc/mopidy/mopidy.conf')
source=("https://github.com/mopidy/mopidy/archive/v${pkgver}.tar.gz"
        'mopidy.conf'
        'logging.conf'
        'mopidy.sysusers')
sha512sums=('4d8c48180fb6402a64e16ac0d6448f354a3655a6924da7a55b70c8b2420cc1ede211ebaec7cfba0a365ca7595ae3d177a10184951c2203cc6e9a1a9631ce90de'
            'eb66e8e826640a939b1ba51569ab7fab041b8b5e8823ea2d5f05596faf1de8882fd8c1c32bdb92534e759243fb5ff741bda0d2ebb3282af542d1287c8c68b5ea'
            '0c438058500ab7559baae21b03b10e2b80b10c77776b240b2100da1f4c84ea8efe24dc7a38a95034e75605eaf5d21604d13e5b8c7358778c555ddb6372a49388'
            'feba29cac82fbf9e80eea5977e6576458d33704b8f0381d0f0b305bb47dc3a7134648da665dd0824afc2abb9681c4e0ecd893740884bd1df4562fd289bfe3c1e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make -C docs SPHINXBUILD=sphinx-build2 man
  make -C docs SPHINXBUILD=sphinx-build2 html
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm755 extra/mopidyctl/mopidyctl "${pkgdir}/usr/bin/mopidyctl"

  install -dm755 "${pkgdir}/usr/share/doc/mopidy"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/mopidy"

  install -dm755 "${pkgdir}/usr/share/man/man1/"
  gzip -c docs/_build/man/mopidy.1 > "${pkgdir}/usr/share/man/man1/mopidy.1.gz"

  install -dm755 "${pkgdir}/usr/share/man/man8/"
  gzip -c extra/mopidyctl/mopidyctl.8 > "${pkgdir}/usr/share/man/man8/mopidyctl.8.gz"

  install -Dm644 "${srcdir}/mopidy.conf" "${pkgdir}/etc/mopidy/mopidy.conf"
  install -Dm644 "${srcdir}/logging.conf" "${pkgdir}/etc/mopidy/logging.conf"

  install -Dm644 "extra/systemd/mopidy.service" "${pkgdir}/usr/lib/systemd/system/mopidy.service"
  install -Dm644 "extra/desktop/mopidy.desktop" "${pkgdir}/usr/share/applications/mopidy.desktop"

  install -dm755 -g 46 -o 46 "${pkgdir}/var/cache/mopidy"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/log/mopidy"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy/local"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy/media"
  install -dm755 -g 46 -o 46 "${pkgdir}/var/lib/mopidy/playlists"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/mopidy.sysusers" "${pkgdir}/usr/lib/sysusers.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
