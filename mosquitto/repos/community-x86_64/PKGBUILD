# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Radek Podgorny <radek@podgorny.cz>
# Contributor: Dan Anderson <dan-anderson at cox dptnet>

pkgname=mosquitto
pkgver=1.4.14
pkgrel=1
pkgdesc="An Open Source MQTT v3.1/v3.1.1 Broker"
arch=('i686' 'x86_64')
url="http://mosquitto.org/"
depends=('openssl' 'c-ares' 'libwebsockets')
makedepends=('docbook-xsl' 'c-ares')
license=('BSD')
source=(https://mosquitto.org/files/source/$pkgname-$pkgver.tar.gz{,.asc} "$pkgname.service" "sysusers_mosquitto.conf")
backup=("etc/$pkgname/$pkgname.conf")
sha256sums=('156b1fa731d12baad4b8b22f7b6a8af50ba881fc711b81e9919ec103cf2942d1'
            'SKIP'
            '3480aa1a9da543230fbbb0e7e11cde9db2736e486cd024fbd505f927188fa3a2'
            '7ba9ae9e3219e24c1585760f39fab5632ded52172cd8193cc4adbd8af46c5197')
validpgpkeys=('A0D6EEA1DCAE49A635A3B2F0779B22DFB3E717B7')

build() {
  cd "$pkgname-$pkgver"
  # FIX upstream by making SBINDIR=foo or use CMAKE
  sed -i 's|/sbin|/bin|g' src/Makefile
  make WITH_WEBSOCKETS=yes
}

package() {
  cd "$pkgname-$pkgver"

  make prefix=/usr DESTDIR="$pkgdir/" install

  # Shipped in git.
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/etc/$pkgname/$pkgname.conf.example" "$pkgdir/etc/$pkgname/$pkgname.conf"

  install -D -m644 "${srcdir}"/sysusers_mosquitto.conf "${pkgdir}"/usr/lib/sysusers.d/mosquitto.conf
}
