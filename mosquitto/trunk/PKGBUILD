# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Radek Podgorny <radek@podgorny.cz>
# Contributor: Dan Anderson <dan-anderson at cox dptnet>

pkgname=mosquitto
pkgver=1.6.3
pkgrel=1
pkgdesc="An Open Source MQTT v3.1/v3.1.1 Broker"
arch=('x86_64')
url="https://mosquitto.org/"
depends=('openssl' 'c-ares' 'libwebsockets' 'libutil-linux')
makedepends=('docbook-xsl' 'c-ares')
license=('BSD')
source=(https://mosquitto.org/files/source/mosquitto-$pkgver.tar.gz{,.asc}
        "$pkgname.service"
        "sysusers_mosquitto.conf")
backup=("etc/$pkgname/$pkgname.conf")
sha256sums=('9ef5cc75f4fe31d7bf50654ddf4728ad9e1ae2e5609a4b42ecbbcb4a209ed17e'
            'SKIP'
            '539b6aa2f88bba25fead1a5cbb285f18470c244391c0dfcac8d3f59e99a0652d'
            '7ba9ae9e3219e24c1585760f39fab5632ded52172cd8193cc4adbd8af46c5197')
validpgpkeys=('A0D6EEA1DCAE49A635A3B2F0779B22DFB3E717B7')

prepare() {
  cd "$pkgname-$pkgver"
  # FIX upstream by making SBINDIR=foo or use CMAKE
  sed -i 's|/sbin|/bin|g' src/Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make WITH_WEBSOCKETS=yes
}

package() {
  cd "$pkgname-$pkgver"

  make prefix=/usr DESTDIR="$pkgdir/" install

  # Shipped in git.
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/etc/$pkgname/$pkgname.conf.example" "$pkgdir/etc/$pkgname/$pkgname.conf"

  install -D -m644 "${srcdir}"/sysusers_mosquitto.conf "${pkgdir}"/usr/lib/sysusers.d/mosquitto.conf
}
