# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
_pkgbase=mozo
pkgname=${_pkgbase}-gtk3
pkgver=${_ver}.0
pkgrel=2
pkgdesc="MATE menu editing tool (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('mate-menus>=1.12' 'python2-gobject')
makedepends=('git' 'mate-common')
groups=('mate-extra-gtk3')
conflicts=("${_pkgbase}")
source=("${_pkgbase}"::"git+https://github.com/mate-desktop/${_pkgbase}.git#branch=gtk3")
sha1sums=('SKIP')
install=${_pkgbase}.install

prepare() {
    cd "${srcdir}/${_pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${_pkgbase}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${_pkgbase}"
    make DESTDIR="${pkgdir}" install
}
