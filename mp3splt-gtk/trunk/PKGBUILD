# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=mp3splt-gtk
pkgver=0.9.2
pkgrel=3
pkgdesc="Split mp3 and ogg files without decoding"
arch=('x86_64')
url="http://mp3splt.sourceforge.net/"
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'libmp3splt' 'gst-plugins-good' 'gst-plugins-ugly')
changelog=$pkgname.changelog
source=(https://downloads.sourceforge.net/sourceforge/mp3splt/$pkgname-$pkgver.tar.gz)
sha256sums=('2084cc942b5d1aa6ff74848a41d05cb37876cbc74dfdc341ceb424c763887650')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./configure --prefix=/usr --disable-gnome --disable-mp3splttest --disable-audacious
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
