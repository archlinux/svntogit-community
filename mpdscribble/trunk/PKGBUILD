# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowwski.pl>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: evr <evanroman at gmail>
# Contributor: Luiz Ribeiro <luizribeiro@gmail.com>

pkgname=mpdscribble
pkgver=0.22
pkgrel=5
pkgdesc='An mpd client which submits track info to last.fm'
url='http://mpd.wikia.com/wiki/Client:Mpdscribble'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup' 'glib2' 'libmpdclient')
install='mpdscribble.install'
source=(http://downloads.sourceforge.net/musicpd/mpdscribble-${pkgver}.tar.bz2
        rc.d service)
md5sums=('652ee927b797e9a4cef45494e77047db'
         'f54c937c7b3ab8e4d39e4b903c2993fc'
         '7e0a80c12225b0fe7be9c993f0079e95')

build() {
  cd mpdscribble-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-http-client=soup

  make
}

package() {
  cd mpdscribble-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}"/rc.d \
    "${pkgdir}"/etc/rc.d/mpdscribble

  install -Dm644 "${srcdir}"/service \
    "${pkgdir}"/usr/lib/systemd/system/mpdscribble.service

  # default config is really an example
  install -D -m644 "${pkgdir}"/etc/mpdscribble.conf \
    "${pkgdir}"/usr/share/mpdscribble/mpdscribble.conf.example
  rm -f "${pkgdir}"/etc/mpdscribble.conf

  install -d "${pkgdir}"/var/cache/mpdscribble
  touch "${pkgdir}"/var/cache/mpdscribble/mpdscribble.cache
}
