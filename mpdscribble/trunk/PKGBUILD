# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: evr <evanroman at gmail>
# Contributor: Luiz Ribeiro <luizribeiro@gmail.com>

pkgname=mpdscribble
pkgver=0.22
pkgrel=1
pkgdesc='An mpd client which submits track info to last.fm'
url='http://mpd.wikia.com/wiki/Client:Mpdscribble'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup' 'glib2' 'libmpdclient')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/musicpd/mpdscribble-${pkgver}.tar.bz2"
        'mpdscribble')
md5sums=('652ee927b797e9a4cef45494e77047db'
         'f54c937c7b3ab8e4d39e4b903c2993fc')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-http-client=soup

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/mpdscribble \
    ${pkgdir}/etc/rc.d/mpdscribble

  # default config is really an example
  install -m644 ${pkgdir}/etc/mpdscribble.conf \
    ${pkgdir}/etc/mpdscribble.conf.example
  rm -f ${pkgdir}/etc/mpdscribble.conf

  install -d ${pkgdir}/var/cache/mpdscribble
  touch ${pkgdir}/var/cache/mpdscribble/mpdscribble.cache
}
