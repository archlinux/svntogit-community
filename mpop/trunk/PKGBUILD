# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Israel Herraiz <isra@herraiz.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=mpop
pkgver=1.2.4
pkgrel=3
pkgdesc="A small, fast POP3 client suitable as a fetchmail replacement"
arch=('i686' 'x86_64')
url="http://mpop.sourceforge.net/"
license=('GPL3')
depends=('gnutls' 'libidn')
install=mpop.install
source=("http://downloads.sourceforge.net/mpop/mpop-$pkgver.tar.xz"{,.sig})
md5sums=('7ceca49cb4a453c52374afcf9ae90c21'
         'SKIP')
validpgpkeys=('2F788CDEF4181652720EF132F4926138953E5294')

build() {
  cd "$srcdir/mpop-$pkgver"

  ./configure --prefix=/usr --with-ssl=gnutls
  make
}

package() {
  cd "$srcdir/mpop-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 doc/mpoprc.example "$pkgdir/usr/share/mpop/mpoprc.example"
}
