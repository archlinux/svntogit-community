# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=mps-youtube
pkgver=0.2.6
pkgrel=1

pkgdesc="Terminal based YouTube jukebox with playlist management"
url='https://github.com/np1/mps-youtube'
arch=('any')
license=('GPL3')

depends=('python' 'python-setuptools' 'python-pafy')
optdepends=('mpv: Alternative to mplayer for playback'
            'mplayer: Alternative to mpv for playback'
            'ffmpeg: for transcoding downloaded content'
            'xclip: for copying content to the clipboard')

install=mps-youtube.install

source=("mps-youtube-$pkgver.tar.gz::https://github.com/np1/mps-youtube/archive/v$pkgver.tar.gz"
        "fix-license-readme-changelog-dir.patch")

sha256sums=('df3cc7ad9a49a0da390c38f40c905daa992d48de5fb421865ec19cc1fbec453f'
            'aa3993ecec633bbf13248adc874b01bd7e80d61a9e0757ffe52964e93f490d27')

prepare() {
	cd mps-youtube-"$pkgver"

	patch -p1 <"$srcdir"/fix-license-readme-changelog-dir.patch
}

package() {
	cd mps-youtube-"$pkgver"

	python setup.py install --root="$pkgdir" --optimize=1
}
