# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.2.1
pkgrel=4
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
         'portaudio' 'ffmpeg' 'lcms2' 'libdvdread' 'libcdio-paranoia' 'libxinerama'
         'libquvi' 'mpg123' 'libxv' 'libxkbcommon' 'libva' 'lirc-utils'
         'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'lua51'
        )
makedepends=('mesa' 'python-docutils')
options=(!emptydirs)
install=mpv.install
source=("https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz")
md5sums=('4e7c8823354f97753f0cbe17c61ec211')

build() {
  unset CFLAGS
  unset LDFLAGS

  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-lircc \
    --disable-joystick \
    --disable-radio \
    --disable-radio-capture \
    --disable-radio-v4l2 \
    --disable-rpath \
    --disable-libbs2b \
    --disable-direct3d \
    --disable-corevideo \
    --disable-cocoa \
    --disable-coreaudio
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example,encoding-example-profiles}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv

  #install -Dm644 etc/mpv.desktop "$pkgdir"/usr/share/applications/mpv.desktop
}
