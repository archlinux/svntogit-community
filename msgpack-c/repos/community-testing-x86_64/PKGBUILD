# Maintainer: Baptiste Jonglez
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=3.0.1
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=("x86_64")
url='http://msgpack.org/'
license=("Apache")
depends=("glibc")
checkdepends=("gtest")
makedepends=("cmake")
source=("https://github.com/msgpack/msgpack-c/releases/download/cpp-${pkgver}/msgpack-${pkgver}.tar.gz")
sha256sums=('9859d44d336f9b023a79a3026bb6a558b2ea346107ab4eadba58236048650690')

build() {
  cd "msgpack-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DMSGPACK_CXX11=ON \
        -DMSGPACK_BUILD_EXAMPLES=OFF \
        .
  make
}

check() {
  cd "msgpack-${pkgver}"
  make test 
}

package() {
  cd "msgpack-${pkgver}"
  make DESTDIR="$pkgdir" install
}
