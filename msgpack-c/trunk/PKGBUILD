# Maintainer: Baptiste Jonglez
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=3.2.0
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=("x86_64")
url='https://msgpack.org/'
license=("Apache")
depends=("glibc")
checkdepends=("gtest")
makedepends=("cmake")
source=("https://github.com/msgpack/msgpack-c/releases/download/cpp-${pkgver}/msgpack-${pkgver}.tar.gz")
sha256sums=('fbaa28c363a316fd7523f31d1745cf03eab0d1e1ea5a1c60aa0dffd4ce551afe')

build() {
  cd "msgpack-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DBUILD_SHARED_LIBS=ON \
        -DMSGPACK_ENABLE_STATIC=OFF \
        -DMSGPACK_CXX11=ON \
        -DMSGPACK_BUILD_EXAMPLES=OFF \
        .
  make
}

check() {
  cd "msgpack-${pkgver}"
  make test 
}

package() {
  cd "msgpack-${pkgver}"
  make DESTDIR="$pkgdir" install
}
