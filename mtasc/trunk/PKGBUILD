# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Niel Drummond <niel.drummond@grumpytoad.org>

pkgname=mtasc
pkgver=20091229
pkgrel=2
pkgdesc="An open source flash (swf) compiler"
arch=(i686 x86_64)
url="http://mtasc.org/"
license=('GPL2')
depends=('zlib')
makedepends=('ocaml' 'cvs')
source=("http://arch.p5n.pp.ru/~sergej/dl/mtasc-$pkgver.tar.gz")
md5sums=('eb686e4fcaa066dfa77d29b2d4551aab')

build() {
  cd $srcdir/mtasc-$pkgver
  sed -i 's|let base_path = normalize_path (try Extc.executable_path() with _ -> ".") in|let base_path = "/usr/share/mtasc/" in|' ocaml/mtasc/main.ml
  (cd ocaml/extc && make)
  ocaml install.ml
}

package() {
  cd $srcdir/mtasc-$pkgver
  install -D -m755 $srcdir/mtasc-$pkgver/bin/${pkgname} $pkgdir/usr/bin/${pkgname}
  for f in $( cd ocaml/mtasc/ && find ./{std,std8} -type f -iname '*.as' | grep -iv cvs ); do
    install -D -m644 ocaml/mtasc/$f $pkgdir/usr/share/mtasc/$f
  done
}
