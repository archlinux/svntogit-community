# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: yosh64 <yosh64.at.gmail.dot.com>

pkgname=mtpaint
pkgver=3.40
pkgrel=8
pkgdesc='Simple paint program for creating icons and pixel based artwork'
arch=('x86_64' 'i686')
url='http://mtpaint.sourceforge.net/'
license=('GPL3')
depends=('gtk2' 'giflib' 'openjpeg' 'desktop-file-utils' 'lcms2')
makedepends=('setconf')
install='mtpaint.install'
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        'libpng15.patch')
sha256sums=('ef321d2b404839c7b909bdf5283eb22a37fbdd35b4cc9e380ddc400573d7c890'
            'be51d45d1146d6c61d1c2c12d1712134da5048dd7314e2741ff336cac6838de3')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	patch -Np1 -i '../libpng15.patch'
  sed -i 's:openjpeg.h:openjpeg-1.5/openjpeg.h:' src/png.c
  sed -i 's:EGifOpenFileName(file_name, FALSE):EGifOpenFileName(file_name, FALSE, NULL):' src/png.c
  sed -i 's:DGifOpenFileName(file_name):DGifOpenFileName(file_name, NULL):g' src/png.c
  sed -i 's:MakeMapObject(:GifMakeMapObject(:g' src/png.c
  sed -i 's:FreeMapObject(:GifFreeMapObject(:g' src/png.c
}

build() {
	cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS="`pkg-config gtk+-2.0 --cflags` $CFLAGS"
	./configure --prefix=/usr --mandir=/usr/share/man man intl gif jpeg tiff
  sed -i 's:-lpng:-lpng -lgif:' _conf.txt
  sed -i 's:ungif:gif:g' _conf.txt
  setconf _conf.txt CC 'gcc -w'
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:

