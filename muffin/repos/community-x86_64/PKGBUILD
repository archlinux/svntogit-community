# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.4.3
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'gobject-introspection-runtime' 'libcanberra' 'libinput'
         'libsm' 'libxkbcommon-x11' 'startup-notification' 'zenity' 'graphene' 'pipewire')
makedepends=('meson' 'intltool' 'gobject-introspection')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('dfaed5015ae5eb89c603f155dcb7f9d9c4434827eb94da188a7a2f5a2b8bd422')
b2sums=('dde8faa82a893397179d57986f09872b7515b3a53ae0ef2e43dce2f24596d4f7795cde69d9510a08e6932ee727908d2aa7d299d746591bf0e05ee689826b3836')

build() {
    arch-meson build $pkgname-$pkgver
    meson compile -C build
}

package() {
    meson install --destdir="$pkgdir" -C build
}
