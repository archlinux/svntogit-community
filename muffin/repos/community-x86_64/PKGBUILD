# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.4.2
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'gobject-introspection-runtime' 'libcanberra' 'libinput'
         'libsm' 'libxkbcommon-x11' 'startup-notification' 'zenity' 'graphene' 'pipewire')
makedepends=('meson' 'intltool' 'gobject-introspection')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('650b595e15468a2971d99dcb8ba19b64ee3e5b86a77944ca43811b3e0ae644a5')
b2sums=('c488eae5b9ce787b1f08014ad1d840a778597a16a2079cc6f47127e7a55a5d6f2c00a5683941c88b72ba41f205d0e254b614eda9880d8b008229d7c748376284')

build() {
    arch-meson build $pkgname-$pkgver
    meson compile -C build
}

package() {
    meson install --destdir="$pkgdir" -C build
}
