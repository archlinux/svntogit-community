# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.4.1
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'gobject-introspection-runtime' 'libcanberra' 'libinput'
         'libsm' 'libxkbcommon-x11' 'startup-notification' 'zenity' 'graphene' 'pipewire')
makedepends=('meson' 'intltool' 'gobject-introspection')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('88f405856559fc4fa0b6c3f396339c52625ec4cac225e0edafcfd1b6390e1978')
b2sums=('ba2f58c29a0f518cf8ec400b1b05d7f1abe31276decf1cb9a1523ffd6b5701eaa9b7d741b0e86130d1429f67038a671f2d2b4fcfd859f5c7bdf9446b395831bd')

build() {
    arch-meson build $pkgname-$pkgver
    meson compile -C build
}

package() {
    meson install --destdir="$pkgdir" -C build
}
