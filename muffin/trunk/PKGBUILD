# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=2.0.5
pkgrel=3
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gobject-introspection' 'cinnamon-desktop' 'libcanberra'
         'startup-notification' 'zenity' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common')
options=('!emptydirs')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/muffin/archive/$pkgver.tar.gz"
        cogl_ABI.patch
        gtkdoc.patch)
sha256sums=('81a57d452ea88ea926b284248033f021960400b3b9d13f5a334a77cf3bd4c7ee'
            'bce6cf03f7d635345db8a664084b98122aa23802867c7e6e4573273ffccc01e5'
            '4f7deff60dc65557aefb9f0473bcc58a7c33c0d25ed6ebfcdd6602f7e1639caa')

prepare() {
  cd ${pkgname}-${pkgver}

  # Port to cogl 1.18
  patch -Np1 -i ../cogl_ABI.patch

  # Build fix
  patch -Np1 -i ../gtkdoc.patch
}

build() {
  cd ${pkgname}-${pkgver}

  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin \
  				 			 --localstatedir=/var --disable-static --disable-schemas-compile \
                 --enable-compile-warnings=minimum
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
