# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.6.1
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(cinnamon-desktop gobject-introspection-runtime libcanberra libinput
         libsm libxkbcommon-x11 startup-notification zenity graphene pipewire)
makedepends=(meson intltool gobject-introspection)
options=(!emptydirs)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('03cebb5dfbe110b980626b0c0b2374c3ec5d776fa6b7adc7529bdf815f3c13da')
b2sums=('555b0d3ec9a2215c7ba8f81ff2a6adb0e5d9ec2d800c54fe77f22362179506415dff7e0908b5d32df690b9a1213c7ceaf5b8ec8448979729a7e1ff79603fa856')

build() {
  arch-meson build ${pkgname}-${pkgver}
  meson compile -C build
}

package() {
  meson install --destdir="${pkgdir}" -C build
}
