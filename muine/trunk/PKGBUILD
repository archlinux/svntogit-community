# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben Mazer <blm@groknil.org>

pkgname=muine
pkgver=0.8.11
pkgrel=4
pkgdesc="A music player written in C Sharp"
arch=('i686' 'x86_64')
license=('GPL')
url="http://muine.gooeylinux.org/"
depends=('gst-plugins-base' 'gconf-sharp' 'taglib-sharp' 'notify-sharp')
makedepends=('intltool' 'gnome-common')
optdepends=('gst-plugins-bad: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/0.8/$pkgname-$pkgver.tar.bz2
        fix-multimedia-key-support.patch
        use-notify-sharp.patch
        fix-overwrite-dialog.patch
        use-dbus-sharp.patch
        drop-gnome-sharp-dependency.patch
        use-standard-icons.patch
        use-gstreamer-1.0.patch
        enable-deprecations.patch
        drop-musicbrainz.patch
        fix-dnd.patch)
sha256sums=('fac70bd9ab8a3c7c48c1f48d10f06080f6b5a42ce32cce21a6ab472cb178ecd4'
            'b798a754ca545f9efb97e6d2e5911f26af028a41c0f40a4e3f2cb9c8fe38cbfa'
            '870bb03f031c7ed248b417896b4e02d2ed3659e1b7e320d032d48b81d558ee8a'
            '061548c33fd100d08af479a6228ffc3bc990caef63438d4a3f634fa6f04b0385'
            'c7b45fce8aaf988bcc169cb574061aeea898a92c2aa4a79afafdf77d26221eba'
            '8671a431e9c30ed4373fbbe659f90fbe411e178f31daa4fb8dc1914ec0a6864c'
            'd3e8171b2f3ceb27e039652289ac79c846a797c88eb6945af833da80eb33c154'
            '4887f59d1a039e907961681b57eea5dfb2d7d5d27d1e37ae677d4ac6394095b5'
            '598763b8f81a08b6eea54f5710480e7ca2f706ed4fa82527d3525a649ab774ab'
            '5bf67b8c879138d3b50d21ee0a95025640ef0ed06044f2938580513192ce2ecc'
            '91f657c23792c62de9a391035c635a602e5afe430182aa8e8912d84112199718')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-multimedia-key-support.patch
  patch -Np1 -i ../use-notify-sharp.patch
  patch -Np1 -i ../fix-overwrite-dialog.patch
  patch -Np1 -i ../use-dbus-sharp.patch
  patch -Np1 -i ../drop-gnome-sharp-dependency.patch
  patch -Np1 -i ../use-standard-icons.patch
  patch -Np1 -i ../use-gstreamer-1.0.patch
  patch -Np1 -i ../enable-deprecations.patch
  patch -Np1 -i ../drop-musicbrainz.patch
  patch -Np1 -i ../fix-dnd.patch
  sed -i 's/MCS, gmcs/MCS, mcs/' configure.in
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --enable-compile-warnings=minimum \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
