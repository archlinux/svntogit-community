# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.4.9
pkgrel=9
pkgdesc='Multipath tools for Linux'
arch=('i686' 'x86_64')
url="http://christophe.varoqui.free.fr/"
license=('GPL')
depends=('bash' 'libaio' 'device-mapper')
backup=('etc/multipath.conf' 'etc/multipath.conf.annotated')
install=multipath-tools.install
options=(!emptydirs)
source=(http://christophe.varoqui.free.fr/multipath-tools/$pkgname-$pkgver.tar.bz2
        multipath.conf multipath.conf.annotated multipathd.rc
        fix-build.patch buffer-overflows.patch log_enquery_overflow.patch
        blacklist-cciss-devices.patch explicitly-include-posix_types.h.patch)
md5sums=('a6d4b48afc28f1f50f5ee4b1b06d2765'
         '9324ff0ba8330dcb21b2fcf64988026f'
         'be11462922eeeb9fcd2ba5f3f137b7d9'
         '7c04ef96441363b0d43a2ebb13a87659'
         '885c0ba9c90b73cc93aa3f78005f81d6'
         'c5aab36777b0304a3525533cdd31bddc'
         '00eae05e02f1b85062e998574ab1b833'
         '61b4038c4c145ca52e836145ea6bdd6c'
         'd99beb6d8a9b84f90125c7cacf8a6006')

build() {
  patch -Np1 -i fix-build.patch
  patch -Np1 -i buffer-overflows.patch
  patch -Np1 -i log_enquery_overflow.patch
  patch -Np1 -i blacklist-cciss-devices.patch
  patch -Np1 -i explicitly-include-posix_types.h.patch

  # Needs to be fixed upstream. Refer to
  # https://bbs.archlinux.org/viewtopic.php?pid=793814#p793814
  export LDFLAGS=${LDFLAGS/-Wl,--as-needed}
  
  make LIB='usr/lib'
}

package() {
  make LIB='usr/lib' DESTDIR="$pkgdir" bindir='/usr/bin' libudevdir='/usr/lib/udev' install
  install -D -m 644 multipath.conf "$pkgdir"/etc/multipath.conf
  install -D -m 644 multipath.conf.annotated "$pkgdir"/etc/multipath.conf.annotated
  install -D -m 755 multipathd.rc "$pkgdir"/etc/rc.d/multipathd
}
