# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Roberto Carvajal <roberto@archlinux.org>

pkgname=multitail
pkgver=5.2.13
pkgrel=1
pkgdesc="Lets you view one or multiple files like the original tail program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.vanheusden.com/multitail"
depends=('ncurses')
backup=('etc/multitail.conf')
source=("${url}/${pkgname}-${pkgver}.tgz")
md5sums=('71a774b6d6bb430672c8a9252baac6e6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D -m 755 multitail      "$pkgdir/usr/bin/multitail"
  install -D -m 644 multitail.1    "$pkgdir/usr/share/man/man1/multitail.1"
  install -D -m 644 multitail.conf "$pkgdir/etc/multitail.conf"
}
# vim: ts=2 sw=2 et ft=sh
