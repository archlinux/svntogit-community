# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Roberto Carvajal <roberto@archlinux.org>

pkgname=multitail
pkgver=6.5.1
pkgrel=1
pkgdesc="Lets you view one or multiple files like the original tail program"
arch=('x86_64')
license=('GPL')
url="https://www.vanheusden.com/multitail"
depends=('ncurses')
backup=('etc/multitail.conf')
#source=("https://www.vanheusden.com/multitail/${pkgname}-${pkgver}.tgz")
source=(https://github.com/halturin/multitail/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/halturin/multitail/commit/d7d10f3b.patch)
md5sums=('b4538d88508c6f7774b06d15bec8fcb6'
         '248d9464c6cad37c7b563592881b902e')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 < ../d7d10f3b.patch # Fix build with -Werror=format-security
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|ncursesw/panel.h|panel.h|' mt.h
  sed -i 's|ncursesw/ncurses.h|ncurses.h|' mt.h
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D -m 755 multitail      "$pkgdir/usr/bin/multitail"
  install -D -m 644 multitail.1    "$pkgdir/usr/share/man/man1/multitail.1"
  install -D -m 644 multitail.conf "$pkgdir/etc/multitail.conf"
}
# vim: ts=2 sw=2 et ft=sh
