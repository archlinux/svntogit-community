# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=muon
pkgver=5.3.1
pkgrel=1
pkgdesc='A collection of package management tools for KDE'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/muon'
license=(LGPL)
depends=(packagekit-qt5 plasma-framework knewstuff appstream-qt archlinux-appstream-data hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('056efb8d201323d5a56a7628f9d456ff')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../muon-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
