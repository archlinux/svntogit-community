# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=muon
pkgver=5.4.3
pkgrel=2
pkgdesc='A collection of package management tools for KDE'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/muon'
license=(LGPL)
depends=(packagekit-qt5 knewstuff kdeclarative appstream-qt archlinux-appstream-data hicolor-icon-theme)
makedepends=(extra-cmake-modules python plasma-framework)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('20cce4f714d1341049755f04034e577b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../muon-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
