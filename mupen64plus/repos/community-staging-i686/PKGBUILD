# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Zephyr

pkgname=mupen64plus
pkgver=2.0
pkgrel=2
pkgdesc='Nintendo64 Emulator'
arch=('i686' 'x86_64')
url='http://code.google.com/p/mupen64plus/'
license=('GPL')
depends=('glu' 'libsamplerate' 'libpng' 'sdl' 'freetype2' 'boost-libs')
makedepends=('mesa' 'boost')
source=("http://mupen64plus.googlecode.com/files/mupen64plus-bundle-src-${pkgver}.tar.gz")
md5sums=('f855b4233faa9616b780c364fc4a9a9a')

build() {
  cd mupen64plus-bundle-src-${pkgver}

  sh m64p_build.sh
}

package() {
  cd mupen64plus-bundle-src-$pkgver
  
  # set LDCONFIG since we are using fakeroot and scripts run root commands by checking the uid
  ./m64p_install.sh DESTDIR="$pkgdir" PREFIX='/usr' MANDIR='/usr/share/man' LDCONFIG='true'
}

# vim: ts=2:sw=2:et
