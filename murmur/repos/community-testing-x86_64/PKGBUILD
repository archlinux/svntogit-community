# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Otto Allmendinger <otto.allmendinger@googlemail.com>
# Contributor: Malte Rabenseifner <malte@zearan.de>

_name=mumble
pkgname=murmur
pkgver=1.3.0
pkgrel=4
pkgdesc="The voice chat application server for Mumble"
arch=('x86_64')
url="https://wiki.mumble.info/wiki/Main_Page"
license=('BSD')
depends=('avahi' 'gcc-libs' 'glibc' 'grpc' 'libcap' 'libprotobuf.so'
'lsb-release' 'openssl' 'qt5-base' 'zeroc-ice')
makedepends=('boost' 'python' 'qt5-tools')
backup=("etc/murmur.ini")
install="murmur.install"
source=("https://github.com/mumble-voip/${_name}/releases/download/${pkgver}/${_name}-${pkgver}.tar.gz"{,.sig}
        "murmur.dbus.conf"
        "murmur.service"
        "murmur.sysusers"
        "murmur.tmpfiles")
sha512sums=('2a629fc97f3c7c587c9a3b40fc96cf15d668acada37282ec1c4a5b169ad37717d60af94d12c7bce45f2816c265f76a99ebad40a006adcf8ca38a117e7c0a4122'
            'SKIP'
            '97c7effdddec324e40195c36ef4927950a5de26d2ee2d268d89df6fb547207bbbe30292773316cae6f57ec9923244f205fb0edc377b798771ba7385e3c11d86a'
            '1773802b938ae2a80fa21e26cd33d162f00de00e074af9f1481f1c682ef32756ffdd2384a1d1f5a4202df55a982a80067efa78d7ad4e077bf5c7f7ccfc4399e4'
            '5af28d0c2b2b072cfbd500b5f63549e88a86cf3fc15e4d2df89e787c4d2bafdecbe078a518e0d1b25d82f9873cb06838ec1c9ebed625ffb7e8c80fcd942ebf74'
            '411784e8e0dcf6c163780ae895ae1a6bdad0bb2dd2b128911c484ac3eff073d95c5791b625493a2b8296d24bd7e6ac72d3c42180817e48b29f0c6a8fd841807c')
validpgpkeys=('56D0B23AE00B1EE9A8BAAC0F5B8CF87BB893449B') # Mumble Automatic Build Infrastructure 2019 <mumble-auto-build-2019@mumble.info>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # setting default configuration
  sed -e "1i; vi:ft=cfg" \
      -e "s|database=|database=/var/db/murmur/murmur.sqlite|" \
      -e "s|;logfile=murmur.log|logfile=|" \
      -e "s|;uname=|uname=murmur|" \
      -i scripts/murmur.ini
}

build() {
  cd "${pkgname}-${pkgver}"

  qmake-qt5 main.pro CONFIG+="no-client grpc"
  make release
}

package() {
  cd "${pkgname}-${pkgver}"
  # murmur has no install target: https://github.com/mumble-voip/mumble/issues/1029
  install -vDm 755 release/murmurd -t "${pkgdir}"/usr/bin
  install -vDm 640 scripts/murmur.ini -t "${pkgdir}"/etc
  install -vDm 644 "${srcdir}"/murmur.dbus.conf "${pkgdir}"/usr/share/dbus-1/system.d/murmur.conf
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 man/murmur*.1 -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "${srcdir}"/murmur.service -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 "${srcdir}"/murmur.sysusers "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "${srcdir}"/murmur.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -vDm 644 src/murmur/{Murmur.ice,MurmurRPC.proto} -t "${pkgdir}/usr/share/${pkgname}"
}
# vim: sw=2:ts=2 et:
