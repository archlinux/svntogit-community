# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Otto Allmendinger <otto.allmendinger@googlemail.com>
# Contributor: Malte Rabenseifner <malte@zearan.de>

pkgname=murmur
pkgver=1.3.0
_pkgver=1.3.0
__pkgver=1.3.0
pkgrel=3
pkgdesc="The voice chat application server for Mumble"
arch=('x86_64')
url="http://mumble.sourceforge.net"
license=('BSD')
depends=(qt5-base avahi lsb-release protobuf icu zeroc-ice grpc)
makedepends=(boost python qt5-tools)
backup=("etc/murmur.ini")
install="murmur.install"
source=(https://github.com/mumble-voip/mumble/releases/download/${_pkgver}/mumble-${_pkgver}.tar.gz{,.sig}
        "murmur.dbus.conf"
        "murmur.service"
        "murmur.sysusers")
sha512sums=('2a629fc97f3c7c587c9a3b40fc96cf15d668acada37282ec1c4a5b169ad37717d60af94d12c7bce45f2816c265f76a99ebad40a006adcf8ca38a117e7c0a4122'
            'SKIP'
            '97c7effdddec324e40195c36ef4927950a5de26d2ee2d268d89df6fb547207bbbe30292773316cae6f57ec9923244f205fb0edc377b798771ba7385e3c11d86a'
            '1773802b938ae2a80fa21e26cd33d162f00de00e074af9f1481f1c682ef32756ffdd2384a1d1f5a4202df55a982a80067efa78d7ad4e077bf5c7f7ccfc4399e4'
            '5af28d0c2b2b072cfbd500b5f63549e88a86cf3fc15e4d2df89e787c4d2bafdecbe078a518e0d1b25d82f9873cb06838ec1c9ebed625ffb7e8c80fcd942ebf74')
validpgpkeys=('56D0B23AE00B1EE9A8BAAC0F5B8CF87BB893449B') # Mumble Automatic Build Infrastructure 2019 <mumble-auto-build-2019@mumble.info>

build() {
  cd "${srcdir}"/mumble-$__pkgver

  qmake-qt5 main.pro CONFIG+="no-client grpc"
  make release
}

package() {
  cd "${srcdir}"/mumble-$__pkgver

  sed -e "1i; vi:ft=cfg" \
    -e "s|database=|database=/var/db/murmur/murmur.sqlite|" \
    -e "s|;logfile=murmur.log|logfile=|" \
    -e "s|;uname=|uname=murmur|" \
    -i scripts/murmur.ini

  install -dm755 -o 122 -g 122 "${pkgdir}"/var/db/murmur
  install -Dm755 release/murmurd "${pkgdir}"/usr/bin/murmurd
  install -Dm644 scripts/murmur.ini "${pkgdir}"/etc/murmur.ini
  install -Dm644 "${srcdir}"/murmur.dbus.conf "${pkgdir}"/usr/share/dbus-1/system.d/murmur.conf
  install -Dm644 README "${pkgdir}"/usr/share/doc/murmur/README
  install -Dm644 man/murmurd.1 "${pkgdir}"/usr/share/man/man1/murmurd.1
  install -Dm644 "${srcdir}"/murmur.service "${pkgdir}"/usr/lib/systemd/system/murmur.service
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 "${srcdir}"/murmur.sysusers "${pkgdir}"/usr/lib/sysusers.d/$pkgname.conf

  install -Dm644 src/murmur/Murmur.ice ${pkgdir}/usr/share/murmur/Murmur.ice
  install -Dm644 src/murmur/MurmurRPC.proto ${pkgdir}/usr/share/murmur/MurmurRPC.proto
}

# vim: sw=2:ts=2 et:
