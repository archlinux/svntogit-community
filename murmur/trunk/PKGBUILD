# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Otto Allmendinger <otto.allmendinger@googlemail.com>
# Contributor: Malte Rabenseifner <malte@zearan.de> 

pkgname=murmur
pkgver=1.2.11
pkgrel=1
pkgdesc="The voice chat application server for Mumble"
arch=('i686' 'x86_64')
url="http://mumble.sourceforge.net"
license=('BSD')
depends=('avahi' 'lsb-release' 'protobuf' 'qt4' 'icu')
makedepends=('boost')
backup=("etc/murmur.ini")
install="murmur.install"
source=(https://github.com/mumble-voip/mumble/releases/download/${pkgver}/mumble-${pkgver}.tar.gz{,.sig}
        "murmur.dbus.conf"
        "murmur.service")
md5sums=('bd1f15c6c8c8950e1d02452689759d38'
         'SKIP'
         'eddea4cdbd0bde4b960a67e16b5d5478'
         'd27a9adcd7561859e7b033046729bc0e')
validpgpkeys=(710E91FDF4284F2E69E0DEBDD7446C0F751B4695)

build() {
  cd ${srcdir}/mumble-$pkgver

  qmake-qt4 main.pro CONFIG+="no-client no-ice"
  make release
}

package() {
  cd ${srcdir}/mumble-$pkgver

  sed -e "1i# vi:ft=cfg" \
    -e "s|database=|database=/var/lib/murmur/murmur.sqlite|" \
    -e "s|#logfile=murmur.log|logfile=|" \
    -e "s|#uname=|uname=murmur|" \
    -i scripts/murmur.ini

  install -dm755 -o 122 -g 122 ${pkgdir}/var/lib/murmur
  install -Dm755 release/murmurd ${pkgdir}/usr/bin/murmurd
  install -Dm644 scripts/murmur.ini ${pkgdir}/etc/murmur.ini
  install -Dm644 ${srcdir}/murmur.dbus.conf ${pkgdir}/etc/dbus-1/system.d/murmur.conf
  install -Dm644 README ${pkgdir}/usr/share/doc/murmur/README
  install -Dm644 man/murmurd.1 ${pkgdir}/usr/share/man/man1/murmurd.1
  install -Dm644 ${srcdir}/murmur.service ${pkgdir}/usr/lib/systemd/system/murmur.service
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim: sw=2:ts=2 et:
