# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Otto Allmendinger <otto.allmendinger@googlemail.com>
# Contributor: Malte Rabenseifner <malte@zearan.de> 

pkgname=murmur
pkgver=1.2.3
pkgrel=10
pkgdesc="The voice chat application server for Mumble"
arch=('i686' 'x86_64')
url="http://mumble.sourceforge.net"
license=('GPL')
depends=('avahi' 'lsb-release' 'protobuf' 'qt4' 'icu')
makedepends=('boost')
backup=("etc/murmur.ini"
        "etc/logrotate.d/murmur"
        "etc/conf.d/murmur" )
install="murmur.install"
source=("http://downloads.sourceforge.net/mumble/mumble-$pkgver.tar.gz"
        "murmur.rc.d"
        "murmur.conf.d"
        "murmur.dbus.conf"
        "murmur.logrotate.d"
        "murmur.service"
        "murmur.conf")
md5sums=('55804e3dfe98b16eeb40abca6c254625'
         'a4f6fc515f258eb4db437a226e16de5d'
         '11c1cc875e8021739ec2087252cf0533'
         'eddea4cdbd0bde4b960a67e16b5d5478'
         'c56726cb267ead68f37f35c28e36e6fa'
         '5ad8798aea4f8b70f47f16df64f755b3'
         'e00d0d2610b77efebce5b3556609251f')

build() {
  cd ${srcdir}/mumble-$pkgver

  qmake main.pro CONFIG+="no-client no-ice"
  make release
}

package() {
  cd ${srcdir}/mumble-$pkgver

  sed -e "1i# vi:ft=cfg" \
    -e "s|database=|database=/var/lib/murmur/murmur.sqlite|" \
    -e "s|dbus=session|#dbus=system|" \
    -e "s|#logfile=murmur.log|logfile=/var/log/murmur/murmur.log|" \
    -e "s|#pidfile=|pidfile=/var/run/murmur/murmur.pid|" \
    -e "s|#uname=|uname=murmur|" \
    -i scripts/murmur.ini

  install -dm755 ${pkgdir}/var/lib/murmur
  install -dm755 ${pkgdir}/var/log/murmur
  install -Dm755 release/murmurd ${pkgdir}/usr/sbin/murmurd
  install -Dm644 scripts/murmur.ini ${pkgdir}/etc/murmur.ini
  install -Dm755 ${srcdir}/murmur.rc.d ${pkgdir}/etc/rc.d/murmur
  install -Dm644 ${srcdir}/murmur.conf.d ${pkgdir}/etc/conf.d/murmur
  install -Dm644 ${srcdir}/murmur.dbus.conf ${pkgdir}/etc/dbus-1/system.d/murmur.conf
  install -Dm644 ${srcdir}/murmur.logrotate.d ${pkgdir}/etc/logrotate.d/murmur
  install -Dm644 README ${pkgdir}/usr/share/doc/murmur/README
  install -Dm644 man/murmurd.1 ${pkgdir}/usr/share/man/man1/murmurd.1
  install -Dm644 ${srcdir}/murmur.conf ${pkgdir}/usr/lib/tmpfiles.d/murmur.conf
  install -Dm644 ${srcdir}/murmur.service ${pkgdir}/usr/lib/systemd/system/murmur.service
}

# vim: sw=2:ts=2 et:
