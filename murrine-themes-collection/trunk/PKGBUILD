# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=murrine-themes-collection
pkgver=20100910
pkgrel=1
pkgdesc="A collection of Murrine GTK2 themes"
license=('GPL')
arch=('any')
url="http://www.cimitan.com/murrine/themes/gallery"
depends=('gtk-engine-murrine')
source=(http://sites.google.com/site/jovialubuntu/Home/Alliance-theme/Alliance-theme-v0.92.tar.gz \
	http://www.gnome-look.org/CONTENT/content-files/94653-Black-Diamond.tar.gz \
	http://www.gnome-look.org/CONTENT/content-files/81401-Blue-Crystal-0.9.2.tar.gz \
	http://www.cimitan.com/murrine/files/Caramello_04.tar.bz2 \
	http://www.gnome-look.org/CONTENT/content-files/93558-Murreza.tar.bz2 \
	http://www.cimitan.com/murrine/files/Kiwi.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaAquaIsh.tar.bz2 \
        http://www.cimitan.com/murrine/files/MurrinaBlu-0.32.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaBlue.tar.bz2 \
        http://www.cimitan.com/murrine/files/MurrinaCandido.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaChrome.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaCream.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaElement.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaFancyCandy.tar.bz2 \
        http://www.cimitan.com/murrine/files/MurrinaGilouche.tar.bz2 \
        http://www.cimitan.com/murrine/files/Murrine-Gray.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaLoveGray.tar.bz2 \
        http://www.cimitan.com/murrine/files/MurrinaOranche_02.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaOransun.tar.gz \
        http://www.cimitan.com/murrine/files/MurrinaTango.tar.bz2 \
        http://www.cimitan.com/murrine/files/MurrinaVerdeOlivo.tar.bz2 \
	http://www.cimitan.com/murrine/files/Murrine-Gray.tar_0.gz \
	http://www.cimitan.com/murrine/files/Murrine-Light.tar.gz \
	http://www.cimitan.com/murrine/files/Murrine-Sky.tar.gz \
	http://www.cimitan.com/murrine/files/MurrineBeige.tar.gz \
        http://www.cimitan.com/murrine/files/MurrineRounded.tar.bz2 \
	http://www.cimitan.com/murrine/files/NOX-svn-r22.tar.gz \
	http://www.cimitan.com/murrine/files/Orangine.tar.gz)
md5sums=('86cb2a3e398aa14e0c1007d13efba653'
         'f04a51651b07c1ce6e3097e8786d9ee5'
         'ec90e4e3f55a620489ccb5aa0226b87f'
         '5dbc0178ddaed12b7544f44ee417242f'
         '2c749b6c112e825a52775cabfe3ff784'
         '7f6fb102d77c2e6247cf09e30ee29257'
         'e18bb191756ed1b793432f6bc7607db4'
         '9f7ddce9b35bbec8bc258600fecff6e0'
         'c42534308c8d45a3c953571ea94f50ac'
         '3fe082faed0389c5ce186282a99659b4'
         '4c4a9d72172ef67e6004a608878c9ee4'
         'b3993c593f26a54f2259bbf8c42e54b6'
         '03349c71b1e0f2b99df91bb75bce98c6'
         '5a66f3de41547a0a27f925ac8d8d8c46'
         '65d8f1f28f9c8ed14b15ec894dd0bfc0'
         '0a1f0b1dab626d281d808b927003523c'
         '31ce9fce5114c1cfe471dc614de539cc'
         'f4e7a49a5a2cd14378b071fc6c448a4d'
         '59721e24d5b56008ee8ad31004568de8'
         '43a6848cc8a9eededffb516cfb02b635'
         '7fb55d613c31a1455a9db6c121fff0bb'
         '6782b5429767a90a9e84825538a61f6a'
         '4428ad427be93727da949d2f5aa908cb'
         '9b5e10156d2334b4466e6111999f3857'
         '87f633fedde0c9c50ac1521a59ee32e6'
         '1529a04eb9bea0a17eee76405d32e947'
         'ba12603b8b9c228bd4833fe5ff36eb9b'
         '8135b5b738f8cbfc76f882d537a79d3a')

package() {
  cd ${srcdir}
  install -d ${pkgdir}/usr/share/themes

  for i in `ls`; do
    if [ -d "$i" ]; then
      cp -rf ${i} ${pkgdir}/usr/share/themes
    fi
  done
  
  # Remove Icon themes
  rm -rf ${pkgdir}/usr/share/themes/MurrineRounded{Icon,LessFramed{,Icon}}

  # Install Murreza themes
  mv ${pkgdir}/usr/share/themes/Murreza/Murreza{Silver,Warm} \
    ${pkgdir}/usr/share/themes/
  rm -rf ${pkgdir}/usr/share/themes/Murreza

  # Useless files
  rm -rf ${pkgdir}/usr/share/themes/fbpanel-config

  find ${pkgdir}/usr/share/themes/ -name "gtkrc" -print |
    while read filename
    do (
      sed -i 's|highlight_ratio|highlight_shade|g' $filename
      sed -i 's|hilight_ratio|highlight_shade|g' $filename
      sed -i 's|lightborder_ratio|lightborder_shade|g' $filename
    )
    done

  find ${pkgdir}/usr/share/themes/ ! -type d -exec chmod 644 {} \;
}
