# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=muse
pkgver=4.0.0
pkgrel=3
pkgdesc="A digital audio workstation with support for both Audio and MIDI"
arch=('x86_64')
url="https://github.com/muse-sequencer/muse"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'gtkmm' 'python-pyqt5' 'python-pyro' 'qt5-base' 'qt5-svg')
makedepends=('alsa-lib' 'cmake' 'dssi' 'fluidsynth' 'glib2' 'jack' 'ladspa'
'liblo' 'liblrdf' 'libsamplerate' 'libsndfile' 'lilv' 'lv2' 'qt5-tools'
'rtaudio' 'rubberband' 'serd')
provides=('dssi-host' 'ladspa-host' 'lv2-host' 'vst-host')
source=("$pkgname-$pkgver.tar.gz::https://github.com/muse-sequencer/muse/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('316d8afaa4412c1de3febd06f517245fcc0f380b78efab8a59041653c70828496e95f4ebd2e06ea080fb494c69ab73457e77264166be50b85abf093835d6c2d4')
b2sums=('7b9fd19d5f9a5bf776d00f3c4695b7fb214de4e5be629c05edb82daa0113784b7cd42e2204ebca83fd7f6b82801a0bb9b815cc5618fa48d8d512a30af3e9b09f')

prepare() {
  cd "$pkgname-$pkgver"
  # rename MusE-INSTALL_NAME to muse, because this reflects the package name
  sed -e 's/muse-4.0/muse/g' -i "src/CMakeLists.txt"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -D CMAKE_INSTALL_PREFIX=/usr \
        -D CMAKE_BUILD_TYPE='None' \
        -D ENABLE_PYTHON=ON \
        -D ENABLE_LV2_GTK2=OFF \
        -Wno-dev \
        -B build \
        -S src
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libfluidsynth.so' 'libglib-2.0.so'
  'libgobject-2.0.so' 'libinstpatch-1.0.so' 'libjack.so' 'liblilv-0.so'
  'liblo.so' 'liblrdf.so' 'librtaudio.so' 'librubberband.so' 'libsamplerate.so'
  'libsndfile.so' 'libserd-0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install -C build
}
