# Contributor: Cristian Maureira <saint [at] archlinux.cl>
# Previous Contributor: Dr.Egg <rwhite @ archlinux . us>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de

pkgname=musescore
pkgver=1.1
pkgrel=2
pkgdesc="A music score editor written in Qt4"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('qt>=4.5.0' 'libsndfile' 'qtscriptgenerator')
makedepends=('cmake' 'doxygen')
optdepends=('portaudio' 'jack')
source=(http://sourceforge.net/projects/mscore/files/mscore/MuseScore-1.1/mscore-1.1.tar.bz2 'paths.patch')
md5sums=('68b43af92093a16f7f074c0eb560a867'
         'e5011154d217f633f21b4bb70e589510')
install=musescore.install

build() {
  cd $srcdir/mscore-${pkgver}
  export QT_PLUGINS_DIR=/usr/lib/qt/plugins
  [ -d build ] && make clean
  patch -p1 < $srcdir/paths.patch
  sed -i 's+${Mscore_INSTALL_NAME}man+doc/mscore+' \
    "$srcdir"/mscore-$pkgver/mscore/manual/CMakeLists.txt
  sed -i 's+BUILD_SCRIPTGEN TRUE+BUILD_SCRIPTGEN FALSE+' \
    "$srcdir"/mscore-$pkgver/mscore/CMakeLists.txt
  sed -i 's+static_cast<const+static_cast<+' \
    $srcdir/mscore-$pkgver/mscore/mscore/exportxml.cpp
  make PREFIX=/usr release 
}
package() {
  cd $srcdir/mscore-${pkgver}
  make DESTDIR="$pkgdir" install
}
