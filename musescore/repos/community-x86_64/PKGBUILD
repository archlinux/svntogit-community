# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

pkgbase=musescore
pkgname=('musescore' 'musescore-docs')
pkgver=1.3
pkgrel=6
pkgdesc="Create, play and print beautiful sheet music"
arch=('i686' 'x86_64')
url="http://musescore.org/"
license=('GPL')
makedepends=('cmake' 'doxygen' 'portaudio' 'qtscriptgenerator' 'texlive-core')
source=("http://downloads.sourceforge.net/mscore/mscore-${pkgver}.tar.bz2"
        'mscore-paths.patch')
sha256sums=('a0b60cc892ac0266c58fc6392be72c0a21c3aa7fd0b6e4f1dddad1c8b36be683'
            '00984b9a2bb020b4f907cee7e6f95a8f2f0318613acfe79ad6ad9732e85b5150')

prepare() {
  cd mscore-${pkgver}/mscore

  patch -Np2 -i ../../mscore-paths.patch
}

build() {
  cd mscore-${pkgver}/mscore

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DBUILD_SCRIPTGEN='FALSE'
  make lupdate
  make lrelease
  make
}

package_musescore() {
  depends=('portaudio' 'qtscriptgenerator')
  optdepends=('musescore-docs: Documentation')
  install="${pkgname}.install"

  cd mscore-${pkgver}/mscore/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/mscore-${pkgver}/man
}

package_musescore-docs() {
  pkgdesc="Documentation for MuseScore"

  cd mscore-${pkgver}/mscore

  install -dm 755 "${pkgdir}"/usr/share/doc/${pkgbase}
  install -m 644 manual/*.pdf "${pkgdir}"/usr/share/doc/${pkgbase}/
}

# vim: ts=2 sw=2 et:
