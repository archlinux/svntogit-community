# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

pkgname=musescore
pkgver=3.6.2
pkgrel=1
pkgdesc='Create, play and print beautiful sheet music'
arch=(x86_64)
url=https://musescore.org/
license=(GPL)
groups=(pro-audio)
depends=(
  alsa-lib
  freetype2
  libpulse
  libsndfile
  libvorbisfile.so
  libportaudio.so
  libportmidi.so
  qt5-base
  qt5-declarative
  qt5-graphicaleffects
  qt5-quickcontrols
  qt5-quickcontrols2
  qt5-svg
  qt5-tools
  qt5-webengine
  qt5-xmlpatterns
  zlib
)
makedepends=(
  cmake
  doxygen
  git
  lame
  qt5-script
  texlive-core
)
optdepends=('lame: MP3 export')
_tag=3224f342d12f4af8ea782e929c49f5ce85f97da6
source=(git+https://github.com/musescore/MuseScore.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd MuseScore
  git cherry-pick -n 63d8b7ee90e9ec6604e334a3ebb12fced7fab36d
  sed 's/option(BUILD_CRASH_REPORTER "Build with crash reporter" ON)/option(BUILD_CRASH_REPORTER "Build with crash reporter" OFF)/' -i CMakeLists.txt
}

pkgver() {
  cd MuseScore
  git describe --tags | sed 's/^v//'
}

build() {
  make PREFIX=/usr NO_RPATH=TRUE USE_SYSTEM_FREETYPE=ON MUSESCORE_BUILD_CONFIG=release -C MuseScore revision release
}

package() {
  make DESTDIR="${pkgdir}" UPDATE_CACHE=FALSE -C MuseScore/build.release install
}

# vim: ts=2 sw=2 et:
