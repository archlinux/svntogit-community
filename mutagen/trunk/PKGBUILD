# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('python-mutagen' 'python2-mutagen')
pkgver=1.43.0
pkgrel=1
arch=('any')
pkgdesc="An audio metadata tag reader and writer (python library)"
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python-hypothesis' 'python-pyflakes'
'python-pycodestyle' 'python2-pytest' 'python2-hypothesis' 'python2-pyflakes'
'python2-pycodestyle')
source=("https://github.com/quodlibet/${pkgbase}/releases/download/release-$pkgver/$pkgbase-$pkgver.tar.gz"{,.sig})
sha256sums=('3a982d39f1b800520a32afdebe3543f972e83a6ddd0c0198739a161ee705b588'
            'SKIP')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC') # Christoph Reiter <reiter.christoph@gmail.com>

prepare() {
  # copying source for python2 version, so we can modify the man/ script names
  cp -av "${pkgbase}-$pkgver" "${pkgname[1]}-${pkgver}"
  cd "${pkgname[1]}-$pkgver"
  for script in {mid3{cp,iconv,v2},moggsplit,mutagen-{inspect,pony}}; do
    sed -e "s/${script}/${script}2/g" -i setup.py
    mv -v "man/${script}"{,2}.1
  done
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
  cd "../${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  python setup.py test
  cd "../${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer (python library)"
  depends=('python-setuptools')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {NEWS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-mutagen() {
  pkgdesc="An audio metadata tag reader and writer (python2 library)"
  depends=('python2-setuptools')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
                           --optimize=1 \
                           --prefix=/usr \
                           --root="${pkgdir}"
  install -vDm 644 {NEWS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

