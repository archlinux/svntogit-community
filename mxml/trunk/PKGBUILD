# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=mxml
pkgver=3.3
pkgrel=1
pkgdesc="A small XML parsing library"
arch=('x86_64')
url="https://www.msweet.org/mxml"
license=('LGPL')
depends=('glibc')
provides=('libmxml.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/michaelrsweet/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('5a8453bec17e46afc9b100df03dba2219353a076152f34a5dc41b7f042fa570723effc3472680a4380674c412c3ab31e017f6704b1e8308227a4d9b3f13cb197')
b2sums=('2b815cd8c2031099e2767caad93cc389962ec547fadade92b0a7fb48a2352fd0940c9e434c2081b7419e0cfb489266d7116dacefd6fb9d7ce40d6de0f49cb2a6')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-shared
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DSTROOT="${pkgdir}" install
}
