# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrew Belitsky <belitsky.a@gmail.com>
pkgbase=mygui
pkgname=('mygui' 'mygui-docs')
pkgver=3.2.0
pkgrel=9
pkgdesc="A multilayer and overlappable GUI System for OGRE"
arch=('i686' 'x86_64')
url="http://mygui.info/"
license=('LGPL')
depends=('boost-libs' 'ogre' 'ois')
makedepends=('boost' 'cmake' 'doxygen' 'graphviz' 'ttf-dejavu')
install=mygui.install
source=("http://downloads.sourceforge.net/my-gui/MyGUI_$pkgver.zip")
md5sums=('7c20486a0db82f497d8b7c5300613b61')

build() {
  cd $srcdir/MyGUI_$pkgver

  # change CMake config so demos install to /opt
  sed -i 's:"bin:"../opt/MYGUI:' CMake/Utils/MyGUIConfigTargets.cmake
  sed -i -e 's:../share:/usr/share:' \
         -e 's:"bin":"../opt/MYGUI":' \
         CMake/InstallResources.cmake
  sed -i '71 i set(MYGUI_GCC_VISIBILITY_FLAGS "")' CMakeLists.txt
  sed -i 's/\${OIS_LIBRARIES}/${OIS_LIBRARIES} boost_system/g' Common/CMakeLists.txt

  # get a clean build dir
  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  # generate CMake Makefile
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYGUI_INSTALL_SAMPLES=TRUE \
    -DMYGUI_INSTALL_TOOLS=TRUE \
    -DMYGUI_INSTALL_DOCS=TRUE \
    -DMYGUI_INSTALL_MEDIA=TRUE \
    -DFREETYPE_INCLUDE_DIR=/usr/include/freetype2/ \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=TRUE

  make
  make api-docs
}

package_mygui() {
  optdepends=('mygui-docs: documentation')

  cd $srcdir/MyGUI_$pkgver/build
  
  make DESTDIR="$pkgdir" install

  # make demos work
  chown root:users $pkgdir/opt/MYGUI
  chmod 775 $pkgdir/opt/MYGUI
}

package_mygui-docs() {
  pkgdesc="Documentation for mygui"
  depends=()

  cd $srcdir/MyGUI_$pkgver/build
  
  # install docs
  install -d $pkgdir/usr/share/doc/
  cp -r Docs/html $pkgdir/usr/share/doc/MYGUI
}

# vim:set ts=2 sw=2 et:
