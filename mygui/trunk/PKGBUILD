# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrew Belitsky <belitsky.a@gmail.com>
pkgbase=mygui
pkgname=('mygui' 'mygui-docs')
pkgver=3.2.0rc1
pkgrel=10
pkgdesc="A multilayer and overlappable GUI System for OGRE"
arch=('i686' 'x86_64')
url="http://mygui.info/"
license=('LGPL')
depends=('boost-libs' 'ogre' 'ois')
makedepends=('boost' 'cmake' 'doxygen' 'graphviz' 'ttf-dejavu')
install=mygui.install
source=("http://downloads.sourceforge.net/my-gui/MyGUI_3.2.0_RC1.zip")
md5sums=('1ac1340148e81ad49a26c8a87acefc6b')

build() {
  cd $srcdir/MyGUI_3.2.0_RC1

  # change CMake config so demos install to /opt
  sed -i 's:"bin:"../opt/MYGUI:' CMake/Utils/MyGUIConfigTargets.cmake
  sed -i -e 's:../share:/usr/share:' \
         -e 's:"bin":"../opt/MYGUI":' \
         CMake/InstallResources.cmake

  # get a clean build dir
  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  # generate CMake Makefile
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYGUI_INSTALL_SAMPLES=TRUE \
    -DMYGUI_INSTALL_TOOLS=TRUE \
    -DMYGUI_INSTALL_DOCS=TRUE \
    -DMYGUI_INSTALL_MEDIA=TRUE \
    -DCMAKE_BUILD_TYPE=Release # set=Debug for debugging version

  # compile
  make

  # generate docs
  if [[ $(which dot) && $(which doxygen) ]]; then
    make api-docs 2>/dev/null
  fi
}

package_mygui() {
  optdepends=('mygui-docs: documentation')

  cd $srcdir/MyGUI_3.2.0_RC1
  
  cd build

  # install the whole bunch
  make DESTDIR="$pkgdir" install

  # make demos work
  chown root:users $pkgdir/opt/MYGUI
  chmod 775 $pkgdir/opt/MYGUI
}

package_mygui-docs() {
  pkgdesc="Documentation for mygui"
  depends=()

  cd $srcdir/MyGUI_3.2.0_RC1
  
  cd build

  # install docs
  install -d $pkgdir/usr/share/doc/
  cp -r Docs/html $pkgdir/usr/share/doc/MYGUI
}

# vim:set ts=2 sw=2 et:
