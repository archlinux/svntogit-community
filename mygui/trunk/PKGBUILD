# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrew Belitsky <belitsky.a@gmail.com>
pkgname=mygui
replaces=('mygui-docs')
pkgver=3.4.0
pkgrel=1
pkgdesc="A multilayer and overlappable GUI System"
arch=('x86_64')
url="http://mygui.info/"
license=('LGPL')
depends=('boost-libs' 'sdl2' 'sdl2_image' 'glu')
makedepends=('boost' 'cmake' 'doxygen' 'graphviz' 'ttf-dejavu')
install=mygui.install
source=("https://github.com/MyGUI/mygui/archive/MyGUI${pkgver}.tar.gz")
sha512sums=('3f560fd9c7e5b514da40e41c7753dc20649f80b763f5ee20afc9627df3be14242ea833eff09eac16b9e88b1c9fbed7c09def0c5068c832e710c247191fb53487')

prepare() {
  mkdir -p mygui-MyGUI${pkgver}/build
}

build() {
  cd mygui-MyGUI${pkgver}/build

  # change CMake config so demos install to /opt
  sed -i 's:"bin:"../opt/MYGUI:' ../CMake/Utils/MyGUIConfigTargets.cmake
  sed -i -e 's:../share:/usr/share:' \
         -e 's:"bin":"../opt/MYGUI":' \
         ../CMake/InstallResources.cmake

  # generate CMake Makefile
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYGUI_INSTALL_SAMPLES=TRUE \
    -DMYGUI_INSTALL_TOOLS=TRUE \
    -DMYGUI_INSTALL_DOCS=TRUE \
    -DMYGUI_INSTALL_MEDIA=TRUE \
    -DMYGUI_BUILD_DEMOS=TRUE \
    -DMYGUI_BUILD_TOOLS=TRUE \
    -DCMAKE_BUILD_TYPE=Release \
    -DMYGUI_RENDERSYSTEM=7 \
    -DBUILD_SHARED_LIBS=TRUE

  make
  make api-docs
}

package() {
  cd mygui-MyGUI${pkgver}/build

  make DESTDIR="$pkgdir" install

  # make demos work
  # install -Dm775 lib/libCommon.so ${pkgdir}/usr/lib/libCommon.so
  # chown root:users $pkgdir/opt/MYGUI
  # chmod 775 $pkgdir/opt/MYGUI

  # install docs
  install -d "${pkgdir}"/usr/share/doc/
  cp -r Docs/html "${pkgdir}"/usr/share/doc/MYGUI
}

# vim:set ts=2 sw=2 et:
