# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=10.1.3
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL3)
depends=(glibc lua)
makedepends=(cmake flac jq libid3tag openssl pcre2 perl)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/jcorporation/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('3e87ad231ab7d6a78cf8bd58232d322653e79401f2b61adc36ccbf1b02eed6aebe7cf7b717c04373ff9cf4e5664bb4b5b6f53a1fe9ca7529011d2b3680f4184c')
b2sums=('ef4e2091c79d75de097ae8e32e644eef4cfb70ef99a404b4343f73360e3f353e5947b3469f4be1332998ecda616127f90710d6e686cf36d5a4a624c4e3bd47f3')

build() {
  local cmake_options=(
    -B build
    -S $_name-$pkgver
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_FLAC=ON
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
