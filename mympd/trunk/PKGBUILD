# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=10.0.3
pkgrel=4
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL3)
depends=(glibc lua53 openssl)
makedepends=(cmake flac jq libid3tag pcre2 perl)
options=(debug)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/jcorporation/$pkgname/archive/refs/tags/v$pkgver.tar.gz
  $pkgname-10.0.3-harden_systemd.patch::https://github.com/jcorporation/myMPD/commit/fcf667cbe9945ac4ec91d04129d30bcb466733be.patch
  $pkgname-10.0.3-full_relro.patch
)
sha512sums=('c4365c091b535fd30914c7ac53d8cdb4edd446c769f11b3279bab7b11f53992278692c40b1ec26c33bba43818d723e0be2a13713b9b880c0a24e903cc13827e1'
            'dccb0819652f763d7a85e95bcca04bd0a11ecff4171c2068c876e2d0aba7d4428d3ede5681a798e7694b307b947c6e2fa102e2a24faff08b9133b47b065d0b3f'
            'c7d50053eeb06ac80c8019c7554ab16b77e7625867f703d9e6342258613516053af1ac14fe1b1f7846fbb06909c3f671c87484b4b128f10b0a9346b60945d488')
b2sums=('5a7fe7adb9ab26c29b223d4adb8b0a82178ade961eb904fd85c2d2150b2383132bfbc0ab678463349f1e8be24673d4ac0edc30d5d69b7351c136d42e64fcb37a'
        '550e369832740ccd8b28a7a3309a70b2ff986feed621445cd008ce8dc9e9a707658baa99135658c2a8f08d3ab61e9dfb2deb0bcbbbf9402754bcfd45baeea5a2'
        'f199bed4ef3baec3b04df1315b0e6a12a6c1c6064b7fc2e1256a2199edbdee34bc43593cec4bb297b8fcf9f2e2a9383b8f29c10674e0e26b412f1383e11f78e4')

prepare() {
  # harden systemd service: https://github.com/jcorporation/myMPD/issues/873
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-10.0.3-harden_systemd.patch
  # do not do a debug build when unsetting CMAKE_BUILD_TYPE: https://github.com/jcorporation/myMPD/issues/872
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-10.0.3-full_relro.patch

  cd $_name-$pkgver
  mkdir -vp build
  ./build.sh createassets
  mv -v release/* build/
}

build() {
  cd $_name-$pkgver
  # NOTE: out-of-tree builds currently not possible: https://github.com/jcorporation/myMPD/issues/871
  cmake \
    -B build \
    -S . \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DEMBEDDED_ASSETS=ON \
    -DENABLE_FLAC=ON \
    -DENABLE_FLAC=ON \
    -DENABLE_IPV6=ON \
    -DENABLE_LIBID3TAG=ON \
    -DENABLE_LUA=ON \
    -DENABLE_SSL=ON \
    -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir $_name-$pkgver/build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install $_name-$pkgver/build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
