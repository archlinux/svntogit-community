# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=myodbc
pkgver=5.1.11
pkgrel=3
pkgdesc="ODBC driver/connector for MySQL"
arch=(i686 x86_64)
url="http://dev.mysql.com/downloads/connector/odbc/"
depends=('unixodbc' 'libmysqlclient')
makedepends=('cmake')
license=('GPL')
options=('libtool')
source=("http://mysql.skynet.be/Downloads/Connector-ODBC/5.1/mysql-connector-odbc-$pkgver-src.tar.gz")
md5sums=('1d868f404038bbce6623a28255c8a9f7')

build() {
  cd $srcdir/mysql-connector-odbc-${pkgver}-src
  unset LDFLAGS
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_UNIXODBC=1 .
  make
}

package() {
  cd $srcdir/mysql-connector-odbc-${pkgver}-src
  make DESTDIR=$pkgdir install
  pushd $pkgdir/usr
  rm -rf test
  install -dm0755 share/mysql-connector-odbc
  mv ChangeLog COPYING INSTALL Licenses_for_Third-Party_Components.txt README README.debug \
	share/mysql-connector-odbc/
  popd
}
