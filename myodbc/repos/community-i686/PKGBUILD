# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=myodbc
pkgver=5.2.6
pkgrel=2
pkgdesc="ODBC driver/connector for mariadb"
arch=(i686 x86_64)
url="http://dev.mysql.com/downloads/connector/odbc/"
depends=('unixodbc' 'libmariadbclient')
makedepends=('cmake')
license=('GPL')
options=('libtool')
source=("http://cdn.mysql.com/Downloads/Connector-ODBC/5.2/mysql-connector-odbc-$pkgver-src.tar.gz")
md5sums=('066bfed7dc472ef4b68bafc95ca45027')

build() {
  cd $srcdir/mysql-connector-odbc-${pkgver}-src
  unset CFLAGS
  export LDFLAGS="-lpthread -ldl"
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYSQLCLIENT_LIB_NAME=libmysqlclient_r.a \
    -DWITH_UNIXODBC=1 .
  make
}

package() {
  cd $srcdir/mysql-connector-odbc-${pkgver}-src
  make DESTDIR=$pkgdir install
  pushd $pkgdir/usr
  rm -rf test
  install -dm0755 share/mysql-connector-odbc
  mv ChangeLog COPYING INSTALL Licenses_for_Third-Party_Components.txt README README.debug \
	share/mysql-connector-odbc/
  popd
}
