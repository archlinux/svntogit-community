# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=mypaint
pkgver=1.1.0
pkgrel=5
pkgdesc="A fast and easy painting application for digital painters, with brush dynamics"
arch=('i686' 'x86_64')
url="http://mypaint.intilinux.com/"
license=('GPL' 'LGPL')
depends=('pygtk' 'python2-numpy' 'protobuf-python' 'python2' 'json-c' 'lcms2')
makedepends=('scons>=0.97' 'swig>=1.3.39')
install=mypaint.install
source=(http://download.gna.org/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('7846a8406259d0fc81c9a2157a2348bf')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  # python2 fix
  sed -i 's_python generate.py_python2 generate.py_' brushlib/SConscript
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
  sed -i "s|'json|'json-c|" brushlib/SConscript
}

build() {
  cd $srcdir/$pkgname-$pkgver
  scons
}

package() {
  cd $srcdir/$pkgname-$pkgver
  scons prefix="$pkgdir"/usr install
}
