# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Alexandre Boily <alexandreboily@gmail.com>
# Contributor: Illarion Kovalchuk <illarion.kovalchuk@gmail.com>
# Contributor: totoloco <totoloco at gmail _dot_com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mysql-workbench
pkgver=6.2.4
pkgrel=8
pkgdesc='A cross-platform, visual database design tool developed by MySQL'
arch=('i686' 'x86_64')
url='https://www.mysql.com/products/workbench/'
license=('GPL2')
depends=('python2' 'libzip' 'libmariadbclient' 'lua51' 'gtkmm' 'ctemplate'
	'libgnome-keyring' 'libgl' 'python2-paramiko' 'python2-pexpect' 'gdal'
	'pcre' 'tinyxml' 'libxml2' 'mysql-python' 'cairo'
	'python2-cairo' 'hicolor-icon-theme' 'desktop-file-utils' 'libiodbc'
	'mysql-connector-c++' 'vsqlite++' 'unixodbc' 'freetype2'
	'libantlr3c')
optdepends=('gnome-keyring: store SSH/MySQL passwords in GNOME password manager'
	'python2-pyodbc: database migration')
makedepends=('cmake' 'boost' 'curl' 'mesa' 'swig2' 'java-runtime')
install=mysql-workbench.install
validpgpkeys=('A4A9406876FCBD3C456770C88C718D3B5072E1F5')
source=("http://cdn.mysql.com/Downloads/MySQLGUITools/mysql-workbench-community-${pkgver}-src.tar.gz"{,.asc}
	'http://www.antlr3.org/download/antlr-3.4-complete.jar'
	'0001-mysql-workbench-no-check-for-updates.patch'
	'0002-mysql-workbench-ctemplate.patch'
	'0003-mysql-workbench-keyring.patch'
	'0004-mysql-workbench-paramiko.patch'
	'0005-mysql-workbench-py-append.patch'
	'0006-mysql-workbench-column-label.patch'
	'arch_linux_profile.xml')
sha256sums=('b3e0775b7725fde3be8a63b6b1fd293a41bb6cbbc9dc459924b774fb4ad1afaa'
            'SKIP'
            '9d3e866b610460664522520f73b81777b5626fb0a282a5952b9800b751550bf7'
            'b189e15c6b6f5a707357d9a9297f39ee3a33264fd28b44d5de6f537f851f82cf'
            'c2e01e57a213383445dbf49222e32f3e35c96f199fb7b62fbecabfa0eb1b8f0b'
            '683a60e2bb38d8687f7e5228b49f62b374bf2c93488bdb89307205dbf5b3c476'
            'b58e605b58a8d30662b444e315a0c5dd84e24e0a7701aa1aaf161e1b60c5790f'
            'be232de2a22245bf350eb17c50292f9869195310c64f0cf41941df179a8ed586'
            '701c7d9f68b1b0f63e8e8ece0b61cb57c2168f29e10b113c409f7ccfd68251e0'
            '28724c4b4cec29ce19aada08279df1b086381cd788fef7ae07c1860f7d17af7e')

prepare() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	# Disable 'Help' -> 'Check for Updates'
	# Updates are provided via Arch Linux packages
	patch -Np1 < "${srcdir}"/0001-mysql-workbench-no-check-for-updates.patch

	# fix deprecated calls to tpl->ReloadIfChanged
	# http://bugs.mysql.com/72585
	patch -Np1 < "${srcdir}"/0002-mysql-workbench-ctemplate.patch

	# fix GNOME keyring >= 3.14
	# http://bugs.mysql.com/74771
	patch -Np1 < "${srcdir}"/0003-mysql-workbench-keyring.patch

	# fix SSH tunnel
	patch -Np1 < "${srcdir}"/0004-mysql-workbench-paramiko.patch

	# fix python syntax error
	patch -Np1 < "${srcdir}"/0005-mysql-workbench-py-append.patch

	# fix schema and table inspector with mysql-connector-c++ >= 1.1.5
	patch -Np1 < "${srcdir}"/0006-mysql-workbench-column-label.patch

	# fix GDAL
	sed -i '/#include/s|gdal/||' backend/wbpublic/grtui/geom_draw_box.h backend/wbpublic/grt/spatial_handler.h

	# put antlr into place
	install -D ${srcdir}/antlr-3.4-complete.jar ${srcdir}/linux-res/bin/antlr-3.4-complete.jar
}

build() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src/"

	cmake . \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DSWIG_EXECUTABLE=/usr/bin/swig-2

	make
}

package() {
	cd "${srcdir}/mysql-workbench-community-${pkgver}-src"

	make DESTDIR="${pkgdir}" install

	install -D -m 0644 "${srcdir}"/arch_linux_profile.xml \
		"${pkgdir}"/usr/share/mysql-workbench/mysql.profiles/Arch_Linux_\(MariaDB_5.5_Package\).xml
}

