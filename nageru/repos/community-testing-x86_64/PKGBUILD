# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=nageru
pkgver=2.1.0
pkgrel=3
pkgdesc="live video mixer, based around the standard M/E workflow."
arch=(x86_64)
url="https://nageru.sesse.net/"
license=('GPL3')
depends=('libmicrohttpd' 'ffmpeg' 'libx264' 'protobuf' 
         'movit' 'bmusb' 'qcustomplot' 'pciutils' 'zita-resampler'
         'sqlite3' 'luajit' 'cmake')
makedepends=('meson' 'eigen')
source=("https://nageru.sesse.net/nageru-${pkgver}.tar.gz")
sha256sums=('646cc513002646540cfbff746c6fdda2ac365711ead9492a63a06d86a9034389')

build() {
  cd "$pkgname-$pkgver"
  arch-meson ../build
  ninja -v -C ../build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C ../build test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir" ninja -C ../build install
}

# vim:set ts=2 sw=2 et:
