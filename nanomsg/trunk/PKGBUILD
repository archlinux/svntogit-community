# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: KokaKiwi <admin@kokaelkiwi.eu>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgname=nanomsg
pkgver=0.7
_tag=$pkgver-beta
pkgrel=1
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
url='http://nanomsg.org/'
license=(MIT)
arch=(i686 x86_64)
depends=(glibc)
source=(nanomsg-$pkgver.tar.gz::https://github.com/nanomsg/nanomsg/archive/$_tag.tar.gz)
sha1sums=('880434156edb8e2fadf16630cbdec9add1d4fd80')

build() {
  cd nanomsg-$_tag
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd nanomsg-$_tag
  make check -j1
}

package() {
  cd nanomsg-$_tag
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
