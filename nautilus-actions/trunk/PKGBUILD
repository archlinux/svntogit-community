# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tor Krill <tor@krill.nu>

pkgname=nautilus-actions
pkgver=3.2.4
pkgrel=2
pkgdesc="Configures programs to be launched when files are selected in Nautilus"
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/nautilus-actions"
license=('GPL')
depends=('nautilus' 'libunique3' 'libgtop' 'libsm' 'gconf')
makedepends=('intltool' 'gnome-doc-utils' 'python')
source=(http://www.nautilus-actions.org/downloads/$pkgname-$pkgver.tar.gz)
sha1sums=('bfa90e86ff0624d345962a9add71d3b2e8bb2047')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Install GConf schemas
  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain nautilus-actions \
                     "$pkgdir"/usr/share/nautilus-actions/gconf-schemas/*.schemas
  rm -r "$pkgdir/usr/share/nautilus-actions/gconf-schemas/"

  # Remove redundant documentation
  rm -r "$pkgdir/usr/share/doc/"
}
