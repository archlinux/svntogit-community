# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=nautilus-image-converter
pkgver=0.4.0
pkgrel=1
pkgdesc="Nautilus extension to rotate/resize image files"
arch=('x86_64')
url="https://git.gnome.org/browse/nautilus-image-converter"
license=('GPL')
depends=('imagemagick' 'nautilus')
makedepends=('git' 'meson')
_commit=56e39b3a9d48e4daba011743803950505ba56def
source=("git+https://gitlab.gnome.org/coreyberla/nautilus-image-converter#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/^NAUTILUS_IMAGE_CONVERTER_//;s/_/./g;s/-/+/g'
}

build() {
	meson build $pkgname \
	  --prefix=/usr
	meson compile -C build
}

package() {
	meson install -C build --destdir="$pkgdir"
}
