# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giorgio Gilestro <giorgio@gilest.ro>

pkgbase=nautilus-python
pkgname=('python-nautilus' 'python2-nautilus')
pkgver=1.2.2
pkgrel=2
pkgdesc="Python bindings for the Nautilus Extension API"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/NautilusPython"
license=('GPL')
depends=('nautilus' 'python-gobject' 'python2-gobject')
makedepends=('gtk-doc')
source=(https://download.gnome.org/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
sha256sums=('90cda3fd613ed012214e47c2dc6f814db4e204a5af38e840479819ab9c59f112')

build() {
  # Python 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgver python3
  cd python3
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-gtk-doc \
              PYTHON=python3
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  cd "$srcdir"
  # Python 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgver python2
  cd python2
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-gtk-doc \
              PYTHON=python2
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_python-nautilus() {
  pkgdesc="Python 3 bindings for the Nautilus Extension API"
  depends=('nautilus' 'python-gobject')
  conflicts=('python2-nautilus')

  cd python3
  make DESTDIR="$pkgdir" install
}

package_python2-nautilus() {
  pkgdesc="Python 2 bindings for the Nautilus Extension API"
  depends=('nautilus' 'python2-gobject')
  conflicts=('python-nautilus')

  cd python2
  make DESTDIR="$pkgdir" install
}
