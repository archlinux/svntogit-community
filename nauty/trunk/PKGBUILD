# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=nauty
pkgver=25r9
pkgrel=1
pkgdesc="A program for computing automorphism groups of graphs and digraphs"
arch=('i686' 'x86_64')
url="http://cs.anu.edu.au/~bdm/nauty/"
license=('custom')
depends=('glibc')
source=("http://cs.anu.edu.au/~bdm/nauty/nauty$pkgver.tar.gz" 'LICENSE')
md5sums=('e8ecd08b0892a1fb13329c147f08de6d'
         '228d316cce6fb60dbb8cee9068b44a31')

build() {
  cd nauty$pkgver

  ./configure
  make
}

package() {
  cd nauty$pkgver

  mkdir -p "$pkgdir"/usr/bin
  for program in addedgeg amtog biplabg catg complg copyg countg deledgeg directg dreadnaut dretog genbg geng genrang labelg listg multig \
   newedgeg NRswitchg pickg planarg shortg showg gentourng;
  do
   cp $program "$pkgdir"/usr/bin/nauty-$program
  done

  mkdir -p "$pkgdir"/usr/share/licenses/nauty
  install "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/nauty
}

