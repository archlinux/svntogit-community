# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>

pkgname=nbd
pkgver=3.2
pkgrel=2
pkgdesc="tools for network block devices, allowing you to use remote block devices over TCP/IP"
arch=('i686' 'x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
	config nbd nbd.service)
backup=('etc/nbd-server/config')
depends=('glib2')
install=nbd.install
md5sums=('9cafce4ecfb55e670d7ded4c8f4e6bc9'
         '2d05d426b8c2708d5f8a0d028fcbae05'
         'e7b13a8b3369e11927d9917664557efa'
         '5d1acc143018117174f79eff15afa038')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m755 "${srcdir}"/nbd "${pkgdir}"/etc/rc.d/nbd
  install -D -m644 "${srcdir}"/nbd.service "${pkgdir}"/usr/lib/systemd/system/nbd.service

  install -D -m644 README "${pkgdir}"/usr/share/doc/nbd/README
}
