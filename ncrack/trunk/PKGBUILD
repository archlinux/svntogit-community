# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=ncrack
pkgver=0.5
pkgrel=4
pkgdesc='A high-speed network authentication cracking tool'
arch=(x86_64)
url='https://nmap.org/ncrack/'
license=(GPL)
depends=(openssl-1.0)
source=("https://nmap.org/ncrack/dist/ncrack-$pkgver.tar.gz"
         ncrack-nullcheck.patch)
sha256sums=('dbad9440c861831836d47ece95aeb2bd40374a3eb03a14dea0fe1bfa73ecd4bc'
            'efb8b466cb51a58277c6f0dc17d478e9d4a9722f0ad2000272de900a9ca804da')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../ncrack-nullcheck.patch
}

build() {
  cd $pkgname-$pkgver
  CFLAGS+=" -I/usr/include/openssl-1.0" \
  CPPFLAGS+=" -I/usr/include/openssl-1.0" \
  LDFLAGS+=" -L/usr/lib/openssl-1.0" \
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
