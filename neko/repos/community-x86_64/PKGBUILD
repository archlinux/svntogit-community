# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Caleb McCombs <erdrick016+aur@gmail.com>
# Contributor: Christian Hesse <arch@eworm.de>

pkgname=neko
pkgver=2.3.0
pkgrel=2
pkgdesc='High-level and dynamically typed programming language'
url='https://nekovm.org/'
license=(LGPL)
arch=(x86_64)
depends=(gc gtk3 mariadb-libs mbedtls sqlite)
makedepends=(apache apr cmake git mbedtls ninja)
optdepends=('apache: for extending Apache with mod_neko')
options=(!strip)
source=("git+https://github.com/HaxeFoundation/neko#tag=v${pkgver//./-}")
sha256sums=('SKIP')

prepare() {
  sed -i '/xlocale.h/d' $pkgname/libs/std/sys.c

  # Port to GTK 3
  mv $pkgname/cmake/FindGTK2.cmake $pkgname/cmake/FindGTK3.cmake
  sed -i 's/gtk_timeout_add/gdk_threads_add_timeout/g' $pkgname/libs/ui/ui.c
  find $pkgname -type f -exec sed -i 's/gtk2/gtk3/g;s/GTK2/GTK3/g;s/gtk-2/gtk-3/g;s/GTK-2/GTK-3/g;s/gtk+-2/gtk+-3/g;s/GTK+-2/GTK+-3/g' {} \;
}

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DRUN_LDCONFIG=OFF \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: nekovm.org/download
