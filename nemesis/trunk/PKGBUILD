# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=nemesis
pkgver=1.4
pkgrel=5
pkgdesc="command-line network packet crafting and injection utility"
arch=(i686 x86_64)
url="http://nemesis.sourceforge.net/"
license=('BSD')
depends=()
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/nemesis/nemesis-$pkgver.tar.gz
	http://archlinux-stuff.googlecode.com/files/libnet-1.0.2a.tar.gz
	nemesis-proto_tcp.c.diff)
md5sums=('acd7de57798a7f90f445f80d4d4a6e61'
         'ddf53f0f484184390e8c2a1bd0853667'
         'd802f7a12610e68124200310ad80e0ac')

package() {
  # build libnet
  cd $srcdir/Libnet-1.0.2a
  # very dirty hack
  sed -i 's#malloc(p_size)#malloc(p_size*2)#' src/libnet_packet_mem.c
  [ -f Makefile ] || ./configure --prefix=$srcdir/libnet
  make
  make MAN_PREFIX=$srcdir/libnet/usr/man/man3 install

  # build nemesis
  cd $srcdir/nemesis-$pkgver
  export PATH="$srcdir/libnet/bin:$PATH"
  export CFLAGS="-D__GLIBC__=0 -I$srcdir/libnet/include -DLIBNET_LIL_ENDIAN"
  export LDFLAGS="-L$srcdir/libnet/lib"
  patch -Np1 <$srcdir/nemesis-proto_tcp.c.diff
  [ -f Makefile ] || ./configure --prefix=/usr --mandir=/usr/share/man \
	    --with-libnet-includes=$srcdir/libnet/include \
	    --with-libnet-libraries=$srcdir/libnet/lib
  make
  make DESTDIR=$pkgdir install
  install -D -m0644 LICENSE $pkgdir/usr/share/licenses/nemesis/LICENSE
}
