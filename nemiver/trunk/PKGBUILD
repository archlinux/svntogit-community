# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: jordz <jordz@archlinux.us>

pkgname=nemiver
pkgver=0.9.5
pkgrel=3
pkgdesc='C/C++ debugger for GNOME'
arch=('x86_64' 'i686')
license=('GPL')
url='http://www.gnome.org/projects/nemiver/'
install="$pkgname.install"
depends=('gdlmm' 'gdb' 'gtksourceviewmm' 'libgtop' 'vte3' 'ghex' 'gsettings-desktop-schemas')
makedepends=('boost' 'intltool' 'itstool' 'gconf' 'gnome-common' 'yelp-tools')
source=("http://ftp.acc.umu.se/pub/GNOME/sources/nemiver/0.9/$pkgname-$pkgver.tar.xz"
        vte2.91.patch)
options=('!emptydirs')
sha256sums=('33590e3964092cabb629a6ba8415786b054702063a944eca5fe4156048baf8d9'
            '25b7b6d81fac0d465ac8ac854a99135702fef49d1fba214c0f72b4d4674d52eb')

prepare() {	
  cd "$pkgname-$pkgver"

  # Consolidated from patches on git master for https://bugzilla.gnome.org/show_bug.cgi?id=730243
  patch -Np1 -i ../vte2.91.patch
  NOCONFIGURE=1 AUTOPOINT=intltoolize gnome-autogen.sh
}

build() {	
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --with-boost \
    --disable-scrollkeeper \
    --enable-symsvis=yes \
    --enable-memoryview=yes \
    --enable-gsettings=yes \
    --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
