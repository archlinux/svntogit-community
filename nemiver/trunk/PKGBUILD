# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: jordz <jordz@archlinux.us>

pkgname=nemiver
pkgver=0.9.6
pkgrel=4
pkgdesc='C/C++ debugger for GNOME'
arch=('x86_64' 'i686')
license=('GPL')
url='http://www.gnome.org/projects/nemiver/'
depends=('gdlmm' 'gdb' 'gtksourceviewmm' 'libgtop' 'vte3' 'ghex')
makedepends=('boost' 'intltool' 'itstool' 'gconf' 'gnome-common' 'yelp-tools')
source=("http://ftp.acc.umu.se/pub/GNOME/sources/nemiver/0.9/$pkgname-$pkgver.tar.xz"
        'Ben Iofel.patch')
options=('!emptydirs')
sha256sums=('85ab8cf6c4f83262f441cb0952a6147d075c3c53d0687389a3555e946b694ef2'
            '8b7966ef3be114fe999c1f7764b04f05ae191633bfb005f8e3ea80461e4d6bfd')

prepare() {
  cd "$pkgname-$pkgver"

  NOCONFIGURE=1 AUTOPOINT=intltoolize gnome-autogen.sh
  patch -p1 -i '../Ben Iofel.patch'
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --with-boost \
    --disable-scrollkeeper \
    --enable-symsvis=yes \
    --enable-memoryview=yes \
    --enable-gsettings=yes \
    --disable-schemas-compile
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: git.gnome.org/browse/nemiver/plain/configure.ac
# vim:set ts=2 sw=2 et:
