# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Chris Salzberg <chris@dejimata.com>
# Contributor: Leonidas Spyropoulos <artafinde@gmail.com>
pkgname=neomutt
pkgver=20220429
pkgrel=2
pkgdesc='A version of mutt with added features'
url='https://neomutt.org/'
license=('GPL')
arch=('x86_64')
depends=('glibc' 'gpgme' 'lua53' 'notmuch-runtime' 'krb5' 'gnutls' 'sqlite'
         'libsasl' 'ncurses' 'libidn2' 'lmdb' 'gdbm' 'kyotocabinet'
         'lz4' 'zlib' 'zstd' 'db')
optdepends=('python: keybase.py'
            'perl: smime_keys'
            'ca-certificates: default CA certificates')
makedepends=('git' 'gnupg' 'libxslt' 'docbook-xsl' 'w3m')
_github='https://github.com/neomutt/neomutt'
source=("$pkgname-$pkgver.tar.gz::$_github/archive/$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$_github/releases/download/$pkgver/$pkgver.tar.gz.sig"
        "default-ca-certificates.patch"
        "find_gpg-error_with_pkgconf.patch")
sha256sums=('45496542897ba8de6bc7cce3f5951d9033ed1c49e5d6f1353adaeefe795d9043'
            'SKIP'
            '571716b8979e9b43328416c3c56eff228b6c58355d7c080b8987ea89c6360776'
            '4de55bd0ab92d6499dfa49d06a2148f6cbff5cd2e5dabdcc73c8667c5f9c2dfe')
validpgpkeys=('86C2397270DD7A561263CA4E5FAF0A6EE7371805') # Richard Russon (flatcap) <rich@flatcap.org>
options=('debug')

prepare() {
    cd "$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/default-ca-certificates.patch"
    # 23620d6fdf4a59cc0720c5d4d92bc5e7d241aed7
    patch -Np1 -i "$srcdir/find_gpg-error_with_pkgconf.patch"
}

build() {
    cd "$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --gpgme \
        --sqlite \
        --autocrypt \
        --lua \
        --notmuch \
        --gss \
        --gnutls \
        --sasl \
        --with-ui=ncurses \
        --with-idn2=/usr \
        --disable-idn \
        --idn2 \
        --bdb \
        --lmdb \
        --kyotocabinet \
        --gdbm \
        --lz4 \
        --zlib \
        --zstd
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
