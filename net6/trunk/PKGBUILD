# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=net6
pkgver=1.3.14
pkgrel=4
pkgdesc="A library that provides a TCP protocol abstraction for C++"
arch=('i686' 'x86_64')
url="http://gobby.0x539.de/"
license=('LGPL')
depends=('gnutls' 'libsigc++2.0')
source=(http://releases.0x539.de/$pkgname/$pkgname-$pkgver.tar.gz
	gnutls.patch)
md5sums=('be6db739f71c5c08421bf6181e77f3b2'
         '4fb70f33b352522b46a25988668535d0')

prepare() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -p1 <$srcdir/gnutls.patch
}

build() {
  cd ${srcdir}/$pkgname-$pkgver

  # libsigc++ 2.6.0 (and later) uses C++11 features
  CXXFLAGS+=' -std=c++11'

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
}
