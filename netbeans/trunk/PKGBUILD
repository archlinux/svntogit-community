# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ben Wolsieffer <benwolsieffer@gmail.com>

pkgname=netbeans
pkgver=8.0.2
_subver=201411102027
pkgrel=1
pkgdesc='IDE for Java, HTML5, PHP, Groovy, C and C++'
arch=('any')
url='http://netbeans.org/'
license=('CDDL')
# classpath is not needed, see FS#38567 (Oracle JDK does not need atk either)
depends=('java-environment' 'ttf-font' 'libxtst' 'giflib' 'atk')
backup=('usr/share/netbeans/etc/netbeans.conf')
makedepends=('gendesk' 'setconf')
optdepends=('php: for developing programs in php'
            'groovy: for developing programs in groovy'
            'lib32-glibc')
install='netbeans.install'
options=('!strip')
# http://dlc-cdn.sun.com/netbeans/8.0/final/zip/
source=("http://dlc-cdn.sun.com/$pkgname/$pkgver/final/zip/$pkgname-$pkgver-$_subver.zip"
        'http://www.jimvernon.com/wp-content/uploads/2014/02/netbeans.png')
sha256sums=('e6e8e1cf033453ce60ee512b63122ca74af27f3f08a8eaa2ba5e1eda4aceb9f2'
            '1d519f018ebeff6e29436c8492dbb266446bd0144fb5519b2ef6637664de6f4a')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --custom "StartupWMClass=NetBeans IDE $pkgver" --name "NetBeans"
  setconf "$pkgname/bin/$pkgname" basedir "/usr/share/$pkgname"
}

package() {
  mkdir -p "$pkgdir/usr/"{bin,share/applications}

  # Binary file
  install -Dm755 "$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"

  # Resources
  cp -r "$pkgname" "$pkgdir/usr/share/"

  # Desktop shortcut and icon
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Cleanup of directory that contains the binary file and .exe-files
  rm -r "$pkgdir/usr/share/$pkgname/bin"
}

# vim:set ts=2 sw=2 et:
