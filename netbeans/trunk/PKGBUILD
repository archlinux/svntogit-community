# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netbeans
pkgver=8.0.1
_subver=201408251540
pkgrel=1
pkgdesc='IDE for Java, HTML5, PHP, Groovy, C and C++'
arch=('any')
url='http://netbeans.org/'
license=('CDDL')
# classpath is not needed, see FS#38567 (Oracle JDK does not need atk either)
depends=('java-environment' 'ttf-font' 'libxtst' 'giflib' 'atk')
backup=('usr/share/netbeans/etc/netbeans.conf')
makedepends=('gendesk' 'setconf')
optdepends=('php: for developing programs in php'
            'groovy: for developing programs in groovy'
            'lib32-glibc')
install='netbeans.install'
options=('!strip')
# http://dlc.sun.com.edgesuite.net/netbeans/8.0/final/zip/
source=("http://dlc.sun.com.edgesuite.net/$pkgname/$pkgver/final/zip/$pkgname-$pkgver-$_subver.zip"
        'http://www.jimvernon.com/wp-content/uploads/2014/02/netbeans.png')
sha256sums=('5c572e18ff610d7d78044c7602bfaa97d0faaeb6ba3f4fbe1427cadb12285e5e'
            '1d519f018ebeff6e29436c8492dbb266446bd0144fb5519b2ef6637664de6f4a')

prepare() {
  gendesk -f -n --pkgname "$pkgname" --pkgdesc "$pkgdesc"
  setconf "$pkgname/bin/$pkgname" basedir "/usr/share/$pkgname"
  setconf -a "$pkgname/etc/$pkgname.conf" netbeans_jdkhome '"/usr/lib/jvm/default"'
}

package() {
  mkdir -p "$pkgdir/usr/"{bin,share/applications}

  # Binary file
  install -Dm755 "$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"

  # Resources
  cp -r "$pkgname" "$pkgdir/usr/share/"

  # Desktop shortcut and icon
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Cleanup of directory that contains the binary file and .exe-files
  rm -r "$pkgdir/usr/share/$pkgname/bin"
}

# vim:set ts=2 sw=2 et:
