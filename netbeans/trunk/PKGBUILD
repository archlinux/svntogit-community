# Contributor: Timm Preetz <timm@preetz.us>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netbeans
pkgver=7.0.1
pkgrel=1
pkgdesc='Netbeans IDE development platform.'
arch=('any')
url='http://www.netbeans.org'
license=('CDDL')
depends=('java-environment')
optdepends=('php: for developing programs in php'
            'python: for developing programs in python')
install='netbeans.install'
source=('http://dlc.sun.com.edgesuite.net/netbeans/download/7.0.1/fcs_dir_hidden/zip/netbeans-7.0.1-201107282000-ml.zip'
        'netbeans.desktop')
md5sums=('4d3c76236e3424a49e99023dfe228124'
         '88c631d0d263218e01ea886fde2bc913')

build() {
  # cleanup OS specific files
  rm $(find -name '*\.exe' -or -name '*\.bat')
  rm -r $(find -name 'MacOSX*' -or -name 'Windows*' -or -name 'SunOS*')
  rm -r $(find -name 'hpux*' -or -name 'mac*' -or -name 'solaris*' -or -name 'windows*')
}

package() {
  install -d ${pkgdir}/usr/share
  cp -r netbeans ${pkgdir}/usr/share

  install -d ${pkgdir}/usr/share/applications
  install -m644 netbeans.desktop \
    ${pkgdir}/usr/share/applications

  install -d ${pkgdir}/usr/bin
  ln -s /usr/share/netbeans/bin/netbeans ${pkgdir}/usr/bin/netbeans
}

