# $Id$
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf-cxx
pkgver=4.3.0
pkgrel=2
pkgdesc="NetCDF c++ bindings"
arch=('x86_64')
url="https://github.com/Unidata/netcdf-cxx4/"
license=('custom')
depends=('gcc-libs' 'netcdf' 'curl')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('25da1c97d7a01bc4cee34121c32909872edd38404589c0427fefa1301743f18f')

build() {
    cd ${pkgname}4-${pkgver}
    ./configure \
        --prefix=/usr \
        --enable-shared
    make
}

check() {
    cd ${pkgname}4-${pkgver}
    make check
}

package() {
    cd ${pkgname}4-${pkgver}

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYRIGHT -t ${pkgdir}/usr/share/licenses/${pkgname}/
}
