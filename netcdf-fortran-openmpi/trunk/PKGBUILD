# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

_pkg=netcdf-fortran
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=4.4.5
pkgrel=1
pkgdesc="NetCDF fortran bindings with parallel support (${_mpi} version)"
arch=('x86_64')
url="https://github.com/Unidata/netcdf-fortran"
license=('custom')
depends=('gcc-libs' 'netcdf-openmpi' 'curl')
makedepends=('gcc-fortran' 'doxygen')
checkdepends=('valgrind')
provides=("${_pkg}")
conflicts=("${_pkg}")
options=('!makeflags' 'staticlibs')
source=(${_pkg}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('01643461ac42d1986e38a052eb021135bae5b6cd592373fb44cf832236791c03')

build() {
    cd ${_pkg}-${pkgver}
    ./configure \
        CC=mpicc \
        FC=mpifort \
        --prefix=/usr \
        --enable-extra-tests \
        --enable-extra-example-tests \
        --enable-large-file-tests \
        --enable-parallel-tests \
        --enable-valgrind-tests
    make
}

check() {
    cd ${_pkg}-${pkgver}

    # This is required starting with OpenMPI 3.0 when trying to run more
    # processes than the number of available cores
    export OMPI_MCA_rmaps_base_oversubscribe=yes

    # https://github.com/Unidata/netcdf-fortran/issues/109
    make check || warning "Tests failed"
}

package() {
    cd ${_pkg}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}/
}
