# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

_pkg=netcdf-fortran
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=4.5.4
pkgrel=1
pkgdesc="NetCDF fortran bindings with parallel support (${_mpi} version)"
arch=(x86_64)
url="https://github.com/Unidata/netcdf-fortran"
license=(custom)
depends=(gcc-libs netcdf-openmpi curl)
makedepends=(cmake gcc-fortran doxygen)
checkdepends=(valgrind)
provides=("${_pkg}")
conflicts=("${_pkg}")
options=(!makeflags staticlibs)
source=(${url}/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz
       ${pkgname}-fix-f90tst_parallel_compressed-F90.patch::https://github.com/Unidata/netcdf-fortran/commit/79c361c15afb33ee0bae786eecb0daf96b676e1b.patch
       ${pkgname}-fix-run_f90_par_test.patch::https://github.com/Unidata/netcdf-fortran/pull/341.patch)
sha256sums=('1a8613cb639e83e2df5a8e6c21fa48a0c64b053c244abddecec66cfcac03a48a'
            'c7be863cd34cb0f42200175f9ed1f9322f508171b7bd280993d76e669d1ef857'
            '123e129190d5c1796da20261167204a4509ba8231a6d26e27cfed1479b84f802')

prepare() {
  cd ${_pkg}-${pkgver}
  patch -p1 < ../${pkgname}-fix-f90tst_parallel_compressed-F90.patch
  patch -p1 < ../${pkgname}-fix-run_f90_par_test.patch
}

build() {
  export CC=mpicc FC=mpifort
  cmake -B build -S ${_pkg}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_FILTER_TEST=ON \
    -DENABLE_LARGE_FILE_TESTS=ON \
    -DENABLE_PARALLEL_TESTS=ON \
    -DTEST_WITH_VALGRIND=ON
  make -C build
}

check() {
  export OMPI_MCA_opal_warn_on_missing_libcuda=0
  # This is required starting with OpenMPI 3.0 when trying to run more
  # processes than the number of available cores
  export OMPI_MCA_rmaps_base_oversubscribe=yes

  # https://github.com/Unidata/netcdf-fortran/pull/341
  chmod +x build/nf03_test4/run_f90_par_test.sh
  # https://github.com/Unidata/netcdf-fortran/issues/340
  make -C build test || echo "Tests failed"
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${_pkg}-${pkgver}/COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}/
  # Remove unwanted artifacts https://github.com/Unidata/netcdf-fortran/issues/338
  rmdir "${pkgdir}"/usr/include/CMakeFiles/{netcdff{,_c}.dir,}
  rm "${pkgdir}"/usr/lib/objects-RelWithDebInfo/{nf_,f}test_c/f03lib.c.o
  rmdir "${pkgdir}"/usr/lib/objects-RelWithDebInfo/{{nf_,f}test_c,}
  rm "${pkgdir}"/usr/lib/objects-Release/netcdff_c/nf_{lib,v2compat}.c.o
  rmdir "${pkgdir}"/usr/lib/objects-Release/{netcdff_c,}
}
