# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf-fortran
pkgver=4.4.5
pkgrel=1
pkgdesc="NetCDF fortran bindings"
arch=('x86_64')
url="https://github.com/Unidata/netcdf-fortran"
license=('custom')
depends=('gcc-libs' 'netcdf' 'curl')
makedepends=('gcc-fortran' 'doxygen')
checkdepends=('valgrind')
options=('!makeflags' 'staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('01643461ac42d1986e38a052eb021135bae5b6cd592373fb44cf832236791c03')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --enable-extra-tests \
        --enable-extra-example-tests \
        --enable-large-file-tests \
        --enable-valgrind-tests
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
