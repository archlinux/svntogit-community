# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Edward Pacman <edward@edward-p.xyz>

pkgname=netfilter-fullconenat
pkgver=r73.0cf3b48
pkgrel=37
pkgdesc="A kernel module that turns MASQUERADE into full cone SNAT"
arch=('x86_64')
url="https://github.com/Chion82/netfilter-full-cone-nat"
license=('GPL2')
depends=("linux")
makedepends=('linux-headers' 'git')
source=("netfilter-fullconenat.conf" Kbuild
        "${pkgname}::git+https://github.com/Chion82/netfilter-full-cone-nat.git")
sha256sums=('a17dfdf1fd046219daeacc60065e3a81c80c2eb2cfdf6d8068278c509577f571'
            '7ff12ad066a68c65f23fc7e01654ca459ce3458172e3dce30f42553fa44dd7c2'
            'SKIP')

pkgver() {
  cd "$srcdir/${pkgname}"
  ( set -o pipefail
    git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  cd $pkgname
  ln -s ../Kbuild
  make -C /usr/src/linux M=$PWD modules
}

package() {
  # Install modules-load.conf
  install -Dm644 netfilter-fullconenat.conf "${pkgdir}/usr/lib/modules-load.d/netfilter-fullconenat.conf"

  install -Dt "${pkgdir}/usr/lib/modules/$(</usr/src/linux/version)/kernel/net/netfilter" -m644 \
    $pkgname/*.ko
  find "${pkgdir}" -name '*.ko' -exec strip --strip-debug {} +
  find "${pkgdir}" -name '*.ko' -exec xz {} +
}
