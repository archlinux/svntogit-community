# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=netsurf-buildsystem
pkgver=1.0
pkgrel=1
pkgdesc='The Netsurf buildsystem'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('MIT')
source=("http://download.netsurf-browser.org/libs/releases/buildsystem-$pkgver.tar.gz")
sha256sums=('4495e8e12faf0ace668ab80401064ce7f65a195efe63c924c066a109649ff146')

build() {
  cd "$srcdir/buildsystem-$pkgver"

  sed -i 's:-Werror::' Makefile
  make PREFIX=/usr COMPONENT_TYPE='lib-shared'
  make PREFIX=/usr COMPONENT_TYPE='lib-static'
}

package() {
  cd "$srcdir/buildsystem-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE='lib-shared'   
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE='lib-static'
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
