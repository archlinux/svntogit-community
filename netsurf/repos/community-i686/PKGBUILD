# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.3
pkgrel=1
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('MIT' 'GPL2')
depends=('libmng' 'librsvg' 'libwebp' 'curl' 'lcms' 'desktop-file-utils' 'libnsbmp>=0.1.1' 'libnsgif>=0.1.1' 'libcss>=0.5.0' 'libdom>=0.1.2' 'gtk2' 'libutf8proc' 'libnsutils' 'libwebp') # gtk3
makedepends=('re2c' 'netsurf-buildsystem' 'perl-html-parser' 'nsgenbind' 'inetutils' 'libwebp')
install="$pkgname.install"
source=("http://download.$pkgname-browser.org/$pkgname/releases/source/$pkgname-$pkgver-src.tar.gz"
        'netsurf.sh')
sha256sums=('23f92a0940f577b9605f2e54786fea3521680d32d6c00f2a3db1eae3f29e3686'
            '70310682d1612457d7bb3096549110b6ec127f50e97853259fada6be0c52924b')

prepare() {
  cd "$pkgname-$pkgver/gtk"

  sed 's:_BSD_SOURCE:_DEFAULT_SOURCE:' -i Makefile.target
  sed 's:netsurf-gtk:netsurf:' -i res/netsurf-gtk.desktop
}

build() {
  make -C "$pkgname-$pkgver" \
    PREFIX=/usr \
    TARGET=gtk \
    NETSURF_USE_WEBP=YES \
    NETSURF_USE_VIDEO=NO \
    NETSURF_USE_JS=NO \
    NETSURF_USE_MOZJS=NO # Javascript is disabled, for now. See FS#41582
}

package() {
  cd "$pkgname-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir"

  # Script and executable
  mv "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/bin/$pkgname.elf"
  install -Dm755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  # Desktop shortcut
  install -Dm644 "gtk/res/$pkgname.xpm" \
    "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 "gtk/res/$pkgname-gtk.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # License
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
