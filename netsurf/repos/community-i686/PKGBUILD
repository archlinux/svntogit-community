# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.1
pkgrel=2
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('MIT' 'GPL2')
depends=('libmng' 'librsvg' 'libwebp' 'curl' 'lcms' 'desktop-file-utils' 'libnsbmp>=0.1.1' 'libnsgif>=0.1.1' 'libcss>=0.3.0' 'libdom>=0.1.0' 'gtk2') # gtk3
makedepends=('re2c' 'gendesk' 'netsurf-buildsystem' 'perl-html-parser' 'nsgenbind' 'inetutils')
install="$pkgname.install"
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://download.$pkgname-browser.org/$pkgname/releases/source/$pkgname-$pkgver-src.tar.gz"
        'netsurf.sh')
sha256sums=('f0dbcc5d80bf03d706aa8b28a322aa7f169a40813848c2d1505691f6e2c7ef00'
            '1ef12b2e2bc68f9eff8465646b7359e3cb483e1042aed750cc283a70dd4c540c'
            '70310682d1612457d7bb3096549110b6ec127f50e97853259fada6be0c52924b')

prepare() {
  gendesk -f --pkgname "$pkgname" --pkgdesc "$pkgdesc" --exec 'netsurf %U' \
    --genericname 'Web Browser' --comment 'Lightweight web browser' \
    --mimetypes 'text/html;application/xhtml+xml;x-scheme-handler/http;x-scheme-handler/https;x-scheme-handler/geo'
}

build() {
  make -C "$pkgname-$pkgver" \
    PREFIX=/usr \
    TARGET=gtk \
    NETSURF_USE_WEBP=YES \
    NETSURF_USE_VIDEO=NO \
    NETSURF_USE_JS=NO \
    NETSURF_USE_MOZJS=NO # Javascript is disabled, for now. See FS#41582
}

package() {
  make -C "$pkgname-$pkgver" install PREFIX=/usr DESTDIR="$pkgdir"
  mv "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/bin/$pkgname.elf"
  install -Dm755 "$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # use softlinks instead of hardlinks
  rm -r "$pkgdir/usr/share/netsurf/C"
  ln -s '/usr/share/netsurf/en' "$pkgdir/usr/share/netsurf/C"
}

# vim:set ts=2 sw=2 et:
