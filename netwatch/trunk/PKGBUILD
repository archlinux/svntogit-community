# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=netwatch
pkgver=1.3.1_2
pkgrel=1
pkgdesc="monitor network connections"
arch=(x86_64)
url="http://www.slctech.org/~mackay/NETWATCH/netwatch.html"
license=('GPL')
options=(zipman)
depends=(ncurses)
source=(https://www.slctech.org/~mackay/NETWATCH/netwatch-${pkgver/_/-}.tgz)
sha256sums=('9374fa55a41a4addd8897f1ff10a3453c840bf28534f500e8661a614231cc161')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  sed -i 's|${CC-cc} |${CC-cc} -O2 |g' configure
}

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  install -D -m0755 netwatch "$pkgdir"/usr/bin/netwatch
  install -D -m0755 netresolv "$pkgdir"/usr/bin/netresolv
  install -D -m0644 netwatch.1 "$pkgdir"/usr/share/man/man1/netwatch.1
}
