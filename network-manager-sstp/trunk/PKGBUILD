# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Echizen Ryoma <echizenryoma.zhang@gmail.com>
# Contributor: Martchus <martchus@gmx.net>

pkgname=network-manager-sstp
pkgver=1.3.0
pkgrel=1
pkgdesc="SSTP support for NetworkManager"
arch=('x86_64')
url="https://gitlab.gnome.org/GNOME/network-manager-sstp"
license=('GPL2')
depends=('gtk3' 'libnma' 'libsecret' 'sstp-client')
optdepends=('ppp')
makedepends=('intltool' 'ppp' 'python')
source=("https://gitlab.gnome.org/GNOME/network-manager-sstp/-/archive/release-$pkgver/network-manager-sstp-release-$pkgver.tar.bz2")
sha256sums=('0454cb06835d0c0cca5fe78481b297f7e7a17046f31c84017fe5cb70d81b41e3')

build() {
  pppd_version=(`pppd --version 2>&1 | awk '{print $3}'`)
  cd network-manager-sstp-release-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$pppd_version \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib/NetworkManager \
    --with-libnm-glib=no \
    --enable-more-warnings=yes
  # libnm-glib disabled due to missing libnm-gtk package
  # set_more_warnings being error by default, which adds -Werror

  make
}

package() {
  cd network-manager-sstp-release-$pkgver

  make DESTDIR="$pkgdir" dbusservicedir=/usr/share/dbus-1/system.d install
}
