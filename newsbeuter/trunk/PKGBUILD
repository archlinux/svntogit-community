# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsbeuter
pkgver=2.9
pkgrel=2
pkgdesc="A RSS feed reader for the text console with special Podcast support"
arch=('i686' 'x86_64')
url="http://www.newsbeuter.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext')
optdepends=('ruby')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://www.newsbeuter.org/downloads/$pkgname-$pkgver.tar.gz
        newsbeuter-2.9-ncursesw6.patch)
sha256sums=('74a8bf019b09c3b270ba95adc29f2bbe48ea1f55cc0634276b21fcce1f043dc8'
            '5ae54c463f44d91725da3be655d2b107d598ade6da86ab4a99b10b039b8dba27')

prepare() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -p1 -i ../newsbeuter-2.9-ncursesw6.patch
}

build() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=/usr
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make prefix=/usr DESTDIR=$pkgdir install

  #license
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
