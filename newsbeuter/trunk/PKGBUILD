# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsbeuter
pkgver=2.9
pkgrel=4
pkgdesc="A RSS feed reader for the text console with special Podcast support"
arch=('i686' 'x86_64')
url="http://www.newsbeuter.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://www.newsbeuter.org/downloads/$pkgname-$pkgver.tar.gz
        $pkgname-$pkgver-ncursesw6.patch
        $pkgname-segfault.patch::https://github.com/akrennmair/newsbeuter/commit/33577f842d9b74c119f3cebda95ef8652304db81.patch)
sha256sums=('74a8bf019b09c3b270ba95adc29f2bbe48ea1f55cc0634276b21fcce1f043dc8'
            '5ae54c463f44d91725da3be655d2b107d598ade6da86ab4a99b10b039b8dba27'
            '6359708ee01f5e1b773a6ed79b7369b30aad5397b85fd252c2fa7d0c2616ea86')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  patch -p1 -i ../$pkgname-$pkgver-ncursesw6.patch
  patch -p1 -i ../$pkgname-segfault.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr DESTDIR="${pkgdir}" install

#license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
