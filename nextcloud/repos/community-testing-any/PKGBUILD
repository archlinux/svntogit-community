# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nextcloud
# NOTE: always check first whether the version is supposed to be packaged:
# https://github.com/nextcloud/server/wiki/Maintenance-and-Release-Schedule
pkgver=25.0.0
pkgrel=1
pkgdesc="A cloud server to store your files centrally on a hardware controlled by you"
arch=(any)
url="https://nextcloud.com"
license=(AGPL3)
makedepends=(php php-gd)
optdepends=(
  'curl: for webcron jobs from within uwsgi'
  'ffmpeg: for preview generation'
  'libreoffice: for preview generation'
  'mariadb: to use a local MariaDB server'
  'memcached: to use a local memcached server'
  'mod_itk: to use the Apache web server'
  'postgresql: to use a local PostgreSQL server'
  'redis: to use a local Redis server'
  'smbclient: for SMB/CIFS integration'
  'sudo: for privilege elevation in occ command when not run as root'
)
backup=(
  etc/webapps/$pkgname/config/config.php
  etc/webapps/$pkgname/.htaccess
  etc/uwsgi/$pkgname.ini
)
# there are no elf files, no need to strip anything
options=(!strip)
install=$pkgname.install
source=(
  https://download.nextcloud.com/server/releases/$pkgname-$pkgver.tar.bz2{,.asc}
  apache.example.conf
  $pkgname.config.php
  $pkgname.hook
  $pkgname.occ.sh
  $pkgname.sysusers
  $pkgname.tmpfiles
  $pkgname.uwsgi
  $pkgname-cron.service
  $pkgname-cron.timer
)
sha512sums=('6f7a7a4e7b613df7b246bf9a582a15598d40a96023bdde1d8e3fff1b5888588d9c957f58ea813470696edc7c1ad5e8d1721d5eaa3d9c91549ca8acc411a8211e'
            'SKIP'
            '5f9be223225e6ad2772989c440df8585a852ea0be697274384064ec73df47dab2f5d0847c275732fab26c419a713911d8f77ebc2b639bf6e8ede76272b752cde'
            '92592f9a76ee534d0ae6d3aedf2257c147337debe7e2d6dbec1daff66a682e3abc74611e08662058de314bf3ea6ff1c85efd678674b782d71251c38786e446f9'
            '2791382d7b005c64cd6938695f273e83435cc5f1b785fe7d44d6dbd69864c502847e45d93c4a5f6752a759a660118279e3e22e5bc32f79b8775693fde603a9c5'
            '20f12f0bb1d0ac735fb86e79e90fb99028e06e0d5a6842d449e443b418442682cad2573cfd4bf8e2a802c2aeb5780bce4647d45054bbc38156ae037802358698'
            '1d06f339c43d57e5f5406c2698b815241fd9e39039a9e55f777face510c9a5ddae5ccd4e051393e8f16f391301a6aa03fff2462837a2d3441d969ea7195e7d84'
            '919008a2bf6cf2492cb1e9c8e28c1e082d74e45da5a96e6669f8971d6cf721e36d23625c89918fe10d8bc1b202c0340aa897849d9ec501a26f3f4572ccb57a98'
            '67d32011e3dccf41ecaaeffa7486e70b8ee516b3423796c19cc9c95ba6ecd5d031540eeacefb9423fdfc70b1baad15a4885b7147cfbbb2773b349e72d3fe4df2'
            '66f542d8b84701d81e6f6d896163faa232996ee081bcabe4acc121064ef538b5c1ea43728222b5cadc60a5d433b1e54ad7c1fe9d1d188ffa8eaf331fd4cd8ec0'
            'eb69785a5b0df0e2a11f53f9c2e2dbf75681d7c698e88cbcc5ceed713a558d528511cc576813cc083cbaa28a2c83d543a39436ad4805670348ef22634305b1c8')
b2sums=('a339f5bf01255cccdbb9941d360e091a34149e6d3a365497344911c8f156cd7b272ed657fe48720054c448c3e52fafec37dbe0cd69be42f424479d35dd90e547'
        'SKIP'
        '7d40d120c28291f1a7537f3e1b5049fa83c9c0af949371345ea9ccdf3de4a0f3a3a06edbc52d9254dba483e4d11acf562cf679b9e379c5dfc2f61dfbfc2e4cef'
        '4ad061efbc20b4d0e9f8cfd01c5e8e612fa5af29bce754b77f90c1835b98ee445ef4de3d8c83da91461053622620af14d880eb9014b51f2383c75f1860d479f4'
        'c69f18de513562e32f0f655fee7ef81877d74a76717e2eaad9a13882bf8edd0dad99bec5958e460a4ec32eecfdd425ae009d11ac27af16a3ad1d6498282be32f'
        'f3d1423a457c7a24c25c8b18e24b838b63e99a7f7562ded954ea605c76bbd31e8e79158314e1343e341aa21b6a26dc57ca4f00b871bbc170f6b7d5044985e661'
        '1a61a89531636b65dade03cd7edca8747b3e1e880f9bbd4d5a53cffa452d70fe17e345cfed739ae92e99e336d4c1f2633c84a5c84a4ee6da518762bf1396b1db'
        'd132044cb7f1d61a1ce87800d2e1142e3ac385acdec25e2f5ad685603a0ffa88e105f4c35688584d65d287adbb40b8d9a02d6a80d7a0b58bb8801a6769405218'
        '67fa7d9207ab3e8309d4adcce734bd25f82bd03f1f88adea7e40adaddebb345b96a793df7960db79f4f39947f5edff61548bb409066b81e3fe643d313e68d66a'
        'a84fbdc9b5caf28eae6bfb147ea0c77e4cfc5a42f29b3fe93c0ecf8414f95ed20999a131efb7c7ba49041304fe7104813c574e6b5aaa6b03881aea3428144f33'
        'e6214351ca16bfe97683e838e727d6b5eaa62194878272204034c62aac4a632c9b33cd31aa0570c2eb936d7e9e7ff60190ac05a3a8cc2e27eda423a7d89b6d39')
validpgpkeys=('28806A878AE423A28372792ED75899B9A724937A') # Nextcloud Security <security@nextcloud.com>

_get_php_versions() {
  local _phps=(php7 php)
  local _min _max

  _min=$(sed -nr 's/^.*PHP_VERSION_ID < ([0-9]*).*$/\1/p' $pkgname/lib/versioncheck.php)
  _max=$(sed -nr 's/^.*PHP_VERSION_ID >= ([0-9]*).*$/\1/p' $pkgname/lib/versioncheck.php)
  _min_php="${_min:0:1}.${_min:2:1}"
  _max_php="${_max:0:1}.${_max:2:1}"

  _php_interpreter=""
  for _php in "${_phps[@]}"; do
    if command -v "$_php" > /dev/null; then
      if [[ -z "$_php_interpreter" ]]; then
        _php_interpreter="$_php"
      fi
    fi
  done
  _system_php="$($_php_interpreter --version |head -n1 |cut -d ' ' -f2)"
  printf "Detected php interpreter %s\n" $_php_interpreter
  printf "Detected php %s\n" $_system_php
  printf "%s requires %s >= php < %s\n" $pkgname $_min_php $_max_php
}

prepare() {
  local _file

  _get_php_versions

  # setup files with default PHP interpreter
  mkdir -vp custom_files
  for _file in nextcloud.{occ.sh,uwsgi} nextcloud-cron.service; do
    sed "s/PHP_INTERPRETER/$_php_interpreter/g" $_file > custom_files/$_file
  done
}

check() {
  _get_php_versions
  if [[ "$(vercmp "$_system_php" "$_min_php" )" -lt 0 ]]; then
    printf "%s requires php >= %s, but %s is provided\n" $pkgname $_min_php $_system_php
    exit 1
  fi
  if [[ "$(vercmp "$_system_php" "$_max_php" )" -gt 0 ]]; then
    printf "%s requires php < %s, but %s is provided\n" $pkgname $_max_php $_system_php
    exit 1
  fi
}

package() {
  local _dep_ver=""

  _get_php_versions
  depends=("php-interpreter<$_max_php")

  # NOTE: this sets up a custom prefix based on the php major version for for a php based package (e.g. php7-apcu)
  if [[ "$_php_interpreter" != *php ]]; then
    _dep_ver="${_system_php%%.*}"
  fi

  depends+=("php$_dep_ver-gd")

  # NOTE: due to weird makepkg checks we can't add optdepends with dynamically derived contents directly
  _optdepends=(
    "php$_dep_ver-apcu: for caching with APCU"
    "php$_dep_ver-apache: to use the Apache web server"
    "php$_dep_ver-fpm: to run in fastCGI process manager"
    "php$_dep_ver-imagick: for preview generation"
    "php$_dep_ver-imap: for external user authentication"
    "php$_dep_ver-intl: to increase language translation performance and fix sorting"
    "php$_dep_ver-memcached: for caching with memcached"
    "php$_dep_ver-pgsql: to use the PostgreSQL database backend"
    "php$_dep_ver-redis: for caching with redis"
    "php$_dep_ver-sqlite: to use the SQLite database backend"
    "uwsgi-plugin-php$_dep_ver: run as application container"
  )
  optdepends+=("${_optdepends[@]}")

  cd $pkgname
  # sysusers.d integration
  install -vDm 644 ../$pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  # tmpfiles.d integration
  install -vDm 644 ../$pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  # state dir for data and writable apps
  install -vdm 755 "$pkgdir/var/lib/$pkgname/"
  install -vdm 770 "$pkgdir/var/lib/$pkgname/data"
  install -vdm 755 "$pkgdir/var/lib/$pkgname/apps"
  # log dir
  install -vdm 755 "$pkgdir/var/log/$pkgname/"
  # uwsgi integration
  install -vDm 644 ../custom_files/$pkgname.uwsgi "$pkgdir/etc/uwsgi/$pkgname.ini"
  # config dir
  install -vDm 644 .htaccess -t "$pkgdir/etc/webapps/$pkgname/"
  install -vDm 640 ../$pkgname.config.php "$pkgdir/etc/webapps/$pkgname/config/config.php"
  find config -type f -exec install -vDm 644 {} "$pkgdir/etc/webapps/$pkgname/"{} \;
  # remove config dir and .htaccess from source so that it is not installed to default location
  rm -rf config .htaccess

  # webapp
  find . -type f -not -iname "*.orig" -exec install -vDm 644 {} "$pkgdir/usr/share/webapps/$pkgname/"{} \;

  # symlink config directory into place
  ln -sv /etc/webapps/$pkgname/config "$pkgdir/usr/share/webapps/$pkgname/"
  # symlink .htaccess file into place
  ln -sv /etc/webapps/$pkgname/.htaccess "$pkgdir/usr/share/webapps/$pkgname/"
  # add symlink to default writable apps directory
  ln -sv /var/lib/$pkgname/apps "$pkgdir/usr/share/webapps/$pkgname/wapps"
  # install occ command
  install -vDm 755 occ -t "$pkgdir/usr/share/webapps/$pkgname/"
  # occ script
  install -vDm 755 ../custom_files/$pkgname.occ.sh "$pkgdir/usr/bin/occ"
  # optional pacman hook
  install -vDm 644 ../$pkgname.hook -t "$pkgdir/usr/share/doc/$pkgname/"
  # apache example conf
  install -vDm 644 ../apache.example.conf -t "$pkgdir/usr/share/doc/$pkgname/"
  # systemd service
  install -vDm 644 ../custom_files/$pkgname-cron.service -t "$pkgdir/usr/lib/systemd/system/"
  install -vDm 644 ../$pkgname-cron.timer -t "$pkgdir/usr/lib/systemd/system/"
}
