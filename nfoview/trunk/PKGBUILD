# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=1.29
_commit=84dc60c423506ad0a8411f3552429fb080f97260
pkgrel=1
pkgdesc="Simple NFO file viewer in GTK+ 3"
arch=(any)
url="https://otsaloma.io/nfoview/"
license=(GPL3)
depends=(gtk3 python-gobject)
checkdepends=(python-pytest xorg-server-xvfb)
makedepends=(git python-build python-installer python-setuptools python-wheel)
optdepends=('terminus-font: recommended font')
source=($pkgname::git+https://github.com/otsaloma/$pkgname.git#tag=$_commit?signed)
sha512sums=('SKIP')
validpgpkeys=('8DD6B3FCDAB33C91EA915B913A584A9504CACA67') # Osmo Salomaa <otsaloma@iki.fi>

build() {
  make -C $pkgname
}

check() {
  cd $pkgname
  xvfb-run pytest -v $pkgname
}

package() {
  make DESTDIR="$pkgdir" PREFIX=/usr install -C $pkgname
  install -vDm 644 $pkgname/{AUTHORS,NEWS,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
