# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=1.16
pkgrel=2
pkgdesc="Simple NFO file viewer in GTK+ 3"
arch=('any')
url="http://otsaloma.github.io/nfoview/"
license=('GPL3')
depends=('gtk3' 'python-gobject' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('terminus-font: recommended font')
install=$pkgname.install
source=("https://github.com/otsaloma/$pkgname/archive/$pkgver.tar.gz"
        'x-nfo.desktop')
md5sums=('5683ccf049761dda7f1b9a68aae4a39f'
         '29d0ad75aeecfaf21965c01173d23e48')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python3 ./setup.py install --root "$pkgdir" --optimize 1

  install -Dm644 "$srcdir/x-nfo.desktop" \
    "$pkgdir/usr/share/mimelnk/text/x-nfo.desktop"
}

# vim:set ts=2 sw=2 et:
