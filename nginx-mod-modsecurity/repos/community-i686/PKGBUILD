# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-modsecurity
pkgver=20170403
pkgrel=2
_commit=3de175b

_modname=ModSecurity-nginx
_nginxver=1.12.1

pkgdesc='ModSecurity v3 Nginx Connector (module for mainline nginx)'
arch=('i686' 'x86_64')
depends=("nginx=$_nginxver" 'libmodsecurity')
makedepends=('git')
url="https://github.com/SpiderLabs/ModSecurity-nginx"
license=('GPL3')

source=(
	"http://nginx.org/download/nginx-$_nginxver.tar.gz"
	"git+https://github.com/SpiderLabs/ModSecurity-nginx.git#commit=$_commit"
)

sha256sums=('8793bf426485a30f91021b6b945a9fd8a84d87d17b566562c3797aba8fac76fb'
            'SKIP')

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat --add-dynamic-module=../$_modname
	make modules
}

package() {
	cd "$srcdir"/nginx-$_nginxver/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
