# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

_nginxver=1.12.1
_setmiscver=0.31
_ndkver=0.3.0

pkgbase=nginx-mod-ndk-set-misc
pkgname=(nginx-mod-ndk nginx-mod-set-misc)
pkgver=$_setmiscver+$_ndkver
pkgrel=2

arch=('i686' 'x86_64')
depends=("nginx=$_nginxver")
url="https://github.com/openresty/set-misc-nginx-module"
license=('BSD')

source=(
	http://nginx.org/download/nginx-$_nginxver.tar.gz
	https://github.com/openresty/set-misc-nginx-module/archive/v$_setmiscver/set-misc-$_setmiscver.tar.gz
	https://github.com/simpl/ngx_devel_kit/archive/v$_ndkver/ngx_devel_kit-$_ndkver.tar.gz
)

sha256sums=('8793bf426485a30f91021b6b945a9fd8a84d87d17b566562c3797aba8fac76fb'
            '97946a68937b50ab8637e1a90a13198fe376d801dc3e7447052e43c28e9ee7de'
            '88e05a99a8a7419066f5ae75966fb1efc409bad4522d14986da074554ae61619')

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat \
	  --add-dynamic-module=../ngx_devel_kit-$_ndkver \
	  --add-dynamic-module=../set-misc-nginx-module-$_setmiscver
	make modules
}

package_nginx-mod-ndk() {
	pkgdesc="Nginx module that adds additional generic tools that module developers can use in their own modules"

	cd "$srcdir"/nginx-$_nginxver/objs
	install -Dm755 ndk_http_module.so "$pkgdir"/usr/lib/nginx/modules/ndk_http_module.so

	cd "$srcdir"/ngx_devel_kit-$_ndkver
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_nginx-mod-set-misc() {
	pkgdesc="Nginx module that adds various set_xxx directives"
	depends=("nginx=$_nginxver" 'nginx-mod-ndk')

	cd "$srcdir"/nginx-$_nginxver/objs
	install -Dm755 ngx_http_set_misc_module.so "$pkgdir"/usr/lib/nginx/modules/ngx_http_set_misc_module.so

	install -Dm644 "$srcdir"/set-misc-nginx-module-$_setmiscver/README.markdown \
	               "$pkgdir"/usr/share/licenses/$pkgname/README.markdown
}
