# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-njs
pkgver=0.7.4 # https://hg.nginx.org/njs/tags
pkgrel=2

_modname="${pkgname#nginx-mod-}"

pkgdesc='nginScript module for nginx'
arch=('x86_64')
depends=('nginx')
makedepends=('nginx-src')
url="https://nginx.org/en/docs/njs_about.html"
license=('CUSTOM')

source=(
  njs-$pkgver.tar.gz::https://hg.nginx.org/njs/archive/$pkgver.tar.gz
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('243045d638162b5b81a5f1081cf36ee7115e5caaad14c9104a464e5de0344bb0')

prepare() {
  mkdir -p build
  cd build
  ln -sf /usr/src/nginx/auto
  ln -sf /usr/src/nginx/src
}

build() {
  cd build
  /usr/src/nginx/configure \
    --with-compat \
    --with-stream \
    --with-http_ssl_module \
    --with-http_v2_module \
    --add-dynamic-module=../njs-$pkgver/nginx
  make modules
}

package() {
  install -Dm644 "$srcdir"/njs-$pkgver/LICENSE \
              "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd build/objs
  for mod in *.so; do
      install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done
}
