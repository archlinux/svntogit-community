# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-njs
pkgver=0.1.12 # https://hg.nginx.org/njs/tags
pkgrel=1

_modname="${pkgname#nginx-mod-}"
_nginxver=1.12.1

pkgdesc='nginScript module for nginx'
arch=('i686' 'x86_64')
depends=("nginx=$_nginxver")
url="https://nginx.org/en/docs/njs_about.html"
license=('CUSTOM')

source=(
	https://nginx.org/download/nginx-$_nginxver.tar.gz
	njs-$pkgver.tar.gz::https://hg.nginx.org/njs/archive/$pkgver.tar.gz
)

sha256sums=('8793bf426485a30f91021b6b945a9fd8a84d87d17b566562c3797aba8fac76fb'
            '8884507ef789ee36ee2635224df60b09746a37b20274b13b971dd306fe01bec0')

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat --with-stream --add-dynamic-module=../njs-$pkgver/nginx
	make modules
}

package() {
	install -Dm644 "$srcdir"/njs-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx-$_nginxver/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
