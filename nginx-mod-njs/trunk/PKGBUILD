# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-njs
pkgver=0.5.2 # https://hg.nginx.org/njs/tags
pkgrel=1

_modname="${pkgname#nginx-mod-}"

pkgdesc='nginScript module for nginx'
arch=('x86_64')
depends=('nginx')
makedepends=('nginx-src')
url="https://nginx.org/en/docs/njs_about.html"
license=('CUSTOM')

source=(
	njs-$pkgver.tar.gz::https://hg.nginx.org/njs/archive/$pkgver.tar.gz
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('719aa9ff5bf7d69789355fa95b1d7cfebbfcc6464d2b3504dca10529b5f83d04')

prepare() {
	mkdir -p build
	cd build
	ln -sf /usr/src/nginx/auto
	ln -sf /usr/src/nginx/src
}

build() {
	cd build
	/usr/src/nginx/configure --with-compat --with-stream --add-dynamic-module=../njs-$pkgver/nginx
	make modules
}

package() {
	install -Dm644 "$srcdir"/njs-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd build/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
