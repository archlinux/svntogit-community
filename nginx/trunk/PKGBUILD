# $Id$
# Maintainer: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Maintainer: Sébastien Luttringer
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Miroslaw Szot <mss@czlug.icis.pcz.pl>

pkgbase=nginx
pkgname=(nginx nginx-extra)
pkgver=1.4.2
pkgrel=1.2
arch=('i686' 'x86_64')
makedepends=('pcre' 'zlib' 'openssl' 'gd' 'geoip' 'passenger')
url='http://nginx.org'
license=('custom')
install=nginx.install
backup=('etc/nginx/fastcgi.conf'
        'etc/nginx/fastcgi_params'
        'etc/nginx/koi-win'
        'etc/nginx/koi-utf'
        'etc/nginx/mime.types'
        'etc/nginx/nginx.conf'
        'etc/nginx/scgi_params'
        'etc/nginx/uwsgi_params'
        'etc/nginx/win-utf'
        'etc/logrotate.d/nginx')
source=("http://nginx.org/download/nginx-$pkgver.tar.gz"
        'http://web.iti.upv.es/~sto/nginx/ngx_http_auth_pam_module-1.2.tar.gz'
        'service'
        'logrotate')
sha256sums=('5361ffb7b0ebf8b1a04369bc3d1295eaed091680c1c58115f88d56c8e51f3611'
            '5a85970ba61a99f55a26d2536a11d512b39bbd622f5737d25a9a8c10db81efa9'
            '05fdc0c0483410944b988d7f4beabb00bec4a44a41bd13ebc9b78585da7d3f9b'
            '272907d3213d69dac3bd6024d6d150caa23cb67d4f121e4171f34ba5581f9e98')

_base_options=(
    '--prefix=/etc/nginx'
    '--conf-path=/etc/nginx/nginx.conf'
    '--sbin-path=/usr/bin/nginx'
    '--pid-path=/run/nginx.pid'
    '--lock-path=/run/lock/nginx.lock'
    '--user=http'
    '--group=http'
    '--http-log-path=/var/log/nginx/access.log'
    '--error-log-path=/var/log/nginx/error.log'
    '--http-client-body-temp-path=/var/lib/nginx/client-body'
    '--http-proxy-temp-path=/var/lib/nginx/proxy'
    '--http-fastcgi-temp-path=/var/lib/nginx/fastcgi'
    '--http-scgi-temp-path=/var/lib/nginx/scgi'
    '--http-uwsgi-temp-path=/var/lib/nginx/uwsgi'
    '--with-imap'
    '--with-imap_ssl_module'
    '--with-ipv6'
    '--with-pcre-jit'
    '--with-file-aio'
    '--with-http_dav_module'
    '--with-http_gunzip_module'
    '--with-http_gzip_static_module'
    '--with-http_realip_module'
    '--with-http_spdy_module'
    '--with-http_ssl_module'
    '--with-http_stub_status_module'
)

prepare() {
    cp -a $pkgname-$pkgver $pkgname-extra-$pkgver
}

build() {
    msg2 'Build standard version'
    cd "$srcdir/$pkgname-$pkgver"
    ./configure "${_base_options[@]}"
    make

    msg2 'Build extra version'
    cd "$srcdir/$pkgname-extra-$pkgver"
    ./configure "${_base_options[@]}" \
        --with-http_addition_module \
        --with-http_degradation_module \
        --with-http_flv_module \
        --with-http_geoip_module \
        --with-http_image_filter_module \
        --with-http_mp4_module \
        --with-http_secure_link_module \
        --with-http_sub_module \
        --add-module=/usr/lib/passenger/ext/nginx \
        --add-module=../ngx_http_auth_pam_module-1.2
    make
}

package_nginx() {
    pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server'
    conflicts=('nginx-extra')
    depends=('pcre' 'zlib' 'openssl')

    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install

    sed -e 's|\<user\s\+\w\+;|user html;|g' \
        -e '44s|html|/usr/share/nginx/html|' \
        -e '54s|html|/usr/share/nginx/html|' \
        -i "$pkgdir"/etc/nginx/nginx.conf
    rm "$pkgdir"/etc/nginx/*.default

    install -d "$pkgdir"/var/lib/nginx
    install -dm700 "$pkgdir"/var/lib/nginx/proxy

    chmod 750 "$pkgdir"/var/log/nginx
    chown http:log "$pkgdir"/var/log/nginx

    install -d "$pkgdir"/usr/share/nginx
    mv "$pkgdir"/etc/nginx/html/ "$pkgdir"/usr/share/nginx

    install -Dm644 "$srcdir"/logrotate "$pkgdir"/etc/logrotate.d/nginx
    install -Dm644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/nginx.service
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rmdir "$pkgdir/run"
}

package_nginx-extra() {
    package_nginx

    pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server with extra modules'
    conflicts=('nginx')
    depends=('pcre' 'zlib' 'openssl' 'gd' 'geoip')
}

# vim:set ts=4 sw=4 et:
