# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=ngrep
pkgver=1.45
pkgrel=8
pkgdesc='A grep-like utility that allows you to search for network packets on an interface.'
arch=('i686' 'x86_64')
url='http://ngrep.sourceforge.net/'
license=('custom')
depends=('libpcap>=1.0.0')
# Fix build failure when using "-j" (FS#27091).
options=(!makeflags)
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2" "ngrep-fix-ipv6-support.patch")
md5sums=('bc8150331601f3b869549c94866b4f1c' '90e6dfb335bccf12615d3c468ceeb60f')
# Added patch for broken ipv6 support (FS#27225)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/ngrep-fix-ipv6-support.patch

  ./configure --prefix=/usr --with-pcap-includes=/usr/include/pcap --enable-ipv6
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
