# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Luis Henrique <lmello.009@gmail.com>

pkgname=ngspice
pkgver=34
pkgrel=1
pkgdesc='Mixed-level/Mixed-signal circuit simulator based on Spice3f5, Ciber1b1, and Xspice.'
url='http://ngspice.sourceforge.net'
license=('BSD')
arch=('x86_64')
depends=('libxaw' 'libedit' 'gcc-libs' 'readline')
source=("https://downloads.sourceforge.net/project/$pkgname/ng-spice-rework/$pkgver/$pkgname-$pkgver.tar.gz"
        "http://ngspice.sourceforge.net/docs/ngspice-$pkgver-manual.pdf"
        "https://github.com/imr/ngspice/commit/8abd8069a8.diff")  # sf's git doesn't let you pull patches
sha1sums=('b0aa799133921acea130119271e7495bf0882440'
          'c03b965f8b1ccbbc4707bda64cc6f9446fceaca0'
          '786c3e45a041940d5c29262823147ad3b3dce455')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # remove on v35 (https://sourceforge.net/p/ngspice/bugs/539/)
  sed -i 's|int  cp_maxhistlength|//&|' ../8abd8069a8.diff
  patch -Np1 -i ../8abd8069a8.diff
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # FS#45230, create so lib
  # shared lib sets flags and modifies headers, needs dedicated pass
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-readline=yes \
              --enable-xspice \
              --enable-cider \
              --enable-openmp \
              --enable-oldapps \
              --with-ngshared
  make
  #strip ./src/.libs/libngspice.so.0.0.0

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --with-readline=yes \
              --enable-xspice \
              --enable-cider \
              --enable-oldapps \
              --enable-openmp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644  "$srcdir/ngspice-$pkgver-manual.pdf" "$pkgdir/usr/share/doc/$pkgname/manual.pdf"

  install -Dm755 src/.libs/libngspice.so.0.0.0 "$pkgdir/usr/lib/libngspice.so.0.0.0"
  ln -s "libngspice.so.0.0.0" "$pkgdir/usr/lib/libngspice.so.0"
  ln -s "libngspice.so.0.0.0" "$pkgdir/usr/lib/libngspice.so"
}
