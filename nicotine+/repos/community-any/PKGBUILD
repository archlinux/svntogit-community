# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=nicotine+
pkgver=3.2.2
pkgrel=2
pkgdesc='Soulseek music-sharing client, written in python'
arch=(any)
url='https://github.com/Nicotine-Plus/nicotine-plus'
license=(GPL)
depends=(python-gobject gtk3 gspell libayatana-appindicator)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://github.com/Nicotine-Plus/nicotine-plus/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('f58286f4dec6febfeab075cdfe2d2fc35c58349a504f7d393ec129df132ad40f')

build() {
  cd nicotine-plus-$pkgver
  
  python -m build --wheel --no-isolation
}

package() {
  cd nicotine-plus-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
}
