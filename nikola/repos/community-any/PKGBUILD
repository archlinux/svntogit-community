# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Nikola
pkgname=nikola
pkgver=8.1.3
pkgrel=2
pkgdesc="A static site and blog generator"
arch=('any')
url="https://getnikola.com/"
license=('MIT')
# still requires python-setuptools due to the use of pkg_resources
depends=('python-babel' 'python-blinker' 'python-doit' 'python-dateutil'
'python-docutils' 'python-logbook' 'python-lxml' 'python-mako'
'python-markdown' 'python-natsort' 'python-piexif' 'python-pillow'
'python-pygments' 'python-pyrss2gen' 'python-requests' 'python-setuptools'
'python-unidecode' 'python-yapsy')
makedepends=('jupyter-notebook' 'python-aiohttp' 'python-ghp-import'
'python-husl' 'python-ipykernel' 'python-jinja' 'python-micawber'
'python-phpserialize' 'python-pygal' 'python-pyphen' 'python-ruamel-yaml'
'python-toml' 'python-typogrify' 'python-watchdog' 'zeromq')
checkdepends=('python-freezegun' 'python-pytest')
optdepends=('jupyter-notebook: for .ipynb support'
            'python-aiohttp: for `nikola auto`'
            'python-ghp-import: for upload to Github pages'
            'python-hsluv: for section color mixing'
            'python-html5lib: for HTML5 minify and format support'
            'python-husl: for optional color support'
            'python-ipykernel: for .ipynb support'
            'python-jinja: for jinja2 based themes'
            'python-micawber: for embedding media in posts'
            'python-phpserialize: for Wordpress import'
            'python-pygal: for SVG graph plotting'
            'python-pyphen: for hyphenation support'
            'python-ruamel-yaml: for YAML support'
            'python-toml: for TOML support'
            'python-typogrify: for typographical enhancements'
            'python-watchdog: for `nikola auto`')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('7b6a8391d023a339125392082cc3d2ea2a2fe25b8938bd82b8c9efe0cbb3e8b83537f1bd00d97a648e3fc2b096e673702026fa3942ab9c97e76bf21b5afefe16'
            'SKIP')
b2sums=('535870b7cd147169b511166186ac200ab9d31d976c58363e42235e07ea8ba68e01738d378190663864e6a8605986771500569004e7a73ea043ae8982be09afe1'
        'SKIP')
validpgpkeys=('DF32C99CFF84282CF9D4CF7171023DBD5EAAEA16') # Chris Warrick <kwpolska@gmail.com>

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # remove options to pytest so that we don't require python-coverage et. al.
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  # shell completions
  PYTHONPATH="build" \
  python -m "${pkgname}" tabcompletion \
    --shell bash \
    --hardcode-tasks \
    > "${pkgname}_bash"
  PYTHONPATH="build" \
  python -m "${pkgname}" tabcompletion \
    --shell zsh \
    --hardcode-tasks \
    > "${pkgname}_zsh"
  # ensure that the shell completion is assigned to the correct executable
  sed -e "s/__main__.py/${pkgname}/g" -i "${pkgname}_"{bash,zsh}
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -k "not test_compiling_markdown"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE.txt \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "${pkgname}_bash" \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -vDm 644 "${pkgname}_zsh" \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
