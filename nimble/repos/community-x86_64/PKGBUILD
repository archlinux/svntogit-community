# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=nimble
pkgver=0.13.1
pkgrel=1
epoch=1
pkgdesc='Package manager for the Nim programming language'
url='https://github.com/nim-lang/nimble'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl')
makedepends=('nim')
source=(https://github.com/nim-lang/nimble/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('d13fc755e2018911faa1039ca6c73a3ecaa7aa57fb57faee9babb750155ac5e73be4f60110ee64cdd56874245f7c046d6c825356fd84a8fcbba9683db2406520')
b2sums=('47b5d4b9b68238433c11d75283ced572ba04ebb1495ecc9a07b59c40f68907bd76f46b02987d454bbc51d8298e476699bfe8a49ea8e1007bbfa2505b0df0b0b3')

build() {
  cd ${pkgname}-${pkgver}
  nim c -d:release src/nimble
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 src/nimble -t "${pkgdir}/usr/bin"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # Nimble looks for nimscriptapi.nim in /usr/bin/nimblepkg/, of all places.
  cp -r src/nimblepkg "${pkgdir}/usr/share/${pkgname}"
  ln -s /usr/share/${pkgname} "${pkgdir}/usr/bin/nimblepkg"

  # completions
  install -Dm 644 nimble.bash-completion "${pkgdir}/usr/share/bash-completion/completions/nimble"
  install -Dm 644 nimble.zsh-completion "${pkgdir}/usr/share/zsh/site-functions/_nimble"
}

# vim: ts=2 sw=2 et:
