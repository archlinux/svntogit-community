# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=nimble
pkgver=0.8.8
pkgrel=1
epoch=1
pkgdesc='Package manager for the Nim programming language'
url='https://github.com/nim-lang/nimble'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('nim')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/nim-lang/nimble/archive/v${pkgver}.tar.gz)
sha256sums=('4a849370f58af0d6fe940c15536ab9487f75517c695dd845f84ad96e71f2f4bd')
sha512sums=('4201ed0856b26505e83aa885624065a0200dead9cbc0d5bc0ba750da597b9c37708c1783f7fe518d2bdff954b53ccbc9f92d723f2ce41f4799c8fb9d895c1c6e')

build() {
  cd ${pkgname}-${pkgver}
  nim c -d:release src/nimble
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 src/nimble -t "${pkgdir}/usr/bin"
  install -Dm 644 license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Nimble looks for nimscriptapi.nim in /usr/bin/nimblepkg/, of all places.
  cp -r src/nimblepkg "${pkgdir}/usr/share/${pkgname}"
  ln -s /usr/share/${pkgname} "${pkgdir}/usr/bin/nimblepkg"
}

# vim: ts=2 sw=2 et:
