# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Dominik Picheta <morfeusz8@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jesus Alvarez <jeezusjr@gmail.com>

pkgname=nimrod
pkgver=0.9.6
pkgrel=1
pkgdesc='Imperative, multi-paradigm, compiled programming language'
arch=('x86_64' 'i686')
url='http://nimrod-code.org/'
license=('MIT')
makedepends=('git')
options=('!emptydirs')
source=("$pkgname::git://github.com/Araq/${pkgname/n/N}.git#commit=b0179f9af5")
#source=("$pkgname::git://github.com/Araq/${pkgname/n/N}.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  rm build/empty.txt && rmdir build
  git clone --depth=1 git://github.com/nimrod-code/csources build
}

build() {
  cd "$pkgname/build"
  sh build.sh
  cd ..

  ./bin/nimrod c koch
  ./koch boot -d:release -d:useGnuReadline

  export PATH="$srcdir/$pkgname/bin:$PATH"

  cd lib
  nimrod c --app:lib -d:createNimRtl -d:release nimrtl.nim

  cd ../tools
  nimrod c -d:release nimgrep.nim
}

package() {
  cd "$pkgname"

  export PATH="$srcdir/$pkgname/bin:$PATH"
  
  ./koch install "$pkgdir"

  install -d "$pkgdir/usr/share/nimrod/doc" "$pkgdir/usr/lib/nimrod" "$pkgdir/etc" "$pkgdir/usr/bin"

  cd "$pkgdir/nimrod"
  mv "lib/"* "$pkgdir/usr/lib/nimrod/"
  mv "config/"* "$pkgdir/etc/"
  cp -a "$srcdir/$pkgname/lib/packages" "$pkgdir/usr/lib/nimrod/"
  mv "bin/"* "$pkgdir/usr/bin/"

  cd "$srcdir/$pkgname"
  rm -r "$pkgdir/nimrod"
  mv examples web "$pkgdir/usr/share/nimrod/doc/"
  mv "doc/"* "$pkgdir/usr/share/nimrod/doc/"
  install -m644 "lib/libnimrtl.so" "$pkgdir/usr/lib/libnimrtl.so"
  install -m755 "tools/nimgrep" "$pkgdir/usr/bin/"
  install -Dm644 "copying.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
