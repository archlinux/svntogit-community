# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Dominik Picheta <morfeusz8@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=nimrod
pkgver=0.9.2
pkgrel=3
pkgdesc='Statically typed, imperative programming language, that supports procedural, functional, object oriented and generic programming styles while remaining simple and efficient'
arch=('x86_64' 'i686')
url='http://nimrod-code.org/'
license=('MIT')
depends=('readline')
makedepends=('setconf')
source=("http://nimrod-code.org/download/${pkgname}_$pkgver.zip")
sha256sums=('4903ff46b90caa61194551f66fdff291b1ecba8bb4eec1611373cd34ecaf7090')

prepare() {
  cd "$pkgname"

  setconf install.sh bindir "$pkgdir/usr/bin"
  setconf install.sh configdir "$pkgdir/etc"
  setconf install.sh libdir "$pkgdir/usr/lib/$pkgname"
  setconf install.sh docdir "$pkgdir/usr/share/doc/$pkgname"
  setconf install.sh datadir "$pkgdir/usr/share/$pkgname"
}

build() {
  cd "$pkgname"

  sh build.sh
  ./bin/nimrod c -d:release koch
  PATH="$PATH":./bin/nimrod ./koch boot -d:release -d:useGnuReadline
}

package() {
  cd "$pkgname"

  install -d "$pkgdir/usr/bin"
  install -d "$pkgdir/etc"
  install -d "$pkgdir/usr/lib/$pkgname"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -d "$pkgdir/usr/share/$pkgname"
  sh install.sh /usr/bin
  install -Dm644 copying.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
