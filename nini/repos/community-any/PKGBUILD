# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: MutantMonkey <mutantmonkey@gmail.com>
# Contributor: Madek <gonzaloseguel@gmail.com>

pkgname=nini
_pkgname=Nini
pkgver=1.1.0
pkgrel=4
pkgdesc="An uncommonly powerful .NET configuration library designed to help build highly configurable applications quickly"
arch=('any')
url="http://nini.sourceforge.net/"
license=('MIT')
depends=('mono')
source=(http://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.zip
        $pkgname-1.1.pc 
        $_pkgname.snk.txt)
md5sums=('63384f75fabd401cff1500f738d452f4'
         '6e2cabea421a72247d4f1cdf1ffac9b6'
         '1cfbb58985d299127dbb5a8de59a6ce5')

build() {
  cd "$srcdir/$_pkgname"

  base64 -d ../$_pkgname.snk.txt>$_pkgname.snk
  gmcs -t:library -out:Bin/$_pkgname.dll -keyfile:$_pkgname.snk \
       -r:System,System.Data,System.Xml \
       Source/{AssemblyInfo,Config/*,/Ini/*,/Util/*}.cs
}

package() {
  cd "$srcdir/$_pkgname"

  gacutil -i Bin/$_pkgname.dll -package $_pkgname -root $pkgdir/usr/lib/
  install -Dm644 $srcdir/$pkgname-1.1.pc $pkgdir/usr/lib/pkgconfig/$pkgname-1.1.pc
  install -Dm644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}
