# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>
# Contributor: Mika Fischer <mika.fischer@zoopnet.de>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=ninja
pkgver=1.5.1
pkgrel=1
pkgdesc='Small build system with a focus on speed'
arch=(i686 x86_64)
url='http://martine.github.io/ninja/'
license=(Apache)
depends=(gcc-libs)
makedepends=(asciidoc python2 re2c emacs-nox)
checkdepends=(gtest)
source=(https://github.com/martine/ninja/archive/v$pkgver.zip)
sha1sums=('b5fdd88d1766d5abf9623a79ebfbc49f62d91ca3')

build() {
  cd ninja-$pkgver

  python2 ./bootstrap.py
  emacs -Q --batch -f batch-byte-compile misc/ninja-mode.el
  asciidoc doc/manual.asciidoc
}

check() {
  cd ninja-$pkgver

  python2 ./configure.py --with-gtest=/usr/src/gtest
  ./ninja ninja_test
  ./ninja_test --gtest_filter=-SubprocessTest.SetWithLots
}

package() {
  cd ninja-$pkgver

  install -m755 -D ninja "$pkgdir/usr/bin/ninja"
  install -m644 -D doc/manual.asciidoc "$pkgdir/usr/share/doc/ninja/manual.asciidoc"
  install -m644 -D doc/manual.html "$pkgdir/usr/share/doc/ninja/manual.html"

  install -m644 -D misc/ninja-mode.el "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.el"
  install -m644 -D misc/ninja-mode.elc "$pkgdir/usr/share/emacs/site-lisp/ninja-mode.elc"

  install -m644 -D misc/bash-completion "$pkgdir/usr/share/bash-completion/completions/ninja"
  install -m644 -D misc/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_ninja"
}
