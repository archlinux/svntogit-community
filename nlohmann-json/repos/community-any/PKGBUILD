# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nlohmann-json
# upstream doesn't use annotated tags
_commit=db78ac1d7716f56fc9f1b030b715f872f93964e4 # v3.9.1
pkgver=3.9.1
pkgrel=2
# tests are tracked in a separate repo
_test_pkgver=3.0.0
pkgdesc="JSON for Modern C++"
arch=('any')
url="https://github.com/nlohmann/json"
license=('MIT')
makedepends=('cmake' 'git')
source=("${pkgname}::git+https://github.com/nlohmann/json#commit=${_commit}?signed"
        "json_test_data-${_test_pkgver}.tar.gz::https://github.com/nlohmann/json_test_data/archive/v${_test_pkgver}.tar.gz")
sha512sums=('SKIP'
            'd9af8419b837c592ec7519cd5772651c761078a9c43cf2a309cee55c323aee0df0c233fb58a07d5ee2e77492ac8b16398de234b387eae037a60e3c9ba5b08891')
b2sums=('SKIP'
        'f0a47b41805bf1426f612e9a82efea2a3e5b1c15740c1c531d859e60dc5daeb85209b4fe363fd8fb84e3bbf01a2578c74538ba3e769726494047979f5a4d468d')
validpgpkeys=('797167AE41C0A6D9232E48457F3CEA63AE251B69') # Niels Lohmann <mail@nlohmann.me>

prepare() {
  mkdir -vp "$pkgname/build"
  mv -v "json_test_data-${_test_pkgver}/" "$pkgname/build/json_test_data/"
}

build() {
  cd "$pkgname"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_TESTING=ON \
        -DJSON_BuildTests=ON \
        -DJSON_MultipleHeaders=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}"
  make -k test -C build
}

package() {
  cd "$pkgname"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {CODE_OF_CONDUCT,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
