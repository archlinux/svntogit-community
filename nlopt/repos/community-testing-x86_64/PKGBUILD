# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=nlopt
pkgver=2.6.2
pkgrel=2
pkgdesc="Nonlinear optimization library"
arch=(x86_64)
url="https://nlopt.readthedocs.io/en/latest/"
license=(LGPL)
depends=(gcc-libs)
makedepends=(cmake python-numpy octave guile swig)
optdepends=('python-numpy: to use with python')
source=("https://github.com/stevengj/nlopt/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('cfa5981736dd60d0109c534984c4e13c615314d3584cf1c392a155bfe1a3b17e')

build() {
    cmake -B build -S ${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make -C build
}

check() {
    cd build
    ctest --output-on-failure
}

package() {
    make -C build DESTDIR="${pkgdir}" install
}
