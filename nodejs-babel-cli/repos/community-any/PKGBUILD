# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

_npmname=babel-cli
pkgname=nodejs-$_npmname
pkgver=6.5.1
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$_npmname/-/$_npmname-$pkgver.tgz"
        LICENSE)
noextract=($_npmname-$pkgver.tgz)
sha256sums=('af66cd37450609881969bfa0068ff949e7b5fc963f756b19a5ae18a59d853e42'
            '8a5a22eb7991f5a022b91273b2395c2342f56fb43deddd29c248efb40c8ad62c')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$_npmname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  find "$pkgdir/usr" -type f -exec chmod 644 '{}' +
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
  chmod 755 "$pkgdir/usr/bin"/*
}
