# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

_npmname=babel-cli
pkgname=nodejs-$_npmname
pkgver=6.8.0
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs' 'semver')
makedepends=('npm')
source=("https://registry.npmjs.org/$_npmname/-/$_npmname-$pkgver.tgz"
        LICENSE)
noextract=($_npmname-$pkgver.tgz)
sha256sums=('f3062bd5dff18ee8254b130aa2a5ff9268388771ee725aef53b4c1195875509e'
            '8a5a22eb7991f5a022b91273b2395c2342f56fb43deddd29c248efb40c8ad62c')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$_npmname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  # Experimental dedup
  for dir in $_npmname/node_modules $_npmname/node_modules/semver-truncate/node_modules; do
    cd "$pkgdir"/usr/lib/node_modules/$dir
    for dep in semver; do
      rm -r $dep;
      npm link $dep;
    done
  done
}
