# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

pkgname=nodejs-lts-argon
pkgver=4.8.5
pkgrel=2
pkgdesc='Evented I/O for V8 javascript (LTS release: Argon)'
arch=('i686' 'x86_64')
url='https://nodejs.org/'
license=('MIT')
depends=('openssl-1.0' 'zlib' 'icu' 'libuv' 'http-parser')
makedepends=('python2' 'procps-ng')
optdepends=('npm: nodejs package manager')
provides=("nodejs=${pkgver}")
conflicts=('nodejs')
source=("${url}/dist/v${pkgver}/node-v${pkgver}.tar.xz"
        'nodejs-v8-icu59.patch')
sha256sums=('a48aeefc2b4fae0ea6410a8045ca106af244223012eab003bd5cb00aa3b50d53'
            '3b4d747522f655054829a67b5358f6f1c8bcbb2845f8e0f9866c9c78e55e1be8')

prepare() {
  cd node-v${pkgver}

  patch -p1 -i ../nodejs-v8-icu59.patch

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e 's_^\( *exec \+\)python\( \+.*\)$_\1python2\2_'\
    -e 's_^\(.*\)python\( \+-c \+.*\)$_\1python2\2_'\
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
}

build() {
  cd node-v${pkgver}
  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --shared-http-parser

  make
}

check() {
  cd node-v${pkgver}
  # At least two expected failures because of OpenSSL 1.1 vs 1.0
  make test || warning "Tests failed"
}

package() {
  cd node-v${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
