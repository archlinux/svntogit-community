# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=nodejs-lts-erbium
pkgver=12.22.7
pkgrel=2
pkgdesc="Evented I/O for V8 javascript (LTS release: Erbium)"
arch=(x86_64)
url="https://nodejs.org/"
license=(MIT)
depends=(openssl zlib icu libuv c-ares brotli libnghttp2) # http-parser
makedepends=(python procps-ng)
optdepends=('npm: nodejs package manager')
provides=("nodejs=$pkgver")
conflicts=(nodejs)
source=("${url}"/dist/v${pkgver}/node-v${pkgver}.tar.xz
        nodejs-shared-libuv-uvwasi.patch::https://patch-diff.githubusercontent.com/raw/nodejs/node/pull/39260.patch
        python3.patch
        ares_compat.patch
        libnghttp1.45.1_compat.patch)
# https://nodejs.org/download/release/latest-erbium/SHASUMS256.txt.asc
sha256sums=(cc6a23b44870679a94bd8f3c8d4e1f4b77bb2712a36888ab87463459e6785f6b
            05692e382d71b5b92d5b86d513b9bb01750548630506ffe5b6f1920cbc5861bd
            20a0f718c94a4686dfc66edd994788759cbda65734cbc52082884f6d0913477d
            b8e79d3495f425079352f9e95c753b257e4d9b8ecdca468660711e6da0efa6bf
            81f8a060b40786c02de400620da4060483d910fe7a6b68d8328f3747caf54619)
validpgpkeys=(C4F0DFFF4E8C1A8236409D08E73BC641CC11F4C8  # Myles Borins <mylesborins@google.com>
              77984A986EBC2AA786BC0F66B01FBB92821C587A  # Gibson Fahnestock <gibfahn@gmail.com>
              B9AE9905FFD7803F25714661B63B535A4C206CA9  # Evan Lucas <evanlucas@me.com>
              DD8F2338BAE7501E3DD5AC78C273792F7D83545D  # Rod Vagg <rod@vagg.org>
              B9E2F5981AA6E0CD28160D9FF13993A75599653C  # Shelley Vohr <shelley.vohr@gmail.com>
              4ED778F539E3634C779C87C6D7062848A1AB005C  # Beth Griggs <Bethany.Griggs@uk.ibm.com>
              C82FA3AE1CBEDC6BE46B9360C43CEC45C17AB93C  # Richard Lau <rlau@redhat.com> 
              74F12602B6F1C4E913FAA37AD3A89613643B6201) # Danielle Adams <adamzdanielle@gmail.com>

prepare() {
  cd node-v${pkgver}
  patch -p1 < ../python3.patch
  patch -p1 < ../nodejs-shared-libuv-uvwasi.patch
  patch -p1 < ../ares_compat.patch
  patch -p1 < ../libnghttp1.45.1_compat.patch
}

build() {
  cd node-v${pkgver}

  ./configure \
    --prefix=/usr \
    --with-intl=system-icu \
    --without-npm \
    --shared-openssl \
    --shared-zlib \
    --shared-libuv \
    --experimental-http-parser \
    --shared-cares \
    --shared-brotli \
    --shared-nghttp2
    #--shared-http-parser \

  make
}

check() {
  cd node-v${pkgver}
  make test-only
}

package() {
  cd node-v${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
