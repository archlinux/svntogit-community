# Maintainer: Felix Yan <felixonmars@archlinux.org>

_npmname=source-map
pkgname=nodejs-$_npmname
pkgver=0.7.3
pkgrel=2
pkgdesc="Library to generate and consume the source map format"
arch=('any')
url="https://github.com/mozilla/source-map"
license=('BSD')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$_npmname/-/$_npmname-$pkgver.tgz")
noextract=($_npmname-$pkgver.tgz)
sha256sums=('ad69b787ba774733c3e4f0eb20403df7b1fa466252d453533dbcc6a5ad15833e')
options=("!strip")

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$_npmname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s ../../../lib/node_modules/source-map/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
