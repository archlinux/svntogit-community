# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=0.10.0
pkgrel=1
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('openssl' 'python2')
checkdepends=('curl') # curl used for check()
options=('!emptydirs')
source=(http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz)
sha256sums=('1624dc37866ebfb5431e3393e6b049cf238cac8ad4d20c6d567263b1259177ab')

build() {
  cd node-v${pkgver}

  msg 'Fixing for python2 name'
  find -type f -exec sed \
    -e 's_^#!/usr/bin/env python$_&2_' \
    -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' \
    -e 's_^#!/usr/bin/python$_&2_' \
    -e "s_'python'_'python2'_" -i {} \;
  find test/ -type f -exec sed 's_python _python2 _' -i {} \;
  export PYTHON=python2

  ./configure \
    --prefix=/usr \
    --shared-openssl

  make
}

check() {
  cd node-v${pkgver}
  make test || true
}

package() {
  cd node-v${pkgver}

  make DESTDIR=$pkgdir install

  # install docs as per user request
  install -d $pkgdir/usr/share/doc/nodejs
  cp -r doc/api/*.html \
    $pkgdir/usr/share/doc/nodejs

  install -D -m644 LICENSE \
    $pkgdir/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
