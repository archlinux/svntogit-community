# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=0.8.2
pkgrel=3
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2')
checkdepends=('curl') # curl used for check()
optdepends=('openssl: TLS support')
options=('!emptydirs')
source=("http://nodejs.org/dist/v${pkgver}/node-v${pkgver}.tar.gz")
md5sums=('2d1a74aa9e5ecfdb4c64d200b642eb1e')

build() {
  cd node-v${pkgver}

  msg 'fixing for python2 name'
  find -type f -exec sed -e 's_^#!/usr/bin/env python$_&2_' -e 's_^\(#!/usr/bin/python2\).[45]$_\1_' -e 's_^#!/usr/bin/python$_&2_' -e "s_'python'_'python2'_" -i {} \;
  find test -type f -exec sed -e "s|python |python2 |" -i {} \;
  #sed -i "s|cmd_R = 'python |cmd_R = 'python2 |" wscript
  sed -i "s|python |python2 |" Makefile
  #find test -type f -exec sed -e 's/python/&2/' -i {} \;
  #sed -i "s/python/python2/" configure

  export PYTHON=python2

  ./configure \
    --prefix=/usr \
    --shared-openssl

  make
}

check() {
  cd node-v${pkgver}

  # test failures in 0.6 are known
  # specifically https://github.com/joyent/node/pull/1699
  make test || true
}

package() {
  cd node-v${pkgver}

  make DESTDIR=${pkgdir} install

  # install docs as per user request
  install -d ${pkgdir}/usr/share/doc/nodejs
  cp -r doc/api/*.html \
    ${pkgdir}/usr/share/doc/nodejs

  # compress man pages
  find $pkgdir/usr/lib/node_modules/npm/man -type f | xargs gzip -9

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
