# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>

pkgname=noise
pkgver=0.3.0
pkgrel=2
pkgdesc='The Pantheon Music Player'
arch=('i686' 'x86_64')
url='https://launchpad.net/noise'
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'gst-plugins-base-libs' 'json-glib' 'libgpod' 'libnotify' 'libpeas' 'libsoup' 'sqlheavy' 'taglib')
makedepends=('cmake' 'vala')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname}/${pkgver%.?}.x/${pkgver}/+download/${pkgname}-${pkgver}.tgz"
        'mesa-demos-conflict.patch')
sha256sums=('56d9dc11e376f0727748e5f60ad35555626314ecead6d62139a56ab487fdf7cf'
            'df2c77d7da40126b8957e40924f8d07f8e722cfae27d7f59eb3fa307768a3620')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../mesa-demos-conflict.patch
  sed 's|${CMAKE_INSTALL_PREFIX}/${PLUGIN_DIR_UNPREFIXED}|${PLUGIN_DIR_UNPREFIXED}|' -i CMakeLists.txt
}

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
           -DGSETTINGS_COMPILE='OFF'
  make -j1
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
