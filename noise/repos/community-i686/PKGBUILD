# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>

pkgname=noise
pkgver=0.2.4
pkgrel=2
pkgdesc="Simple, fast, and good looking music player"
arch=('i686' 'x86_64')
url="https://launchpad.net/noise"
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'gstreamer0.10-base' 'json-glib' 'libgpod' 'libnotify' 'libpeas' 'libsoup' 'sqlheavy' 'taglib')
makedepends=('cmake' 'vala')
optdepends=('elementary-icon-theme: Missing UI icons'
            'gstreamer0.10-base-plugins: "Base" plugin libraries'
            'gstreamer0.10-good-plugins: "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: "Ugly" plugin libraries')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname}/${pkgver%.?}/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('ffcd5f2cba112af13707a7ba7f0ceace7b812d4b6082671684ff78d76fc1d8a9')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
