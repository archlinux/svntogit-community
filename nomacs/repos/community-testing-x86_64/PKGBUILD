# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_pkgver=3.12.0
pkgname=nomacs
pkgver=3.12
pkgrel=3
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('desktop-file-utils' 'exiv2' 'libraw' 'opencv' 'quazip' 'qt5-svg')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${_plugins_pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${_plugins_pkgver}.tar.gz")
sha512sums=('4508354d48a6115cd40f701cc90cbbcb84490e99eaac0295db48f6fea99550d5e2b3e7365128d0c9cc4eeb621906f55e275603d84cb8b6bb164d0b5e2e192119'
            'b46a2523737893bd342e90331d22082a84f3ff9abf4fdcf360d3fb9f2724cf616a6d77eacc272f117e103a9978f99b70feb8364f4c818681a147f78bfe356032')

prepare() {
  cd "${pkgname}-${pkgver}"
  # move plugins
  mv "${srcdir}/${pkgname}-plugins-${_plugins_pkgver}" "ImageLounge/plugins"

  # fix build with openCV 4
  sed -e 's|CV_DIST_C|cv::DIST_C|g' -e 's|CV_RGB2HSV|cv::COLOR_RGB2HSV|g' -e 's|CV_HSV2RGB|cv::COLOR_HSV2RGB|g' \
      -e 's|CV_RGB2Lab|cv::COLOR_RGB2Lab|g' -e 's|CV_INTER_AREA|cv::INTER_AREA|g' -e 's|CV_RETR_LIST|cv::RETR_LIST|g' \
      -e 's|CV_CHAIN_APPROX_SIMPLE|cv::CHAIN_APPROX_SIMPLE|g' -e 's|CV_RGB2GRAY|cv::COLOR_RGB2GRAY|g' \
      -i ImageLounge/plugins/FakeMiniaturesPlugin/src/*.cpp -i ImageLounge/plugins/PageExtractionPlugin/src/*.cpp
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake ./ImageLounge -DCMAKE_INSTALL_PREFIX=/usr \
                      -DUSE_SYSTEM_QUAZIP=ON \
                      -DENABLE_TRANSLATIONS=true
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
