# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=3.8.0
pkgrel=6
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${pkgver}.tar.gz"
        "${pkgname}-no-coverage.patch")
sha512sums=('90ffd1ea7f6d8e2f07b2b093ebc7cfec682f11b874d95e249f3d35a2ceb8b75e7a0c55f19c978231c3c8342abe5cb251359771fd90aa39b50ee0a6d1514e88e1'
            '7fa43377a908912d0eafddd45734ff5698739fd9bee5e87b24a154ea448c81270087f22b61e89f95e71511b1d0ab5e3fceef567d34a4c64a41708df2313e6879'
            '321308d9b7dbc5d2ffa00cecb6f20a66dd39767239bc87b6d8aec0997a2a65872e1447c6ea2527038a5e332f32962e1fa9e09c83b93cf34ac0e03733479b36bb')

prepare() {
  cd "${pkgname}-${pkgver}"

  # move plugins
  mv "${srcdir}/${pkgname}-plugins-${pkgver}" "ImageLounge/plugins"

  # disable code coverage:
  # https://github.com/nomacs/nomacs/issues/170
  patch -p1 -i "../${pkgname}-no-coverage.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake ./ImageLounge -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
