# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_pkgver=3.12.0
pkgname=nomacs
pkgver=3.12
pkgrel=4
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('desktop-file-utils' 'exiv2' 'libraw' 'opencv' 'quazip' 'qt5-svg')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${_plugins_pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${_plugins_pkgver}.tar.gz"
        "opencv4.patch::https://github.com/${pkgname}/${pkgname}-plugins/commit/7fdb4371135909bf5a2ba0caac2370e2a1a1c692.patch")
sha512sums=('4508354d48a6115cd40f701cc90cbbcb84490e99eaac0295db48f6fea99550d5e2b3e7365128d0c9cc4eeb621906f55e275603d84cb8b6bb164d0b5e2e192119'
            'b46a2523737893bd342e90331d22082a84f3ff9abf4fdcf360d3fb9f2724cf616a6d77eacc272f117e103a9978f99b70feb8364f4c818681a147f78bfe356032'
            '456bb01e88974c4bad1b9ad0c24857a473777c78755adfcfbebbe0377db5e756c08321c462b9a56eb141dccb7da80582a4a6c17ebc0af80c229982c532c32ccb')

prepare() {
  cd "${pkgname}-${pkgver}"
  # move plugins
  mv "${srcdir}/${pkgname}-plugins-${_plugins_pkgver}" "ImageLounge/plugins"

  # upstream patch for opencv >= 4.0.0:
  # https://github.com/nomacs/nomacs-plugins/issues/26
  cd ImageLounge/plugins
  patch -Np1 -i "${srcdir}/opencv4.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake ./ImageLounge -DCMAKE_INSTALL_PREFIX=/usr \
                      -DUSE_SYSTEM_QUAZIP=ON \
                      -DENABLE_TRANSLATIONS=true
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
