# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=3.0.0
pkgrel=2
pkgdesc="A Qt image viewer"
arch=(i686 x86_64)
url="http://www.nomacs.org/"
license=('GPL3')
#depends=('qt4' 'exiv2' 'libraw' 'opencv')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools')
install="$pkgname.install"
source=("https://github.com/nomacs/nomacs/releases/download/$pkgver/nomacs-$pkgver-source.tar.bz2"
        "https://github.com/nomacs/nomacs-plugins/archive/207cb0f5fb13907bf1ccf3549ad72768841cec63.zip"
        nomacs-opencv3.patch)
md5sums=('e1630a4371d0e0f8aba9358ab20d43e5'
         'bbe351379e24c51e691b390a9c867719'
         '0b51fcf4de8ee1debd1b8fadef2dd11d')

prepare() {
  mv nomacs-plugins-207cb0f5fb13907bf1ccf3549ad72768841cec63 $pkgname-$pkgver/plugins
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b

# Fix opencv 3 detection (Gentoo)
  patch -p2 -i ../nomacs-opencv3.patch
}

build() {
  cd $pkgname-$pkgver/b
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make -j1
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
