# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jorge Mokross <mokross@gmail.com>

pkgname=notify-sharp
pkgver=0.4.1
pkgrel=2
pkgdesc="C Sharp D-Bus client library for desktop notifications"
arch=('any')
url="https://www.meebey.net/projects/notify-sharp/"
license=('MIT')
depends=('gtk-sharp-2' 'dbus-sharp-glib') 
provides=('notify-sharp-svn')
replaces=('notify-sharp-svn')
source=(https://www.meebey.net/projects/notify-sharp/downloads/$pkgname-$pkgver.tar.gz
        use_dbussharp_2.pc)
md5sums=('46fcb7a6b9b1cd0241366b8234e31e37'
         '2018c4612d301c84b6cc8f6f7dd38155')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../use_dbussharp_2.pc
  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
