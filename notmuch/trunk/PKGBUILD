# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: fauno <fauno at kiwwwi.com.ar>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
# Contributor: Richard Murri <admin@richardmurri.com>

pkgbase=notmuch
pkgname=('notmuch' 'notmuch-vim' 'notmuch-mutt' 'notmuch-runtime')
pkgver=0.15.2
pkgrel=4
arch=('i686' 'x86_64')
url="http://notmuchmail.org/"
license=('GPL3')
makedepends=('python2' 'python' 'emacs' 'gnupg' 'ruby' 'pkgconfig' 'xapian-core' 'gmime' 'talloc')
options=(!distcc !makeflags)
source=("http://notmuchmail.org/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('e86968dfcd7ffaf162b7b87de56b7f18')

prepare(){
    #cp -dpr --no-preserve=ownership "$srcdir/$pkgname-$pkgver" "$srcdir/$pkgname-runtime-$pkgver"
    cp -dpr --no-preserve=ownership "$srcdir/${pkgname}-$pkgver"/bindings/python{,2}

    find "$srcdir/${pkgname}-${pkgver}/bindings/python" -name '*.py' -exec sed -i -e '1s,python$,python3,' {} +
    find "$srcdir/${pkgname}-${pkgver}/bindings/python2" -name '*.py' -exec sed -i -e '1s,python$,python2,' {} +
}
build() {
    cd "$srcdir/${pkgname}-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc \
		--includedir=/usr/include \
		--without-zsh-completion

    make 
    ( pushd "contrib/$pkgbase-deliver"
    export CPPFLAGS="-I ../../lib"
    ./autogen.sh
    ./configure --prefix=/usr --sysconfdir=/etc \
		--includedir=/usr/include \
        LDFLAGS="-L$srcdir/$pkgname-$pkgver/lib" \
        CPPFLAGS="-I$srcdir/$pkgname-$pkgver/lib"
    make)
    make -C "contrib/${pkgbase}-mutt" "${pkgname}-mutt.1"

    cd bindings/ruby
    ruby extconf.rb

	cd "$srcdir/${pkgname}-${pkgver}/bindings/python"
	python setup.py build

	cd "$srcdir/${pkgname}-${pkgver}/bindings/python2"
	python2 setup.py build
}

package_notmuch-runtime(){
    pkgdesc="Runtime for notmuch and notmuch-mutt"
    depends=('xapian-core' 'gmime' 'talloc')
    cd "$srcdir/${pkgbase}-$pkgver"
    make DESTDIR="$pkgdir/" LIBDIR_IN_LDCONFIG=0 install
	install -Dm755 notmuch $pkgdir/usr/bin/notmuch
}


package_notmuch-vim(){
	pkgdesc="Vim plugins for notmuch"
	depends=('notmuch-runtime')
    mkdir -p "$pkgdir"/usr/share/vim/vimfiles/{plugin,syntax}
    make -C "$srcdir/$pkgbase-$pkgver/vim" prefix="$pkgdir/usr/share/vim/vimfiles" install
}


package_notmuch(){
    pkgdesc="Notmuch is not much of an email program"
    depends=('notmuch-runtime')
    optdepends=('emacs: for using the emacs interface'
                'vim: for using the vim interface'
                'python2: for using the python2 bindings'
                'ruby: for using the ruby bindings'
                'gnupg: for email encryption')

    cd "$srcdir/${pkgname}-$pkgver"
    # Install ruby bindings
    sed -i -e 's,/site_ruby,,g' bindings/ruby/Makefile
    make -C bindings/ruby exec_prefix=$pkgdir/usr prefix=/usr install

    # Install notmuch-deliver
    make -C "contrib/${pkgbase}-deliver" DESTDIR="$pkgdir" install

    # Install python bindings
    cd "$srcdir/${pkgname}-${pkgver}/bindings/python2"
	python2 setup.py install --prefix=/usr --root="$pkgdir"

    cd "$srcdir/${pkgname}-${pkgver}/bindings/python"
	python setup.py install --prefix=/usr --root="$pkgdir"
}

package_notmuch-mutt(){
    pkgdesc="The mail indexer"
    depends=('notmuch-runtime' 'perl-mailtools' 'perl-mail-box' 'perl-term-readline-gnu' 'perl-string-shellquote' 'perl-file-which')
    cd "$srcdir/$pkgbase-$pkgver"

    install -Dm755 "contrib/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dm644 "contrib/$pkgname/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

#vim: set filetype=PKGBUILD sw=4 ts=4 et
