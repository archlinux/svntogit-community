# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=nsd
pkgver=4.1.1
pkgrel=1
pkgdesc='Authoritative only, high performance and simple DNS server'
url='http://www.nlnetlabs.nl/nsd/'
license=('BSD')
arch=('i686' 'x86_64')
depends=('openssl' 'libevent')
makedepends=('flex')
source=("http://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'tmpfiles.d'
        'service')
sha1sums=('11ca8a7bef74500f486db5deac994fdf3dec7358'
          '7ad2b8da961a893095eede8963cf8026b13186a1'
          'f09a10e90b375410e742fff1fb10631541357c67')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/ \
		--sbindir=/usr/bin \
		--datarootdir=/usr/share \
		--with-pidfile=/run/nsd/nsd.pid \
		--enable-ratelimit
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/nsd.conf
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/nsd.service
	rmdir "${pkgdir}"/{tmp,run/{nsd,}}

	rm doc/differences.pdf
	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	install -m644 doc/* "${pkgdir}"/usr/share/doc/"${pkgname}"
	ln -s ../../doc/"${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
