# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=nsd
pkgver=4.1.24
pkgrel=1
pkgdesc='Authoritative only, high performance and simple DNS server'
url='https://www.nlnetlabs.nl/nsd/'
license=('BSD')
arch=('x86_64')
depends=('openssl' 'libevent')
makedepends=('flex')
validpgpkeys=('EDFAA3F2CA4E6EB05681AF8E9F6F1C2D7E045F8D')
source=("https://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'tmpfiles.d'
        'sysusers.d'
        'service')
sha256sums=('4fb687c8e494610ad8692a127ac101ed73df851142a42766c33de06e54449311'
            'SKIP'
            '0be5badb996297150da49b4c655d801bfba76fd8d7785e0899184c130630fe66'
            '6490660d5d4b3e28e16d73e50e35a786a41149917999939fac461ebc60465c79'
            '5ec7616df32c29ddec25a6ec772db5053e234410cf1042f2945d0f554b1f9b65')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/ \
		--sbindir=/usr/bin \
		--datarootdir=/usr/share \
		--with-pidfile=/run/nsd/nsd.pid \
		--enable-ratelimit \
		--enable-relro-now \
		--enable-pie \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rmdir "${pkgdir}"/{tmp,run/{nsd,}}

	rm doc/differences.pdf
	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	install -m644 doc/* "${pkgdir}"/usr/share/doc/"${pkgname}"
	ln -s ../../doc/"${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/nsd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/nsd.conf
	install -Dm644 ../sysusers.d "${pkgdir}"/usr/lib/sysusers.d/nsd.conf
}
