# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=nsjail
pkgver=2.9
pkgrel=5
pkgdesc="Light-weight process isolation tool"
arch=(x86_64)
url="https://nsjail.com"
license=(Apache)
depends=(protobuf libnl)
makedepends=(git)
source=("https://github.com/google/nsjail/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'git+https://github.com/google/kafel.git#commit=722b93a630e53ca472a4c69f61cb3dfc44ea5041')
sha256sums=('f8578a48330d3d089c9234a2b562be08043f8fe77453c322724acdf403d0afc3'
            'SKIP')

prepare() {
    mv kafel ${pkgname}-${pkgver}
    sed -e 's|-Werror||' -i $pkgname-$pkgver/Makefile
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 nsjail -t "${pkgdir}"/usr/bin/ 
}
