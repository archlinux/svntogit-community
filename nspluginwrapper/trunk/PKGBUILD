# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=nspluginwrapper
pkgver=1.4.4
pkgrel=2.1
pkgdesc='Cross-platform NPAPI compatible plugin viewer'
arch=('i686' 'x86_64')
url="http://nspluginwrapper.davidben.net/"
license=('GPL')
depends=(
  'curl'
  'libxt' 'lib32-libxt'
  'gcc-libs' 'lib32-gcc-libs'
  'gtk2' 'lib32-gtk2'
)
makedepends=('gcc-multilib')
install="install"
source=(http://nspluginwrapper.davidben.net/download/$pkgname-$pkgver.tar.gz
        'fix_missing_lib.patch')
md5sums=('36f3e290fc4ce56f65ee695078961188'
         'd40ad2f55d9989e04e3ef0cf4326b1df')

if [[ $CARCH == i686 ]]; then
  # Strip lib32 etc. on i686
  depends=(${depends[@]/*32-*/})
  makedepends=(${makedepends[@]/*32-*/})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
fi

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p0 -i "$srcdir/fix_missing_lib.patch"

  configure_args=""
  if [[ $CARCH == x86_64 ]]; then
    configure_args="$configure_args --with-lib32=lib32 --with-lib64=lib"
  fi

  ./configure $configure_args
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
