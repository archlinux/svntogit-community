# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=nss-pam-ldapd
pkgver=0.9.5
pkgrel=1

pkgdesc='LDAP module for NSS and PAM'
url="http://arthurdejong.org/nss-pam-ldapd/"
arch=('i686' 'x86_64')
license=('LGPL2.1')

depends=('pam')
conflicts=('pam_ldap')

backup=('etc/nslcd.conf')

options=('!emptydirs')

install=nss-pam-ldapd.install
source=("http://arthurdejong.org/nss-pam-ldapd/nss-pam-ldapd-$pkgver.tar.gz"{,.sig}
        "nslcd.service"
        "nslcd.tmpfiles")
md5sums=('1f0517d2c46ca1d8ecdbe23a671edc83' 'SKIP'
         'c2edd35bab384c907eb442c606de856f'
         'd78a702ae9e81af03ae0a85a6af4d2f3')

validpgpkeys=('452EC2CB65CF68C2A1ADBF5F2A8B746810E0AFC1')

build() {
  cd nss-pam-ldapd-"$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-pam-seclib-dir=/usr/lib/security

  make
}

package() {
  cd nss-pam-ldapd-"$pkgver"

  install -Dm644 "$srcdir/nslcd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/nslcd.conf"
  install -Dm644 "$srcdir/nslcd.service" "$pkgdir/usr/lib/systemd/system/nslcd.service"
  install -Dm644 "$srcdir/$pkgname-$pkgver/nslcd.conf" "$pkgdir/etc/nslcd.conf"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
