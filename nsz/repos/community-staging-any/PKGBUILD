# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Michael Armbruster <michael@armbrust.me>

pkgname=nsz
pkgrel=2
pkgver=4.2.1
pkgdesc='Nintendo Switch homebrew compatible NSP/XCI compressor/decompressor'
arch=(any)
url=https://github.com/nicoboss/nsz
license=(MIT)
depends=(
  python-enlighten
  python-pycryptodome
  python-zstandard
)
makedepends=(
  git
  python-setuptools
)
optdepends=('python-kivy: GUI support')
_tag=ad358da6b640f69cf16541ebc691ac0a8cd76493
source=(git+https://github.com/nicoboss/nsz.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd nsz
  git describe --tags
}

build() {
  cd nsz
  python setup.py build
}

package() {
  cd nsz
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
