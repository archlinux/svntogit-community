# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=9.10.0
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(i686 x86_64)
url="http://www.shoup.net/ntl/"
license=(GPL)
depends=(gf2x gmp)
makedepends=()
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('3846639577fc4bcba30f45e079d82a69')

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NATIVE=off
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
