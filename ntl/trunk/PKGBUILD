# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=9.3.0
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(i686 x86_64)
url="http://www.shoup.net/ntl/"
license=(GPL)
depends=(gf2x gmp)
makedepends=()
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('e55dd4fcb3a6b4901776239ccffb2300')

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NTL_GMP_LIP=on NTL_LEGACY_SP_MULMOD=on
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
