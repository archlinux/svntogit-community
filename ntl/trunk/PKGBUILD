# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=6.2.1
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=('i686' 'x86_64')
url="http://www.shoup.net/ntl/"
license=('GPL')
depends=('gf2x' 'gmp')
makedepends=()
options=('!libtool')
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz" 'ntl-sage.patch')
md5sums=('00b7e1cab9bf9b0eae2ad9beab4b0fc9'
         'a95cae771ac6fc823c8c09cd05d40461')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i $srcdir/ntl-sage.patch
}

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NTL_GMP_LIP=on
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}

