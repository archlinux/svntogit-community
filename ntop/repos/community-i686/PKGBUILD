# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=ntop
pkgver=4.1.0
pkgrel=1
pkgdesc='A network traffic probe that shows the network usage.'
arch=('i686' 'x86_64')
url='http://www.ntop.org/'
license=('GPL')
depends=('libevent' 'libpcap' 'gd' 'glib' 'libxml2' 'openssl' 'rrdtool' 'pcre' 'geoip' 'lua')
options=('!libtool' '!makeflags')
install='ntop.install'
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}/Stable/${pkgname}-${pkgver}.tar.gz"
        'ntop'
        'ntop.conf.d')
md5sums=('116df99cf548431393991ea15443c52f'
         '7b0d7bb57432e768ff387e9f7236f87c'
         '6a9371de1dcf31d8ef13a4cf349c02b3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var/lib --disable-snmp
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install-recursive

  install -dm0755 -o nobody -g nobody "${pkgdir}/var/lib/ntop"
  install -dm0755 -o nobody -g nobody "${pkgdir}/var/lib/ntop/rrd"

  install -Dm0755 "${srcdir}/ntop" "${pkgdir}/etc/rc.d/ntop"
  install -Dm0644 "${srcdir}/ntop.conf.d" "${pkgdir}/etc/conf.d/ntop"

  for _f in "${pkgdir}/usr/lib/ntop/plugins/"*.so; do
    _plug="$(basename ${_f})"
    ln -sf "../../lib${_plug}" "${_f}"
  done
}
