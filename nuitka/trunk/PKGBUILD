# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

_name=Nuitka
pkgname=nuitka
pkgver=0.4.7
pkgrel=1
pkgdesc='Python to native compiler'
arch=('any')
url='http://www.nuitka.net/'
license=('APACHE')
depends=('python2')
options=('!emptydirs')
source=("http://nuitka.net/releases/$_name-$pkgver.tar.gz")
sha256sums=('3ad25562491f4256bf77f71361e8a7af168c421d6c76c987012c7b90941f4e5d')

prepare() {
  cd "$_name-$pkgver"

  sed -i 's/env python/env python2/' "$pkgname/build/inline_copy/bin/scons.py"
}

package() {
  cd "$_name-$pkgver"

  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim: sw=2 ts=2 et:
