# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego <cdprincipe@gmail.com>

pkgname=numix-themes
pkgver=2.5.1.r233.a6be634
pkgrel=1
pkgdesc='A flat and light theme with a modern look (GNOME, Openbox, Unity, Xfce)'
arch=('any')
url='http://numixproject.org/'
license=('GPL3')
depends=('gtk-engine-murrine')
makedepends=('git' 'ruby-bundler')
_commit='a6be63453516d3be718ddffc9f791ecc34da70bf'
source=("git+https://github.com/shimmerproject/Numix.git#commit=${_commit}"
        "Gemfile")
sha256sums=('SKIP'
            'b1061ba7ce09c9eb3e41f59362aa8c21d27073b0658f13fb5eabdaea654afe2c')

pkgver() {
  cd Numix

  git describe --tags | sed 's/^v//; s/-/.r/; s/-g/./'
}

build() {
  cd Numix

  bundle install --path .
  make SASS="bundle exec sass"
}

package() {
  cd Numix

  install -dm 755 "$pkgdir"/usr/share/themes/Numix
  rm -rf .git .gitignore CREDITS LICENSE README.md
  cp -dr --no-preserve='ownership' * "$pkgdir"/usr/share/themes/Numix/
}

# vim: ts=2 sw=2 et:
