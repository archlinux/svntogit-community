# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Travis Willard <travisw@wmpub.ca> 
# Contributor: Thayer Williams <thayer@archlinux.org> 
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=numlockx
pkgver=1.2
pkgrel=2
pkgdesc='Turns on the numlock key in X11.'
arch=('i686' 'x86_64')
license=('MIT')
url='http://home.kde.org/~seli/numlockx/'
depends=('libxtst')
source=("http://home.kde.org/~seli/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('be9109370447eae23f6f3f8527bb1a67')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr x_includes=/usr/include/X11 x_libraries=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make prefix="${pkgdir}/usr" install
  
  # Install the custom MIT license
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
