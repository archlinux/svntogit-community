# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor:  KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.42.0
pkgrel=1
pkgdesc="A new type of shell"
arch=('x86_64')
url="https://www.nushell.sh"
license=('MIT')
depends=('openssl' 'libxcb' 'libx11' 'curl' 'bzip2')
makedepends=('rust')
options=('!lto')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/nushell/nushell/archive/$pkgver.tar.gz")
sha512sums=('d68bc66241fc08e98f528e5f244650d9d7f74d38692f49f1cb9a4c0e5de80ba5fae5dd7be50dea92a0912657776fee84266bd8a1943c4ee8ff13432be21073cb')
b2sums=('f2a08ad466da41b850cf57413bf65ffcd728b8e5069a8b90bb8c03a5aa2c9e1c7e43383820ee848f024eb64c533abd189f444bb5739f2becc139a3e7aec07caa')

# NOTE: as of 0.40.0, upstream uses upx to reduce binary size, but this strips
# RELRO + PIE, so we do not enable this

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --workspace --features=extra
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --workspace --features=extra
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # remove binaries not present in upstream releases
  rm -f "$pkgdir/usr/bin/table"
  rm -f "$pkgdir/usr/bin/"nu_plugin_{core,extra}_*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr docs/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
