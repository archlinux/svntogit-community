# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adam Griffiths 
# Contributor: Nick B <Shirakawasuna at gmail _dot_ com>
pkgname=nvidia-cg-toolkit 
pkgver=3.0
_date="February2011"
pkgrel=3
pkgdesc="NVIDIA Cg libraries" 
arch=('i686' 'x86_64')
url="http://developer.nvidia.com/object/cg_toolkit.html" 
license=('custom')
depends=('glibc' 'mesa') 
conflicts=('cgcompiler') # replaces cgcompiler 
source=(http://developer.download.nvidia.com/cg/Cg_${pkgver}/Cg-${pkgver}_${_date}_${CARCH/i686/x86}.tgz)
[ "$CARCH" = "i686" ] &&
md5sums=('e95ff73ddbfd40870de49909fc39df6a')
[ "$CARCH" = "x86_64" ] && 
md5sums=('6384d97792821d3e6ff6244ff89c4f92')

build() {
  return 0
}

package() { 
  install -Dm755 ${srcdir}/usr/bin/cgc ${pkgdir}/usr/bin/cgc
  install -dm755 ${pkgdir}/usr/include/Cg
  install -m644 ${srcdir}/usr/include/Cg/* ${pkgdir}/usr/include/Cg
  install -dm755 ${pkgdir}/usr/lib
  [ "$CARCH" = "i686" ] && install -m644 ${srcdir}/usr/lib/* ${pkgdir}/usr/lib
  [ "$CARCH" = "x86_64" ] && install -m644 ${srcdir}/usr/lib64/* ${pkgdir}/usr/lib
  install -dm755 ${pkgdir}/usr/share/man/man3
  install -m644 ${srcdir}/usr/share/man/man3/* ${pkgdir}/usr/share/man/man3
  install -dm755 ${pkgdir}/usr/share/man/manCg
  install -m644 ${srcdir}/usr/share/man/manCg/* ${pkgdir}/usr/share/man/manCg/
  install -Dm644 ${srcdir}/usr//local/Cg/docs/license.txt ${pkgdir}/usr/share/licenses/nvidia-cg-toolkit/license.txt
  cp -r ${srcdir}/usr/local/Cg ${pkgdir}/usr/share/
  find $pkgdir/usr/share/ -type d -exec chmod -R 755 '{}' ';'
  find $pkgdir/usr/share/ -type f -exec chmod -R 644 '{}' ';'
}

# vim: sw=2:ts=2 et:
