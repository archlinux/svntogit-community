# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=12.0
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('x86_64' 'i686')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('gnutls' 'libpar2' 'libsigc++' 'libxml2' 'python')
changelog=${pkgname}.changelog
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('023c4e3b9c7e920d9ea72b60135b438ce13543454f79984c06fd15365b9a882e')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/${pkgname}
  install -m644 -t "${pkgdir}"/usr/share/${pkgname} AUTHORS ChangeLog README
}

# vim: ts=2 sw=2 et:
