# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=15.0
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('libxml2' 'python')
changelog=$pkgname.changelog
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname-$pkgver.tar.gz)
sha256sums=('3ef13f3e5917e4cda19c4fc0cd37e79967a19b4e3448c239ff24e37712a6cc0a')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck --with-tlslib=OpenSSL
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/$pkgname
  install -m644 -t "${pkgdir}"/usr/share/$pkgname AUTHORS ChangeLog README
}
