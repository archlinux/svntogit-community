# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: SÃ©bastien Luttringer
# Contributor: L42y <423300@gmail.com>

pkgname=oath-toolkit
pkgver=2.6.2
pkgrel=4
pkgdesc='OATH one-time password toolkit'
arch=('x86_64')
url='http://www.nongnu.org/oath-toolkit/'
license=('GPL3')
depends=('glibc' 'pam' 'xmlsec')
validpgpkeys=('9AA9BDB11BB1B99A21285A330664A76954265E8C') # Simon Josefsson
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        'oath-toolkit-update-gnulib.patch::https://gitlab.com/oath-toolkit/oath-toolkit/commit/2fffce2a471f74a585939c84cce16ef3015e5d3d.diff'
        'fseeko.patch')
sha256sums=('b03446fa4b549af5ebe4d35d7aba51163442d255660558cd861ebce536824aa0'
            'SKIP'
            '4093d69a22af60fac339fcee22ff29c3b8418b76bc1286e5226505af884e0c21'
            'b93dc7bafe32a3477127e55b1c470063660b1f5c3f25622ea581b41502783020')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 < "${srcdir}"/oath-toolkit-update-gnulib.patch
  patch -Np1 < "${srcdir}"/fseeko.patch
  
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # Install basic docs too
  install -D -m 0644 pam_oath/README "$pkgdir/usr/share/doc/$pkgname/PAM_README"
  install -D -m 0644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

