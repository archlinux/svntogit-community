# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: L42y <423300@gmail.com>

pkgname=oath-toolkit
pkgver=2.4.1
pkgrel=1
pkgdesc='OATH one-time password toolkit'
arch=('i686' 'x86_64')
url='http://www.nongnu.org/oath-toolkit/'
license=('GPL3')
depends=('glibc' 'pam' 'xmlsec')
validpgpkeys=('0424D4EE81A0E3D119C6F835EDA21E94B565716F') # Simon Josefsson
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('951bafd1d86e6013903c10be3b6623bb'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # Install basic docs too
  install -D -m 644 pam_oath/README "$pkgdir/usr/share/doc/$pkgname/PAM_README"
  install -D -m 644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
