# $Id$
# Maintainer: SÃ©bastien Luttringer
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: L42y <423300@gmail.com>

pkgname=oath-toolkit
pkgver=2.6.0
pkgrel=1
pkgdesc='OATH one-time password toolkit'
arch=('i686' 'x86_64')
url='http://www.nongnu.org/oath-toolkit/'
license=('GPL3')
depends=('glibc' 'pam' 'xmlsec')
validpgpkeys=('9AA9BDB11BB1B99A21285A330664A76954265E8C') # Simon Josefsson
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-fix-make-check-on-32-bit-systems.patch')
md5sums=('f19f1c47eb7d1c609b3e244876b9ce63'
         'SKIP'
         'd3c1a32357fb27f67a82bffd08331062')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 < "${srcdir}/0001-fix-make-check-on-32-bit-systems.patch"
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # Install basic docs too
  install -D -m 644 pam_oath/README "$pkgdir/usr/share/doc/$pkgname/PAM_README"
  install -D -m 644 README "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim:set ts=2 sw=2 et:
