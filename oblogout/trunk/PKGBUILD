# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: XaBz <xabz.2k5 gmail.com>

pkgname=oblogout
pkgver=0.2
pkgrel=11
pkgdesc="Openbox logout script"
arch=('any')
url="https://launchpad.net/oblogout"
depends=('pygtk' 'cairo' 'python-imaging' 'python2-distutils-extra'
         'dbus-python' 'consolekit' 'upower')
license=('GPL2')
backup=(etc/oblogout.conf)  
conflicts=('openboxlogout-gnome')
source=("http://launchpad.net/oblogout/$pkgver/$pkgver.0/+download/$pkgname-$pkgver.tar.bz2"
	      "$pkgname.patch"
	      "$pkgname.conf"
	      "ru.po")
install=$pkgname.install
sha256sums=('f80003b85e9f4f1c588675453ea03f40a86fc50546d509ceb69706eb045782a9'
            'cea52a14df0ef924fcdd63b2e82c14b94cc2a7753994c176621ace9502bd9109'
            '9ce4a8ccb207c0203791895491632a84a7dd65d1e43eb53ab21496d965b198e2'
            'aa8504055ec1e0661c3588cde85aad3267ffa7a42414c106e6ce8bff71f9d883')

build() {
  cd "$srcdir/$pkgname"

  cp "$srcdir/ru.po" "$srcdir/$pkgname/po/"
  patch -p0 < "$srcdir/$pkgname.patch"
	sed -i '0,/on/s//on2/' "$pkgname/"*.py
}

package() {
  cd "$srcdir/$pkgname"

  python2 setup.py install --root="$pkgdir/"
  install -m644 "$srcdir/oblogout.conf" "$pkgdir/etc/"
}

# vim:set ts=2 sw=2 et:
