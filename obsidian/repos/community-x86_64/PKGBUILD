# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Gennadiy Mykhailiuta <gmykhailiuta@gmail.com>
# Contributor: Xuanwo <xuanwo@archlinuxcn.org>

pkgname=obsidian
pkgver=1.0.0
pkgrel=1
pkgdesc="A powerful knowledge base that works on top of a local folder of plain text Markdown files"
arch=('x86_64')
url="https://obsidian.md/"
license=('custom:Commercial')
depends=('zlib' 'hicolor-icon-theme' 'fuse' 'electron18')
makedepends=('asar')
options=(!strip)
source=("${pkgname}.sh"
        "${pkgname}.desktop"
        "https://github.com/obsidianmd/obsidian-releases/releases/download/v${pkgver}/obsidian-${pkgver}.tar.gz"
)
sha256sums=('d6fd456d3c17d2d532a09b3d96c278f39764e2f02759a809facfbc1c99201a4e'
            'be67713e873a5935d910efc6f6005c104b35c7b3a002dfcb5643fd185d1b0056'
            '1f50a5f52073fe6c0df207b3164715e0ac48efec558d057602dc8b2b28d5a48f')

prepare() {
    cd "$srcdir"
    asar ef "${pkgname}-${pkgver}"/resources/obsidian.asar icon.png
}

package() {
    cd "$srcdir"

    # Install executable file
    install -Dm755 "$pkgname".sh "$pkgdir"/usr/bin/obsidian

    # Install desktop file
    install -Dm644 "$pkgname".desktop -t "$pkgdir"/usr/share/applications/
    install -Dm644 icon.png "$pkgdir"/usr/share/pixmaps/obsidian.png

    # Most of the release package is electron, but we use system's default one
    # So strip away asar packages and put them to /usr/lib/
    cd "${pkgname}-${pkgver}"/resources/
    install -dm0755 "$pkgdir"/usr/lib/obsidian
    find . -type d -exec install -d {,"$pkgdir"/usr/lib/obsidian/}{} \;
    find . -type f -exec install -D {,"$pkgdir"/usr/lib/obsidian/}{} \;
}
