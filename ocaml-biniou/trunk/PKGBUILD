# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=biniou
pkgname=ocaml-${_pkgname}
pkgver=1.2.0
pkgrel=2
pkgdesc="An optimized parsing and printing library for JSON"
arch=('x86_64')
url="https://github.com/mjambon/${_pkgname}"
license=('BSD')
options=('!strip' 'staticlibs')
depends=('ocaml-easy-format')
makedepends=('ocaml-findlib' 'dune' 'opam')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('d939a9d58660201738c3d22ad5b7976deb2917b22591a07525807bb741357d36')

build() {
    cd ${_pkgname}-${pkgver}
    make all
}

check() {
    cd ${_pkgname}-${pkgver}
    make test
}

package() {
    cd ${_pkgname}-${pkgver}

    # Initialize OPAM
    export OPAMROOT="${srcdir}"/opam
    opam init -n

    # Work around missing install command
    OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)" jbuilder install

    # Install LICENSE
    install -Dm644 "${srcdir}"/opam/system/doc/${_pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
