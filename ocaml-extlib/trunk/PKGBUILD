# Maintainer: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>

pkgname=ocaml-extlib
pkgver=1.5.1
pkgrel=4
pkgdesc="Extends the OCaml standard library"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ocaml-extlib/"
license=('LGPL')
depends=('ocaml')
makedepends=('ocaml-findlib')
source=(http://ocaml-extlib.googlecode.com/files/extlib-${pkgver}.tar.gz)
md5sums=('8dc2944cbf63141347956bd353893d75')

_OCAMLFIND_DESTDIR="${pkgdir}/usr/lib/ocaml"
_DOCDIR="${pkgdir}/usr/share/doc/ocaml-extlib"

build() {
  cd ${srcdir}/extlib-${pkgver}
  make all
  make opt
  sed -i 's/cp odoc_style.css/#cp odoc_style.css/g' Makefile
  sed -i 's/ocamldoc -sort -html/ocamldoc -sort -keep-code -html -colorize-code/g' Makefile
  make doc
}

package () {
  cd ${srcdir}/extlib-${pkgver}
  mkdir -p $_OCAMLFIND_DESTDIR/extlib
  mkdir -p $_DOCDIR
  install -m 0644 *.{a,cmxa,cma,cmi,cmo,mli,cmx,ml} $_OCAMLFIND_DESTDIR/extlib
  install -m 0644 META.txt $_OCAMLFIND_DESTDIR/extlib/META
  cp -r doc/* $_DOCDIR/
}
