# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Sebastian Wiesner <lunaryorn@googlemail.com>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>
# Contributor: scj <scj@archlinux.us>
# Contributor: Alif <alive4ever@live.com>

pkgname=ocaml-findlib
pkgver=1.9.5
pkgrel=2
pkgdesc='OCaml package manager'
arch=(x86_64)
url='http://projects.camlcity.org/projects/findlib.html'
license=(MIT)
depends=(ocaml-compiler-libs 'ocaml=4.14.0')
source=("http://download.camlcity.org/download/findlib-$pkgver.tar.gz")
b2sums=('6f43281298a9e443f9b92bcb8996bc2a55c2be86df492540fe8b5fb3fbb199daf1daef68e14c981d153c885c443ebc4af9ffb957235dd14904b9d75ca387cfa2')
options=(!makeflags !strip staticlibs zipman)

build() {
  cd findlib-$pkgver
  ./configure \
    -config /etc/findlib.conf \
    -mandir /usr/share/man \
    -sitelib /usr/lib/ocaml
  make all opt
}

package () {
  cd findlib-$pkgver
  make prefix="$pkgdir" install
  install -Dm755 src/findlib/ocamlfind_opt "$pkgdir/usr/bin/ocamlfind_opt"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
