# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Sebastian Wiesner <lunaryorn@googlemail.com>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>
# Contributor: scj <scj@archlinux.us>
# Contributor: Alif <alive4ever@live.com>

pkgname=ocaml-findlib
pkgver=1.8.1
pkgrel=7
pkgdesc='OCaml package manager'
arch=(x86_64)
url='http://projects.camlcity.org/projects/findlib.html'
license=(MIT)
depends=(ocaml-compiler-libs)
source=("http://download.camlcity.org/download/findlib-$pkgver.tar.gz")
sha256sums=('8e85cfa57e8745715432df3116697c8f41cb24b5ec16d1d5acd25e0196d34303')
options=(staticlibs !strip zipman !makeflags)

build() {
  cd "findlib-$pkgver"

  ./configure \
    -sitelib /usr/lib/ocaml \
    -config /etc/findlib.conf \
    -mandir /usr/share/man

  make all opt
}

package () {
  cd "findlib-$pkgver"

  make prefix="$pkgdir" install

  install -Dm755 src/findlib/ocamlfind_opt "$pkgdir/usr/bin/ocamlfind_opt"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
