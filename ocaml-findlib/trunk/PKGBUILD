# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Sebastian Wiesner <lunaryorn@googlemail.com>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>
# Contributor: scj <scj@archlinux.us>
# Contributor: Alif <alive4ever@live.com>

pkgname=ocaml-findlib
pkgver=1.7.3
pkgrel=1
pkgdesc='OCaml package manager'
arch=('x86_64' 'i686')
url='http://projects.camlcity.org/projects/findlib.html'
license=('MIT')
depends=('ocaml')
makedepends=('setconf')
source=("http://download.camlcity.org/download/findlib-$pkgver.tar.gz")
sha256sums=('d196608fa23c36c2aace27d5ef124a815132a5fcea668d41fa7d6c1ca246bd8b')
options=('staticlibs' '!strip' 'zipman' '!makeflags')

build() {
  cd "findlib-$pkgver"

  ./configure \
    -sitelib /usr/lib/ocaml \
    -config /etc/findlib.conf \
    -mandir /usr/share/man

  make all opt
}

package () {
  cd "findlib-$pkgver"

  make prefix="$pkgdir" install

  # Add a directory to the path for compatibility with old-style packages
  #setconf "$pkgdir/etc/findlib.conf" path='"/usr/lib/ocaml:/usr/lib/ocaml-site-lib"'

  install -m755 src/findlib/ocamlfind_opt "$pkgdir/usr/bin/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
