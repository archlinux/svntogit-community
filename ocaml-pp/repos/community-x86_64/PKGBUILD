# Maintainer: Konstantin Gizdov <arch at kge dot pw>
_pkgname='pp'
pkgname="ocaml-${_pkgname}"
pkgver=1.1.2
pkgrel=3
pkgdesc="OCaml Pretty printing: a lean alternative to the Format module of the OCaml standard library."
arch=('x86_64')
url="https://github.com/ocaml-dune/pp"
license=('MIT')
makedepends=('dune')
depends=('glibc' 'ocaml>=4.14.0')
options=('!strip')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8c4dbce6930c62f5cee26ebef907b7b862b03fb57a07a3c6bebc31ee40ef16a6fe2b9450a7fe7016dd48bcf9807009c411f5ea8e587550644495bd7970c63085')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    dune build -p "${_pkgname}"
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    DESTDIR="${pkgdir}" dune install --prefix "/usr" --libdir="$(ocamlc -where)"

    install -dm755 "${pkgdir}/usr/share/"
    mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"

    install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s /usr/share/doc/pp/LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
