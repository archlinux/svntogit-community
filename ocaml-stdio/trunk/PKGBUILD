# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Peukert <dan.peukert@gmail.co>
# Contributor: Dimitris Kiziridis <ragouel at outlook dot com>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

_projectname='stdio'
pkgname="ocaml-$_projectname"
pkgver=0.14.0
pkgrel=4
pkgdesc='Standard IO Library for OCaml'
arch=('x86_64')
url="https://github.com/janestreet/$_projectname"
license=('MIT')
depends=('glibc' 'ocaml>=4.13.1' 'ocaml-base')
makedepends=('dune')
options=('!strip')
source=("$pkgname-$pkgver-$pkgrel.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('1685cb86b29b07075ba6028431cf4e3f687c071d89996a6437442db2dfe1b0b2')

_sourcedirectory="$_projectname-$pkgver"

build() {
	cd "$srcdir/$_sourcedirectory/"
	dune build --release --verbose
}

package() {
	cd "$srcdir/$_sourcedirectory/"
	DESTDIR="$pkgdir" dune install --prefix '/usr' --libdir 'lib/ocaml' --release --verbose

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	mv "$pkgdir/usr/doc/$_projectname/"* "$pkgdir/usr/share/doc/$pkgname/"
	rm -r "$pkgdir/usr/doc/"

	install -dm755 "$pkgdir/usr/share/licenses/$pkgname"
	ln -sf "/usr/share/doc/$pkgname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
