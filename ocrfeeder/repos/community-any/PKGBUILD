# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ocrfeeder
pkgver=0.8.1
pkgrel=3
pkgdesc="GTK+ document layout analysis and optical character recognition application"
arch=('any')
url="https://wiki.gnome.org/Apps/OCRFeeder"
license=('GPL')
depends=('python2-pyenchant' 'python2-gobject' 'python2-lxml' 'python2-pillow'
         'python2-reportlab' 'gtk3' 'goocanvas' 'gtkspell3' 'ghostscript' 'unpaper' 'python2-sane'
         'iso-codes')
makedepends=('intltool' 'gnome-doc-utils' 'python2-gobject2')
optdepends=('tesseract: character recognition'
            'ocrad: character recognition'
            'gocr: character recognition'
            'cuneiform: character recognition')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5816401c9b4bfce98fd9a7b649a3c7ecc3d6968a30f0e4d1b3677d172d55253b')

prepare() {
  cd $pkgname-$pkgver

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/ocrfeeder*.in src/ocrfeeder/odf/*.py
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              PYTHON=python2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../ocrfeeder/icons/ocrfeeder.svg "$pkgdir/usr/share/pixmaps/ocrfeeder.svg"
}
