# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.5.5
pkgrel=1
pkgdesc='Synchronizes emails between two repositories'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2')
makedepends=('python2-docutils')
source=("https://github.com/OfflineIMAP/offlineimap/archive/v${pkgver}.tar.gz")
sha1sums=('8cf4b1dfe98f14d51d8e331dd7393accc9320ac7')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/\^/=/g' -i docs/MANUAL.rst
	find . -type f -exec \
		sed '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' -i {} \;
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python2 setup.py build
	rst2man2 docs/MANUAL.rst offlineimap.1
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python2 setup.py install --root="${pkgdir}" --optimize=1
	install -Dm644 offlineimap.1 "${pkgdir}"/usr/share/man/man1/offlineimap.1
	install -Dm644 offlineimap.conf "${pkgdir}"/usr/share/offlineimap/offlineimap.conf
	install -Dm644 offlineimap.conf.minimal "${pkgdir}"/usr/share/offlineimap/offlineimap.conf.minimal
}
