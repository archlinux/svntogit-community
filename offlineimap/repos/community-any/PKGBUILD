# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.5.7
pkgrel=1
pkgdesc='Synchronizes emails between two repositories'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2')
makedepends=('asciidoc')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/OfflineIMAP/offlineimap/archive/v${pkgver}.tar.gz)
sha1sums=('1c7f622f482a52e94e3968ed1904d0ac0a95b1c8')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  find . -type f -exec sed '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' -i {} \;
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  python2 setup.py build

  cd "${srcdir}"/${pkgname}-${pkgver}/docs
  make man
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 docs/offlineimap.1 "${pkgdir}"/usr/share/man/man1/offlineimap.1
  install -Dm644 docs/offlineimapui.7 "${pkgdir}"/usr/share/man/man7/offlineimapui.7
  install -Dm644 offlineimap.conf "${pkgdir}"/usr/share/offlineimap/offlineimap.conf
  install -Dm644 offlineimap.conf.minimal "${pkgdir}"/usr/share/offlineimap/offlineimap.conf.minimal
}
