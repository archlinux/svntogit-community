# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Daenyth
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Vrob (Markus Heuser) mheuser@mi.fu-berlin.de

pkgname=ogmrip
pkgver=0.13.8
pkgrel=1
pkgdesc="Libraries and GTK2 interface for DVD ripping using mencoder"
arch=('i686' 'x86_64')
url="http://ogmrip.sourceforge.net/"
license=('GPL')
depends=('libdvdread>=4.1.3' 'mplayer' 'mencoder' 'enca' 'pkg-config' 'gconf' 'libglade' 'libnotify' 'enchant')
makedepends=('patch' 'intltool' 'perl-xml-parser' 'faac' 'lame' 'mkvtoolnix' 'libtheora' 'ogmtools' 'gpac' 'vorbis-tools' 'tesseract')
optdepends=('faac: support for the AAC audio codec'
            'lame: support for the MP3 audio codec'
            'mkvtoolnix: create, alter, and inspect Matroska files'
            'libtheora: decode and encode Ogg Theora files'
            'ogmtools: support for the Ogg Media container'
            'gpac: support for the MP4 container'
            'vorbis-tools: support for the Ogg Vorbis audio codec'
            'gocr: support for the SRT subtitle format'
            'ocrad: support for the SRT subtitle format'
            'tesseract: support for the SRT subtitle format')
options=('!libtool' '!makeflags')
changelog=$pkgname.changelog
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ea6a75e6cc4fe7df1ed65648af208c87748959a6b372847b1005c3b92cc5a423')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/usr/share
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
