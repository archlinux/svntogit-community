# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Daenyth
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Vrob (Markus Heuser) mheuser@mi.fu-berlin.de

pkgname=ogmrip
pkgver=0.13.6
pkgrel=3
pkgdesc="Libraries and GTK2 interface for DVD ripping using mencoder"
arch=('i686' 'x86_64')
url="http://ogmrip.sourceforge.net/"
license=('GPL')
depends=('libdvdread>=4.1.3' 'mplayer' 'mencoder' 'enca' 'pkgconfig' 'gconf' 'libglade' 'libnotify' 'enchant')
makedepends=('patch' 'intltool' 'perl-xml-parser' 'faac' 'lame' 'mkvtoolnix' 'libtheora' 'ogmtools' 'gpac' 'vorbis-tools' 'tesseract')
optdepends=('faac: support for the AAC audio codec'
            'lame: support for the MP3 audio codec'
            'mkvtoolnix: create, alter, and inspect Matroska files'
            'libtheora: decode and encode Ogg Theora files'
            'ogmtools: support for the Ogg Media container'
            'gpac: support for the MP4 container'
            'vorbis-tools: support for the Ogg Vorbis audio codec'
            'gocr: support for the SRT subtitle format'
            'ocrad: support for the SRT subtitle format'
            'tesseract: support for the SRT subtitle format')
options=('!libtool' '!makeflags')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ogmrip-0.13.6-libnotify-0.7.patch)
md5sums=('e094c49cdf758883293dd44513fb37e8'
         'a7f2c64c9f1d96664d29200e461bb36e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np0 -i "$srcdir/ogmrip-0.13.6-libnotify-0.7.patch"

  ./configure --prefix=/usr --sysconfdir=/usr/share
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
