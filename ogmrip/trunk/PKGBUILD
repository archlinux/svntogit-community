# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Daenyth
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Vrob (Markus Heuser) mheuser@mi.fu-berlin.de

pkgname=ogmrip
pkgver=0.13.4
pkgrel=1
pkgdesc="Libraries and GTK2 interface for DVD ripping using mencoder"
arch=('i686' 'x86_64')
url="http://ogmrip.sourceforge.net/"
license=('GPL')
depends=('libdvdread>=4.1.3' 'mplayer' 'enca' 'pkgconfig' 'gconf' 'libglade' 'hal' 'libnotify' 'enchant')
makedepends=('patch' 'intltool' 'perlxml' 'faac' 'lame' 'mkvtoolnix' 'libtheora' 'ogmtools' 'gpac' 'vorbis-tools' 'tesseract')
optdepends=('faac: support for the AAC audio codec'
             'lame: support for the MP3 audio codec'
             'mkvtoolnix: create, alter, and inspect Matroska files'
             'libtheora: decode and encode Ogg Theora files'
             'ogmtools: support for the Ogg Media container'
             'gpac: support for the MP4 container'
             'vorbis-tools: support for the Ogg Vorbis audio codec'
             'gocr: support for the SRT subtitle format'
             'ocrad: support for the SRT subtitle format'
             'tesseract: support for the SRT subtitle format')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        $pkgname-$pkgver-gtk.patch)
md5sums=('cf4b970312a4cb03864b498c02b68837'
         '1e37d0199aa0ccc38f6d3aae209efe3d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np0 -i ${srcdir}/$pkgname-$pkgver-gtk.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/usr/share
  make || return 1
  make DESTDIR=${pkgdir} install
}
