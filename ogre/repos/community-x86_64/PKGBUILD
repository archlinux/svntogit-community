# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=ogre
pkgver=2.1
pkgrel=1
pkgdesc='Scene-oriented, flexible 3D engine written in C++'
arch=('x86_64')
url='http://www.ogre3d.org'
license=('custom:MIT')
depends=('freeimage' 'freetype2' 'libxaw' 'libxrandr' 'openexr' 'rapidjson'
         'nvidia-cg-toolkit' 'zziplib' 'sdl2' 'glu' 'tinyxml' 'zlib')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'python' 'ninja' 'cppunit')
install=ogre.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/OGRECave/ogre-next/archive/${pkgver}.tar.gz")
optdepends=('python: python bindings')
sha512sums=('9802e3bf20ddb09453524d1ddafa7a2083dfd0e609563f478fe31b087dd06d463f69800b9c6485cb3db92d98f282ef67ace3e272c1ef531e982ea9f719617fb5')

prepare() {
  mkdir ogre-next-${pkgver}/build
}

build() {
  cd ogre-next-${pkgver}/build

  cmake .. \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOGRE_CONFIG_ENABLE_JSON=TRUE \
    -DOGRE_CONFIG_THREADS=1 \
    -DOGRE_CONFIG_THREAD_PROVIDER="std" \
    -DOGRE_BUILD_COMPONENT_PLANAR_REFLECTIONS=ON \
    -DOGRE_USE_BOOST=TRUE \
    -DOGRE_BUILD_TESTS=TRUE \
    -DOGRE_INSTALL_SAMPLES_SOURCE=TRUE

  ninja
  ninja OgreDoc
  # All of these are broken
  # -DOGRE_BUILD_COMPONENT_TERRAIN=ON \
  # -DOGRE_BUILD_COMPONENT_VOLUME=ON \
  # -DOGRE_BUILD_COMPONENT_PAGING=ON \
  # -DOGRE_BUILD_COMPONENT_PROPERTY=ON \
  # -DOGRE_BUILD_COMPONENT_RTSHADERSYSTEM=ON \
  # -DOGRE_BUILD_COMPONENT_SCENE_FORMAT=ON \
}

check() {
  cd ogre-next-${pkgver}/build

  # Upstream pls
  # ninja test
}

package() {
  cd ogre-next-${pkgver}/build

  DESTDIR="${pkgdir}" ninja install

  mkdir -p "${pkgdir}"/opt/ogre/samples
  mv "${pkgdir}"/usr/bin/Sample_* "${pkgdir}"/opt/ogre/samples
  mv "${pkgdir}"/usr/bin/Test_* "${pkgdir}"/opt/ogre/samples
  install -Dm644 ../Docs/License.html "${pkgdir}"/usr/share/licenses/${pkgname}/License.html
}

# vim:set ts=2 sw=2 et:
