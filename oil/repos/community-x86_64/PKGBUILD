# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Yigit Sever <yigit at yigitsever dot com>
# Contributor: Qontinuum <qontinuum@artixlinux.org>
# Contributor: timetoplatypus <timetoplatypus@protonmail.com>
# Contributor: andychu <andy@oilshell.org>

pkgname=oil
pkgver=0.12.4
pkgrel=1
pkgdesc='A new Unix shell'
arch=('x86_64')
url='https://www.oilshell.org'
license=('Apache')
depends=('readline')
makedepends=('bash')
replaces=('osh')
install=oil.install
options=('!lto' '!strip')
source=("$url/download/$pkgname-$pkgver.tar.xz")
sha512sums=('1083be4730f63ebde9d7214339552e0132dfc9e27b76f9c78f7c5d88b25d61bf06a3031b370ad42fae3cae85b714b9669d9e1929fa9a6bd797d351e68cb452a8')
b2sums=('1e0e81b67f40635dddd47ee1de8d3ecbfa74ac0fadd54798a18c1a8b2d05dd9af43d385b21d772fd5afcfb84edf8857de0b969c09eb9030114f604b97051ec32')

build() {
	cd "$pkgname-$pkgver"

	./configure \
    --prefix=/usr \
    --with-readline

	make
}

package() {
	cd "$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}
