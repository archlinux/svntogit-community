# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=onboard
pkgver=0.97.1
pkgrel=2
pkgdesc="On-screen keyboard useful on tablet PCs or for mobility impaired users"
arch=('i686' 'x86_64')
url="https://launchpad.net/onboard"
license=('GPL')
depends=('python2-cairo' 'python2-dbus' 'python2-gobject' 'libwnck3' 'python2-virtkey' 'gsettings-desktop-schemas' 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('python2-distutils-extra')
optdepends=('at-spi2-atk: auto-show when editing text'
            'mousetweaks: hover click with mouse')
options=('!emptydirs')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('065dcdf89d5fe145ad7876c5721c0df5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's/\"python\"/\"python2\"/g' scripts/sokSettings.py

  python2 setup.py install --root=$pkgdir/ --optimize=1
}

package() {
  cd "$pkgdir"

  find . -type f -name *.py | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}
