# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=onboard
pkgver=1.4.1
pkgrel=3
pkgdesc="On-screen keyboard useful on tablet PCs or for mobility impaired users"
arch=('x86_64')
url="https://launchpad.net/onboard"
license=('GPL')
depends=('gtk3' 'hunspell' 'iso-codes' 'libcanberra' 'libxkbfile' 'python-cairo' 'python-dbus' 'python-gobject' 'systemd')
makedepends=('python-distutils-extra')
optdepends=('mousetweaks: hover click with mouse')
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('BCFE9B5A71A0B0F860521CF963087078B67DCFED') # Francesco Fumanti
md5sums=('1a2fbe82e934f5b37841d17ff51e80e8'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
