# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>
pkgname=oolite
pkgver=1.84
pkgrel=5
pkgdesc="A space-sim based on the classic Elite"
arch=('i686' 'x86_64')
url="http://www.oolite.org/"
license=('GPL2')
depends=('gnustep-gui' 'sdl_mixer' 'glu' 'espeak' 'nspr' 'python2' 'icu' 'gnustep-base' 'openal')
makedepends=('gnustep-make' 'curl' 'zip' 'mesa' 'gcc-objc')
#options=(makeflags)
source=(oolite
        spidermonkey185ff4-GCC6.patch
        lightdot-issue202-NSDate.patch
        "https://github.com/OoliteProject/oolite/releases/download/${pkgver}/oolite-source-${pkgver}.tar.bz2")
md5sums=('ade14e17fe9d52d16b35f2e97046362c'
         '9e3ffab5a49afeebe7ba501161c8d4dc'
         '09e27cd86792feef0125d47627df1e26'
         '538e4249dbf4d3418216e0bdb7cbf408')

prepare() {
  cd ${srcdir}/oolite-source-${pkgver}
  patch -Np1 -i ${srcdir}/lightdot-issue202-NSDate.patch
  cd ${srcdir}/oolite-source-${pkgver}/deps/mozilla
  patch -Np0 -i ${srcdir}/spidermonkey185ff4-GCC6.patch
}

build() {
  cd oolite-source-${pkgver}
  unset CPPFLAGS
  #sed -i '/void png_error/d' src/Core/Materials/OOPNGTextureLoader.m
  sed -i "/ADDITIONAL_OBJCFLAGS/s/=/= -fobjc-exceptions/" GNUmakefile
  source /usr/share/GNUstep/Makefiles/GNUstep.sh
  make -f Makefile release-deployment
}

package() {
  cd oolite-source-${pkgver}

  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/share/{oolite,applications,pixmaps,doc/oolite}

  cp -r oolite.app/* ${pkgdir}/usr/share/oolite/

  install -D -m755 ../oolite ${pkgdir}/usr/bin/oolite
  install -D -m644 installers/FreeDesktop/oolite-icon.png ${pkgdir}/usr/share/pixmaps/oolite-icon.png
  install -D -m644 installers/FreeDesktop/oolite.desktop ${pkgdir}/usr/share/applications/oolite.desktop
  install -D -m644 Doc/AdviceForNewCommanders.pdf Doc/OoliteReadMe.pdf Doc/OoliteRS.pdf ${pkgdir}/usr/share/doc/oolite/
 }
# vim:set ts=2 sw=2 et:
