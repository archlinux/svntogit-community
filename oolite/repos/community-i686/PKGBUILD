# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>
pkgname=oolite
pkgver=1.75.3
pkgrel=1
pkgdesc="A space-sim based on the classic Elite"
arch=('i686' 'x86_64')
url="http://oolite-linux.berlios.de"
license=('GPL2')
depends=('gnustep-gui' 'sdl_mixer' 'mesa' 'espeak' 'nspr' 'python2' 'icu')
makedepends=('gcc-objc' 'gnustep-make' 'curl' 'zip')
options=(!makeflags)
source=(oolite http://download2.berlios.de/oolite-linux/oolite-dev-source-${pkgver}.tar.bz2)
md5sums=('b182976fade9b64a3b3702e2a8c72bf0'
         'e6c3d1e41f60f8cf410f766a3ecadcc7')

build() {
    cd $srcdir
    cp -r ${srcdir}/oolite-dev-source-${pkgver} ${srcdir}/${pkgname}-build

    cd ${srcdir}/${pkgname}-build
    make -f libjs.make debug=no
    make -f Makefile release
}

package() {
    mkdir -p ${pkgdir}/usr/bin
    mkdir -p ${pkgdir}/usr/share/{oolite,applications,pixmaps,doc/oolite}
    cd ${srcdir}/${pkgname}-build
    cp -r oolite.app/* ${pkgdir}/usr/share/oolite/
#install shellscript and icon
    install -D -m755 ../oolite ${pkgdir}/usr/bin/oolite
    install -D -m644 installers/FreeDesktop/oolite-icon.png ${pkgdir}/usr/share/pixmaps/oolite-icon.png
# copy  .desktop file
    install -D -m644 installers/FreeDesktop/oolite.desktop ${pkgdir}/usr/share/applications/oolite.desktop
# documentation
    install -D -m644 Doc/AdviceForNewCommanders.pdf Doc/OoliteReadMe.pdf Doc/OoliteRS.pdf ${pkgdir}/usr/share/doc/oolite/
 }
# vim:set ts=2 sw=2 et:
