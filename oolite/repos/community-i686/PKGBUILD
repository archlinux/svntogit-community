# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl>
pkgname=oolite
pkgver=1.77
pkgrel=2
pkgdesc="A space-sim based on the classic Elite"
arch=('i686' 'x86_64')
url="http://oolite-linux.berlios.de"
license=('GPL2')
depends=('gnustep-gui' 'sdl_mixer' 'glu' 'espeak' 'nspr' 'python2' 'icu' 'gnustep-base')
makedepends=('gcc-objc' 'gnustep-make' 'curl' 'zip' 'mesa')
options=(!makeflags)
source=(oolite
        http://download.berlios.de/oolite-linux/oolite-source-${pkgver}.tar.bz2)
md5sums=('b182976fade9b64a3b3702e2a8c72bf0'
         '78e0f1da12d1478534eb2af4d0983b01')

build() {
    cd $srcdir
    cp -r ${srcdir}/oolite-source-${pkgver} ${srcdir}/${pkgname}-build

    cd ${srcdir}/${pkgname}-build
    unset CPPFLAGS
    sed -i '/void png_error/d' src/Core/Materials/OOPNGTextureLoader.m
    sed -i "/ADDITIONAL_OBJCFLAGS/s/=/= -fobjc-exceptions/" GNUmakefile
    make -f libjs.make debug=no
    make -f Makefile release
}

package() {
    mkdir -p ${pkgdir}/usr/bin
    mkdir -p ${pkgdir}/usr/share/{oolite,applications,pixmaps,doc/oolite}
    cd ${srcdir}/${pkgname}-build
    cp -r oolite.app/* ${pkgdir}/usr/share/oolite/
#install shellscript and icon
    install -D -m755 ../oolite ${pkgdir}/usr/bin/oolite
    install -D -m644 installers/FreeDesktop/oolite-icon.png ${pkgdir}/usr/share/pixmaps/oolite-icon.png
# copy  .desktop file
    install -D -m644 installers/FreeDesktop/oolite.desktop ${pkgdir}/usr/share/applications/oolite.desktop
# documentation
    install -D -m644 Doc/AdviceForNewCommanders.pdf Doc/OoliteReadMe.pdf Doc/OoliteRS.pdf ${pkgdir}/usr/share/doc/oolite/
 }
# vim:set ts=2 sw=2 et:
