# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.1.5
pkgrel=1
pkgdesc='iSCSI userland tools'
arch=('x86_64')
url='https://www.open-iscsi.com/'
license=('GPL')
makedepends=('systemd')
depends=('systemd-libs' 'util-linux-libs' 'kmod' 'openssl' 'open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz"
        '0001-Fix-compiler-error-introduced-with-recent-IPv6-commit.patch')
sha256sums=('d8a06c1ea86d2a312d598f52c151b210a34d82445a6f522f7c2bd6581b3a99af'
            '4b0db394a45bc5a0cd68be840f0852e2d6cdba834cf2bf15e48a8ebf41e4b9d2')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver}

  patch -Np1 < ../0001-Fix-compiler-error-introduced-with-recent-IPv6-commit.patch
}

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make user LIB_DIR=/usr/lib
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" LIB_DIR=/usr/lib install install_systemd

  install -D -m0644 "$srcdir"/${pkgname}-${pkgver}/etc/iscsid.conf "$pkgdir"/etc/iscsi
  echo -n > "$pkgdir"/etc/iscsi/initiatorname.iscsi

  # copy docs
  mkdir -p "$pkgdir"/usr/share/doc/${pkgname}
  install -m0644 Changelog "$pkgdir"/usr/share/doc/${pkgname}/
  install -m0644 README "$pkgdir"/usr/share/doc/${pkgname}/

  mv "$pkgdir"/sbin "$pkgdir"/usr/bin
}
