# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>

pkgname=open-iscsi
pkgver=2.0.877
pkgrel=2
pkgdesc='iSCSI userland tools'
arch=('x86_64')
url='https://www.open-iscsi.com/'
license=('GPL')
depends=('libsystemd' 'libutil-linux' 'kmod' 'openssl' 'open-isns')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf'
	'etc/iscsi/initiatorname.iscsi')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/open-iscsi/open-iscsi/archive/$pkgver.tar.gz"
        '0001-libkmod.patch')
sha256sums=('69eb95b0c39dee2da9d0d751bfdcdb8d11f9d37390de15c1a0b4558f9d0c4a57'
            'cacf5d5826868bc4afe4df6ac5721aafb75f82469c50d6d24cd7d685471a9cd8')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver}

  # fix build with libkmod
  patch -Np1 < ../0001-libkmod.patch

  # include iscsistart in the package
  sed -i -e '/^PROGRAMS = /s/$/ usr\/iscsistart/' Makefile
}

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make user LIB_DIR=/usr/lib
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" LIB_DIR=/usr/lib install

  install -D -m0644 etc/systemd/iscsid.service "$pkgdir"/usr/lib/systemd/system/iscsid.service
  install -D -m0644 etc/systemd/iscsid.socket "$pkgdir"/usr/lib/systemd/system/iscsid.socket
  install -D -m0644 etc/systemd/iscsi.service "$pkgdir"/usr/lib/systemd/system/iscsi.service

  install -D -m0644 "$srcdir"/${pkgname}-${pkgver}/etc/iscsid.conf "$pkgdir"/etc/iscsi
  echo -n > "$pkgdir"/etc/iscsi/initiatorname.iscsi

  # copy docs
  mkdir -p "$pkgdir"/usr/share/doc/${pkgname}
  install -m0644 Changelog "$pkgdir"/usr/share/doc/${pkgname}/
  install -m0644 README "$pkgdir"/usr/share/doc/${pkgname}/

  mv "$pkgdir"/sbin "$pkgdir"/usr/bin
}
