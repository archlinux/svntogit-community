# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools-modules
pkgver=2012.05.21
_pkgsubver=724730
pkgrel=1
pkgdesc="kernel modules for the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('GPL')
makedepends=('libdnet' 'icu' 'uriparser' 'linux-headers')
depends=("linux")
install=$pkgname.install
options=('!strip')
source=("http://downloads.sourceforge.net/open-vm-tools/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz"
	"modprobe.conf")
md5sums=('91affb5b23db8abceff930613845f434'
         'ac9b8e3cb798f5056ca92767ec705117')

build() {
  _kernver=`pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -`
  KERNEL_RELEASE=`cat /lib/modules/extramodules-3.${_kernver}-ARCH/version`

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  sed -i 's|proc-3.2.8|procps|g' configure
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr \
	--without-x --with-kernel-release=$KERNEL_RELEASE

  cd modules
  make modules -j1
}

package() {
  _kernver=`pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -`
  depends=("linux>=3.${_kernver}" "linux<3.`expr ${_kernver} + 1`")
  KERNEL_VERSION=`cat /lib/modules/extramodules-3.${_kernver}-ARCH/version`
  msg "Kernel = $KERNEL_VERSION"

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  mkdir -p $pkgdir/lib/modules/extramodules-3.${_kernver}-ARCH/
  for MOD in `find -type f -name '*.ko'`; do
    install -Dm644 $MOD $pkgdir/lib/modules/extramodules-3.${_kernver}-ARCH/
  done
  gzip $pkgdir/lib/modules/extramodules-3.${_kernver}-ARCH/*.ko

  install -D -m 644 ${srcdir}/modprobe.conf ${pkgdir}/etc/modprobe.d/${pkgname}.conf
  sed -i "s|extramodules-.*-ARCH|extramodules-3.${_kernver}-ARCH|" $startdir/$pkgname.install
}
