# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools-modules
pkgver=2011.08.21
_pkgsubver=471295
pkgrel=1
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('GPL')
makedepends=('libdnet' 'icu' 'uriparser' 'linux-headers')
depends=("linux")
install=$pkgname.install
source=("http://downloads.sourceforge.net/open-vm-tools/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz")
md5sums=('fd32f03ab6068c265597a74b916ca81e')

build() {
  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  sed -i 's#-lproc-3.2.7#-lproc-3.2.8#' configure
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --without-x
  (cd modules && make modules)
}

package() {
  _kernver=`pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -`
  depends=("linux>=3.${_kernver}" "linux<3.`expr ${_kernver} + 1`")
  KERNEL_VERSION=`uname -r`
  msg "Kernel = $KERNEL_VERSION"

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  mkdir -p $pkgdir/lib/modules/$KERNEL_VERSION/misc/
  for MOD in `find -type f -name '*.ko'`; do
    install -D -m644 $MOD $pkgdir/lib/modules/$KERNEL_VERSION/misc/
  done
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='$KERNEL_VERSION'/" $startdir/$pkgname.install
}
