# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
epoch=6
pkgver=10.1.5
pkgrel=1
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="https://github.com/vmware/open-vm-tools"
license=('LGPL')
depends=('libdnet' 'icu' 'procps-ng' 'uriparser' 'libsigc++' 'libxss'
	 'iproute2' 'fuse2' 'lsb-release' 'libmspack')
makedepends=('chrpath' 'doxygen' 'gtkmm' 'libxtst')
optdepends=('gtkmm: DnD/CP plugin'
	    'libxtst: DnD/CP, resolution set plugins'
	    'netctl: suspend-resume network state'
	    'networkmanager: suspend-resume network state')
backup=('etc/pam.d/vmware-guestd')
options=('docs')
source=($pkgname-$pkgver.tar.gz::https://github.com/vmware/open-vm-tools/archive/stable-${pkgver/_/-}.tar.gz
	network-script
	tools.conf
	vmware-guestd
	xautostart.conf
	vmtoolsd.service
	vmware-vmblock-fuse.service)
sha256sums=('aba7bf570b8951b61784941460659c204359d98fcbfb164df3c30d468a19fb1c'
            'c4add159c534384eb07e5e9a0694a60c253341916eca928f85e75f77c95d2e95'
            'f39403b884d897933d43cdd1093f308016963cf08162b21717240e37f7fd02ab'
            '53dbc915fb145fd8fbabe0b8be6c5bc25bfc9f40e1be28740fdc0b8e99889b26'
            '6ca56abad77f7d7c0507f2eb5cfb7bdfb14f34f2d392816fb7b384520f12436f'
            '5a9403f78356873258521644068f2c9639b994d5954e5ad953934136822b2689'
            '99e3cc1da20a751049144cc02dec77174a55109b5b5960e1facd78709da7724f')

prepare() {
  cd "$srcdir/$pkgname-"*"/open-vm-tools"
  sed -i \
    -e 's|-Werror||g' \
    -e 's|.*common-agent/etc/config/Makefile.*||' \
    configure.ac
  mkdir -p common-agent/etc/config
  autoreconf -vi
}

build() {
  cd "$srcdir/$pkgname-"*"/open-vm-tools"

  # glibmm 2.46 uses C++11 features
  CXXFLAGS+=' -std=gnu++11 -fpermissive '
  CFLAGS+=' -fpermissive '

  sh ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --without-xmlsecurity \
    --without-kernel-modules
  make
}

package() {
  cd "$srcdir/$pkgname-"*"/open-vm-tools"

  make install DESTDIR="$pkgdir"
  install -Dm0644 vmware-user-suid-wrapper/vmware-user.desktop "$pkgdir"/usr/share/applications/vmware-user.desktop

  # vmware-user XDG autostart
  mkdir -p "$pkgdir"/etc/xdg/autostart
  ln -s /usr/share/applications/vmware-user.desktop "$pkgdir"/etc/xdg/autostart/vmware-user.desktop

  install -D -m 755 scripts/common/vmware-xdg-detect-de "$pkgdir"/usr/bin/vmware-xdg-detect-de
  chmod 07755 "$pkgdir"/usr/bin/vmware-user-suid-wrapper

  install -Dm0755 "$srcdir"/network-script "$pkgdir"/etc/vmware-tools/scripts/vmware/network
  install -Dm0644 "$srcdir"/tools.conf "$pkgdir"/etc/vmware-tools/tools.conf
  install -Dm0644 "$srcdir"/xautostart.conf "$pkgdir"/etc/vmware-tools/xautostart.conf
  install -Dm0644 "$srcdir"/vmware-guestd "$pkgdir"/etc/pam.d/vmware-guestd
  rm -rf "$pkgdir"/usr/etc

  # We don't want a symlink in /sbin
  rm "$pkgdir"/sbin/mount.vmhgfs
  rmdir "$pkgdir"/sbin

  cd "$pkgdir" && find -type f -exec sh -c "file {} | grep ELF >/dev/null && echo {} && chrpath -d {}" \;
  install -Dm644 "$srcdir"/vmtoolsd.service "$pkgdir"/usr/lib/systemd/system/vmtoolsd.service
  install -Dm644 "$srcdir"/vmware-vmblock-fuse.service "$pkgdir"/usr/lib/systemd/system/vmware-vmblock-fuse.service

  rm -f "$pkgdir"/etc/vmware-tools/scripts/vmware/*.orig
}
