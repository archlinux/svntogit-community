# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
epoch=6
pkgver=11.0.1
pkgrel=2
pkgdesc='The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools'
arch=('x86_64')
url='https://github.com/vmware/open-vm-tools'
license=('LGPL')
depends=('libdnet' 'icu' 'procps-ng' 'uriparser' 'libsigc++' 'libxss'
         'iproute2' 'fuse2' 'lsb-release' 'libmspack')
makedepends=('chrpath' 'doxygen' 'gtkmm3' 'libxtst' 'python' 'rpcsvc-proto')
optdepends=('gtkmm3: DnD/CP plugin'
            'libxtst: DnD/CP, resolution set plugins'
            'netctl: suspend-resume network state'
            'networkmanager: suspend-resume network state')
backup=('etc/xdg/autostart/vmware-user.desktop')
options=('docs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/vmware/open-vm-tools/archive/stable-${pkgver/_/-}.tar.gz"
        '0001-Remove-references-to-deprecated-G_INLINE_FUNC.patch'
        '0002-stop-systemd-243-udev-complaints.patch'
        'vmtoolsd.service'
        'vmware-vmblock-fuse.service')
sha256sums=('99f1e3c5245bb002c1e66cbb7a1078e1c3567db5f92cc2e00ab08557e9df4758'
            'c97484a15dbc65d7eb14bbd96dd3785e2a1a130a44e3950f9865a988c28bc137'
            'a01990c7cda8426e5bfcebc33b4024ff037367e27d58c006f5b0fe6a7f5bd52c'
            '5a9403f78356873258521644068f2c9639b994d5954e5ad953934136822b2689'
            '99e3cc1da20a751049144cc02dec77174a55109b5b5960e1facd78709da7724f')

prepare() {
  cd "$srcdir/$pkgname-stable-${pkgver}/"

  patch -Np1 < ../0001-Remove-references-to-deprecated-G_INLINE_FUNC.patch
  patch -Np1 < ../0002-stop-systemd-243-udev-complaints.patch
}

build() {
  cd "$srcdir/$pkgname-stable-${pkgver}/open-vm-tools/"

  autoreconf -vi
  sh ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --without-xmlsecurity \
    --without-kernel-modules
  make
}

package() {
  cd "$srcdir/$pkgname-stable-${pkgver}/open-vm-tools/"

  make install DESTDIR="$pkgdir"
  chmod 7755 "$pkgdir"/usr/bin/vmware-user-suid-wrapper

  # install vmware-xdg-detect-de
  install -D -m0755 scripts/common/vmware-xdg-detect-de "$pkgdir"/usr/bin/vmware-xdg-detect-de

  # We don't want a symlink in /sbin
  rm "$pkgdir"/sbin/mount.vmhgfs
  rmdir "$pkgdir"/sbin

  # install systemd files
  install -D -m0644 "$srcdir"/vmtoolsd.service "$pkgdir"/usr/lib/systemd/system/vmtoolsd.service
  install -D -m0644 "$srcdir"/vmware-vmblock-fuse.service "$pkgdir"/usr/lib/systemd/system/vmware-vmblock-fuse.service
}
