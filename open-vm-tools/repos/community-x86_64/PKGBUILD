# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
pkgver=2011.11.20
_pkgsubver=535097
pkgrel=1
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools."
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('LGPL')
depends=('open-vm-tools-modules' 'libdnet' 'icu' 'procps' 'glib2' 'uriparser' 'libsigc++' 'libxss')
makedepends=('chrpath' 'doxygen' 'gtkmm' 'fuse' 'libxtst')
optdepends=('gtkmm' 'libnotify' 'libxtst' 'fuse' 'libsm')
options=('docs' '!libtool')
install=$pkgname.install
source=(http://switch.dl.sourceforge.net/$pkgname/$pkgname-$pkgver-${_pkgsubver}.tar.gz
	scripts-network.patch
	scripts-network-FS19541.patch
	open-vm-tools-X11Bool.patch
	open-vm-tools.conf.d
	open-vm-tools.rc.d
	tools.conf
	vmware-guestd
	xautostart.conf)
md5sums=('428d803f6fe3424af88768fc2f88f9ae'
         '06f7448e274db2a911f582e276088fc9'
         '199508b0d3fe6fa9b726d09170bf51f1'
         '8c333a979578bdc0c3134c1dd6bb7353'
         '79b0a14d86191fee70a4639da8bd7785'
         '136ff53d0d3a303d09266a407fd2d215'
         'b55d15e2c4be396aad709aeca91033d3'
         '73cc1a2665b0dd62427733d62ead8b9a'
         '75a25d83417e683957321f97a00f8465')

build() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"
  [ $NOEXTRACT -eq 1 ] || {
    sed -i 's#CFLAGS="$CFLAGS -Werror"##' configure configure.ac
  }
  [ -f Makefile ] || ./configure --prefix=/usr --without-kernel-modules
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"

  make install DESTDIR=$pkgdir
  install -D -m 755 scripts/common/vmware-xdg-detect-de $pkgdir/usr/bin/vmware-xdg-detect-de
  chmod 07755 $pkgdir/usr/bin/vmware-user-suid-wrapper

  cd $pkgdir
  patch -p1 -i $srcdir/scripts-network.patch etc/vmware-tools/scripts/vmware/network
  patch -p1 -i $srcdir/scripts-network-FS19541.patch etc/vmware-tools/scripts/vmware/network

  install -D -m 755 $srcdir/open-vm-tools.rc.d $pkgdir/etc/rc.d/open-vm-tools
  install -D -m 644 $srcdir/open-vm-tools.conf.d $pkgdir/etc/conf.d/open-vm-tools
  install -D -m 644 $srcdir/tools.conf $pkgdir/etc/vmware-tools/tools.conf
  install -D -m 644 $srcdir/xautostart.conf $pkgdir/etc/vmware-tools/xautostart.conf
  install -D -m 644 $srcdir/vmware-guestd $pkgdir/etc/pam.d/vmware-guestd
  rm -rf $pkgdir/usr/etc

  ln -fs /usr/sbin/mount.vmhgfs $pkgdir/sbin/mount.vmhgfs

  cd $pkgdir && find -type f -exec sh -c "file {} | grep ELF >/dev/null && echo {} && chrpath -d {}" \;
}
