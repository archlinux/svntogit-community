# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=openbox
pkgver=3.5.0
pkgrel=1
pkgdesc="A window manager for the X11 windowing system"
arch=('i686' 'x86_64')
url="http://openbox.org"
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
         'libxcursor' 'pango')
optdepends=('pyxdg: for the xdg-autostart script')
groups=('lxde')
install=${pkgname}.install
options=('!libtool')
backup=('etc/xdg/openbox/menu.xml' 'etc/xdg/openbox/rc.xml')
source=("http://www.icculus.org/openbox/releases/${pkgname}-${pkgver}.tar.gz"
        'which-2.20.patch')
md5sums=('00441b53cf14c03566c8e82643544ff9'
         '7ca3b5244bb092d46f5bcf1e2bdf4a18')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  # FS#11455
  patch -p1 -i "${srcdir}"/which-2.20.patch
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    data/autostart/openbox-xdg-autostart
  ./configure --prefix=/usr \
    --with-x \
    --enable-startup-notification \
    --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  sed -i "s:startkde:/usr/bin/\0:" \
    "${pkgdir}"/usr/share/xsessions/openbox-kde.desktop
  sed -i "s:=gnome-session:=/usr/bin/gnome-session:" \
    "${pkgdir}"/usr/share/xsessions/openbox-gnome.desktop
}
