# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname="openbsd-netcat"
pkgver=1.105
pkgrel=4
_patch=7
pkgdesc="TCP/IP swiss army knife. OpenBSD variant."
arch=('i686' 'x86_64')
url="http://packages.debian.org/sid/netcat-openbsd"
license=('BSD')
depends=('libbsd')
makedepends=('gcc' 'make')
source=("http://ftp.debian.org/debian/pool/main/n/netcat-openbsd/netcat-openbsd_${pkgver}.orig.tar.gz"
	"http://ftp.debian.org/debian/pool/main/n/netcat-openbsd/netcat-openbsd_${pkgver}-${_patch}.debian.tar.gz")
md5sums=('7e67b22f1ad41a1b7effbb59ff28fca1'
         'e914f8eb7eda5c75c679dd77787ac76b')

build() {
  cd $srcdir/netcat-openbsd-${pkgver}
  for i in `cat ../debian/patches/series`; do
	echo "** patch $i" 1>&2
	cat "../debian/patches/$i"
  done | patch -p1
  make \
	CFLAGS="$CFLAGS -DDEBIAN_VERSION=\"\\\"${pkgrel}\\\"\" -I/usr/include/libbsd" \
	LDFLAGS="$LDFLAGS -lbsd"
}

package() {
  cd $srcdir/netcat-openbsd-${pkgver}
  install -Dm0755 nc $pkgdir/usr/bin/nc.openbsd
  install -Dm0644 nc.1 $pkgdir/usr/share/man/man1/nc.openbsd.1
  install -dm0755 $pkgdir/usr/share/licenses/$pkgname/
  head -n28 netcat.c | tail -n+2 >$pkgdir/usr/share/licenses/$pkgname/LICENSE
}
