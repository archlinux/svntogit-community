# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=opencc
pkgname=(opencc opencc-doc)
pkgver=0.4.3
pkgrel=2
url="http://code.google.com/p/opencc/"
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('cmake' 'doxygen')
source=("http://opencc.googlecode.com/files/${pkgbase}-${pkgver}.tar.gz")

build() {
  cd ${pkgbase}-${pkgver}
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_DOCUMENTATION:BOOL=ON
  make
}

package_opencc() {
  pkgdesc="Library for Open Chinese Convert"
  depends=('glibc')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  # Remove docs - install in splitted package
  rm -r "$pkgdir/usr/share/opencc/doc"
}

package_opencc-doc() {
  pkgdesc="Documentation for Library for Open Chinese Convert"

  cd ${pkgbase}-${pkgver}/doc
  make DESTDIR="${pkgdir}" install

  # Remove manpages - already installed in main package
  rm -r "$pkgdir/usr/share/man"
}

sha512sums=('ac31c3c018467cf047e077d399903560feb0f8c214accd1ef45d0b1477d011af8e9bf2dbde4497c133daef61b6508460e9b2b04c0b5f336c2d67fc840a1d07e6')
