# $Id$
# Maintainer: Jonathan Steel <mail at jsteel dot org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Wesley <rudirennsau at hotmail dot com>

pkgname=openclonk
pkgver=5.3.3
pkgrel=1
pkgdesc="A multiplayer-action-tactic-skill game"
arch=('i686' 'x86_64')
url="http://openclonk.org"
license=('custom')
conflicts=('openclonk-hg')
depends=('gtk2' 'glew' 'sdl_mixer' 'libxpm'  'hicolor-icon-theme' 'libupnp')
makedepends=('cmake' 'boost' 'imagemagick' 'mesa')
install=$pkgname.install
source=(http://hg.openclonk.org/$pkgname/archive/$pkgname-release-$pkgver-src.tar.gz)
md5sums=('a61d8a7d7c41bcc8d85a647c25095658')

build() {
  cd "$srcdir"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr

  make 
}

package() {
  cd "$srcdir"/build

  make DESTDIR="$pkgdir"/ install

  install -Dm644 clonk.png "$pkgdir"/usr/share/pixmaps/clonk.png

  cd "$srcdir"

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 licenses/*.txt "$pkgdir"/usr/share/licenses/$pkgname
}
