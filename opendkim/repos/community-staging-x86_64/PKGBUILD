# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Thomas Jost <schnouki@schnouki.net>

pkgname=opendkim
pkgver=2.10.3
pkgrel=8
pkgdesc="An open source implementation of the DKIM sender authentication system. Based on a fork of dkim-milter."
arch=(x86_64)
url="http://www.opendkim.org/"
license=('BSD' 'custom:Sendmail')
depends=("db" "openssl" "libbsd" "opendbx" "libldap")
optdepends=("perl: for opendkim-genkey")
makedepends=("libmilter")
options=(!emptydirs)
backup=(etc/conf.d/opendkim)
validpgpkeys=('5CDD574C22FF4D2480ACABDF5254B96BC608B511')
source=(https://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
        opendkim.conf
        opendkim.sysusers
        opendkim.tmpfiles
        opendkim.service
        openssl_1.1.0_compat.patch)
sha256sums=('43a0ba57bf942095fe159d0748d8933c6b1dd1117caf0273fa9a0003215e681b'
            'SKIP'
            'a3b5ecf4f4fe4a79392a75f7c4b6cc93a51ef8503215bb0f64d795f8754d4b68'
            '525ac5df345e753461efa8f6528f5532a00ecf2562f985a3c6c7169235170499'
            'ab66c9fdcb8913f2a550289dd44d56d17b960cf5275839ffd03cba70a27ab6ed'
            '3e1cb3a3ddb794253d5a1c59673c40687bbc2b1bf878e70ccd03356f6d1e6735'
            '5095a7516493af08396060852851231c9b1cba5ee00493f810f0dfc9a69f8dfe')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir"/openssl_1.1.0_compat.patch
  autoreconf -i
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin --with-db \
    --with-odbx --with-sql-backend --with-openldap
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/opendkim.sysusers" "$pkgdir/usr/lib/sysusers.d/opendkim.conf"
  install -Dm644 "$srcdir/opendkim.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/opendkim.conf"

  # configure default configuration
  install -Dm644 "$srcdir/opendkim.conf" "$pkgdir/etc/conf.d/opendkim"

  # License
  mkdir -p "$pkgdir/usr/share/licenses/opendkim"
  for f in LICENSE LICENSE.Sendmail; do
    ln -s ../../doc/opendkim/$f "$pkgdir/usr/share/licenses/opendkim/$f"
  done

  install -Dm0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
