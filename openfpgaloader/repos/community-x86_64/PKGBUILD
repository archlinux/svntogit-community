# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=openFPGALoader
pkgname=openfpgaloader
pkgver=0.1
pkgrel=2
pkgdesc='Universal utility for programming FPGA'
arch=('x86_64')
url='https://github.com/trabucayre/openFPGALoader'
license=('AGPL')
depends=('libftdi' 'libusb-1.0.so' 'libudev.so')
makedepends=('cmake' 'libusb' 'systemd-libs')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0a2f8e831bf7f2c623d6c7253c4bb7a77257d45add987a2dbd1a74c2422d823d1ce46574973a59921bd9ea4beb33b0cff02610d41abd28af2c5a0ebcb7db8e4f')

prepare() {
  sed -i 's|MODE="664", GROUP="plugdev"|MODE="666"|g' $_pkgname-$pkgver/99-openfpgaloader.rules
}

build() {
  mkdir -p $_pkgname-$pkgver/build
  cd $_pkgname-$pkgver/build

  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_UDEV=True

  make
}

package() {
  cd $_pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 ../99-openfpgaloader.rules "$pkgdir"/usr/lib/udev/rules.d/99-openfpgaloader.rules
}

