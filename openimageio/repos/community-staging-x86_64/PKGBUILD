# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=openimageio
pkgver=2.1.16.0
pkgrel=2
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'openjpeg2' 'glew' 'libtiff' 'opencolorio' 'intel-tbb' 'libpng' 'libraw' 'libwebp')
makedepends=('cmake' 'qt5-base' 'python' 'boost' 'mesa' 'git' 'freetype2' 'fontconfig' 'libxrender' 'ninja')
optdepends=('qt5-base: iv image viewer'
            'python: bindings support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenImageIO/oiio/archive/Release-$pkgver.tar.gz"
         openimageio-libraw-0.20.patch::"https://github.com/OpenImageIO/oiio/commit/7b602c17.patch")
sha512sums=('30de5e5f915ba319d6e63d0bac2c30c5368f8ebd1434e91cb5bcfad3d39efda5b1779a3fedbcdf0c94ec09f1d1370da0b785af06a1fb8f164dfa52adec0db6cb'
            '5f8bc45c6ed3f3a12c1755e6e074047600714c587d490d423a8f1630b2bb9ec80031c1529e41d7e4f6c7a910e302655d2d09366f18761688fff1d50cd0ab0e16')

prepare() {
  cd oiio-Release-$pkgver
  patch -p1 -i ../openimageio-libraw-0.20.patch # Fix build with libraw 0.20
}

build() {
  cd oiio-Release-$pkgver

  cmake \
      -Bbuild \
      -GNinja \
      -DUSE_PYTHON=ON \
      -DPYTHON_VERSION=3.8 \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DOIIO_BUILD_TESTS=ON \
      -DOIIO_BUILD_TOOLS=ON \
      -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd oiio-Release-$pkgver

  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE".md
}
