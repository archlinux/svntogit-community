# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: goodmen <goodmenzy@gmail.com>

pkgname=openipmi
_pkgname=OpenIPMI
pkgver=2.0.22
pkgrel=5
pkgdesc='Full-function IPMI (Intelligent Platform Management Interface) system'
url='http://openipmi.sourceforge.net/'
arch=('i686' 'x86_64')
license=('LGPL2.1')
makedepends=('swig')
depends=('popt' 'ncurses' 'net-snmp' 'glib2' 'gdbm')
source=("https://downloads.sourceforge.net/project/${pkgname}/${_pkgname}%202.0%20Library/${_pkgname}-${pkgver}.tar.gz"
         '0001-Add-openssl-1.1.0-support.patch')
sha256sums=('4988900043c35fcfa9b2bf275d6593904f6429221befb770ba6ecb5502108e55'
            'ec0364fd24d5ffb5349ed01d67910c5f15301b028e008977330786f9cf388ec2')

options=('!libtool')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
        patch -Np1 -i $srcdir/0001-Add-openssl-1.1.0-support.patch

	sed \
		-e '/Requires:/s/pthread//' \
		-e '/Libs:/s/$/ -lpthread/' \
		-i OpenIPMIpthread.pc.in
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install-exec
	make DESTDIR="${pkgdir}" install
	install -Dm644 doc/IPMI.pdf "${pkgdir}/usr/share/doc/${pkgname}/IPMI.pdf"
}
