# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Arthur Zamarin <arthurzam@gmail.com>

pkgname=openlibm
pkgver=0.7.0
pkgrel=1
pkgdesc='Standalone implementation of C mathematical functions'
arch=(x86_64)
url='https://openlibm.org/'
license=(BSD)
depends=(glibc)
source=("$pkgname-$pkgver.tar.gz::https://github.com/JuliaLang/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('1699f773198018b55b12631db9c1801fe3ed191e618a1ee1be743f4570ae06a3')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|/usr/local|/usr|' -i Make.inc
}

build() {
  cd $pkgname-$pkgver
  make prefix=/usr
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
