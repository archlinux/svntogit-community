# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=openlierox
pkgver=0.58_rc3
pkgrel=7
pkgdesc="A real-time excessive Worms-clone"
arch=('i686' 'x86_64')
url="http://www.openlierox.net/"
license=('LGPL')
depends=('curl' 'gcc-libs' 'sdl_image' 'sdl_mixer' 'gd' 'hawknl' 'libxml2' 'libzip')
makedepends=('cmake')
optdepends=('bash: for scripts support'
            'python: for scripts support')
conflicts=('openlierox-beta' 'openlierox-svn')
source=(http://downloads.sourceforge.net/$pkgname/OpenLieroX_$pkgver.src.tar.bz2)
md5sums=('c4f7057d8f210e30bdef3cfdd75d613b')

build() {
  cd "$srcdir"/OpenLieroX

  sed -i '1i #include <sys/stat.h>' src/breakpad/external/src/common/linux/file_id.cc
  sed -i 's|/usr/share/games|/usr/share|g' include/FindFile.h

  sed -i '/curl\/types\.h/d' include/HTTP.h
  sed -i '/curl\/types\.h/d' src/common/HTTP.cpp

  [[ -d build ]] && rm -r build 
  mkdir build
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSTEM_DATA_DIR=/usr/share \
		-DHAWKNL_BUILTIN=OFF \
		-DBREAKPAD=OFF

  make
}

package() {
  cd "$srcdir"/OpenLieroX

  # Awesome, they are using CMake but they do not want it to also make the install.

  # install binary
  install -Dm755 bin/$pkgname "$pkgdir"/usr/bin/$pkgname

  # install data
  install -m755 -d "$pkgdir"/usr/share/OpenLieroX
  cp -r share/gamedir/* "$pkgdir"/usr/share/OpenLieroX/
  find "$pkgdir"/usr/share/OpenLieroX -type d -exec chmod 755 {} +
  find "$pkgdir"/usr/share/OpenLieroX -type f -exec chmod 644 {} +

  # install manual
  install -Dm644 doc/$pkgname.6 "$pkgdir"/usr/share/man/man6/$pkgname.6

  # install icon
  install -Dm644 share/OpenLieroX.svg \
    "$pkgdir"/usr/share/pixmaps/OpenLieroX.svg

  # install .desktop file
  install -Dm644 share/$pkgname-$pkgname.desktop \
    "$pkgdir"/usr/share/applications/$pkgname.desktop
}
