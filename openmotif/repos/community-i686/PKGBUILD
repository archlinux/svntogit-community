# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=openmotif
pkgver=2.3.7
pkgrel=1
pkgdesc="Open Motif"
arch=('i686' 'x86_64')
url="http://www.motifzone.org/"
license=('LGPL')
depends=(libxext libxp libxft libxt libxmu libpng libjpeg)
makedepends=(xbitmaps xproto printproto xextproto automake)
conflicts=(lesstif)
source=("http://downloads.sourceforge.net/project/motif/Motif%20$pkgver%20Source%20Code/motif-$pkgver.tar.gz")
sha256sums=('8f7aadbb0f42df2093d4690735a2b9a02ea2bf69dfb15ae0a39cae28f1580d14')

build() {
  cd "$srcdir"/motif-$pkgver
  export LDFLAGS="-lX11"
  [ -x configure ] || ./autogen.sh --prefix=/usr
  [ -f Makefile ] || ./configure --prefix=/usr
  make -j1
}

package() {
  cd "$srcdir"/motif-$pkgver
  make DESTDIR="$pkgdir" install
}
