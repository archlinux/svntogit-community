# $Id:
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sandy Carter <bwrsandman@gmail.com>
pkgname=openmw
pkgver=0.43.0
pkgrel=2
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'bullet' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://github.com/OpenMW/openmw/archive/openmw-${pkgver}.tar.gz")
sha512sums=('e902dd2274c05838bb1171f07d7a673fc7b85d1290a9e01c8ab89f1c279bd236332ecdc0f2b7944ec7209a0e6022ff472639b5724bf26904e3a2d338c7be401a')

build() {
  mkdir -p build
  cd build
  cmake "../$pkgname-$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDESIRED_QT_VERSION=5 \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
