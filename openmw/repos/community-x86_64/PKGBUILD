# $Id:
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sandy Carter <bwrsandman@gmail.com>
pkgname=openmw
pkgver=0.42.0
pkgrel=2
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64' 'i686')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'bullet' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://github.com/OpenMW/openmw/archive/openmw-${pkgver}.tar.gz")
sha512sums=('f638e804e03e965db8e19cb37b3be67804202c1e43b0abf2730e0be104093edc31fc3656f90c570980b66ece4f5c889b44f0f59789e865327d221714a9ed65f3')

build() {
  mkdir -p build
  cd build
  cmake "../$pkgname-$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDESIRED_QT_VERSION=5 \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
