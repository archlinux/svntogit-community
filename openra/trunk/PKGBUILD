# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Matthew Bowra-Dean <matthew@ijw.co.nz>
pkgname=openra
pkgver=20150424
pkgrel=1
pkgdesc="An open-source implementation of the Red Alert engine using .NET/mono and OpenGL"
arch=('any')
url="http://open-ra.org"
license=('GPL3')
install=openra.install
depends=('mono' 'ttf-dejavu' 'openal' 'libgl' 'freetype2' 'sdl2' 'lua51' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'desktop-file-utils' 'xdg-utils' 'zenity')
makedepends=('git' 'unzip')
options=(!strip)
source=("git+https://github.com/OpenRA/OpenRA.git#tag=release-${pkgver}"
        "https://build.opensuse.org/source/games:openra/playtest/thirdparty.tar.gz?rev=bf7c8edd543f4dc53cea52c78667427c")
md5sums=('SKIP'
         '3e65480d58f2602c127b362f7a1813fe')

prepare() {
  cd OpenRA

  make version

  # Don't ask :(
  mv ${srcdir}/thirdparty/* thirdparty/
  cp thirdparty/*dll* .
}

build() {
  cd OpenRA

  #make cli-dependencies
  make native-dependencies
  make core
  make tools
}

package() {
  cd OpenRA

  make prefix=/usr DESTDIR="$pkgdir" install-all
  make prefix=/usr DESTDIR="$pkgdir" install-linux-shortcuts
  make prefix=/usr DESTDIR="$pkgdir" install-linux-mime
}
