# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-anomaly-detection-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Anomaly Detection Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/monitoring-plugins/ad"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/anomaly-detection/archive/${pkgver}.tar.gz"
)
sha256sums=('7028b95095a639303dabd68403a3dd7f376e56e23dcc0292f9f2c750257998d8')

build() {
  cd "anomaly-detection-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-anomaly-detection"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-anomaly-detection"
  unzip "${srcdir}/anomaly-detection-${pkgver}/build/distributions/opensearch-anomaly-detection-${pkgver}.zip"

  install -Dm644 "${srcdir}/anomaly-detection-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
