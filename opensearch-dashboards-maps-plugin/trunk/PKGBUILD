# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-maps-plugin'
_pluginname='dashboards-maps'
pkgver=2.2.1.0
_dashboardsver=2.2.1
pkgrel=1
pkgdesc='OpenSearch Dashboards Maps Plugin'
url='https://opensearch.org/docs/latest/dashboards/geojson-regionmaps/'
arch=('x86_64')
license=('Apache')
depends=("opensearch-dashboards=${_dashboardsver}")
makedepends=('yarn' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=('SKIP'
            'SKIP')

build() {
  mv "${_pluginname}/src/plugins/custom_import_map" "OpenSearch-Dashboards/plugins/"
  cd "OpenSearch-Dashboards/plugins/custom_import_map"
  yarn osd bootstrap
  yarn plugin-helpers build --skip-archive # `yarn build` will always create the ZIP
}

check() {
  cd "OpenSearch-Dashboards/plugins/custom_import_map"
  yarn test:jest -u
}

package() {
  install -Dm644 "${_pluginname}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"
  cd "${srcdir}/OpenSearch-Dashboards/plugins/custom_import_map"
  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"
  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
