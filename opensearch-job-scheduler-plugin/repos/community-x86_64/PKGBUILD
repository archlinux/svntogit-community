# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-job-scheduler-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Job Scheduler Plugin"
arch=('x86_64')
url="https://github.com/opensearch-project/job-scheduler"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/job-scheduler/archive/${pkgver}.tar.gz"
)
sha256sums=('9be7f9f0d9aad21a4cbc96a0c7e8cdd0b8b9de3b10373b840136e741f0fbbf70')

build() {
  cd "job-scheduler-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-job-scheduler"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-job-scheduler"
  unzip "${srcdir}/job-scheduler-${pkgver}/build/distributions/opensearch-job-scheduler-${pkgver}.zip"

  install -Dm644 "${srcdir}/job-scheduler-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
