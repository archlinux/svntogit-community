# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-observability-plugin
pkgver=1.2.3.0
_opensearchver=1.2.3
pkgrel=1
pkgdesc="OpenSearch Observability Plugin"
arch=('x86_64')
url="https://github.com/opensearch-project/observability"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/observability/archive/${pkgver}.tar.gz"
)
sha256sums=('41d9c8707e5e052043a6e66cdd1c0a94c71cade3794b138d1e449a3075246685')

build() {
  cd "observability-${pkgver}/opensearch-observability"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-observability"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-observability"
  unzip "${srcdir}/observability-${pkgver}/opensearch-observability/build/distributions/opensearch-observability-${pkgver}.zip"

  install -Dm644 "${srcdir}/observability-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
