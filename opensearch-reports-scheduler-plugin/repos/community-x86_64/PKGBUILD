# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-reports-scheduler-plugin
pkgver=2.0.1.0
pkgrel=1
_opensearchver=2.0.1
_jdkver=11
pkgdesc="OpenSearch Reports Scheduler Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/dashboards/reporting"
license=('Apache')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/dashboards-reports/archive/${pkgver}.tar.gz"
)
sha256sums=('9253df61357aed1be4d7d16522061b4bc43596fedc25ff614309e00024d482c5')

build() {
  cd "dashboards-reports-${pkgver}/reports-scheduler"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-reports-scheduler"
  unzip "${srcdir}/dashboards-reports-${pkgver}/reports-scheduler/build/distributions/opensearch-reports-scheduler-${pkgver}.zip"

  install -Dm644 "${srcdir}/dashboards-reports-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
