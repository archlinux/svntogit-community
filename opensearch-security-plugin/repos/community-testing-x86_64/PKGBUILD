# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-security-plugin
pkgver=1.2.4.0
pkgrel=1
pkgdesc="OpenSearch Security Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/security-plugin"
license=('Apache')
depends=('bash')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip' 'maven' 'git')
source=("git+https://github.com/opensearch-project/security.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd "security"
  sed -i "s|^    <version>${pkgver}-SNAPSHOT</version>$|    <version>${pkgver}</version>|" pom.xml
  sed -i 's|^        <opensearch.version>\([0-9\.]*\)-SNAPSHOT</opensearch.version>$|        <opensearch.version>\1</opensearch.version>|' pom.xml
}

build() {
  cd "security"
  # TODO: Tests take forever and fail, find a way to skip integration tests
  mvn clean package -Padvanced -DskipTests
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-security"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-security"
  unzip "${srcdir}/security/target/releases/opensearch-security-${pkgver}.zip"

  install -Dm644 "${srcdir}/security/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
