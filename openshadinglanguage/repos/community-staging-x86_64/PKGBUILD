# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openshadinglanguage
pkgver=1.11.10.0
pkgrel=2
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses' 'clang')
makedepends=('boost' 'cmake' 'python' 'llvm' 'ninja')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
sha512sums=('c81e784ada4d6aed5923c979eb9f601595630c3d4a59098bfb69459364480a7cd6022f9f4c18dd724a3bba2026e40f5a157d91683a1d76bdb1e8e30f97f495a1')

build() {
  cd OpenShadingLanguage-Release-$pkgver

  cmake . \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=14 \
    -DLLVM_STATIC=0 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-Release-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
