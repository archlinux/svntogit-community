# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openshadinglanguage
pkgver=1.10.9
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'python' 'llvm' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
sha512sums=('aed137bfed55dad86087f39c67c03a62a5c61495cb3b20395c8b6b86463a2c23e78fa502970274960522cd7d2dd5ae6fefe84b1f7fe932cc63c2bcb8772d96de')

prepare() {
  mkdir OpenShadingLanguage-Release-$pkgver/src/build
}

build() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLLVM_STATIC=0 \
    -DENABLERTTI=ON \
    -DSTOP_ON_WARNING=OFF
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install
  mkdir "$pkgdir"/usr/share/OSL
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders

  install -Dm644 "$pkgdir"/usr/share/doc/OSL/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
