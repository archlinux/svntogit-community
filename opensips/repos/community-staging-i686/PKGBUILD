# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=opensips
pkgver=2.2.2
pkgrel=4
pkgdesc="An Open Source SIP Server able to act as a SIP proxy, registrar, location server, redirect server ..."
url="http://www.opensips.org"
depends=('gcc-libs' 'openssl' 'db' 'attr' 'libxml2')
makedepends=('postgresql-libs>=8.4.1' 'unixodbc' 'libldap>=2.4.18' 'libmariadbclient'
	     'lynx' 'libxslt' 'libmicrohttpd')
optdepends=('postgresql-libs'
	    'unixodbc'
	    'libldap'
	    'libmariadbclient'
	    'libsasl'
	    'python2'
	    'pcre')
backup=("etc/opensips/opensips.cfg"
	"etc/opensips/osipsconsolerc"
	"etc/opensips/opensipsctlrc")
arch=('i686' 'x86_64')
license=('GPL')
install=opensips.install
options=('!emptydirs' 'zipman' '!makeflags' 'docs')
source=(https://opensips.org/pub/opensips/${pkgver}/opensips-${pkgver}.tar.gz
	opensips.service
	port-tls-1.1.0.patch)
sha256sums=('a21777b37c3669617d24b97697dc3f4e613ec87b292f4541cf956a2d539e70c4'
            'c2fec4be085b108db10834fa9832e98d696c2de6408f85f96cf89c13bf6be819'
            '1ad2558c329a1b41948ff9ef1c8169289b38500ce8183e50bae653ef82afdbec')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver/

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  sed -i 's|sbin|bin|g' Makefile
  sed -i 's|bin-dir = sbin/|bin-dir = bin/|' Makefile.defs

  patch -Np1 -i ../port-tls-1.1.0.patch
}

_modules="ldap db_mysql db_postgres db_unixodbc presence presence_xml h350 proto_tls tlsops tls_mgm db_http httpd tm rr"

build() {
  cd "$srcdir"/$pkgname-$pkgver/

  make \
	include_modules="${_modules}" \
	LIBDIR=lib PREFIX=/usr
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/

  make \
	include_modules="${_modules}" \
	BASEDIR="$pkgdir" PREFIX=/usr LIBDIR=lib install

  # Conforms to the arch packaging standards (http://wiki.archlinux.org/index.php/Arch_Packaging_Standards)
  mkdir -p "$pkgdir"/etc/
  mv "$pkgdir"/usr/etc/opensips/ "$pkgdir"/etc/
  sed -i 's#mpath=".*lib/opensips/modules/"#mpath="/usr/lib/opensips/modules/"#' "$pkgdir"/etc/opensips/opensips.cfg

  # fix bad paths
  cd "$pkgdir"/usr/share
  find -type f -exec sed -i "s#"$pkgdir"##" {} \;

  cd "$pkgdir"/usr/lib/opensips/opensipsctl
  find -type f -exec sed -i "s#"$pkgdir"##" {} \;

  mv "$pkgdir"/usr/sbin "$pkgdir"/usr/bin

  cd "$pkgdir"/usr/bin
  sed -i "s#"$pkgdir"##" opensipsctl opensipsdbctl osipsconsole

  cd "$pkgdir"/etc
  find -type f -exec sed -i "s#"$pkgdir"##" {} \;

  install -Dm0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
