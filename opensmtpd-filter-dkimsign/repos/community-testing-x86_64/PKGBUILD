# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Maarten de Vries <maarten@de-vri.es>

_pkg=filter-dkimsign
pkgname=opensmtpd-${_pkg}
pkgver=0.5
pkgrel=3
pkgdesc="OpenSMTPD filter for signing mail with DKIM"
license=(BSD)
url="https://imperialat.at/dev/filter-dkimsign/"
arch=(x86_64)
depends=(libopensmtpd openssl)
makedepends=(gzip mandoc)
source=(https://distfiles.sigtrap.nl/${_pkg}-${pkgver}.tar.gz
        LICENSE)
sha512sums=('dfb3f7b47b139e2f3ad63c591318725378b501880e096659f3b8f4bd13012f18bc4d591209bb803d89efbbfcddd68a88a657c4d9eb0ca56806cdeced05fd9abc'
            '24ffeb515e767416728adf4f02aac9b6305447ff4ce8acdfb1ecb8321f76e261e5a536b20df9acec90542e3c3f30e259c0d11cd99d401eb154fd5faf89a6e125')

build() {
  cd ${_pkg}-${pkgver}
  make -f Makefile.gnu
}

package() {
  cd ${_pkg}-${pkgver}
  make -f Makefile.gnu BINDIR=/usr/lib/smtpd/opensmtpd/ DESTDIR="${pkgdir}" install
  install -Dm644 ../LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
