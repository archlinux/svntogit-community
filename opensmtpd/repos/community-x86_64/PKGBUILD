# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: parchd <parchd@archlinux.info>

pkgname=opensmtpd
pkgver=6.0.3p1
pkgrel=1
pkgdesc='Free implementation of the server-side SMTP protocol'
arch=('x86_64')
url='https://www.opensmtpd.org/'
license=('custom')
depends=('libasr' 'libevent' 'openssl-1.0' 'pam')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
backup=('etc/smtpd/smtpd.conf' 'etc/smtpd/aliases')
options=('emptydirs')
source=("https://www.opensmtpd.org/archives/$pkgname-$pkgver.tar.gz"
        'opensmtpd.sysusers'
        'smtpd.service'
        'smtpd.socket')
sha256sums=('291881862888655565e8bbe3cfb743310f5dc0edb6fd28a889a9a547ad767a81'
            'b38b64f1457b7227d55585edc711cce525e089516d16b093e6c44387380e6b13'
            'abf5baeb2a87c60d668ad18ea41cc08cab7a4f76339dd6df05de15cdaadaf922'
            '32d46de5562d01de445d04c93bcc9f94bf103539b676e449c32e3603a3866cf8')

prepare() {
  sed -ri 's,/etc/mail,/etc/smtpd,g' "$pkgname-$pkgver/smtpd/smtpd.conf"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --with-path-mbox=/var/spool/mail \
    --with-path-empty=/var/empty \
    --with-path-socket=/run \
    --with-path-CAfile=/etc/ssl/certs/ca-certificates.crt \
    --with-user-smtpd=smtpd \
    --with-user-queue=smtpq \
    --with-group-queue=smtpq \
    --with-auth-pam \
    --with-libssl='/usr/lib/openssl-1.0' \
    --with-cflags='-I/usr/include/openssl-1.0'

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/sendmail"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/mailq"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/newaliases"
  ln -s /usr/bin/smtpctl "$pkgdir/usr/bin/makemap"

  # install license, systemd unit files, sysusers
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/smtpd.service" "$pkgdir/usr/lib/systemd/system/smtpd.service"
  install -Dm644 "$srcdir/smtpd.socket" "$pkgdir/usr/lib/systemd/system/smtpd.socket"
  install -Dm644 "$srcdir/opensmtpd.sysusers" "$pkgdir/usr/lib/sysusers.d/opensmtpd.conf"

  # install an empty aliases file (used by the default config)
  install -Dm644 /dev/null "$pkgdir/etc/smtpd/aliases"
}
