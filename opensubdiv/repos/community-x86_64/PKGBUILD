# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=opensubdiv
pkgver=3.1.1
pkgrel=3
pkgdesc="An Open-Source subdivision surface library"
arch=(x86_64)
url="http://graphics.pixar.com/opensubdiv"
license=('apache')
depends=(zlib ptex intel-tbb libxcursor xorg-xrandr libxinerama)
makedepends=(cmake doxygen glfw glew python2 python-pygments python-docutils opencl-headers)
#makedepends_x86_64=(cuda) Broken currently :/
source=("https://github.com/PixarAnimationStudios/OpenSubdiv/archive/v${pkgver//./_}.tar.gz")
sha512sums=('700db7e1dde3e916011721a5b42d5e32ea4ae355ab5e3e415541d62efa2059d56d5f30cef853285e95a93f3a3b5fe24a906547a52cce94d47b101ae20021d5b5')

prepare() {
    cd "OpenSubdiv-${pkgver//./_}"

    find . -name \*.py | xargs sed -i "s/env python/env python2/g"
}

build() {
    cd "OpenSubdiv-${pkgver//./_}"

    rm -rf build
    mkdir build && cd build

    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
}

package() {
    cd "OpenSubdiv-${pkgver//./_}"/build

    make DESTDIR="$pkgdir/" install

    rm -rf ${pkgdir}/usr/bin
}
