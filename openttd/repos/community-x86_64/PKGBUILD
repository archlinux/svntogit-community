# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.10.3
pkgrel=1
pkgdesc='An engine for running Transport Tycoon Deluxe.'
arch=('x86_64')
url='https://www.openttd.org'
license=('GPL')
depends=('libpng' 'sdl2' 'icu' 'fontconfig' 'lzo' 'hicolor-icon-theme' 'desktop-file-utils' 'xz' 'fluidsynth')
optdepends=('openttd-opengfx: free graphics' 
            'openttd-opensfx: free soundset')
source=("https://proxy.binaries.openttd.org/openttd-releases/${pkgver}/${pkgname}-${pkgver}-source.tar.xz")
sha512sums=('d1cd535e8e49f0ac147704341287af1b16bde6725fd9152dd36dcb780559b400da8bb31c4e8eb665a0d11e9b1f5bf8a96be0f97f7a85bd967eec534fdd3dd0d4')

build() {
  cd ${pkgname}-${pkgver} 

  # http://site.icu-project.org/download/61#TOC-Migration-Issues
  CXXFLAGS+=' -DU_USING_ICU_NAMESPACE=1'

 ./configure \
    --prefix-dir=/usr \
    --binary-name=${pkgname} \
    --binary-dir=bin \
    --data-dir=share/${pkgname} \
    --install-dir="${pkgdir}" \
    --doc-dir=share/doc/${pkgname} \
    --with-fluidsynth \
    --menu-name="OpenTTD"

  make
}

package() {
  cd ${pkgname}-${pkgver} 

  make install
}
