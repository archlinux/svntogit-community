# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-cli
pkgver=1.3.1
pkgrel=1
pkgdesc='The OpenVAS Command-Line Interface'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries')
makedepends=('cmake' 'xmltoman' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1803/$pkgname-$pkgver.tar.gz"
        "http://wald.intevation.org/frs/download.php/1804/$pkgname-$pkgver.tar.gz.asc")
md5sums=('70ac828c04995fdef997ea9e6ba29daf'
         'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
