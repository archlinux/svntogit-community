# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-libraries
pkgver=7.0.5
pkgrel=1
pkgdesc='The OpenVAS libraries'
arch=('i686' 'x86_64')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls' 'libpcap' 'gpgme' 'libssh' 'glib2')
makedepends=('cmake' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1787/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/1788/$pkgname-${pkgver}.tar.gz.asc")
md5sums=('22ce18293221704e33534a313916ca2f'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDIR=/usr/lib -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  rmdir "$pkgdir/var/run"
}
