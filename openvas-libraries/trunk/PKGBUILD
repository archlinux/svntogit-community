# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-libraries
pkgver=7.0.9
pkgrel=1
pkgdesc='The OpenVAS libraries'
arch=('i686' 'x86_64')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls' 'libpcap' 'gpgme' 'libssh' 'glib2' 'libldap')
makedepends=('cmake' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1971/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/1972/$pkgname-${pkgver}.tar.gz.asc")
md5sums=('dd0c8da845caf5d6f842f61282d71036'
         'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd $pkgname-$pkgver
  LDFLAGS+=" -Wl,--no-as-needed"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDIR=/usr/lib -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  rmdir "$pkgdir/var/run"
}
