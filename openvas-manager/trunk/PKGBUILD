# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-manager
pkgver=5.0.4
pkgrel=1
pkgdesc='A layer between the OpenVAS Scanner and various client applications'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries' 'sqlite3')
makedepends=('cmake' 'doxygen' 'xmltoman')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1730/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/1731/$pkgname-${pkgver}.tar.gz.asc")
md5sums=('ed1a74bd4961aa693b5674217f258753'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
