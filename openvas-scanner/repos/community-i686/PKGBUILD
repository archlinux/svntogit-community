# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-scanner
pkgver=4.0.2
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries')
makedepends=('cmake' 'doxygen')
# This URL needs to be manually updated on every pkgver upgrade.
# http://www.openvas.org/install-source.html
source=("http://wald.intevation.org/frs/download.php/1686/openvas-scanner-${pkgver}.tar.gz"
        'openvas-scanner.service')
md5sums=('99e53eeaaa086abd3515a8e2488ebf1c'
         '5cc11ac11a7454aaa9148eab844b81af')

build() {
  cd openvas-scanner-$pkgver
  cmake -DSBINDIR=/usr/bin -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd openvas-scanner-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../openvas-scanner.service "$pkgdir/usr/lib/systemd/system/openvas-scanner.service"
}
