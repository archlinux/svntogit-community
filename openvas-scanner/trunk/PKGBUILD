# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-scanner
pkgver=4.0.5
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries')
makedepends=('cmake' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/1844/$pkgname-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/1837/$pkgname-${pkgver}.tar.gz.asc"
        "$pkgname.service")
md5sums=('7484c3a67cce4ab32a4b10fcf4716a18'
         'SKIP'
         '5cc11ac11a7454aaa9148eab844b81af')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../$pkgname.service "$pkgdir/usr/lib/systemd/system/${pkgname}.service"
}
