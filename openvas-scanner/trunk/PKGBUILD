# $Id$
# Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-scanner
pkgver=5.1.1
pkgrel=1
pkgdesc='The OpenVAS scanning Daemon'
arch=('x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries' 'redis')
makedepends=('cmake' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2423/openvas-scanner-5.1.1.tar.gz"
        "http://wald.intevation.org/frs/download.php/2424/openvas-scanner-5.1.1.tar.gz.sig"
        "${pkgname}.service")
sha512sums=('8ca23fe9581c98381c030ee8e30a09876d538133980a589eff357321d2ceea0b24dbf8965ef7417e12abb9af240c6a26413b3041b219d28017c7f9ea04779fe1'
            'SKIP'
            '574a9125928e672a9a22a00cab9c635897eedad0eaae550295c21d45cabd52d1d1cd376f441665d37fd1bb9fa48360039f553c300eb15dd9b83cf14df7f37de5')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_BUILD_TYPE=Release -DSBINDIR=/usr/bin \
    -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 ../"${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
