# $Id$
# Maintainer : Aaron Griffin <aaron@archlinux.org>

pkgname=oprofile
pkgver=0.9.9
pkgrel=1
pkgdesc='System-wide profiler for Linux systems'
arch=('i686' 'x86_64')
url="http://oprofile.sourceforge.net"
license=('GPL')
depends=('popt' 'gcc-libs' 'zlib' 'sh')
makedepends=('qt4')
optdepends=('qt4: for oprof_start gui')
install=oprofile.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
sha1sums=('02a1f6609affb04a348dbddfdf8f03e66154f5be')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-kernel-support --with-x --enable-gui=qt4
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/var/lib/oprofile"

# installing development libraries and headers - FS#20089
  install -d  "$pkgdir/usr/include/oprofile"
  install -m644 config.h libop/op_*.h libregex/op_*.h libdb/odb.h \
    libpp/{locate_images.h,image_errors.h} libutil/op_*.h \
    libutil++/{op_*.h,bfd_support.h,utility.h,cached_value.h} "$pkgdir/usr/include/oprofile"
  install -m644 libop/libop.a libdb/libodb.a "$pkgdir/usr/lib/"
  install -m644 libabi/libabi.a "$pkgdir/usr/lib/libopabi.a"
  install -m644 libutil/libutil.a "$pkgdir/usr/lib/liboputil.a"
  install -m644 libutil++/libutil++.a "$pkgdir/usr/lib/liboputil++.a"
}
