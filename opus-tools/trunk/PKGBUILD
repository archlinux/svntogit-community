# $Id$
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Michael Düll <mail@akurei.me>
# Contributor: pumbur

pkgname=opus-tools
pkgver=0.1.7
pkgrel=1
pkgdesc='Collection of tools for Opus audio codec'
arch=('i686' 'x86_64')
url='http://wiki.xiph.org/Opus-tools'
license=('BSD')
depends=('flac' 'libogg' 'opus')
source=(http://downloads.xiph.org/releases/opus/$pkgname-$pkgver.tar.gz)
md5sums=('62af32d1669bcabf1aabaf7187a377f6')

build(){
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 
