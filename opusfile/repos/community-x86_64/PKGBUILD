# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=opusfile
pkgver=0.6
pkgrel=1
pkgdesc='Library for opening, seeking, and decoding .opus files'
arch=('x86_64' 'i686')
url='http://www.opus-codec.org/'
license=('BSD')
depends=('libogg' 'openssl' 'opus')
source=("http://downloads.xiph.org/releases/opus/$pkgname-$pkgver.tar.gz")
sha256sums=('2428717b356e139f18ed2fdb5ad990b5654a238907a0058200b39c46a7d03ea6')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/opusfile/LICENSE"
}

# vim:set ts=2 sw=2 et:
