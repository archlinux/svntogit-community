# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=osbuild
pkgdesc='Build Pipelines for Operating System Artifacts'
pkgver=46
pkgrel=1
url="https://www.osbuild.org"
arch=(any)
license=(Apache)
depends=(bubblewrap curl e2fsprogs qemu python util-linux python-yaml)
makedepends=(make python-docutils python-setuptools)
optdepends=('dnf: build rpm/dnf images'
            'pacman: build pacman images')
source=($pkgname-$pkgver.tar.gz::https://github.com/osbuild/osbuild/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('0132d7b8d81855d1534dacbafdd8d8c818664a4ee2665eef238c44027ef55698')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  make man
  python setup.py install --skip-build --root="$pkgdir"

  install -m 0755 -d "$pkgdir/usr/lib/osbuild"

  for group in assemblers devices inputs runners schemas sources stages ; do
    cp -vdr --no-preserve=ownership "$group" "$pkgdir/usr/lib/osbuild/"
  done
}
