# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=osbuild
pkgdesc='Build Pipelines for Operating System Artifacts'
pkgver=43
pkgrel=1
url="https://www.osbuild.org"
arch=(any)
license=(Apache)
depends=(bubblewrap curl e2fsprogs qemu python util-linux)
makedepends=(make python-docutils python-setuptools)
optdepends=('rpm: build rpm/dnf images')
source=($pkgname-$pkgver.tar.gz::https://github.com/osbuild/osbuild/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('7bf85d8042a41bcb7c53f4c04cf879bc9b55fca8664a4757aeec0c5cc53c729b')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  make man
  python setup.py install --skip-build --root="$pkgdir"

  install -m 0755 -d "$pkgdir/usr/lib/osbuild"

  for group in assemblers devices inputs runners schemas sources stages ; do
    cp -vdr --no-preserve=ownership "$group" "$pkgdir/usr/lib/osbuild/"
  done
}
