# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db-tools
pkgver=1.6.0
pkgrel=1
pkgdesc="Tools for managing the osinfo database"
arch=('x86_64')
url="https://libosinfo.org/"
license=('GPL')
depends=('json-glib' 'libarchive' 'libsoup' 'libxslt')
makedepends=('intltool')
source=("https://releases.pagure.org/libosinfo/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF' # Daniel P. Berrange
              '09B9C8FF223EF113AFA06A39EE926C2BDACC177B') # Fabiano Fidêncio <fidencio@redhat.com>
sha256sums=('d0d5b1196d73a7abed051be48d0e5b4aa196aac4cdbf8ddf52f57c0c492b2574'
            'SKIP')

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
