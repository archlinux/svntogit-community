# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=2.5.0
pkgrel=1
pkgdesc="Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=(x86_64)
url="https://www.ospray.org/"
license=(Apache)
depends=(intel-tbb embree ispc libgl glfw glu openmpi openimagedenoise openvkl rkcommon)
makedepends=(cmake)
source=(https://github.com/ospray/OSPRay/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('074bfd83b5a554daf8da8d9b778b6ef1061e54a1688eac13e0bdccf95593883d')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOSPRAY_BUILD_ISA=ALL \
    -DOSPRAY_MODULE_DENOISER=ON \
    -DOSPRAY_MODULE_MPI=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
