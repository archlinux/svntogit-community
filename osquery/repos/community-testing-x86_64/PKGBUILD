# Maintainer: Anatol Pomozov

pkgname=osquery
pkgver=5.0.1
pkgrel=1
pkgdesc='SQL powered operating system instrumentation, monitoring, and analytics'
arch=(x86_64)
url='https://osquery.io'
license=(Apache GPL2)
depends=(zlib)
makedepends=(cmake ninja clang python gcc-libs git libunwind)
options=(!strip)
source=(git+https://github.com/osquery/osquery.git#tag=$pkgver
        osquery.patch
        libaudit.patch)
sha256sums=('SKIP'
            '5b73f732648752c7adf374a9d088d1486129eb6a41664ecd48634ecf1d6f91c7'
            '96218ef5b7d6d6deb3a7b4b3dfed8068b7e4d10acd5b19372b9882f89d4478a8')

prepare() {
  cd $srcdir/osquery
  patch -p1 < $srcdir/osquery.patch
}

build() {
  cd osquery

  CC=clang CXX=clang++ cmake \
      -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DOSQUERY_VERSION=$pkgver \
      -DOSQUERY_IGNORE_CMAKE_MAX_VERSION_CHECK=TRUE \
      -G Ninja -S . -B build

  # submodules are checked out by cmake, patch it after it were checked out
  (cd $srcdir/osquery/libraries/cmake/source/libaudit/src && git reset --hard && patch -p1 < $srcdir/libaudit.patch)

  ninja -C build
}

package() {
  cd osquery

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 tools/deployment/osquery.example.conf "$pkgdir/etc/osquery/osquery.conf"
  install -Dm644 tools/deployment/linux_packaging/osqueryd.sysconfig "$pkgdir/etc/sysconfig/osqueryd"
  install -Dm644 tools/deployment/linux_packaging/rpm/osqueryd.service "$pkgdir/usr/lib/systemd/system/osqueryd.service"
  rm -r $pkgdir/control
}
