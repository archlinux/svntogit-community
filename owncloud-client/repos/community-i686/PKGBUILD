# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=1.8.1
pkgrel=2
pkgdesc='ownCloud client based on mirall'
arch=(i686 x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain-qt4 qtwebkit neon)
makedepends=(cmake python-sphinx)
optdepends=('python2-nautilus: integration with Nautilus')
install=$pkgname.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/$_name-$pkgver.tar.xz"{,.asc} 'gcc-5.patch')
md5sums=('94c38c8cc15f2e34fae49e06ee3dc66e'
         'SKIP'
         '1f6cc012ba0fb8517097c73cdb59560e')
validpgpkeys=('F05F7DD7953A07DF36579DAA498C45EBE94E7B37')

prepare() {
  mkdir -p build

# Fix build with GCC 5
  cd $_name-$pkgver
  patch -p1 -i "$srcdir"/gcc-5.patch
}

build() {
  cd build

  cmake ../$_name-$pkgver \
	-DBUILD_WITH_QT4=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname \
        
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
