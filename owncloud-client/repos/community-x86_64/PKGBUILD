# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=owncloud-client
pkgver=2.8.2.4246
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain libcloudproviders hicolor-icon-theme)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=(https://download.owncloud.com/desktop/ownCloud/stable/$pkgver/source/ownCloud-$pkgver.tar.xz{,.asc}
        fix-build-qt.patch fix-build-glib2.patch)
sha256sums=('0cc2e5154a1349bd21941ac3c32e8621778a9ff150730a19de2710e22d32fc43'
            'SKIP'
            '1dd13fd494dae3f5639a42df2c55f76897457de82be0e7601d09efabbddd3be0'
            '091b40721869e0bb38fae7d4e0bb0baf2a4aaaa1c6cdceec52e6a44aa6065e98')
validpgpkeys=(F05F7DD7953A07DF36579DAA498C45EBE94E7B37) # ownCloud Client Team (Signing Key) <info@owncloud.com>

prepare() {
  patch -d ownCloud-$pkgver -p1 < fix-build-qt.patch # Fix build with recent Qt
  patch -d ownCloud-$pkgver -p1 < fix-build-glib2.patch # Fix build with glib 2.68
}

build() {
  cmake -B build -S ownCloud-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
