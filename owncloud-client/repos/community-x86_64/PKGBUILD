# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=owncloud-client
pkgver=2.10.1.7389
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain hicolor-icon-theme libcloudproviders)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=(https://download.owncloud.com/desktop/ownCloud/stable/$pkgver/source/ownCloud-$pkgver.tar.xz{,.asc}
        https://github.com/owncloud/client/commit/c1e3bb84.patch)
sha256sums=('5e59c8d2199f194765e070f8ad0e23ab5c11d98d065504acb2b89d7bf82e721d'
            'SKIP'
            '6f3a1bfb3246e77e894f3039a3e2742cce7fd89525f9aa61fae78969e60477a3')
validpgpkeys=(F05F7DD7953A07DF36579DAA498C45EBE94E7B37) # ownCloud Client Team (Signing Key) <info@owncloud.com>

prepare() {
  patch -d ownCloud-$pkgver -p1 < c1e3bb84.patch # Fix build with GCC 12
}

build() {
  cmake -B build -S ownCloud-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
