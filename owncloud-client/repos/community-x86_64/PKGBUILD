# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=1.8.4
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(i686 x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain-qt5 qt5-webkit)
makedepends=(cmake python-sphinx qt5-tools)
optdepends=('python2-nautilus: integration with Nautilus')
install=$pkgname.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/$_name-$pkgver.tar.xz"{,.asc})
md5sums=('ce82c234fca674062599b3ceb4ac50c6'
         'SKIP')
validpgpkeys=('F05F7DD7953A07DF36579DAA498C45EBE94E7B37')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_name-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname \
        
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
