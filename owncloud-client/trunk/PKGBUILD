# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=2.4.1
pkgrel=2
pkgdesc='ownCloud client based on mirall'
arch=(x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain qt5-webkit hicolor-icon-theme)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python2-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=(https://download.owncloud.com/desktop/stable/owncloudclient-$pkgver.tar.xz{,.asc}
        owncloud-client-encoding.patch::"https://github.com/owncloud/client/commit/29557ea5.patch")
sha256sums=('4462ae581c281123dc62f3604f1aa54c8f4a60cd8157b982e2d76faac0f7aa23'
            'SKIP'
            'd29099815692117f7c6fb6fd02e1c2f46e719e190c289bb1396af1b7c2658626')
validpgpkeys=(F05F7DD7953A07DF36579DAA498C45EBE94E7B37) # ownCloud Client Team (Signing Key) <info@owncloud.com>

prepare() {
  mkdir -p build

  cd $_name-$pkgver
  patch -p1 -i ../owncloud-client-encoding.patch # Fix python2 encoding issues
}

build() {
  cd build

  cmake ../$_name-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
