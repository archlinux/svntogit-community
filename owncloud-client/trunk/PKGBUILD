# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=owncloudclient
pkgname=owncloud-client
pkgver=2.3.4
pkgrel=1
pkgdesc='ownCloud client based on mirall'
arch=(i686 x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain qt5-webkit hicolor-icon-theme xdg-utils)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python2-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python2-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/owncloudclient-$pkgver.tar.xz")
sha256sums=('89409e68f8290930088c5ed1da483a1c1b56d45695ac9192e81a2d71690df1ba')
validpgpkeys=(F05F7DD7953A07DF36579DAA498C45EBE94E7B37) # ownCloud Client Team (Signing Key) <info@owncloud.com>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$_name-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
