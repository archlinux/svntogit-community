# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Based on owncloud-git PKGBUILD by Alexander Ovsyannikov

pkgname=owncloud
pkgver=8.1.3
pkgrel=2
pkgdesc="A cloud server to store your files centrally on a hardware controlled by you"
arch=('any')
url="http://owncloud.org/"
license=('GPL')
depends=('php-gd')
optdepends=('php-apache: to use the Apache web server'
	    'php-sqlite: to use the SQLite database backend'
	    'php-pgsql: to use the PostgreSQL database backend'
	    'php-ldap: LDAP authentication'
	    'php-intl'
	    'php-apcu'
	    'php-xcache'
	    'mariadb: to use the MySQL database backend'
	    'smbclient: to mount SAMBA shares'
	    'php-mcrypt'
#	    'php-imagick: file preview'
	    'ffmpeg: file preview'
	    'libreoffice: file preview')
makedepends=()
options=('!strip')
backup=('etc/webapps/owncloud/apache.example.conf')
validpgpkeys=('E3036906AD9F30807351FAC32D5D5E97F6978A26')
_watch="https://owncloud.org/changelog/"
#source=("http://download.owncloud.org/community/$pkgname-${pkgver}.tar.bz2"{,.asc}
source=("$pkgname-$pkgver.tar.gz::https://github.com/owncloud/core/archive/v$pkgver.tar.gz"
	"$pkgname-3rdparty-$pkgver.tar.gz::https://github.com/owncloud/3rdparty/archive/v$pkgver.tar.gz"
	"$pkgname-apps-$pkgver.tar.gz::https://github.com/owncloud/apps/archive/v$pkgver.tar.gz"
	"$pkgname-updater-$pkgver.tar.gz::https://github.com/owncloud/updater/archive/v$pkgver.tar.gz"
	"$pkgname-templateeditor-$pkgver.tar.gz::https://github.com/owncloud/templateeditor/archive/v$pkgver.tar.gz"
	"$pkgname-firstrunwizard-$pkgver.tar.gz::https://github.com/owncloud/firstrunwizard/archive/v$pkgver.tar.gz"
	"$pkgname-files_texteditor-$pkgver.tar.gz::https://github.com/owncloud/files_texteditor/archive/v$pkgver.tar.gz"
	"$pkgname-files_locking-$pkgver.tar.gz::https://github.com/owncloud/files_locking/archive/v$pkgver.tar.gz"
	"$pkgname-files_pdfviewer-$pkgver.tar.gz::https://github.com/owncloud/files_pdfviewer/archive/v$pkgver.tar.gz"
	"$pkgname-activity-$pkgver.tar.gz::https://github.com/owncloud/activity/archive/v$pkgver.tar.gz"
	'apache.example.conf')
md5sums=('b3a4a356640a2f1415a741113cba35b5'
         '1016792e4c6c1d0ced5cd6c46465ea05'
         'b55b5017bde60982d6f32f687fa99690'
         'bbcb0103050d82068eb916cdc6f5f93a'
         '829075bb969385bfdf8cb5cab8701e97'
         'e16ab75d376d1315bbb83283c2cd02e2'
         'f9b0597c207a6fdc765c914d5e972f37'
         'c10cf2aff361523ae6f40d72f39a3f46'
         '83c20940fce448d33b884a9d0353f69f'
         '12046749a7667a755ff29fc025e9dcfa'
         '64c6edf88bcbb37d063e4bf77b39237f')

prepare() {
  cd $srcdir/core-$pkgver
  sed -i "s|'appstoreenabled'.*|'appstoreenabled' => false,|" config/config.sample.php
  rm -rf $srcdir/core-$pkgver/3rdparty
  mv $srcdir/3rdparty-$pkgver $srcdir/core-$pkgver/3rdparty
  mv $srcdir/apps-$pkgver/* $srcdir/core-$pkgver/apps
  for i in files_locking files_pdfviewer files_texteditor firstrunwizard \
           templateeditor updater activity; do
    mv $srcdir/$i-$pkgver $srcdir/core-$pkgver/apps/$i
  done
  find . -type f -name .gitattributes -delete
  find . -type f -name .gitkeep -delete
  find . -type f -name .gitignore -delete
  find . -type f -name .gitmodules -delete
  find . -type f -name .travis.yml -delete
  find . -type d -name .git -exec rm -rf {} \;
  find ./apps -maxdepth 1 -type f -delete
}

package() {
  # install license
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  cp ${srcdir}/core-$pkgver/COPYING-* ${pkgdir}/usr/share/licenses/${pkgname}

  # install project
  install -d ${pkgdir}/usr/share/webapps/
  cp -a ${srcdir}/core-$pkgver ${pkgdir}/usr/share/webapps/${pkgname}
  rm -rf ${pkgdir}/usr/share/webapps/${pkgname}/tests

  # install apache config file
  install -d  ${pkgdir}/etc/webapps/${pkgname}
  install -m 644 ${srcdir}/apache.example.conf  ${pkgdir}/etc/webapps/${pkgname}

  # move config to /etc
  mv ${pkgdir}/usr/share/webapps/owncloud/config ${pkgdir}/etc/webapps/${pkgname}/config
  chown -R http:http ${pkgdir}/etc/webapps/${pkgname}
  ln -s /etc/webapps/${pkgname}/config ${pkgdir}/usr/share/webapps/owncloud/config
  chown -R root:http ${pkgdir}/usr/share/webapps/${pkgname}

  find ${pkgdir}/usr/share/webapps/owncloud -type f -exec chmod 0644 {} \;
  find ${pkgdir}/usr/share/webapps/owncloud -type d -exec chmod 0755 {} \;
#  find ${pkgdir}/etc/webapps/owncloud -type f -print0 | xargs -0 chmod 0640
#  find ${pkgdir}/etc/webapps/owncloud -type d -print0 | xargs -0 chmod 0750

  chmod a+x ${pkgdir}/usr/share/webapps/${pkgname}/occ
}
