# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Based on owncloud-git PKGBUILD by Alexander Ovsyannikov

pkgname=owncloud
pkgver=6.0.4
pkgrel=1
pkgdesc="A cloud server to store your files centrally on a hardware controlled by you"
arch=('any')
url="http://owncloud.org/"
license=('GPL')
depends=('php-gd' 'php-intl')
optdepends=('php-apache: to use the Apache web server'
	    'php-sqlite: to use the SQLite database backend'
	    'php-pgsql: to use the PostgreSQL database backend'
	    'mariadb: to use the MySQL database backend'
	    'smbclient: to mount SAMBA shares'
	    'php-mcrypt'
	    'php-imagick: file preview'
	    'ffmpeg: file preview'
	    'libreoffice-common: file preview')
makedepends=()
options=('!strip')
source=("http://download.owncloud.org/community/$pkgname-${pkgver}.tar.bz2"{,.asc}
	'apache.example.conf'
	'img.patch::https://gist.githubusercontent.com/anonymous/11535580/raw/stdin')
backup=('etc/webapps/owncloud/apache.example.conf')
md5sums=('0a92cf5971e9c9c58e40219b385bd8f4'
         'SKIP'
         '02d29d2dbb6943c63b102c32c3bfcd77'
         '35815f95b015d0bfeebf5bbfb5271cc2')

prepare() {
  cd $srcdir/$pkgname/lib/private && patch -p0 <$srcdir/img.patch
}

package() {
  # install license
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  cp ${srcdir}/${pkgname}/COPYING-* ${pkgdir}/usr/share/licenses/${pkgname}

  # install project
  install -d ${pkgdir}/usr/share/webapps/
  cp -a ${srcdir}/${pkgname} ${pkgdir}/usr/share/webapps/

  # install apache config file
  install -d  ${pkgdir}/etc/webapps/${pkgname}
  install -m 644 ${srcdir}/apache.example.conf  ${pkgdir}/etc/webapps/${pkgname}

  # move config to /etc
  mv ${pkgdir}/usr/share/webapps/owncloud/config ${pkgdir}/etc/webapps/${pkgname}/config
  chown -R http:http ${pkgdir}/etc/webapps/${pkgname}/config
  ln -s /etc/webapps/${pkgname}/config ${pkgdir}/usr/share/webapps/owncloud/config
}
