# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=oxide
pkgver=1.10.3
pkgrel=2
pkgdesc="Web browser engine library for Qt"
arch=('i686' 'x86_64')
url="https://launchpad.net/oxide"
license=('LGPL')
depends=('qt5-location' 'pango' 'nss' 'alsa-lib' 'libxcursor' 'libxcomposite'
         'libxrandr' 'libpulse' 'pciutils' 'libexif' 'bzip2' 'libevent' 'flac'
         'speech-dispatcher' 'snappy' 'harfbuzz')
makedepends=('cmake' 'python2' 'ninja' 'gperf' 'yasm')
source=("http://archive.ubuntu.com/ubuntu/pool/main/o/oxide-qt/oxide-qt_$pkgver.orig.tar.xz")
sha256sums=('43c886c05b932e38d1f7565badef1f843581c52c834ae1ea8bd7783c8a3f9705')

prepare() {
  mkdir build
  mkdir python-bin
  ln -s /usr/bin/python2 python-bin/python
  export PATH="$PWD/python-bin:$PATH"

  find oxide-qt-$pkgver -name '*.py' -exec sed -i -r 's|/usr/bin/python$|&2|g' {} +

  _gyp_args=(
    -Dlinux_link_gsettings=1
    -Dlinux_link_libpci=1
    -Dlinux_link_libspeechd=1
    -Dlinux_link_pulseaudio=1
    -Dlinux_use_bundled_binutils=0
    -Dlinux_use_bundled_gold=0
    -Dlinux_use_gold_flags=0
    -Drelease_extra_cflags=\""$CFLAGS"\"
    -Dlibspeechd_h_prefix=speech-dispatcher/
    -Duse_gnome_keyring=0
    -Duse_system_bzip2=1
    -Duse_system_flac=1
    -Duse_system_ffmpeg=0
    -Duse_system_harfbuzz=1
    -Duse_system_icu=0
    -Duse_system_libevent=1
    -Duse_system_libjpeg=1
    -Duse_system_libpng=1
    -Duse_system_libvpx=0
    -Duse_system_libxml=0
    -Duse_system_snappy=1
    -Duse_system_xdg_utils=1
    -Duse_system_yasm=1
    -Duse_system_zlib=0
    -Dusb_ids_path=/usr/share/hwdata/usb.ids
    -Duse_mojo=0
    -Duse_gconf=0
    -Denable_hangout_services_extension=1
    -Ddisable_fatal_linker_warnings=1
    -Ddisable_glibc=1
  )

  for _arg in "${_gyp_args[@]}"; do
    echo "list(APPEND OXIDE_GYP_EXTRA_ARGS $_arg)" >> oxide-qt-$pkgver/qt/config.cmake
  done
}

build() {
  cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TESTS=1 \
    -DENABLE_PLUGINS=1 \
    -DENABLE_CHROMEDRIVER=1 \
    -DENABLE_PROPRIETARY_CODECS=1 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../oxide-qt-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
