# $Id$
# Maintainer: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>

pkgname=ozerocdoff
pkgver=2
pkgrel=8
pkgdesc="Userspace driver for Option High Speed Mobile Devices"
arch=('i686' 'x86_64')
url="http://www.pharscape.org/ozerocdoff.html"
depends=('bash' 'libusb-compat')
license=('GPL2')
options=('!emptydirs')
source=(udev.tar.gz::"http://www.pharscape.org/forum/index.php?action=dlattach;topic=545.0;attach=4"
	"remove-old-rules.patch")
md5sums=('88a78f9380d9591f8d20a657b72e3e5c'
         '28ed1afd16fdec92de3baf66b32b0b12')

prepare() {
  cd "${srcdir}/udev"
  patch -p1 <$srcdir/remove-old-rules.patch
  sed -i 's|usr/sbin|usr/bin|g' Makefile
}

build() {
  cd "${srcdir}/udev"
  make clean
  make
}

package() {
  cd "${srcdir}/udev"
  make DESTDIR="${pkgdir}" install
  rm -rf $pkgdir/usr/lib/hal
  rm -rf $pkgdir/usr/share/hal
  mkdir -p $pkgdir/usr/lib
  mv $pkgdir/etc/udev $pkgdir/usr/lib
}
