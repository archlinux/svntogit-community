# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=p2pool
pkgver=2.0
_commit=8aebcdc580c424903221c95ebe2304a2e48b01b8
pkgrel=2
pkgdesc='Decentralized pool for Monero mining'
arch=('x86_64')
license=('GPL3')
url="https://github.com/SChernykh/p2pool"
depends=('libuv' 'libsodium' 'zeromq')
makedepends=('cmake' 'git')
source=("${pkgname}"::"git+https://github.com/SChernykh/${pkgname}#commit=${_commit}"
        https://github.com/SChernykh/p2pool/commit/6b4640c413aeaf35bfd5d3e49f4383944f4abfde.patch)
b2sums=('SKIP'
        '7d66b61b2fc14bd782d39c1b9e62d849c49b90f5939d1ba49b7c36a728c9a7bce190c3c632f56d1ea28659f86ae15f189f377997b5301433c929a173df688532')

prepare() {
  cd ${pkgname}
  git submodule init
  git submodule update
  mkdir -p build
  # remove unused gss dependency
  patch -Np1 -i ../6b4640c413aeaf35bfd5d3e49f4383944f4abfde.patch
}

build() {
  cd ${pkgname}/build
  cmake .. -DCMAKE_BUILD_TYPE=Release
  make -j$(nproc)
}

package() {
  cd ${pkgname}
  install -Dm775 "build/p2pool" -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
