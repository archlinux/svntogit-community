# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=packagekit-qt5
pkgver=1.0.1
pkgrel=2
pkgdesc='Qt5 bindings for PackageKit'
arch=('x86_64')
url='http://www.packagekit.org/'
license=('LGPL')
depends=('packagekit' 'qt5-base')
makedepends=('cmake')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17'  # Richard Hughes <richard@hughsie.com>
              'D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB'  # Matthias Klumpp <matthias@tenstral.net>
              '70C26659D184ABC01FFAF45228DDEDC6E4480AD0') # Daniel Nicoletti <dantti12@gmail.com>
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hughsie/PackageKit-Qt/archive/v${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/hughsie/PackageKit-Qt/releases/download/v${pkgver}/PackageKit-Qt-${pkgver}.tar.gz.asc")
sha256sums=('4556ad405b552d0369ed7dfc20b9fed7e1201378eea18964b0068ce669ed9818'
            'SKIP')

build() {
  	mkdir -p PackageKit-Qt-${pkgver}/build
  	cd PackageKit-Qt-${pkgver}/build

  	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
  	cd PackageKit-Qt-${pkgver}/build

	make DESTDIR="${pkgdir}" install
}
