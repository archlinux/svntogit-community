# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=packagekit-qt5
pkgver=0.9.6
pkgrel=1
pkgdesc='Qt5 bindings for PackageKit'
arch=('x86_64')
url='http://www.packagekit.org/'
license=('LGPL')
depends=('packagekit' 'qt5-base')
makedepends=('cmake')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17'	# Richard Hughes <richard@hughsie.com>
	'D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB')		# Matthias Klumpp <matthias@tenstral.net>
source=("https://www.freedesktop.org/software/PackageKit/releases/PackageKit-Qt-${pkgver}.tar.xz"{,.asc})
sha256sums=('0f4f504b2053472f9299c7ca42e33bcc85b8a8754223d2dc1912b533ec1ca680'
            'SKIP')

build() {
  	mkdir -p "${srcdir}/PackageKit-Qt-${pkgver}/build"
  	cd "${srcdir}/PackageKit-Qt-${pkgver}/build"

  	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
  	cd "${srcdir}/PackageKit-Qt-${pkgver}/build"

	make DESTDIR="${pkgdir}" install
}
