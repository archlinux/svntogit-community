# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname='packagekit'
pkgver=1.0.11
pkgrel=2
pkgdesc='A system designed to make installation and updates of packages easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('dbus-glib' 'pacman>=4.2.0' 'polkit' 'shared-mime-info' 'sqlite')
makedepends=('gobject-introspection' 'gtk-doc' 'intltool'
	'networkmanager' 'bash-completion' 'vala')
optdepends=('networkmanager: detect connection status'
	'bash-completion: command completion in bash')
install='packagekit.install'
backup=('var/lib/PackageKit/transactions.db'
	'etc/PackageKit/alpm.d/pacman.conf'
	'etc/PackageKit/alpm.d/repos.list')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes
source=("http://www.freedesktop.org/software/PackageKit/releases/PackageKit-${pkgver}.tar.xz"{,.asc}
	'0001-require-dbus.socket-for-offline-update.patch::https://github.com/hughsie/PackageKit/commit/a54c5ae66c8f8a4227c22419140d66a02f56f25c.patch')
sha256sums=('bbd27bbc77795394f25ab009fe1b3420e365541c99248b18bbaa2fa7f029eaf7'
            'SKIP'
            'c87318eb04033334c6e3e2fcbaaf1116a63668c066e3aff488d0ff9c8ac6fd97')

prepare() {
	cd "${srcdir}/PackageKit-${pkgver}"

	patch -Np1 < "${srcdir}/0001-require-dbus.socket-for-offline-update.patch"
}

build() {
	cd "${srcdir}/PackageKit-${pkgver}"

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-dummy \
		--enable-alpm
	make
}

package() {
	cd "${srcdir}/PackageKit-${pkgver}"

	# install directory with root owner, polkit group and
	# correct permission
	install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"

	make DESTDIR="${pkgdir}" install
}

