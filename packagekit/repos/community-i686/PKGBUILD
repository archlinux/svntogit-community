# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname='packagekit'
pkgver=1.0.5
pkgrel=3
pkgdesc='A system designed to make installation and updates of packages easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('dbus-glib' 'pacman' 'polkit' 'shared-mime-info' 'sqlite')
makedepends=('dbus-glib' 'gobject-introspection' 'gtk-doc' 'intltool'
	'networkmanager' 'pacman>=4.2.0' 'polkit'
	'shared-mime-info' 'sqlite' 'bash-completion')
optdepends=('networkmanager: detect connection status'
	'bash-completion: command completion in bash')
install='packagekit.install'
backup=('var/lib/PackageKit/transactions.db'
	'etc/PackageKit/alpm.d/pacman.conf'
	'etc/PackageKit/alpm.d/repos.list')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes
source=("http://www.freedesktop.org/software/PackageKit/releases/PackageKit-${pkgver}.tar.xz"{,.asc}
	'alpm-track-pacman-round-2.patch')
sha256sums=('e0010fbe266042f07aa3b47377e94139466876dabe86a191587758f23c1ac8d6'
	'SKIP'
	'6f34263db8cbf36456f0e61e85cf2418c947c318a765fff2208063661a15d6d2')

prepare() {
	cd "${srcdir}/PackageKit-${pkgver}"

	patch -Np1 < "${srcdir}/alpm-track-pacman-round-2.patch"
}

build() {
	cd "${srcdir}/PackageKit-${pkgver}"

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-dummy \
		--enable-alpm
	make
}

package() {
	cd "${srcdir}/PackageKit-${pkgver}"

	# install directory with polkit's owner and permission
	install -d -m0700 -o 102 "${pkgdir}"/usr/share/polkit-1/rules.d

	make DESTDIR="${pkgdir}" install
}

