# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname='packagekit'
pkgver=1.0.8
pkgrel=1
pkgdesc='A system designed to make installation and updates of packages easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('dbus-glib' 'pacman>=4.2.0' 'polkit' 'shared-mime-info' 'sqlite')
makedepends=('gobject-introspection' 'gtk-doc' 'intltool'
	'networkmanager' 'bash-completion' 'vala')
optdepends=('networkmanager: detect connection status'
	'bash-completion: command completion in bash')
install='packagekit.install'
backup=('var/lib/PackageKit/transactions.db'
	'etc/PackageKit/alpm.d/pacman.conf'
	'etc/PackageKit/alpm.d/repos.list')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes
source=("http://www.freedesktop.org/software/PackageKit/releases/PackageKit-${pkgver}.tar.xz"{,.asc}
	'https://raw.githubusercontent.com/hughsie/PackageKit/master/lib/packagekit-glib2/PackageKitGlib-1.0.metadata')
sha256sums=('74b013689561322527429addbe1858d450d196bd64226407b8a6f3c627b15ded'
            'SKIP'
            'c906451b6a0462e48cbdf05de7efdbf0bc9ca58d686b8329f5dfb29cacdc260a')

build() {
	cd "${srcdir}/PackageKit-${pkgver}"

	cp "${srcdir}/PackageKitGlib-1.0.metadata" "lib/packagekit-glib2/"

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-dummy \
		--enable-alpm
	make
}

package() {
	cd "${srcdir}/PackageKit-${pkgver}"

	# install directory with polkit's owner and permission
	install -d -m0700 -o 102 "${pkgdir}"/usr/share/polkit-1/rules.d

	make DESTDIR="${pkgdir}" install
}

