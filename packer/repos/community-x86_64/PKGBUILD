# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
pkgname=packer
replaces=('packer-io')
pkgver=1.3.3
pkgrel=2
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL')
makedepends=('git' 'go-pie' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('fc8965fc4c71d12e2d4bf0c2c7cb565834205749390cdbb879acb23143d4b7f4c566b6513afbec61ad73bf3e08a551ce001367ffad105dba68b72f2092d4bb97')

prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p "src/github.com/hashicorp"
  mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
  sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}
build() {
  cd "src/github.com/hashicorp/${pkgname}"
  go build -o packer-binary
}

package() {
  cd "src/github.com/hashicorp/${pkgname}"
  install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
}

# vim:set et sw=2 ts=2 tw=79:
