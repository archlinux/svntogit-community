# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Rudy Matela <rudy.matela@gmail.com>

pkgname=packeth
pkgver=1.7.2
pkgrel=1
pkgdesc="A Linux GUI packet generator tool for ethernet."
depends=(gtk2)
source=("http://downloads.sourceforge.net/packeth/packETH-$pkgver.tar.bz2")
url="http://packeth.sourceforge.net/"
md5sums=('dab613ab413e959cb5c1ef6fd385a041')
arch=('i686' 'x86_64')
license=('GPL')

build() {
  cd "$srcdir"/packETH-$pkgver

  sed -i 's/^LIBS=/LIBS+=/' Makefile.am
  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/packETH-$pkgver
  make install DESTDIR="$pkgdir/"
}
