# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=pacman-bintrans
pkgname=('pacman-bintrans' 'pacman-bintrans-tools')
pkgver=0.1.1
pkgrel=1
pkgdesc='Binary transparency for pacman'
url='https://github.com/kpcyrd/pacman-bintrans'
arch=('x86_64')
license=('GPL3')
depends=('rekor' 'openssl')
checkdepends=('shared-mime-info')
makedepends=('cargo')
source=(https://github.com/kpcyrd/${pkgbase}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgbase}/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz.asc)
sha512sums=('764bf127a983f88098a2b3bd1a4d893c1decb8a16413e32911cc0af1726c222ff9e6854d838cc7160eba5b38c1400b3d366c3fcf5834728f5aafd6d86228846f'
            'SKIP')
b2sums=('c75cfcada6a807b6eb84f8610821e264275604753ad041fe4c11115a33a1087b4de76f9fd9b6ebd36c6753d9552bff175acea0afb822f690f9378e1ff9cb2a42'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")


prepare() {
  cd "${pkgbase}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgbase}-${pkgver}"
  cargo build --frozen --release --workspace
}

check() {
  cd "${pkgbase}-${pkgver}"
  cargo test --frozen --workspace
}

package_pacman-bintrans() {
  cd "${pkgbase}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/pacman-bintrans

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgbase}"
}

package_pacman-bintrans-tools() {
  pkgdesc='Binary transparency for pacman (sign/monitor tools)'
  depends=('shared-mime-info' 'rekor' 'openssl' 'sqlite' 'zstd')

  cd "${pkgbase}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/pacman-bintrans-sign \
    target/release/pacman-bintrans-monitor
}

# vim: ts=2 sw=2 et:
