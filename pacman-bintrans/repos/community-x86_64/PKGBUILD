# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgbase=pacman-bintrans
pkgname=('pacman-bintrans' 'pacman-bintrans-tools')
pkgver=0.3.0
pkgrel=2
pkgdesc='Binary transparency for pacman'
url='https://github.com/kpcyrd/pacman-bintrans'
arch=('x86_64')
license=('GPL3')
depends=('rekor' 'bzip2' 'openssl' 'shared-mime-info' 'zstd')
makedepends=('cargo')
source=(https://github.com/kpcyrd/${pkgbase}/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgbase}/releases/download/v${pkgver}/${pkgbase}-${pkgver}.tar.gz.asc)
sha512sums=('cf78425708097ea06f3788eb84a2e3641a47f5b1ad87fd7aaf6a6b58910a8c3e427ea4ce38f77dee18723764afcdf0b8be681b2543b76d1859906209c76040a0'
            'SKIP')
b2sums=('bfe8d2752b098a2af637f66cb35018d994b54018d46d0fe539e6867afdb77195be6e7bb6ddec077cb552407c96837d28d749b5850d216e59c63b5bbd199c8798'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")


prepare() {
  cd "${pkgbase}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgbase}-${pkgver}"
  cargo build --frozen --release --workspace
}

check() {
  cd "${pkgbase}-${pkgver}"
  cargo test --frozen --workspace
}

package_pacman-bintrans() {
  cd "${pkgbase}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/pacman-bintrans

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgbase}"
}

package_pacman-bintrans-tools() {
  pkgdesc='Binary transparency for pacman (sign/monitor tools)'
  depends=('shared-mime-info' 'rekor' 'openssl' 'sqlite' 'zstd')

  cd "${pkgbase}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/pacman-bintrans-sign \
    target/release/pacman-bintrans-monitor
}

# vim: ts=2 sw=2 et:
