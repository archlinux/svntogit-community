# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pacman-contrib
pkgver=1.2.0
pkgrel=1

pkgdesc="Contributed scripts and tools for pacman systems"
url="https://git.archlinux.org/pacman-contrib.git/about/"
arch=('x86_64')
license=('GPL')

depends=('pacman' 'fakeroot')
optdepends=('mlocate: pacdiff'
            'findutils: pacdiff')
makedepends=('asciidoc')

validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5') # Johannes Löthberg <johannes@kyriasis.com>
source=("https://git.archlinux.org/pacman-contrib.git/snapshot/pacman-contrib-$pkgver.tar".{gz,asc})
sha256sums=('317f53819e35647a19138cb0d68e16206af4a80f52115a7cd622c4a367f914b7'
            'SKIP')

build() {
  cd pacman-contrib-$pkgver

  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var
  make
}

check() {
  make -C pacman-contrib-$pkgver check
}

package() {
  cd pacman-contrib-$pkgver

  make DESTDIR="$pkgdir" install
}
