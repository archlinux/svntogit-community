# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>
 
pkgname=pacmanlogviewer
pkgver=1.3.1
pkgrel=6
pkgdesc="Inspect pacman log files"
arch=('x86_64')
url='https://opendesktop.org/content/show.php?content=150484'
license=('GPL')
depends=('qt5-base' 'hicolor-icon-theme')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz"::"https://dl.opendesktop.org/api/files/downloadfile/id/1466643430/s/4b2ebaddef63ec5c37e97b3f501f8311/t/1512728225/150484-${pkgname}-${pkgver}.tar.gz")
sha256sums=('c75ee4974983f090b0d0dd84cc6358bab4085ec20e3dfcb84513153562f344b4')
 
build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_WITH_QT5=ON .
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
} 
