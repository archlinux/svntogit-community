# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=pacredir
pkgver=0.4.2
pkgrel=2
pkgdesc='redirect pacman requests, assisted by avahi service discovery'
arch=('x86_64')
url='https://github.com/eworm-de/pacredir'
depends=('systemd-libs' 'avahi' 'curl' 'iniparser' 'darkhttpd' 'libmicrohttpd')
makedepends=('systemd' 'markdown')
conflicts=('pacserve')
license=('GPL')
install=pacredir.install
backup=('etc/pacredir.conf' 'etc/pacman.d/pacredir')
validpgpkeys=('BD84DE71F493DF6814B0167254EDC91609BC9183') # Christian Hesse <mail@eworm.de>
source=("https://www.eworm.de/download/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        '0001-fix-build-with-libmicrohttpd-0.9.71.patch')
sha256sums=('e2fb8478cfbca7322bb95f146d0fdb64017306f44af22c29651c05e0103dd0da'
            'SKIP'
            '0d82ebeb5d4520290e0490dc1a71e503d2a8ad36adfe0a8cebf5c5b851676aea')

prepare() {
	cd ${pkgname}-${pkgver}/

	patch -Np1 < ../0001-fix-build-with-libmicrohttpd-0.9.71.patch
}

build() {
	cd ${pkgname}-${pkgver}/

	make
}

package() {
	cd ${pkgname}-${pkgver}/

	make DESTDIR="${pkgdir}" install
}

