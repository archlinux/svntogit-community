# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=padthv1
pkgver=0.9.27
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=(x86_64)
url="https://padthv1.sourceforge.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base qt6-svg)
makedepends=(alsa-lib cmake fftw jack liblo lv2 qt6-tools)
optdepends=(
  'alsa-lib: for standalone application'
  'jack: for standalone application'
  'liblo: for standalone application'
  'new-session-manager: for session management using the standalone application'
  'qt6-wayland: for native wayland support'
)
options=(debug)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('c207d16dfd7e505a92bb1c61a8db787faabf62d7f8c1bd9331ecf18368b631df8f51481409f3dd7027e28f1e85e925ed8f9e94818ede35e8d876f03a3111c0cf')
b2sums=('51292b02052b413aabfb66efb789f0f983179560f729a7662d2317432226887b7663a865c880b1654964f34a2a0bf571a952205f30435da9eb146eeda9ad1071')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfftw3.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
