# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=padthv1
pkgver=0.9.26
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=(x86_64)
url="https://padthv1.sourceforge.io/"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base qt6-svg)
makedepends=(alsa-lib cmake fftw jack liblo lv2 qt6-tools)
optdepends=(
  'alsa-lib: for standalone application'
  'jack: for standalone application'
  'liblo: for standalone application'
  'new-session-manager: for session management using the standalone application'
  'qt6-wayland: for native wayland support'
)
options=(debug)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('981885c2409c8a3fe0302ef1509b06eba91b7a43a003b9c129ffc7250de82ffed992029ecd34c1bd54e67017aaf3e8f0d09e3391929a6d69e3e61ac0cb188d81')
b2sums=('bea163d1bcad90ec54e2f60cb2e0fb24d11a9c5f6c5b9b46a6f3f6c3c42921cbd91b0182572b44de2daad78825584722766fa3f5566c3cd549b4169c7a8c5877')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfftw3.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
