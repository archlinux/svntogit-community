# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jschares <jacob.schares@gmail.com>
# Contributor: celmir <scibecelmir@tuxfamily.org>
# Contributor: Feufochmar <guill.delacourt@gmail.com>
pkgname=paintown
pkgver=3.5.0
pkgrel=2
pkgdesc="An open source fighting game in the same genre as Streets of Rage and Teenage Mutant Ninja Turtles."
arch=('i686' 'x86_64')
url="http://paintown.sourceforge.net/"
license=('GPL')
depends=('python2' 'freetype2' 'allegro' 'sdl')
makedepends=('cmake')
#install=paintown.install
source=(http://downloads.sourceforge.net/sourceforge/paintown/$pkgname-$pkgver.tar.gz
        paintown.png
        paintown.desktop)
md5sums=('58de3c1cd8aea7e3117ec97f5e169a23'
         'ed547f1377212ea079cf9b0a1ad2d587'
         '4144c695ad0bc4621e96ef9760f33b19')

build() {
  cd "$srcdir/$pkgname-$pkgver"
# fix python2 header
  for file in $(find . -name '*.py' -print); do
     sed -i "s#/usr/bin/python#/usr/bin/env python2#" $file
  done
# not needed as cmake is used instead of scons
#  sed -i "s#python#python2#" Makefile
  mkdir -p build
  cd build
  cmake ..
  make
  cp bin/paintown ..
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p $pkgdir/usr/{bin,share/{paintown,pixmaps,applications,licenses/paintown}}
  sh install.sh -d $pkgdir/usr/share/paintown -b $pkgdir/usr/bin
# correct the gnerated file to remove the wrong path (because $pkgdir/usr/bin is written in the file)
  sed -i "s#$pkgdir##g" $pkgdir/usr/bin/paintown
  install -m644 $srcdir/paintown.desktop $pkgdir/usr/share/applications/paintown.desktop
  install -m644 $srcdir/paintown.png $pkgdir/usr/share/pixmaps/paintown.png
  install -m644 LICENSE $pkgdir/usr/share/licenses/paintown/LICENSE
  install -m644 LEGAL $pkgdir/usr/share/licenses/paintown/LEGAL
}

# vim:set ts=2 sw=2 et:
