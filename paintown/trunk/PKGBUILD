# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jschares <jacob.schares@gmail.com>
# Contributor: celmir <scibecelmir@tuxfamily.org>
# Contributor: Feufochmar <guill.delacourt@gmail.com>

pkgname=paintown
pkgver=3.6.0
pkgrel=3
pkgdesc="Open source fighting game in the same genre as Streets of Rage and Teenage Mutant Ninja Turtles"
arch=('x86_64')
url="http://paintown.org"
license=('GPL')
depends=('python2' 'freetype2' 'sdl' 'libpng' 'mpg123' 'libogg' 'libvorbis')
makedepends=('cmake')
source=(https://github.com/kazzmir/paintown/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2)
sha512sums=('abe6c0a451f1be883331ddc688eb8eb061bb4d82714e7da67ac6129435f2fad6c6964852764b9c4c125f9056fb9cc3782aebed494a535f180bb4e4b9a0ab1669')

prepare() {
  cd "$pkgname-$pkgver"

  # fix python2 header
  for file in $(find . -name '*.py' -print); do
     sed -i "s#/usr/bin/python#/usr/bin/env python2#" $file
  done

  sed -i "s/find_package(FREETYPE)/find_package(Freetype)/g" CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"

  mkdir -p build && cd build
  cmake -DFREETYPE_INCLUDE_DIR=/usr/include/freetype2 ..
  make
  cp bin/paintown ..
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p $pkgdir/usr/{bin,share/{paintown,pixmaps,applications,licenses/paintown}}
  sh install.sh -d $pkgdir/usr/share/paintown -b $pkgdir/usr/bin
  # correct the gnerated file to remove the wrong path (because $pkgdir/usr/bin is written in the file)
  sed -i "s#$pkgdir##g" $pkgdir/usr/bin/paintown
  install -m644 misc/paintown.desktop $pkgdir/usr/share/applications/paintown.desktop
  install -m644 misc/logo-256x256.png $pkgdir/usr/share/pixmaps/paintown.png
  install -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 LEGAL $pkgdir/usr/share/licenses/$pkgname/LEGAL
}

# vim:set ts=2 sw=2 et:
