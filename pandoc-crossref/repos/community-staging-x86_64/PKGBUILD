# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=pandoc-crossref
pkgname=(pandoc-crossref haskell-pandoc-crossref)
pkgver=0.2.1.3
pkgrel=10
pkgdesc="Pandoc filter for cross-references"
url="http://hackage.haskell.org/package/${pkgbase}"
license=("GPL2")
arch=('i686' 'x86_64')
makedepends=("ghc=8.0.1" "haskell-data-default" "haskell-data-accessor" "haskell-data-accessor-template"
             "haskell-data-accessor-transformers" "haskell-mtl" "haskell-pandoc" "haskell-pandoc-types"
             "haskell-roman-numerals" "haskell-syb" "haskell-yaml")
source=("http://hackage.haskell.org/packages/archive/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz"
        ghc8.patch)
sha256sums=('d14b78972c48a722b7e53d12fb601e4379d5384f9a58c8ce46ab42b058125471'
            '2a74124f1652d17da63e6cff5939b02633b0045929b175cc4570f7cadbc8cdc1')

prepare() {
    sed -i 's/data-default >= 0.4 && <0.7/data-default >= 0.4 \&\& <0.8/' ${pkgbase}-${pkgver}/${pkgbase}.cabal
    (cd $pkgbase-$pkgver; patch -p1 -i ../ghc8.patch)
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_pandoc-crossref() {
    depends=('pandoc')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share}
}

package_haskell-pandoc-crossref() {
    pkgdesc="Pandoc filter for cross-references (docs and libraries)"
    depends=("${makedepends[@]}" 'pandoc-crossref')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgbase}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/bin
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
