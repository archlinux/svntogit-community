# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=pandoc
pkgname=(pandoc haskell-pandoc)
pkgver=1.16.0.2
pkgrel=26
pkgdesc="Conversion between markup formats"
url="http://pandoc.org"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=("ghc=7.10.3" "haskell-http" "haskell-juicypixels" "haskell-sha" "haskell-aeson"
             "haskell-base64-bytestring" "haskell-blaze-html" "haskell-blaze-markup"
             "haskell-cmark" "haskell-data-default" "haskell-mtl" "haskell-extensible-exceptions"
             "haskell-filemanip" "haskell-haddock-library" "haskell-highlighting-kate"
             "haskell-hslua" "haskell-http-client" "haskell-syb" "haskell-http-client-tls"
             "haskell-http-types"  "haskell-text" "haskell-texmath" "haskell-network"
             "haskell-old-time" "haskell-pandoc-types" "haskell-parsec" "haskell-random"
             "haskell-scientific" "haskell-tagsoup" "haskell-temporary" "haskell-network-uri"
             "haskell-unordered-containers" "haskell-zip-archive" "haskell-vector" "haskell-xml"
             "haskell-yaml" "haskell-zlib")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f5f3262ef4574a111936fea0118557c523a8b0eaa7fea84b64b377b20a80f348')

prepare() {
    sed -e 's/aeson >= 0.*$/aeson >= 0.7.0.5,/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-old-locale -fnetwork-uri -fhttps -f-trypandoc -f-embed_data_files
    runhaskell Setup build

    runhaskell Setup haddock --hoogle --html

    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_pandoc() {
    depends=('gmp' 'libffi' 'zlib')
    optdepends=('pandoc-citeproc: for citation rendering with pandoc-citeproc filter'
                'texlive-core: for pdf output')
    replaces=('pandoc-static')
    conflicts=('pandoc-static')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
}

package_haskell-pandoc() {
    pkgdesc="Conversion between markup formats (docs and libraries)"
    depends=("${makedepends[@]}" 'pandoc')
    install="haskell-pandoc.install"

    cd "${srcdir}/${pkgbase}-${pkgver}"    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/share/{pandoc,man} "$pkgdir"/usr/bin
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
