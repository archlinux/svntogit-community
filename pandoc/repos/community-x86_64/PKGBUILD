# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc
pkgver=1.15.0.6
pkgrel=3
pkgdesc="Conversion between markup formats"
url="http://pandoc.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=("ghc=7.10.2" "haskell-http" "haskell-juicypixels" "haskell-sha" "haskell-aeson"
         "haskell-base64-bytestring" "haskell-blaze-html" "haskell-blaze-markup"
         "haskell-cmark" "haskell-data-default" "haskell-deepseq-generics" "haskell-mtl"
         "haskell-extensible-exceptions" "haskell-filemanip" "haskell-haddock-library"
         "haskell-highlighting-kate" "haskell-hslua" "haskell-http-client" "haskell-syb"
         "haskell-http-client-tls" "haskell-http-types"  "haskell-text" "haskell-texmath"
         "haskell-network" "haskell-old-time" "haskell-pandoc-types" "haskell-parsec"
         "haskell-random" "haskell-scientific" "haskell-tagsoup" "haskell-temporary"
         "haskell-network-uri" "haskell-unordered-containers" "haskell-zip-archive"
         "haskell-vector" "haskell-xml" "haskell-yaml" "haskell-zlib")
optdepends=('haskell-pandoc-citeproc: for citation rendering with pandoc-citeproc filter')
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('d950968142526d5a765af7dfb26b47c3f60d4f883aa9d9eab668614179f1ed46')

prepare() {
    # Allowed by upstream already
    sed -e 's|http-types >= 0.8 && < 0.9|http-types >= 0.8 \&\& < 0.10|' \
        -e 's|syb >= 0.1 && < 0.6|syb >= 0.1 \&\& < 0.7|' \
        -e 's|aeson >= 0.7 && < 0.10|aeson >= 0.7 \&\& < 0.11|' \
        -e 's|aeson >= 0.7.0.5 && < 0.10|aeson >= 0.7.0.5 \&\& < 0.11|' \
        -i $pkgname-$pkgver/$pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-old-locale -fnetwork-uri -fhttps -f-trypandoc -f-embed_data_files
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/man/man1"
    install -d -m755 "${pkgdir}/usr/share/man/man5"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
