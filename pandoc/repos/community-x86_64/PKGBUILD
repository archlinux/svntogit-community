# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc
pkgver=2.17.1.1
pkgrel=45
pkgdesc='Conversion between markup formats'
url='https://pandoc.org'
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-glob' 'haskell-juicypixels' 'haskell-sha' 'haskell-aeson'
         'haskell-aeson-pretty' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-case-insensitive' 'haskell-citeproc'
         'haskell-commonmark' 'haskell-commonmark-extensions' 'haskell-commonmark-pandoc'
         'haskell-connection' 'haskell-data-default' 'haskell-doclayout' 'haskell-doctemplates'
         'haskell-emojis' 'haskell-file-embed' 'haskell-haddock-library' 'haskell-hslua'
         'haskell-hslua-aeson' 'haskell-hslua-marshalling' 'haskell-hslua-module-path'
         'haskell-hslua-module-system' 'haskell-hslua-module-text' 'haskell-hslua-module-version'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types' 'haskell-ipynb'
         'haskell-jira-wiki-markup' 'haskell-lpeg' 'haskell-network' 'haskell-network-uri'
         'haskell-pandoc-lua-marshal' 'haskell-pandoc-types' 'haskell-pretty-show' 'haskell-random'
         'haskell-safe' 'haskell-scientific' 'haskell-skylighting' 'haskell-skylighting-core'
         'haskell-split' 'haskell-syb' 'haskell-tagsoup' 'haskell-temporary' 'haskell-texmath'
         'haskell-text-conversions' 'haskell-unicode-collation' 'haskell-unicode-transforms'
         'haskell-xml' 'haskell-xml-conduit' 'haskell-yaml' 'haskell-zip-archive' 'haskell-zlib')
optdepends=('pandoc-crossref: for numbering figures, equations, tables and cross-references to them with pandoc-crossref filter'
            'texlive-core: for pdf output')
conflicts=('haskell-pandoc')
replaces=('haskell-pandoc')
makedepends=('ghc' 'uusi' 'haskell-diff' 'haskell-tasty' 'haskell-tasty-golden'
             'haskell-tasty-hunit' 'haskell-tasty-lua' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1402240d094d62e071349d87d0a2ffe3e82269a80c82a39cefc7c2938ac86594011459068c1dd526127d72dd8c20816896d5c38761e9691370fa11190ec51b06')

prepare() {
    cd $pkgname-$pkgver
    # TODO: find a better solution
    sed -i "s|let env' = dynlibEnv ++ |let env' = dynlibEnv ++ [(\"LD_LIBRARY_PATH\", \"$PWD/dist/build\")] ++ |" test/Tests/Command.hs
    uusi -u texmath $pkgname.cabal
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-trypandoc -f-embed_data_files -f-static
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test || echo "Tests failed"
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm "${pkgdir}/usr/share/doc/${pkgname}/COPYING.md"
    install -Dm644 man/pandoc.1 -t "${pkgdir}"/usr/share/man/man1/
}
