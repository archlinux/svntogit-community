# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc
pkgver=1.19.2.1
pkgrel=114
pkgdesc="Conversion between markup formats"
url="http://pandoc.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-http" "haskell-juicypixels" "haskell-sha" "haskell-aeson"
         "haskell-base64-bytestring" "haskell-blaze-html" "haskell-blaze-markup" "haskell-cmark"
         "haskell-data-default" "haskell-doctemplates" "haskell-mtl" "haskell-extensible-exceptions"
         "haskell-filemanip" "haskell-haddock-library" "haskell-skylighting" "haskell-hslua"
         "haskell-http-client" "haskell-syb" "haskell-http-client-tls" "haskell-http-types"
         "haskell-text" "haskell-texmath" "haskell-network" "haskell-old-time"
         "haskell-pandoc-types" "haskell-parsec" "haskell-random" "haskell-scientific"
         "haskell-tagsoup" "haskell-temporary" "haskell-network-uri" "haskell-unordered-containers"
         "haskell-zip-archive" "haskell-vector" "haskell-xml" "haskell-yaml" "haskell-zlib")
optdepends=('pandoc-citeproc: for citation rendering with pandoc-citeproc filter'
            'texlive-core: for pdf output')
conflicts=('haskell-pandoc')
replaces=('haskell-pandoc')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        ghc-8.2.1.patch)
sha256sums=('08692f3d77bf95bb9ba3407f7af26de7c23134e7efcdafad0bdaf9050e2c7801'
            '2d28962550964540ad543833b38fee8ae8d247d2b8ec08fcef9b9879bf967846')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 -i ../ghc-8.2.1.patch
    sed -e 's/syb >= 0.1 && < 0.7,/syb >= 0.1,/' \
        -e 's/aeson >= 0.7.* && < 1.2,/aeson >= 0.7,/' \
        -e 's/process >= 1 && < 1.5,/process >= 1,/' \
        -e 's/time >= 1.5 && < 1.7/time >= 1.5/' \
        -e '/Build-Depends: pandoc,/a \                 texmath,' \
        -i $pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-old-locale -fnetwork-uri -fhttps -f-trypandoc -f-embed_data_files -f-weigh-pandoc
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.md"
}
