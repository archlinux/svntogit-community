# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc
pkgver=2.9.2.1
pkgrel=24
pkgdesc='Conversion between markup formats'
url='https://pandoc.org'
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-http' 'haskell-juicypixels' 'haskell-sha' 'haskell-aeson'
         'haskell-aeson-pretty' 'haskell-attoparsec'
         'haskell-base64-bytestring' 'haskell-blaze-html' 'haskell-blaze-markup'
         'haskell-case-insensitive' 'haskell-cmark-gfm' 'haskell-data-default' 'haskell-doclayout'
         'haskell-doctemplates' 'haskell-emojis' 'haskell-glob'
         'haskell-haddock-library' 'haskell-ipynb' 'haskell-jira-wiki-markup' 'haskell-skylighting'
         'haskell-skylighting-core' 'haskell-hslua' 'haskell-hslua-module-system'
         'haskell-hslua-module-text' 'haskell-http-client' 'haskell-syb' 'haskell-hsyaml'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-safe' 'haskell-split'
         'haskell-texmath' 'haskell-network' 'haskell-pandoc-types' 'haskell-random'
         'haskell-scientific' 'haskell-tagsoup' 'haskell-temporary' 'haskell-text-conversions'
         'haskell-network-uri' 'haskell-unicode-transforms' 'haskell-unordered-containers'
         'haskell-zip-archive' 'haskell-vector' 'haskell-xml' 'haskell-zlib')
optdepends=('pandoc-citeproc: for citation rendering with pandoc-citeproc filter'
            'pandoc-crossref: for numbering figures, equations, tables and cross-references to them with pandoc-crossref filter'
            'texlive-core: for pdf output')
conflicts=('haskell-pandoc')
replaces=('haskell-pandoc')
makedepends=('ghc' 'haskell-diff' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-lua'
             'haskell-tasty-quickcheck' 'haskell-tasty-golden' 'haskell-quickcheck'
             'haskell-executable-path')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        pandoc-revert-noprelude.patch::https://github.com/jgm/pandoc/commit/a9ef15bbd574bf09827c2c25ef1cd3efaf288f7c.patch)
sha512sums=('35ac491a7e0a2f6d8fe0f2c2ffe265748b13920904e974f88c92b3f82edb2f1c03e28ebef1bc344ead13d566117a5e50707a68edc49f95159157f974210b23b2'
            'ca91b56003c02cd10cc9a073f63eac6e3d4371ebec80bb85500ea67e8aa0ac8938fb5487651ad72e6f964b9d5266c1578e649323e3390037fb87b443850eb4e1')

prepare() {
    cd "${srcdir}/$pkgname-${pkgver}"
    patch -p1 -i ../pandoc-revert-noprelude.patch
    sed -i 's/< 1.1/< 1.2/;s/< *2.14/<3/' $pkgname.cabal

    # TODO: find a better solution
    sed -i "s|let env' = dynlibEnv ++ |let env' = dynlibEnv ++ [(\"LD_LIBRARY_PATH\", \"$PWD/dist/build\")] ++ |" test/Tests/Command.hs
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-trypandoc -f-embed_data_files -f-static
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test || warning "Tests failed"
}

package() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm "${pkgdir}/usr/share/doc/${pkgname}/COPYING.md"
    install -Dm644 man/pandoc.1 "${pkgdir}"/usr/share/man/man1/pandoc.1
}
