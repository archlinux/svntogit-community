# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Owain Sutton <owainsutton @ gmail dot com>

pkgname=panflute
pkgver=0.6.2
pkgrel=2
pkgdesc="Music player interface and panel applet - successor to music-applet"
arch=('i686' 'x86_64')
url="https://launchpad.net/panflute"
license=('GPL')
depends=('dbus-python' 'gnome-python' 'python-notify' 'pygtk' 'python-numpy' 'gnome-python-desktop')
makedepends=('perlxml' 'gnome-doc-utils>=0.18' 'intltool' 'gnome-panel'
	    'gnome-python-desktop' 'python-mpd')
optdepends=('python-mpd: for mpd support')
replaces=('music-applet')
install=panflute.install
source=(http://launchpad.net/${pkgname}/0.6/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b38869e27bb22f9c3ea3d41f7c9cb3626fec49eac176410a5aa27bea9745617f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/panflute \
	--disable-schemas-install

  make
  make DESTDIR="${pkgdir}" install

  sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find ${pkgdir}/usr/lib/python2.7/site-packages/panflute/ -name '*.py') \
    $pkgdir/usr/lib/panflute/panflute-applet \
    $pkgdir/usr/bin/panflute-{daemon,launch-player}

  install -m755 -d "${pkgdir}"/usr/share/gconf/schemas
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/${pkgname}.schemas --domain ${pkgname} \
 	"${pkgdir}"/etc/gconf/schemas/*.schemas

  rm -rf "${pkgdir}"/etc/gconf
}
