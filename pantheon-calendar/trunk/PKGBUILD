# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calendar
pkgver=6.1.1
pkgrel=1
pkgdesc='The Pantheon Calendar'
arch=(x86_64)
url=https://github.com/elementary/calendar
license=(GPL3)
groups=(pantheon)
depends=(
  clutter
  evolution-data-server
  folks
  gdk-pixbuf2
  geoclue
  geocode-glib
  glib2
  gtk3
  libchamplain
  libfolks.so
  libgee
  libgranite.so
  libhandy-1.so
  libical
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=fcef19b3e62119308b059f7ab08c0b5b32ba9547
source=(pantheon-calendar::git+https://github.com/elementary/calendar.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd pantheon-calendar

  git describe --tags
}

build() {
  arch-meson pantheon-calendar build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim: ts=2 sw=2 et:
