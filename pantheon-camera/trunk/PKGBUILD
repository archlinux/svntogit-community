# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-camera
pkgver=1.0.6
pkgrel=4
pkgdesc='The Pantheon Camera Application'
arch=(x86_64)
url=https://github.com/elementary/camera
license=(GPL3)
groups=(pantheon)
depends=(
  clutter-gst
  clutter-gtk
  glibc
  gstreamer
  gst-plugins-good
  libcanberra.so
  libclutter-1.0.so
  libgdk-3.so
  libgio-2.0.so
  libglib-2.0.so
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
_tag=6f5c984751479290bb22321a3e1773746d8f520e
source=(pantheon-camera::git+https://github.com/elementary/camera.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-camera

  git describe --tags
}

prepare() {
  cd pantheon-camera

  git cherry-pick -n 9012be222d3c7cda9d28b0992ea5adc7967651b4
  git cherry-pick -n 5cad0ff713f37b7127a01ab638d4cc383c0b3899
}

build() {
  arch-meson pantheon-camera build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
