# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-camera
pkgver=6.0.3
pkgrel=2
pkgdesc='The Pantheon Camera Application'
arch=(x86_64)
url=https://github.com/elementary/camera
license=(GPL3)
groups=(pantheon)
depends=(
  clutter-gst
  clutter-gtk
  glibc
  gstreamer
  gst-plugins-good
  libcanberra.so
  libclutter-1.0.so
  libgdk-3.so
  libgio-2.0.so
  libglib-2.0.so
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
_tag=7878707f9a91a6e4b6b2abd3b0498aa22971a3f2
source=(pantheon-camera::git+https://github.com/elementary/camera.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd pantheon-camera
  git cherry-pick -n ead143b7e3246c5fa9bb37c95d491fb07cea9e04
}

pkgver() {
  cd pantheon-camera
  git describe --tags
}

build() {
  arch-meson pantheon-camera build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
