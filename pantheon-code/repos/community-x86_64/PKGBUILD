# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=3.0
pkgrel=1
pkgdesc='The Pantheon Text Editor'
arch=('x86_64')
url='https://github.com/elementary/code'
license=('GPL3')
depends=(
  'editorconfig-core-c' 'glib2' 'gtk3' 'gtksourceview3' 'libgee' 'libgit2-glib'
  'libpeas' 'pango' 'zeitgeist'
  'libgranite.so'
)
makedepends=(
  'git' 'gobject-introspection' 'gtkspell3' 'intltool' 'libsoup' 'meson' 'vala'
  'vte3' 'webkit2gtk'
)
optdepends=('gtkspell3: Spell Check plugin'
            'libsoup: Pastebin plugin'
            'vala: Outline plugin'
            'vte3: Terminal plugin'
            'webkit2gtk: Browser Preview plugin')
source=("pantheon-code::git+https://github.com/elementary/code.git#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  arch-meson pantheon-code build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
