# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=3.1.1
pkgrel=2
pkgdesc='The Pantheon Text Editor'
arch=(x86_64)
url=https://github.com/elementary/code
license=(GPL3)
depends=(
  editorconfig-core-c
  glib2
  gtk3
  gtksourceview3
  libgee
  libgit2-glib
  libgranite.so
  libpeas
  pango
)
makedepends=(
  git
  gobject-introspection
  gtkspell3
  intltool
  libsoup
  meson
  vala
  vte3
  webkit2gtk
)
optdepends=(
  'gtkspell3: Spell Check plugin'
  'libsoup: Pastebin plugin'
  'vala: Outline plugin'
  'vte3: Terminal plugin'
  'webkit2gtk: Browser Preview plugin'
)
source=(pantheon-code::git+https://github.com/elementary/code.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  cd $pkgname
}

build() {
  arch-meson pantheon-code build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
