# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=6.0.2
pkgrel=1
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url=https://github.com/elementary/files
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libcloudproviders
  libgee
  libgit2-glib
  libgranite.so
  libhandy-1.so
  libnotify
  pango
  plank
  sqlite
  zeitgeist
)
makedepends=(
  flatpak-builder
  git
  gnome-common
  meson
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'gvfs: Cleaner devices section'
  'tumbler: Thumbnails generation'
)
_tag=35337c6f994428df6964002822b16c10f476d082
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-files

  git describe --tags
}

build() {
  arch-meson pantheon-files build \
    -D with-unity=disabled
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
