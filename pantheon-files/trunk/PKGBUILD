# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=0.2.1
pkgrel=1
pkgdesc='The Pantheon File Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-files'
license=('GPL3')
depends=('desktop-file-utils' 'gconf' 'granite' 'libnotify' 'zeitgeist')
makedepends=('cmake' 'gnome-common' 'sqlite' 'vala')
optdepends=('contractor: Various context menu entries'
            'tumbler: Thumbnails generation')
install='pantheon-files.install'
source=("https://launchpad.net/pantheon-files/${pkgver%.*}.x/${pkgver}/+download/pantheon-files-${pkgver}.tgz")
sha256sums=('4a562d9f3bcd2a19d485060ec5baf9ac471efffe36c1c07de9035df5a4f8a699')

build() {
  cd pantheon-files-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_SKIP_RPATH='TRUE' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-files-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
