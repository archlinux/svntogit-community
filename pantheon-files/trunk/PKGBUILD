# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=0.3.2
pkgrel=1
pkgdesc='The Pantheon File Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-files'
license=('GPL3')
depends=('atk' 'cairo' 'dbus-glib' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3'
         'libcanberra' 'libgee' 'pango' 'sqlite' 'zeitgeist'
         'libgranite.so')
makedepends=('cmake' 'gnome-common' 'vala')
optdepends=('contractor: Various context menu entries'
            'tumbler: Thumbnails generation')
source=("https://launchpad.net/pantheon-files/0.3.x/${pkgver}/+download/pantheon-files-${pkgver}.tar.xz")
sha256sums=('d2553be0819ba8240aa0888bfc984054e4f8a8e4dbcbf07a935f6eb06b1b4263')

prepare() {
  cd pantheon-files-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-files-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DCMAKE_SKIP_RPATH='TRUE' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-files-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
