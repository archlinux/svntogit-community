# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>

pkgname=pantheon-music
pkgver=0.4.2
pkgrel=1
pkgdesc='The Pantheon Music Player'
arch=('x86_64')
url='https://github.com/elementary/music'
license=('GPL3')
groups=('pantheon')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'gst-plugins-base-libs'
         'gstreamer' 'gtk3' 'json-glib' 'libaccounts-glib' 'libgda' 'libgee'
         'libgpod' 'libgsignon-glib' 'libpeas' 'libsoup' 'pango' 'taglib'
         'zeitgeist'
         'libgranite.so')
makedepends=('cmake' 'gobject-introspection' 'intltool' 'vala')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries')
replaces=('noise-player')
provides=('noise-player')
conflicts=('noise-player')
source=("pantheon-music-${pkgver}.tar.gz::https://github.com/elementary/music/archive/${pkgver}.tar.gz"
        'mesa-demos-conflict.patch'
        'pantheon-music-install-dirs.patch')
sha256sums=('9e707e52da12eeb89b42630c697ac0adfefd20a0a12eac37ff3c65b106e0c9d3'
            '205f26c7dd613036739517620da37fea0f2a9f9d74ea546d5bce2c0a859f2792'
            'd0ebc710a742d942e04a8df61d91dd75ae4b2aeafe7fece08f1fbc90c507f1df')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd music-${pkgver}

  patch -Np1 -i ../mesa-demos-conflict.patch
  patch -Np1 -i ../pantheon-music-install-dirs.patch
}

build() {
  cd build

  cmake ../music-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF'
  make -j1
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
