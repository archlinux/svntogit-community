# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-notifications
pkgver=6.0.0
pkgrel=1
pkgdesc='Gtk Notifications Server'
arch=(x86_64)
url=https://github.com/elementary/notifications
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcanberra.so
  libcanberra-gtk3.so
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=57f6303205d4b592898af97f1feeafdb0265f103
source=(pantheon-notifications::git+https://github.com/elementary/notifications.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-notifications
  git describe --tags
}

build() {
  arch-meson pantheon-notifications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
