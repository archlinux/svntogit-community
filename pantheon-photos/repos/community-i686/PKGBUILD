# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=0.1.1
pkgrel=2
pkgdesc='The Pantheon Photos Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-photos'
license=('GPL3')
depends=('cairo' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'gtk3' 'hicolor-icon-theme'
         'libexif' 'libgee' 'libgexiv2' 'libgphoto2' 'libraw' 'pango' 'sqlite'
         'libgranite.so' 'libgudev-1.0.so')
makedepends=('vala' 'webkitgtk')
optdepends=('webkitgtk: Publishing plugins')
conflicts=('shotwell')
source=("https://launchpad.net/pantheon-photos/${pkgver%.*.*}.x/${pkgver}/+download/pantheon-photos-${pkgver}.tar.xz")
sha256sums=('f6952f9a8f30db966b961b5e08cf2b017076861503a8a49d12e939052ff71d10')

build() {
  cd pantheon-photos-${pkgver}

  ./configure \
    --prefix='/usr' \
    --libexec='/usr/lib/shotwell' \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd pantheon-photos-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
