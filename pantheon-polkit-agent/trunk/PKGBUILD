# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-polkit-agent
pkgver=1.0.4
pkgrel=1
pkgdesc='Pantheon Polkit Agent'
arch=(x86_64)
url=https://github.com/elementary/pantheon-agent-polkit
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgranite.so
  polkit
)
makedepends=(
  git
  meson
  intltool
  vala
)
_tag=180e936524614293f852e8ea0760aadc0c10bb6f
source=(pantheon-polkit-agent::git+https://github.com/elementary/pantheon-agent-polkit.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-polkit-agent

  git describe --tags
}

build() {
  arch-meson pantheon-polkit-agent build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
