# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=6.0.2
pkgrel=2
pkgdesc='The Pantheon Screenshot Tool'
arch=(x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=03358ba8638b83cea0384685a602d8da8fffd845
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd pantheon-screenshot
  git cherry-pick -n 80a5d942e813dd098e1ef0f6629b81d2ccef05ae
}

pkgver() {
  cd pantheon-screenshot
  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
