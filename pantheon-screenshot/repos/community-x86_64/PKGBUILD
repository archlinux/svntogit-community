# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=6.0.0
pkgrel=1
pkgdesc='The Pantheon Screenshot Tool'
arch=(x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=d1e4f33992fff3d4987ba9c4e36d3db5c150aa98
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-screenshot

  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
