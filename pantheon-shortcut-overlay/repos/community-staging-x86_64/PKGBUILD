# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-shortcut-overlay
pkgver=2.0.0
pkgrel=1
pkgdesc='A native, OS-wide shortcut overlay'
arch=(x86_64)
url=https://github.com/elementary/shortcut-overlay
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite-7.so
  libhandy-1.so
)
makedepends=(
  git
  meson
  vala
)
_tag=e5f206eea1b347a22ad1c74090fa9873693097d8
source=(pantheon-shortcut-overlay::git+https://github.com/elementary/shortcut-overlay.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd pantheon-shortcut-overlay
  git cherry-pick -n 130f78eb4b7770586ea98ba0a5fdbbf5bb116f3f
}

pkgver() {
  cd pantheon-shortcut-overlay
  git describe --tags
}

build() {
  arch-meson pantheon-shortcut-overlay build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
