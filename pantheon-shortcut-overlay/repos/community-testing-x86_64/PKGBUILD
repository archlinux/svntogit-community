# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-shortcut-overlay
pkgver=1.2.1
pkgrel=2
pkgdesc='A native, OS-wide shortcut overlay'
arch=(x86_64)
url=https://github.com/elementary/shortcut-overlay
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  meson
  vala
)
_tag=e9b604293eb8451d16eb6afa697c6867432a94fe
source=(pantheon-shortcut-overlay::git+https://github.com/elementary/shortcut-overlay.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd pantheon-shortcut-overlay
  git cherry-pick -n 130f78eb4b7770586ea98ba0a5fdbbf5bb116f3f
}

pkgver() {
  cd pantheon-shortcut-overlay
  git describe --tags
}

build() {
  arch-meson pantheon-shortcut-overlay build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
