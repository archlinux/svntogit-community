# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=6.0.1
pkgrel=2
pkgdesc='The Pantheon Terminal Emulator'
arch=(x86_64)
url=https://github.com/elementary/terminal
license=(GPL3)
depends=(
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  pango
  vte3
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
groups=(pantheon)
_tag=7b6dc14405fc2c8671c0afa140bab4a8f906fb07
source=(pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd pantheon-terminal
  git cherry-pick -n 15e3ace08cb25e53941249fa1ee680a1e2f871b4
}

pkgver() {
  cd pantheon-terminal
  git describe --tags
}

build() {
  arch-meson pantheon-terminal build \
    -Dubuntu-bionic-patched-vte=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
