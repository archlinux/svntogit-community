# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.4.0.4
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-terminal'
license=('GPL3')
depends=('desktop-file-utils' 'glib2' 'glibc' 'gtk3' 'pango' 'vte3'
         'libgranite.so')
makedepends=('cmake' 'intltool' 'vala')
groups=('pantheon')
source=("https://launchpad.net/pantheon-terminal/0.4.x/${pkgver}/+download/pantheon-terminal-${pkgver}.tar.xz")
sha256sums=('91909f24de3d6bd6d6bee3e89c18e900cbaf04d88f85c36dfe6baaa9c69129fb')

prepare() {
  cd pantheon-terminal-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-terminal-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-terminal-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
