# $Id$
# Maintainer: alucryd <alucryd at gmail dot com>

pkgname=pantheon-terminal
pkgver=0.3.0
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url="https://launchpad.net/${pkgname}"
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'libnotify' 'vte3')
makedepends=('cmake' 'vala')
install="${pkgname}.install"
source=("${url}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('b8bf8331ac5ebe0412b2023f19dfb3db78b0210002368466b3ba33cb01fece94')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
