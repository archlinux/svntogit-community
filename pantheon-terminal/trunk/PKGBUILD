# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.3.1.3
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-terminal'
license=('GPL3')
depends=('desktop-file-utils' 'glib2' 'glibc' 'gtk3' 'libnotify' 'pango'
         'vte290'
         'libgranite.so')
makedepends=('cmake' 'vala')
install='pantheon-terminal.install'
source=("https://launchpad.net/pantheon-terminal/0.3.x/${pkgver}/+download/pantheon-terminal-${pkgver}.tgz")
sha256sums=('5a13e8bdbdc0823b315fcbeaa29ac2f6fdfa688267ce122ac4291af162c7d92d')

prepare() {
  cd pantheon-terminal-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-terminal-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-terminal-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
