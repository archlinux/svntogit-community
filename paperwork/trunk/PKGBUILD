# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=paperwork
pkgver=2.0
pkgrel=2
pkgdesc="Personal document manager for GNOME to manage scanned documents and PDFs"
arch=(any)
url="https://openpaper.work/"
license=(GPL3)
depends=(gtk3 libinsane libnotify poppler-glib python-cairo python-dateutil python-distro
         python-gobject python-levenshtein python-pillowfight python-pycountry python-pydbus
         python-pyocr python-setuptools python-simplebayes python-whoosh python-pyxdg)
makedepends=(git)
_commit=24ba2639601d8fb51836d822c61e46836c8c1069  # tags/2.0^0
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/paperwork.git#commit=$_commit"
        "$pkgname-$pkgver-data.tar.gz::https://download.openpaper.work/data/paperwork/master_${_commit:0:8}/data.tar.gz")
sha256sums=('SKIP'
            '10e89ab9877c57931c1a5cfebe7f4ef452374abd81e509c6b0d9d0e5dc12c431')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname
  tar -xvzf ../$pkgname-$pkgver-data.tar.gz
  make version version=$pkgver
  make l10n_compile
}

package() {
  cd $pkgname
  for dir in openpaperwork-{core,gtk} paperwork-{backend,gtk}; do
    pushd $dir
    python3 setup.py install --root="$pkgdir" --optimize=1
    popd
  done

  PYTHONPATH=`echo "$pkgdir"/usr/lib/python*/site-packages/` \
    "$pkgdir"/usr/bin/paperwork-gtk install \
      --icon_base_dir="$pkgdir/usr/share/icons" \
      --data_base_dir="$pkgdir/usr/share"
}
