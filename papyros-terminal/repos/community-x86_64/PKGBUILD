# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Spencer <sonrisesoftware@gmail.com>

pkgname=papyros-terminal
pkgver=0.1.0
pkgrel=1
pkgdesc="The terminal app for Papyros"
arch=("i686" "x86_64")
url="https://github.com/papyros/terminal-app"
license=("GPL")
depends=("qt5-base" "qt5-declarative" "qml-material" "qmltermwidget"
         "libpapyros")
makedepends=("git" "extra-cmake-modules")
source=("git+https://github.com/papyros/terminal-app.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
  sed -i 's/Roboto Mono for Powerline/Monospace/' terminal-app/src/qml/Settings.qml
}

build() {
  cd build
  cmake "$srcdir/terminal-app" -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
