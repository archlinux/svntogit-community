# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

_pkg=parallel-netcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=1.8.1
pkgrel=3
pkgdesc="A Parallel I/O Library for NetCDF File Access"
arch=('x86_64' 'i686')
url="https://trac.mcs.anl.gov/projects/parallel-netcdf"
license=('custom')
depends=('openmpi')
makedepends=('gcc-fortran')
checkdepends=('inetutils')
provides=('parallel-netcdf')
conflicts=('parallel-netcdf')
replaces=('parallel-netcdf')
options=('!makeflags')
source=("http://cucis.ece.northwestern.edu/projects/PnetCDF/Release/${_pkg}-${pkgver}.tar.gz")
sha1sums=('2a8179158731f3fa24c9b183b4b1442e8853d229')

build() {
    cd ${_pkg}-${pkgver}

    export CPPFLAGS="${CPPFLAGS} -fPIC"
    ./configure --prefix=/usr
    make
}

check() {
    cd ${_pkg}-${pkgver}
    # Tests failing with i686: https://github.com/open-mpi/ompi/issues/3195
    if [ ${CARCH} = 'x86_64' ]; then
        make check
        make ptest
    fi
    # Tests crashing (but no failure) with x86_64: https://github.com/open-mpi/ompi/issues/3695
}

package() {
    cd ${_pkg}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}
}
