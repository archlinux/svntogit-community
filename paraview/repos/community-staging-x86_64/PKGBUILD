# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Oliver Goethel <deezy>
# Contributor: eolianoe eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: George Eleftheriou <eleftg>
# Contributor: Mathias Anselmann <mathias.anselmann@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Michele Mocciola <mickele>
# Contributor: Simon Zilliken <simon____AT____zilliken____DOT____name>
# Contributor: chuckdaniels

_pkg=paraview
_mpi=openmpi
pkgname=${_pkg}
#-${_mpi}
pkgver=5.6.2
pkgrel=3
pkgdesc="Parallel Visualization application using VTK (${_mpi} version)"
arch=(x86_64)
url="https://www.paraview.org"
license=(BSD custom)
depends=(boost-libs qt5-tools qt5-x11extras intel-tbb openmpi
         ffmpeg ospray python-matplotlib python-numpy
         cgns protobuf python-pygments
         double-conversion expat freetype2 gdal glew hdf5 
         libjpeg jsoncpp libjsoncpp.so libxml2 lz4 xz python-mpi4py netcdf
         libogg libpng pdal proj pugixml libtheora libtiff zlib)
#        gl2ps
#        netcdf-cxx libharu
#        sqlite apparently not used in this VTK configuration
makedepends=(cmake boost mesa gcc-fortran ninja qt5-tools qt5-xmlpatterns eigen pegtl)
source=("${url}/files/v${pkgver:0:3}/ParaView-v${pkgver}.tar.xz"
        proj6.patch)
sha256sums=('1f3710b77c58a46891808dbe23dc59a1259d9c6b7bb123aaaeaa6ddf2be882ea'
            '2271cf8b57a4251d04a5e6f48b05939f60131b5b9a7947c21f013af2cbe55e28')

prepare() {
    mkdir -p build
    cd ParaView-v${pkgver}/VTK
    patch -p1 -i ../../proj6.patch
}

build() {
    cd build

    # Flags to enable system libs in VTK building, as in VTK package
    # GL2PS has non-upstreamed patches?
    # KISSFFT is not packaged
    # UTF8 (utfcpp) is not packaged (but present in AUR)
    # VERDICT is not packaged
    # ZFP is not packaged
    # NETCDFCPP blocked by https://github.com/Unidata/netcdf-cxx4/issues/43
    # LIBHARU blocked by https://github.com/libharu/libharu/pull/157
    # SQLITE apparently not used in this VTK configuration
    local VTK_USE_SYSTEM_LIB=""
    for lib in DOUBLECONVERSION EIGEN EXPAT FREETYPE GLEW HDF5 JPEG JSONCPP LIBPROJ LIBXML2 LZ4 LZMA MPI4PY NETCDF OGG PEGTL PNG PUGIXML THEORA TIFF ZLIB
    do
        VTK_USE_SYSTEM_LIB+="-DVTK_USE_SYSTEM_${lib}:BOOL=ON "
    done
    # Specific system libs for ParaView version
    for lib in CGNS PROTOBUF PYGMENTS
    do
        VTK_USE_SYSTEM_LIB+="-DVTK_USE_SYSTEM_${lib}:BOOL=ON "
    done

    cmake ../ParaView-v${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DOSPRAY_INSTALL_DIR=/usr \
        -DPARAVIEW_ENABLE_FFMPEG=ON \
        -DPARAVIEW_ENABLE_GDAL=ON \
        -DPARAVIEW_ENABLE_MATPLOTLIB=ON \
        -DPARAVIEW_ENABLE_PDAL=ON \
        -DPARAVIEW_ENABLE_PYTHON=ON \
        -DPARAVIEW_INSTALL_DEVELOPMENT_FILES=ON \
        -DPARAVIEW_USE_MPI=ON \
        -DPARAVIEW_USE_VISITBRIDGE=ON \
        -DPARAVIEW_USE_OSPRAY=ON \
        -DVISIT_BUILD_READER_CGNS=ON \
        -DVTK_PYTHON_FULL_THREADSAFE=ON \
        -DVTK_PYTHON_VERSION=3 \
        -DVTK_SMP_IMPLEMENTATION_TYPE=TBB \
        -DVTKm_ENABLE_MPI=ON \
        -DVTKm_ENABLE_RENDERING=ON \
        -DVTKm_USE_DOUBLE_PRECISION=ON \
        -DModule_vtkGeovisCore=ON \
        -DModule_vtkGeovisGDAL=ON \
        -DModule_vtkIOGDAL=ON \
        -DModule_vtkIOPDAL=ON \
        ${VTK_USE_SYSTEM_LIB} \
        -GNinja

    ninja ${MAKEFLAGS}
}

package() {
    cd build

    DESTDIR="${pkgdir}" ninja install

    # Install license
    install -Dm644 "${srcdir}"/ParaView-v${pkgver}/License_v1.2.txt "${pkgdir}"/usr/share/licenses/paraview/LICENSE

    # Remove IceT man pages to avoid conflicts
    rm -- "${pkgdir}"/usr/share/man/man3/icet*.3
    rmdir "${pkgdir}"/usr/share/man/{man3/,}
}
