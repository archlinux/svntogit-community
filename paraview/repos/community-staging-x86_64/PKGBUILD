# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Mathieu Westphal <mathieu.westphal@kitware.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: <xantares09@hotmail.com>

_pkg=paraview
_mpi=openmpi
pkgname=${_pkg}
#-${_mpi}
pkgver=5.10.1
pkgrel=9
pkgdesc="Parallel Visualization application using VTK (${_mpi} version)"
arch=(x86_64)
url="https://www.paraview.org"
license=(BSD custom)
depends=(boost-libs qt5-tools qt5-svg intel-tbb openmpi ffmpeg
         adios2 liblas ospray pdal python-numpy protobuf
         cgns double-conversion expat freetype2 gdal gl2ps glew hdf5 libjpeg
         jsoncpp libjsoncpp.so libharu libxml2 lz4 xz python-mpi4py netcdf
         libogg libpng pugixml libtheora libtiff zlib)
optdepends=(python-matplotlib python-pandas)
makedepends=(cmake boost mesa gcc-fortran ninja qt5-xmlpatterns cli11 eigen utf8cpp)
# pegtl: https://gitlab.kitware.com/vtk/vtk/-/issues/18151
# rapidjson: https://gitlab.kitware.com/vtk/vtk/-/issues/18366
# fmt: https://gitlab.kitware.com/paraview/paraview/-/issues/21315
# catalyst, exprtk, ioss: not packaged
conflicts=(paraview-opt)
replaces=(paraview-opt)
source=(${url}/files/v${pkgver%.*}/ParaView-v${pkgver/R/-R}.tar.xz
        paraview-wrapper.sh
        template.sh
        vtkm-tbb-2021.patch::https://gitlab.kitware.com/vtk/vtk-m/-/merge_requests/2509.patch
        vtk-ffmpeg5.patch::https://gitlab.kitware.com/vtk/vtk/-/merge_requests/8863.patch
        paraview-revert-smp-info.patch::https://gitlab.kitware.com/paraview/paraview/-/merge_requests/5131.patch
        vtk-python3.10.patch::https://gitlab.kitware.com/vtk/vtk/-/merge_requests/8738.patch
        paraview-python3.10.patch::https://gitlab.kitware.com/paraview/paraview/-/merge_requests/5429.patch
        vtk-exodus-netcdf4.9-compat.patch::https://github.com/sandialabs/seacas/commit/5e1b19181110c52b76d13e3f60da2c8cf678c941.patch)
sha256sums=('520e3cdfba4f8592be477314c2f6c37ec73fb1d5b25ac30bdbd1c5214758b9c2'
            'd1cdb4a81db280d6b21bee3d9e5f8fc3a87f00a909d33850a1964c72091ec138'
            '844d44a14817c656ba8d6998298785125a98f03f7b5ece58d966c226db1e0d83'
            '37cff664c4eaacf44ecb995e62e9e54e54880bae0857d598c74660a2159ccb2e'
            '955ef22d2541beb9935557ef09a802b6504855a350f27ff056734679da8f0281'
            '360cb70b9b9ba255caf8513467df54f2ad02fa006b399ed7524d7867247acc32'
            'dbc9a67caf548cf5afd571018d91aadcfcf78ddf8c66ac75b0772935e26ad29f'
            'fcc0d8c27276615d024767ec283baff70ab911028c4ee9ef4479ab45f8c950c8'
            '1272fafccd3d9677f3fd9cc0a70e47a21f6ec86379765e25c6bccd9053875259')

prepare() {
  # Specify python version in wrapper
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  sed -i "s|@PYTHON@|${python_version}|" paraview-wrapper.sh
  # Arch required changes
  cd ParaView-v${pkgver/R/-R}
  # We have a patched libharu
  sed -i "s|2.4.0|2.3.0|" VTK/ThirdParty/libharu/CMakeLists.txt
  # Fix licences install (upstreamed but not released)
  sed -i 's|CACHE PATH "License|CACHE STRING "License|g' VTK/CMakeLists.txt
  # Fix build with TBB 2021
  patch -p1 -d VTK/ThirdParty/vtkm/vtkvtkm/vtk-m < ../vtkm-tbb-2021.patch
  # Fix build with FFmpeg 5
  patch -p1 -d VTK < ../vtk-ffmpeg5.patch
  # https://gitlab.kitware.com/paraview/paraview/-/issues/21323
  patch -p1 -R < ../paraview-revert-smp-info.patch || true
  # Fix Python 3.10 compatibility https://gitlab.kitware.com/vtk/vtk/-/issues/18518
  patch -p1 -d VTK < ../vtk-python3.10.patch || true
  patch -p1 < ../paraview-python3.10.patch
  # Fix build with netCDF 4.9
  sed -i 's|1999-2020|1999-2021|' VTK/ThirdParty/exodusII/vtkexodusII/src/ex_utils.c
  patch -p5 -d VTK/ThirdParty/exodusII/vtkexodusII/ < ../vtk-exodus-netcdf4.9-compat.patch
}

build() {
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"
  cmake -B build -S ParaView-v${pkgver/R/-R} -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/paraview \
    -DCMAKE_INSTALL_LICENSEDIR=share/licenses/paraview \
    -DCMAKE_SKIP_INSTALL_RPATH=OFF \
    -DPARAVIEW_ENABLE_ADIOS2=ON \
    -DPARAVIEW_ENABLE_FFMPEG=ON \
    -DPARAVIEW_ENABLE_FIDES=ON \
    -DPARAVIEW_ENABLE_GDAL=ON \
    -DPARAVIEW_ENABLE_LAS=ON \
    -DPARAVIEW_ENABLE_MOTIONFX=ON \
    -DPARAVIEW_ENABLE_PDAL=ON \
    -DPARAVIEW_ENABLE_RAYTRACING=ON \
    -DPARAVIEW_ENABLE_VISITBRIDGE=ON \
    -DPARAVIEW_ENABLE_XDMF3=ON \
    -DPARAVIEW_USE_MPI=ON \
    -DPARAVIEW_USE_PYTHON=ON \
    -DPARAVIEW_VERSIONED_INSTALL=OFF \
    -DPARAVIEW_BUILD_WITH_EXTERNAL=ON \
    -DVTK_SMP_IMPLEMENTATION_TYPE=TBB \
    -DVTKm_ENABLE_MPI=ON \
    -DCATALYST_USE_MPI=ON \
    -DFIDES_USE_EXTERNAL_RAPIDJSON=OFF \
    -DVTK_MODULE_USE_EXTERNAL_ParaView_vtkcatalyst=OFF \
    -DVTK_MODULE_USE_EXTERNAL_VTK_exprtk=OFF \
    -DVTK_MODULE_USE_EXTERNAL_VTK_fmt=OFF \
    -DVTK_MODULE_USE_EXTERNAL_VTK_ioss=OFF \
    -DVTK_MODULE_USE_EXTERNAL_VTK_pegtl=OFF \
    -Wno-dev
  ninja -C build ${MAKEFLAGS}
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install

  # Install wrappers
  install -Dm755 paraview-wrapper.sh "${pkgdir}"/usr/bin/paraview-wrapper
  for binary in paraview "${pkgdir}"/opt/paraview/bin/pv*
  do
      install -Dm755 template.sh "${pkgdir}"/usr/bin/${binary##*/}
      sed -i "s|@BINARY@|${binary##*/}|" "${pkgdir}"/usr/bin/${binary##*/}
  done

  # Install licenses, shortcuts, icons
  install -dm755 "${pkgdir}"/usr/share
  mv "${pkgdir}"/{opt/paraview,usr}/share/applications
  mv "${pkgdir}"/{opt/paraview,usr}/share/icons
  mv "${pkgdir}"/{opt/paraview,usr}/share/licenses
  mv "${pkgdir}"/{opt/paraview,usr}/share/metainfo

  # byte-compile python modules since the CMake build does not do it.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m compileall -o 0 -o 1 -o 2 --hardlink-dupes -s "${pkgdir}" "${pkgdir}"${site_packages/usr/opt/paraview}
}
