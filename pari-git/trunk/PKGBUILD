# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari-git
_pkgname=pari
pkgver=20150407.f0d91b9
pkgrel=2
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='http://pari.math.u-bordeaux.fr/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gmp' 'readline' 'libx11')
makedepends=('perl' 'texlive-core' 'git')
optdepends=('perl: gphelp, tex2mail')
source=('git+http://pari.math.u-bordeaux.fr/git/pari.git#commit=f0d91b910702e6fb4f9a5f0e82846e92e813e6cd'
        'public_memory_functions.patch')
sha1sums=('SKIP'
          'a88bca1bc18e6eea90c34a9ea88f16c7f1ec28c8')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd "${srcdir}/${_pkgname}"
	patch -p1 -i ../public_memory_functions.patch
}

build() {
	cd "${srcdir}/${_pkgname}"
	./Configure \
		--prefix=/usr \
		--with-readline \
		--mt=pthread \
		--with-gmp \
	
	make all
}

check() {
	cd "${srcdir}/${_pkgname}"
	make bench
}

package() {
	cd "${srcdir}/${_pkgname}"
	make DESTDIR="${pkgdir}" install
	ln -sf gp.1.gz "${pkgdir}"/usr/share/man/man1/pari.1
}
