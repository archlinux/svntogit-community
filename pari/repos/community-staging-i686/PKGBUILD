# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.7.2
pkgrel=2
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='http://pari.math.u-bordeaux.fr/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gmp' 'readline' 'libx11')
makedepends=('perl' 'texlive-core')
optdepends=('perl: gphelp, tex2mail')
source=("http://pari.math.u-bordeaux.fr/pub/pari/unix/${pkgname}-${pkgver}.tar.gz"
        'err_handle.patch')
sha1sums=('27ef49c39860055ff97608bc323bff2afda1aa51'
          '182066ab765eddb0b7fd98b441c158304545f5ec')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/\$addlib64//g' -i config/get_libpth
	patch -p1 -i ../err_handle.patch # sage-5.6 uses thisÂ¸ from upstream trunk
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./Configure \
		--prefix=/usr \
		--with-readline \
		--mt=pthread \
		--with-gmp \

	make all
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make bench
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -sf gp.1.gz "${pkgdir}"/usr/share/man/man1/pari.1
}
