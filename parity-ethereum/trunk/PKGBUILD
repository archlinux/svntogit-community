# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=parity-ethereum
pkgver=2.7.1
_commit=6885be06a47bfb34869a068b44f8fa7b46bd2123
pkgrel=1
pkgdesc='Fast, light, robust Ethereum implementation'
arch=('x86_64')
url='https://parity.io/'
license=('GPL3')
depends=('systemd-libs')
makedepends=('clang' 'cmake' 'git' 'rust' 'systemd')
provides=('parity')
conflicts=('parity')
replaces=('parity')
backup=('etc/parity.toml')
source=("git+https://github.com/paritytech/parity-ethereum.git#commit=${_commit}"
        'parity-system.service'
        'parity-user.service'
        'parity.toml')
sha256sums=('SKIP'
            'b2382103002707202324a40d1de186d35de7317c5112d9e5d197f97075c6c109'
            '7d979968367564dc48636344350403984ceb85f88a0b7d838bc047686e767289'
            'f4efce76a180b5a26b7e9bceee1d192b1d4d4790b711fa928fd8c9172d0cd290')

build() {
    cd ${pkgname}

    cargo build --release --features final
    cargo build --release -p evmbin
    cargo build --release -p ethstore-cli
    cargo build --release -p ethkey-cli
}

package() {
    cd ${pkgname}

    install -Dm755 -t "${pkgdir}"/usr/bin \
        target/release/{ethstore,parity,parity-evm}
    install -Dm755 target/release/ethkey "${pkgdir}"/usr/bin/parity-ethkey
    for type in system user; do
        install -Dm644 ../parity-${type}.service \
            "${pkgdir}/usr/lib/systemd/${type}/parity.service"
    done
    install -Dm644 -t "${pkgdir}/etc" ../parity.toml
}
