# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=parity
pkgver=1.6.9
_commit=d44b008d92bf7b36266c86ac53c78954a7553791
pkgrel=1
pkgdesc='Fast, light, robust Ethereum implementation'
arch=('i686' 'x86_64')
url='https://parity.io/'
license=('GPL3')
depends=('libsystemd' 'openssl')
makedepends=('cargo' 'git' 'systemd')
source=("git+https://github.com/paritytech/parity.git#commit=${_commit}"
        'parity.service')
sha256sums=('SKIP'
            '2b3714df9417fa11b7636114b8a5f126c2f6fd3934e74d81bdc85061e8d6e737')

build() {
    cd ${pkgname}

    cargo build --release --features final
    cargo build --release -p evmbin
    cargo build --release -p ethstore
    cargo build --release -p ethkey
}

package() {
    cd ${pkgname}

    install -Dm755 -t "${pkgdir}"/usr/bin \
        target/release/{ethkey,ethstore,parity,parity-evm}
    install -Dm644 -t "${pkgdir}"/usr/lib/systemd/user ../parity.service
}
