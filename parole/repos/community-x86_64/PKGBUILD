# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: TDY <tdy@gmx.com>

pkgname=parole
pkgver=0.9.2
pkgrel=1
pkgdesc="A modern media player based on the GStreamer framework"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/parole/"
license=('GPL')
depends=('gstreamer' 'gst-plugins-base-libs' 'hicolor-icon-theme' 'libnotify>=0.4.1'
         'libxfce4ui>=4.12.0' 'taglib>=1.4' 'desktop-file-utils' 'gst-plugins-good'
         'clutter-gtk')
makedepends=('xfce4-dev-tools')
optdepends=('gst-libav'
	    'gst-plugins-bad'
	    'gst-plugins-base'
	    'gst-plugins-ugly')
source=(https://git.xfce.org/apps/parole/snapshot/parole-$pkgver.tar.bz2)
sha256sums=('b29b561fad417d7cb16f1698a5f74f10d3120e2ea0970466fd93ccd9c9b14825')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib \
    --enable-taglib --enable-libnotify --with-gstreamer=1.0 \
    --enable-clutter
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
