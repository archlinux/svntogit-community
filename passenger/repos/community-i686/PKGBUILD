# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=passenger
pkgname=(passenger mod_passenger nginx-mod-passenger)
pkgver=5.1.8
pkgrel=1
_nginxver=1.12.1
pkgdesc="Fast and robust web server and application server for Ruby, Python and Node.js"
arch=('i686' 'x86_64')
url="https://www.phusionpassenger.com"
_watch="https://github.com/phusion/passenger/releases"
license=('MIT')
makedepends=('ruby' 'ruby-rack' 'curl' 'apache')
options=('!emptydirs' 'staticlibs')
validpgpkeys=('D5F0851426939232F437AB722AC745A50A212A8C'
              '16378A33A6EF16762922526E561F9B9CAC40B2F7')
source=(https://s3.amazonaws.com/phusion-passenger/releases/passenger-$pkgver.tar.gz{,.asc}
        https://nginx.org/download/nginx-$_nginxver.tar.gz)
sha256sums=('fef10e4a34c3faa48306c21c0789ee4d4d56fc0e30205cc470a91b486b7a4a7d'
            'SKIP'
            '8793bf426485a30f91021b6b945a9fd8a84d87d17b566562c3797aba8fac76fb')

build(){
  cd "$srcdir"/nginx-$_nginxver
  ./configure --with-compat --add-dynamic-module=../$pkgbase-$pkgver/src/nginx_module
  make modules

  cd "$srcdir"/$pkgbase-$pkgver
  rake nginx CACHING=false
  ./bin/passenger-install-apache2-module -a
}

package_passenger() {
  cd "$srcdir"/passenger-$pkgver
  depends=('ruby' 'ruby-rack' 'curl')

  mkdir -p "$pkgdir"/usr/lib/passenger/
  cp -R * "$pkgdir"/usr/lib/passenger/

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mod_passenger() {
  pkgdesc="Passenger apache module"
  depends=('passenger' 'apache')
  install=passenger.install

  cd "$srcdir"/passenger-$pkgver

  mkdir -p "$pkgdir"/usr/lib/httpd/modules
  ln -sf /usr/lib/passenger/buildout/apache2/mod_passenger.so "$pkgdir"/usr/lib/httpd/modules/mod_passenger.so

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_nginx-mod-passenger() {
  pkgdesc="Passenger nginx module"
  depends=('passenger' "nginx=$_nginxver")

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$srcdir"/nginx-$_nginxver/objs
  for mod in ngx_*.so; do
    install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
