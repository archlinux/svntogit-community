# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=pastebinit
pkgver=1.4
pkgrel=1
pkgdesc="Send anything you want directly to a pastebin from the command line"
arch=('any')
url="http://launchpad.net/pastebinit"
license=("GPL")
depends=("python")
makedepends=("asciidoc")
source=("http://launchpad.net/pastebinit/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz") 

package() {
  cd ${pkgname}-${pkgver}
  install -d "$pkgdir"/usr/{bin,share/man/man1,share/locale}
  install -m 755 pastebinit "$pkgdir"/usr/bin

  cp -a pastebin.d "$pkgdir"/usr/share
  a2x -f manpage pastebinit.xml
  install -m 644 pastebinit.1 "$pkgdir"/usr/share/man/man1

  cd po
  make
  cp -a mo/* "$pkgdir"/usr/share/locale
}

sha512sums=('79c8953cc960482b6a8b9f1d2deb73d113181ba525f2835a2b38ae36a87e8648af2d229bb382703035a8b07e2dd7aae46fed9f6e3368e2546c3e4ba5bff88d66')
