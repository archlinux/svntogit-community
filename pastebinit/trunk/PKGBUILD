# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=pastebinit
pkgver=1.4.1
pkgrel=1
pkgdesc="Send anything you want directly to a pastebin from the command line"
arch=('any')
url="http://launchpad.net/pastebinit"
license=("GPL")
depends=("python")
makedepends=("asciidoc")
source=("http://launchpad.net/pastebinit/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz") 

package() {
  cd ${pkgname}-${pkgver}
  install -d "$pkgdir"/usr/{bin,share/man/man1,share/locale}
  install -m 755 pastebinit "$pkgdir"/usr/bin

  cp -a pastebin.d "$pkgdir"/usr/share
  a2x -f manpage pastebinit.xml
  install -m 644 pastebinit.1 "$pkgdir"/usr/share/man/man1

  cd po
  make
  cp -a mo/* "$pkgdir"/usr/share/locale
}

sha512sums=('1337c4fce52949afded095d683eb088f0cc796aab9093a8843ec4cbc5cbe63ef626f875ff5fda2a95847e346deecce3195e20e46c84c28d04004e2fde6788c2e')
