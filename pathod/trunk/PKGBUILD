# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pathod
pkgver=0.11.1
pkgrel=1
pkgdesc="A pathological web daemon"
url="http://pathod.net"
depends=('python2-netlib' 'python2-requests' 'python2-flask')
checkdepends=('python2-mock' 'python2-nose')
arch=('any')
license=('MIT')
source=("https://pypi.python.org/packages/source/p/pathod/pathod-$pkgver.tar.gz")
sha256sums=('69d91a494a8ac71e8d1e2c0cc7140fc52be0a04aa37f7fc8560ca3b168479058')

prepare() {
  cd $pkgname-$pkgver

  rm -r netlib

  sed -e '1s/env python$/env python2/g' -i libpathod/cmdline.py
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

check() {
  cd $pkgname-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2
}

package() {
  cd $pkgname-$pkgver  
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
