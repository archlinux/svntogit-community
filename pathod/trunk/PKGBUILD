# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pathod
pkgver=0.12.1
pkgrel=1
pkgdesc="A pathological web daemon"
url="http://pathod.net"
depends=('python2-netlib' 'python2-requests' 'python2-flask' 'python2-pyparsing')
checkdepends=('python2-mock' 'python2-nose')
arch=('any')
license=('MIT')
source=("https://pypi.python.org/packages/source/p/pathod/pathod-$pkgver.tar.gz")
sha256sums=('0777c1acd7e627900461351be011a3ddfeafd89fddf76896681c21b3dfee841a')

prepare() {
  cd $pkgname-$pkgver
  sed -e '1s/env python$/env python2/g' -i libpathod/cmdline.py
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

check() {
  # One test failed: https://github.com/mitmproxy/pathod/issues/24

  cd $pkgname-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2 || warning "Tests failed"
}

package() {
  cd $pkgname-$pkgver  
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
