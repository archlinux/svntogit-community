# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=pavumeter
pkgver=0.9.3
pkgrel=3
pkgdesc="A simple GTK volume meter for the PulseAudio sound server"
arch=(i686 x86_64)
url="http://0pointer.de/lennart/projects/pavumeter"
license=(GPL)
depends=(gtkmm pulseaudio gnome-icon-theme libsigc++)
groups=(pulseaudio-gnome)
source=(http://0pointer.de/lennart/projects/pavumeter/${pkgname}-${pkgver}.tar.gz
        pavumeter.desktop pavumeter-record.desktop)
md5sums=('3a6b36cfefa8d6dd00084dafa50c934c' 'ead0c02a2af5fcf4acc381da39b836df'
         '2d784d472cd22dfc5320c30112e1e59d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-lynx
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/pavumeter.desktop \
    ${pkgdir}/usr/share/applications/pavumeter.desktop
  install -Dm644 ${srcdir}/pavumeter-record.desktop \
    ${pkgdir}/usr/share/applications/pavumeter-record.desktop
}
