# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: sh0 <mee@sh0.org>
# Contributor: Maxwel <msg.maxwel@gmail.com>

pkgname=pax-utils
pkgver=1.0.3
pkgrel=1
pkgdesc='ELF related utils for ELF 32/64 binaries that can check files for security relevant properties'
url='http://hardened.gentoo.org/pax-utils.xml'
arch=('i686' 'x86_64')
license=('GPL')
depends=('bash' 'libcap' 'python-pyelftools')
source=("http://distfiles.gentoo.org/distfiles/${pkgname}-${pkgver}.tar.xz")
sha256sums=('8535a94e1f77841da92d5526d2935abb786437fdf11be9ed077a78ab5e6b5670')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make USE_CAP='yes'
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # FIXME lddpath.py uses /lib64 before /usr/lib
  LD_LIBRARY_PATH="/usr/lib" make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make USE_PYTHON='yes' DESTDIR="${pkgdir}" install
}
