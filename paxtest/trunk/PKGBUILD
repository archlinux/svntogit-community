# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Lubomir Krajcovic <lubomir.krajcovic(AT)gmail(DOT)com>
# Contributor: sh0 <mee(AT)sh0(DOT)org>
# Contributor: leoarcher <carvalho.inacio(AT)gmail(DOT)com>

pkgname=paxtest
pkgver=0.9.15
pkgrel=1
pkgdesc='PaX regression test suite'
url='https://pax.grsecurity.net'
arch=('i686' 'x86_64')
license=('GPL')
depends=(sh)
source=("https://grsecurity.net/~spender/${pkgname}-${pkgver}.tar.gz")
md5sums=('8b6ad80d37aadaf084162ccd8798d28d')

_make="make -f Makefile.psm BINDIR=/usr/bin RUNDIR=/usr/lib/$pkgname"

build() {
  cd $pkgname-$pkgver
  $_make
}

package() {
  cd $pkgname-$pkgver
  $_make DESTDIR="$pkgdir" install
}
