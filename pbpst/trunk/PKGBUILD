# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>
#
# Use extra-x86_64-build --  -d /dev/fuse to build in a clean chroot

pkgname=pbpst
pkgver=1.1.2
pkgrel=1
pkgdesc='A small tool to interact with pb instances'
url='https://github.com/HalosGhost/pbpst'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('curl' 'jansson')
makedepends=('git' 'tup' 'clang' 'python-sphinx')
source=("https://github.com/HalosGhost/pbpst/archive/v${pkgver}.tar.gz")
md5sums=('eb5d018a3301295976f86b59423dd1a7')

build () {
    cd "pbpst-${pkgver}"

    ./configure --cflags="$CFLAGS"

    make
}

package () {
    cd "pbpst-${pkgver}"
    make DESTDIR="${pkgdir}" PREFIX='/usr' install
}
