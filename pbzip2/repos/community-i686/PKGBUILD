# $Id$
# Contributor: Vasco Costa <vasco dot costa at geekslot dot com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=pbzip2
pkgver=1.1.11
pkgrel=1
pkgdesc="Parallel implementation of the bzip2 block-sorting file compressor"
arch=('i686' 'x86_64')
url="http://compression.ca/pbzip2/"
license=('BSD')
depends=('bzip2' 'gcc-libs')
source=(https://launchpad.net/$pkgname/1.1/$pkgver/+download/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('b4861389bd99c73d304a03b0dc0731a8a26905da8e94ca4cef873b96e1ab0e7f'
            'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make PREFIX="$pkgdir/usr" install

  # Fix location of man pages
  install -d "$pkgdir/usr/share"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share/"

  # Symlinks for useful shortcuts
  ln -sf pbzip2 "$pkgdir/usr/bin/pbunzip2"
  ln -sf pbzip2 "$pkgdir/usr/bin/pbzcat"

  # License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
