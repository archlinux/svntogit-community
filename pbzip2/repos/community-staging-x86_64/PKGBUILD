# $Id$
# Contributor: Vasco Costa <vasco dot costa at geekslot dot com>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=pbzip2
pkgver=1.1.12
pkgrel=2
pkgdesc="Parallel implementation of the bzip2 block-sorting file compressor"
arch=('i686' 'x86_64')
url="http://compression.ca/pbzip2/"
license=('BSD')
depends=('bzip2' 'gcc-libs')
source=(https://launchpad.net/$pkgname/1.1/$pkgver/+download/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('573bb358a5a7d3bf5f42f881af324cedf960c786e8d66dd03d448ddd8a0166ee'
            'SKIP')
validpgpkeys=('F3D99FF0F0AA96A659AD61A9D332F1B09A0D9836')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
