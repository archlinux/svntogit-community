# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=pcmanfm-qt
pkgver=0.9.0
pkgrel=1
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=("liblxqt" "libfm" "lxmenu-data")
makedepends=("cmake" "qt5-tools")
install="$pkgname.install"
source=("http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=("2df0f0a0c092f9ee6e7311255ab2aed66c8ea897b3399affb96732c46421e2df")


build() {
	mkdir -p build
	cd build

	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
