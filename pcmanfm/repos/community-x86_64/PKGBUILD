# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=pcmanfm
pkgver=1.2.0
pkgrel=1
pkgdesc='Extremely fast and lightweight file manager'
arch=('i686' 'x86_64')
url='http://pcmanfm.sourceforge.net/'
license=('GPL')
groups=('lxde')
depends=('gtk2' 'desktop-file-utils' 'libfm' 'lxmenu-data')
makedepends=('intltool')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('11d59a492c9802866279a6e7e2b3f49e')

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr
  make LDFLAGS+=' -lm'
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -e 's|x-directory/normal|inode/directory|' -i \
    "$pkgdir"/usr/share/applications/pcmanfm.desktop
}
