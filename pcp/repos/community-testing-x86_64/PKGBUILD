# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Iwan Timmer <irtimmer@gmail.com>

pkgname=pcp
pkgver=4.3.4
pkgrel=2
pkgdesc="System performance analysis toolkit"
arch=('x86_64')
url="https://pcp.io"
license=('LGPL')
depends=('python' 'avahi' 'procps-ng')
makedepends=('libmicrohttpd' 'cairo' 'qt5-svg')
optdepends=('libmicrohttpd: support for pmwebd'
            'cairo: support for pmwebd'
            'qt5-svg: support for PCP-GUI and pmchart'
            'perl-xml-tokeparser: support for sar2pcp'
            'perl-date-parse: support for sar2pcp')
source=("https://dl.bintray.com/pcp/source/pcp-$pkgver.src.tar.gz"
        "sysuser.conf"
        "tmpfile.conf")
sha256sums=('85e6c36110c82da3eb35d361c730e5ccdb6a97895087386dd416e6c2fb70662f'
            '3e21ef7925296f73c91fe85c8dba341ce00366163845dd8993e2813b0ad5e4a4'
            '086980818148ec622a28704b4e1a9989c4dbd468015f03f8e80e7b2a83a31ec4')

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/$pkgname/ \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --without-dstat-symlink
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DIST_ROOT="$pkgdir" PCP_USER=root PCP_GROUP=root install

  rm -rf "$pkgdir"/var/run
  install -D -m644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/pcp.conf
  install -D -m644 "$srcdir"/tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/pcp.conf

  rm -rf "$pkgdir"/var/lib/pcp/testsuite "$pkgdir"/tmp
}
