# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
pkgver=1.8.1
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('udev')
makedepends=('pkgconfig')
options=('!libtool' '!docs')
source=("https://alioth.debian.org/frs/download.php/3687/pcsc-lite-${pkgver}.tar.bz2"
        'pcscd')
md5sums=('fd035e4f610eba6fa545159e60d0d780'
         'c97b6bce695222efb0f003d8e8369010')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  ./configure --prefix=/usr \
              --enable-libudev \
              --sysconfdir=/etc \
              --enable-ipcdir=/run/pcscd \
              --enable-usbdropdir=/usr/lib/pcsc/drivers

  make
}

package() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m755 ${srcdir}/pcscd ${pkgdir}/etc/rc.d/pcscd
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
