# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
pkgver=1.7.4
pkgrel=2
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('udev')
makedepends=('pkgconfig')
options=('!libtool' '!docs')
source=(https://alioth.debian.org/frs/download.php/3598/pcsc-lite-${pkgver}.tar.bz2
        pcscd)
md5sums=('1caf0b8ca2ecbf82fe3b035b3fff22dd'
         '80d90388e4d2a551cb7aefe3bd5147bd')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  ./configure --prefix=/usr \
              --enable-libudev \
              --sysconfdir=/etc \
              --enable-ipcdir=/run/pcscd \
              --enable-usbdropdir=/usr/lib/pcsc/drivers

  make
}

package() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m755 ${srcdir}/pcscd ${pkgdir}/etc/rc.d/pcscd
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
