# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.4.0
pkgrel=8
pkgdesc='A Sony PlayStation 2 emulator'
arch=('x86_64')
url='http://www.pcsx2.net'
license=('GPL2' 'GPL3' 'LGPL2.1' 'LGPL3')
depends=('lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo'
         'lib32-libpulse' 'lib32-nvidia-cg-toolkit' 'lib32-portaudio'
         'lib32-sdl2' 'lib32-soundtouch' 'lib32-wxgtk2')
makedepends=('cmake' 'gcc-multilib' 'png++')
source=("pcsx2-${pkgver}.tar.gz::https://github.com/PCSX2/pcsx2/archive/v${pkgver}.tar.gz"
        'pcsx2-gcc6.patch')
sha256sums=('2c8a986e2b6514d7018f6cfd39c4f2a72229b9b4ab06ca6b1d3466dfd9c33005'
            'd78c63808a609a249ec7e8330eeabee306a55ee08d2c9ce9c383b46b334bf7d0')

prepare() {
  cd pcsx2-${pkgver}

  patch -Np1 -i ../pcsx2-gcc6.patch
}

build() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  # TODO: Drop wxWidgets_CONFIG_EXECUTABLE once the upstream fix is released
  # https://github.com/PCSX2/pcsx2/issues/1933
  cmake ../pcsx2-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config32' \
    -DCMAKE_TOOLCHAIN_FILE='cmake/linux-compiler-i386-multilib.cmake' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_LIBRARY_PATH='/usr/lib32' \
    -DPLUGIN_DIR='/usr/lib32/pcsx2' \
    -DGAMEINDEX_DIR='/usr/share/pcsx2' \
    -DDISABLE_ADVANCE_SIMD='TRUE' \
    -DEXTRA_PLUGINS='TRUE' \
    -DREBUILD_SHADER='TRUE' \
    -DGLSL_API='TRUE' \
    -DPACKAGE_MODE='TRUE' \
    -DXDG_STD='TRUE'

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
