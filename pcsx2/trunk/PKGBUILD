# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.7.2101
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator'
arch=(x86_64)
url=https://www.pcsx2.net
license=(
  GPL2
  GPL3
  LGPL2.1
  LGPL3
)
depends=(
  gcc-libs
  glibc
  libaio
  libasound.so
  libcap
  libfmt.so
  libfreetype.so
  libgdk-3.so
  libgdk_pixbuf-2.0.so
  libgio-2.0.so
  libGLEW.so
  libgl
  libglib-2.0.so
  libgobject-2.0.so
  libgtk-3.so
  libharfbuzz.so
  libpcap.so
  libpng
  libportaudio.so
  libpulse.so
  libsamplerate.so
  libudev.so
  libx11
  libxcb
  libxml2
  libxrandr
  libyaml-cpp.so
  sdl2
  soundtouch
  wxgtk3
  wxgtk-common
  xz
  zlib
)
makedepends=(
  cmake
  git
  ninja
  png++
)
_tag=bb55c320c1427a4fe593b77386c2cea8d301e1ca
source=(
  git+https://github.com/PCSX2/pcsx2.git#tag=${_tag}
  git+https://github.com/rtissera/libchdr.git
)
b2sums=(
  SKIP
  SKIP
)
install=pcsx2.install

prepare() {
  cd pcsx2/3rdparty
  for submodule in libchdr/libchdr; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../../${submodule#*/}
    git submodule update ${submodule}
  done
}

pkgver() {
  cd pcsx2

  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S pcsx2 -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDISABLE_ADVANCE_SIMD=ON \
    -DDISABLE_BUILD_DATE=ON \
    -DDISABLE_PCSX2_WRAPPER=ON \
    -DDISABLE_SETCAP=ON \
    -DENABLE_TESTS=OFF \
    -DLTO_PCSX2_CORE=ON \
    -DPACKAGE_MODE=ON \
    -DSDL2_API=ON \
    -DUSE_SYSTEM_YAML=ON \
    -DUSE_VTUNE=OFF \
    -DXDG_STD=ON \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
    -Wno-dev
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
