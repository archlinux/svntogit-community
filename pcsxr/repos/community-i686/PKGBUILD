# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: quantax -- contact via Arch Linux forum or AUR
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=pcsxr
pkgver=1.9.93
pkgrel=4
pkgdesc='A Sony PlayStation (PSX) emulator based on the PCSX-df project'
arch=('i686' 'x86_64')
url='http://pcsxr.codeplex.com/'
license=('GPL')
depends=('libcdio' 'libxv' 'sdl' 'gtk3')
makedepends=('mesa' 'intltool')
[[ $CARCH == 'i686' ]] && makedepends+=('nasm')

# Accessible through /srv/ftp/other/community on nymeria.
source=("https://sources.archlinux.org/other/community/pcsxr/pcsxr-${pkgver}.tar.bz2")

build() {
    cd "$srcdir/$pkgname"

    autoreconf -f -i
    intltoolize --force

    ./configure --prefix=/usr --enable-libcdio --enable-opengl
    make
}

package() {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir" install
}

md5sums=('d75725b4c3fcb2cb11d39b3ace10dc31')
