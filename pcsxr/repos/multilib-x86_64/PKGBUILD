# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: quantax -- contact via Arch Linux forum or AUR
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=pcsxr
pkgver=1.9.93
pkgrel=5
pkgdesc='A Sony PlayStation (PSX) emulator based on the PCSX-df project'
arch=('i686' 'x86_64')
url='http://pcsxr.codeplex.com/'
license=('GPL')
depends=('libcdio' 'libxv' 'sdl' 'gtk3')
makedepends=('mesa' 'intltool' 'nasm')
options=('!libtool')

if [[ $CARCH == 'x86_64' ]]; then
    makedepends+=('lib32-mesa')
    depends+=('lib32-libcdio' 'lib32-libxv' 'lib32-sdl' 'lib32-gtk3')
fi

# Accessible through /srv/ftp/other/community on nymeria.
source=("https://sources.archlinux.org/other/community/pcsxr/pcsxr-${pkgver}.tar.bz2")

build() {
    cd "$srcdir/$pkgname"

    if [[ $CARCH == 'x86_64' ]]; then
        export CC="gcc -m32"
        export CXX="g++ -m32"
    fi

    autoreconf -f -i
    intltoolize --force

    ./configure --prefix=/usr \
        --libdir=/usr/lib32 \
        --enable-dynarec=x86 \
        --enable-libcdio \
        --enable-opengl
    make
}

package() {
    cd "$srcdir/$pkgname"
    make DESTDIR="$pkgdir" install
}

md5sums=('d75725b4c3fcb2cb11d39b3ace10dc31')
