# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.48.0
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=3
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('x86_64')
url="http://msp.ucsd.edu/software.html"
license=('custom:BSD')
depends=('fftw' 'jack' 'tk')
optdepends=('portaudio: for alternative portaudio support')
provides=('puredata')
source=("http://msp.ucsd.edu/Software/${pkgname}-${_ver}.src.tar.gz"
        "${pkgname}.desktop"
        "${pkgname}.png")
sha512sums=('2312590131a2110de1c84fa2b5d407286f2a073564c6f4cd285d48dcf1ae9d003dc5f20ce74a970eeb4ae8ff68d537e2eaf9ca6322d4f975a7a39893f4d8f0f0'
            '4d3596b337456117ecdf0f1709358b4298198e0b347cb13b884e35ad2f2f667561a10b44683cf916c1da123daebee7141e9ab8bd89405a579b69d30b0e718f37'
            '61300b58f10018b0bd28424ac00d1b8158f8ace9855742e19e48c98f915cabcade6c6041460aa795005c0fb6abc93e7116f9999c6d001999916c9fe6c85c162d')

prepare() {
  cd "${pkgname}-${_ver}"
  autoreconf -vi
}

build() {
  cd "${pkgname}-${_ver}"
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --enable-portaudio \
              --enable-fftw \
              --without-local-portaudio
  make
}

package() {
  cd "${pkgname}-${_ver}"
  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # desktop file
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  # icon from puredata.info
  # https://puredata.info/Members/claudiusmaximus/icons/index_html/
  install -Dm644 "${srcdir}/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # readme
  install -Dm644 README.txt "${pkgdir}/usr/share/doc/${pgkname}/README.txt"
  # fix broken symlink
  rm "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ln -s "/usr/bin/${pkgname}" "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
}
