# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.3.0
pkgrel=3
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'laszip' 'jsoncpp' 'hexer-hobu' 'libunwind' 'laz-perf')
makedepends=('cmake' 'python' 'python-numpy' 'ninja')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.gz")
sha512sums=('9de87de7370175e76b95d3ec08667eb28b90a63eed8133f144427ff931e575d1f37aa13496f12054577539ee552458475dd054bcbdedfabe52eb0e32c4483d05')

build() {
    cd "PDAL-$pkgver-src/"

    cmake \
        -B build \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
        -DWITH_LAZPERF=ON \
        -DWITH_LASZIP=ON \
        -DWITH_COMPLETION=ON

    ninja -C build
}

package() {
    cd "PDAL-$pkgver-src"

    DESTDIR="$pkgdir/" ninja -C build install

    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
