# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=1.5.0
pkgrel=1
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('i686' 'x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'laszip' 'jsoncpp')
makedepends=('cmake' 'python' 'python-numpy' 'laz-perf')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("http://download.osgeo.org/pdal/PDAL-$pkgver-src.tar.gz")
sha512sums=('40aa3ef21d8ee25ab26b532e2d934bdc4557268d33b8d93d1717cd2c9ab9d17435381c479d1623c4df52ea31b6aa55168a8a6e7171d5da1f8eda116191bb5b74')

prepare() {
    cd "PDAL-$pkgver-src"

    [[ -d build ]] || mkdir build
}

build() {
    cd "PDAL-$pkgver-src/build"

    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_PLUGIN_PYTHON=ON \
        -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
        -DBUILD_PLUGIN_SQLITE=ON \
        -DWITH_LAZPERF=ON \
        -DWITH_LASZIP=ON \
        -DWITH_COMPLETION=ON

    make
}

package() {
    cd "PDAL-$pkgver-src/build"

    make DESTDIR="$pkgdir/" install

    cd "$srcdir/PDAL-$pkgver-src"
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
