# $Id$
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.7
pkgrel=2
pkgdesc="Creates DjVu files from PDF files"
arch=('i686' 'x86_64')
url="http://jwilk.net/software/pdf2djvu"
license=('GPL')
depends=('poppler' 'djvulibre' 'gcc-libs' 'graphicsmagick' 'exiv2')
makedepends=('python2-nose' 'ttf-liberation')
source=(https://github.com/jwilk/pdf2djvu/releases/download/$pkgver/pdf2djvu-$pkgver.tar.xz{,.asc})
sha256sums=('9e7d42632a130cf73fc6144a78b0795dc0d690ecccd25360fa3c999e6f7222c1'
            'SKIP')
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}

    sed -i 's/nosetests/nosetests2/' tests/Makefile
    make test
}

package() {
    cd ${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
}
