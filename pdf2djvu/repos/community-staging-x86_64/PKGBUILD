# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.13
pkgrel=3
pkgdesc="Creates DjVu files from PDF files"
arch=('x86_64')
url="https://jwilk.net/software/pdf2djvu"
license=('GPL')
depends=('poppler' 'djvulibre' 'gcc-libs' 'graphicsmagick' 'exiv2')
makedepends=('python2-nose' 'ttf-liberation')
source=(https://github.com/jwilk/pdf2djvu/releases/download/$pkgver/pdf2djvu-$pkgver.tar.xz{,.asc})
sha256sums=('e0ab16d83129625c86df7c121eddf286458f63cb81ae791e5a76df10a3ab4c63'
            'SKIP')
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}

    make PYTHON=python2 test
}

package() {
    cd ${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
}
