# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.12
pkgrel=6
pkgdesc="Creates DjVu files from PDF files"
arch=('x86_64')
url="https://jwilk.net/software/pdf2djvu"
license=('GPL')
depends=('poppler' 'djvulibre' 'gcc-libs' 'graphicsmagick' 'exiv2')
makedepends=('python2-nose' 'ttf-liberation')
source=(https://github.com/jwilk/pdf2djvu/releases/download/$pkgver/pdf2djvu-$pkgver.tar.xz{,.asc}
        pdf2djvu-poppler-0.76.patch)
sha256sums=('03fa9d9aa1b8086b562e2461ed74e5f9f008f16bfc1459ba62fa3738b37d8e83'
            'SKIP'
            '5b43f0a47ecf51756135c323df6720ed3bdde4cb6e2e754cf91ded6c03dce67c')
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../pdf2djvu-poppler-0.76.patch # Fix build with poppler 0.76
}

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}

    make PYTHON=python2 test
}

package() {
    cd ${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
}
