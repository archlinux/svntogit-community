# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.9.10
pkgrel=2
pkgdesc="Creates DjVu files from PDF files"
arch=('x86_64')
url="http://jwilk.net/software/pdf2djvu"
license=('GPL')
depends=('poppler' 'djvulibre' 'gcc-libs' 'graphicsmagick' 'exiv2')
makedepends=('python2-nose' 'ttf-liberation')
source=(https://github.com/jwilk/pdf2djvu/releases/download/$pkgver/pdf2djvu-$pkgver.tar.xz{,.asc}
        pdf2djvu-poppler-0.69.0.patch::https://github.com/jwilk/pdf2djvu/commit/2a82de9b1d.patch)
sha256sums=('76479a86fdb6457ffc415e79561e9ea3a4027f1faffb9080445873b3c97bdb08'
            'SKIP'
            'f5a7c87cd38403f88655476d5c325e6b527b85e6fafce393ad6e9a6534803d8f')
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')

prepare() {
        cd ${pkgname}-${pkgver}
        patch -Np1 -i ../pdf2djvu-poppler-0.69.0.patch
}

build() {
    cd ${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}

    sed -i 's/nosetests/nosetests2/' tests/Makefile
    make test
}

package() {
    cd ${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
}
