# $Id$
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=pdf2djvu
pkgver=0.8
pkgrel=1
pkgdesc="Creates DjVu files from PDF files"
arch=('i686' 'x86_64')
url="http://pdf2djvu.googlecode.com"
license=('GPL')
depends=('poppler' 'djvulibre' 'libxslt' 'gcc-libs' 'graphicsmagick')
makedepends=('pstreams' 'python2-nose' 'ttf-liberation')
source=(https://bitbucket.org/jwilk/pdf2djvu/downloads/pdf2djvu-$pkgver.tar.xz{,.asc})
validpgpkeys=('CDB5A1243ACDB63009AD07212D4EB3A6015475F5')
md5sums=('30d30eab76c6b0717561c4ca61214687'
         'SKIP')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver}

    sed -i 's/nosetests/nosetests2/' tests/Makefile
    make test
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    make install DESTDIR=${pkgdir}
    install -Dm644 doc/${pkgname}.1 ${pkgdir}/usr/share/man/man1/${pkgname}.1
}

