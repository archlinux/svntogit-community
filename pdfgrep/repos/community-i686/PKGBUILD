# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Gabriel B. Casella <gbc921@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=pdfgrep
pkgver=1.3.0
pkgrel=2
pkgdesc="A tool to search text in PDF files"
arch=('i686' 'x86_64')
url="http://pdfgrep.sourceforge.net/"
license=('GPL')
depends=('poppler')
conflicts=('pdfgrep-git')
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('07d2819dd11b0f92278df11a95221833')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
