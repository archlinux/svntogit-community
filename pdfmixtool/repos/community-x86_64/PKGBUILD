# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Marco Scarpetta <marcoscarpetta02@gmail.com>

pkgname=pdfmixtool
pkgver=0.6
pkgrel=2
pkgdesc='Application to split, merge, rotate and mix PDF files'
arch=('x86_64')
url='https://scarpetta.eu/pdfmixtool/'
license=('GPL3')
depends=('hicolor-icon-theme' 'qpdf' 'qt5-base')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("https://gitlab.com/scarpetta/pdfmixtool/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('a1d43d63e0981724434bb0db62ee91185d17e45402dbec8df1b2e34893ffc782')

prepare() {
  # Update URL
  sed -i 's/scarpetta.gitlab.io/scarpetta.eu/' $pkgname-v$pkgver/resources/eu.scarpetta.PDFMixTool.appdata.xml
}

build() {
  cmake -S $pkgname-v$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
