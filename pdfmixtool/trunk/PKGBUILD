# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Marco Scarpetta <marcoscarpetta02@gmail.com>

pkgname=pdfmixtool
pkgver=0.3.4
pkgrel=3
pkgdesc='Application to split, merge, rotate and mix PDF files'
arch=(x86_64)
url='https://scarpetta.eu/pdfmixtool/'
license=(GPL3)
depends=(hicolor-icon-theme podofo qt5-base)
makedepends=(cmake qt5-tools)
source=("https://gitlab.com/scarpetta/pdfmixtool/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha256sums=('93a03cbe017ebd23f241724c6366c903bc4f95f7786eaf800fdae5f1817516a1')

prepare() {
  [[ -d build ]] || mkdir build
}

build() {
  cd build
  cmake -G "Unix Makefiles" ../$pkgname-v$pkgver/ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
