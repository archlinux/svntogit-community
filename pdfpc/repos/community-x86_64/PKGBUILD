# $Id$
# Maintainer: Jiachen Yang <farseerfc@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc
pkgver=4.0.6
_pkgver=$pkgver
pkgrel=1
pkgdesc='A presenter console with multi-monitor support for PDF files'
arch=('i686' 'x86_64')
url='https://pdfpc.github.io/'
license=('GPL')
depends=('gtk3' 'poppler-glib' 'libgee' 'gstreamer' 'gst-plugins-base')
optdepends=('gst-plugins-good: more codecs for video playback support')
makedepends=('cmake' 'vala')
source=("$pkgname-$_pkgver.tar.gz::https://github.com/pdfpc/pdfpc/archive/v$_pkgver.tar.gz")
sha256sums=('42b6bd421e977a98cf00d688da7b392b8d8be423eb56ea7ca929c3c0334f8aa4')

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    cmake  -DCMAKE_INSTALL_PREFIX="/usr/" -DSYSCONFDIR="/etc" .
    make
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=4 sw=4 et:
