# Maintainer: Jiachen Yang <farseerfc@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc
pkgver=4.5.0
_pkgver=${pkgver}
pkgrel=2
pkgdesc="Presenter console with multi-monitor support for PDF files"
arch=(x86_64)
url="https://pdfpc.github.io/"
license=(GPL)
depends=(gtk3 poppler-glib libgee gstreamer gst-plugins-base webkit2gtk discount)
optdepends=('gst-plugin-gtk: for video playback'
            'gst-plugins-good: more codecs for video playback support'
            'gst-libav: more codecs for video playback support')
makedepends=(cmake vala)
source=(https://github.com/pdfpc/pdfpc/archive/v$_pkgver/$pkgname-$_pkgver.tar.gz)
sha256sums=('e53ede1576da55403bba23671df5b946c756ec83ba30fbeb0cb7302f28b54a64')

#prepare() {
#    cd $pkgname-$_pkgver
#}

build() {
    cmake -B build -S $pkgname-$_pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr
    make -C build
}

package() {
    make -C build DESTDIR="$pkgdir" install
}

# vim:set ts=4 sw=4 et:
