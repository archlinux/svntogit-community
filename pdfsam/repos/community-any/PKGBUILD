# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Tom <reztho at archlinux dot us>

pkgname=pdfsam
pkgver=3.0.2
_pkgver="${pkgver}.RELEASE"
pkgrel=2
pkgdesc="A free open source tool to split and merge pdf documents"
arch=('any')
url="http://www.pdfsam.org/"
license=('AGPL3')
depends=('java-environment>=8' 'sh')
makedepends=('desktop-file-utils' 'gendesk')
optdepends=('java-openjfx: required if openjdk is used')
source=("https://github.com/torakiki/${pkgname}/releases/download/v${_pkgver}/${pkgname}-${_pkgver}-bin.zip"
        "pdfsam"
        "pdfsam.png")
md5sums=('cba57fcce6075348e9dbecd9a014182b'
         'e98c7828d1d8b4fdb7ba76c0e0f180db'
         '033e0e52c29624ce31d86b8a8b43adb6')

prepare() {
  # create *.desktop file
  gendesk -f -n \
          --pkgname="${pkgname}" \
          --pkgdesc="${pkgdesk}" \
          --name="PDFsam" \
          --categories="Office"
}

package() {
  install -dm755 "${pkgdir}/usr/share/java/${pkgname}/modules"
  install -Dm644 "${pkgname}-${_pkgver}/pdfsam-community-${_pkgver}.jar" \
                 "${pkgdir}/usr/share/java/${pkgname}/"
  install -Dm755 "${pkgname}-${_pkgver}/bin/pdfsam.sh" \
                 "${pkgdir}/usr/share/java/${pkgname}/bin/pdfsam.sh"
  install -Dm755 "${pkgname}-${_pkgver}/etc/logback.xml" \
                 "${pkgdir}/usr/share/java/${pkgname}/etc/logback.xml"
  install -Dm755 "${pkgname}-${_pkgver}/resources/splash.gif" \
                 "${pkgdir}/usr/share/java/${pkgname}/resources/splash.gif"

  # exec
  install -Dm755 "pdfsam" "${pkgdir}/usr/bin/pdfsam"

  # desktop
  install -Dm644 "pdfsam.png" "${pkgdir}/usr/share/pixmaps/pdfsam.png"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
