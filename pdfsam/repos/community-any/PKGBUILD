# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Tom <reztho at archlinux dot us>

pkgname=pdfsam
pkgver=3.3.5
pkgrel=1
pkgdesc="A free open source tool to split and merge pdf documents"
arch=('any')
url="http://www.pdfsam.org/"
license=('AGPL3')
depends=('java-environment>=8' 'java-openjfx>=8' 'sh')
makedepends=('desktop-file-utils' 'gendesk')
source=("https://github.com/torakiki/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}-bin.zip"
        "pdfsam"
        "pdfsam.png")

prepare() {
  # create *.desktop file
  gendesk -f -n \
          --pkgname="${pkgname}" \
          --pkgdesc="${pkgdesk}" \
          --name="PDFsam" \
          --categories="Office"
}

package() {
  install -dm755 "${pkgdir}/usr/share/java/${pkgname}/modules"
  install -Dm644 "${pkgname}-${pkgver}/pdfsam-community-${pkgver}.jar" \
                 "${pkgdir}/usr/share/java/${pkgname}/"
  install -Dm755 "${pkgname}-${pkgver}/bin/pdfsam.sh" \
                 "${pkgdir}/usr/share/java/${pkgname}/bin/pdfsam.sh"
  install -Dm755 "${pkgname}-${pkgver}/etc/logback.xml" \
                 "${pkgdir}/usr/share/java/${pkgname}/etc/logback.xml"
  install -Dm755 "${pkgname}-${pkgver}/resources/splash.gif" \
                 "${pkgdir}/usr/share/java/${pkgname}/resources/splash.gif"

  # exec
  install -Dm755 "pdfsam" "${pkgdir}/usr/bin/pdfsam"

  # desktop
  install -Dm644 "pdfsam.png" "${pkgdir}/usr/share/pixmaps/pdfsam.png"
  install -Dm644 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}

sha512sums=('b7446428bbdba7975a624f8fe1d9c1750a366e9342166ecdfb54edb50d491db655257981c2aea76993f7e87c35533819e2f0ac417867dc1bcba4ce7da712642e'
            '89b66081b761fd392550339c3ca04c9af828528355ded8aa4a8c6f94988010a5b44c3c1bfb2042c37113a1c764344ffba9ebf888a528d02ba36cbac29ad1a936'
            '219c3b78b1246103ca6c0b4b50f23a38f994458f54ddfdd37058df33587e258bbe0c3b7f0866dc3d10e657c9425a8b3be915ef6a09a2401e937ac50315653dfb')
