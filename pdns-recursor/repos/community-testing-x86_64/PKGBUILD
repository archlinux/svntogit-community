# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=pdns-recursor
pkgver=3.3
pkgrel=4
pkgdesc='PowerDNS recursor'
url='http://www.powerdns.com'
license=('GPL')
arch=('x86_64' 'i686')
depends=('gcc-libs') # 'lua')
makedepends=('boost')
backup=('etc/powerdns/recursor.conf')
source=("http://downloads.powerdns.com/releases/$pkgname-$pkgver.tar.bz2"
        'pdns-recursor.rc'
	'pdns-recursor.service')
sha256sums=('7b62d97f2bb02bb0ba366dabb5e504338b949a572b9062ec2198012fc3b525bc'
            '7ff45a2da128e82da409f123f8346cc085b57337d6d44a9fd76d17024898b1ca'
            'c5849127cb42b578ff4c38ee6ad39e109bb749ce8b0147ac3619af90803b6875')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Disabled Lua while waiting for upstream support for lua 5.2
  #LUA=1 LUA_CPPFLAGS_CONFIG= LUA_LIBS_CONFIG=-llua make
  LUA=0 make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d -m755 "$pkgdir/etc/rc.d"
  mv "$pkgdir/etc/powerdns/recursor.conf-dist" "$pkgdir/etc/powerdns/recursor.conf"
  install -m755 "$srcdir/pdns-recursor.rc" "$pkgdir/etc/rc.d/pdns-recursor"
  rm -r "$pkgdir/etc/init.d"
  install -Dm644 "$srcdir/pdns-recursor.service" \
    "$pkgdir/usr/lib/systemd/system/pdns-recursor.service"
}

# vim:set ts=2 sw=2 et:
