# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>

pkgname=pdns-recursor
pkgver=3.5.3
pkgrel=1
pkgdesc='PowerDNS recursor'
url='https://www.powerdns.com/'
license=('GPL')
arch=('x86_64' 'i686')
depends=('gcc-libs' 'lua51')
makedepends=('boost')
backup=('etc/powerdns/recursor.conf')
source=("http://downloads.powerdns.com/releases/$pkgname-$pkgver.tar.bz2"
        'pdns-recursor.service')
sha256sums=('192c0b47a1cfcdccaa88d70fd33766a4c381f4223f966416f15e169df5d58eaa'
            '51688d9576736fdf210048098fa96c8710bfbd6a63eb4d69d20bac57b299b5f9')

build() {
  cd "$pkgname-$pkgver"

  make -j1 LUA=1 \
    LUA_CPPFLAGS_CONFIG="$(pkg-config --cflags lua5.1)" \
    LUA_LIBS_CONFIG="$(pkg-config --libs lua5.1)"
}

package() {
  cd "$pkgname-$pkgver"

  make SBINDIR="/usr/bin" DESTDIR="$pkgdir" install
  mv "$pkgdir/etc/powerdns/recursor.conf-dist" \
    "$pkgdir/etc/powerdns/recursor.conf"
  rm -r "$pkgdir/etc/init.d"
  install -Dm644 "$srcdir/pdns-recursor.service" \
    "$pkgdir/usr/lib/systemd/system/pdns-recursor.service"
}

# vim:set ts=2 sw=2 et:
