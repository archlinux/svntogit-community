# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Henrik Nymann Jensen <h@henriknj.dk>

pkgname=pdnsd
pkgver=1.2.9.a
_pkgver=${pkgver/.a/a}
pkgrel=4
pkgdesc="Proxy DNS server with permanent caching"
arch=(i686 x86_64)
url="http://members.home.nl/p.a.rombouts/pdnsd/"
license=('GPL')
depends=('glibc')
source=(http://members.home.nl/p.a.rombouts/pdnsd/releases/pdnsd-$_pkgver-par.tar.gz
	service)
md5sums=('2f3e705d59a0f9308ad9504b24400769'
         '4f86bdbd8495b53341070d5720491996')

build() {
	cd $srcdir/$pkgname-${_pkgver}
	./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6
	make
}

package() {
	cd $srcdir/$pkgname-${_pkgver}
	make DESTDIR=$pkgdir install
	install -Dm0644 $srcdir/service $pkgdir/usr/lib/systemd/system/pdnsd.service
}
