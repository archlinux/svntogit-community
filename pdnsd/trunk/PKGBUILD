# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Henrik Nymann Jensen <h@henriknj.dk>

pkgname=pdnsd
pkgver=1.2.9.a
_pkgver=${pkgver/.a/a}
pkgrel=12
pkgdesc="Proxy DNS server with permanent caching"
arch=(x86_64)
url="http://members.home.nl/p.a.rombouts/pdnsd/"
license=('GPL')
depends=('glibc')
install=${pkgname}.install
source=(http://members.home.nl/p.a.rombouts/pdnsd/releases/pdnsd-$_pkgver-par.tar.gz
        http://members.home.nl/p.a.rombouts/pdnsd/releases/pdnsd-$_pkgver-par.tar.gz.asc
	service)
sha512sums=('4fde296642306aa986ddb7cad0b5991bcd397656d93ef4962706276848c7fa62c664a40198e807d029637f116e8000c237d98b91cbbfec5c08b134d8bb3adf81'
            'SKIP'
            'ae278f8899f90ded9eaf814af620207b8a255149a12a1debe00cf9b60339153c6f81cb5c51b84af944f4d89f2f332481cf281a47dd4651e31e77302b21cb5e77')
validpgpkeys=(21EC7442EE80FB0594E317612C4C1357A484EE22)

build() {
  cd "$srcdir/$pkgname-$_pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 ../service "$pkgdir/usr/lib/systemd/system/pdnsd.service"

  install -dm0755 "$pkgdir/usr/share/doc/pdnsd"
  sed -i 's/"nobody"/"pdnsd"/' "$pkgdir/etc/pdnsd.conf.sample"
  mv "$pkgdir/etc/pdnsd.conf.sample" "$pkgdir/usr/share/doc/pdnsd/pdnsd.conf"

  chown -R 184:184 "$pkgdir/var/cache/pdnsd"
}
