# $Id: PKGBUILD 127560 2015-02-11 13:27:16Z spupykin $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>

pkgname=pear-auth-sasl
_pkgname=Auth_SASL
pkgver=1.0.6
pkgrel=1
pkgdesc="Abstraction of various SASL mechanism responses"
url=http://pear.php.net/package/${_pkgname}
arch=('any')
license=('BSD')
makedepends=('php-pear')
noextract=(${_pkgname}-$pkgver.tgz)
source=(http://download.pear.php.net/package/${_pkgname}-$pkgver.tgz)
md5sums=('75792cb080b66c3ed942c793ce977b09')

package() {
  cd ${srcdir}
  local _PEARDIR="${pkgdir}/usr/share/pear"
  local _PEAROPTS="-D php_dir=${_PEARDIR} -D doc_dir=${_PEARDIR}/doc"
  local _PEAROPTS="${_PEAROPTS} -D test_dir=${_PEARDIR}/test"
  local _PEAROPTS="${_PEAROPTS} -D data_dir=${_PEARDIR}/data"
  pear ${_PEAROPTS} install -O -n ${_pkgname}-${pkgver}.tgz
  rm -r ${_PEARDIR}/{.channels,.depdb*,.filemap,.lock,.registry}
}
