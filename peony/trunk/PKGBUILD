# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=3.1.0
pkgrel=1
pkgdesc="File manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gsettings-qt' 'kwayland' 'kwindowsystem' 'libcanberra' 'libnotify' 'poppler-qt5'
         'qt5-ukui-platformtheme' 'qt5-x11extras' 'udisks2')
makedepends=('qt5-tools')
groups=('ukui')
source=("https://github.com/ukui/peony/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b9760c22730a81a895f8aa54df1b780f46ba3a639ce4ddc919b30a87915467eb6def4babb2d6f05943c57f77cb230ba219f8177c7ec707a08c532c3b39b5cce9')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
