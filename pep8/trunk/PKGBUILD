# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=pep8
pkgname=("$pkgbase" "$pkgbase-python2")
pkgver=1.5.1
pkgrel=2
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/jcrocholl/pep8/"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/$pkgbase/$pkgbase-${pkgver}.tar.gz"
        LICENSE)
sha256sums=('818ac9c3b31b2ab63265fb421727025e48f3b6447260632acee43270f172b450'
            '4a18840b7c132952f3f3d42c355289018ea263cbe75b25db31c9feb1b61e9b40')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}

  sed -i '1s/env python$/python2/' $pkgbase-$pkgver-py2/pep8.py
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  # Not passing tests for 1.5.1
  #python setup.py test

  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  # Not passing tests for 1.5.1
  #python2 setup.py test
}

package_pep8() {
  depends=('python' 'python-setuptools')
  provides=('pep8-python3')
  conflicts=('pep8-python3')
  replaces=('pep8-python3')

  cd $pkgbase-$pkgver
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_pep8-python2() {
  depends=('python2' 'python2-setuptools')
  
  cd $pkgbase-$pkgver-py2
  python2 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir/usr/bin/pep8" "$pkgdir/usr/bin/pep8-python2"

  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
