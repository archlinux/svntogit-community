# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Felix Hanley <felix@seconddrawer.com.au>
# Contributor: SÅ‚awomir Kowalski <suawekk@gmail.com>

pkgname=percona-toolkit
pkgver=3.0.10
pkgrel=2
pkgdesc="Collection of advanced command-line tools to perform a variety of MySQL and system tasks."
url="https://www.percona.com/software/database-tools/percona-toolkit"
arch=('any')
license=('GPL')
depends=('perl-term-readkey')
makedepends=('perl-dbd-mysql' 'perl-dbi')
optdepends=(
    'perl-dbd-mysql: MySQL connections'
    'perl-dbi: MySQL connections'
)
options=('!emptydirs' 'purge')
source=("https://www.percona.com/downloads/$pkgname/$pkgver/source/tarball/$pkgname-$pkgver.tar.gz")
sha256sums=('ee89aa2a3c5a1a98e234a2564859fb95685838bef72cc76548ddfa62843844d6')

build() {
    cd "$srcdir"/$pkgname-$pkgver
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps MODULEBUILDRC=/dev/null
    perl Makefile.PL
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
