# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=0.6.1
pkgrel=11
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('i686' 'x86_64')
url="http://performous.org/"
license=('GPL')
depends=('boost-libs>=1.48' 'imagemagick' 'glew>=1.7.0' 'libxml++' 'portaudio' 'portmidi' 'opencv' 'librsvg')
#depends=('boost-libs>=1.48' 'sdl' 'jack' 'imagemagick' 'ffmpeg' 'glew>=1.7.0' 'libxml++' 'portaudio' 'portmidi' \
#         'opencv' 'librsvg' 'libjpeg' 'libpng' 'cairo')
makedepends=('cmake' 'pkgconfig' 'help2man' 'boost>=1.48')
optdepends=('performous-freesongs: free songs for performous')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/Performous-$pkgver-Source.tar.bz2
        boost-filesystem-v3.patch
        ffmpeg-0.8.patch::"http://performous.git.sourceforge.net/git/gitweb.cgi?p=performous/performous;a=patch;h=18449f6e56451f68b980c8359a4d1dc06f82db1a")
md5sums=('451a759de77984b5a699e91107fe52e2'
         '42a8c825d80b0de16bd5752d2a80e585'
         'ff0ffa681dfaa09c4f42133a65309bf0')

build() {
  cd ${srcdir}/Performous-${pkgver}-Source

  # fix to built against boost 1.46 and later, upstream (git) now support v3
  patch -Np1 -i ../boost-filesystem-v3.patch
  # fix with ffmpeg-0.8
  patch -Np1 -i ../ffmpeg-0.8.patch

  mkdir -p build
  cd build

  # fix config loading with libxml++
  export LDFLAGS=${LDFLAGS/-Wl,--as-needed/}

  cmake -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd ${srcdir}/Performous-${pkgver}-Source/build

  make DESTDIR="$pkgdir" install
}
