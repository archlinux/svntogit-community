# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=0.6.1
pkgrel=15
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('i686' 'x86_64')
url="http://performous.org/"
license=('GPL')
depends=('boost-libs' 'imagemagick' 'glew' 'libxml++' 'portaudio' 'portmidi' 'opencv' 'librsvg')
#depends=('boost-libs>=1.48' 'sdl' 'jack' 'imagemagick' 'ffmpeg' 'glew>=1.7.0' 'libxml++' 'portaudio' 'portmidi' \
#         'opencv' 'librsvg' 'libjpeg' 'libpng' 'cairo')
makedepends=('cmake' 'pkgconfig' 'help2man' 'boost>=1.50')
optdepends=('performous-freesongs: free songs for performous')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/Performous-$pkgver-Source.tar.bz2
        boost-filesystem-v3.patch
	ffmpeg-0.11.patch
	png15.patch
	fix-boost-1.50-xtime.patch)
md5sums=('451a759de77984b5a699e91107fe52e2'
         '42a8c825d80b0de16bd5752d2a80e585'
         '07e52e926595d053155bbfb7168e308f'
         '89157d5e21b0efd09fcbeee299d23c7e')

build() {
  cd ${srcdir}/Performous-${pkgver}-Source

  # fix to built against boost 1.46 and later, upstream (git) now support v3
  patch -Np1 -i ../boost-filesystem-v3.patch
  # fix with ffmpeg-0.11
  patch -Np1 -i ../ffmpeg-0.11.patch
  # fix for libpng 1.5
  patch -Np1 -i ../png15.patch
  # fix glib2.0 building
  # #error "Only <glib.h> can be included directly."
  sed -i -e 's#/gconvert.h#.h#g' game/unicode.cc
  # boost 1.50 fix
  patch -Np1 -i ../fix-boost-1.50-xtime.patch

  mkdir -p build
  cd build

  # fix config loading with libxml++
  export LDFLAGS=${LDFLAGS/-Wl,--as-needed/}

  cmake -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd ${srcdir}/Performous-${pkgver}-Source/build

  make DESTDIR="$pkgdir" install
}
md5sums=('451a759de77984b5a699e91107fe52e2'
         '42a8c825d80b0de16bd5752d2a80e585'
         '07e52e926595d053155bbfb7168e308f'
         '89157d5e21b0efd09fcbeee299d23c7e'
         '6b43ab7f1c3e2cacd3540242634eabb3')
