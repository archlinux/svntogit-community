# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=0.7.0
pkgrel=7
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('i686' 'x86_64')
url="http://performous.org/"
license=('GPL')
depends=('boost-libs' 'imagemagick' 'glew' 'libxml++' 'portaudio' 'portmidi' 'librsvg' 'ffmpeg') # opencv
makedepends=('cmake' 'pkgconfig' 'help2man' 'boost' 'mesa')
optdepends=('performous-freesongs: free songs for performous')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/performous-$pkgver.tar.bz2
        performous-0.7.0-cmake.patch
	fix-noisy-sound-output.patch)
md5sums=('60f3f72b51a630a4aa996484aedb9526'
         '7da8aef3f4dd28e19f95957ae9ac2bb1'
         '820dc6d1a91c23f0ef56e2cd1759afb6')

prepare() {
  cd performous-${pkgver}

  # patch from gentoo
  patch -Np0 -i ../performous-0.7.0-cmake.patch

  # fix FS#33948 (merged upstream)
  patch -Np1 -i ../fix-noisy-sound-output.patch
}

build() {
  cd performous-${pkgver}

  mkdir -p build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd performous-${pkgver}/build

  make DESTDIR="$pkgdir" install
}
