# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-algorithm-diff
epoch=1
pkgver=1.201
pkgrel=3
pkgdesc="Perl/CPAN Module Algorithm::Diff : Diff also Longest Common Subsequence"
arch=("any")
url="https://search.cpan.org/dist/Algorithm-Diff"
license=("GPL" "PerlArtistic")
depends=(perl)
source=("https://cpan.metacpan.org/authors/id/R/RJ/RJBS/Algorithm-Diff-$pkgver.tar.gz")
sha256sums=('0022da5982645d9ef0207f3eb9ef63e70e9713ed2340ed7b3850779b0d842a7d')

build() {
  cd "$srcdir"/Algorithm-Diff-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Algorithm-Diff-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
