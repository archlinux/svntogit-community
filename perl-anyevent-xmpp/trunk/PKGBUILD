# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jakob Nixdorf <flocke@user-helfen-usern.de>

pkgname=perl-anyevent-xmpp
pkgver=0.52
pkgrel=1
pkgdesc='Implementation of the XMPP Protocol'
arch=('any')
depends=('perl>=5.10.0' 'perl-anyevent' 'perlxml' 'perl-digest-sha1'
	 'perl-object-event' 'perl-authen-sasl' 'perl-net-libidn'
	 'perl-xml-writer' 'perl-net-ssleay')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/AnyEvent-XMPP"
options=('!emptydirs')
replaces=('perl-net-xmpp2')
source=(http://search.cpan.org/CPAN/authors/id/E/EL/ELMEX/AnyEvent-XMPP-$pkgver.tar.gz)
md5sums=('8eae83900004fc24aae01f651423c2a5')

build()
{
  cd ${srcdir}/AnyEvent-XMPP-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd ${srcdir}/AnyEvent-XMPP-$pkgver
  install -dm0755 $pkgdir/usr/share/doc/$pkgname
  cp -a samples $pkgdir/usr/share/doc/$pkgname/
  make DESTDIR=${pkgdir} install
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name 'perllocal.pod' -delete
}
