# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=perl-archive-cpio
_cpanname=Archive-Cpio
pkgver=0.10
pkgrel=3
pkgdesc='module for manipulations of cpio archives'
url='https://metacpan.org/pod/Archive::Cpio'
arch=('any')
license=('unknown')
depends=('perl>=5.10.0')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://cpan.metacpan.org/authors/id/P/PI/PIXEL/${_cpanname}-${pkgver}.tar.gz)
sha256sums=('246fb31669764e78336b2191134122e07c44f2d82dc4f37d552ab28f8668bed3')
sha512sums=('c828f6f199de0efd7c7fbe2c3d4c5304df3c714720d91dd9a7d76e2c8d16bab1b92fc93a0b8afe78bd89b043ce7684c31d54b13c4e840f68ee040ca7bff43c3d')

build() {
  cd ${_cpanname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_cpanname}-${pkgver}
  make test
}

package() {
  cd ${_cpanname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 Changes -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
