# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-berkeleydb
pkgver=0.55
pkgrel=2
pkgdesc="Interface to Berkeley DB version 2, 3 or 4"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/BerkeleyDB/"
license=('GPL' 'PerlArtistic')
depends=('perl' 'db')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PM/PMQS/BerkeleyDB-$pkgver.tar.gz)
md5sums=('7aa018597792a2f56e80f31d3fe76fa2')

build() {
  cd  $srcdir/BerkeleyDB-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  _dbver1=`pacman -Q db | cut -d\  -f2 | cut -d- -f1 | cut -f1 -d.`
  _dbver2=`pacman -Q db | cut -d\  -f2 | cut -d- -f1 | cut -f2 -d.`
  depends=('perl' "db>=${_dbver1}.${_dbver2}" "db<${_dbver1}.$(expr $_dbver2 + 1)")

  cd  $srcdir/BerkeleyDB-$pkgver
  make pure_install doc_install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
