# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-cairo-gobject
_perl_module=Cairo-GObject
pkgver=1.004
pkgrel=7
pkgdesc="Integrate Cairo into the Glib type system"
arch=('x86_64')
url="https://metacpan.org/release/${_perl_module}"
license=('LGPL')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
depends=('glib-perl' 'cairo-perl')
source=("https://cpan.metacpan.org/authors/id/X/XA/XAOC/${_perl_module}-${pkgver}.tar.gz")
sha256sums=('3bb9d40e802e51f56f1364abc553758152131803c12d85ba6e14bad6813409d5')
options=('!emptydirs')

build() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
}
