# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi-fast
pkgver=2.13
pkgrel=2
pkgdesc='CGI Interface for Fast CGI'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' 'perl-cgi>=4' 'perl-fcgi>=0.67')
url=https://metacpan.org/release/CGI-Fast
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI-Fast-$pkgver.tar.gz")
md5sums=(1085839daf37d49a538caded0b561b4d)
sha512sums=(58e7010f5cdfa13e8ba4e528dfe2d7bcc4ac33f48482de186ef27ec0a38ac091c9d534761231c7dd947516532c78520259e841533690ffba7b90fb3d0069f53c)
_ddir="CGI-Fast-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
