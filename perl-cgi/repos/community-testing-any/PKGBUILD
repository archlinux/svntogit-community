# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cgi
pkgver=4.43
pkgrel=2
pkgdesc='Handle Common Gateway Interface requests and responses'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' perl-html-parser)
checkdepends=('perl-test-deep>=0.11' perl-test-nowarnings
              'perl-test-warn>=0.3')
url=https://metacpan.org/release/CGI
source=("http://search.cpan.org/CPAN/authors/id/L/LE/LEEJO/CGI-$pkgver.tar.gz")
md5sums=(8c1f2cf11e4410911827ea48d58aaecc)
sha512sums=(620634861c66bbebaa31c03273a9f91151865954310e697afc697ca3274a97ae90235bcdafd829ed63d647ed5256689073de09a5d71a0779b6219ab8045be61d)
_ddir="CGI-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
