# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-cpan-meta-check
pkgver=0.014
pkgrel=4
pkgdesc="Verify requirements in a CPAN::Meta object"
arch=('any')
license=('PerlArtistic' 'GPL')
url='http://search.cpan.org/dist/CPAN-Meta-Check'
depends=('perl')
makedepends=('perl-test-deep')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/L/LE/LEONT/CPAN-Meta-Check-$pkgver.tar.gz")
sha512sums=('a10af09a7ce771ca3983adea8053d141750fd09849609a28e7f973831fd3a3ece55281841aec93c099154bb59135986da4983bf612ae4e956c45dbb3f2770f5e')

build() {
  cd CPAN-Meta-Check-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd CPAN-Meta-Check-$pkgver
  make test
}

package() {
  cd CPAN-Meta-Check-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
