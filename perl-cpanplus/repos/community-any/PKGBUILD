# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cpanplus
pkgver=0.9152
pkgrel=2
pkgdesc='Ameliorated interface to the CPAN'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl perl-log-message perl-archive-extract perl-term-ui perl-log-message-simple perl-module-pluggable perl-object-accessor perl-package-constants)
url=https://metacpan.org/release/CPANPLUS
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPANPLUS-$pkgver.tar.gz")
md5sums=('c1c762c6d1f67d731e4ecc4d30fc5b97')
sha512sums=('4665d00006ffccbdbda5a9228763c35edb51581e7c66058f27bb4f21134e56c50eb54ab5bf6540b109dfb48175b65d57d1838638ac806e01add0b2865e3746cf')
_ddir="CPANPLUS-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
