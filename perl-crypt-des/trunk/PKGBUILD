# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
#Contributor: Pete Morris <pete dot morris at gmail dot com>
pkgname=perl-crypt-des
pkgver=2.07
pkgrel=7
pkgdesc="Perl DES encryption module"
arch=('x86_64')
url="http://search.cpan.org/~dparis/Crypt-DES"
license=("GPL" "PerlArtistic")
depends=('glibc')
source=("https://www.cpan.org/authors/id/D/DP/DPARIS/Crypt-DES-$pkgver.tar.gz")
sha256sums=('2db1ebb5837b4cb20051c0ee5b733b4453e3137df0a92306034c867621edd7e7')

build() {
  cd "$srcdir"/Crypt-DES-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Crypt-DES-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
