# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Kogan <michael dot kogan at gmx dot net>

pkgname=perl-data-random
pkgver=0.12
pkgrel=3
pkgdesc="Data::Random - Perl module to generate random data"
arch=('any')
url="http://search.cpan.org/~barefoot/Data-Random/lib/Data/Random.pm"
license=('GPL' 'PerlArtistic')
depends=('perl-date-calc' 'perl-gd' 'perl-yaml-tiny')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/B/BA/BAREFOOT/Data-Random-${pkgver}.tar.gz")
md5sums=('7d9ff7be567d6c07fe916e2a279dea3e')

build() {
  cd "$srcdir/Data-Random-${pkgver}"
  PERL_USE_UNSAFE_INC=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Data-Random-${pkgver}"
  make test
}

package() {
  cd "$srcdir/Data-Random-${pkgver}"
  make DESTDIR=$pkgdir install
}
