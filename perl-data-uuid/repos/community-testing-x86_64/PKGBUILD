# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=perl-data-uuid
_perl_namespace=Data
_perl_module=UUID
pkgver=1.221
pkgrel=3
pkgdesc="Globally/Universally Unique Identifiers (GUIDs/UUIDs)"
arch=('x86_64')
url="https://metacpan.org/release/${_perl_namespace}-${_perl_module}"
license=('BSD')
depends=('perl')
source=("https://www.cpan.org/modules/by-module/${_perl_namespace}/${_perl_namespace}-${_perl_module}-${pkgver}.tar.gz")
sha256sums=('3cc7b2a3a7b74b45a059e013f7fd878078500ea4b7269036f84556b022078667')
options=('!emptydirs')

build() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  make test
}

package() {
  cd "${_perl_namespace}-${_perl_module}-${pkgver}"
  make pure_install DESTDIR="${pkgdir}"
  # Install license file
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" "LICENSE"
}
