# $Id$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname='perl-datetime-calendar-julian'
_dist=DateTime-Calendar-Julian
pkgver=0.04
pkgrel=4
pkgdesc="Perl module for Dates in the Julian calendar"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-datetime')
makedepends=()
url="https://metacpan.org/release/${_dist}"
source=(http://search.cpan.org/CPAN/authors/id/P/PI/PIJLL/${_dist}-${pkgver}.tar.gz)
sha512sums=('e822d57dabbf76dcca9dc1eb097cef9c15579aeb1782a51eb3dd01d47eda0aef2aa92096df67602297a24c387445f6eb381bb3eedfe33486990844f3c96b4960')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

