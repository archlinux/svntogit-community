# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=perl-datetime-format-iso8601
pkgver=0.16
pkgrel=2
pkgdesc="Parses ISO8601 formats"
arch=('any')
url="https://search.cpan.org/dist/DateTime-Format-ISO8601/"
license=('PerlArtistic')
depends=('perl-datetime-format-builder' 'perl-datetime')
makedepends=()
optdepends=('perl-file-find-rule'
            'perl-test-pod')
options=(!emptydirs)
source=("https://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/DateTime-Format-ISO8601-$pkgver.tar.gz")
sha256sums=('582847f6e029065334a00564f20cd7c28f4e5cd4ec21513d0f692531ed3b56e1')

build() {
  cd "$srcdir"/DateTime-Format-ISO8601-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/DateTime-Format-ISO8601-$pkgver
  make install DESTDIR="${pkgdir}"
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
