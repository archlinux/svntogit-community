# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-datetime
pkgver=0.78
pkgrel=1
pkgdesc="A complete, easy to use date and time object"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DateTime"
license=('GPL' 'PerlArtistic')
depends=('perl-datetime-timezone>=0.59' 'perl-datetime-locale>=0.41'
	 'perl-params-validate>=0.76' 'perl-math-round')
LC_NUMERIC=C
provides=("perl-datetime=`printf %.4f $pkgver`")
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/D/DR/DROLSKY/DateTime-$pkgver.tar.gz)
md5sums=('9d71a13933e3afc55c2c54202d40ae13')

build() {
  cd  $srcdir/DateTime-$pkgver
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  ./Build
}

check() {
  cd  $srcdir/DateTime-$pkgver
#  ./Build test
  true
}

package() {
  cd  $srcdir/DateTime-$pkgver
  ./Build install
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
