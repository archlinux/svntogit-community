# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-dbd-odbc
pkgver=1.60
pkgrel=1
pkgdesc="ODBC Driver for DBI"
arch=('x86_64')
url="https://search.cpan.org/dist/DBD-ODBC/"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'perl' 'unixodbc')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/MJ/MJEVANS/DBD-ODBC-${pkgver}.tar.gz")
sha256sums=('bd0ed6134349fbf7562b0873e85799c56a08c643f11f286ff2126595cf4ced4b')

build() {
  cd  DBD-ODBC-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  DBD-ODBC-${pkgver}
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
