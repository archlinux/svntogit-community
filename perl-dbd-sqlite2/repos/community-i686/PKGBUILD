# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-dbd-sqlite2
pkgver=0.37
pkgrel=2
pkgdesc="Perl/CPAN Module DBD::SQLite2"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/DBD-SQLite2"
license=("GPL" "PerlArtistic")
depends=("perl-dbi")
source=("http://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/DBD-SQLite2-$pkgver.tar.gz")
md5sums=('f05f60d8a54f0024abb3b3e39861ee3d')

build() {
  cd DBD-SQLite2-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd DBD-SQLite2-${pkgver}

  make install DESTDIR=${pkgdir}

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
