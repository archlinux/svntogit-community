# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-dbd-sqlite2
pkgver=0.38
pkgrel=1
pkgdesc="Perl/CPAN Module DBD::SQLite2"
arch=("x86_64")
url="https://search.cpan.org/dist/DBD-SQLite2"
license=("GPL" "PerlArtistic")
depends=("perl-dbi")
source=("https://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/DBD-SQLite2-$pkgver.tar.gz")
sha256sums=('75c2e0cb76e4f9e51a5cdf986be3bf1471d478ad7b215db2f5b7c8582ac17e33')

build() {
  cd DBD-SQLite2-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd DBD-SQLite2-${pkgver}

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
