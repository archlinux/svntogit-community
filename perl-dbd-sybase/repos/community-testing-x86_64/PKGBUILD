# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-dbd-sybase
pkgver=1.16
pkgrel=2
pkgdesc="Sybase Driver for DBI"
arch=('x86_64')
url="http://search.cpan.org/dist/DBD-Sybase/"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi>=1.52' 'perl' 'freetds')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/ME/MEWP/DBD-Sybase-${pkgver}.tar.gz")
sha256sums=('67f427e8b7ffae2acc3cc8984463d878abbb2289bc17db79a294e56c8311d6cc')

build() {
  cd  "$srcdir"/DBD-Sybase-${pkgver}
  export SYBASE=/usr
  echo -e "\n\n\n\n\n\n" | PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/DBD-Sybase-${pkgver}
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
