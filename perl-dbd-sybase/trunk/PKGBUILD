# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-dbd-sybase
pkgver=1.20
pkgrel=1
pkgdesc="Sybase Driver for DBI"
arch=('x86_64')
url="https://search.cpan.org/dist/DBD-Sybase/"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi>=1.52' 'perl' 'freetds')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/ME/MEWP/DBD-Sybase-${pkgver}.tgz")
sha256sums=('6030a82bf8c7544da372b712d80fc4e2567b28146ce640ba66e2de54312b0e5e')

build() {
  cd  "$srcdir"/DBD-Sybase-${pkgver}
  export SYBASE=/usr
  echo -e "\n\n\n\n\n\n" | PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/DBD-Sybase-${pkgver}
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
