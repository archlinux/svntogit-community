# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Alan Young <harleypig@gmail.com>

pkgname=perl-devel-globaldestruction
_pkgname=Devel-GlobalDestruction
pkgver=0.12
pkgrel=1
pkgdesc="Expose the flag which marks global"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-sub-exporter' 'perl-sub-exporter-progressive')
url="http://search.cpan.org/dist/Devel-GlobalDestruction"
source=(http://search.cpan.org/CPAN/authors/id/H/HA/HAARG/$_pkgname-$pkgver.tar.gz)
md5sums=('87382bd2f512b0703dd55da6b2b16ec4')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""  \
      PERL_AUTOINSTALL="--skipdeps"           \
      PERL_MM_OPT="INSTALLDIRS=vendor"        \
      PERL_MB_OPT="--installdirs vendor"      \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/$_pkgname-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir"/$_pkgname-$pkgver

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver

  make DESTDIR="$pkgdir"/ install
}
