# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Ross melin <rdmelin@gmail.com>

pkgname=perl-device-serialport
pkgver=1.04
pkgrel=8
pkgdesc="POSIX clone of Win32::SerialPort"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Device-SerialPort"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/C/CO/COOK/Device-SerialPort-$pkgver.tar.gz)
md5sums=('82c698151f934eb28c65d1838cee7d9e')

build() {
  cd  $srcdir/Device-SerialPort-$pkgver
  eval `perl -V:archname`
  PERL_MM_USE_DEFAULT=1 /usr/bin/perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  $srcdir/Device-SerialPort-$pkgver
  make DESTDIR=$pkgdir install
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
