# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-djabberd
pkgver=0.85
pkgrel=9
pkgdesc="Jabber daemon written on Perl"
arch=('any')
url="https://search.cpan.org/~bradfitz/DJabberd/"
license=("GPL" "PerlArtistic")
depends=("perl-danga-socket" "perl-log-log4perl" "perl-unicode-stringprep"
	 "perl-net-dns" "perl-xml-libxml" "perl-net-ssleay")
source=("https://search.cpan.org/CPAN/authors/id/M/MA/MART/DJabberd-$pkgver.tar.gz")
sha256sums=('61845b2327aa1448abd65320933c3d7e9b41be7cd6a66635c9cb1e2a83bf2af0')

build() {
  cd "$srcdir"/DJabberd-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/DJabberd-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
