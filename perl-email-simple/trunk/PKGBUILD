# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-email-simple
pkgver=2.213
pkgrel=2
pkgdesc="simple parsing of RFC2822 message format and headers "
arch=('any')
url="http://search.cpan.org/dist/Email-Simple"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-email-date-format')
provides=("perl-email-simple-creator=$pkgver")
replaces=("perl-email-simple-creator")
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/R/RJ/RJBS/Email-Simple-$pkgver.tar.gz)
sha256sums=('cdb97a9d647231cc0f7207a31ed6ec5ffde6b8ac6504910dc378bf619ed57cc5')

build() {
  cd  "$srcdir"/Email-Simple-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Email-Simple-$pkgver
  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}

