# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-exception-class
pkgver=1.40
pkgrel=1
pkgdesc='A module that allows you to declare real exception classes in Perl'
arch=('any')
url="https://metacpan.org/release/Exception-Class"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.8.1' 'perl-class-data-inheritable>=0.02' 'perl-devel-stacktrace>=2.00')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Exception-Class-$pkgver.tar.gz)
sha256sums=('d1171f2d03a485e87eb561f34688991c17b939d2689bcda47f3f51b372669a1f')

build() {
  cd "${srcdir}"/Exception-Class-$pkgver

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}"/Exception-Class-$pkgver

  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "${srcdir}"/Exception-Class-$pkgver

  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
