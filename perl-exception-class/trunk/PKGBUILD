# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-exception-class
pkgver=1.39
pkgrel=1
pkgdesc='A module that allows you to declare real exception classes in Perl'
arch=('any')
url="https://metacpan.org/release/Exception-Class"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.8.1' 'perl-class-data-inheritable>=0.02' 'perl-devel-stacktrace>=1.20')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Exception-Class-$pkgver.tar.gz)
sha256sums=('819c756a69a6d1e31f396eca6a508b07b0a522a035b904cd1d432353cc362083')

build() {
  cd "${srcdir}"/Exception-Class-$pkgver

  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}"/Exception-Class-$pkgver

  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"

  find "${pkgdir}" -name .packlist -o -name perllocal.pod -delete
}
