# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Packager: Justin Davis <jrcd83@gmail.com>

pkgname=perl-extutils-makemaker
pkgver=6.62
pkgrel=1
pkgdesc='Create a module Makefile'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.006' 'perl-pathtools' 'perl-podlators'
         'perl-cpan-meta>=2.112150' 'perl-extutils-install>=1.52'
         'perl-extutils-command>=1.16' 'perl-extutils-manifest>=1.58')
makedepends=('perl-data-dumper')
url=https://metacpan.org/release/ExtUtils-MakeMaker
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHWERN/ExtUtils-MakeMaker-${pkgver}.tar.gz")
md5sums=('2ae291030c52999b5672b2a502eab195')

build() {
  cd "${srcdir}/ExtUtils-MakeMaker-${pkgver}"
  sed -i '/my::bundles/d' Makefile.PL
  PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
  PERL_AUTOINSTALL=--skipdeps \
  PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
  PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
  MODULEBUILDRC=/dev/null perl Makefile.PL
  make
}

check() {
  cd "${srcdir}/ExtUtils-MakeMaker-${pkgver}"
  PERL_MM_USE_DEFAULT=1 PERL5LIB="" make test
}

package() {
  cd "${srcdir}/ExtUtils-MakeMaker-${pkgver}"
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
