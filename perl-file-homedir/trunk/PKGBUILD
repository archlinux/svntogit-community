# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-file-homedir
pkgver=1.004
pkgrel=1
pkgdesc="Find your home and other directories on any platform"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/File-HomeDir"
depends=('perl-file-which')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/R/RE/REHSACK/File-HomeDir-$pkgver.tar.gz")
sha512sums=('21671f6badb23fc7e89ef7507266a0b85b9bbf38897b24651f6a84795b5174dd4bfa43251231cbdb1566183e12b253e2504f1fe0f8e195c35d9b5e3d639aa334')

build() {
  cd File-HomeDir-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-HomeDir-$pkgver
  make test
}

package() {
  cd File-HomeDir-$pkgver
  make DESTDIR="$pkgdir" install
}
