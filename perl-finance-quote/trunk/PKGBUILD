# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-finance-quote
pkgver=1.47
pkgrel=2
pkgdesc="Perl/CPAN Module Finance::Quote : Fetch stock prices over the Internet"
arch=(any)
url="http://search.cpan.org/dist/Finance-Quote/"
license=("GPL" "PerlArtistic")
depends=("perl-html-tree" "perl-html-tableextract" "perl-crypt-ssleay"
	 "perl-libwww" "perl-date-calc" "perl-json" "perl-datetime" "perl-cgi")
source=("http://search.cpan.org/CPAN/authors/id/E/EC/ECOCODE/Finance-Quote-$pkgver.tar.gz")
sha256sums=('27af2df1f5eee570a8af577be3850468a869251dd212ae644ac2a1710ac2eb3f')

build() {
  cd "$srcdir"/Finance-Quote-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Finance-Quote-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
