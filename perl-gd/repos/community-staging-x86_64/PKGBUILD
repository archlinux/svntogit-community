# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-gd
pkgver=2.66
pkgrel=3
pkgdesc="Interface to Gd Graphics Library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/GD"
license=('GPL' 'PerlArtistic')
depends=('gd')
makedepends=('perl-extutils-pkgconfig')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/GD-$pkgver.tar.gz)
sha256sums=('ea28343301b82ea862bc58c0c2d1953a074e23cf8d1d09df94c05c52f1c98281')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
    PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
    MODULEBUILDRC=/dev/null

  cd  "$srcdir"/GD-$pkgver
  perl Makefile.PL
  make
}

package() {
  cd  "$srcdir"/GD-$pkgver
  make install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
