# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Phillip Smith <fukawi2@NO-SPAM.gmail.com>
# http://github.com/fukawi2/aur-packages
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: antoniolucas <antoniolucasnobar at gmail dot com>

pkgname=perl-graphviz
pkgver=2.24
pkgrel=4
pkgdesc="GraphViz - Interface to AT&T's GraphViz. Deprecated. See GraphViz2"
arch=('any')
url="http://search.cpan.org/dist/GraphViz"
license=('GPL' 'PerlArtistic')
options=('!emptydirs')
depends=('graphviz' 'perl-file-which' 'perl-ipc-run' 'perl-libwww' 'perl-parse-recdescent'
         'perl-xml-twig' 'perl-xml-xpath')
checkdepends=('perl-test-pod')
source=("https://cpan.metacpan.org/authors/id/R/RS/RSAVAGE/GraphViz-$pkgver.tgz")
md5sums=('b57b32444e73d24cf9c374bf38bad699')

build() {
  cd GraphViz-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd GraphViz-$pkgver
  make test
}

package() {
  cd GraphViz-$pkgver
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
