# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-gssapi
pkgver=0.28
pkgrel=10
pkgdesc="Perl/CPAN Module GSSAPI"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/GSSAPI"
license=("GPL" "PerlArtistic")
depends=('krb5')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/A/AG/AGROLMS/GSSAPI-${pkgver}.tar.gz")
md5sums=('65f00a0749212af064289c8a05e59b3f')

build() {
  cd GSSAPI-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd GSSAPI-${pkgver}

  make install DESTDIR=${pkgdir}

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
