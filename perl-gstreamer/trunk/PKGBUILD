# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=perl-gstreamer
pkgver=0.16
pkgrel=1
pkgdesc="Interface to the GStreamer library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/GStreamer"
license=('GPL' 'PerlArtistic')
depends=('glib-perl>=1.180' 'gstreamer0.10' 'perl-extutils-pkgconfig>=1.07' 'perl-extutils-depends>=0.205')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/T/TS/TSCH/GStreamer-${pkgver}.tar.gz")
md5sums=('e2d51158f3c671c0e4c11a82c9171b2b')

build() {
  cd  GStreamer-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd  GStreamer-${pkgver}

  make install DESTDIR=${pkgdir}

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
