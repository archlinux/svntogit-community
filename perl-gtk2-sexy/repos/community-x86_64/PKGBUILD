# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Charles Mauch <cmauch@gmail.com>
# Creator: Florian Ragwitz <http://search.cpan.org/~/>

pkgname=perl-gtk2-sexy
pkgver=0.05
pkgrel=11
pkgdesc="Perl/CPAN Module Gtk2::Sexy"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Gtk2-Sexy"
license=('GPL' 'PerlArtistic')
depends=('gtk2' 'libsexy')
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends' 'gtk2-perl')
options=(docs !emptydirs)
source=("http://www.cpan.org/authors/id/F/FL/FLORA/Gtk2-Sexy-${pkgver}.tar.gz")
md5sums=('3e291808250d7b956ba8443013a1b461')

build() {
  cd Gtk2-Sexy-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd Gtk2-Sexy-${pkgver}
  make install DESTDIR=${pkgdir}
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
