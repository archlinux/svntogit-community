# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>

pkgname=perl-gtk2-trayicon
pkgver=0.06
pkgrel=17
pkgdesc="Perl interface to the EggTrayIcon library"
arch=('x86_64')
url="https://search.cpan.org/dist/Gtk2-TrayIcon/"
depends=('gtk2-perl')
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
license=("GPL")
options=('!emptydirs')
source=("https://downloads.sourceforge.net/sourceforge/gtk2-perl/Gtk2-TrayIcon-${pkgver}.tar.gz")
md5sums=('522c328f14681a25d76eeaf317e05049')

build() {
  cd  Gtk2-TrayIcon-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  Gtk2-TrayIcon-${pkgver}
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
