# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=perl-gtk3-simplelist
_perl_module=Gtk3-SimpleList
pkgver=0.18
pkgrel=1
pkgdesc="Perl interface to the Gtk3's complex MVC list widget"
arch=('any')
url="https://metacpan.org/release/${_perl_module}"
license=('LGPL')
depends=('perl-gtk3')
source=("https://cpan.metacpan.org/authors/id/T/TV/TVIGNAUD/${_perl_module}-${pkgver}.tar.gz")
sha512sums=('b95cb83730282e666b3ab4847cc1b75e7523a413368ac1c1452a0e7cd1f54949e0372980ce9c4974cc6cfdef845998f0cc6b932fbf74e000ddf86e7190016bcd')
options=('!emptydirs')

build() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
