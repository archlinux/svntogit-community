# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-html-strip
pkgver=2.10
pkgrel=4
pkgdesc="Perl/CPAN Module HTML::Strip"
arch=('x86_64')
url="http://search.cpan.org/~kilinrax/HTML-Strip/"
license=("GPL" "PerlArtistic")
depends=('perl')
source=("https://www.cpan.org/authors/id/K/KI/KILINRAX/HTML-Strip-$pkgver.tar.gz")
sha256sums=('2af30a61f1ecc0bea983043c8078e48380ccb0319388a74483e09aa782f1ccfa')

build() {
  cd "$srcdir"/HTML-Strip-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
package() {
  cd "$srcdir"/HTML-Strip-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
