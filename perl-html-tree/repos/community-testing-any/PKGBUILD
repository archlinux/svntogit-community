# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-html-tree
pkgver=5.06
pkgrel=2
pkgdesc="Make parse trees out of HTML source"
arch=('any')
url="http://search.cpan.org/dist/HTML-Tree"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-html-tagset>=3.02' 'perl-html-parser>=3.46')
makedepends=('perl-test-fatal' 'perl-module-build')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/K/KE/KENTNL/HTML-Tree-$pkgver.tar.gz)
sha256sums=('9c36eb19cbdf9a5906c858948ca51c35bd7561f52cc18c43281acbe57327536e')

build() {
  cd  "$srcdir"/HTML-Tree-$pkgver
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
}

package() {
  cd  "$srcdir"/HTML-Tree-$pkgver
  perl Build install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
