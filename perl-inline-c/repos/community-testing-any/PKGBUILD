# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-inline-c
pkgver=0.81
pkgrel=1
pkgdesc="C Language Support for Inline"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Inline-C"
depends=('perl-inline' 'perl-parse-recdescent' 'perl-pegex')
makedepends=('perl-file-copy-recursive' 'perl-test-warn')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/T/TI/TINITA/Inline-C-$pkgver.tar.gz")
sha512sums=('c3d0d669c26017b4b63cc101377d067438c31fc5d1e6187d01194f70158049e386683b792716aad58772eae839512ecaefd4b3ac7a2377dd1404a651c24b2277')

build() {
  cd Inline-C-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Inline-C-$pkgver
  make test
}

package() {
  cd Inline-C-$pkgver
  make DESTDIR="$pkgdir" install
}
