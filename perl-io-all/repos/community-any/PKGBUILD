# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-io-all
pkgver=0.57
pkgrel=1
pkgdesc="IO::All of it to Graham and Damian!"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.1' 'perl-io-string>=1.08')
url="http://search.cpan.org/dist/IO-All/"
source=("http://search.cpan.org/CPAN/authors/id/F/FR/FREW/IO-All-${pkgver}.tar.gz")
sha1sums=('07cc71d462fba7469f3190df839e9a3c6dd427e3')
sha1sums=('df2d21b3443a41b5915ea09f04192b527ca44cac')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps \
      PERL_MM_OPT="INSTALLDIRS=vendor" \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

  cd "${srcdir}/IO-All-${pkgver}"
  perl Makefile.PL
  make
}

check() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  cd "${srcdir}/IO-All-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/IO-All-${pkgver}"
  make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
