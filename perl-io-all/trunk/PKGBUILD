# Packager: Justin Davis <jrcd83@gmail.com>

pkgname=perl-io-all
pkgver=0.87
pkgrel=1
pkgdesc="IO::All of it to Graham and Damian!"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.1' 'perl-io-string>=1.08')
url="https://search.cpan.org/dist/IO-All/"
source=("https://cpan.metacpan.org/authors/id/F/FR/FREW/IO-All-$pkgver.tar.gz")
sha256sums=('54e21d250c0229127e30b77a3461e10077854ec244f26fb670f1b445ed4c4d5b')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps \
      PERL_MM_OPT="INSTALLDIRS=vendor" \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

  cd "${srcdir}/IO-All-${pkgver}"
  perl Makefile.PL
  make
}

check() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  cd "${srcdir}/IO-All-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/IO-All-${pkgver}"
  make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
