# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer

pkgname=perl-io-dirent
pkgver=0.05
pkgrel=6
pkgdesc='Perl IO::Dirent module - Access to dirent structs returned by readdir'
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url='http://search.cpan.org/~scottw/IO-Dirent/'
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/S/SC/SCOTTW/IO-Dirent-$pkgver.tar.gz")
md5sums=('248ab22c31a310c3482fd99592ae550c')

build() {
  cd IO-Dirent-$pkgver
  USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd IO-Dirent-$pkgver
  make test
}

package() {
  cd IO-Dirent-$pkgver
  make install DESTDIR="$pkgdir/"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

# vim:set ts=2 sw=2 et:
