# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan-Erik Rediger <badboy at archlinux dot us>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-ipc-run
pkgver=0.92
pkgrel=1
pkgdesc="IPC::Run - system() and background procs w/ piping, redirs, ptys"
arch=('any')
url="http://search.cpan.org/dist/IPC-Run"
license=('GPL' 'PerlArtistic')
# IPC::Run depends on IO::Pty which is provided by perl-io-tty
depends=('perl>=5.10.0' 'perl-io-tty')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/IPC-Run-$pkgver.tar.gz")
md5sums=('9d209c8d4bda50bea5f5a09e03db173e')

build() {
  cd $srcdir/IPC-Run-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd $srcdir/IPC-Run-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name perllocal.pod -delete
  find $pkgdir -name .packlist -delete
}
