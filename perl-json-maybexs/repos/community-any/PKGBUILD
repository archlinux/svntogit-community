# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: <michael dot kogan at gmx dot net>
# Contributor: John D Jones III AKA jnbek <jnbek1972 -_AT_- g m a i l -_Dot_- com>

pkgname=perl-json-maybexs
_perl_module=JSON-MaybeXS
pkgver=1.004003
pkgrel=2
pkgdesc="Use Cpanel::JSON::XS with a fallback to JSON::XS and JSON::PP"
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-test-needs')
optdepends=('perl-cpanel-json-xs: Using Cpanel-JSON-XS')
options=('!emptydirs')
url="https://metacpan.org/release/${_perl_module}"
source=("https://cpan.metacpan.org/authors/id/E/ET/ETHER/${_perl_module}-${pkgver}.tar.gz")
sha512sums=('4690be671ea59f82ab24cc5c72d0f4a98a21f40d33c0acac853a2b15eac38933f7af3e1212ee6b5479d5e639246ebb5726c25e837996722fcfef7029dd5ed6fe')

build() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
