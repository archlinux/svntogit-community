# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=perl-json-parse
pkgver=0.61
pkgrel=1
pkgdesc="JSON::Parse - Read JSON into a Perl variable"
arch=('x86_64')
url="https://metacpan.org/release/JSON-Parse"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/B/BK/BKB/JSON-Parse-$pkgver.tar.gz)
sha256sums=('ce8e55e70bef9bcbba2e96af631d10a605900961a22cad977e71aab56c3f2806')

build() {
  cd  "$srcdir"/JSON-Parse-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/JSON-Parse-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
