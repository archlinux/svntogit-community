# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tor Krill <tor@krill.nu>

pkgname=perl-json-xs
pkgver=3.02
pkgrel=3
pkgdesc="JSON::XS - JSON serialising/deserialising, done correctly and fast"
url="http://search.cpan.org/dist/JSON-XS/"
license=("GPL")
arch=('i686' 'x86_64')
depends=('perl' 'perl-common-sense' 'perl-types-serialiser' 'perl-canary-stability')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/JSON-XS-$pkgver.tar.gz)
md5sums=('0d4078f18003b50be970709a77b6afbf')

build() {
  cd  $srcdir/JSON-XS-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
package() {
  cd  $srcdir/JSON-XS-$pkgver
  make DESTDIR=$pkgdir install

  find $pkgdir -name '.packlist' -delete && \
  find $pkgdir -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
