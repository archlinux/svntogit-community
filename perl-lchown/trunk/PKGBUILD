# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-lchown
pkgver=1.01
pkgrel=5
pkgdesc="use the lchown(2) system call from Perl"
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
makedepends=('perl-module-build')
url=https://metacpan.org/release/Lchown
source=("http://search.cpan.org/CPAN/authors/id/N/NC/NCLEATON/Lchown-${pkgver}.tar.gz")
md5sums=('e3db31be650437eb5d9bfc4da6252ee3')

build() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/Lchown-${pkgver}"
    /usr/bin/perl Build.PL
    ./Build
}

check() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/Lchown-${pkgver}"
    ./Build test
}

package() {
    cd "${srcdir}/Lchown-${pkgver}"
    ./Build install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
