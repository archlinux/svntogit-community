# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-local-lib
pkgver=2.000024
pkgrel=1
pkgdesc="Create and use a local lib/ for perl modules with PERL5LIB"
arch=('any')
url="https://search.cpan.org/dist/local-lib/"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/H/HA/HAARG/local-lib-$pkgver.tar.gz")
sha256sums=('2e9b917bd48a0615e42633b2a327494e04610d8f710765b9493d306cead98a05')

build() {
  cd local-lib-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd local-lib-${pkgver}
  make test
}

package() {
  cd local-lib-${pkgver}
  make DESTDIR="$pkgdir" install
}
