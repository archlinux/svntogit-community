# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-log-message-simple
pkgver=0.10
pkgrel=4
pkgdesc='Simplified interface to Log::Message'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl perl-log-message)
url=https://metacpan.org/release/Log-Message-Simple
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/Log-Message-Simple-$pkgver.tar.gz")
md5sums=(0a0f9df6bc1148cc2aae4cb2882c0664)
sha512sums=(7915f19dabae801da08e230d846faa6da8f82dd8ac90ed9aedbab9022c45ef0464c1fb511d5ac074c7a77d59eab2a8b549da686cc79ed9a8c40a8eb9d12fb418)
_ddir="Log-Message-Simple-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
