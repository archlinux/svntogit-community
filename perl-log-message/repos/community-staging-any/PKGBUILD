# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-log-message
pkgver=0.08
pkgrel=4
pkgdesc='Powerful and flexible message logging mechanism'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Log-Message
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/Log-Message-$pkgver.tar.gz")
md5sums=(406fe7d1e37980afa105b663c436c4af)
sha512sums=(7dffaa66bae132a958b08948369761b1871af4fb0f719f1b39f9978131034444ab02db62cb5ca3ee89607640070e410b020e23ebfd4d3d8d9ca8b01ffed2ced8)
_ddir="Log-Message-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
