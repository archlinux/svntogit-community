# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-mail-box-parser-c
pkgver=3.008
pkgrel=4
pkgdesc="Perl/CPAN Module Mail::Box::Parser::C : extends MailBox with the speed of C"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/Mail-Box-Parser-C"
license=("GPL" "PerlArtistic")
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/M/MA/MARKOV/Mail-Box-Parser-C-${pkgver}.tar.gz")
sha256sums=('43fdb60a3e023137529e642bf5a22385fc43d1ab3826277cc7e170e9a104d62c')

build() {
  cd Mail-Box-Parser-C-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd Mail-Box-Parser-C-${pkgver}

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
