# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-mixin-linewise
pkgver=0.003
pkgrel=2
pkgdesc='Perl Mixin::Linewise module - write your linewise code for handles'
arch=('any')
url='http://search.cpan.org/~rjbs/Mixin-Linewise-0.003/'
license=('GPL' 'PerlArtistic')
depends=('perl-io-string' 'perl-sub-exporter')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Mixin-Linewise-$pkgver.tar.gz")
md5sums=('10c1f76b6afce3b7080b2689b6a964d1')

build() {
  cd Mixin-Linewise-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Mixin-Linewise-$pkgver
  make test
}

package() {
  cd Mixin-Linewise-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
