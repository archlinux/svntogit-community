# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-module-pluggable
pkgver=5.2
pkgrel=4
pkgdesc='automatically give your module the ability to have plugins'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='https://metacpan.org/release/Module-Pluggable'
source=("http://search.cpan.org/CPAN/authors/id/S/SI/SIMONW/Module-Pluggable-$pkgver.tar.gz")
md5sums=('87ce2971662efd0b69a81bb4dc9ea76c')
sha512sums=('7df8ee6713c8e0d4df756736c43c2033632cb8887c82ed5b9f38476dbf402b5daa3af83d3b2bd1228afb020ce5855831812f86299b63518e04e0929390b0c5f5')
_ddir="Module-Pluggable-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
