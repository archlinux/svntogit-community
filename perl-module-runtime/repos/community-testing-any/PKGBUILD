# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-module-runtime
pkgver=0.014
pkgrel=4
pkgdesc="runtime module handling"
arch=('any')
url="http://search.cpan.org/dist/Module-Runtime/"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-module-build')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/Z/ZE/ZEFRAM/Module-Runtime-$pkgver.tar.gz")
md5sums=('a6597bc25536a30476f0d75b64d35734')

build() {
  cd "$srcdir"/Module-Runtime-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Module-Runtime-$pkgver
  make install DESTDIR="${pkgdir}"
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
