# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname=perl-module-scandeps
_pkgname=Module-ScanDeps
pkgver=1.31
pkgrel=1
pkgdesc="Scan file prerequisites"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-requires')
url="https://search.cpan.org/dist/Module-ScanDeps"
source=("https://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/Module-ScanDeps-$pkgver.tar.gz")
sha512sums=('c7164d768b6401a0d01516f5a09ddf2a0399355738ff17d04757aeb235b47f8d973083d0425ca53757588916c7fb2959d8c042361055a0e8eab7bf6d4c31d6fd')

build() {
  cd Module-ScanDeps-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Module-ScanDeps-$pkgver
  make test
}

package() {
  cd Module-ScanDeps-$pkgver
  make DESTDIR="$pkgdir" install
}
