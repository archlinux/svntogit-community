# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=MooX-HandlesVia
pkgname=perl-${_pkg,,}
pkgver=0.001008
pkgrel=6
pkgdesc="NativeTrait-like behavior for Moo"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers' 'perl-data-perl' 'perl-module-runtime' 'perl-moo' 'perl-role-tiny')
makedepends=('perl-moox-types-mooselike')
checkdepends=('perl-test-exception' 'perl-test-fatal' 'perl-test-simple')
source=("https://cpan.metacpan.org/authors/id/M/MA/MATTP/${_pkg}-${pkgver}.tar.gz")
sha512sums=('ad5dd6c5a05d6f44788e31a74d595c3a6c1b8bf62f51d59bab2d65f6963c2995205ad64f6930b103026d3497d3d2a83bd192b7566228c31bf944265e060703c0')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
