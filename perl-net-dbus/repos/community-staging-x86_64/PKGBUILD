# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-net-dbus
pkgver=1.2.0
pkgrel=4
pkgdesc="Binding for DBus messaging protocol"
arch=('x86_64')
url="https://search.cpan.org/dist/Net-DBus"
license=('GPL' 'PerlArtistic')
depends=('dbus' 'perl-xml-twig')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/D/DA/DANBERR/Net-DBus-${pkgver}.tar.gz")
sha256sums=('e7a1ac9ef4a1235b3fdbd5888f86c347182306467bd79abc9b0756a64b441cbc')

build() {
  cd  Net-DBus-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  Net-DBus-${pkgver}
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
