# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: John D Jones III <jnbek1972 -_AT_- g m a i l -_Dot_- com>
# Generator  : CPANPLUS::Dist::Arch 1.28

pkgname=perl-net-dns-sec
pkgver=1.06
pkgrel=1
pkgdesc="DNSSEC extensions to Net::DNS"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
depends=('perl-crypt-openssl-bignum' 'perl-crypt-openssl-dsa' 'perl-crypt-openssl-rsa' 'perl-digest-bubblebabble' 'perl-mime-base32' 'perl-net-dns' 'perl')
checkdepends=('perl-test-pod')
url='http://search.mcpan.org/dist/Net-DNS-SEC'
options=('!emptydirs')
source=("http://search.mcpan.org/CPAN/authors/id/N/NL/NLNETLABS/Net-DNS-SEC-$pkgver.tar.gz")
sha512sums=('7fa7c902149c1ed4693f8b0a274aff9e636b0143512d9469ab87633504ba16ef1519473e241da240bfd0faeb789547f404f59fda848cc929f199303c64606c76')
_distdir="Net-DNS-SEC-$pkgver"

build() {
  cd "$srcdir/$_distdir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_distdir"
  make test
}

package() {
  cd "$srcdir/$_distdir"
  make DESTDIR="$pkgdir/" install

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
        _perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
        _perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
        depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

# vim:set ts=2 sw=2 et:
