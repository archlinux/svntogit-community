# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=perl-net-idn-encode
_cpanname=Net-IDN-Encode
pkgver=2.500
pkgrel=5
pkgdesc="Net::IDN::Encode provides an easy-to-use interface for encoding and decoding Internationalized Domain Names (IDNs)."
arch=('any')
url="https://search.cpan.org/~cfaerber/${_cpanname}/"
license=('GPL')
depends=('perl')
checkdepends=('perl-test-nowarnings' 'perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/C/CF/CFAERBER/${_cpanname}-${pkgver}.tar.gz)
sha512sums=('3537309ea391e4f762eb8ce43929bfa024d63ee59c94f8d2ea16179aa6ad66ecef29188551749593302f14dc5b3182dbce18ebaf5176a34b40d6fa5b7e5763fb')

build() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 sts=2 et:
