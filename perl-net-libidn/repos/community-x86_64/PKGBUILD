# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-net-libidn
pkgver=0.12
pkgrel=10
pkgdesc="Perl/CPAN Module Net::LibIDN"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/Net-LibIDN/"
license=("GPL" "PerlArtistic")
depends=(perl libidn)
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TH/THOR/Net-LibIDN-${pkgver}.tar.gz")
md5sums=('c3e4de2065009d67bcb1df0afb473e12')

build() {
  cd Net-LibIDN-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd Net-LibIDN-${pkgver}

  make install DESTDIR=${pkgdir}

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
