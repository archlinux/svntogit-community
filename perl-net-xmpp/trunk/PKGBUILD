# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-net-xmpp
pkgver=1.05
pkgrel=8
pkgdesc="Perl/CPAN Module Net::XMPP"
arch=("any")
url="https://search.cpan.org/dist/Net-XMPP"
license=("GPL" "PerlArtistic")
makedepends=("perl-digest-sha1" "perl-xml-stream" "perl-module-build")
depends=("perl-digest-sha1" "perl-xml-stream" "perl-digest-hmac")
source=("https://search.cpan.org/CPAN/authors/id/D/DA/DAPATRICK/Net-XMPP-$pkgver.tar.gz")
sha256sums=('4c02200577c2b235a163a09bfaa152bf000fe5f1499ad80ce16ab66808949362')

build() {
  cd "$srcdir"/Net-XMPP-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Net-XMPP-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
