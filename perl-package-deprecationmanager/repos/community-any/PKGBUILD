# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-package-deprecationmanager
pkgver=0.14
pkgrel=1
pkgdesc="Manage deprecation warnings for your distribution"
arch=('any')
license=('Artistic2.0')
options=('!emptydirs')
depends=('perl-list-moreutils' 'perl-params-util' 'perl-sub-install')
checkdepends=('perl-test-fatal' 'perl-test-requires')
url="https://metacpan.org/release/Package-DeprecationManager"
source=(http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Package-DeprecationManager-$pkgver.tar.gz)
md5sums=('e15e14a4e3f914eff937a55549692f51')

build() {
  cd "$srcdir/Package-DeprecationManager-$pkgver"
  perl Makefile.PL
  make
}

check() {
  cd "$srcdir/Package-DeprecationManager-$pkgver"
  make test
}

package() {
  cd "$srcdir/Package-DeprecationManager-$pkgver"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
