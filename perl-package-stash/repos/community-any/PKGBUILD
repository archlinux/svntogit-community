# Maintainer: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-package-stash
pkgver=0.33
pkgrel=1
pkgdesc="routines for manipulating stashes"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' 'perl-dist-checkconflicts>=0.02'
         'perl-package-deprecationmanager' 'perl-scalar-list-utils')
makedepends=('perl-extutils-makemaker>=6.30')
checkdepends=('perl-test-fatal' 'perl-test-requires' 'perl-test-simple')
url=https://metacpan.org/release/Package-Stash
source=("http://search.cpan.org/CPAN/authors/id/D/DO/DOY/Package-Stash-${pkgver}.tar.gz")
md5sums=(fe1f6efa0dbd46f8520903b436f185f1)
sha512sums=(8b3e62caa7ac99f1429d4ab4c3a00265a8ca2aa6281c5c254c4fb455d8a262c445a1daafa405208f902fd352e18ef121a37303ceb27caabb0bdc3c14b807508b)
_distdir="${srcdir}/Package-Stash-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
