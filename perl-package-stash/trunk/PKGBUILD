# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-package-stash
pkgver=0.36
pkgrel=1
pkgdesc="Routines for manipulating stashes"
arch=('any')
url="http://search.cpan.org/dist/Package-Stash"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-dist-checkconflicts' 'perl-package-stash-xs'
         'perl-package-deprecationmanager' 'perl-scalar-list-utils')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-fatal' 'perl-test-requires' 'perl-test-simple'
              'perl-module-implementation')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/D/DO/DOY/Package-Stash-$pkgver.tar.gz)
md5sums=('066aa5099af88457c4f1eb2448272061')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""  \
      PERL_AUTOINSTALL="--skipdeps"           \
      PERL_MM_OPT="INSTALLDIRS=vendor"        \
      PERL_MB_OPT="--installdirs vendor"      \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/Package-Stash-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$srcdir"/Package-Stash-$pkgver

    make test
  )
}

package() {
    cd "$srcdir"/Package-Stash-$pkgver

    make DESTDIR="$pkgdir"/ install
}
