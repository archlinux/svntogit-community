# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-par-dist
pkgver=0.50
pkgrel=1
pkgdesc="Perl bindings to create and manipulate PAR distributions"
arch=('any')
url="https://search.cpan.org/dist/PAR-Dist/"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/PAR-Dist-$pkgver.tar.gz")
sha512sums=('07048cbda958812c792d202ddde8624a1b3b14db42feb5ac90535439f288873599ef6e59bef8b2812263a3f7a354b4912982c2e053b91355add573549eae9382')

build() {
  cd "$srcdir/PAR-Dist-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/PAR-Dist-$pkgver"
  make test
}

package() {
  cd "$srcdir/PAR-Dist-$pkgver"
  make install DESTDIR="$pkgdir"
}
