# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Amish <contact at via dot aur>
# Contributor: Justin Davis <jrcd83@gmail.com>
# Contributor: Tony Sokhon <tonyskn@gmail.com>

pkgname=perl-par-packer
pkgver=1.052
pkgrel=2
pkgdesc='PAR Packager'
arch=('x86_64')
url='https://metacpan.org/release/PAR-Packer'
license=('GPL')
# This package links agains libperl.so
depends=('perl=5.32.1' 'perl-archive-zip' 'perl-getopt-argvfile' 'perl-ipc-run3' 'perl-module-scandeps'
          'perl-par' 'perl-par-dist')
makedepends=('perl-extutils-makemaker')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/R/RS/RSCHUPP/PAR-Packer-${pkgver}.tar.gz")
b2sums=('cc8b30b3dd2b5c239574750442212b9d2d4a6ef35c8d93aca091e463cf8810feb051417ed4024f5aed011d468d98ee10b511fd70b5a94072995353a72481b923')

build() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd PAR-Packer-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  #remove conflicting and empty man page
  rm "${pkgdir}"/usr/share/man/man1/pp.1
}
