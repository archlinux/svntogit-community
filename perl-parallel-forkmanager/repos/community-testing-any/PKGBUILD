# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-parallel-forkmanager
pkgver=2.02
pkgrel=4
pkgdesc="A simple parallel processing fork manager"
arch=('any')
url="https://metacpan.org/release/Parallel-ForkManager"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-moo')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-warn')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/Y/YA/YANICK/Parallel-ForkManager-$pkgver.tar.gz)
sha256sums=('c1b2970a8bb666c3de7caac4a8f4dbcc043ab819bbc337692ec7bf27adae4404')

build() {
  cd Parallel-ForkManager-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}
check() {
  cd Parallel-ForkManager-$pkgver
  make test
}
package() {
  cd Parallel-ForkManager-$pkgver
  make install DESTDIR="$pkgdir"
}
