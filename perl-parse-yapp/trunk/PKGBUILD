# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-parse-yapp
pkgver=1.21
pkgrel=1
pkgdesc="Perl/CPAN Module Parse::Yapp : Generates OO LALR parser modules"
arch=("any")
url="https://search.cpan.org/dist/Parse-Yapp"
license=("GPL" "PerlArtistic")
depends=('perl')
source=("https://cpan.metacpan.org/authors/id/W/WB/WBRASWELL/Parse-Yapp-$pkgver.tar.gz")
sha256sums=('3810e998308fba2e0f4f26043035032b027ce51ce5c8a52a8b8e340ca65f13e5')

build() {
  cd "$srcdir"/Parse-Yapp-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Parse-Yapp-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
