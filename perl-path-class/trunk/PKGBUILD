# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-path-class
pkgver=0.26
pkgrel=1
pkgdesc="Cross-platform path specification manipulation"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
url=https://metacpan.org/release/Path-Class
source=("http://search.cpan.org/CPAN/authors/id/K/KW/KWILLIAMS/Path-Class-${pkgver}.tar.gz")
md5sums=('ec77aaa16cff0c29143c578f30d420cd')
_distdir="${srcdir}/Path-Class-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Build.PL
    ./Build
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    ./Build test
)

package() {
    cd "$_distdir"
    ./Build install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
