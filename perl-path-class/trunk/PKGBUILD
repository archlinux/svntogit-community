# Packager: Justin "juster" Davis <jrcd83@gmail.com>

pkgname=perl-path-class
pkgver=0.37
pkgrel=8
pkgdesc="Cross-platform path specification manipulation"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
makedepends=('perl-module-build')
url="https://search.cpan.org/dist/Path-Class"
source=("https://search.cpan.org/CPAN/authors/id/K/KW/KWILLIAMS/Path-Class-${pkgver}.tar.gz")
sha256sums=('654781948602386f2cb2e4473a739f17dc6953d92aabc2498a4ca2561bc248ce')

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd Path-Class-${pkgver}
    /usr/bin/perl Build.PL
    ./Build
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Path-Class-${pkgver}
    ./Build test
)

package() {
    export PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" 

    cd Path-Class-${pkgver}
    ./Build install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
