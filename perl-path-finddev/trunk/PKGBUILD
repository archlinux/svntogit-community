# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-path-finddev
pkgver=0.5.3
pkgrel=5
pkgdesc="Find a development path somewhere in an upper hierarchy"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Path-FindDev"
depends=('perl-class-tiny' 'perl-path-isdev' 'perl-path-tiny' 'perl-sub-exporter')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/K/KE/KENTNL/Path-FindDev-v$pkgver.tar.gz")
sha512sums=('dd7a6a05f9e21c45a5ef25b3e2598a8f769a2ab3ef24ddb8e918b17a0f025bc5ba5b23fecf832911b7003de6cdaceb0e4a8c1139b4e23b080920739604e37bf6')

build() {
  cd Path-FindDev-v$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Path-FindDev-v$pkgver
  make test
}

package() {
  cd Path-FindDev-v$pkgver
  make DESTDIR="$pkgdir" install
}
