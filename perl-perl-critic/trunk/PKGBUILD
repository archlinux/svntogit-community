# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-perl-critic
pkgver=1.136
pkgrel=1
pkgdesc="Critique Perl source code for best-practices"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Perl-Critic"
depends=('perl-b-keywords' 'perl-config-tiny' 'perl-exception-class' 'perl-file-homedir'
         'perl-file-which' 'perl-io-string' 'perl-list-moreutils' 'perl-module-build'
         'perl-module-pluggable' 'perl-ppi' 'perl-ppix-quotelike' 'perl-ppix-regexp'
         'perl-ppix-utilities' 'perl-pod-spell' 'perl-tidy' 'perl-readonly' 'perl-string-format'
         'perl-task-weaken')
makedepends=('perl-test-deep')
checkdepends=('perl-test-memory-cycle')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PE/PETDANCE/Perl-Critic-$pkgver.tar.gz")
sha512sums=('91664725ad6698203b73bcd678a540b57654d7411e3bfa467221216eaebaddd38d518dc1652137b012655d030f83cb48478b4eef3789422578773ce784ba3934')

build() {
  cd Perl-Critic-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Perl-Critic-$pkgver
  perl Build test
}

package() {
  cd Perl-Critic-$pkgver
  perl Build install destdir="$pkgdir"
}
