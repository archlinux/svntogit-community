# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-perl-minimumversion
pkgver=1.38
pkgrel=2
pkgdesc="Find a minimum required version of perl for Perl code"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Perl-MinimumVersion"
depends=('perl-file-find-rule' 'perl-file-find-rule-perl' 'perl-ppi' 'perl-ppix-regexp'
         'perl-params-util' 'perl-perl-critic')
makedepends=('perl-test-script')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/N/NE/NEILB/Perl-MinimumVersion-$pkgver.tar.gz")
sha512sums=('4a1b5a30c1b2c82ebdd5908b667d012376372890278fb3f1a08ead300b8ce02396f511df5250123689611ad5c0b0ad2b41a4c1ebe76c33109f2226eef95c9d4f')

build() {
  cd Perl-MinimumVersion-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Perl-MinimumVersion-$pkgver
  make test
}

package() {
  cd Perl-MinimumVersion-$pkgver
  make DESTDIR="$pkgdir" install
}
