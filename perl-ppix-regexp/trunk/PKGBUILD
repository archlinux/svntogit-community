# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-regexp
pkgver=0.064
pkgrel=1
pkgdesc="Parse regular expressions"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-Regexp"
depends=('perl-list-moreutils' 'perl-ppi' 'perl-task-weaken')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-Regexp-$pkgver.tar.gz")
sha512sums=('5b484957759c55b4960809b9cd95b66f8ca415a627ee7282649067e839b00944703046a7578d668f488e998c45ee087e042bdc9c4a5597a0ae481d8edee969b2')

build() {
  cd PPIx-Regexp-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-Regexp-$pkgver
  make test
}

package() {
  cd PPIx-Regexp-$pkgver
  make DESTDIR="$pkgdir" install
}
