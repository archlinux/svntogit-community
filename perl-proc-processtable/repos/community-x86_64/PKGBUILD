# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-proc-processtable
pkgver=0.56
pkgrel=1
pkgdesc="Provides a consistent interface to process table information."
arch=('x86_64')
url="http://search.cpan.org/dist/Proc-ProcessTable/"
license=('PerlArtistic')
depends=('perl')
provides=('perl-proc-killall' 'perl-proc-killfam' 'perl-proc-processtable-process')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/J/JW/JWB/Proc-ProcessTable-$pkgver.tar.gz")
sha512sums=('70a5f0b27e99ed45434664340068fef028cbf3c93242d7208f82f4608d030198ba65593443508e31a0637ddd8c20f3ecba80c25dabd903c5b3b04c3833db9a2d')

build() {
  cd "$srcdir/Proc-ProcessTable-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Proc-ProcessTable-$pkgver"
  make test
}

package() {
  cd "$srcdir/Proc-ProcessTable-$pkgver"

  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
