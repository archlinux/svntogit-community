# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: yaroslav <proninyaroslav@mail.ru>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

_dist=Regexp-Common
pkgname=perl-regexp-common
pkgver=2016010801
pkgrel=5
pkgdesc="Commonly requested regular expressions"
arch=('any')
url="http://search.cpan.org/dist/$_dist"
license=('PerlArtistic' 'Artistic2.0' 'BSD' 'MIT')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/A/AB/ABIGAIL/$_dist-$pkgver.tar.gz)
sha256sums=('65558ad12b5f6250c483b5980e8533048c46912d6d69c76150f8f95a5b8eb1c4')

build() {
  cd $_dist-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd $_dist-$pkgver
  make test
}

package() {
  cd $_dist-$pkgver
  make DESTDIR="$pkgdir" install

  # We have a sale on licenses; grab yours today!
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYRIGHT*
}

# vim:set ts=2 sw=2 et:
