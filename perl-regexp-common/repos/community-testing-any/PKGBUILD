# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: yaroslav <proninyaroslav@mail.ru>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-regexp-common
pkgver=2017060201
pkgrel=5
pkgdesc="Commonly requested regular expressions"
arch=('any')
url="https://metacpan.org/release/Regexp-Common"
license=('PerlArtistic' 'Artistic2.0' 'BSD' 'MIT')
depends=('perl')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/A/AB/ABIGAIL/Regexp-Common-$pkgver.tar.gz)
sha256sums=('ee07853aee06f310e040b6bf1a0199a18d81896d3219b9b35c9630d0eb69089b')

build() {
  cd Regexp-Common-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Regexp-Common-$pkgver
  make test
}

package() {
  cd Regexp-Common-$pkgver
  make DESTDIR="$pkgdir" install

  # We have a sale on licenses; grab yours today!
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYRIGHT*
}

# vim:set ts=2 sw=2 et:
