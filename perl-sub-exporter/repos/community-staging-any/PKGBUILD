# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-sub-exporter
pkgver=0.988
pkgrel=2
pkgdesc="A sophisticated exporter for custom-built routines"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-data-optlist' 'perl-params-util' 'perl-sub-install')
url="https://metacpan.org/release/Sub-Exporter"
source=(https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Sub-Exporter-$pkgver.tar.gz)
sha512sums=('e992739097eca3aab0465e33ca60d91e4b08ee40d7cd041be4452873790e438bb1fb293331f919ae1e9b879fc063d17586218a9e97838cf61216172d6bdba273')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL="--skipdeps"            \
      PERL_MM_OPT="INSTALLDIRS=vendor"         \
      PERL_MB_OPT="--installdirs vendor"       \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/Sub-Exporter-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""

    cd "$srcdir"/Sub-Exporter-$pkgver

    make test
  )
}

package() {
  cd "$srcdir"/Sub-Exporter-$pkgver

  make DESTDIR="$pkgdir"/ install
}
