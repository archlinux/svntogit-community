# Maintainer: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-sub-exporter
pkgver=0.984
pkgrel=1
pkgdesc="a sophisticated exporter for custom-built routines"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.0' 'perl-data-optlist>=0.1' 'perl-params-util>=0.14'
         'perl-sub-install>=0.92')
url=https://metacpan.org/release/Sub-Exporter
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Sub-Exporter-${pkgver}.tar.gz")
md5sums=('cfa19c5f09bc08f49174856c0bfd7849')
_distdir="${srcdir}/Sub-Exporter-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
