pkgname=perl-sub-handlesvia
_cpanname=Sub-HandlesVia
pkgver=0.016
pkgrel=3
pkgdesc='Perl extension for easily overriding subroutines'
url='https://metacpan.org/release/Sub-HandlesVia'
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-moo' 'perl-mouse' 'perl-exporter-tiny' 'perl-type-tiny' 'perl-class-tiny')
checkdepends=('perl-test-requires' 'perl-test-fatal')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/T/TO/TOBYINK/${_cpanname}-${pkgver}.tar.gz)
sha512sums=('91932d7c56e4fc1bb9e737282ee45ffdcc6d4143ee0307cbcbee9fb3b3953bd955b62bc20809cef61d4e643a4d246dbab062a636a46f951a93bdf2f22a1b04d2')

build() {
  cd ${_cpanname}-${pkgver}
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_cpanname}-${pkgver}
  make test
}

package() {
  cd ${_cpanname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
