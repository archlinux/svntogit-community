# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org> 
# Contributor: John D Jones III <jnbek1972 -_AT_- g m a i l -_Dot_- com>

pkgname=perl-sub-identify
pkgver=0.12
pkgrel=2
pkgdesc="Retrieve names of code references"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-test-pod')
url='http://search.mcpan.org/dist/Sub-Identify'
source=("http://search.mcpan.org/CPAN/authors/id/R/RG/RGARCIA/Sub-Identify-$pkgver.tar.gz")
sha512sums=('47b89866ecdd2b7caaba4a440bd695ab42d92c56721bc43a7fc114c2870a5e536ed17e05eec4ac62525c9fad26e24c299aadc852fd15d58bd626dfdb6478e40f')

build() {
  cd "$srcdir/Sub-Identify-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Sub-Identify-$pkgver"
  make test
}

package() {
  cd "$srcdir/Sub-Identify-$pkgver"
  make DESTDIR="$pkgdir"/ install
}

# vim:set ts=2 sw=2 et:
