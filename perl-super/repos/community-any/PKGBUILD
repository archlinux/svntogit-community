# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org> 
# Contributor: John D Jones III <jnbek1972 -_AT_- g m a i l -_Dot_- com>

pkgname=perl-super
pkgver=1.20141117
pkgrel=4
pkgdesc="Control superclass method dispatch"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-sub-identify')
url='http://search.cpan.org/dist/SUPER'
source=("http://search.cpan.org/CPAN/authors/id/C/CH/CHROMATIC/SUPER-$pkgver.tar.gz")
sha512sums=('c17fe478fcb9ebc2749c27fc75b839bd77e81c9d0170697b2a697dcebb9f4eef4b954d023b76d33a8b4201b110e5e997b1e0993faf7a04ef2e1191a2f7c516a0')

build() {
  cd "$srcdir/SUPER-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/SUPER-$pkgver"
  make test
}

package() {
  cd "$srcdir/SUPER-$pkgver"
  make DESTDIR="$pkgdir"/ install
}

# vim:set ts=2 sw=2 et:
