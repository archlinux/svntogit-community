# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-svn-mirror
pkgver=0.75
pkgrel=6
pkgdesc="Perl/CPAN Module SVN::Mirror"
arch=("any")
url="http://search.cpan.org/dist/SVN-Mirror"
license=("GPL" "PerlArtistic")
makedepends=("perl-svn-simple-edit" "perl-uri" "perl-class-accessor" "perl-file-chdir"
	     "perl-timedate" "perl-term-readkey")
depends=("subversion" "perl-svn-simple-edit" "perl-uri" "perl-class-accessor" "perl-file-chdir"
	 "perl-timedate" "perl-term-readkey")
source=("https://www.cpan.org/authors/id/C/CL/CLKAO/SVN-Mirror-$pkgver.tar.gz")
sha256sums=('3f48d92f4210a4b7c1984f25f5d7b7f594ccc0b09c907aa3fa693fc6dd5e0cb5')

build() {
  cd "$srcdir"/SVN-Mirror-$pkgver
  PERL_USE_UNSAFE_INC=1 PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/SVN-Mirror-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
