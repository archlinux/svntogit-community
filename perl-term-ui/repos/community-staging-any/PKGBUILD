# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-term-ui
pkgver=0.46
pkgrel=4
pkgdesc='User interfaces via Term::ReadLine made easy'
arch=('any')
license=('PerlArtistic' 'GPL')
options=("!emptydirs")
depends=('perl-log-message' 'perl-log-message-simple')
url='https://metacpan.org/release/Term-UI'
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/Term-UI-$pkgver.tar.gz")
sha512sums=('7788d651e29e9c7b9c0c2b2074b39f143216a321a5a71c9c4e52fe32e44a246faade8cfdf109bcf7cfe1bac5d9d97fc920c6344a5692e8508361f1c780c41054')
_ddir="Term-UI-$pkgver"

build() (
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package() (
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  #find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
