# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-mockmodule
pkgver=0.15
pkgrel=2
pkgdesc="Override subroutines in a module for unit testing"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-super')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage' 'perl-test-warnings')
url='http://search.cpan.org/dist/Test-MockModule'
source=("http://search.cpan.org/CPAN/authors/id/G/GF/GFRANKS/Test-MockModule-$pkgver.tar.gz")
sha512sums=('20f64f9984010ee0b3c26532a8835156b7cc8837e436c0785a2d3b7959cb01f07584b816d754da9f9205e457fe5d4a8316d984bf215d242000704fe0c52284a7')

build() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build install destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
