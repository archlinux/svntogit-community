# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-more-utf8
pkgver=0.05
pkgrel=2
pkgdesc="Enhancing Test::More for UTF8-based projects"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-module-install')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
url='http://search.cpan.org/dist/Test-More-UTF8'
source=("http://search.cpan.org/CPAN/authors/id/M/MO/MONS/Test-More-UTF8-$pkgver.tar.gz")
sha512sums=('9a1ba4216037aa1517ef57b2e5a40bd78f2f3aaaff3683a4e45f10d4b574b5e2752509a5620d8a76156135b4fa7182320a182a5a6900bf450979d805d7dc05a3')

build() {
  cd "$srcdir/Test-More-UTF8-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Test-More-UTF8-$pkgver"
  make test
}

package() {
  cd "$srcdir/Test-More-UTF8-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
