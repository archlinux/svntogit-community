# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname='perl-test-requires'
pkgver=0.08
pkgrel=1
pkgdesc="Checks to see if the module can be loaded"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/Test-Requires'
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TOKUHIROM/Test-Requires-$pkgver.tar.gz")
md5sums=('339ec20fbbeaa904a968bdee629688a6')

build() {
  cd "$srcdir"/Test-Requires-$pkgver

  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL="--skipdeps"                          \
    PERL_MM_OPT="INSTALLDIRS=vendor" DESTDIR='$pkgdir'     \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  perl Makefile.PL
  make
}

check() {
  cd "$srcdir"/Test-Requires-$pkgver

  make test
}

package() {
  cd "$srcdir"/Test-Requires-$pkgver

  make DESTDIR="$pkgdir"/ install
}
