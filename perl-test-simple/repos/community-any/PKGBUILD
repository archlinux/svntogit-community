# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-simple
pkgver=1.302175
pkgrel=1
pkgdesc="Basic utilities for writing tests"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test-Simple"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test-Simple-$pkgver.tar.gz")
sha512sums=('526b876f7e8b783e1f5c4b16a40d1a1392f4c43c782106117bfebecf3d7eddc0b98c6168b6baae7da48d1df15079ca9c35787310f0785a7ba7ce806b2fb177a7')

build() {
  cd Test-Simple-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Simple-$pkgver
  make test
}

package() {
  cd Test-Simple-$pkgver
  make DESTDIR="$pkgdir" install
}
