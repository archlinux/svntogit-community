# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-test-spec
pkgver=0.54
pkgrel=2
pkgdesc='Write tests in a declarative specification style'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-package-stash>=0.23' 'perl-test-deep>=0.103' perl-test-trap
         perl-tie-ixhash)
checkdepends=('perl-module-implementation' 'perl-try-tiny')
url="http://search.cpan.org/dist/Test::Spec/"
source=("http://search.cpan.org/CPAN/authors/id/A/AK/AKZHAN/Test-Spec-$pkgver.tar.gz")
sha256sums=('0a31cf1265dcee90bbc42456ad60bb363afc7fac6697fffb0fd49bba92a165d2')

build()
(
  cd Test-Spec-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd Test-Spec-$pkgver
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd Test-Spec-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
