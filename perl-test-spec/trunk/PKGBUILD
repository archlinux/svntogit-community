# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-test-spec
pkgver=0.46
pkgrel=1
pkgdesc='Write tests in a declarative specification style'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-package-stash>=0.23' 'perl-test-deep>=0.103' perl-test-trap
         perl-tie-ixhash)
checkdepends=('perl-module-implementation' 'perl-try-tiny')
url="https://metacpan.org/release/Test-Spec"
source=("http://search.cpan.org/CPAN/authors/id/P/PH/PHILIP/Test-Spec-$pkgver.tar.gz")
md5sums=('354e659a946dbe6cffd1239ad375a9aa')

build()
(
  cd Test-Spec-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd Test-Spec-$pkgver
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd Test-Spec-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
