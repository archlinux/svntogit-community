# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-test-spec
pkgver=0.51
pkgrel=3
pkgdesc='Write tests in a declarative specification style'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-package-stash>=0.23' 'perl-test-deep>=0.103' perl-test-trap
         perl-tie-ixhash)
checkdepends=('perl-module-implementation' 'perl-try-tiny')
url="https://metacpan.org/release/Test-Spec"
source=("https://cpan.metacpan.org/authors/id/A/AN/ANDYJONES/Test-Spec-$pkgver.tar.gz")
md5sums=('a52b44c071ec4c32e30f4c949e8b2f1a')

build()
(
  cd Test-Spec-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd Test-Spec-$pkgver
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd Test-Spec-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
