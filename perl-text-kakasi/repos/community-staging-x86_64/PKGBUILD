# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-text-kakasi
pkgver=2.04
pkgrel=17
pkgdesc="Perl frontend to kakasi"
arch=('x86_64')
url="http://search.cpan.org/dist/Text-Kakasi"
license=('GPL' 'PerlArtistic')
depends=('perl' 'kakasi')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/D/DA/DANKOGAI/Text-Kakasi-${pkgver}.tar.gz")
sha256sums=('844c01e78ba4bfb89c0702995a86f488de7c29b40a75e7af0e4f39d55624dba0')

build() {
  cd  Text-Kakasi-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd  Text-Kakasi-${pkgver}

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
