# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-text-template
pkgver=1.59
pkgrel=2
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='https://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings' 'perl-test-more-utf8')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/MS/MSCHOUT/Text-Template-$pkgver.tar.gz")
sha512sums=('18c56dfe10aa3e7af7ea4821c1d8002d89eb7fdf46917877e7e132cfee59783a91953255f79a355f435eff16304ae206e59557328ef51593745ae6c50e3acab7')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
