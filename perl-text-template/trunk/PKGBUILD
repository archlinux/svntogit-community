# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-text-template
pkgver=1.61
pkgrel=2
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='https://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings' 'perl-test-more-utf8')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/MS/MSCHOUT/Text-Template-$pkgver.tar.gz")
sha512sums=('b527f587fc3fee48fed495b7dcbb1da0db1e423873ab99f9360f4b1a2727d85aa9b06ef8ab59a90d575d9731821e1a0263db8d0d357b01965633c58257c556b4')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
