# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname='perl-types-serialiser'
pkgver=1.0
pkgrel=4
pkgdesc="simple data types for common serialisation formats"
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl-common-sense')
makedepends=()
url='http://search.cpan.org/dist/Types-Serialiser'
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/Types-Serialiser-$pkgver.tar.gz")
md5sums=('76460a2bfbc644672499af89192e03fe')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
      MODULEBUILDRC=/dev/null

  cd "$srcdir/Types-Serialiser-$pkgver"
  perl Makefile.PL
  make
}

check() {
  cd "$srcdir/Types-Serialiser-$pkgver"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "$srcdir/Types-Serialiser-$pkgver"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
