# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname='perl-types-serialiser'
pkgver=1.01
pkgrel=1
pkgdesc="simple data types for common serialisation formats"
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl-common-sense')
makedepends=()
url='https://search.cpan.org/dist/Types-Serialiser'
source=("https://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/Types-Serialiser-$pkgver.tar.gz")
sha256sums=('f8c7173b0914d0e3d957282077b366f0c8c70256715eaef3298ff32b92388a80')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
      MODULEBUILDRC=/dev/null

  cd "$srcdir/Types-Serialiser-$pkgver"
  perl Makefile.PL
  make
}

check() {
  cd "$srcdir/Types-Serialiser-$pkgver"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  make test
}

package() {
  cd "$srcdir/Types-Serialiser-$pkgver"
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
