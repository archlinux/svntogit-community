# $ID$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-xml-libxml-simple
_dist=XML-LibXML-Simple
pkgver=0.99
pkgrel=2
pkgdesc="An analogue of XML::Simple using libxml"
arch=('any')
url="https://metacpan.org/release/${_dist}"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-xml-libxml' 'perl-file-slurp-tiny')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MA/MARKOV/${_dist}-${pkgver}.tar.gz)
md5sums=('15b7cbe0887e292057fbcfcb3d87ef2e')
sha512sums=('806e039eefb447c43c747d1621bb432a3df13cb36798853ef2fe8a0ccaa1d7651d67fd6d386e19936100ffd4c04185004e5f0bb4ced66ed544efb6d6157b4e47')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
