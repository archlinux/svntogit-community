# Maintainer: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-xml-libxml
pkgver=1.70
pkgrel=4
pkgdesc="Interface to the libxml library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/XML-LibXML"
license=('GPL')
depends=('libxml2' 'perl-xml-sax>=0.11' 'perl-xml-namespacesupport>=1.07')
provides=('perl-libxml' 'perl-xml-libxml-common')
replaces=('perl-libxml' 'perl-xml-libxml-common')
conflicts=('perl-libxml' 'perl-xml-libxml-common')
install=perl-xml-libxml.install
source=(http://www.cpan.org/authors/id/P/PA/PAJAS/XML-LibXML-$pkgver.tar.gz) 
md5sums=('33d4294f708e20c298cfe534d1166844')

build() {
  cd $srcdir/XML-LibXML-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd $srcdir/XML-LibXML-$pkgver
  make test
}

package() {
  cd $srcdir/XML-LibXML-$pkgver
  make pure_install doc_install DESTDIR=$pkgdir
}
