# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-libxslt
pkgver=2.002000
pkgrel=1
pkgdesc="Interface to the gnome libxslt library"
arch=('x86_64')
url="https://search.cpan.org/dist/XML-LibXSLT"
license=('GPL')
depends=('perl-xml-libxml' 'libxslt')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/S/SH/SHLOMIF/XML-LibXSLT-$pkgver.tar.gz)
sha512sums=('4266e1bbd4bdceb638add43874948c69a79b364b60083ed635ec80ea4da0bd65dc5a7c5a6d76644909271e565e50c09ab1fabeec09ab37ed486d4c4bd158dba4')

build() {
  cd XML-LibXSLT-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-LibXSLT-$pkgver
  make test
}

package() {
  cd XML-LibXSLT-$pkgver
  make DESTDIR="$pkgdir" install
}
