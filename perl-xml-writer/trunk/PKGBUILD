# $Id$
# Maintainer: Justin Davis <jrcd83@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-writer
pkgver=0.625
pkgrel=3
pkgdesc="Module for writing XML documents"
arch=('any')
url="http://search.cpan.org/dist/XML-Writer"
license=('custom')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/J/JO/JOSEPHW/XML-Writer-$pkgver.tar.gz)
md5sums=('b9c2420c243c6a36ce45a008740fcede')

build() {
  cd XML-Writer-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-Writer-$pkgver
  make test
}

package() {
  cd  XML-Writer-$pkgver
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
