# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: AUR Perl <aurperl@juster.info>
# Generator  : CPANPLUS::Dist::Arch 1.15

pkgname=perl-yaml-tiny
pkgver=1.73
pkgrel=2
pkgdesc="Read/Write YAML files with as little code as possible"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-cpan-meta>=2.150001' 'perl-pod-coverage')
url='http://search.cpan.org/dist/YAML-Tiny'
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/YAML-Tiny-$pkgver.tar.gz")
sha512sums=('5999e220025aa8076e5e0e9e73e80c2da21660e77f6744d73e8e29962221d02d33a36e9829c44abf7d4f45abae069d8e121c8019bd1600b7e64db5e54efd9987')

build() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
}

check() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
