# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: AUR Perl <aurperl@juster.info>
# Generator  : CPANPLUS::Dist::Arch 1.15

pkgname=perl-yaml-tiny
pkgver=1.67
pkgrel=1
pkgdesc="Read/Write YAML files with as little code as possible"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-cpan-meta>=2.150001')
url='http://search.cpan.org/dist/YAML-Tiny'
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/YAML-Tiny-$pkgver.tar.gz")
md5sums=('61e0abcfb2ae5e86a74887967b372922')

build() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
}

check() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
