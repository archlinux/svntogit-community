# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: David Stelter <david.stelter@gmail.com>
# Contributor: Francisco Lopes <chico.lopes@gmail.com>

pkgname='pfff'
pkgver=0.28.1
pkgrel=4
pkgdesc='Tools and APIs for code analysis, visualization and transformation'
arch=('i686' 'x86_64')
url='https://github.com/facebook/pfff'
license=('LGPL2.1')
depends=('ocaml' 'ocaml-findlib' 'camlp4')
optdepends=('gtk2: required by codegraph and codemap')
options=(!makeflags staticlibs) # parallel build fails, need *.a files
source=("$pkgname-$pkgver.tar.gz::https://github.com/facebook/pfff/archive/v$pkgver.tar.gz"
        414290b4e2593e8f32d014279bf04193ecd55a8b.patch
        e532fc97da64a646bb706a9fa812a047cc557987.patch
        f6a4cc7f93c687ced9da5b2d21f9bbb717e633e0.patch)
sha256sums=('ed411376d975d14ceca10414f9d76b212d957b7a69aa393667fe5d5584c9927c'
            'ff73c3d2258b950fe381a270640f28cb919e47450e2e526d844b1e6b802cfb5f'
            'bcc115ce7f02d0898891ac036351cc907d03287826a5d3b99aacdd167cda36c8'
            '8c48852ea4b99924eaf7cbeb03379327d1d3df06dc28531eebb3b84b2864b269')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -p1 -i "$srcdir"/414290b4e2593e8f32d014279bf04193ecd55a8b.patch
	patch -p1 -i "$srcdir"/e532fc97da64a646bb706a9fa812a047cc557987.patch
	patch -p1 -i "$srcdir"/f6a4cc7f93c687ced9da5b2d21f9bbb717e633e0.patch
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr
	make clean
	make depend
	make
	make opt
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
	install -dm755 "$pkgdir"/usr/lib/ocaml
	make OCAMLFIND_LDCONF="$pkgdir"/tmp/ld.conf OCAMLFIND_DESTDIR="$pkgdir"/usr/lib/ocaml install-findlib
}
