# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: David Stelter <david.stelter@gmail.com>
# Contributor: Francisco Lopes <chico.lopes@gmail.com>

pkgname='pfff'
pkgver=0.28.1.20150303
pkgrel=1
_commit=7edb85da40598e42ae8a55d3c108e629dcb8ac47

pkgdesc='Tools and APIs for code analysis, visualization and transformation'
arch=('i686' 'x86_64')
url='https://github.com/facebook/pfff'
license=('LGPL2.1')
depends=('ocaml' 'ocaml-findlib' 'camlp4' 'zlib')
optdepends=('gtk2: required by codegraph and codemap')
options=(!makeflags staticlibs) # parallel build fails, need *.a files
source=("$pkgname-$pkgver.tar.gz::https://github.com/facebook/pfff/archive/$_commit.tar.gz")
sha256sums=('15fb620b28f0bafa17479bda79a9641edae070048106ef24656f7d2f1b7af087')

build() {
	cd "$srcdir"/$pkgname-$_commit
	./configure --prefix=/usr
	make clean
	make depend
	make
	make opt
}

package() {
	cd "$srcdir"/$pkgname-$_commit
	make DESTDIR=$pkgdir install
	install -dm755 "$pkgdir"/usr/lib/ocaml
	make OCAMLFIND_LDCONF="$pkgdir"/tmp/ld.conf OCAMLFIND_DESTDIR="$pkgdir"/usr/lib/ocaml install-findlib
}
