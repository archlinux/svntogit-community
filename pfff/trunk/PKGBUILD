# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: David Stelter <david.stelter@gmail.com>
# Contributor: Francisco Lopes <chico.lopes@gmail.com>

pkgname='pfff'
pkgver=0.29
pkgrel=4

pkgdesc='Tools and APIs for code analysis, visualization and transformation'
arch=('i686' 'x86_64')
url='https://github.com/facebook/pfff'
license=('LGPL2.1')
depends=('ocaml' 'ocaml-findlib' 'camlp4' 'zlib')
optdepends=('gtk2: required by codegraph and codemap')
options=(!makeflags staticlibs) # parallel build fails, need *.a files
source=("$pkgname-$pkgver.tar.gz::https://github.com/facebook/pfff/archive/v$pkgver.tar.gz"
"lang_js-analyze-module_js.ml-Fix-invalid-documentati.patch"
"lang_js-analyze-utils_js.mli.patch"
"0001-Replace-use-of-uint32-by-standard-complient-way.patch"
"0001-external-ocamlzip-zip.ml-remove-duplicate-exception.patch"
"0001-lang_php-matcher-Makefile-remove-warn-error.patch")
sha256sums=('d31c68f1ebb1770297eb26441d21d83728c0fbdb9c7bb3be27625db45fae72d1'
            '28b220807087b9378e0fcab1187163cdeb8d3da80f53f00ec9af434467ace57f'
            'bb10f6b25afe0cd0df5084d9e3624f43cbf7489b148c67d99479d11cf90999b7'
            '1110287c8f402d57628c17e68b7881af0da895ffe4860933586070ab7a779d60'
            'e41743e0d29a4be5c2481fb969d43c4ca64633fcb9657716fdb3fd0635333d18'
            'a2170d5c4ef76395837a00eec2500d6ec4654fc26df5997f7d30e88176dfa854')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
        patch -Np1 -i ../0001-Replace-use-of-uint32-by-standard-complient-way.patch
	patch -Np1 -i ../0001-external-ocamlzip-zip.ml-remove-duplicate-exception.patch
	patch -Np1 -i ../0001-lang_php-matcher-Makefile-remove-warn-error.patch
	patch -Np1 -i ../lang_js-analyze-utils_js.mli.patch
	patch -Np1 -i ../lang_js-analyze-module_js.ml-Fix-invalid-documentati.patch
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr
	make clean
	make depend
	WARNING_FLAGS="-w +A-4-29-6-45-41-44-48" make
	WARNING_FLAGS="-w +A-4-29-6-45-41-44-48" make opt
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
	install -dm755 "$pkgdir"/usr/lib/ocaml
	make OCAMLFIND_LDCONF="$pkgdir"/tmp/ld.conf OCAMLFIND_DESTDIR="$pkgdir"/usr/lib/ocaml install-findlib
}
