# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pflask
pkgver=0.2.20151217
_commit=599418bb6453eaa0ccab493f9411f13726c1a636
_wafver=1.8.6
pkgrel=2
pkgdesc="Lightweight process containers for Linux"
arch=('x86_64')
url="https://ghedo.github.io/pflask/"
license=('BSD')
depends=('libdbus' 'bash')
makedepends=('python-sphinx' 'git')
source=("git+https://github.com/ghedo/pflask.git#commit=$_commit"
        "0001-Fix-manages-not-being-installed-in-the-correct-direc.patch"
        "https://waf.io/waf-$_wafver")
sha256sums=('SKIP'
            'f38dbe6e1b4aa9cb75c2a44d50202996e5d454e5260c5ff17de67c49a45fec9a'
            '81c4e6a3144c7b2021a839e7277bdaf1cedbbc87302186897b4ae03f4effcbf5')

prepare() {
  cd pflask
  install -Dm755 ../waf-$_wafver waf

  # backport https://github.com/ghedo/pflask/pull/24
  patch -p1 < ../0001-Fix-manages-not-being-installed-in-the-correct-direc.patch
}

build() {
  cd pflask
  PREFIX=/usr ./waf configure
  ./waf build
}

package() {
  cd pflask
  DESTDIR="$pkgdir/" ./waf install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
