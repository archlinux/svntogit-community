# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>
# Contributor: bekks <eduard.warkentin@gmx.de>

pkgname=pgadmin3
pkgver=1.18.1
pkgrel=3
pkgdesc="Comprehensive design and management interface for PostgreSQL"
arch=('i686' 'x86_64')
url="http://www.pgadmin.org"
license=('custom')
depends=('wxgtk' 'postgresql-libs' 'libxslt')
makedepends=('libpqxx' 'krb5' 'postgresql' 'imagemagick')
install=pgadmin3.install
source=(http://ftp.postgresql.org/pub/pgadmin3/release/v${pkgver}/src/pgadmin3-${pkgver}.tar.gz)
md5sums=('7dde96680407c900af72d0839a377de4')

build() {
  cd "$srcdir"
  convert pgadmin3-${pkgver}/pgadmin/include/images/pgAdmin3.ico pgadmin3.png

  cd "pgadmin3-${pkgver}"
  [ -f Makefile ] || ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/pgadmin3-${pkgver}

  make DESTDIR=$pkgdir/ install
  install -Dm644 i18n/$pkgname.lng $pkgdir/usr/share/pgadmin3/i18n
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 pgadmin/include/images/pgAdmin3.ico $pkgdir/usr/share/pgadmin3/pgAdmin3.ico
  install -Dm644 "$srcdir/pgadmin3-3.png" "$pkgdir/usr/share/pgadmin3/pgAdmin3.png"
  install -Dm644 "pkg/pgadmin3.desktop" "$pkgdir/usr/share/applications/pgadmin3.desktop"
}
