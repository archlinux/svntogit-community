# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>
# Contributor: bekks <eduard.warkentin@gmx.de>

pkgname=pgadmin3
pkgver=1.20.0
pkgrel=6
pkgdesc="Comprehensive design and management interface for PostgreSQL"
arch=('i686' 'x86_64')
url="http://www.pgadmin.org"
license=('custom')
depends=('wxgtk2.8' 'postgresql-libs' 'libxslt')
makedepends=('libpqxx' 'krb5' 'postgresql' 'imagemagick')
install=pgadmin3.install
source=(http://ftp.postgresql.org/pub/pgadmin3/release/v${pkgver}/src/pgadmin3-${pkgver}.tar.gz
	0001-Move-misplaced-unlock-of-s_currentObjectMutex.patch)
md5sums=('3ca39fb598d0866eca90d30b85d5d5c4'
         '69fbfdfe1bac75d7d1cdfeacd322cf5d')

prepare() {
  cd "$srcdir"
  convert pgadmin3-${pkgver}/pgadmin/include/images/pgAdmin3.ico pgAdmin3.png

  cd "pgadmin3-${pkgver}"
  sed -i 's/wx-config/wx-config-2.8/' configure
  sed -i 's/wxrc/wxrc-2.8/g' stringextract pgadmin/ui/embed-xrc

  patch -p1 <$srcdir/0001-Move-misplaced-unlock-of-s_currentObjectMutex.patch
}

build() {
  cd "$srcdir"/pgadmin3-${pkgver}
  [ -f Makefile ] ||  ./configure --prefix=/usr --with-wx-version=2.8
  make
}

package() {
  cd "$srcdir"/pgadmin3-${pkgver}

  make DESTDIR="$pkgdir/" install
  install -Dm644 i18n/$pkgname.lng "$pkgdir/usr/share/pgadmin3/i18n"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm644 pgadmin/include/images/pgAdmin3.ico "$pkgdir/usr/share/pgadmin3/pgAdmin3.ico"
  install -Dm644 "$srcdir/pgAdmin3-3.png" "$pkgdir/usr/share/pgadmin3/pgAdmin3.png"

  install -Dm644 "$srcdir/pgAdmin3-5.png" "$pkgdir/usr/share/icons/hicolor/16x16/apps/pgAdmin3.png"
  install -Dm644 "$srcdir/pgAdmin3-4.png" "$pkgdir/usr/share/icons/hicolor/32x32/apps/pgAdmin3.png"
  install -Dm644 "$srcdir/pgAdmin3-3.png" "$pkgdir/usr/share/icons/hicolor/48x48/apps/pgAdmin3.png"

  install -Dm644 "pkg/pgadmin3.desktop" "$pkgdir/usr/share/applications/pgadmin3.desktop"
}
