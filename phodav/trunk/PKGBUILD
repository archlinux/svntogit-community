# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=phodav
pkgver=2.2
pkgrel=2
pkgdesc="WebDav server implementation using libsoup (RFC 4918)"
arch=('x86_64')
url="https://wiki.gnome.org/phodav"
license=('LGPL2.1')
depends=('avahi' 'libsoup')
makedepends=('asciidoc' 'intltool' 'xmlto')
backup=('etc/conf.d/spice-webdavd')
source=("https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('080305dc9e804562f200ffc8c7d345c76754fce9cb01489dddb1b2c52c0557c1')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's|/etc/sysconfig/spice-webdavd|/etc/conf.d/spice-webdavd|
	        s|/usr/sbin/spice-webdavd|/usr/bin/spice-webdavd|' data/spice-webdavd.service
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	install -dm755 "$pkgdir/etc/conf.d/"
	echo 'SPICE_WEBDAVD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-webdavd"
}
