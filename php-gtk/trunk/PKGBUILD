# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=php-gtk
pkgver=2.0.1
pkgrel=1
pkgdesc="PHP bindings for the Gtk+ 2 library"
arch=('i686' 'x86_64')
url="http://gtk.php.net/"
license=('LGPL')
depends=('php' 'gtk2>=2.12.0' 'libglade>=2.6.2')
makedepends=('pkgconfig' 'gtk2+extra' 'gtkhtml' 'libsexy' 'xulrunner'
             'gtksourceview' 'gtkspell')
optdepends=('extra: gtk2+extra'
            'html: gtkhtml'
            'libsexy: libsexy'
            'mozembed: xulrunner'
            'sourceview: gtksourceview'
            'spell: gtkspell')
backup=(etc/php/php-gtk.ini)
install=php-gtk.install
source=(http://gtk.php.net/distributions/$pkgname-$pkgver.tar.gz
        php.ini-template
        shared-link.patch)
md5sums=('d57ce885f32749320765e01c079e113b'
         '219a7f7f336a4ed3fe27bb352b8199c3'
         '34a417289deae9655f91b19f399b403c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Remove pre-generated files
  find ext -name gen\*.[ch] -exec rm -f {} \;

  # Use the correct libgtkhtml version
  sed -i 's|libgtkhtml-3.8|libgtkhtml-3.14|' ext/html/{config.m4,html.defs}

  # Fix linking for shared modules
  patch -Np1 -i "$srcdir/shared-link.patch" || return 1

  ./buildconf
  ./configure --prefix=/usr \
              --with-extra=shared \
              --with-html=shared \
              --with-libsexy=shared \
              --with-mozembed=shared \
              --with-sourceview=shared \
              --with-spell=shared
  make || return 1
  make INSTALL_ROOT="$pkgdir/" install

  install -D -m644 "$srcdir/php.ini-template" "$pkgdir/etc/php/php-gtk.ini"
  EXTENSION_DIR=$(php-config --extension-dir)
  sed -i "/^extension_dir/cextension_dir = \"$EXTENSION_DIR\"" \
         "$pkgdir/etc/php/php-gtk.ini"
}

# vim:set ts=2 sw=2 et:
