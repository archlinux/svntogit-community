# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgname=php-igbinary
pkgver=3.1.3
pkgrel=1
pkgdesc="A drop in replacement for the standard php serializer"
arch=('x86_64')
url="https://github.com/igbinary/igbinary"
license=('BSD')
depends=('glibc' 'php')
backup=("etc/php/conf.d/${_name}.ini")
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_name}/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('41f2134c644f64d60e5a5084a8dd0e01fa32760d7072dff16df1b87e9f6fe4894edf562bb057942f0cfb68c2f3d3df9c4df0890d46a0ab4ad061ff5bd7661be2')
b2sums=('49175bacfb4f1432afdf6180779195e0697a7613d4723fe745368adecd62dfedcf1d13c6c586e4a83000a08351291ccee69cad0bfb37dd746b2b49e9b9dd3f64')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # disable the extension by default
  sed 's/extension/;extension/g' -i "${_name}.php.ini"
  phpize
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-igbinary
  make
}

check() {
  cd "$pkgname-$pkgver"
  NO_INTERACTION=1 make -k test
}

package() {
  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
