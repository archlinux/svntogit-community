# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgbase=php-igbinary
pkgname=('php-igbinary' 'php7-igbinary')
pkgver=3.2.6
pkgrel=1
pkgdesc="A drop in replacement for the standard php serializer"
arch=('x86_64')
url="https://github.com/igbinary/igbinary"
license=('BSD')
depends=('glibc')
makedepends=('php7' 'php')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_name}/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('b6c9e7b68716052d93a8535af4666fa3d5b4071bc181819cdc0ecd7f8e1c099001905ff2134eec6343daa77102a423ad0903f8800808c30806d3cb7faa4c7a06')
b2sums=('05fd24a640ab9e1f2402645bf322b0a959be69c068ff203552f2806240d8504f1ff3c7b209d9b050ba7815a90bcec2aab823a24a2a2a1cc149da90d25166f18a')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgbase-$pkgver"
  # disable the extension by default
  sed 's/extension/;extension/g' -i "$pkgbase-$pkgver/${_name}.php.ini"

  cp -av "$pkgbase-$pkgver" "${pkgname[1]}-$pkgver"
  (
    cd "${pkgbase}-$pkgver"
    phpize
  )
  (
    cd "${pkgname[1]}-$pkgver"
    phpize7
  )
}

build() {
  (
    cd "$pkgbase-$pkgver"
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
  (
    cd "${pkgname[1]}-$pkgver"
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
}

check() {
  (
    cd "$pkgbase-$pkgver"
    NO_INTERACTION=1 make -k test
  )
  (
    cd "${pkgname[1]}-$pkgver"
    NO_INTERACTION=1 make -k test
  )
}

package_php-igbinary() {
  depends+=('php')
  backup=("etc/php/conf.d/${_name}.ini")

  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_php7-igbinary() {
  depends+=('php7')
  backup=("etc/php7/conf.d/${_name}.ini")

  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php7/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
