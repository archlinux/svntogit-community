# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgbase=php-igbinary
pkgname=(php-igbinary php7-igbinary)
pkgver=3.2.12
pkgrel=1
pkgdesc="A drop in replacement for the standard php serializer"
arch=(x86_64)
url="https://github.com/igbinary/igbinary"
license=(BSD)
depends=(glibc)
makedepends=(php7 php)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$_name/$_name/archive/$pkgver.tar.gz)
sha512sums=('08a239a0144024db62c9c28a3e1dc7f4d65ba769e025d5609276ce2e5362bcf7ff6adcf2b503c2d512f0693a6e38fbc1b58e817e99a4af535faeafd76574d656')
b2sums=('253f0da6259fe77216b643e34f981bc7e4f883bd29ecf6135dbe88e3364b16a16fa84723ba8376b36af9a149b2590610d00dde662df3afc4d74a4a0dda1f0c97')

prepare() {
  mv -v $_name-$pkgver $pkgbase-$pkgver
  # disable the extension by default
  sed 's/extension/;extension/g' -i $pkgbase-$pkgver/$_name.php.ini

  cp -av $pkgbase-$pkgver ${pkgname[1]}-$pkgver
  (
    cd $pkgbase-$pkgver
    phpize
  )
  (
    cd ${pkgname[1]}-$pkgver
    phpize7
  )
}

build() {
  (
    cd $pkgbase-$pkgver
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
  (
    cd ${pkgname[1]}-$pkgver
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
}

check() {
  (
    NO_INTERACTION=1 make -k test -C $pkgbase-$pkgver
  )
  (
    NO_INTERACTION=1 make -k test -C ${pkgname[1]}-$pkgver
  )
}

package_php-igbinary() {
  local _interpreter_version="$(php --version |head -n1 |cut -d ' ' -f2)"

  depends+=(php)
  _provides=(php-igbinary-interpreter=${_interpreter_version%.*})
  provides=("${_provides[@]}")
  backup=(etc/php/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 $_name.php.ini "$pkgdir/etc/php/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_php7-igbinary() {
  local _interpreter_version="$(php7 --version |head -n1 |cut -d ' ' -f2)"

  depends+=(php7)
  _provides=(php-igbinary=$pkgver php-igbinary-interpreter=${_interpreter_version%.*})
  provides=("${_provides[@]}")
  backup=(etc/php7/conf.d/$_name.ini)

  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -vDm 644 "$_name.php.ini" "$pkgdir/etc/php7/conf.d/$_name.ini"
  install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {CREDITS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
