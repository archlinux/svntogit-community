# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=php-memcached
pkgver=2.0.1
pkgrel=5
pkgdesc="PHP extension for interfacing with memcached via libmemcached library"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/memcached"
license=('PHP')
depends=('php' 'libmemcached')
backup=('etc/php/conf.d/memcached.ini')
source=(http://pecl.php.net/get/memcached-$pkgver.tgz)
sha256sums=('250779594e846b7b31390c384697eb24a5096123b0d3e2f4a1a0b79d14fb1bff')

build() {
  cd "$srcdir/memcached-$pkgver"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/memcached-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=memcached.so' >memcached.ini
  install -Dm644 memcached.ini "$pkgdir/etc/php/conf.d/memcached.ini"
}

# vim:set ts=2 sw=2 et:
