# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=php-memcached
pkgver=2.2.0
_commit=e573a6e8fc815f12153d2afd561fc84f74811e2f
pkgrel=3
pkgdesc="PHP extension for interfacing with memcached via libmemcached library"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/memcached"
license=('PHP')
depends=('php' 'libmemcached')
makedepends=('git')
backup=('etc/php/conf.d/memcached.ini')
source=("git+https://github.com/php-memcached-dev/php-memcached.git#commit=${_commit}")
sha256sums=('SKIP')

build() {
  cd "$srcdir/$pkgname"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"

  make INSTALL_ROOT="$pkgdir" install

  install -d "$pkgdir/etc/php/conf.d"
  echo ';extension=memcached.so' >"$pkgdir/etc/php/conf.d/memcached.ini"
}

# vim:set ts=2 sw=2 et:
