# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=('php-memcached' 'php7-memcached')
pkgver=3.1.5.r50.gd3b8ce1
_commit=d3b8ce13f13cee3017aa8a00a0cfb2e7709154d8
pkgrel=1
pkgdesc="PHP extension for interfacing with memcached via libmemcached library"
arch=('x86_64')
url="https://github.com/php-memcached-dev/php-memcached"
license=('PHP')
depends=('libmemcached')
makedepends=('php' 'php7' 'git')
source=("git+https://github.com/php-memcached-dev/php-memcached.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cp -a $pkgbase{,-php7}
}

build() {
  cd $pkgbase
  phpize
  ./configure --prefix=/usr
  make

  cd ../$pkgbase-php7
  phpize7
  ./configure --prefix=/usr
  make
}

package_php-memcached() {
  depends+=('php')
  backup=('etc/php/conf.d/memcached.ini')

  cd $pkgbase

  make INSTALL_ROOT="$pkgdir" install
  install -d "$pkgdir/etc/php/conf.d"
  echo ';extension=memcached.so' >"$pkgdir/etc/php/conf.d/memcached.ini"
}

package_php7-memcached() {
  pkgdesc=${pkgdesc/PHP/PHP 7}
  depends+=('php7')
  backup=('etc/php7/conf.d/memcached.ini')

  cd $pkgbase-php7

  make INSTALL_ROOT="$pkgdir" install
  install -d "$pkgdir/etc/php7/conf.d"
  echo ';extension=memcached.so' >"$pkgdir/etc/php7/conf.d/memcached.ini"
}

# vim:set ts=2 sw=2 et:
