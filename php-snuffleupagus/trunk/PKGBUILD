# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Julien (jvoisin) Voisin <julien.voisin+snuffleupagus@dustri.org>

pkgname=php-snuffleupagus
_pkgname=snuffleupagus
pkgver=0.8.3
pkgrel=1
pkgdesc="Security module for PHP"
url='https://snuffleupagus.readthedocs.io/'
arch=('x86_64')
license=('LGPL3')
depends=('php')
backup=('etc/php/conf.d/snuffleupagus.ini'
        'etc/php/conf.d/snuffleupagus.rules')
source=(https://github.com/jvoisin/${_pkgname}/archive/v${pkgver}/${_pkgname}-${pkgver}.tar.gz)
b2sums=('989dbb57a8375cb394dfb41f07de5c048b66936a8e5b3dd14fb3609149dd371203f7a30d1fbcfe8ef6faa2ba5864c68b209b1b209541c348c0277c8bd12b568a')

prepare() {
  cd ${_pkgname}-${pkgver}

  # tests are incompatible with PHP 8.1
  rm src/tests/{disable_function/disabled_functions_include_once,disable_function/disabled_functions_include_simulation,disable_function/disabled_functions_register_tick_function,disable_function/disabled_functions_require,disable_function/disabled_functions_require_once,disable_function/disabled_functions_require_simulation,strict_mode/strict_mode_enabled,stream_wrapper/stream_wrapper}.phpt
}

build() {
  cd ${_pkgname}-${pkgver}/src
  phpize
  ./configure --prefix=/usr --enable-"${_pkgname}"
  make
}

check() {
  cd ${_pkgname}-${pkgver}/src
  NO_INTERACTION=1 REPORT_EXIT_STATUS=1 make test
}

package() {
  cd ${_pkgname}-${pkgver}
  make -C src INSTALL_ROOT="${pkgdir}" install
  install -D -m644 "config/default.rules" "${pkgdir}/etc/php/conf.d/${_pkgname}.rules"
  install -D -m644 "config/${_pkgname}.ini" "${pkgdir}/etc/php/conf.d/${_pkgname}.ini"
}

# vim: ts=2 sw=2 et:
