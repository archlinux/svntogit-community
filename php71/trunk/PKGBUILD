# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgbase=php71
pkgname=('php71'
         'php71-cgi'
         'php71-apache'
         'php71-fpm'
         'php71-embed'
         'php71-phpdbg'
         'php71-dblib'
         'php71-enchant'
         'php71-gd'
         'php71-imap'
         'php71-intl'
         'php71-mcrypt'
         'php71-odbc'
         'php71-pgsql'
         'php71-pspell'
         'php71-snmp'
         'php71-sqlite'
         'php71-tidy'
         'php71-xsl')
pkgver=7.1.14
pkgrel=1
arch=('x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('apache' 'aspell' 'c-client' 'db' 'enchant' 'gd' 'gmp' 'icu' 'libmcrypt' 'libxslt' 'libzip' 'net-snmp'
             'postgresql-libs' 'sqlite' 'systemd' 'tidy' 'unixodbc' 'curl' 'libtool' 'postfix' 'freetds' 'pcre')
checkdepends=('procps-ng')
validpgpkeys=('1729F83938DA44E27BA0F4D3DBDB397470D12172')
source=("https://php.net/distributions/${pkgbase%71}-${pkgver}.tar.xz"{,.asc}
        'apache.patch' 'apache.conf' 'php-fpm.patch' 'php-fpm.tmpfiles' 'php.ini.patch' enchant-2.patch)
sha256sums=('c09f0c1074f5689b492d79034adb84e6a6c6d08c6763c02282e6318d41156779'
            'SKIP'
            '258b33b6531b1128d9804c8b608b6013423a421edcf764747042d07e79ec6df3'
            'ebc0af1ef3a6baccb013d0ccb29923895a7b22ff2d032e3bba802dc6328301ce'
            '7d982a3cefb3eaec0b3458340e4991c2959c10d1518ecb7c1d041884b14efc73'
            '640dba0d960bfeaae9ad38d2826d3f6b5d6c175a4d3e16664eefff29141faad5'
            '78f60b1d9f3a0ef8af77208feed76e303b3a13e93b80613c1e5a729004a5343c'
            'b11c3de747ef222696f5135556d624e3f7f0135a3cb1b06082f1ec8e9b7eeb0a')

prepare() {
	cd ${srcdir}/${pkgbase%71}-${pkgver}

	patch -p0 -i ${srcdir}/apache.patch
	patch -p0 -i ${srcdir}/php-fpm.patch
	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p1 -i ../enchant-2.patch
}

build() {
	local _phpconfig="--srcdir=../${pkgbase%71}-${pkgver} \
		--config-cache \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/php \
		--localstatedir=/var \
		--with-layout=GNU \
		--with-config-file-path=/etc/php \
		--with-config-file-scan-dir=/etc/php/conf.d \
		--disable-rpath \
		--mandir=/usr/share/man \
		--without-pear \
		"

	local _phpextensions="\
		--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-gd-native-ttf \
		--enable-intl=shared \
		--enable-mbstring \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-zip=shared \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-freetype-dir=/usr \
		--with-gd=shared,/usr \
		--with-gdbm \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-imap-ssl \
		--with-imap=shared \
		--with-kerberos=/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-libzip \
		--with-mcrypt=shared \
		--with-mhash \
		--with-mysql-sock=/run/mysqld/mysqld.sock \
		--with-mysqli=shared,mysqlnd \
		--with-openssl \
		--with-pcre-regex=/usr \
		--with-pdo-dblib=shared,/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pgsql=shared \
		--with-pspell=shared \
		--with-readline \
		--with-snmp=shared \
		--with-sqlite3=shared,/usr \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		--enable-pcntl \
		"

	EXTENSION_DIR=/usr/lib/php/modules
	export EXTENSION_DIR

	mkdir ${srcdir}/build
	cd ${srcdir}/build
	ln -s ../${pkgbase%71}-${pkgver}/configure
	./configure ${_phpconfig} \
		--enable-cgi \
		--enable-fpm \
		--with-fpm-systemd \
		--with-fpm-acl \
		--with-fpm-user=http \
		--with-fpm-group=http \
		--enable-embed=shared \
		${_phpextensions}
	make

	# apache
	# reuse the previous run; this will save us a lot of time
	cp -a ${srcdir}/build ${srcdir}/build-apache
	cd ${srcdir}/build-apache
	./configure ${_phpconfig} \
		--with-apxs2 \
		${_phpextensions}
	make

	# phpdbg
	cp -a ${srcdir}/build ${srcdir}/build-phpdbg
	cd ${srcdir}/build-phpdbg
	./configure ${_phpconfig} \
		--enable-phpdbg \
		${_phpextensions}
	make
}

check() {
	cd ${srcdir}/${pkgbase%71}-${pkgver}

	# Check if sendmail was configured correctly (FS#47600)
	${srcdir}/build/sapi/cli/php -n -r 'echo ini_get("sendmail_path");' | grep -q '/usr/bin/sendmail'

	export REPORT_EXIT_STATUS=1
	export NO_INTERACTION=1
	export SKIP_ONLINE_TESTS=1
	export SKIP_SLOW_TESTS=1

	${srcdir}/build/sapi/cli/php -n run-tests.php -n -P {tests,Zend}
}

package_php71() {
	pkgdesc='A general-purpose scripting language that is especially suited to web development'
	depends=('libxml2' 'curl' 'libzip' 'pcre')
	conflicts=('php')
	backup=('etc/php/php.ini')
	provides=("${pkgname/71/}=${pkgver}" "php71-ldap=${pkgver}" "php-ldap=${pkgver}")

	cd ${srcdir}/build
	make -j1 INSTALL_ROOT=${pkgdir} install-{modules,cli,build,headers,programs,pharcmd}
	install -D -m644 ${srcdir}/${pkgbase%71}-${pkgver}/php.ini-production ${pkgdir}/etc/php/php.ini
	install -d -m755 ${pkgdir}/etc/php/conf.d/

	# remove static modules
	rm -f ${pkgdir}/usr/lib/php/modules/*.a
	# remove modules provided by sub packages
	rm -f ${pkgdir}/usr/lib/php/modules/{enchant,gd,imap,intl,mcrypt,odbc,pdo_dblib,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
	# remove empty directory
	rmdir ${pkgdir}/usr/include/php/include
}

package_php71-cgi() {
	pkgdesc='CGI and FCGI SAPI for PHP'
	depends=('php71')
	conflicts=('php-cgi')
	provides=("${pkgname/71/}=${pkgver}")

	cd ${srcdir}/build
	make -j1 INSTALL_ROOT=${pkgdir} install-cgi
}

package_php71-apache() {
	pkgdesc='Apache SAPI for PHP'
	depends=('php71' 'apache')
	backup=('etc/httpd/conf/extra/php7_module.conf')
	conflicts=('php-apache')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build-apache/libs/libphp7.so ${pkgdir}/usr/lib/httpd/modules/libphp7.so
	install -D -m644 ${srcdir}/apache.conf ${pkgdir}/etc/httpd/conf/extra/php7_module.conf
}

package_php71-fpm() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php71' 'systemd')
	backup=('etc/php/php-fpm.conf' 'etc/php/php-fpm.d/www.conf')
	options=('!emptydirs')
	conflicts=('php-fpm')
	provides=("${pkgname/71/}=${pkgver}")

	cd ${srcdir}/build
	make -j1 INSTALL_ROOT=${pkgdir} install-fpm
	install -D -m644 sapi/fpm/php-fpm.service ${pkgdir}/usr/lib/systemd/system/php-fpm.service
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/php-fpm.conf
}

package_php71-embed() {
	pkgdesc='Embedded PHP SAPI library'
	depends=('php71' 'libsystemd')
	options=('!emptydirs')
	conflicts=('php-embed')
	provides=("${pkgname/71/}=${pkgver}")

	cd ${srcdir}/build
	make -j1 INSTALL_ROOT=${pkgdir} PHP_SAPI=embed install-sapi
}

package_php71-phpdbg() {
	pkgdesc='Interactive PHP debugger'
	depends=('php71')
	options=('!emptydirs')
	conflicts=('php-phpdbg')
	provides=("${pkgname/71/}=${pkgver}")

	cd ${srcdir}/build-phpdbg
	make -j1 INSTALL_ROOT=${pkgdir} install-phpdbg
}

package_php71-dblib() {
	pkgdesc='dblib module for PHP'
	depends=('php71' 'freetds')
	conflicts=('php-dblib')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/pdo_dblib.so ${pkgdir}/usr/lib/php/modules/pdo_dblib.so
}

package_php71-enchant() {
	pkgdesc='enchant module for PHP'
	depends=('php71' 'enchant')
	conflicts=('php-enchant')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/enchant.so ${pkgdir}/usr/lib/php/modules/enchant.so
}

package_php71-gd() {
	pkgdesc='gd module for PHP'
	depends=('php71' 'gd')
	conflicts=('php-gd')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/gd.so ${pkgdir}/usr/lib/php/modules/gd.so
}

package_php71-imap() {
	pkgdesc='imap module for PHP'
	depends=('php71' 'c-client')
	conflicts=('php-imap')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/imap.so ${pkgdir}/usr/lib/php/modules/imap.so
}

package_php71-intl() {
	pkgdesc='intl module for PHP'
	depends=('php71' 'icu')
	conflicts=('php-intl')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/intl.so ${pkgdir}/usr/lib/php/modules/intl.so
}

package_php71-mcrypt() {
	pkgdesc='mcrypt module for PHP'
	depends=('php71' 'libmcrypt' 'libtool')
	conflicts=('php-mcrypt')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/mcrypt.so ${pkgdir}/usr/lib/php/modules/mcrypt.so
}

package_php71-odbc() {
	pkgdesc='ODBC modules for PHP'
	depends=('php71' 'unixodbc')
	conflicts=('php-odbc')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/odbc.so ${pkgdir}/usr/lib/php/modules/odbc.so
	install -D -m755 ${srcdir}/build/modules/pdo_odbc.so ${pkgdir}/usr/lib/php/modules/pdo_odbc.so
}

package_php71-pgsql() {
	pkgdesc='PostgreSQL modules for PHP'
	depends=('php71' 'postgresql-libs')
	conflicts=('php-pgsql')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/pgsql.so ${pkgdir}/usr/lib/php/modules/pgsql.so
	install -D -m755 ${srcdir}/build/modules/pdo_pgsql.so ${pkgdir}/usr/lib/php/modules/pdo_pgsql.so
}

package_php71-pspell() {
	pkgdesc='pspell module for PHP'
	depends=('php71' 'aspell')
	conflicts=('php-pspell')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/pspell.so ${pkgdir}/usr/lib/php/modules/pspell.so
}

package_php71-snmp() {
	pkgdesc='snmp module for PHP'
	depends=('php71' 'net-snmp')
	conflicts=('php-snmp')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/snmp.so ${pkgdir}/usr/lib/php/modules/snmp.so
}

package_php71-sqlite() {
	pkgdesc='sqlite module for PHP'
	depends=('php71' 'sqlite')
	conflicts=('php-sqlite')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/sqlite3.so ${pkgdir}/usr/lib/php/modules/sqlite3.so
	install -D -m755 ${srcdir}/build/modules/pdo_sqlite.so ${pkgdir}/usr/lib/php/modules/pdo_sqlite.so
}

package_php71-tidy() {
	pkgdesc='tidy module for PHP'
	depends=('php71' 'tidy')
	conflicts=('php-tidy')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/tidy.so ${pkgdir}/usr/lib/php/modules/tidy.so
}

package_php71-xsl() {
	pkgdesc='xsl module for PHP'
	depends=('php71' 'libxslt')
	conflicts=('php-xsl')
	provides=("${pkgname/71/}=${pkgver}")

	install -D -m755 ${srcdir}/build/modules/xsl.so ${pkgdir}/usr/lib/php/modules/xsl.so
}
