# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.6.4
pkgrel=6
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="https://github.com/leenooks/phpLDAPadmin"
license=('GPL')
depends=('libldap' 'php')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=($pkgname-$pkgver.tar.gz::https://github.com/leenooks/phpLDAPadmin/archive/$pkgver.tar.gz
	https://github.com/leenooks/phpLDAPadmin/commit/661cab652b279680c07e81b4b71d1955ee2818ab.patch
	disable-mcrypt.patch
	sort-in-templates.patch
	more-php8-fixes.patch)
sha256sums=('92e390c013714c75439737633ad4576bc642d87b0091fab6226cbbcb13a0ea48'
            '828984c8e015ea3ee43cfb61006b7502c5513007f710120bbc2102f7ad4e1a47'
            'b31774abab4dd603ec9acbeda49adb188890173ee93042a811b5724e01edd837'
            'fc2d4bdf4d43a90aa541fe20eeed494caa339eacf3f08b06539efcdbacc3578f'
            'e430c2ee5d5bb84ff9db3f1b6acec884115da89274270b5eeb83b8e7a6eeb991')

prepare() {
  cd "$srcdir"/phpLDAPadmin-${pkgver}/
  find -type f -name '*.png' -exec optipng -quiet -force -fix {} \;
  patch -p1 -i "$srcdir"/disable-mcrypt.patch
  patch -p1 -i "$srcdir"/sort-in-templates.patch
  patch -p1 -i "$srcdir"/661cab652b279680c07e81b4b71d1955ee2818ab.patch
  patch -p1 -i "$srcdir"/more-php8-fixes.patch
  find -type f -name '*.orig' -exec rm -f {} \;
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra "$srcdir"/phpLDAPadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example "$pkgdir"/etc/webapps/phpldapadmin/config.php
}
