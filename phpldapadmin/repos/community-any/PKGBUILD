# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.3
pkgrel=13
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="http://phpldapadmin.sourceforge.net/"
license=('GPL')
depends=('libldap' 'php')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=(https://downloads.sourceforge.net/project/phpldapadmin/phpldapadmin-php5/$pkgver/phpldapadmin-$pkgver.tgz
        phpldapadmin-1.2.3-php5_5.patch
        phpldapadmin-1.2.3-php7_2.patch
        phpldapadmin-1.2.3-php7_4.patch
        disable-mcrypt.patch
        sort-in-templates.patch)
sha256sums=('aecaf8c3ce77ba3899dd31ee5ee183555b2f9349eb8e196dcd33d8a3c485ed58'
            'ecafce992e8b68426eacdc3702268f2cb57d9ebed662b0edcc614a6e24623c76'
            'c35a31e15a406cd2b49331a92a2ab491fd7024c4bc82e173e1f271514cfc4bc6'
            'd6e9b6361549be4a4e5a05ffac0917307973fe35717041f7cdde4171271ad168'
            'b31774abab4dd603ec9acbeda49adb188890173ee93042a811b5724e01edd837'
            'fc2d4bdf4d43a90aa541fe20eeed494caa339eacf3f08b06539efcdbacc3578f')

prepare() {
  cd "$srcdir"/phpldapadmin-${pkgver}/
  find -type f -name '*.png' -exec optipng -quiet -force -fix {} \;
  # patch from https://pkgs.fedoraproject.org/cgit/phpldapadmin.git/tree/
  patch -p1 -i "$srcdir"/phpldapadmin-1.2.3-php5_5.patch
  # php >7 deprecates mcrypt. TODO: replace it with openssl?
  patch -p1 -i "$srcdir"/disable-mcrypt.patch
  patch -p1 -i "$srcdir"/phpldapadmin-1.2.3-php7_2.patch
  patch -p1 -i "$srcdir"/phpldapadmin-1.2.3-php7_4.patch
  patch -p1 -i "$srcdir"/sort-in-templates.patch
  # rm .orig
  find -type f -name '*.orig' -exec rm -f {} \;
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra "$srcdir"/phpldapadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example "$pkgdir"/etc/webapps/phpldapadmin/config.php
}
