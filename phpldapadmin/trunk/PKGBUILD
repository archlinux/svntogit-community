# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.3
pkgrel=8
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="http://phpldapadmin.sourceforge.net/"
license=('GPL')
depends=('libldap' 'php' 'php-ldap')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=(https://downloads.sourceforge.net/project/phpldapadmin/phpldapadmin-php5/$pkgver/phpldapadmin-$pkgver.tgz
        phpldapadmin-1.2.3-php5_5.patch
        disable-mcrypt.patch)
sha256sums=('aecaf8c3ce77ba3899dd31ee5ee183555b2f9349eb8e196dcd33d8a3c485ed58'
            'ecafce992e8b68426eacdc3702268f2cb57d9ebed662b0edcc614a6e24623c76'
            'b31774abab4dd603ec9acbeda49adb188890173ee93042a811b5724e01edd837')

build() {
  cd "$srcdir"/phpldapadmin-${pkgver}/
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
  # patch from https://pkgs.fedoraproject.org/cgit/phpldapadmin.git/tree/
  patch -p1 -i "$srcdir"/phpldapadmin-1.2.3-php5_5.patch
  # php >7 deprecates mcrypt. TODO: replace it with openssl?
  patch -p1 -i "$srcdir"/disable-mcrypt.patch
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra "$srcdir"/phpldapadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example "$pkgdir"/etc/webapps/phpldapadmin/config.php
}
