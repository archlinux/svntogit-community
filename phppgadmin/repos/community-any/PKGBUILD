# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: wizzomafizzo <wizzomafizzo@gmail.com>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>

pkgname=phppgadmin
pkgver=5.1
pkgrel=4
pkgdesc="A web-based administration tool for PostgreSQL"
arch=('any')
url="http://sourceforge.net/projects/phppgadmin"
license=('GPL')
depends=('php' 'php-pgsql')
backup=('etc/webapps/phppgadmin/config.inc.php')
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/phpPgAdmin-$pkgver.tar.gz
	https://github.com/phppgadmin/phppgadmin/commit/d4bce721052053e32df3dadc51975c75735b2e08.patch
	https://github.com/phppgadmin/phppgadmin/commit/5a976d4982c5250b42dcd147083729c844f219af.patch)
sha256sums=('42294e7b19d3b4003912eaad9a34df4096c0380871aedce152aa13d4955878a5'
            'ebca071295948bb18e2e808bf635ff253acaf24658fe116e4cf3e6b82425ee55'
            'c0088729d76e542e8d5caa3c0fc9c7049fc4f6a9d21cc5aba075e2050c6e96d8')

prepare() {
  cd "$srcdir"/phpPgAdmin-$pkgver
  patch -p1 <../d4bce721052053e32df3dadc51975c75735b2e08.patch || true
  patch -p1 <../5a976d4982c5250b42dcd147083729c844f219af.patch || true
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/phppgadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phppgadmin
  cd ${_instdir}
  cp -ra "$srcdir"/phpPgAdmin-$pkgver/* .
  cp ./conf/config.inc.php-dist "$pkgdir"/etc/webapps/phppgadmin/config.inc.php
  rm -f ${_instdir}/conf/config.inc.php
  ln -s /etc/webapps/phppgadmin/config.inc.php ${_instdir}/conf/config.inc.php
}
