# $Id$
# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor:Techlive Zheng <techlivezheng at gmail dot com>

pkgname=phpvirtualbox
pkgver=5.0_5
pkgrel=2
pkgdesc="PHP/AJAX web interface for VirtualBox 4.*"
arch=(any)
url="http://sourceforge.net/projects/phpvirtualbox/"
license=('GPL')
depends=('php')
backup=("etc/webapps/phpvirtualbox/config.php")
source=(https://downloads.sourceforge.net/project/phpvirtualbox/phpvirtualbox-${pkgver/_/-}.zip
	https://github.com/imoore76/phpvirtualbox/commit/10b6a7383c810dfa6261869cb1f9cc8bca2ad814.patch
	https://github.com/imoore76/phpvirtualbox/commit/207b1decbb9de27f2c884ecb75f70d78d9820c80.patch
	https://github.com/imoore76/phpvirtualbox/commit/4e9f9fd85dd2caad6bb7c50a742e1558aee57f77.patch)
sha256sums=('c80c423fda30e5795872c9b9adc68429deee5726e468729472a573c324303c05'
            '0b2bad7e69911a251a126cc835ce647b5ec86cd506fe99c984367ec9e885569e'
            'db51fceccd16de6c959055ed5794e788173894c1a4ac23e064a7f2d467157dea'
            '308c7652e895c799363c758218a11c8d2c9b5e3f9ee41ffd65b2679be5f1a56c')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver/_/-}
  rm -f endpoints/lib/*.wsdl
  for p in $(ls -1 $srcdir/*.patch); do
      patch -p1 <$p
  done
  sed -i "s|${pkgver/_/-}|5.1-1|" endpoints/lib/config.php
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver/_/-}

  mkdir -p "$pkgdir"/etc/webapps/phpvirtualbox
  install -D -m644 config.php-example "$pkgdir"/etc/webapps/phpvirtualbox/config.php

  # Apache configuration
  cat > "$pkgdir"/etc/webapps/phpvirtualbox/apache.example.conf <<EOF
  Alias /phpvirtualbox "/usr/share/webapps/phpvirtualbox"
  <Directory "/usr/share/webapps/phpvirtualbox">
    Options FollowSymlinks
    AllowOverride All
    Require all granted
  </Directory>
EOF

  find . -type f -exec install -D -m644 {,"$pkgdir"/usr/share/webapps/${pkgname}/}{} \;
  ln -s /etc/webapps/phpvirtualbox/config.php "$pkgdir"/usr/share/webapps/phpvirtualbox/config.php
}
