# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=picom
pkgver=7.5
pkgrel=3
pkgdesc='X compositor that may fix tearing issues'
arch=(x86_64)
url='https://github.com/yshui/picom'
license=(MIT MPL)
replaces=(compton)
provides=(compton)
conflicts=(compton)
makedepends=(asciidoc git mesa meson ninja setconf uthash)
depends=(hicolor-icon-theme libconfig libdbus libev libgl libxdg-basedir pcre
         pixman xcb-util-image xcb-util-renderutil)
backup=(etc/xdg/picom.conf)
optdepends=('dbus:          For controlling picom via D-Bus'
            'xorg-xwininfo: For picom-trans'
            'xorg-xprop:    For picom-trans'
            'python:        For running picom-convgen.py')
# git tag: v7.5
source=("git+$url?signed#commit=3838f053afb1aa05ba1ab489f2dfaa44c00c9ef0?37C999F617EA1A47"
        picom.conf)
validpgpkeys=('A22F5C0F4FCF9C7C89A167462C965E9E5D45D730') # Yuxuan Shui <yshuiv7@gmail.com>
sha256sums=('SKIP'
            'cf395428e60f3c6d7b39583c623e846fd61516f8afc8eb570d065ff63de6b23f')

build() {
  mkdir -p build
  arch-meson build $pkgname -D build_docs=true -D b_ndebug=true
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 picom.conf "$pkgdir/etc/xdg/picom.conf"
  install -Dm644 build/man/picom.1 "$pkgdir/usr/share/man/man1/picom.1"
  install -Dm644 build/man/picom-trans.1 "$pkgdir/usr/share/man/man1/picom-trans.1"
  cd $pkgname
  install -Dm644 picom.sample.conf "$pkgdir/etc/xdg/picom.conf.example"
  install -Dm644 LICENSES/MIT "$pkgdir/usr/share/licenses/$pkgname/MIT"
  install -Dm644 LICENSES/MPL-2.0 "$pkgdir/usr/share/licenses/$pkgname/MPL-2.0"
}

# getver: github.com/yshui/picom/releases
# vim: ts=2 sw=2 et
