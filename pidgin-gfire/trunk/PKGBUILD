# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: LookTJ <jesus[dot]christ[dot]i[dot]love[at]gmail[dot]com>

pkgname=pidgin-gfire
pkgver=0.9.4
pkgrel=4
pkgdesc="Gfire is an Plugin for the Pidgin IM client which allows you to connect the Xfire network."
arch=('i686' 'x86_64')
url="http://gfireproject.org/"
license=('GPL')
depends=('glib2' 'pidgin' 'libnotify' 'gtk2')
makedepends=('pkgconfig' 'intltool' 'libtool')
source=("http://downloads.sourceforge.net/gfire/pidgin-gfire-$pkgver.tar.bz2" "gfire-libnotify.patch")
md5sums=('7167828fd77200603a318afdd4d9ebd2'
         '5d6fc2b98837fbebba6bef2648699d5e')
options=(!libtool)

build() {
    cd "$srcdir/pidgin-gfire-$pkgver"

    patch -p0 -i ../gfire-libnotify.patch
    sed "/gthread.h/d" -i src/gf_base.h
    ./autogen.sh
    ./configure --prefix=/usr --enable-libnotify
    make
}

package() {
    cd "$srcdir/pidgin-gfire-$pkgver"

    make DESTDIR="$pkgdir" install
}
