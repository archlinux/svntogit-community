# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: Hugo Doria <hugodoria@gmail.com>
# Contributor pidgin-libnotify: 3ED <kas1987@o2.pl>

pkgname=pidgin-libnotify
pkgver=0.14
pkgrel=10
arch=('i686' 'x86_64')
pkgdesc="Pidgin plugin that enables popups when someone logs in or messages you"
url="http://gaim-libnotify.sourceforge.net/"
license=('GPL')
depends=('pidgin' 'libnotify')
makedepends=('intltool')
source=(https://downloads.sourceforge.net/gaim-libnotify/$pkgname-$pkgver.tar.gz
        language_fixes.patch
        pidgin-libnotify-0.14-libnotify-0.7.patch
        pidgin-libnotify-showbutton.patch
        pidgin-libnotify-getfocus.patch)
md5sums=('bfb5368b69c02d429b2b17c00a6673c0'
         'c89e3cdc5db3525e37be4687f1e17d0a'
         '05538625f14d9f2c12adae5fa6a1fa26'
         'efe1e86aa1e0bc9f7d20efe7f34ad4a7'
         'e624998744d1c2fb8e2c6be94b04c38a')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../language_fixes.patch
  patch -Np0 -i ../pidgin-libnotify-0.14-libnotify-0.7.patch
  patch -Np0 -i ../pidgin-libnotify-showbutton.patch
  patch -Np0 -i ../pidgin-libnotify-getfocus.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-deprecated
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
