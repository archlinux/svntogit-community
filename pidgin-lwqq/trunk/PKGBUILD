# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=pidgin-lwqq
pkgver=0.2a.20130910
_commit=72ab9e26e1dcfd91fa76230f3dc26f887b83e51d
pkgrel=1
pkgdesc="A pidgin plugin based on lwqq, a excellent safe useful library for webqq protocol"
arch=('i686' 'x86_64')
url="https://github.com/xiehuc/pidgin-lwqq"
license=('GPL3')
depends=('libpurple' 'curl' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'libev' 'js185')
optdepends=('pidgin: Multi-protocol instant messaging client'
	    'telepathy-haze: Empathy plugin support')
makedepends=('git' 'cmake')
install=$pkgname.install
source=("git://github.com/xiehuc/pidgin-lwqq.git#commit=$_commit"
#source=("git://github.com/xiehuc/pidgin-lwqq.git#tag=$pkgver"
        "git://github.com/xiehuc/lwqq.git")

prepare() {
  cd "$srcdir/$pkgname"
  git submodule init
  git config submodule."liblwqq".url "$srcdir/lwqq"
  git submodule update
}

build() {
  cd "${srcdir}/$pkgname"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="${pkgdir}" install
}

sha512sums=('SKIP'
            'SKIP')
