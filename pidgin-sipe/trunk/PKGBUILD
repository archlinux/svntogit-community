# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Gicu Gorodenco <cyclopsihus@gmail.com>
# Contributor: MaLiK <malik_cjm@o2.pl>
# Contributor: dpevp <daniel.plaza.espi@gmail.com>
# Contributor: Magnus Therning <magnus@therning.org>
# Contributor: Anthony <anthony@littlegno.me>

pkgname=pidgin-sipe
pkgver=1.19.0
pkgrel=2
pkgdesc="Third-party Pidgin plugin for Microsoft Office 365/Lync/LCS/OCS"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://sipe.sourceforge.net/"
depends=('gmime' 'libpurple')
makedepends=('intltool')
options=('!buildflags')
source=(http://downloads.sourceforge.net/project/sipe/sipe/$pkgname-$pkgver/$pkgname-$pkgver.tar.bz2
        0001-Fix-278-488-error-after-libnice-upgrade.patch)
md5sums=('0022c30982527c9cf94e4ad0a162aa08'
         '5fe05bae567b8ff764b76e8a290b42cb')

prepare() {
  cd $pkgname-$pkgver

  # https://sourceforge.net/p/sipe/bugs/278/
  patch -Np1 -i ../0001-Fix-278-488-error-after-libnice-upgrade.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --disable-telepathy

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
