# Maintainer: Connor Behan <connor.behan@gmail.com>

pkgname=pidgin-talkfilters
pkgver=2.7.0
pkgrel=4
pkgdesc="Implements GNU talkfilters in pidgin chats"
arch=('x86_64')
url="https://bitbucket.org/rekkanoryo/purple-plugin-pack"
license=('GPL')
depends=('libpurple' 'talkfilters')
makedepends=('intltool' 'gettext' 'python2' 'pidgin')
source=(https://bitbucket.org/rekkanoryo/purple-plugin-pack/downloads/purple-plugin-pack-$pkgver.tar.bz2)
sha256sums=('2bbcf5e778a33968ba7f2864d2a6cb526a8984be3e4151642a583eee8eafb03c')

build() {
  cd "$srcdir"/purple-plugin-pack-$pkgver

  sed -i -e 's|GETTEXT_PACKAGE=plugin_pack|GETTEXT_PACKAGE=pidgin_talkfilters|'\
    configure.ac
  autoreconf -vfi
  PYTHON=/usr/bin/python2 \
    ./configure --prefix=/usr --with-plugins=talkfilters

  make
}

package() {
  cd "$srcdir"/purple-plugin-pack-$pkgver

  make DESTDIR="$pkgdir" install
}
