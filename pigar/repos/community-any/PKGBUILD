# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pigar
pkgname=("pigar" "python2-pigar")
pkgver=0.9.0
pkgrel=1
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-colorama' 'python2-colorama'
             'python2-futures' 'python-requests' 'python2-requests')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Damnever/pigar/archive/v$pkgver.tar.gz")
sha512sums=('c1d5b0145bb9b3f964af103146946fe99f1324bff03468bfc3c4316e266456422f2c1a7d8a6304a5b3e6812d2c3bcdc23d501afb013d90277321b22e075dd7e7')

prepare() {
  cp -a pigar-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pigar-$pkgver
  python setup.py build

  cd "$srcdir"/pigar-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pigar-$pkgver
  python -m unittest discover pigar/tests/ -t . -v

  cd "$srcdir"/pigar-$pkgver-py2
  python2 -m unittest discover pigar/tests/ -t . -v
}

package_pigar() {
  depends=('python-setuptools' 'python-colorama' 'python-requests')

  cd pigar-$pkgver
  python3 setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pigar() {
  depends=('python2-setuptools' 'python2-colorama' 'python2-requests' 'python2-futures')

  cd pigar-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/pigar{,2}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
