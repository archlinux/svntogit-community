# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pigar
pkgname=("pigar" "python2-pigar")
pkgver=0.6.10
pkgrel=2
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-colorama' 'python2-colorama'
             'python2-futures' 'python-gevent' 'python2-gevent' 'git')
source=("git+https://github.com/Damnever/pigar.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a pigar{,-py2}
}

build() {
  cd "$srcdir"/pigar
  python setup.py build

  cd "$srcdir"/pigar-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pigar
  python -m unittest discover pigar/tests/ -t . -v

  cd "$srcdir"/pigar-py2
  python2 -m unittest discover pigar/tests/ -t . -v
}

package_pigar() {
  depends=('python-setuptools' 'python-colorama' 'python-gevent')

  cd pigar
  python3 setup.py install -O1 --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pigar() {
  depends=('python2-setuptools' 'python2-colorama' 'python2-gevent' 'python2-futures')

  cd pigar-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/pigar{,2}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
