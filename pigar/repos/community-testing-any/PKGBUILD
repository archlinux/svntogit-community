# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pigar
pkgver=0.10.0
pkgrel=2
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
depends=('jupyter-nbformat' 'python-setuptools' 'python-colorama' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Damnever/pigar/archive/v$pkgver.tar.gz")
sha512sums=('5ee2d211015d06750c25fc2e5f61caab237f584ab3cf71a091cd086e23b14a797b84c03a8779ffd43f1cf0dd46657cb2d3829ef9fb1b005e652345227be36c5a')

build() {
  cd pigar-$pkgver
  python setup.py build
}

check() {
  cd pigar-$pkgver
  python -m unittest discover pigar/tests/ -t . -v
}

package() {
  cd pigar-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
