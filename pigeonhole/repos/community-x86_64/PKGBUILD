# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian Köhler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.3.15
# Make sure to bump pkgrel if changing this.

_dcmajor="$(awk -F'.' 'BEGIN{OFS=FS} NF{NF--};1' <<< "${_dcpkgver}")"

pkgname=pigeonhole
pkgver=0.5.15
pkgrel=1

pkgdesc='Sieve implementation for Dovecot'
url='https://pigeonhole.dovecot.org/'
arch=('x86_64')
license=('LGPL')

depends=("dovecot=${_dcpkgver}")

conflicts=('dovecot-sieve' 'pigeonhole-hg')

source=("https://pigeonhole.dovecot.org/releases/2.3/dovecot-${_dcmajor}-pigeonhole-${pkgver}.tar.gz"{,.sig})

sha256sums=('e1498f50cef74c351a57474cc423b008627ab1ab60724b859283ead6d00550d0'
            'SKIP')
validpgpkeys=(
  '42F3CD50D4F25A41833BEE3704D62B1E3DFBB4F4' # Stephan Bosch <stephan@rename-it.nl>
  '2BE74AAB3EE754DFB9C80D3318A348AEED409DA1'
)


prepare() {
  cd "dovecot-${_dcmajor}-pigeonhole-${pkgver}"

  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      patch -p1 -N -l -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
  cd "dovecot-${_dcmajor}-pigeonhole-${pkgver}"

  ./configure \
    --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static
  make
}

check() {
  cd "dovecot-${_dcmajor}-pigeonhole-${pkgver}"
  make check
}

package() {
  cd "dovecot-${_dcmajor}-pigeonhole-${pkgver}"

  make DESTDIR="$pkgdir" install
}
