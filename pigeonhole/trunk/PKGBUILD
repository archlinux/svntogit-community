# $Id$
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian Köhler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.2.33.2
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.4.21
pkgrel=1

pkgdesc='Sieve implementation for Dovecot'
url='http://pigeonhole.dovecot.org/'
arch=('x86_64')
license=('LGPL')

depends=('dovecot='$_dcpkgver)

conflicts=('dovecot-sieve' 'pigeonhole-hg')

source=("https://pigeonhole.dovecot.org/releases/2.2/dovecot-2.2-pigeonhole-$pkgver.tar.gz"{,.sig}
        dovecot.conf)

sha256sums=('4ae09cb788c5334d167f5a89ee70b0616c3231e5904ad258ce408e4953cfdd6a'
            'SKIP'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858')
validpgpkeys=('42F3CD50D4F25A41833BEE3704D62B1E3DFBB4F4') # Stephan Bosch <stephan@rename-it.nl>

build() {
  cd dovecot-2.2-pigeonhole-$pkgver

  ./configure --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static
  make
}

check() {
  cd dovecot-2.2-pigeonhole-$pkgver
  make check
}

package() {
  cd dovecot-2.2-pigeonhole-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}
