# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Laszlo Papp <djszapi2@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pigz
pkgver=2.2.5
pkgrel=1
pkgdesc="Parallel implementation of the gzip file compressor"
arch=('i686' 'x86_64')
url="http://www.zlib.net/pigz/"
license=('custom')
depends=('zlib')
source=(http://www.zlib.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('188f769ccedc403893d03049ad31e969')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  install -Dm755 "${srcdir}"/${pkgname}-$pkgver/${pkgname} \
    "${pkgdir}"/usr/bin/${pkgname} 

  pushd "${pkgdir}"/usr/bin
    ln -s pigz unpigz 
  popd

  install -Dm644 "${srcdir}"/${pkgname}-$pkgver/${pkgname}.1 \
    "${pkgdir}"/usr/share/man/man1/${pkgname}.1
  install -Dm644 "${srcdir}"/${pkgname}-$pkgver/${pkgname}.pdf \
    "${pkgdir}"/usr/share/doc/${pkgname}/${pkgname}.pdf
  install -Dm644 "${srcdir}"/${pkgname}-$pkgver/README \
    "${pkgdir}"/usr/share/licenses/${pkgname}/README
}
