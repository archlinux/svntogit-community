# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=pingus
pkgver=0.7.5
pkgrel=1
pkgdesc="A Lemmings clone, i.e. a level-based puzzle game."
arch=('i686' 'x86_64')
url="http://pingus.seul.org"
license=('GPL')
depends=('gcc-libs' 'sdl_image' 'sdl_mixer' 'libgl' 'boost-libs>=1.47')
makedepends=('scons' 'boost>=1.47' 'mesa')
source=(http://pingus.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2 pingus.desktop)
md5sums=('68e0bd1327e0eedcaab64d64528540ba'
         '9eec34047bdcff49e08f41e81764e20c')
sha1sums=('65493f9c9914bb401dbcfe1fe6bf2fad2388b755'
          '579a1144f161ce89e6e024cea37210149b89c0c0')

build() {
  cd ${pkgname}-${pkgver}

  scons prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR="${pkgdir}" PREFIX="/usr"

  install -D -m644 "${srcdir}/pingus.desktop" "${pkgdir}/usr/share/applications/pingus.desktop"
}
