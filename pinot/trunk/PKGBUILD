# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=pinot
pkgver=1.08
pkgrel=9
pkgdesc='Personal search and metasearch tool'
arch=('i686' 'x86_64')
url='http://code.google.com/p/pinot-search/'
license=('GPL')
depends=('gtkmm3' 'xapian-core' 'sqlite' 'libxml++' 'curl' 'gmime' 'libexttextcat' 'dbus-glib'
         'shared-mime-info' 'libexif' 'taglib' 'hicolor-icon-theme' 'cairo' 'exiv2' 'boost-libs')
makedepends=('boost' 'desktop-file-utils' 'subversion' 'autoconf' 'automake')
optdepends=('unzip: ZIP files extraction'
            'poppler: PDF to text conversion'
            'catdvi: DVI to text conversion'
            'djvulibre: DjVu text extraction'
            'unrtf: RTF to HTML conversion'
            'antiword: MS Word to text conversion'
            'catdoc: XLS and PPT to text conversion')
options=('!emptydirs')
changelog=$pkgname.changelog
source=()

_svntrunk=http://pinot-search.googlecode.com/svn/tags/v${pkgver}

prepare() {
  cd "${srcdir}"

  msg "Connecting to SVN server...."
  svn co "$_svntrunk"
  msg "SVN checkout done or server timeout"

  msg "Starting build..."
  rm -rf "${srcdir}/${pkgver}-build"
  svn export "${srcdir}/v${pkgver}" "${srcdir}/${pkgver}-build"
  cd "${srcdir}/${pkgver}-build"

  x='/usr/share/libexttextcat/'
  sed -i 's|/LM/russian.lm|/LM/russian-windows1251.lm|' textcat_conf.txt
  sed -i "s|$x|${x}LM/|" \
    textcat_conf.txt \
    textcat3_conf.txt \
    textcat31_conf.txt \
    textcat32_conf.txt
  sed -i -e "s|.*russian$|${x}LM/russian-iso8859_5.lm   russian-iso8859_5\n\
${x}LM/russian-koi8_r.lm      russian-koi8_r\n\
${x}LM/russian-windows1251.lm russian-windows1251|" textcat3_conf.txt

#configure to be run with the desired below args
  sed -i '$s/^/#/' autogen.sh
}


build() {
  cd "${srcdir}/${pkgver}-build"

  # glibmm 2.46 uses C++11 features
  CXXFLAGS+=' -std=c++11'

  ./autogen.sh

  LIBS="-lboost_system" ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --enable-gtkmm3=yes

  make
}

package() {
  cd "${srcdir}/${pkgver}-build"

  make DESTDIR="${pkgdir}" install
}
