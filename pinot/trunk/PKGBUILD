# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=pinot
pkgver=0.98
pkgrel=1
pkgdesc='Personal search and metasearch tool'
arch=('i686' 'x86_64')
url='http://pinot.berlios.de/'
license=('GPL')
depends=('gtkmm' 'xapian-core' 'libtextcat' 'sqlite3' 'libxml++' 'curl'
         'gmime' 'dbus-glib' 'shared-mime-info' 'libexif' 'taglib' 
         'hicolor-icon-theme' 'cairo' 'exiv2')
makedepends=('boost' 'desktop-file-utils')
optdepends=('unzip: ZIP files extraction'
            'poppler: PDF to text conversion'
            'catdvi: DVI to text conversion'
            'djvulibre: DjVu text extraction'
            'unrtf: RTF to HTML conversion'
            'antiword: MS Word to text conversion'
            'catdoc: XLS and PPT to text conversion'
            'deskbar-applet: Pinot Deskbar-Applet module')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('8a89a73a48344074aa8f4534ce68fd18e3d84553645cef864c137ab21d8d341c')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  sed -i 's|/usr/share/libtextcat/|/usr/share/libtextcat/LM/|' textcat_conf.txt
  sed -i -e "s|.*russian$|/usr/share/libtextcat/LM/russian-iso8859_5.lm   russian-iso8859_5\n\
/usr/share/libtextcat/LM/russian-koi8_r.lm      russian-koi8_r\n\
/usr/share/libtextcat/LM/russian-windows1251.lm russian-windows1251|" textcat_conf.txt

  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  # Remove Deskbar-Applet handler
  rm -rf ${pkgdir}/usr/lib/deskbar-applet/handlers
}
