# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Konrad <konrad AT knauber DOT name>
# Contributor: Javier Steinaker <jsteinaker@gmail.com>

pkgname='pinta'
pkgver=2.0.2
pkgrel=1
pkgdesc="Drawing/editing program modeled after Paint.NET. It's goal is to provide a simplified alternative to GIMP for casual users"
arch=('any')
url="https://pinta-project.com/"
license=('MIT')
depends=('gtk3>=3.24.21' 'dotnet-runtime')
makedepends=('intltool' 'dotnet-sdk')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/PintaProject/Pinta/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/PintaProject/Pinta/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc")
sha512sums=('2853ad40f897b92aeb72bbf02ae59e5c7813ba49debea73510d50c06ecee44669615573ed195d9a9d9c8a3f26203ef751c43c9ba255e311fa87783674f3c1c5f'
            'SKIP')
validpgpkeys=('5BDA30D0C2E985E598089E444CF80E80E77C5C35') # Cameron White

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i '/^PINTA_BUILD_OPTS/ s/$/ -maxcpucount:1/' Makefile.in
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" license-*.txt readme.md
}
