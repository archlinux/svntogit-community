# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=pion
pkgver=5.0.5
pkgrel=2
pkgdesc="A C++ development library for implementing lightweight HTTP interfaces"
url="https://github.com/cloudmeter/pion"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'openssl' 'log4cpp')
makedepends=('boost')
provides=('pion-net')
conflicts=('pion-net')
replaces=('pion-net')
source=(https://github.com/cloudmeter/pion/archive/$pkgver.zip)
md5sums=('b8bf0fdac882d9bd4332d3c6d3bccbd8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sh autogen.sh

  # override configure bug (forces debug even when nobody asked for it)
  sed -i "s/ -ggdb//g" configure
  # override Makefile bug
  sed -i "/docs:/ s/doxygen-doc//" Makefile.in

  ./configure --prefix=/usr --with-plugins=/usr/lib/pion/plugins --disable-doxygen-doc

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}
