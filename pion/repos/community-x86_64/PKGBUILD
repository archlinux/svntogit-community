# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=pion
pkgver=5.0.4
pkgrel=2
pkgdesc="A C++ development library for implementing lightweight HTTP interfaces"
url="https://github.com/cloudmeter/pion"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'openssl' 'log4cpp')
makedepends=('boost')
provides=('pion-net')
conflicts=('pion-net')
replaces=('pion-net')
source=(https://github.com/cloudmeter/pion/archive/$pkgver.zip \
        include_boost_units.diff)
md5sums=('5807d39beb0c80fdef5b8ad5cee71c1c'
         'b8c95c443121e4137f107814272c05f8')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 < "$srcdir/include_boost_units.diff" || true
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sh autogen.sh

  # override configure bug (forces debug even when nobody asked for it)
  sed -i "s/ -ggdb//g" configure
  # override Makefile bug
  sed -i "/docs:/ s/doxygen-doc//" Makefile.in

  ./configure --prefix=/usr --with-plugins=/usr/lib/pion/plugins --disable-doxygen-doc

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}
