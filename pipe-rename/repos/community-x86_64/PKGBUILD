# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pipe-rename
pkgver=1.5.0
pkgrel=1
pkgdesc='Rename your files using your favorite text editor'
arch=('x86_64')
url='https://github.com/marcusbuffett/pipe-rename'
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=(
  "$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
  'LICENSE'
)
sha512sums=('2b898f856446eb25ac651956a30a677dc2ca3c7dfcbce64cb9ca50e91e5c4e3b5e8069312fbd26e25b132f03764e9011a3112120e98bdf85165db6e973eca7d1'
            '3a3dbfd7a5a8d02a3a96bb1652bd25cae21d1080b941ecd3bee3f46e98b4a2f30b4a7ecdeb1e55ec2bf8bfefbeb9500287acb964ccb7149a405ee28eebd05889')
b2sums=('bd279c96d7dac07b302920a01447a909e8a7c36f4ac392bd5783c762a4f4f07a494cc2d545b12962bfdcde11cb525d00647777cca1c6b12aa64e8db29e163017'
        'abec091be86ec71b15a70251d769d4177639881c28d71fb9127d86cf711514e0fe40e80edd645e97df532980baf13380433fd2b497e03b4021d49e87434a4965')

prepare() {
  cd "$pkgname-$pkgver"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --frozen --release
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
