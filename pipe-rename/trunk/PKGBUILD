# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pipe-rename
pkgver=1.4.2
pkgrel=1
pkgdesc='Rename your files using your favorite text editor'
arch=('x86_64')
url='https://github.com/marcusbuffett/pipe-rename'
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=(
  "$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
  'LICENSE'
)
sha512sums=('6079527b355a89ef31ad072c3d9a27ca2eaa60225341b466aaf38b3383c1c4706ff91d91b189884232d1462bb81fbf8193a99aae4200ae79d3a5148212e0a746'
            '3a3dbfd7a5a8d02a3a96bb1652bd25cae21d1080b941ecd3bee3f46e98b4a2f30b4a7ecdeb1e55ec2bf8bfefbeb9500287acb964ccb7149a405ee28eebd05889')
b2sums=('9a04121bf5aedd53c032d37fa93b1fe42b8a08a4f57f02ad0f272a2d447ba5376d6a46bf715e2703124a2a665d124defa338665ceb69d17da6ba09ffdc88a202'
        'abec091be86ec71b15a70251d769d4177639881c28d71fb9127d86cf711514e0fe40e80edd645e97df532980baf13380433fd2b497e03b4021d49e87434a4965')

prepare() {
  cd "$pkgname-$pkgver"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --frozen --release
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
