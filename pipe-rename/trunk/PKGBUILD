# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=pipe-rename
pkgver=1.6.0
pkgrel=1
pkgdesc='Rename your files using your favorite text editor'
arch=('x86_64')
url='https://github.com/marcusbuffett/pipe-rename'
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
options=('!lto')
source=(
  "$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
  'LICENSE'
)
sha512sums=('1ec104e034b96b0060246fa65294f11425bd2eae248d4e3ec8d646f0d90fd877a36f12870bffd52abef18768689837667417289bd5471211064c1e4c862f3dac'
            '3a3dbfd7a5a8d02a3a96bb1652bd25cae21d1080b941ecd3bee3f46e98b4a2f30b4a7ecdeb1e55ec2bf8bfefbeb9500287acb964ccb7149a405ee28eebd05889')
b2sums=('c9b7c580ecb43b39ea9989237e5d7e1bae90489181e93ca5970fc243de47c2603ed5caf2ad00ea70c6fe3a81506498f130cce59e392dbd4c329f9cfdb4b5368a'
        'abec091be86ec71b15a70251d769d4177639881c28d71fb9127d86cf711514e0fe40e80edd645e97df532980baf13380433fd2b497e03b4021d49e87434a4965')

prepare() {
  cd "$pkgname-$pkgver"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"

  cargo build --frozen --release
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/renamer

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/LICENSE"
}
