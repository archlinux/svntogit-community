# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi
pkgver=0.99
pkgrel=2
pkgdesc="Editor for audio/video projects using the GStreamer framework"
arch=('x86_64')
url="http://www.pitivi.org/"
license=('LGPL')
depends=('gtk3' 'gst-editing-services' 'gst-plugins-good' 'gst-python'
         'libnotify' 'python-gobject' 'python-numpy' 'python-cairo'
         'python-matplotlib' 'gst-plugins-bad' 'python-dbus' 'gst-transcoder' 'gsound')
makedepends=('intltool' 'itstool' 'meson')
optdepends=('frei0r-plugins: additional video effects, clip transformation feature'
            'gst-libav: additional multimedia codecs'
            'gst-plugins-ugly: additional multimedia codecs')
source=(https://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('c9d02fd669801e0de94b5ca06179da6e8a7e1cb666b843fc12df6f0632db8f7c')

prepare() {
  cd $pkgname-$pkgver
  sed -e '/tests/d' -i meson.build # needs gst-devtools
}

build() {
  cd $pkgname-$pkgver
  meson --prefix=/usr build
  ninja -C build
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install -C build
}
