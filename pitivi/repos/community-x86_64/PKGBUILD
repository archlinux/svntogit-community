# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi
pkgver=0.999
pkgrel=3
pkgdesc="Editor for audio/video projects using the GStreamer framework"
arch=('x86_64')
url="http://pitivi.org/"
license=('LGPL')
depends=('gsound' 'gst-editing-services' 'gst-plugins-bad' 'gst-plugins-good' 'gst-python'
         'gst-transcoder' 'gtk3' 'libnotify' 'python-cairo' 'python-gobject' 'python-matplotlib'
         'python-numpy')
makedepends=('intltool' 'itstool' 'meson')
optdepends=('frei0r-plugins: additional video effects, clip transformation feature'
            'gst-libav: additional multimedia codecs'
            'gst-plugins-ugly: additional multimedia codecs')
source=(https://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('cb6f8a831383e698c2117ca7caec77b8167a6b2e2b0fb9c375e6a4f7c815b757')

prepare() {
  cd $pkgname-$pkgver

  # Don't use legacy path for AppStream metainfo file
  # https://gitlab.gnome.org/GNOME/pitivi/commit/e31aaf78ac9660fc480d55bede5f0f27878a1efc
  sed -i "s/install_dir : join_paths(get_option('datadir'), 'appdata')/install_dir : join_paths(get_option('datadir'), 'metainfo')/" data/meson.build

  sed -e "s|dependency('python3'|dependency('python3-embed'|" -i meson.build # Fix build with python 3.8
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
