# Maintainer:  Jonathan Steel <jsteel at archlinux.org>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=pixz
pkgver=1.0.7
pkgrel=4
pkgdesc="Parallel, indexed xz compressor"
arch=('x86_64')
url="https://github.com/vasi/pixz"
license=('custom')
depends=('libarchive')
makedepends=('asciidoc')
source=(https://github.com/vasi/pixz/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('ed227a0a3147db9380f6fe59cc87cd05')
sha256sums=('d1b6de1c0399e54cbd18321b8091bbffef6d209ec136d4466f398689f62c3b5f')

build() {
  cd $pkgname-$pkgver

  manpage=true ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
