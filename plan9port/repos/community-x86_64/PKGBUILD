# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: phrakture <aaronmgriffin--gmail--com>
# Contributor: Fazlul Shahriar
# Contributor: Chris Brannon <cmbrannon79@gmail.com>

pkgname=plan9port
pkgver=20150629
pkgrel=1
pkgdesc='Ports of applications from Plan 9'
arch=('x86_64' 'i686')
url='http://swtch.com/plan9port/'
license=('custom')
depends=('xorg-server' 'libxt' 'libxext' 'fuse')
makedepends=('gendesk' 'git')
optdepends=('python2: for the codereview script')
provides=('plan9')
install='plan9.install'
options=('!zipman' 'staticlibs')
source=("git://github.com/9fans/plan9port.git#commit=71de840f054e52a8a077b2333edc1f4243480359"
        'plan9.sh' 'acme.png' 'acme.desktop' 'acme.sh')
sha256sums=('SKIP'
            '60df44d57b06962f6860ee501a947d165ab8ccd01dd01ba47c82bc729d2ed6d7'
            'b9046c4b7ed5e8e22bf3ea669d65ff681e616663b9743909a225884ac6fb261b'
            '57fdddb70ba969ff4aa2f6c5b68dec02abe937a69044fe00006726e1900d5ffe'
            '9c6eb7881f7333077d701f5201f7a620214d6c5c91c11b2ea7964483214e6cb2')


prepare() {
  # Generate a desktop shortcut for the Acme editor
  gendesk -f --pkgname acme --name Acme --genericname Editor \
    --comment 'Editor from Plan9' --categories 'Application;Development;TextEditor'

  # Use _DEFAULT_SOURCE instead of _BSD_SOURCE
  grep -rl _BSD_SOURCE "$pkgname" | xargs sed -i s':_BSD_SOURCE:_DEFAULT_SOURCE:g'
}

package() {
  cd "$pkgname"

  # Try the gentoo way for fixing hardcoded paths
  PLAN9=/usr/lib/plan9
  grep --null -l -r '/usr/local/plan9' | xargs --null sed -i "s!/usr/local/plan9!${PLAN9}!g"

  d=usr/lib

  ./INSTALL -b
  install -Dm755 ../plan9.sh "$pkgdir/etc/profile.d/plan9.sh"
  install -d "$pkgdir/$d"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r "$srcdir/$pkgname" "$pkgdir/$d/plan9"
  cd "$pkgdir/$d/plan9"
  ./INSTALL -c -r "$pkgdir/$d/plan9"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Clean up
  rm -rf .hg/
  rm -f .hgignore .hgtags
  find . -name '.cvsignore' -print0 |xargs -0 rm -f
  rm -f config install.log install.sum install.txt configure Makefile INSTALL \
    LICENSE

  # Fix python scripts
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'

  # Package text files
  for i in CHANGES CONTRIBUTORS README.md TODO; do
    install -m644 $i "$pkgdir/usr/share/doc/$pkgname"
    rm -f $i
  done

  # Decompress the plan9 man pages
  for i in `find $pkgdir/$d/plan9/man -type f`; do
    if [ ${i##*.} = "gz" ]; then
      gunzip "$i"
    fi
  done

  # Once more
  grep --null -l -r "$pkgdir$PLAN9" | xargs --null sed -i "s!$pkgdir$PLAN9!${PLAN9}!g" || true
  grep --null -l -r "/build/plan9port/pkg/plan9port/$PLAN9" | xargs --null sed -i "s!/build/plan9port/pkg/plan9port/$PLAN9!${PLAN9}!g" || true

  # Acme
  cd "$srcdir"
  install -Dm644 acme.desktop "$pkgdir/usr/share/applications/acme.desktop"
  install -Dm755 acme.sh "$pkgdir/usr/bin/acme"
  install -Dm644 acme.png "$pkgdir/usr/share/pixmaps/acme.png"

  ## Build man pages
  #export PLAN9="$pkgdir/usr/lib/plan9"
  #export PATH="$PLAN9/bin:/usr/bin"
  #cd "$pkgdir/usr/lib/plan9/dist"
  #9 mk man
}

# vim:set ts=2 sw=2 et:
