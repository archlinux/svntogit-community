# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=planarity
pkgver=3.0.2.0
pkgrel=1
pkgdesc='A library for implementing graph algorithms'
arch=(x86_64)
url='https://github.com/graph-algorithms/edge-addition-planarity-suite'
license=(BSD)
depends=(glibc)
makedepends=()
source=(https://github.com/graph-algorithms/edge-addition-planarity-suite/archive/Version_$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('40f4ee7bbd5d8535460c60fc0fc1f806b10909a1419618fd9235746a420a04c6')

build() {
  cd edge-addition-planarity-suite-Version_$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd edge-addition-planarity-suite-Version_$pkgver
  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
