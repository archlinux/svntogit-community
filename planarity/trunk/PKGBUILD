# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=planarity
pkgver=3.0.1.1
pkgrel=1
pkgdesc='A library for implementing graph algorithms'
arch=(x86_64)
url='https://github.com/graph-algorithms/edge-addition-planarity-suite'
license=(BSD)
depends=(glibc)
makedepends=()
source=(https://github.com/graph-algorithms/edge-addition-planarity-suite/archive/Version_$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e031e7977f1e291bfde9ed61cdbe43c21c45b7075a7b059b7f90636a53e78abc')

build() {
  cd edge-addition-planarity-suite-Version_$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd edge-addition-planarity-suite-Version_$pkgver
  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
