# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Ernesto Ricart <ernest2193@gmail.com>

_pkgname=Plank-Config
# Convert to lowercase
pkgname=${_pkgname,,}
pkgver=1.2
pkgrel=3
pkgdesc="A tool to configure Plank Dock"
arch=('i686' 'x86_64')
url="https://github.com/ernest21/Plank-Config/"
license=('GPL')
depends=('plank')
makedepends=('vala')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/ernest21/${_pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('7b5ee3842c079166d70aadb9eb14fd77')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    chmod +x ./autogen.sh
    NOCONFIGURE=1 ./autogen.sh
}
build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}
package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
