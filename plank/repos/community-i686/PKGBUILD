# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: dcelasun <dcelasun@gmail.com>

pkgname=plank
pkgver=0.9.1
pkgrel=1
pkgdesc='Elegant, simple, clean dock'
arch=('i686' 'x86_64')
url='https://launchpad.net/plank'
license=('GPL3')
depends=('bamf' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala')
install='plank.install'
source=("https://launchpad.net/plank/1.0/${pkgver}/+download/plank-${pkgver}.tar.xz")
sha256sums=('884cd57a0fcc78cf82cf01072dedc46c34b6f766871d5f773b68ee498fded9a2')

build() {
  cd plank-${pkgver}

  ./configure \
    --prefix='/usr' \
    --sysconfdir='/etc' \
    --disable-apport
  make
}

package() {
  cd plank-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
