# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>
# Contributor: dcelasun <dcelasun@gmail.com>

pkgname=plank
pkgver=0.10.0
pkgrel=1
pkgdesc='Elegant, simple, clean dock'
arch=('i686' 'x86_64')
url='https://launchpad.net/plank'
license=('GPL3')
depends=('atk' 'bamf' 'cairo' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk3'
         'libdbusmenu-glib' 'libdbusmenu-gtk3' 'libgee' 'libwnck3' 'libx11'
         'libxfixes' 'libxi' 'pango')
makedepends=('gnome-common' 'intltool' 'vala')
install='plank.install'
source=("https://launchpad.net/plank/1.0/${pkgver}/+download/plank-${pkgver}.tar.xz")
sha256sums=('4023eaa59cca3ee35032c23c2b011e1cabac812b8828af9ca55e6aaa084ad203')

build() {
  cd plank-${pkgver}

  ./configure \
    --prefix='/usr' \
    --sysconfdir='/etc' \
    --disable-apport
  make
}

package() {
  cd plank-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
