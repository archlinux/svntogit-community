# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com> 
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>
# Contributor: dcelasun <dcelasun at gmaild dot com>

pkgname=plank
pkgver=0.5.0
pkgrel=1
pkgdesc="Elegant, simple, clean dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
depends=('bamf' 'libdbusmenu-gtk3' 'libgee06' 'python2')
makedepends=('gnome-common' 'intltool' 'vala')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname}/1.0/${pkgver}/+download/${pkgname}-${pkgver}.tar.xz")
sha256sums=('bf0e14dbdc9d30d57e55cc88644ef6e6c296be924bbfc1b345cba1e0c9be1804')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var' --disable-gee-0.8
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/share/apport}
}

# vim: ts=2 sw=2 et:
