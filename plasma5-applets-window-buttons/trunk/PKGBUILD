# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma5-applets-window-buttons
pkgver=0.10.1
pkgrel=1
pkgdesc='Plasma 5 applet in order to show window buttons in your panels'
arch=(x86_64)
url='https://github.com/psifidotos/applet-window-buttons'
license=(GPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules)
conflicts=(libappletdecoration)
replaces=(libappletdecoration)
source=(https://github.com/psifidotos/applet-window-buttons/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('6d20f7888a76bf72d068d529aaaf57ebf89e60e52caa692b8075502b92fc541f')

build() {
  cmake -B build -S applet-window-buttons-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
