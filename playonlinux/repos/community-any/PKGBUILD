# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: K. Hampf <khampf@users.sourceforge.net>
# Contributor: Skunnyk <skunnyk@archlinux.fr>

pkgname=playonlinux
pkgver=4.2.10
pkgrel=1
pkgdesc="GUI for managing Windows programs under linux"
url="http://www.playonlinux.com/"
license=('GPL')
depends=('wine' 'imagemagick' 'xterm' 'wxpython' 'cabextract' 'unzip' 'mesa-demos' 'gnupg'
         'icoutils' 'xdg-user-dirs' 'libxmu' 'netcat' 'wget' 'p7zip')
arch=('any')
source=(http://www.playonlinux.com/script_files/PlayOnLinux/${pkgver/.0/}/PlayOnLinux_${pkgver/.0/}.tar.gz)
options=(!strip)
md5sums=('9869697c13010e88cf68607cea334dbd')

package() {
  cd "$srcdir/$pkgname"

  install -d $pkgdir/usr/share/$pkgname
  install -d $pkgdir/usr/bin 
  cp -r $srcdir/$pkgname/ $pkgdir/usr/share/

  echo "#!/bin/bash" > $pkgdir/usr/bin/$pkgname 
  echo "/usr/share/$pkgname/$pkgname \"\$@\"" >> $pkgdir/usr/bin/$pkgname
  chmod +x  $pkgdir/usr/bin/$pkgname

  install -D -m644 $srcdir/$pkgname/etc/PlayOnLinux.desktop $pkgdir/usr/share/applications/playonlinux.desktop
  sed -i "s/ %F//g" $pkgdir/usr/share/applications/playonlinux.desktop
}
