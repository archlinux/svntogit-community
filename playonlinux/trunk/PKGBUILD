# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: K. Hampf <khampf@users.sourceforge.net>
# Contributor: Skunnyk <skunnyk@archlinux.fr>

pkgname=playonlinux
pkgver=4.2.2
pkgrel=2
pkgdesc="GUI for managing Windows programs under linux"
url="http://www.playonlinux.com/"
license=('GPL')
depends=('wine' 'imagemagick' 'xterm' 'wxpython2.8' 'cabextract' 'unzip' 'mesa-demos' 'gnupg'
         'icoutils' 'xdg-user-dirs' 'libxmu' 'netcat' 'wget' 'p7zip')
arch=('any')
source=(http://www.playonlinux.com/script_files/PlayOnLinux/${pkgver/.0/}/PlayOnLinux_${pkgver/.0/}.tar.gz
        PlayOnLinux.desktop)
options=(!strip)
md5sums=('d038bfcadb2626bcd429746afa984831'
         'c35b61fb168c4ef7740ad113996ea359')

package() {
  cd "$srcdir/$pkgname"

  install -d $pkgdir/usr/share/$pkgname
  install -d $pkgdir/usr/bin 
  cp -r $srcdir/$pkgname/ $pkgdir/usr/share/

  echo "#!/bin/bash" > $pkgdir/usr/bin/$pkgname 
  echo "/usr/share/$pkgname/$pkgname \"\$@\"" >> $pkgdir/usr/bin/$pkgname
  chmod +x  $pkgdir/usr/bin/$pkgname

  # Needed for wxpython2.8 support, wxpython 3.0 is buggy with POL
  sed -i "s/wxversion.ensureMinimal/wxversion.select/g" $pkgdir/usr/share/$pkgname/python/mainwindow.py

  install -D -m644 $srcdir/$pkgname/etc/PlayOnLinux.desktop $pkgdir/usr/share/applications/playonlinux.desktop 
}
