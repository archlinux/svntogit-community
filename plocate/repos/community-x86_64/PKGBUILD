# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Hans-Nikolai Viessmann <hans AT viess DOT mn>

pkgname=plocate
pkgver=1.1.3
pkgrel=1
pkgdesc="Alternative to locate, faster and compatible with mlocate's database."
arch=('x86_64' 'i686')
url="https://plocate.sesse.net"
license=('GPL2')
install=plocate.install
depends=('zstd' 'liburing' 'systemd')
makedepends=('meson')
source=("${url}/download/${pkgname}-${pkgver}.tar.gz"
        'plocate.sysusers'
        'plocate.tmpfiles')
sha256sums=('5e6174838e53b1d63852215b02bededcb9878d8fc7f375e3b33afd59121811fa'
            '1713a8fc8b81f4a390bf8597c4c0e917474379002dcc984aad2f44218d10c82a'
            'adda399ac42f0faa002caab5bf932777c0006ae7c4387add859c9f77321b4202')

build() {
    cd $pkgname-$pkgver
    arch-meson \
        -Dinstall_cron=false \
        -Dinstall_systemd=true\
        -Dlocategroup=locate \
        -Dupdatedb_progname=updatedb.plocate \
        ../build
    meson compile -C ../build
}

package() {
    cd $pkgname-$pkgver
    DESTDIR="$pkgdir" meson install -C ../build

    chgrp 21 "${pkgdir}/usr/bin/plocate"
    chmod 2755 "${pkgdir}/usr/bin/plocate"

    install -Dm644 ../plocate.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/plocate.conf"
    install -Dm644 ../plocate.sysusers "$pkgdir/usr/lib/sysusers.d/plocate.conf"

    mv "$pkgdir/usr/share/man/man5/updatedb.conf.5" \
       "$pkgdir/usr/share/man/man5/updatedb.plocate.conf.5"
}
