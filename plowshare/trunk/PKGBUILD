# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: breakdown <breakdown(at)archlinux(dot)us>

pkgname=plowshare
pkgver=1.0.1
pkgrel=1
pkgdesc="Command-line downloader and uploader for Rapidshare, Mediafire and other file sharing websites."
arch=('any')
url="http://code.google.com/p/plowshare/"
license=('GPL3')
depends=('curl' 'recode' 'js185')
optdepends=('bash-completion: enable bash auto completion'
            'libcaca: ascii display for no X server'
            'tiv: ascii display for no X server'
            'aview: ascii display for no X server')
source=("http://plowshare.googlecode.com/archive/v$pkgver.tar.gz")
md5sums=('SKIP')

prepare() {
  cd $pkgname-*

  # executable version script
  chmod +x scripts/version

  # force version
  export PLOWSHARE_FORCE_VERSION=$pkgver
}

package() {
  cd $pkgname-*
  DESTDIR="$pkgdir/" PREFIX=/usr make
}

# vim:set ts=2 sw=2 et:
