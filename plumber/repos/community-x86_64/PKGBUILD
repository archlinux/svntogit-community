# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=plumber
pkgver=1.0.1
pkgrel=1
pkgdesc="Swiss army knife CLI tool for interacting with Kafka, RabbitMQ and other messaging systems."
arch=('x86_64')
url="https://github.com/batchcorp/plumber"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/batchcorp/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('da32454205e8d54813e28525157791b68bb06801f078b1dd189a9b47a2a4708585f9d84316d93f22a8978939a3495e025c22c06c4482a5d72a44e9cca47fc2a3')
b2sums=('e331b433efa6a54a81f6d8166e1ccca8e68cb3bd18849492313c72fd4d71bce497f485f3bf3dfa916f218a45193ca6c11d851dbf2d68ada1db881ae5ce365dbd')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -o plumber-bin .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 plumber-bin "${pkgdir}/usr/bin/${pkgname}"
}

