# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: SÃ¡ndor Nagy <sanya868@gmail.com>
# Contributor: Jeramy Harrison <dbralir@gmail.com>
# Contributor: Onni R. <onnir@iki.fi>

pkgname=png++
pkgver=0.2.9
pkgrel=2
pkgdesc='A C++ wrapper for libpng'
arch=('any')
url='http://savannah.nongnu.org/projects/pngpp/'
license=('custom')
depends=('libpng')
makedepends=('doxygen')
source=("https://download.savannah.gnu.org/releases/pngpp/png++-${pkgver}.tar.gz"{,.sig})
validpgpkeys=('C3B58EFD91A0102DDA636107A9FEF7426E5D4A8E')
sha256sums=('abbc6a0565122b6c402d61743451830b4faee6ece454601c5711e1c1b4238791'
            'SKIP')

build() {
  cd png++-${pkgver}

  make docs
}

package() {
  cd png++-${pkgver}

  make PREFIX="${pkgdir}/usr" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/png++
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/png++/
}

# vim: ts=2 sw=2 et:
