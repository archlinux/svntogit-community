# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Hannes Graeuler <hgraeule @ uos dot de>

pkgname=pngquant
pkgver=2.15.1
pkgrel=1
pkgdesc="Command-line utility to quantize PNGs down to 8-bit paletted PNGs"
arch=('x86_64')
url="https://pngquant.org/"
license=('BSD')
depends=('libpng' 'lcms2' 'libimagequant')
source=("https://github.com/kornelski/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('24856b28aca47ed78633fa625eb74b85782d62dacc4bca634fcedf2f6e95c4b7')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-openmp --with-libimagequant=/usr/lib
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -dm755 "$pkgdir/usr/bin"
    make DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
