# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=podman-compose
pkgver=1.0.3
pkgrel=2
pkgdesc='A script to run docker-compose.yml using podman'
arch=(any)
url='https://github.com/containers/podman-compose'
license=(GPL2)
depends=(podman podman-dnsname python-dotenv python-yaml)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz)
sha512sums=('54ee61e6f9cd946b54eed07285e13bec88bc3d718e55915422b563a4d9e79e4e087cd8040adb16aa2fc4546f536cc5b7e31b3e87295cb7c8281c9db61e6fbc71')
b2sums=('d0369a3070b2485efb778547bfbeb732b8ed9e7ba9092a976f89eb1d89c1ad4c3a1b849193d62ef9f7eebe82813245918b1f385cf4dbf71ee7a7f293461ce44d')

build() {
  cd $pkgname-$pkgver

  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" dist/*.whl
  find examples -type f -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
  install -vDm 644 {README.md,docs/*} -t "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
