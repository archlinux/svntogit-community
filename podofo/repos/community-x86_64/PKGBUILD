# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Preecha Patumchareonpol <yumyai at gmail.com> 

pkgname=podofo
pkgver=0.9.2
pkgrel=1
pkgdesc="A C++ library to work with the PDF file format"
arch=('i686' 'x86_64')
url="http://podofo.sourceforge.net"
license=('GPL')
depends=('openssl' 'libidn' 'lua51' 'fontconfig' 'libpng' 'libtiff')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('41966c57fa4266ac5cef80bfade0c8e7')

build() {
  cd ${pkgname}-${pkgver}

  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr .. \
        -DPODOFO_BUILD_SHARED=1 \
        -DPODOFO_HAVE_JPEG_LIB=1 \
        -DPODOFO_HAVE_PNG_LIB=1 \
        -DPODOFO_HAVE_TIFF_LIB=1
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR=$pkgdir install
}
