# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
epoch=1
pkgver=2.0.5
pkgrel=2
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('x86_64')
url="http://www.poedit.net/"
license=('MIT')
depends=('gtkspell3' 'lucene++' 'wxgtk3')
makedepends=('boost')
source=(https://github.com/vslavik/poedit/releases/download/v$pkgver-oss/poedit-${pkgver}.tar.gz)
sha512sums=('097c93306a5b1bc74c7fa0cd49bc7077046219cb1c5e07ea4a45a0487700642eced1f186810c3bf74da74b72d5ebe84efa541229589b42603cd6c678ba4d2950')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --libexecdir=/usr/lib/poedit --with-wx-config=/usr/bin/wx-config-gtk3
  make CPPFLAGS+=' -DUCHAR_TYPE=uint16_t'
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
