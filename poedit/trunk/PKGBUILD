# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
pkgver=1.6.4
pkgrel=1
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('i686' 'x86_64')
url="http://www.poedit.net/"
license=('MIT')
depends=('wxgtk' 'desktop-file-utils' 'lucene++' 'gtkspell')
makedepends=('boost' 'libsm')
install=poedit.install
#source=(http://www.poedit.net/dl/poedit-$pkgver.tar.gz)
source=(https://github.com/vslavik/poedit/releases/download/v$pkgver-oss/poedit-$pkgver.tar.gz
	build-fix.patch)
md5sums=('203e27826c1baa4de403ce6d1cc60444'
         '724aed37a6760c8ac9405f9ee4b70c05')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 <$srcdir/build-fix.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --libexecdir=/usr/lib/poedit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
