# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Vasco Costa <vasco.costa@meiodigital.com>

pkgname=pokerth
_realname=PokerTH
pkgver=0.7.1
pkgrel=9
pkgdesc="Poker game written in C++/QT4"
arch=('i686' 'x86_64')
url="http://www.pokerth.net/"
license=('GPL' 'custom')
depends=('curl' 'boost-libs>=1.43.0' 'gnutls' 'qt' 'sdl_mixer')
makedepends=('boost')
source=(http://downloads.sourceforge.net/sourceforge/pokerth/$_realname-$pkgver-src.tar.bz2)
md5sums=('a26a369bf3aa4b6ed92d0cadacb626e5')

build() {
  cd "$srcdir/$_realname-$pkgver-src"
  qmake $pkgname.pro || return 1
  make || return 1
}

package() {
  cd "$srcdir/$_realname-$pkgver-src"

  make INSTALL_ROOT="$pkgdir" install || return 1

  install -D -m755 $pkgname "$pkgdir/usr/bin/$pkgname" || return 1
  install -D -m644 docs/pokerth.1 "$pkgdir/usr/share/man/man1/pokerth.1" || return 1

  install -D -m644 data/data-copyright.txt "$pkgdir/usr/share/licenses/pokerth/data-copyright.txt" || return 1
  rm -f "$pkgdir/usr/share/pokerth/data/data-copyright.txt"
}
