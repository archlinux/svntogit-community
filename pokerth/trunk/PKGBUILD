# $Id$
# Maintainer:  Mateusz Herych <heniekk@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Vasco Costa <vasco.costa@meiodigital.com>

pkgname=pokerth
_realname=PokerTH
pkgver=0.9.5
pkgrel=1
pkgdesc="Poker game written in C++/QT4"
arch=('i686' 'x86_64')
url="http://www.pokerth.net/"
license=('GPL' 'custom')
depends=('curl' 'boost-libs' 'gsasl' 'gnutls'
         'qt' 'sdl_mixer' 'libircclient' 'tinyxml')
makedepends=('boost')
source=(http://downloads.sourceforge.net/sourceforge/pokerth/$_realname-$pkgver-src.tar.bz2)
md5sums=('4bdea98bfc142a825c64b77feb4667d0')

build() {
  cd "$srcdir/$_realname-$pkgver-src"

  sed -i '1 i #include <unistd.h>' src/third_party/qtsingleapplication/qtlocalpeer.cpp

  sed -i 's/QMAKE_CXXFLAGS += -std=gnu++0x/QMAKE_CXXFLAGS += -std=gnu++0x -DBOOST_FILESYSTEM_VERSION=2/' *.pro

  sed -i '23 i #include <libircclient/libirc_rfcnumeric.h>' src/net/common/ircthread.cpp

  # fix g++: error: unrecognized option '-no_dead_strip_inits_and_terms'
  sed \
    -e 's/QMAKE_LFLAGS += -no_dead_strip_inits_and_terms//' \
    -i zlib_compress.pro pokerth_game.pro pokerth_server.pro

  qmake $pkgname.pro
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver-src"

  make INSTALL_ROOT="$pkgdir" install

  install -D "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -D -m644 docs/pokerth.1 "$pkgdir/usr/share/man/man1/pokerth.1"
  install -D -m644 data/data-copyright.txt "$pkgdir/usr/share/licenses/pokerth/data-copyright.txt"
  rm -f "$pkgdir/usr/share/pokerth/data/data-copyright.txt"
}
