# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=polarssl
pkgver=1.3.8
pkgrel=2
pkgdesc="Portable cryptographic and SSL/TLS library"
arch=('i686' 'x86_64')
url="https://www.polarssl.org/"
license=('GPL2')
source=("https://polarssl.org/code/releases/polarssl-$pkgver-gpl.tgz")
sha1sums=('82ed8ebcf3dd53621da5395b796fc0917083691d')
depends=('glibc')
options=('staticlibs')

build() {
  cd "$pkgname-$pkgver"
  sed -i 's|//\(#define POLARSSL_THREADING_C\)|\1|' include/polarssl/config.h
  sed -i 's|//\(#define POLARSSL_THREADING_PTHREAD\)|\1|' include/polarssl/config.h
  LDFLAGS+=" -I../include " make SHARED=1 no_test
}

check() {
  cd "$pkgname-$pkgver"
  make SHARED=1 check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/usr" install
}
