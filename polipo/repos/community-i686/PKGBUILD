# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Jelle van der Waa <jelle vdwaa nl>
# Contributor: Thomas Holmquist <thomas@vorget.com>
# Contributor: Jan Boehringer <ja(a)bm-boehringer.de>
# Contributor: Alex Griffin <griffin.aj(a)gmail.com>

pkgname=polipo
pkgver=1.0.4.1
pkgrel=5
pkgdesc="A small and fast caching web proxy."
arch=('i686' 'x86_64')
url="http://www.pps.jussieu.fr/~jch/software/polipo/"
license=('GPL')
depends=('bash')
makedepends=('texinfo')
install=polipo.install
source=("http://freehaven.net/~chrisd/polipo/polipo-$pkgver.tar.gz"
        "polipo.cron" "polipo.rc.d" "polipo.conf.d" "polipo.service")
md5sums=('bfc5c85289519658280e093a270d6703'
         'bac0e1a871964c931eb5f7a369b3243c'
         '8f096b7d77a71e0772ce71a8c8b1b3e3'
         '685aa0c6070dee11c701932d23afcc6a'
         '109d0e8c15d669281ff14f8991ad0ed8')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make 	PREFIX="$pkgdir/usr" \
        MANDIR="$pkgdir/usr/share/man" \
        INFODIR="$pkgdir/usr/share/info" \
	LOCAL_ROOT="$pkgdir/usr/share/polipo/www" \
	DISK_CACHE_ROOT="$pkgdir/var/cache/polipo" \
  install 


  # install config files
  install -Dm 644 config.sample $pkgdir/etc/polipo/config.sample
  install -Dm 644 forbidden.sample $pkgdir/etc/polipo/forbidden.sample

  # install license
  install -Dm644 COPYING $pkgdir/usr/share/licenses/polipo/LICENSE

  # install daemon scripts / cron
  install -Dm 755  $srcdir/polipo.rc.d $pkgdir/etc/rc.d/polipo
  install -Dm 755  $srcdir/polipo.cron $pkgdir/usr/share/polipo/polipo.cron
  install -Dm 644  $srcdir/polipo.service $pkgdir/usr/lib/systemd/system/polipo.service
  install -Dm 644  $srcdir/polipo.conf.d $pkgdir/etc/conf.d/polipo.conf
}

# vim:set ts=2 sw=2 et:
