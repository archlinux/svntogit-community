# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=polybori
pkgver=0.8.3
pkgrel=3
pkgdesc="Library for polynomials over boolean rings"
arch=(i686 x86_64)
url="http://polybori.sourceforge.net/"
license=(GPL)
depends=(boost-libs python2 m4ri ntl)
makedepends=(boost scons)
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('0999722a019c4ee5835a115e76a1dfd6')

prepare() {
  cd $pkgname-$pkgver

# Build with NTL support and remove rpath (Fedora)
  sed -e "s/'\${_relative_rpath.*/''])/" -e "s/main_wrapper\.cc/& ntl_wrapper.cc/" -i SConstruct
}

build() {
  cd $pkgname-$pkgver 

  export CPPFLAGS="$CPPFLAGS -DPBORI_HAVE_NTL"
  scons prepare-install PYTHON=python2
}

package() {
  cd $pkgname-$pkgver

  scons install install-headers \
   PYTHON=python2 \
   PREFIX="$pkgdir"/usr \
   PYINSTALLPREFIX="$pkgdir"/usr/lib/python2.7/site-packages \
   MANDIR="$pkgdir"/usr/share/man
}

