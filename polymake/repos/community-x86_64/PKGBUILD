# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=polymake
pkgver=3.0r2
pkgrel=2
pkgdesc="Open source software for research in polyhedral geometry"
arch=(i686 x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(gmp perl-xml-writer perl-xml-libxml perl-xml-libxslt perl-term-readline-gnu)
makedepends=(normaliz cddlib lrs bliss-graphs ppl apache-ant boost) # singular
optdepends=('singular: ideal library' 'ppl: polytope library' 'java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-$pkgver-minimal.tar.bz2")
md5sums=('c1f5d71cdd86d0339c3e642494d6ac67')

prepare() {
  cd polymake-3.0
# fix lrs headers location
  sed -e 's|lrslib.h|lrslib/lrslib.h|g' -i bundled/lrs/apps/polytope/src/lrs_interface.cc \
      -i bundled/group/external/sympol/sympol/raycomputationlrs.cpp
  sed -e 's|<lrslib.h>|<lrslib/lrslib.h>|g' -e 's|\$lrs_path\/include|$lrs_path/include/lrslib|g' -i bundled/lrs/configure.pl
# fix singular detection
  sed -e 's|lresources|lsingular_resources|g' -i bundled/singular/configure.pl
}

build() {
  cd polymake-3.0
  ./configure --prefix=/usr
  make
}

package() {
  cd polymake-3.0
  make DESTDIR="$pkgdir" install
}
