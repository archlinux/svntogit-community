# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=polymake
pkgver=4.0.r1
pkgrel=1
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt
         perl-term-readline-gnu perl-term-readkey perl-json sympol)
makedepends=(ninja cddlib ant boost bliss libnsl jdk8-openjdk permlib)
optdepends=('java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-${pkgver/.r/r}-minimal.tar.bz2")
sha256sums=('b29de50dda6f657f2e82ef6acff62df1b51128a20c5d53bd97226ea22fdc3b52')

build() {
  cd polymake-${pkgver%.*}
  ./configure \
    --prefix=/usr \
    --with-cdd=/usr \
    --with-lrs=/usr \
    --with-permlib=/usr \
    --with-sympol=/usr
  make
}

package() {
  cd polymake-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
