# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mort Yao <soi@mort.ninja>
# Contributor: Alex Merry <dev@randomguy3.me.uk>

pkgname=polyml
pkgver=5.8.2
pkgrel=1
pkgdesc='Standard ML compiler originally written in Poly'
arch=(x86_64)
url='https://polyml.org/'
license=(LGPL)
conflicts=(mesa-demos)
options=(!libtool)
depends=(gmp libffi)
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
b2sums=('28abec92f8372779ca2b431a069f43a38f0560056edc37b615d5ddc48a9d5b17f2d56c35de3e8f38a1449873d2a7cfc419f6305c790ab77590a8b785f7cfc092')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --enable-shared \
    --prefix=/usr \
    --with-gmp \
    --with-system-libffi
  make
}

check() {
  make -C $pkgname-$pkgver -k check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
