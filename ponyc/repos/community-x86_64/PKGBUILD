# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ponyc
pkgver=0.11.4
pkgrel=1
pkgdesc='An actor model, capabilities, high performance programming language'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')
depends=('llvm-libs')
makedepends=('llvm' 'git')
checkdepends=('pcre2')
options=('!strip')
optdepends=('pcre2: Needed for the regex package.')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ponylang/ponyc/archive/$pkgver.tar.gz")
sha512sums=('35b3a4722d00e4fdd2fa3a09b3bde86649fc516d9128f49d1db3b3c42e5dd3b0b6a15f656fa737213931832f3c5e6f785f7a07a6487ad65c0650e245d2dadefd')

build() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose=

  mkdir docs
  build/release/ponyc packages/stdlib -rexpr -g -o docs
}

check() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= test
}

package() {
  cd ponyc-$pkgver
  # The makefile is very strange and requires almost as much post-correction
  # as to simply do the installation myself.  Even with patches to the
  # makefile to stop it attempting to symlink locations it has no business
  # touching.
  #make config=release prefix=/usr destdir="$pkgdir"/usr/lib/pony/"$pkgver" install

  install -Dm755 build/release/ponyc "$pkgdir"/usr/lib/pony/"$pkgver"/bin/ponyc
  install -Dm644 build/release/libponyrt-pic.a "$pkgdir"/usr/lib/libponyrt.a
  install -Dm644 build/release/libponyc.a "$pkgdir"/usr/lib/libponyc.a
  install -Dm644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h

  # Ponyc is designed to read its standard packages from the same directory.
  install -dm755 "$pkgdir"/usr/bin
  ln -sf /usr/lib/pony/"$pkgver"/bin/ponyc "$pkgdir"/usr/bin

  cp -dr --no-preserve=ownership packages "$pkgdir"/usr/lib/pony/"$pkgver"

  # Let's include the examples as documentation is scarce.
  install -dm755 "$pkgdir"/usr/share/doc/pony
  cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

  cp -dr docs/* "$pkgdir"/usr/share/doc/pony

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
