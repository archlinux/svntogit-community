# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ponyc
pkgver=0.28.1
pkgrel=1
pkgdesc='An actor model, capabilities, high performance programming language'
url='https://ponylang.org/'
arch=('x86_64')
license=('BSD')
depends=('llvm7-libs')
makedepends=('llvm7')
checkdepends=('pcre2')
options=('!strip')
optdepends=('pcre2: Needed for the regex package')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ponylang/ponyc/archive/$pkgver.tar.gz")
sha512sums=('5d8ea9da967a79ecaadc4a498e8af78cb6ba43745f825499b1677c0a262c2891fde003d1674e242369b7dd425dbff2f107544be07aeab859bf91caa4f0e3df31')

prepare() {
  cd ponyc-$pkgver
  sed -i 's/-Werror //' Makefile
}

build() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true default_ssl=openssl_1.1.0

  mkdir docs
  build/release/ponyc packages/stdlib -rexpr -g -o docs
}

check() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true default_ssl=openssl_1.1.0 test
}

package() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true default_ssl=openssl_1.1.0 ponydir=/usr/lib/pony/"$pkgver" DESTDIR="$pkgdir" install

  # Ponyc is designed to read its standard packages from the same directory.
  install -dm755 "$pkgdir"/usr/bin
  ln -sf /usr/lib/pony/"$pkgver"/bin/ponyc "$pkgdir"/usr/bin

  # Let's include the examples as documentation is scarce.
  install -dm755 "$pkgdir"/usr/share/doc/pony
  cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

  cp -dr docs/* "$pkgdir"/usr/share/doc/pony

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
