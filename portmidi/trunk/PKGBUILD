# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Denis Martinez <deuns.martinez AT gmail DOT com>

pkgname=portmidi
pkgver=217
pkgrel=1
pkgdesc="Platform independent library for real-time MIDI input/output."
arch=('i686' 'x86_64')
url='http://portmedia.sourceforge.net/'
license=('GPL')
depends=('alsa-lib')
makedepends=('cmake' 'java-environment')
conflicts=('portmidi-newer')
source=("http://downloads.sourceforge.net/project/portmedia/${pkgname}/${pkgver}/${pkgname}-src-${pkgver}.zip")
md5sums=('03f46fd3947e2ef4c8c465baaf832241')

build() {
  cd "${srcdir}/${pkgname}"

  sed -i "s#/usr/local#/usr#" */CMakeLists.txt pm_python/setup.py
  mkdir -p pm_java/Release

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_ARCHIVE_OUTPUT_DIRECTORY=Release \
    -DCMAKE_LIBRARY_OUTPUT_DIRECTORY=Release \
    -DCMAKE_RUNTIME_OUTPUT_DIRECTORY=Release

  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
}

