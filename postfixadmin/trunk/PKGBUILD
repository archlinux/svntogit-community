# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Filip <fila pruda.com>

pkgname=postfixadmin
pkgver=3.3.3
pkgrel=1
pkgdesc="Web based interface to manage Postfix mailboxes, virtual domains and aliases"
arch=('any')
license=('GPL2')
url="http://postfixadmin.sourceforge.net/"
backup=('etc/webapps/postfixadmin/config.inc.php'
        'etc/webapps/postfixadmin/config.local.php'
        'etc/webapps/postfixadmin/.htaccess'
        'etc/uwsgi/postfixadmin.ini')
depends=('php')
optdepends=('apache: run in webserver'
            'dovecot: interface local IMAP/POP3 server'
            'mariadb: use local MySQL server'
            'perl: for example scripts'
            'php-fpm: run in fastCGI process manager'
            'php-imap: use IMAP'
            'php-sqlite: use SQLite database backend'
            'php-pgsql: use PostgreSQL database backend'
            'postfix: interface local mail server'
            'postgresql: use local PostgreSQL server'
            'python: for example scripts'
            'uwsgi: run as application container')
source=("https://github.com/postfixadmin/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles"
        "${pkgname}.uwsgi")
install="${pkgname}.install"
sha512sums=('363b91eb07f263a0f595e020725cacf9f9de058adaef3987efb3257d0e6fba526b075f8328e47763256278797ec946305d1e4c8ff5c55d77753f9ed72bfa4121'
            'dc61527dbec79a3b19c8fcbc424fbfbe483cccf99a606e566ea6cd624d46c70a6d87dd8d4da398fedf12b60cfd0cf364c351d6b826e6eee6b2ca8eac0d9efcbe'
            '7e55c844be7b186d9eb568fe097632651c683582da83e0ee24cd6dd7c5a581fe05e5462174237f542ff8dc5d17a8ab651a77f0087a0c8df01d7b36c7722b1440'
            'd526f45b5904cfa2e0a7f9d31d88b92bc48c233b10cab927cc101d52ba6ba3724066e9528297c59ff1ab578038f68ddabef9cc9b8bb766b64c843cafcc8a25d9')
b2sums=('005c2a370b6fe0d09a8f1845ff9537b7be3629844983b8592cb43f119d6c8691730140161c993e9edd53a73174944aeded8b4e50472e97823edc39721deb7af4'
        'a73314d9befe48869ca121bddfb3af61e916f8458f388e13eed1a7ffd64e7ae75604dc3af92d3d4d5b97c30d3cb089e28cbf64c7e81e324f3ff85d75b15b5f0d'
        '0e0170b08284dcfc5a68afd46a2e726d696905f3ddcd8158fed68c84fec039fabe60379005055e5fa1cc1fbb098c107b739d0e107f027bd7054d0e31b6e9cea6'
        'f34569a8c442b2354d2748e9c69065849386c51cd4c875cf27d96bf207618043e710248f1be2569a7132eefdff34628e34d8ebee960e14799c5648a590c1c74c')

prepare(){
  mv -v "${pkgname}-${pkgname}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # create empty local config (for overrides)
  echo "<?php" > "config.local.php"
  # create .htaccess file
  echo "#Require all denied" > .htaccess
  # remove unneeded executable bits
  find . -executable -type f \
    -and -not -path "*scripts*" -exec chmod -c 644 {} \;
  # remove symlinks
  find . -type l -exec rm -v {} +
  # remove unneeded debian directories
  rm -rv ADDITIONS/squirrelmail-plugin/debian
}

package() {
  cd "${pkgname}-${pkgver}"

  # sysusers.d integration
  install -vDm 644 ../${pkgname}.sysusers \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # tmpfiles.d integration
  install -vDm 644 ../${pkgname}.tmpfiles \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # docs
  install -vDm 644 {README.md,CHANGELOG.TXT} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 DOCUMENTS/{README*,*.{txt,md}} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/documents"
  cp -av ADDITIONS VIRTUAL_VACATION \
    "${pkgdir}/usr/share/doc/${pkgname}"

  # webapp
  find configs languages lib model public scripts templates tests \
    -type f \( \
    -iname "*.conf" -or \
    -iname "*.css" -or \
    -iname "*.eot" -or \
    -iname "*.gif" -or \
    -iname "*.ico" -or \
    -iname "*.js" -or \
    -iname "*.lang" -or \
    -iname "*.lib" -or \
    -iname "*.map" -or \
    -iname "*.php" -or \
    -iname "*.png" -or \
    -iname "*.sql" -or \
    -iname "*.svg" -or \
    -iname "*.tpl" -or \
    -iname "*.ttf" -or \
    -iname "*.woff" -or \
    -iname "*.woff2" \
    \) -exec install -vDm 644 {} "${pkgdir}/usr/share/webapps/${pkgname}/"{} \;
  install -vDm 644 {common,functions.inc,index}.php phpunit.xml \
    -t "${pkgdir}/usr/share/webapps/${pkgname}"
  (
    # adding weird symlinks
    cd "${pkgdir}/usr/share/webapps/${pkgname}/public"
    ln -sv users/password-change.php .
    ln -sv users/password-recover.php .
  )

  # config
  install -vDm 640 config.{inc,local}.php -t "${pkgdir}/etc/webapps/${pkgname}/"
  ln -sv "/etc/webapps/${pkgname}/config.inc.php" \
    "${pkgdir}/usr/share/webapps/${pkgname}/config.inc.php"
  ln -sv "/etc/webapps/${pkgname}/config.local.php" \
    "${pkgdir}/usr/share/webapps/${pkgname}/config.local.php"
  install -vDm 644 .htaccess -t "${pkgdir}/etc/webapps/${pkgname}/"
  ln -sv "/etc/webapps/${pkgname}/.htaccess" \
    "${pkgdir}/usr/share/webapps/${pkgname}/.htaccess"
  install -vDm 644 "../${pkgname}.uwsgi" "${pkgdir}/etc/uwsgi/${pkgname}.ini"
  # template caches
  install -vdm 755 "${pkgdir}/var/cache/${pkgname}/templates_c"
  ln -sv "/var/cache/${pkgname}/templates_c" \
    "${pkgdir}/usr/share/webapps/${pkgname}/"
}
