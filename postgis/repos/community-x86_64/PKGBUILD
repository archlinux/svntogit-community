# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=2.4.3
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'json-c' 'proj' 'protobuf-c')
changelog=$pkgname.changelog
options=('!makeflags')
source=(http://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz
        json-0.13-compatibility.patch)
sha256sums=('ea5374c5db6b645ba5628ddcb08f71d3b3d90a464d366b4e1d20d5a268bde4b9'
            'a7d3a6005cf776d69dc6df0fc4f31a366049487fcde7939a1849e724d0d78c36')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/json-0.13-compatibility.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
