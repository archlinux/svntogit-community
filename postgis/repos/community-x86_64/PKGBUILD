# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.2.1
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('x86_64')
url="https://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'json-c' 'proj' 'protobuf-c' 'libtiff4' 'pcre2')
optdepends=('perl: for contrib script postgis_restore.pl')
makedepends=('clang' 'llvm')
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('fbab68dde6ca3934b24ba08c8ab0cff2594f57f93deab41a15c82ae1bb69893e')
b2sums=('9c49ad518fb968dc4787bedf6480af29c3e70f986df8233ef034f117b8a8920ff6e39dab57813390b813a3d856476634b18e6ce324795839d019a11559155ff5')

prepare() {
  cd ${pkgname}-${pkgver}

#   sed -i 's/pj_get_release/proj_info/' configure # fix for proj8
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
