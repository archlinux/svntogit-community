# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=postgrest
pkgver=6.0.2
pkgrel=59
pkgdesc="REST API for any Postgres database"
url="https://github.com/begriffs/postgrest"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-http' 'haskell-ranged-sets' 'haskell-aeson' 'haskell-ansi-wl-pprint'
         'haskell-base64-bytestring' 'haskell-case-insensitive' 'haskell-cassava'
         'haskell-configurator-pg' 'haskell-contravariant' 'haskell-contravariant-extras'
         'haskell-cookie' 'haskell-either' 'haskell-gitrev' 'haskell-hasql' 'haskell-hasql-pool'
         'haskell-hasql-transaction' 'haskell-heredoc' 'haskell-http-types'
         'haskell-insert-ordered-containers' 'haskell-interpolatedstring-perl6' 'haskell-jose'
         'haskell-lens' 'haskell-lens-aeson' 'haskell-network-uri' 'haskell-optparse-applicative'
         'haskell-parsec' 'haskell-protolude' 'haskell-regex-tdfa' 'haskell-scientific'
         'haskell-swagger2' 'haskell-unordered-containers' 'haskell-vector' 'haskell-wai'
         'haskell-wai-cors' 'haskell-wai-extra' 'haskell-wai-middleware-static'
         'haskell-auto-update' 'haskell-retry' 'haskell-warp')
makedepends=('ghc' 'haskell-aeson-qq' 'haskell-async' 'haskell-hspec' 'haskell-hspec-wai'
             'haskell-hspec-wai-json' 'haskell-monad-control' 'haskell-transformers-base')
checkdepends=('pifpaf' 'postgresql' 'procps-ng')
source=("$pkgname-$pkgver.tar.bz2::https://github.com/begriffs/postgrest/archive/v$pkgver.tar.gz"
        postgrest-swagger-2.4.patch::https://github.com/PostgREST/postgrest/pull/1326.patch)
sha512sums=('abdcec3291c2216cb7383e13c80cb36c7bbecaded95ebebdc4482e44418d11abafab591ed1e5568ac38cdfc8e3f8b2c6c1c2a0e3e846ee6f97ab48b42c4c580d'
            'd8c654e55e424c8ad29c2791362109af6d0ae94a04d8726946f95caa1bd6b11d9a818069df61e03591e02b54ff15b86e4097375343e28f9354f7fafa5efdcee1')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../postgrest-swagger-2.4.patch
    sed -i -e 's/< *4/<5/' -e 's/< *1/<2/' -e 's/< *3.3/<4/' -e 's/< *0.15/<1/' $pkgname.cabal

    sed -i -e '/ fail /d' src/PostgREST/Config.hs

    sed -i 's/5432/9824/' test/fixtures/dumpfixture.sh
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-CI --ghc-option='-pie'
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver

    eval $(pifpaf run postgresql --host 127.0.0.1)
    createdb postgrest_test

    # TODO: user authentication issue?
    POSTGREST_TEST_CONNECTION=$(test/create_test_db "postgres://$USER@localhost" postgrest_test) runhaskell Setup test || warning "Tests failed"

    # Disabled: uses stack
    # test/io-tests.sh

    pifpaf_stop
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
