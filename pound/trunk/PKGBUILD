# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

_name=Pound
pkgname=pound
pkgver=2.8
pkgrel=2
pkgdesc="A reverse proxy, load balancer, and SSL wrapper"
arch=('x86_64')
url="http://www.apsis.ch/pound/index_html"
license=('GPL3')
depends=('gperftools' 'pcre' 'openssl-1.0')
backup=('etc/pound/pound.cfg')
# switch to https://github.com/graygnuorg/pound for openssl
source=("http://www.apsis.ch/${pkgname}/${_name}-${pkgver}.tgz"
        "${_name}-${pkgver}.tgz.asc::http://www.apsis.ch/${pkgname}/${_name}-${pkgver}.asc"
        "${pkgname}.service"
        "${pkgname}.cfg")
sha512sums=('cf0b865b17d3628e273626e07733f1320e4768702c0f64c8ef0f78d46667f770b223bdc7dca88016a95e5ebd23ae646f95a9b2f4a54a5a80001a10047f07eacc'
            'SKIP'
            '7111df07cb3500c378afb19abbd1ef5f97a566101cd68712a1d6c8015f23463fc06347ec8cb189f2774c954343b8b8815416be1a4acd3ec31f20f03e90cdf603'
            '32d33474a115dfc9d5ccc094ffdb3c367108a48976cf3e58442642dab08167cd0a1808eefa1879e3c38b607d2a6a1cd28142dbd690244368760daba1f95526f6')
validpgpkeys=('8BB562A0F1DB703FB7EB1E95AB72C62A8691DD02') # Robert Segall <roseg@apsis.ch>

prepare() {
  cp -av "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # use openssl-1.0 to generate dhparam
  sed -e 's/openssl/openssl-1.0/' -i Makefile.in
}

build() {
  cd "${pkgname}-${pkgver}"
  CPPFLAGS+=" -I/usr/include/openssl-1.0" \
  LDFLAGS+=" -L/usr/lib/openssl-1.0" \
  ./configure --prefix=/usr \
              --sysconfdir=/etc/pound \
              --bindir=/usr/bin \
              --sbindir=/usr/bin \
              --with-ssl='/usr/lib/openssl-1.0' \
              --with-owner=root \
              --with-group=root
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  # configuration
  install -vDm 644 "${srcdir}/${pkgname}.cfg" \
    "${pkgdir}/etc/${pkgname}/${pkgname}.cfg"
  # systemd service
  install -vDm 644 "$srcdir/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
  # docs
  install -vDm 644 {CHANGELOG,FAQ,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
