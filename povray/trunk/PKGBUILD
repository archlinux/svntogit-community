# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: red_over_blue
# Contributor: neri

pkgname=povray
epoch=2
pkgver=3.7.0.9
pkgrel=8
_v=${pkgver%.*.*} # 3.7
pkgdesc='Script based raytracer for creating 3D graphics'
arch=(x86_64)
license=(AGPL3)
url='https://povray.org/'
depends=(boost-libs imath libpng libtiff openexr)
makedepends=(boost git glu)
backup=(etc/povray/$_v/povray.conf
        etc/povray/$_v/povray.ini)
source=("$pkgname-$pkgver.tar.gz::https://github.com/POV-Ray/povray/archive/v$pkgver.tar.gz"
        povray-openexr3.patch)
b2sums=('42772b8b055e77c3469312a8a1833c4ca79943ac49f59f6b18c2f8ad41e865d65f8ceb52f228628f26d4f653ec27f85264929cafdb753686e86c5f6fac4675cf'
        '3c9623a12fff3c568e61f29685069a9c92197f13b7e30b8cbf33eb2591a9d28475f95b35089546f21c818d5f567b347c799fa9c04e667aa0512b8785f2070d03')

prepare() {
  cd $pkgname-$pkgver
  rm -frv libraries/ilmbase/Imath
  patch -p1 < "$srcdir/povray-openexr3.patch"
  cd unix
  sed 's/automake --w/automake --add-missing --w/g' -i prebuild.sh
  sed 's/dist-bzip2/dist-bzip2 subdir-objects/g' -i configure.ac
  ./prebuild.sh
  cd ..
  ./bootstrap
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    LIBS="-lboost_system -lboost_thread" \
    COMPILED_BY='Arch Linux' \
    --prefix=/usr \
    --sysconfdir=/etc
  make CXXFLAGS+='-std=c++11 -lboost_system -lboost_thread -DBOOST_BIND_GLOBAL_PLACEHOLDERS=1 -w'
}

package() {
  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/share/{doc/,}"$pkgname-$_v"
  cp -r icons include ini scenes scripts "$pkgdir/usr/share/$pkgname-$_v"
  cp -r doc "$pkgdir/usr/share/doc/$pkgname-$_v"
  install -Dm755 unix/povray "$pkgdir/usr/bin/povray"
  install -Dm644 povray.conf "$pkgdir/etc/povray/$_v/povray.conf"
  install -Dm644 povray.ini "$pkgdir/etc/povray/$_v/povray.ini"
  install -Dm644 povray.1 "$pkgdir/usr/share/man/man1/povray.1"
}

# getver: povray.org/documentation
