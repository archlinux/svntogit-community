# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>

pkgname=powerdns-recursor
pkgver=4.0.3
pkgrel=5
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gcc-libs' 'boost-libs' 'libsystemd' 'lua' 'openssl' 'protobuf'
         'libboost_context.so')
makedepends=('boost' 'inetutils' 'systemd')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(http://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.bz2{,.asc}
        pdns-recursor-4.0.3-boost-context.patch)
sha512sums=('03c77cff58851f9802eba434fb674d9cbd19b849620996df84b8dccc97539607895e06c1beb662b1ce08146bbc2b51a72bde2d6d90ef88c929ab645d9b5a33c4'
            'SKIP'
            'e28dab595e216416c484bfbf4c597ccb0e327777e5867d05c9f2291d58bbc2835bd5dfdb47cfa7ecc46acc88dbc900bf6257c191703471354e504f4690cf62f1')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7') # Pieter Lexis <pieter.lexis@powerdns.com>

prepare() {
  cd pdns-recursor-${pkgver}
  patch -p1 < "${srcdir}/pdns-recursor-4.0.3-boost-context.patch"
}

build() {
  cd pdns-recursor-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/powerdns \
    --disable-silent-rules \
    --enable-reproducible \
    --enable-systemd
  make
}

package() {
  cd pdns-recursor-${pkgver}
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/powerdns/recursor.conf"{-dist,}
}

# vim: ts=2 sw=2 et:
