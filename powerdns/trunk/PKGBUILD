# $Id: PKGBUILD 101429 2013-11-25 14:11:30Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kevin Mihelich <kevin@archlinuxarm.org>

pkgname=powerdns
pkgver=3.4.9
pkgrel=1
pkgdesc='Authoritative DNS server'
arch=('x86_64' 'i686')
url='http://www.powerdns.com/'
license=('GPL')
depends=('boost-libs' 'gcc-libs' 'sqlite' 'lua')
makedepends=('boost' 'setconf' 'libmariadbclient' 'postgresql-libs' 'lua'
             'libldap' 'sqlite')
optdepends=('libmariadbclient: MariaDB/MySQL backend'
            'postgresql-libs: PostgreSQL backend' 'libldap: LDAP backend')
install="$pkgname.install"
backup=('etc/powerdns/pdns.conf')
source=(http://downloads.powerdns.com/releases/pdns-$pkgver.tar.bz2{,.asc}
        "$pkgname.service")
sha256sums=('2b1124db2d937ed6666525b9fc60a5dd55cb82f4cae58116cb986de53faa5dff'
            'SKIP'
            'b2358a5951ac944feee15ac03d42ffe9c2a72fa6f57746f77ef8997d54a82040')
validpgpkeys=('FBAE0323821C7706A5CA151BDCF513FA7EED19F3') # Peter van Dijk <peter.van.dijk@powerdns.com>

prepare() {
  touch "pdns-$pkgver/AUTHORS" "pdns-$pkgver/NEWS"

  # Make changes to pdns.conf, while basing it on pdns.conf-dist
  cp "pdns-$pkgver/pdns/pdns.conf-dist" pdns.conf

  # Default settings
  for keyvalue in \
    allow-recursion=127.0.0.1 \
    cache-ttl=6 \
    chroot=/var/empty \
    config-dir=/etc \
    daemon=yes \
    distributor-threads=3 \
    guardian=yes \
    local-port=53 \
    loglevel=3 \
    module-dir=/usr/lib/powerdns \
    negquery-cache-ttl=60 \
    setgid=nobody \
    setuid=nobody \
    socket-dir=/var/run \
    webserver=no
  do
    # Future versions of setconf may be able to comment out options as well.
    setconf -a pdns.conf "$keyvalue"
  done
}

build() {
  cd "pdns-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib \
    --mandir=/usr/share/man \
    --with-modules='' \
    --with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo bind" \
    --docdir=/usr/share/doc/powerdns \
    --sbindir=/usr/bin \
    --bindir=/usr/bin
  make
}

package() {
  make -C "pdns-$pkgver" DESTDIR="$pkgdir" install
  install -m644 "pdns.conf" "$pkgdir/etc/powerdns/"
  install -Dm644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  mv "$pkgdir/usr/lib/pdns" "$pkgdir/usr/lib/powerdns"
  rm -f "$pkgdir/etc/powerdns/pdns.conf-dist"
}

# vim:set ts=2 sw=2 et:
