# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Swift Geek
# AUR Maintainer: Timoth√©e Ravier <tim@siosm.fr>
# AUR Maintainer: Stefan Tatschner <rumpelsepp@sevenbyte.org>
# Contributor: Swift Geek
# Contributor: Pablo Olmos de Aguilera C. pablo+aur at odac dot co

_pkgname="powerline"
pkgbase="powerline"
pkgname=("powerline" "powerline2" "python-powerline" "python2-powerline" "powerline-common" "powerline-vim" "powerline-fonts")
pkgdesc='Statusline plugin for vim, and provides statuslines and prompts for several other applications, including zsh, bash, tmux, IPython, Awesome, i3 and Qtile'
pkgver=2.7
pkgrel=1
url="https://github.com/${_pkgname}/${_pkgname}"
license=('MIT')
arch=('x86_64')
makedepends=(
    'python2-setuptools'
    'python-setuptools'
    'python-sphinx'
    )
checkdepends=(
    'python-pexpect'
    'python2-pexpect'
)
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('a61715a2af9b3b5e26bd7ff4717cba41e6b5ad8c8328132154c88dc161d3fab902432dfe94da9c2d87c32e9fa69c9a758fb90168fbd18b3dc47c0d0073fd6623')



prepare() {
    # fix https://bugs.archlinux.org/task/48358
    cd ${srcdir}/${_pkgname}-${pkgver}
    find scripts -type f -iname "powerline-*" -print0 | xargs -0 sed -i "1s/env //"
    find ${_pkgname}/bindings -type f -iname "*.py" -print0 | xargs -0 sed -i "1s/env //"


    cp -r ${srcdir}/${_pkgname}-${pkgver}{,-py2}
    # prepare python2 scripts
    cd ${srcdir}/${_pkgname}-${pkgver}-py2
    find scripts -type f -iname "powerline-*" -print0 | xargs -0 sed -i "1s/python/python2/"
    find ${_pkgname}/bindings -type f -iname "*.py" -print0 | xargs -0 sed -i "1s/python/python2/"
}

build(){
    # build main pages
    cd ${srcdir}/${_pkgname}-${pkgver}/docs
    make man

    # build C client
    cd ${srcdir}/${_pkgname}-${pkgver}
    python setup.py build

    cd ${srcdir}/${_pkgname}-${pkgver}-py2
    python2 setup.py build
}


# FIXME Currently test will fail with dividers and other errors
check(){
    cd ${srcdir}/${_pkgname}-${pkgver}
    LC_ALL=C python setup.py test
    cd ${srcdir}/${_pkgname}-${pkgver}-py2
    LC_ALL=C python2 setup.py test
}

package_powerline(){
    depends=('python-powerline')
    conflicts=('powerline2' 'python-powerline<=2.3-1')
    replaces=('python-powerline<=2.3-1')

    cd ${srcdir}/${_pkgname}-${pkgver}/scripts
    install -Dm755 powerline "${pkgdir}/usr/bin/powerline"
    install -Dm755 powerline-config "${pkgdir}/usr/bin/powerline-config"
    install -Dm755 powerline-daemon "${pkgdir}/usr/bin/powerline-daemon"
    install -Dm755 powerline-lint "${pkgdir}/usr/bin/powerline-lint"
    install -Dm755 powerline-render "${pkgdir}/usr/bin/powerline-render"
    
    install -dm755 "${pkgdir}/usr/share/powerline/"
    ln -s "$(python3 -c 'import site; print(site.getsitepackages()[0])')/powerline/bindings" "${pkgdir}/usr/share/powerline/bindings"
    # license is installed by powerline-common
}

package_powerline2(){
    pkgdesc='Python2 version of statusline plugin for vim, and provides statuslines and prompts for several other applications, including zsh, bash, tmux, IPython, Awesome, i3 and Qtile'
    depends=('python2-powerline')
    conflicts=('powerline' 'python2-powerline<=2.3-1')
    provides=('powerline')
    replaces=('python2-powerline<=2.3-1')

    cd ${srcdir}/${_pkgname}-${pkgver}-py2/scripts
    install -Dm755 powerline{,-config,-daemon,-lint,-render} -t "${pkgdir}/usr/bin/"
    install -dm755 "${pkgdir}/usr/share/powerline/"
    ln -s "$(python2 -c 'import site; print(site.getsitepackages()[0])')/powerline/bindings" "${pkgdir}/usr/share/powerline/bindings"

    install -dm755 "${pkgdir}/usr/share/licenses/"
    ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}


package_python-powerline() {
    pkgdesc='python library for powerline'
    depends=('python>=3.2' 'powerline-common')
    optdepends=('python-psutil: improved system information'
                'python-pygit2: improved git support'
                'powerline-vim: vim plugin'
                'powerline-fonts: patched fonts for powerline'
                )

    # install by redirect scripts and binaries
    cd ${srcdir}/${_pkgname}-${pkgver}
    python setup.py install --skip-build --root="${pkgdir}" --install-data=tmp --install-scripts=tmp --optimize=1
    # remove redirected scripts and binaries
    rm -rf "${pkgdir}/tmp" 

    install -dm755 "${pkgdir}/usr/share/licenses/"
    ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-powerline() {
    pkgdesc='python2 library for powerline'
    depends=('python2>=2.6' 'powerline-common')
    optdepends=('python2-psutil: improved system information'
                'python2-pygit2: improved git support'
                'powerline-vim: vim plugin'
                'powerline-fonts: patched fonts for powerline'
                )

    # install by redirect scripts and binaries
    cd ${srcdir}/${_pkgname}-${pkgver}-py2
    python2 setup.py install --skip-build --root="${pkgdir}" --install-data=tmp --install-scripts=tmp --optimize=1
    # remove redirected scripts and binaries
    rm -rf "${pkgdir}/tmp" 

    install -dm755 "${pkgdir}/usr/share/licenses/"
    ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_powerline-common(){
    pkgdesc='common files for python-powerline and python2-powerline'
    
    cd ${srcdir}/${_pkgname}-${pkgver}

    # license
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${_pkgname}/LICENSE"

    install -dm755 "${pkgdir}/usr/share/licenses/"
    ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"

    # manpages
    install -dm755 "${pkgdir}/usr/share/man/man1/"
    install -Dm644 "${srcdir}/${_pkgname}-${pkgver}/docs/_build/man/"* \
                   "${pkgdir}/usr/share/man/man1/"
}

package_powerline-fonts() {
    pkgdesc='patched fonts for powerline'
    provides=('otf-powerline-symbols')
    depends=('powerline-common')

    cd ${srcdir}/${_pkgname}-${pkgver}

    install -dm755 "${pkgdir}/etc/fonts/conf.d"
    install -Dm644 "font/PowerlineSymbols.otf" "${pkgdir}/usr/share/fonts/OTF/PowerlineSymbols.otf"
    install -Dm644 "font/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.avail/10-powerline-symbols.conf"
    ln -s "../conf.avail/10-powerline-symbols.conf" "${pkgdir}/etc/fonts/conf.d/10-powerline-symbols.conf"

    install -dm755 "${pkgdir}/usr/share/licenses/"
    ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}


package_powerline-vim() {
    pkgdesc='vim plugin for powerline'
    depends=('powerline' 'powerline-common' 'vim-runtime')
    conflicts=('python2-powerline-vim')
    provides=('python2-powerline-vim')

    cd ${srcdir}/${_pkgname}-${pkgver}
    install -Dm644 "powerline/bindings/vim/plugin/powerline.vim" \
                   "${pkgdir}/usr/share/vim/vimfiles/plugin/powerline.vim"

    install -dm755 "${pkgdir}/usr/share/licenses/"
    ln -s "/usr/share/licenses/${_pkgname}" "${pkgdir}/usr/share/licenses/${pkgname}"
}
