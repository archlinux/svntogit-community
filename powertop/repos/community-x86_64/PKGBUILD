# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=powertop
pkgver=2.6
pkgrel=1
pkgdesc='A tool to diagnose issues with power consumption and power management'
arch=('i686' 'x86_64')
url='https://01.org/powertop/'
license=('GPL2')
depends=('gcc-libs' 'libnl' 'ncurses' 'pciutils')
changelog=$pkgname.changelog
source=(https://01.org/sites/default/files/downloads/powertop/$pkgname-$pkgver.tar.gz)
sha256sums=('3024c2b13c8f32cdac0b0ea9359eb5668dc9bd11776cf063481daa878533d1ee')

build() {
  cd $pkgname
  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
