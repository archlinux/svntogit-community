# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: ezzetabi <ezzetabi at gawab dot com>

pkgname=ppl
pkgver=1.2
pkgrel=3
pkgdesc="A modern library for convex polyhedra and other numerical abstractions"
arch=(x86_64)
url="https://bugseng.com/products/ppl"
license=(GPL3)
depends=(glpk)
makedepends=(swi-prolog)
optdepends=('swi-prolog: SWI Prolog interface')
options=(!docs)
source=(http://bugseng.com/products/ppl/download/ftp/releases/$pkgver/ppl-$pkgver.tar.gz)
md5sums=('2b43339316a3139fd75c7e8ca7ddc201')
validpgpkeys=('058E41DB299DE2DD0AC649CA881457CBB5944392') # Roberto Bagnara

build() {
  cd ppl-$pkgver
  ./configure --prefix=/usr --enable-interfaces="c,cxx,swi_prolog"
  make
}

check() {
  cd ppl-$pkgver
  make check || /bin/true # https://www.cs.unipr.it/mantis/view.php?id=2120
}

package() {
  cd ppl-$pkgver
  make DESTDIR="$pkgdir" install
}
