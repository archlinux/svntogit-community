# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: J0k3r <moebius282 e4a t gmail D0_T com>
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor: JerichoKru   <jerichokru(AT)hotmail.com>

pkgname=pragha
pkgver=1.1.2
pkgrel=2
pkgdesc="A lightweight GTK+ music manager - fork of Consonance Music Manager."
arch=('i686' 'x86_64')
url="http://pragha.wikispaces.com/"
license=('GPL3')
depends=('libnotify' 'libcdio-paranoia' 'gst-plugins-base'
         'taglib' 'hicolor-icon-theme'  'gtk2'
         'dbus-glib' 'sqlite' 'desktop-file-utils'
         'libclastfm')
optdepends=('notification-daemon: OSD notification'
            'gst-plugins-good: A collection of gstreamer codecs'
            'gst-plugins-bad: A collection of gstreamer codecs'
            'gst-plugins-ugly: A collection of gstreamer codecs')
makedepends=('intltool')
replaces=('dissonance')
install=pragha.install
source=("http://dissonance.googlecode.com/files/pragha-$pkgver.tar.bz2")
sha1sums=('3e6456275a216110bd6942b2dc19b1120f4d14e5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  LIBS+="-ldbus-glib-1"  \
    ./configure --prefix=/usr \
                --with-gstreamer=1.0 \
                CPPFLAGS="-DHAVE_PARANOIA_NEW_INCLUDES"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -m 644 "data/$pkgname.desktop" "$pkgdir/usr/share/applications"
  install -d "$pkgdir/usr/share/pixmaps"
  install -m 644 "data/$pkgname.png" "$pkgdir/usr/share/pixmaps/"
  install -m 644 "data/$pkgname.1" "$pkgdir/usr/share/man/man1/"

}

# vim:ts=2:sw=2:expandtab

