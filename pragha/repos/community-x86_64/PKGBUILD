# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor:  JerichoKru   <jerichokru(AT)htomail.com>

pkgname=pragha
pkgver=0.99.0
pkgrel=1
pkgdesc="A lightweight GTK+ music manager - fork of Consonance Music Manager."
arch=('i686' 'x86_64')
url="http://pragha.wikispaces.com/"
license=('GPL3')
depends=('libnotify' 'libcdio' 'curl' 
         'taglib' 'gstreamer0.10' 'hicolor-icon-theme'
         'gtk2' 'dbus-glib' 'dbus-core'
         'sqlite3' 'glib2')
optdepends=('notification-daemon: OSD notification')
replaces=('dissonance')
install=pragha.install
source=(http://dissonance.googlecode.com/files/$pkgname-$pkgver.tar.bz2)
sha1sums=('0791335c29121e6288962f4a7fbe074e4db73c9d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  LIBS+="-ldbus-glib-1"  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
	
  install -m 644 "data/$pkgname.desktop" "$pkgdir/usr/share/applications"
  install -d "$pkgdir/usr/share/pixmaps"
  install -m 644 "data/$pkgname.png" "$pkgdir/usr/share/pixmaps/"
  install -m 644 "data/$pkgname.1" "$pkgdir/usr/share/man/man1/"

}

# vim:ts=2:sw=2:expandtab

