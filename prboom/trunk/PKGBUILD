# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: dale <dale@archlinux.org>

pkgname=prboom
pkgver=2.5.0
pkgrel=7
pkgdesc='Game engine which provides a program to play Doom levels'
url='http://prboom.sourceforge.net/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libpng' 'sdl_mixer' 'sdl_net' 'glu')
makedepends=('mesa')
source=("http://downloads.sourceforge.net/prboom/${pkgname}-${pkgver}.tar.gz"
        'libpng-1.4.patch')
md5sums=('a8a15f61fa2626ab98051ab2703378c4'
         'ba53474db8e747035ca2320a445e4ae0')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 < ../libpng-1.4.patch

	./configure --prefix=/usr --disable-i386-asm
	sed -i "s|/games|/bin|g" "${srcdir}/${pkgname}-${pkgver}/src/Makefile"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

