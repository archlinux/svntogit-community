# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Rouslan Solomakhin <rouslan@localnet.com>

pkgname=prelink
pkgver=20130503
pkgrel=1
pkgdesc='ELF prelinking utility to speed up dynamic linking'
arch=('i686' 'x86_64')
url='http://people.redhat.com/jakub/prelink/'
license=('GPL')
depends=('elfutils')
backup=('etc/prelink.conf')
source=("http://people.redhat.com/jakub/prelink/${pkgname}-${pkgver}.tar.bz2"
        'prelink.conf')
sha1sums=('eff86cb26f0cc174486769527286cc388d5216b0'
          'b83bd2d3505076151aa13d40de3d5c08a04440c7')

build() {
  cd ${pkgname}
  ./configure --prefix=/usr --mandir=/usr/share/man --sbin=/usr/bin
  make
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm0644 "${srcdir}/prelink.conf" "${pkgdir}/etc/prelink.conf"
}
