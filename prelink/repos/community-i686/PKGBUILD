# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Rouslan Solomakhin <rouslan@localnet.com>

pkgname=prelink
pkgver=20130503
pkgrel=2
pkgdesc='ELF prelinking utility to speed up dynamic linking'
arch=('x86_64' 'i686')
url='http://people.redhat.com/jakub/prelink/'
license=('GPL')
depends=('elfutils')
makedepends=('elfutils' 'libtool')
backup=('etc/prelink.conf')
source=("http://people.redhat.com/jakub/$pkgname/$pkgname-$pkgver.tar.bz2"
        'prelink.conf')
sha256sums=('6339c7605e9b6f414d1be32530c9c8011f38820d36431c8a62e8674ca37140f0'
            '01969f714d5bccc2ce52062f3b4dadcee93b955ce6cf99325c76f6eea721ec7c')

build() {
  cd "$pkgname"

  ./configure --prefix=/usr --mandir=/usr/share/man --sbin=/usr/bin --disable-static --with-gnu-ld
  make
}

check() {
  make -C "$pkgname" check
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
  install -Dm644 prelink.conf "$pkgdir/etc/prelink.conf"
}

# vim:set ts=2 sw=2 et:
