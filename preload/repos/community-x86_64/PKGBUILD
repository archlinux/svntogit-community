# $Id$
# Maintainer:
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=preload
pkgver=0.6.4
pkgrel=5
arch=('i686' 'x86_64')
pkgdesc="Makes applications run faster by prefetching binaries and shared objects"
url="http://sourceforge.net/projects/preload"
license=('GPL2')
depends=('glib2' 'bash')
makedepends=('help2man' 'pkgconfig')
backup=('etc/preload.conf')
options=('!makeflags')
install=$pkgname.install
source=("http://downloads.sourceforge.net/sourceforge/preload/$pkgname-$pkgver.tar.gz"
        rc.d service)
md5sums=('10786287b55afd3a2b433b4f898809f4'
         'bde0dd7867c77e7c4d10b481b5cddcd3'
         'db9350ab52fd643edbc88ebab2c8c1bb')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --localstatedir=/var
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
  
  install -Dm755 "${srcdir}"/rc.d "${pkgdir}"/etc/rc.d/preload
  install -Dm644 "${srcdir}"/service "${pkgdir}"/usr/lib/systemd/system/preload.service

  rm -rf "${pkgdir}"/etc/rc.d/init.d

  rm -rf "${pkgdir}"/var/lib/preload/preload.state
  rm -rf "${pkgdir}"/var/log/preload.log

  install -d "${pkgdir}"/etc/conf.d
  mv "${pkgdir}"/etc/sysconfig/* "${pkgdir}"/etc/conf.d
  rm -rf "${pkgdir}"/etc/sysconfig

  sed -r -i 's#^((map|exe)prefix =) (.+)$#\1 /opt;\3#' "${pkgdir}"/etc/preload.conf
}
