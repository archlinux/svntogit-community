# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: revel <revelΘmuub·net>
# Contributor: Marcos J. S. Magalhaes <mjsmagalhaes ^dot^ insc _at_ gmail ~dot~ com>

pkgname=premake
pkgver=5.0a16
_pkgver=5.0.0-alpha16
pkgrel=1
pkgdesc="A simple build configuration and project generation tool"
arch=('x86_64')
url="https://premake.github.io/"
license=('BSD')
depends=('glibc')
source=("https://github.com/premake/premake-core/releases/download/v${_pkgver}/premake-${_pkgver}-src.zip")
sha512sums=('2b869da4eb0487405289e2274fc8feefa02fb543559e43f2c2c9e869f701a40d78cfc135f679fa00554f2993a5511c8d740a8fe5576914ba8dc8636b75736b5c')

build() {
  cd "premake-$_pkgver-src/build/gmake2.unix"

  make config=release
}

package() {
  cd "premake-$_pkgver-src"

  install -Dm755 "bin/release/premake5" "${pkgdir}/usr/bin/premake5"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
