# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=7.5
pkgrel=1
pkgdesc="Fast prime number generator"
arch=(x86_64)
url="https://github.com/kimwalisch/primesieve"
license=(BSD)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/kimwalisch/primesieve/archive/v$pkgver.tar.gz")
sha256sums=('fbc4070b2af4b87e2cff7ce3434f79af04d843594be43bba14293674475ec03c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
