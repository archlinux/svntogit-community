# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Juergen Hoetzel <juergen@hoetzel.info>
# Contributor: basilburn (basilburn), Paul Bredbury (brebs) 

pkgname=privoxy
pkgver=3.0.21
pkgrel=1
pkgdesc='A web proxy with advanced filtering capabilities.'
arch=('i686' 'x86_64')
url='http://www.privoxy.org'
license=('GPL')
depends=('pcre' 'zlib')
makedepends=('autoconf')
backup=('etc/privoxy/'{config,trust,default.action,user.filter,default.filter}
        'etc/privoxy/'{match-all,user}.action
        'etc/logrotate.d/privoxy')
install='privoxy.install'
source=("http://downloads.sourceforge.net/ijbswa/${pkgname}-${pkgver}-stable-src.tar.gz"
        'privoxy.logrotate.d'
        'privoxy.service')
md5sums=('79558f2545cfcf9731f7de611646d837'
         'fc3046377137003726a151ee27b142a2'
         '358e614190247fc1671bbcadddf500db')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"

  autoheader
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc/privoxy --enable-compression

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"

  make prefix="${pkgdir}/usr" SBIN_DEST="${pkgdir}/usr/bin" \
    MAN_DEST="${pkgdir}/usr/share/man/man1" GROUP=42 \
    CONF_BASE="${pkgdir}/etc/privoxy" VAR_DEST="${pkgdir}/var" install

  # Don't overwrite existing log files!
  rm "${pkgdir}/var/log/privoxy/"*

  install -Dm0644 "${srcdir}/privoxy.logrotate.d" "${pkgdir}/etc/logrotate.d/privoxy"

  # Fix config paths.
  sed -i -e 's#^confdir.*$#confdir /etc/privoxy#' -e 's#^logdir.*$#logdir /var/log/privoxy#' \
    -e '/^user-manual/s#.*#\#user-manual /usr/share/doc/privoxy/user-manual/#' \
    "${pkgdir}/etc/privoxy/config"

  # Fix group ownership and permissions.
  chgrp -R 42 "${pkgdir}/etc/privoxy/"
  chgrp -R 42 "${pkgdir}/var/log/privoxy/"

  find "${pkgdir}/etc/privoxy/" -type d | xargs chmod 0770
  find "${pkgdir}/etc/privoxy/" -type f | xargs chmod 0660
  chmod 0700 "${pkgdir}/var/log/privoxy"

  # avoid conflict with filesystem>=2012.06
  rmdir "$pkgdir/var/run"

  # systemd unit file
  install -Dm0644 "${srcdir}/privoxy.service" "${pkgdir}/usr/lib/systemd/system/privoxy.service"
}
