# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=processing
pkgver=3.3.4
_rev=0261
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
makedepends=('gendesk' 'java-openjfx' 'java-environment=8' 'apache-ant' 'rsync')
depends=('libgl' 'java-runtime=8')
options=('!strip')
source=("https://github.com/$pkgname/$pkgname/archive/$pkgname-$_rev-$pkgver.tar.gz")
sha256sums=('3e7913632895af01bfba3868801f29320ed3332f04d6b13d3b59a5b83466a64b')

prepare() {
  gendesk -f -n --pkgname="$pkgname" --pkgdesc="$pkgdesc"

  # Use the font's built-in hinting instructions
  sed 's|  java|  _JAVA_OPTIONS="-Dawt.useSystemAAFontSettings=gasp" java|g' \
    -i "$pkgname-$pkgname-$_rev-$pkgver/build/linux/processing"
}

build() {
  cd "$pkgname-$pkgname-$_rev-$pkgver/build"

  ant run
}

package() {
  cd "$pkgname-$pkgname-$_rev-$pkgver"

  install -d "$pkgdir/usr/"{bin/,share/$pkgname/}
  cp -r build/linux/work/* "$pkgdir/usr/share/$pkgname/"

  # Desktop shortcut
  install -Dm644 "build/shared/lib/icons/pde-256.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Symbolic links in /usr/bin
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"
}

# getver: raw.githubusercontent.com/processing/processing/master/build/shared/revisions.txt
# vim: ts=2 sw=2 et:
