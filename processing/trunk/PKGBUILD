# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=processing
pkgver=2.1
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
depends=('libgl' 'libxxf86vm')
makedepends=('gendesk')
options=('!strip') # 'staticlibs')

if [[ $CARCH == "x86_64" ]]; then
  source=("http://download.$pkgname.org/$pkgname-$pkgver-linux64.tgz"
          "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
  sha256sums=('68a5fd3befeb951c57f3cfcf55e755fc2b8fbd22ad23deae01bd10d8460ce84a'
              '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')
else
  source=("http://download.$pkgname.org/$pkgname-$pkgver-linux32.tgz"
          "$pkgname.png::http://wiki.$pkgname.org/skins/${pkgname}skin/${pkgname}_wiki_logo.png")
  sha256sums=('ccb026d685ddb7f28c3292c27ccffa6033a459d0d2dcaa703781736a26dde62f'
             '019ae700333409acbc628779747ab8f153f6647de25e4de3a9bd4a42d0d8056a')
fi

prepare() {
  gendesk -f -n --pkgname="$pkgname" --pkgdesc="$pkgdesc"
}

package() {
  cd "$pkgname-$pkgver"
  
  install -d "$pkgdir/usr/share/$pkgname/"
  install -d "$pkgdir/usr/bin/"
  cp -r * "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"
  install -Dm644 "$srcdir/$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
