# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=processing
pkgver=3.2.3
pkgrel=2
arch=('x86_64' 'i686')
pkgdesc='Programming environment for creating images, animations and interactions'
url='http://www.processing.org/'
license=('GPL' 'LGPL')
makedepends=('gendesk')
depends=('libgl' 'libxxf86vm')
options=('!strip')
source_x86_64=("http://download.$pkgname.org/$pkgname-$pkgver-linux64.tgz")
source_i686=("http://download.$pkgname.org/$pkgname-$pkgver-linux32.tgz")
md5sums_x86_64=('0f558ae55fa6fdac2812daa9308044d3')
md5sums_i686=('8841ed118da2f7bb1000dec9770b8609')

prepare() {
  gendesk -n --pkgname="$pkgname" --pkgdesc="$pkgdesc"
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/bin/" "$pkgdir/usr/share/$pkgname/"
  cp -r * "$pkgdir/usr/share/$pkgname/"
  ln -s "/usr/share/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  ln -s "/usr/share/$pkgname/$pkgname-java" "$pkgdir/usr/bin/$pkgname-java"

  # Desktop icon / application shortcut
  install -Dm644 "lib/icons/pde-128.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# getver: https://raw.githubusercontent.com/processing/processing/master/build/shared/revisions.txt
# vim:set ts=2 sw=2 et:
