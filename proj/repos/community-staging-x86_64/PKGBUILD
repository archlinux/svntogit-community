# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=proj
pkgver=4.9.1
pkgrel=1
pkgdesc='Cartographic Projections library.'
arch=('i686' 'x86_64')
url="http://trac.osgeo.org/proj/"
license=('MIT')
changelog=$pkgname.changelog
source=(http://download.osgeo.org/proj/$pkgname-$pkgver.tar.gz
        http://download.osgeo.org/proj/$pkgname-datumgrid-1.5.tar.gz
        chenyx06a.zip::http://www.swisstopo.admin.ch/internet/swisstopo/en/home/products/software/software.parsys.7090.downloadList.55545.DownloadFile.tmp/chenyx06ntv2.zip)
sha256sums=('fca0388f3f8bc5a1a803d2f6ff30017532367992b30cf144f2d39be88f36c319'
            '6047362ec2cdf67a34acce36bb739df9834e10f4be93b7b51ae7d8b2be084d5e'
            'f61290a32815622bfe182af475c76b0b4c282de66121fdddce2f6201e8bca2e0')

prepare() {
  bsdtar -xzvf "${srcdir}"/${pkgname}-datumgrid-1.5.tar.gz -C "${srcdir}"/proj-${pkgver}/nad
  bsdtar -xzvf "${srcdir}"/chenyx06a.zip -C "${srcdir}"/proj-$pkgver/nad CHENYX06a.gsb
}

build(){
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -D COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
