# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.dot.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=proj
pkgver=8.0.0
pkgrel=3
pkgdesc='Cartographic Projections library'
arch=('x86_64')
url="https://trac.osgeo.org/proj/"
license=('MIT')
depends=('curl' 'libtiff' 'sqlite')
makedepends=('cmake' 'gmock' 'gtest')
changelog=$pkgname.changelog
source=(https://github.com/OSGeo/PROJ/releases/download/$pkgver/$pkgname-$pkgver.tar.gz
        https://download.osgeo.org/proj/proj-data-1.5.tar.gz)
sha256sums=('aa5d4b934450149a350aed7e5fbac880e2f7d3fa2f251c26cb64228f96a2109e'
            '868ac0ef68fa5767208004bcad68a6097b0b52edaa96f3f43adccae33809423a')

build() {
  cmake -S $pkgname-$pkgver -B build -DCMAKE_INSTALL_PREFIX='/usr' -DUSE_EXTERNAL_GTEST=ON
  cmake --build build
}

check(){
  ctest --test-dir build
}
            
package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
#   bsdtar --no-same-owner -xzvf "${srcdir}"/proj-data-1.5.tar.gz -C "${pkgdir}"/usr/share/$pkgname
}
