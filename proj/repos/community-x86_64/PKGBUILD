# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.dot.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=proj
pkgver=4.9.2
pkgrel=1
pkgdesc='Cartographic Projections library.'
arch=('i686' 'x86_64')
url="http://trac.osgeo.org/proj/"
license=('MIT')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/OSGeo/proj.4/archive/$pkgver.tar.gz
        http://download.osgeo.org/proj/proj-datumgrid-1.5.tar.gz
        chenyx06a.zip::http://www.swisstopo.admin.ch/internet/swisstopo/en/home/products/software/products/chenyx06.parsys.00011.downloadList.70576.DownloadFile.tmp/chenyx06ntv2.zip)
sha256sums=('9a40de36183d23a395cf51ba7a465e1e6c982b6e4553b31310c42012d188be6b'
            '6047362ec2cdf67a34acce36bb739df9834e10f4be93b7b51ae7d8b2be084d5e'
            'f61290a32815622bfe182af475c76b0b4c282de66121fdddce2f6201e8bca2e0')

prepare() {
  bsdtar -xzvf "${srcdir}"/proj-datumgrid-1.5.tar.gz -C "${srcdir}"/proj.4-$pkgver/nad
  bsdtar -xzvf "${srcdir}"/chenyx06a.zip -C "${srcdir}"/proj.4-$pkgver/nad CHENYX06a.gsb
}

build(){
  cd "${srcdir}"/proj.4-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/proj.4-$pkgver

  make DESTDIR="${pkgdir}" install

  install -D COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
