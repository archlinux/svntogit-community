# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.dot.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=proj
pkgver=4.9.3
pkgrel=1
pkgdesc='Cartographic Projections library'
arch=('x86_64')
url="http://trac.osgeo.org/proj/"
license=('MIT')
makedepends=('autoconf')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/OSGeo/proj.4/archive/$pkgver.tar.gz
        http://download.osgeo.org/proj/proj-datumgrid-1.6.zip)
sha256sums=('c34254f9d54e1f1fcafd322e529a6ca2785785ff99b529dc40b2a25a8a46c871'
            '054e7b63b474926c79f25fbe5cd8760351e8d93e5a167b5473e993c01bb08866')

prepare() {
  bsdtar -xzvf "${srcdir}"/proj-datumgrid-1.6.zip -C "${srcdir}"/proj.4-$pkgver/nad
}

build(){
  cd "${srcdir}"/$pkgname.4-$pkgver

  autoreconf -if
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname.4-$pkgver

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
