# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=promscale
pkgver=0.7.1
pkgrel=1
pkgdesc="An analytical platform for Prometheus metrics"
arch=('x86_64')
url="https://github.com/timescale/promscale"
license=('Apache')
depends=('glibc')
makedepends=('go')
optdepends=(
  'timescaledb: for a local timescaledb instance'
  'prometheus: for a local prometheus instance'
  'promscale_extension: for optimised postgresql performance'
)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  'systemd.service'
  'sysusers.conf'
)
sha512sums=('d0ad6914dfa772dc992f113738871df7f6adb461303fb57158396d4be0743c1be30c93cfb1466bbaa193121f4c82474f1b6dcb8e15e72abbcdd24a004b31c376'
            '926bfd97c57d8d146a260aaa0983f7e7181688662537f803679b7bf82a43f41197ddc82ad8fa5087dfc5daca95adef0a35c19e5fbfbb29012944e42ba9f651a3'
            'ec5e6ec6b967119722d8fdaaaaf1a4d8dea9ad0b78707a1ddd6b55da524b571c7ecd4c2b97776c6ce9f49d4549d760482a2346c11ab287650469448d9c662726')
b2sums=('f576c4771bef80a8f472b3b4408762632810ed287278e4b9e0fec0ac01a012f61b835385db378044160c30cde8cf17c475ef6f4d1dab90948cfaebe05f89e78f'
        '98ad7412c94ff5a6643e9eec7a1cf4c7828ef84b25ec18b4469b70cc11ccbe933cde4e822c9b33977ae8bb25bf10de6baef13f84a83bde82b3922c7056f2f712'
        '1e720f8e453d9acad5f0ed39a51b59c429322b9e9e885f3d71d0e2fd37276268e639422f8d39ca9fca14e0e5ccf646d717fbe89e0dd696980d03467cfb7d3e0f')

prepare() {
  cd "$pkgname-$pkgver"

  # create directory for build output
  mkdir build_output

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -buildmode=pie \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    -o build_output ./cmd/...
}

package() {
  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"

  # documentation
  cd "$pkgname-$pkgver"
  install -vDm644 cmd/prom-migrator/README.md "$pkgdir/usr/share/doc/$pkgname/prom-migrator.md"
  install -vDm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  cp -vr docs "$pkgdir/usr/share/doc/$pkgname"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" \
    "build_output/$pkgname" \
    build_output/prom-migrator
}
