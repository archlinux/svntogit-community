# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=promscale_extension
pkgver=0.3.0
pkgrel=1
pkgdesc="PostgreSQL extension for Promscale"
arch=('x86_64')
url="https://github.com/timescale/promscale_extension"
license=('custom:Timescale')
depends=('gcc-libs' 'postgresql<14' 'timescaledb')
makedepends=('rust' 'clang' 'llvm' 'cargo-pgx')
optdepends=('promscale: for a local promscale instance')
# last commit to license - used to pin license version
_license_commit='490e9405874d284735e27e3f9f63a2f6dd31a08f'
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  "LICENSE-TIMESCALE-$_license_commit::https://raw.githubusercontent.com/timescale/timescaledb/$_license_commit/tsl/LICENSE-TIMESCALE"
)
sha512sums=('ccfccd7b059f0a571689f23a8c8ac252c7fcd59aef07a0a53493e0ca336c3b1e5d3a4fd33d6527dfb1b2bf3cb7e9aa3aaf43c8ce6d3c1dfa6736f76356539325'
            'b7c5ce0ef87c1c5c2d4688910a915874be742732a023319e855a8fbb666b0da42683b714e1c3368975431796e4fcd0e524fae80a999bb8f15e4406d97b14a15e')
b2sums=('c7036cc6a39f8bab6d51db98fe743e8231d5e5ff07e17dc27ad1e43e13c28563d26672f881f7821584914379e04e730a7db2e76616f168cca762f0a04d5385c9'
        '9ae11a930e930953b16f7d6d1d3fbf0ebb6c4d8687cac1475560603442ed8edd452200468f7fe9c82af651d40ccad192c036940bfe57ef093e7c30cce93383f0')

build() {
  cd "$pkgname-$pkgver"

  # initialise pgx
  export _PGMAJOR=$(pg_config --version | sed -e 's/PostgreSQL //' -e 's/\..*//')
  cargo pgx init "--pg${_PGMAJOR}" pg_config

  # build extension
  make
}

package() {
  cd "$pkgname-$pkgver"

  # extension
  make DESTDIR="$pkgdir" install

  # licenses
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE NOTICE
  install -Dm644 "$srcdir/LICENSE-TIMESCALE-$_license_commit" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-TIMESCALE"
}
