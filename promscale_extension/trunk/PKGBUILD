# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=promscale_extension
pkgver=0.3.2
pkgrel=2
pkgdesc="PostgreSQL extension for Promscale"
arch=('x86_64')
url="https://github.com/timescale/promscale_extension"
license=('custom:Timescale')
depends=(
  'gcc-libs'
  'postgresql<15'
  'timescaledb'
)
makedepends=(
  'git'
  'rust'
  'clang'
  'llvm'
  'cargo-pgx'
)
optdepends=('promscale: for a local promscale instance')
options=('!lto')
# last commit to license - used to pin license version
_license_commit='490e9405874d284735e27e3f9f63a2f6dd31a08f'
_commit='7d73c3e3de3fdfa2b5bdcb4063b7937b13f8fae3'
source=(
  "$pkgname::git+$url.git#commit=$_commit"
  "LICENSE-TIMESCALE-$_license_commit::https://raw.githubusercontent.com/timescale/timescaledb/$_license_commit/tsl/LICENSE-TIMESCALE"
)
sha512sums=('SKIP'
            'b7c5ce0ef87c1c5c2d4688910a915874be742732a023319e855a8fbb666b0da42683b714e1c3368975431796e4fcd0e524fae80a999bb8f15e4406d97b14a15e')
b2sums=('SKIP'
        '9ae11a930e930953b16f7d6d1d3fbf0ebb6c4d8687cac1475560603442ed8edd452200468f7fe9c82af651d40ccad192c036940bfe57ef093e7c30cce93383f0')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # use pgx 0.4.3 for PostgreSQL 14 support
  sed \
    -i Cargo.toml \
    -e 's/^\(pgx.*\) = ".*"/\1 = "0.4.3"/g'

  # download dependencies
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo pgx --version

  # initialise pgx
  export _PGMAJOR=$(pg_config --version | sed -e 's/PostgreSQL //' -e 's/\..*//')
  cargo pgx init "--pg${_PGMAJOR}" /usr/bin/pg_config

  # build extension
  make
}

package() {
  cd "$pkgname"

  # extension
  make DESTDIR="$pkgdir" install

  # licenses
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE NOTICE
  install -Dm644 "$srcdir/LICENSE-TIMESCALE-$_license_commit" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-TIMESCALE"
}
