# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Dwayne Bent <dbb@dbb.io>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=prosody
pkgver=0.9.2
pkgrel=2
pkgdesc="Lightweight and extensible Jabber/XMPP server written in Lua"
arch=('i686' 'x86_64')
url="http://prosody.im/"
license=('MIT')
depends=('lua51' 'lua51-socket' 'lua51-expat' 'lua51-filesystem' 'libidn'
         'openssl')
optdepends=('lua51-sec: TLS encryption support')
install=prosody.install
backup=('etc/logrotate.d/prosody'
        'etc/prosody/prosody.cfg.lua')
source=("http://prosody.im/downloads/source/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'prosody.logrotated'
        'fix-config.patch'
        'prosody.tmpfile.d'
        'prosody.service')
sha256sums=('a2df64d23b7853a9f03126750d33b98193c93b8331b2622d9ebf1fe54f8feb2d'
            'SKIP'
            '5a2466b73bd069fb73be97a4e23b24e4c8dd1adb7db871cb8f5ab4094c1f967f'
            'db37b1b6bcd648f264164c5db5e754fc077671c74df6079c101384e146f4d6da'
            '3f5cc5db2cfe74c4edf91f60e692e04c3504025e893d0880114d52728a2c4bc4'
            '16fbfb54bf21bb73ce9d9acacb60d47f237c35e842c5a00f1153f72f0d2f7f6b')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../fix-config.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --ostype=linux --prefix=/usr --sysconfdir=/etc/prosody \
    --datadir=/var/lib/prosody --with-lua-include=/usr/include/lua5.1 \
    --cflags="${CFLAGS} -fPIC -Wall -Wextra -D_GNU_SOURCE" \
    --ldflags="${LDFLAGS} -shared" --no-example-certs \
    --runwith=lua5.1
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install -C tools/migration

  install -Dm 0644 $srcdir/prosody.logrotated "${pkgdir}"/etc/logrotate.d/prosody
  install -Dm 0644 $srcdir/prosody.tmpfile.d "${pkgdir}"/usr/lib/tmpfiles.d/prosody.conf
  install -Dm 0644 $srcdir/prosody.service "${pkgdir}"/usr/lib/systemd/system/prosody.service

  for i in tools/*.lua; do
    install -Dm 0644 ${i} "${pkgdir}"/usr/share/prosody/${i}
  done

  install -Dm 0644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
  install -dm 755 "${pkgdir}"/var/log/prosody
  rm "${pkgdir}"/etc/prosody/certs/*
}

# vim: ft=sh syn=sh ts=2 sw=2
