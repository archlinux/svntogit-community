# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Dwayne Bent <dbb.0@liqd.org>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>

pkgname=prosody
pkgver=0.7.0
pkgrel=6
pkgdesc="Lightweight and extensible Jabber/XMPP server written in Lua."
arch=('i686' 'x86_64')
url="http://prosody.im/"
license=('MIT')
depends=('lua>=5.1' 'luasocket' 'luaexpat' 'luafilesystem' 'libidn>=0.5.18' 'openssl')
optdepends=('luasec: TLS encryption support'
            'lua-zlib: compression support')
install=prosody.install
backup=('etc/logrotate.d/prosody'
	'etc/prosody/prosody.cfg.lua')
source=("http://prosody.im/depot/${pkgver}/prosody-${pkgver}.tar.gz"
	'prosody.rcd'
	'prosody.logrotated'
	'fix-config.patch')
md5sums=('69cd4f4e89dbef668a6240cde419dedf'
         'b1b159baab8d49b9ec08afaea1f66400'
         '26466fdbea87963a3ca6f48f76fe4a29'
         '431f78012ff4c1d66c173e0b0b3c7c16')

build() {
    cd "$srcdir/prosody-$pkgver"

    patch -p1 <$srcdir/fix-config.patch

    ./configure --prefix=/usr --sysconfdir=/etc/prosody \
        --datadir=/var/lib/prosody
    make
    make DESTDIR="$pkgdir" install

    rm $pkgdir/etc/prosody/certs/*

    install -d "$pkgdir/etc/rc.d"
    install -d "$pkgdir/etc/logrotate.d"
    install -d "$pkgdir/var/log/prosody"

    install -o root -g root -m 755 "$srcdir/prosody.rcd" \
        "$pkgdir/etc/rc.d/prosody"
    install -o root -g root -m 644 "$srcdir/prosody.logrotated" \
        "$pkgdir/etc/logrotate.d/prosody"

    install -D -m0644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
