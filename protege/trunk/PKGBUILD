
# Maintainer: Morten Linderud <morten@linderud.pw> 
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Stefan Husmann <stefan-husmannt-online.de>

pkgname=protege
pkgver=5.5.0
pkgrel=1
pkgdesc='Free, open source ontology editor and knowledge-base framework'
arch=('any')
url='https://protege.stanford.edu/'
license=('MPL')
depends=('java-runtime>=8' 'graphviz')
makedepends=('maven' 'jdk8-openjdk')
source=("$pkgname-$pkgver.tar.gz::https://github.com/protegeproject/protege/archive/v$pkgver.tar.gz"
        "${pkgname}.sh"
        "${pkgname}.desktop"
        "${pkgname}.png")
sha256sums=('e33157b1aeda494bad52c2ab2e5d78889a2848bbdd963d44e4acf11aadd729e9'
            '1c531c402b58130ebcd7e5c0719b4afb49ce928e8e1af07d59c6ee61f26666cc'
            'e0e054d864fafdee0c7a7a034b4a92fe2fe30fcce77f2aabd71afb57dbbbb0ab'
            '3ee61d8cfcbcfc91c86c9dddc24e40f9fc0e6943bf879548340610c95d64d194')

build(){
  cd "protege-$pkgver"
  mvn clean package -Dmaven.test.skip=true
}

package() {
  cd "protege-$pkgver/protege-desktop/target/protege-$pkgver-platform-independent/Protege-$pkgver"
  install -d "${pkgdir}/usr/share/java/${pkgname}"
  cp -r * "${pkgdir}/usr/share/java/${pkgname}"
  install -d "${pkgdir}/usr/bin"
  install -D "${srcdir}/protege.sh" "${pkgdir}/usr/bin/protege"
  install -Dm644 "${srcdir}/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "${srcdir}/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
