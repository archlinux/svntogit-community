# Maintainer: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=protobuf
pkgname=('protobuf' 'protobuf-python')
pkgver=2.4.1
pkgrel=1
pkgdesc="A way of encoding structured data in an efficient yet extensible format"
arch=('i686' 'x86_64')
url="http://code.google.com/p/protobuf/"
license=('APACHE')
depends=('gcc-libs' 'zlib')
makedepends=('python2' 'setuptools')
options=(!libtool)
source=(http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2)
md5sums=('ed436802019c9e1f40cc750eaf78f318')

build() {
	cd $srcdir/$pkgname-$pkgver
	./autogen.sh
	./configure --prefix=/usr --disable-static
}

package_protobuf() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
}

package_protobuf-python() {
	pkgdesc="protobuf python API"
	arch=('any')
	depends=("python2" "protobuf=${pkgver}")

	cd $srcdir/protobuf-$pkgver/python
	python2 setup.py install --prefix=/usr --root $pkgdir
}
