# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

pkgname=proxychains-ng
pkgver=4.9
pkgrel=1
pkgdesc="A hook preloader that allows to redirect TCP traffic of existing dynamically linked programs through one or more SOCKS or HTTP proxies"
arch=('i686' 'x86_64')
url="https://github.com/rofl0r/proxychains"
license=('GPL')
provides=('proxychains')
replaces=('proxychains')
conflicts=('proxychains')
depends=('glibc')
backup=('etc/proxychains.conf')
source=("http://downloads.sourceforge.net/project/$pkgname/proxychains-$pkgver.tar.bz2")
md5sums=('96d2ad3eaeec5a7c001a19fb2d819c80')

build() {
  cd proxychains-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd proxychains-$pkgver
  make DESTDIR="$pkgdir/" install
  make DESTDIR="$pkgdir/" install-config
  ln -s proxychains4 "$pkgdir/usr/bin/proxychains"
}

# vim:set ts=2 sw=2 et:
