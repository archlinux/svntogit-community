# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

pkgname=proxychains-ng
pkgver=4.12
pkgrel=1
pkgdesc="A hook preloader that allows to redirect TCP traffic of existing dynamically linked programs through one or more SOCKS or HTTP proxies"
arch=('x86_64')
url="https://github.com/rofl0r/proxychains"
license=('GPL')
provides=('proxychains')
replaces=('proxychains')
conflicts=('proxychains')
depends=('glibc')
makedepends=('git')
backup=('etc/proxychains.conf')
source=("git+https://github.com/rofl0r/proxychains-ng#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd proxychains-ng
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd proxychains-ng
  make DESTDIR="$pkgdir/" install
  make DESTDIR="$pkgdir/" install-config
  ln -s proxychains4 "$pkgdir/usr/bin/proxychains"
}

# vim:set ts=2 sw=2 et:
