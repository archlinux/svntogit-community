# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=ptex
pkgver=2.4.1
pkgrel=3
pkgdesc="Per-Face Texture Mapping for Production Rendering"
arch=('x86_64')
url="http://ptex.us/"
license=('BSD')
depends=('zlib' 'gcc-libs')
makedepends=('git' 'doxygen' 'cmake' 'ninja')
options=(staticlibs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/wdas/ptex/archive/v${pkgver}.tar.gz")
sha512sums=('ccf6b51650286c39a9f25c105a2303ab5dfc9f9d9d02cd98c6ffc976ed17f39d97968f2b9c68e269fd59db905768c36eab6a03e2014c7b2cbb83cb8a530bd35c')

build() {
    cd "$pkgname-$pkgver"

    CXXFLAGS+=" -ffat-lto-objects"
    cmake \
        -Bbuild \
        -GNinja \
        -DPTEX_SHA=$_commit_sha \
        -DPTEX_VER=$pkgver \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr

    ninja -C build
}

check() {
    cd "$pkgname-$pkgver"
    ninja -C build test
}

package() {
    cd "$pkgname-$pkgver"

    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
