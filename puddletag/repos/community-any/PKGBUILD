# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Lucky <archlinux@builds.lucky.li>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>

pkgname=puddletag
pkgver=1.0.2
pkgrel=1
pkgdesc='A tag editor for GNU/Linux'
url='http://puddletag.sourceforge.net'
license=('GPL')
arch=('any')
depends=('chromaprint' 'mutagen' 'python2-configobj' 'python2-musicbrainz2' 'python2-pyparsing' 'python2-pyqt4')
optdepends=('python2-pillow: FLAC cover art'
            'quodlibet: QuodLibet library support')
source=("http://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.gz")
sha256sums=('37570639908a0f13a2ef78f9d15782d9c6e981f70951acbe5f238dbe711bc2b1')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname}-${pkgver}

  python2 setup.py config
}

package() {
  cd ${pkgname}-${pkgver}

  python2 setup.py install --prefix="${pkgdir}/usr" --optimize='1'
}

# vim: ts=2 sw=2 et:
