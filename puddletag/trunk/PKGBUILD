# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Lucky <archlinux@builds.lucky.li>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>

pkgname=puddletag
pkgver=1.0.2rc1
pkgrel=1
pkgdesc="A tag editor for GNU/Linux"
url="http://puddletag.sourceforge.net"
license=("GPL")
arch=("any")
depends=("chromaprint" "mutagen" "python2-configobj" "python2-musicbrainz2" "python2-pyparsing" "python2-pyqt4")
optdepends=("python2-imaging: FLAC cover art"
            "quodlibet: QuodLibet library support")
source=("http://downloads.sourceforge.net/project/${pkgname}/dev/${pkgname}-${pkgver^^}.tar.gz")
sha256sums=('1dc1e336de5c0e07d4d1f5ef7549f8214807357454b31b055a21260fc1f39a6c')

prepare() {
  cd ${pkgname}-${pkgver^^}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname}-${pkgver^^}

  python2 setup.py config
}

package() {
  cd ${pkgname}-${pkgver^^}

  python2 setup.py install --prefix="${pkgdir}"/usr --optimize=1
}

# vim: ts=2 sw=2 et:
