# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Micha Alt <micha.tucker at gmail dot com>

pkgname=pulp
pkgver=9.0.1
pkgrel=1
pkgdesc="A build system for PureScript projects"
arch=('any')
url="https://github.com/bodil/pulp"
license=('LGPL')
depends=('nodejs' 'browserify' 'uglify-js' 'bower' 'purescript')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
md5sums=('de74c994ba8ffdfd178cf0fd8b9ff234')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in browserify uglify-js; do
    rm -r $dep;
    npm link $dep;
  done
}
