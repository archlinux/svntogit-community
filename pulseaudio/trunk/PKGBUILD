# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pulseaudio
pkgver=0.9.21
pkgrel=6
pkgdesc="A networked sound server"
arch=('i686' 'x86_64')
url="http://pulseaudio.org/"
license=('GPL' 'LGPL')
depends=('libasyncns' 'libsamplerate' 'libtool' 'rtkit' 'speex' 'tdb' 'udev' 'tcp_wrappers')
makedepends=('autoconf' 'avahi' 'bluez' 'doxygen' 'gconf' 'intltool' 'jack'
             'libatomic_ops' 'lirc-utils' 'pkgconfig' 'tcp_wrappers' 'libxtst'
             'libsm')
optdepends=('alsa-plugins: ALSA support'
            'avahi: network support'
            'bluez: bluetooth support'
            'gconf: configure pulseaudio through gconf'
            'jack: jack support'
            'lirc-utils: infra-red support')
options=('emptydirs' '!libtool' '!makeflags')
backup=('etc/pulse/client.conf' 'etc/pulse/daemon.conf' 'etc/pulse/default.pa' 'etc/pulse/system.pa')
install=pulseaudio.install
source=(http://0pointer.de/lennart/projects/pulseaudio/${pkgname}-${pkgver}.tar.gz
        pulseaudio.conf pulseaudio.rc)
md5sums=('76e623c4c72e2258bc8bdeb1599bad74' 'd4a7d4ad51b406588ba7ac7931c5dd88'
         'c5aa09c1d3e2217dc3bb23138e2a5a1d')

build() {
	# Fix linking dependency cycle between libpulse and libpulsecommon
	LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
	LDFLAGS="${LDFLAGS//,--as-needed}"
	export LDFLAGS="${LDFLAGS//--as-needed}"

	# Fix strict aliasing warnings
	export CFLAGS="${CFLAGS} -fno-strict-aliasing"

	cd ${srcdir}/${pkgname}-${pkgver}
	autoreconf -fi || return 1
	./configure --disable-hal --disable-rpath \
              --libexecdir=/usr/lib --localstatedir=/var \
              --prefix=/usr --sysconfdir=/etc --with-database=tdb
	make || return 1
	make doxygen || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install

	install -Dm755 ${srcdir}/pulseaudio.rc \
		${pkgdir}/etc/rc.d/pulseaudio || return 1
	install -Dm644 ${srcdir}/pulseaudio.conf \
		${pkgdir}/etc/conf.d/pulseaudio || return 1
	install -dm755 ${pkgdir}/var/run/pulse || return 1

	# Ugly fix for pulseaudio system users
	install -dm755 ${pkgdir}/var/pulse || return 1
	echo -e '\n### Automatically restore volumes\nload-module module-volume-restore table="/var/pulse/volume-restore.table"' \
	    >> ${pkgdir}/etc/pulse/system.pa || return 1
}

