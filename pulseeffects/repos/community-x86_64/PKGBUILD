# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Wellington <wellingtonwallace@gmail.com>

pkgname=pulseeffects
pkgver=4.6.8
pkgrel=3
pkgdesc='Audio Effects for Pulseaudio Applications'
arch=('x86_64')
url='https://github.com/wwmm/pulseeffects'
license=('GPL3')
depends=('gtk3' 'gtkmm3' 'glibmm' 'libpulse' 'gstreamer' 'gst-plugins-good' 'gst-plugins-bad'
        'lilv' 'boost-libs' 'libsigc++' 'libsndfile' 'libsamplerate' 'zita-convolver' 'libebur128'
        'calf' 'lsp-plugins')
makedepends=('meson' 'boost' 'itstool' 'appstream-glib'
             'calf' 'zam-plugins' 'rubberband' 'mda.lv2')
optdepends=('zam-plugins: maximizer'
            'rubberband: pitch shifting'
            'mda.lv2: loudness')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('672204ad90534ad45228b3473902eb8489c8306c7850f2b2a6f983aa89955529fb5381d6a7d703de94dcd658ffd5b560cce17291a0b23654b225ebd301659e74')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson ..

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}

