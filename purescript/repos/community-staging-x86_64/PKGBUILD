# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=purescript
pkgver=0.10.5
pkgrel=3
pkgdesc="PureScript Programming Language Compiler"
url="http://www.purescript.org/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib')
makedepends=("ghc=8.0.1" "haskell-aeson" "haskell-aeson-better-errors" "haskell-aeson-pretty"
             "haskell-ansi-terminal" "haskell-ansi-wl-pprint" "haskell-base-compat"
             "haskell-bower-json" "haskell-boxes" "haskell-clock" "haskell-data-ordlist"
             "haskell-dlist" "haskell-edit-distance" "haskell-file-embed" "haskell-foldl"
             "haskell-fsnotify" "haskell-glob" "haskell-http-client" "haskell-http-types"
             "haskell-language-javascript" "haskell-lens" "haskell-lifted-base"
             "haskell-monad-control" "haskell-monad-logger" "haskell-mtl"
             "haskell-optparse-applicative" "haskell-parallel" "haskell-parsec"
             "haskell-pattern-arrows" "haskell-pipes" "haskell-pipes-http" "haskell-protolude"
             "haskell-regex-tdfa" "haskell-safe" "haskell-semigroups" "haskell-sourcemap"
             "haskell-spdx" "haskell-split" "haskell-stm" "haskell-syb" "haskell-system-filepath"
             "haskell-text" "haskell-transformers-base" "haskell-transformers-compat"
             "haskell-turtle1.2" "haskell-unordered-containers" "haskell-utf8-string"
             "haskell-vector" "haskell-wai" "haskell-wai-websockets" "haskell-warp"
             "haskell-websockets")
optdepends=('pulp: for development environment')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        http-client-0.5.patch)
sha256sums=('0d36361819866efe703eb3ae37f597316098ec3ead6edc9236ea63d54bdc8916'
            '2e8ac0165d8dfde7f2ec27f4b77fb4139a435cf2148d3853ec95455334601b03')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 -i ../http-client-0.5.patch

    sed -e 's/pipes >= 4.0.0 && < 4.3.0,/pipes >= 4.0.0 \&\& < 4.4.0,/' \
        -e 's/websockets >= 0.9 && <0.10/websockets >= 0.9 \&\& <0.11/' \
        -e 's/aeson >= 0.8 && < 1.0,/aeson >= 0.8,/' \
        -i purescript.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share}
}
