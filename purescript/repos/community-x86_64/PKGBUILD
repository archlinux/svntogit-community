# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=purescript
pkgver=0.9.1
pkgrel=2
pkgdesc="PureScript Programming Language Compiler"
url="http://www.purescript.org/"
license=("MIT")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib')
makedepends=("ghc=8.0.1" "haskell-aeson" "haskell-aeson-better-errors" "haskell-ansi-terminal"
             "haskell-ansi-wl-pprint" "haskell-base-compat" "haskell-bower-json" "haskell-boxes"
             "haskell-dlist" "haskell-edit-distance" "haskell-fsnotify" "haskell-glob"
             "haskell-http-types" "haskell-language-javascript" "haskell-lifted-base"
             "haskell-monad-control" "haskell-monad-logger" "haskell-mtl"
             "haskell-optparse-applicative" "haskell-parallel" "haskell-parsec"
             "haskell-pattern-arrows" "haskell-pipes" "haskell-pipes-http" "haskell-regex-tdfa"
             "haskell-safe" "haskell-semigroups" "haskell-sourcemap" "haskell-spdx" "haskell-split"
             "haskell-stm" "haskell-syb" "haskell-text" "haskell-transformers-base"
             "haskell-transformers-compat" "haskell-unordered-containers" "haskell-utf8-string"
             "haskell-vector")
optdepends=('pulp: for development environment')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('72a4c9148a5d6b6ec2650185be7b8aa6a43a089d73037f870babd0ded9a5d0e7')

prepare() {
    sed -i 's/pipes >= 4.0.0 && < 4.2.0,/pipes >= 4.0.0 \&\& < 4.3.0,/' $pkgname-$pkgver/$pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share}
}
