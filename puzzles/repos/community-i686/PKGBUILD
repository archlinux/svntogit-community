# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=20160428
pkgrel=1
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/puzzles/"
license=('MIT')
makedepends=('pkgconfig' 'subversion' 'xorg-server-xvfb' 'imagemagick' 'git')
depends=('gtk2' 'desktop-file-utils')
source=("git://git.tartarus.org/simon/puzzles.git#commit=b31155b7")
md5sums=("SKIP")

prepare() {
    cd ${srcdir}/${pkgname}

    # Generate the makefiles.
    ./mkfiles.pl
    sed -i 's|\$(gamesdir)|\$(bindir)|' Makefile.gtk
    sed -i 's_-Werror __' Makefile.gtk
    sed -i 's_CFLAGS := -O2_CFLAGS := -O2 -Wno-variadic-macros -Wno-long-long_g' Makefile.gtk

    # Tweak desktop file generation to use proper icon paths.
    sed -i 's_\$icondir\/\$id-48d24.png_$id_' desktop.pl

    # And add to LogicGame category (FS#43512).
    sed -i 's_\(Categories=Game;\)_\1LogicGame;_' desktop.pl
}

build() {
    cd ${srcdir}/${pkgname}
    make -f Makefile.gtk prefix=/usr

    # Generate icons.
    cd ${srcdir}/${pkgname}/icons
    xvfb-run make pngicons
}

package() {
    # Install the icons.
    cd ${srcdir}/${pkgname}/icons

    install -d ${pkgdir}/usr/share/icons/hicolor/48x48/apps/
    for oldname in *-48d24.png; do
        newname=$(sed 's/\(\w*\)-48d24.png/\1.png/' <<< ${oldname})
        install -Dm644 ${oldname} ${pkgdir}/usr/share/icons/hicolor/48x48/apps/${newname}
    done

    # Generate .desktop files.
    cd ${srcdir}/${pkgname}

    install -d ${pkgdir}/usr/share/applications
    ./desktop.pl ${pkgdir}/usr/share/applications \
                 /usr/bin \
                 /unused/argument

    # And finally, install the games.
    cd ${srcdir}/${pkgname}

    install -d ${pkgdir}/usr/{bin,share/licenses/${pkgname}}
    install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/

    make -f Makefile.gtk prefix=/usr DESTDIR=${pkgdir} install

    # Fix FS#14600 (name conflicts with other packages).
    for oldname in net cube blackbox; do
        newname=puzzles-${oldname}
        mv ${pkgdir}/usr/bin/${oldname} ${pkgdir}/usr/bin/${newname}
        sed -i "s_Exec=/usr/bin/${oldname}_Exec=/usr/bin/${newname}_" \
            ${pkgdir}/usr/share/applications/${oldname}.desktop
    done
}
