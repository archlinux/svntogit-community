# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=20200413
pkgrel=1
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=('x86_64')
url="https://www.chiark.greenend.org.uk/~sgtatham/puzzles/"
license=('MIT')
makedepends=('pkgconfig' 'git')
depends=('gtk2' 'desktop-file-utils')
source=("git://git.tartarus.org/simon/puzzles.git#commit=e2135d51c51a39f05e2c20c70111b27c15952803"
        "icons.tar.xz")
md5sums=('SKIP'
         '71488c328e2381c27a5084b30ea3516a')

prepare() {
    cd ${srcdir}
    tar xvf icons.tar.xz

    cd ${srcdir}/${pkgname}

    # Generate the makefiles.
    ./mkfiles.pl
    sed -i 's|\$(gamesdir)|\$(bindir)|' Makefile.gtk
    sed -i 's_-Werror __' Makefile.gtk
    sed -i 's_CFLAGS := -O2_CFLAGS := -O2 -Wno-variadic-macros -Wno-long-long_g' Makefile.gtk

    # Tweak desktop file generation to use proper icon paths.
    sed -i 's_\$icondir\/\$id-48d24.png_$id_' desktop.pl

    # And add to LogicGame category (FS#43512).
    sed -i 's_\(Categories=Game;\)_\1LogicGame;_' desktop.pl
}

build() {
    cd ${srcdir}/${pkgname}
    make -f Makefile.gtk prefix=/usr

    # Generating icons no longer works, xvfb-run reports
    # 'headless mode not supported below GTK3'. We now keep previously
    # generated icons manually.
}

package() {
    # Install the icons.
    cd ${srcdir}/icons

    install -d ${pkgdir}/usr/share/icons/hicolor/48x48/apps/
    for f in ./*.png; do
        install -Dm644 ${f} ${pkgdir}/usr/share/icons/hicolor/48x48/apps/${f}
    done

    # Generate .desktop files.
    cd ${srcdir}/${pkgname}

    install -d ${pkgdir}/usr/share/applications
    ./desktop.pl ${pkgdir}/usr/share/applications \
                 /usr/bin \
                 /unused/argument

    # And finally, install the games.
    cd ${srcdir}/${pkgname}

    install -d ${pkgdir}/usr/{bin,share/licenses/${pkgname}}
    install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/

    make -f Makefile.gtk prefix=/usr DESTDIR=${pkgdir} install

    # Fix FS#14600 (name conflicts with other packages).
    cd ${pkgdir}/usr/bin
    for oldname in *; do
        newname=puzzles-${oldname}
        mv ${pkgdir}/usr/bin/${oldname} ${pkgdir}/usr/bin/${newname}
        sed -i "s_Exec=/usr/bin/${oldname}_Exec=/usr/bin/${newname}_" \
            ${pkgdir}/usr/share/applications/${oldname}.desktop
    done
}
