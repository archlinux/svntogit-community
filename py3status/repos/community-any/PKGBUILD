# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Alexis "Horgix" Chotard <alexis.horgix.chotard@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG

pkgname=py3status
pkgver=3.26
pkgrel=1
pkgdesc="An extensible i3status replacement/wrapper written in python"
url="https://www.ultrabug.fr/tag/py3status/"
arch=('any')
license=('BSD')
depends=('python' 'python-setuptools')
checkdepends=('python-pytest-runner')
optdepends=('i3status: for i3status modules'
            'i3blocks: for i3blocks modules'
            'python-pyudev: for udev event monitoring'
            'python-pydbus: for modules that rely on D-Bus'
            'python-tzlocal: for modules that need time zone info'
            'pacman-contrib: for the arch_updates module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ultrabug/py3status/archive/$pkgver.tar.gz")
sha256sums=('6b41b28f0df909e1fdcd9f2931aa1f01c31986f822ec932289b78d999d51f597')

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

check() {
    cd "$pkgname-$pkgver"
    python setup.py pytest
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -d "$pkgdir/usr/share/doc/$pkgname"
    install -m644 doc/* README.rst CHANGELOG "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
