# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: David Keogh <davekeogh@archlinux.us>

pkgname=pycharm-community-edition
pkgver=2018.3.5
_build=183.5912.18
pkgrel=2
pkgdesc='Python IDE for Professional Developers'
arch=(x86_64)
url=https://www.jetbrains.com/pycharm/
license=(APACHE)
depends=(
  giflib
  java-runtime=8
  python
  sh
  ttf-font
)
optdepends=(
  'ipython2: IPython integration for Python 2'
  'ipython: IPython integration for Python 3'
  'python2: Python 2 support'
)
makedepends=(
  ant
  java-environment=8
  java8-openjfx
  python2-setuptools
  python-setuptools
)
source=(
  pycharm-${_build}.tar.gz::https://github.com/JetBrains/intellij-community/archive/pycharm/${_build}.tar.gz
  pycharm-build.patch
  pycharm.desktop
  pycharm.sh
)
sha256sums=('d122487912ae50a8ee84e6b48dba671dba6d72f3ee8cc7e3eb22927424a096a9'
            '1cc14afd8478e03efccb77b3c490efa4c720301d02c76ff1ff84659008dcb558'
            '28e0d3200c721e61831d8246eba8f72485d3c8379d10bfa75ccb5331b6749480'
            '448f90f35f1ae1de784b663e9fd539baf494a7c08a8a7e2efbd5e75c801da12e')

prepare() {
  cd intellij-community-pycharm-${_build}

  patch -Np1 -i ../pycharm-build.patch
  sed "s/SNAPSHOT/${_build}/" -i python/build.xml
  sed '/intellij\.markdown/d' -i python/{build/plugin-list.txt,intellij.pycharm.community.main.iml}
}

build() {
  cd intellij-community-pycharm-${_build}/python

  unset _JAVA_OPTIONS

  ant build
  tar -xf ../out/pycharm-ce/artifacts/pycharmPC-${_build}-no-jdk.tar.gz -C "${srcdir}"

  cd ../../pycharm-community-${pkgver}

  python2 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
  python3 helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
}

package() {
  cd pycharm-community-${pkgver}

  # workaround FS#40934
  sed -i 's/lcd/on/' bin/*.vmoptions

  rm -rf bin/fsnotifier{,-arm} lib/libpty/linux/x86

  install -dm 755 "${pkgdir}"/usr/share/{licenses,pixmaps,pycharm}
  cp -dr --no-preserve='ownership' bin helpers lib plugins "${pkgdir}"/usr/share/pycharm/
  cp -dr --no-preserve='ownership' license "${pkgdir}"/usr/share/licenses/pycharm/
  ln -s /usr/share/pycharm/bin/pycharm.png "${pkgdir}"/usr/share/pixmaps/
  install -Dm 644 ../pycharm.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 755 ../pycharm.sh "${pkgdir}"/usr/bin/pycharm
}

# vim: ts=2 sw=2 et:
