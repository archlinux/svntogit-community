# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Daniel Maslowski <info@orangecms.org>

pkgbase=pycosat
pkgname=(python-pycosat python2-pycosat)
pkgver=0.6.3
pkgrel=2
arch=(x86_64)
url="https://github.com/ContinuumIO/pycosat"
license=(MIT)
makedepends=(python python2)
source=($pkgbase-$pkgver.tar.gz::https://github.com/ContinuumIO/$pkgbase/archive/$pkgver.tar.gz)
sha256sums=('abcaf3d6afb332b0d8363590d0aaa81f00f71142900fbae2608f7d595d5e4492')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd ../$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-pycosat() {
  pkgdesc="Python 3 bindings to picosat (a SAT solver)"
  depends=(python)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_python2-pycosat() {
  pkgdesc="Python 2 bindings to picosat (a SAT solver)"
  depends=(python2)

  cd $pkgbase-$pkgver-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

