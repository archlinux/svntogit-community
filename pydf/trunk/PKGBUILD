# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>

pkgname=pydf
pkgver=10
pkgrel=2
pkgdesc='Colorized df clone'
arch=("any")
url="http://kassiopeia.juls.savba.sk/~garabik/software/pydf/"
license=('custom')
depends=('python')
backup=('etc/pydfrc')
source=(http://kassiopeia.juls.savba.sk/~garabik/software/pydf/pydf_$pkgver.tar.gz)
md5sums=('7f7eeafa90cad534875b2cb23aa3b2f1')

build() {
	cd "$srcdir/pydf-$pkgver"
	sed -i 's|#! /usr/bin/python|#!/usr/bin/python3|' pydf
}

package() {
	cd "$srcdir/pydf-$pkgver"
	install -Dm755 pydf "$pkgdir/usr/bin/pydf"
	install -Dm644 pydfrc "$pkgdir/etc/pydfrc"
	install -Dm644 pydf.1 "$pkgdir/usr/share/man/man1/pydf.1"
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/pydf/COPYING"
}
