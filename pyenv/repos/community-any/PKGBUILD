# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pyenv
pkgver=2.1.0
pkgrel=1
pkgdesc="Easily switch between multiple versions of Python"
arch=('any')
url="https://github.com/pyenv/pyenv"
license=('MIT')
depends=('bash')
optdepends=('git: installing development versions')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('8cfca28a4aa9feb62bf78815fbb1a7666fb706ef5ed29a55c69ac8515822acccaea83872a22fb71385e0a29062077bc6eec7480179e5ec4ebbca2b824be71c87')
b2sums=('5d1e47bd1acd5759cafcc05306bfb5283f8f35c796969a18a772b8a78f2d6a1b509a56693bfd194b720ca4a84b09cec2aa97c9afb1cccadff2f64edcf1dcc9c4')

package() {
  cd "$pkgname-$pkgver"
  # application
  install -vDm 755 libexec/* -t "${pkgdir}/usr/share/${pkgname}/libexec"
  install -vdm 755 "${pkgdir}/usr/bin"
  ln -sv "/usr/share/${pkgname}/libexec/${pkgname}" \
    "${pkgdir}/usr/bin/${pkgname}"
  # application hooks
  install -vDm 755 pyenv.d/exec/pip-rehash/* \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/exec/pip-rehash"
  install -vDm 644 pyenv.d/exec/*.bash \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/exec/"
  install -vDm 644 pyenv.d/rehash/*.bash \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/rehash"
  install -vDm 644 pyenv.d/rehash/conda.d/* \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/rehash/conda.d"
  install -vDm 644 pyenv.d/rehash/source.d/* \
    -t "${pkgdir}/usr/share/${pkgname}/pyenv.d/rehash/source.d"
  # plugin
  install -vDm 755 plugins/python-build/bin/* \
    -t "${pkgdir}/usr/share/${pkgname}/plugins/python-build/bin"
  for bin in {${pkgname}-{install,uninstall},python-build}; do
    ln -sv "/usr/share/${pkgname}/plugins/python-build/bin/${bin}" \
      "${pkgdir}/usr/bin/${bin}"
  done
  cp -av plugins/python-build/share \
    "${pkgdir}/usr/share/${pkgname}/plugins/python-build"
  # licenses
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 plugins/python-build/LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.python-build"
  # shell completion
  install -vDm 644 completions/*.fish \
    -t "${pkgdir}/usr/share/fish/vendor_completions.d/"
  install -vDm 644 completions/${pkgname}.bash \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -vDm 644 completions/${pkgname}.zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # man page
  install -vDm 644 man/man1/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {CHANGELOG,COMMANDS,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 plugins/python-build/README.md \
    "${pkgdir}/usr/share/doc/${pkgname}/README-python-build.md"
}
