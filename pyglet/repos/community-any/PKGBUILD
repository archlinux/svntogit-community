# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=pyglet
pkgname=('python-pyglet' 'python2-pyglet')
pkgver=1.2.4
pkgrel=1
pkgdesc="A cross-platform windowing and multimedia library for Python"
arch=('any')
url="http://pyglet.org/"
license=('BSD')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
#optdepends=('avbin: provides audio&video support')
source=("https://pypi.python.org/packages/source/p/pyglet/pyglet-$pkgver.tar.gz")
md5sums=('ba640b5fea8d670efdd747daa70a529a')

prepare() {
	cd "$srcdir"
	cp -r "$srcdir/$pkgbase-$pkgver" "$srcdir/${pkgbase}2-$pkgver"
}

build() { 
	cd "$srcdir/${pkgbase}2-$pkgver"
	python2 setup.py build

	cd "$srcdir/$pkgbase-$pkgver"
	python3 setup.py build
}

package_python2-pyglet() {
	depends=('python2')
	conflicts=('pyglet')
	provides=('pyglet')
	replaces=('pyglet')
	cd "$srcdir/${pkgbase}2-$pkgver"
	find ./ -type f -name '*.py' | xargs -n 1 sed -i 's|/usr/bin/env python$|/usr/bin/env python2|'
	find ./ -type f -name '*.py' | xargs -n 1 sed -i 's|/usr/bin/python|/usr/bin/env python2|'
	python2 setup.py install --root="$pkgdir" --optimize=0
        find "$pkgdir/" -name '*.pyc' -delete
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-pyglet() {
	depends=('python')
	cd "$srcdir/$pkgbase-$pkgver"
	python3 setup.py install --root="$pkgdir" --optimize=0
        find "$pkgdir/" -name '*.pyc' -delete
	find "$pkgdir/" -type d -empty -delete
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

