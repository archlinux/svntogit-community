# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=pyglet
pkgname=('python-pyglet' 'python2-pyglet')
pkgver=1.4.7
pkgrel=1
pkgdesc="A cross-platform windowing and multimedia library for Python"
arch=('any')
url="https://github.com/pyglet/pyglet"
license=('BSD')
depends=('python' 'glu' 'python-future')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
optdepends=('ffmpeg: provides audio&video support')
source=("https://files.pythonhosted.org/packages/source/p/pyglet/pyglet-$pkgver.zip")
md5sums=('fa316d8c3655756ecf8c1ef063d07b37')

prepare() {
	cd "$srcdir"

	# https://bitbucket.org/pyglet/pyglet/issues/157/please-do-not-bundle-the-future-module
	rm -rf pyglet-$pkgver/pyglet/extlibs/future/
	cp -r "$srcdir/$pkgbase-$pkgver" "$srcdir/${pkgbase}2-$pkgver"
}

build() {
	cd "$srcdir/${pkgbase}2-$pkgver"
	python2 setup.py build

	cd "$srcdir/$pkgbase-$pkgver"
	python3 setup.py build
}

package_python2-pyglet() {
	depends=('python2' 'glu' 'python2-future')
	cd "$srcdir/${pkgbase}2-$pkgver"
	find ./ -type f -name '*.py' | xargs -n 1 sed -i 's|/usr/bin/env python$|/usr/bin/env python2|'
	find ./ -type f -name '*.py' | xargs -n 1 sed -i 's|/usr/bin/python|/usr/bin/env python2|'
	python2 setup.py install --root="$pkgdir" --optimize=1
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-pyglet() {
	depends=('python' 'glu' 'python-future')
	cd "$srcdir/$pkgbase-$pkgver"
	python3 setup.py install --root="$pkgdir" --optimize=1
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
