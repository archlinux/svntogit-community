# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=pynac
pkgver=0.7.26
pkgrel=8
pkgdesc="Python version of GiNaC, a C++ library for symbolic mathematical calculations"
arch=(x86_64)
url="http://pynac.org"
license=(GPL)
depends=(python singular) # giac disabled because of https://trac.sagemath.org/ticket/26427
source=("https://github.com/pynac/pynac/releases/download/pynac-$pkgver/pynac-$pkgver.tar.bz2"
         pynac-python-3.8.patch)
sha256sums=('cc124ae9757355e3434ff864aeca6503184faf0642d26bc2f7bca6f3f2c9b50f'
            'a5b6de6521d33d4b7e4d06e841dbfc45d71b9ded09fc38ac2e5c7b7a8564f3bd')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../pynac-python-3.8.patch # Define PY_SSIZE_T_CLEAN as required by python 3.8
}

build() {
  cd pynac-$pkgver

  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd pynac-$pkgver
  make DESTDIR="$pkgdir" install
}
