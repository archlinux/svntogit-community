# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pynormaliz
pkgname=(python2-pynormaliz python-pynormaliz)
pkgver=1.5
pkgrel=1
pkgdesc="A Python interface to Normaliz"
arch=(i686 x86_64)
url="https://github.com/sebasguts/PyNormaliz"
license=(GPL2)
makedepends=(python-setuptools python2-setuptools normaliz)
source=("https://pypi.io/packages/source/P/PyNormaliz/PyNormaliz-$pkgver.tar.gz")
md5sums=('22eea4624caff219618640ed67003b9d')

prepare() {
  cp -r PyNormaliz-$pkgver PyNormaliz-$pkgver-py2
}

build() {
  cd PyNormaliz-$pkgver
  python setup.py build

  cd ../PyNormaliz-$pkgver-py2
  python2 setup.py build
}

package_python2-pynormaliz() {
  depends=(python2 normaliz)
  cd PyNormaliz-$pkgver-py2

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python-pynormaliz() {
  depends=(python normaliz)
  cd PyNormaliz-$pkgver
  
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

