# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Štěpán Němec <stepnem@gmail.com>

pkgname=pypinyin
pkgver=0.43.0
pkgrel=1
pkgdesc='Chinese characters transliteration module and tool'
arch=('any')
url='https://github.com/mozillazg/python-pinyin'
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'phrase-pinyin-data' 'pinyin-data')
checkdepends=('python-pytest-cov')
source=("https://github.com/mozillazg/python-pinyin/archive/v$pkgver/python-pinyin-$pkgver.tar.gz")
sha256sums=('7775f3d76ed01ea86f265e8e7872adebbdb2ab682545090ac96cc185a4f18cfb')

prepare() {
  cd python-pinyin-$pkgver
  ln -s /usr/share/pinyin-data/pinyin.txt pinyin-data/
  ln -s /usr/share/phrase-pinyin-data/pinyin.txt phrase-pinyin-data/
}

build() {
  cd python-pinyin-$pkgver
  make gen_data
  python setup.py build
}

check() {
  cd python-pinyin-$pkgver
  pytest pypinyin tests
}

package() {
  cd python-pinyin-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
