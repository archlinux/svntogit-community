# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>
# Contributor: Brecht Machiels <brecht at mos6581 dot org>

pkgbase=pyprof2calltree
pkgname=(pyprof2calltree python2-pyprof2calltree)
pkgver=1.4.3
pkgrel=2
pkgdesc="Help visualize profiling data from cProfile with kcachegrind"
url="https://pypi.python.org/pypi/pyprof2calltree/"
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/pyprof2calltree/pyprof2calltree-$pkgver.tar.gz")
sha512sums=('25b56460c613944259b4b763167b187fe9a6b010e5713cb411facc5d9b0f4157539f8d2fa6eafd4d1c243ae29b4cdcce79bf6dbc14ed7b4abeb93a91e9710e2d')

prepare() {
  sed -n '/# Copyright/,/# THE SOFTWARE\./p' pyprof2calltree-$pkgver/pyprof2calltree.py > LICENSE
  cp -a pyprof2calltree-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyprof2calltree-$pkgver
  python setup.py build

  cd "$srcdir"/pyprof2calltree-$pkgver-py2
  python setup.py build
}

package_pyprof2calltree() {
  depends=('python-setuptools')

  cd pyprof2calltree-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyprof2calltree() {
  depends=('python2-setuptools')

  cd pyprof2calltree-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir/usr/bin/pyprof2calltree"{,2}
}
