# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=pypy
pkgver=2.5.0
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'tk' 'sqlite' 'mercurial')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
source=("hg+http://bitbucket.org/pypy/pypy#tag=release-${pkgver}")
md5sums=('SKIP')

build() {
  cd pypy/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone
}

package() {
  cd pypy

  install -Dm755 pypy/goal/pypy-c "${pkgdir}"/opt/pypy/bin/pypy-c
  install -Dm755 pypy/goal/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so
  cp -r include lib_pypy site-packages "${pkgdir}"/opt/pypy/

  mkdir "${pkgdir}"/opt/pypy/lib-python/
  cp -r lib-python/2.7 "${pkgdir}"/opt/pypy/lib-python/

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy-c "${pkgdir}"/usr/bin/pypy

  install -Dm644 LICENSE "${pkgdir}"/opt/pypy/LICENSE
  install -Dm644 README.rst "${pkgdir}"/opt/pypy/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE

  # Compile binary modules
  export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
  for module in _curses syslog gdbm sqlite3 _tkinter; do
     "${pkgdir}"/opt/pypy/bin/pypy-c -c "import ${module}"
  done

  # Generate bytecode (they apparently haven't intended for this to happen yet)
  #"${pkgdir}"/opt/pypy/bin/pypy-c -m compileall "${pkgdir}"/opt/pypy
  #"${pkgdir}"/opt/pypy/bin/pypy-c -O -m compileall "${pkgdir}"/opt/pypy
}
# vim: ts=2 sw=2 et:
