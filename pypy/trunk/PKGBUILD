# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: William Giokas <1007380@gmail.com>

#_hgrev=63547

pkgname=pypy
pkgver=2.1
#[[ -n $_hgrev ]] && pkgver=2.0beta2.$_hgrev
pkgrel=2
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('libffi')
provides=('python2')
options=(!buildflags)
makedepends=('python2' 'mercurial' 'pypy' 'tk')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module'
            'tk: tk module')
license=('custom:MIT')
#source=("hg+https://bitbucket.org/pypy/pypy#revision=$_hgrev")
source=("https://bitbucket.org/pypy/pypy/downloads/$pkgname-$pkgver-src.tar.bz2")
md5sums=('7c43cd95aa37a7f0ca2465ed930646c0')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit targetpypystandalone
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/tool/release

  python2 package.py ../../../ pypy pypy-c "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy-c "${pkgdir}"/usr/bin/pypy

  install -Dm644 "${pkgdir}"/opt/pypy/LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE
}
# vim: ts=2 sw=2 et:
