# $Id: PKGBUILD 95035 2013-08-04 09:44:24Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=pypy3
pkgver=5.10.0
pkgrel=1
pkgdesc="A Python3 implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('x86_64')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'sqlite' 'tk' 'python2-pycparser')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
source=("https://bitbucket.org/pypy/pypy/downloads/pypy3-v${pkgver}-src.tar.bz2")
sha512sums=('69a373c18dbad9dd617f994ad885c8d8975f0c484ba9dd547e6e892c9a3030a43e9e3ee668aee5f4969403883de1311cef97a0a5e5701921172ee6c2119b771b')

prepare() {
  cd pypy3-v${pkgver}-src
}

build() {
  cd pypy3-v${pkgver}-src/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone

  # Compile binary modules
  PYTHONPATH=../.. ./pypy3-c ../tool/build_cffi_imports.py
}

package() {
  cd pypy3-v${pkgver}-src

  # Prepare installation
  python2 pypy/tool/release/package.py --archive-name pypy --targetdir .
  mkdir unpacked
  tar xf pypy.tar.bz2 -C unpacked

  # Install pypy
  cd unpacked
  install -Dm755 pypy/bin/pypy3 "${pkgdir}"/opt/pypy3/bin/pypy3
  install -Dm755 pypy/bin/libpypy3-c.so "${pkgdir}"/usr/lib/libpypy3-c.so
  cp -r pypy/include pypy/lib_pypy pypy/lib-python pypy/site-packages "${pkgdir}"/opt/pypy3
  cd ..

  # Install symlink
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy3/bin/pypy3 "${pkgdir}"/usr/bin/pypy3

  # Install misc stuff
  install -Dm644 README.rst "${pkgdir}"/opt/pypy3/README.rst
  install -Dm644 LICENSE "${pkgdir}"/opt/pypy3/LICENSE
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy3/LICENSE
}
# vim: ts=2 sw=2 et:
