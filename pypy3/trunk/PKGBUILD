# $Id: PKGBUILD 95035 2013-08-04 09:44:24Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

#_hgrev=63547

pkgname=pypy3
pkgver=2.3.1
_pkgver=2.3.1
#[[ -n $_hgrev ]] && pkgver=2.0beta2.$_hgrev
pkgrel=2
pkgdesc="A Python3 implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
provides=('python')
depends=('python2' 'expat' 'bzip2' 'openssl' 'libffi' 'zlib') #gdbm
makedepends=('sqlite' 'tk')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
#source=("hg+https://bitbucket.org/pypy/pypy#revision=$_hgrev")
source=("https://bitbucket.org/pypy/pypy/downloads/$pkgname-$_pkgver-src.tar.bz2")
md5sums=('83731c1cf0b5ecd5acf39210d7710816')

build() {
  cd ${pkgname}-${_pkgver}-src/pypy/goal

  export libdir=/usr/lib
  python2 ../../rpython/bin/rpython -Ojit targetpypystandalone
}

package() {
  cd ${pkgname}-${_pkgver}-src

  install -Dm755 pypy/goal/pypy-c "${pkgdir}"/opt/pypy3/bin/pypy-c
  #install -Dm755 pypy/goal/libpypy-c.so "${pkgdir}"/usr/lib/libpypy3-c.so
  cp -r include lib_pypy site-packages "${pkgdir}"/opt/pypy3/

  mkdir "${pkgdir}"/opt/pypy3/lib-python/
  cp -r lib-python/3 "${pkgdir}"/opt/pypy3/lib-python/

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy3/bin/pypy-c "${pkgdir}"/usr/bin/pypy3

  install -Dm644 LICENSE "${pkgdir}"/opt/pypy3/LICENSE
  install -Dm644 README.rst "${pkgdir}"/opt/pypy3/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy3/LICENSE

  # Compile binary modules
  for module in _curses syslog sqlite3 _tkinter; do #gdbm broken
    "${pkgdir}/opt/pypy3/bin/pypy-c" -c "import ${module}"
  done
}
# vim: ts=2 sw=2 et:
