# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.247
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://github.com/microsoft/pyright
license=('MIT')
depends=('nodejs>=12')
makedepends=('npm')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('af40d7421ffa162b969aebabc064c1ecb2c73c30a394ed471399eec747107a00ac222c05f845bb61d415a5e88f2ee25cc05299028d0b93d5302007c95a36db08')

prepare() {
  cd $pkgname-$pkgver
  npm ci
}

build() {
  cd $pkgname-$pkgver/packages/$pkgname
  npm run build
}

package() {
  local _npmdir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/{usr/bin,$_npmdir,usr/share/doc/$pkgname}
  ln -s $_npmdir/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s $_npmdir/langserver.index.js \
    "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname-$pkgver
  cp -r {CONTRIBUTING,README}.md docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  cp -r dist {,langserver.}index.js "$pkgdir"/$_npmdir
}

# vim:set ts=2 sw=2 et:
