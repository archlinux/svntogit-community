# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=pysolfc-cardsets
pkgver=2.0
pkgrel=2
pkgdesc="A collection of cardsets adapted for use with PySolFC"
arch=('any')
url="http://pysolfc.sourceforge.net/"
license=('GPL3')
depends=('pysolfc')
source=(http://downloads.sourceforge.net/pysolfc/PySolFC-Cardsets-${pkgver}.tar.bz2)
md5sums=('0ce4aa6801196f9166815b244aeea47e')
sha1sums=('cbf26b7e282c242a2080dfd06e3847abbaf67303')

package() {
  cd "${srcdir}/PySolFC-Cardsets-${pkgver}"
  install -d "${pkgdir}/usr/share/PySolFC"
  cp -r * "${pkgdir}/usr/share/PySolFC"

# removing cardsets included in pysolfc
  rm -r "${pkgdir}"/usr/share/PySolFC/cardset-{2000,crystal-mahjongg,dashavatara-ganjifa}
  rm -r "${pkgdir}"/usr/share/PySolFC/cardset-{dondorf,gnome-mahjongg-1,hexadeck,kintengu}
  rm -r "${pkgdir}"/usr/share/PySolFC/cardset-{matrix,mughal-ganjifa,oxymoron,standard}
  rm -r "${pkgdir}"/usr/share/PySolFC/cardset-{tuxedo,vienna-2k}
}
