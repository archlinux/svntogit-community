# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=pysolfc
pkgver=2.6.4
pkgrel=2
pkgdesc="PySol (Fan Club edition) is an exciting collection of more than 1000 solitaire games"
arch=('any')
url="http://pysolfc.sourceforge.net/"
license=('GPL3')
depends=('tk' 'python-pillow' 'python-random2' 'python-six')
optdepends=('pysolfc-cardsets')
source=(https://downloads.sourceforge.net/pysolfc/PySolFC-${pkgver}.tar.xz)
sha256sums=('4dab3579531530136fb629c045d4b8389f410962e6cf77494f18c61c9de6ea78')

prepare() {
  cd PySolFC-${pkgver}

  sed -i 's/pysol.py/pysol/' data/pysol.desktop
}


package() {
  cd PySolFC-$pkgver

  python setup.py install --root="${pkgdir}"
  mv "${pkgdir}"/usr/bin/pysol.py "${pkgdir}"/usr/bin/pysol
}
