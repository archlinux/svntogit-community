# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=pysolfc
pkgver=2.0
pkgrel=4
pkgdesc='PySol (Fan Club edition) is an exciting collection of more than 1000 solitaire games'
arch=('any')
url="http://pysolfc.sourceforge.net/"
license=('GPL3')
depends=('python2' 'tk' 'pysol-sound-server' 'python-imaging')
conflicts=('pysol')
replaces=('pysol')
source=(http://downloads.sourceforge.net/pysolfc/PySolFC-${pkgver}.tar.bz2)
sha1sums=('7a11aa04b3f5147396c8b5011228094c10ddc257')

package() {
  cd "${srcdir}/PySolFC-${pkgver}"
  find . -name \*.py -exec sed -i -e "s|#!/usr/bin/env python|#!/usr/bin/env python2|" {} \;
  python2 setup.py install --root="${pkgdir}"
  ln -s pysol.py "${pkgdir}/usr/bin/pysol"
}
