# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
pkgver=0.11.0
pkgrel=3
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-async_generator)
checkdepends=(python-flask python-moto python-pytest python-pytest-asyncio)
source=("https://github.com/aio-libs/aiobotocore/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        fix-tests-with-proxies.patch
        $pkgname-moto-1.3.14.diff)
sha256sums=('e2f2ff362745250524df961e50a42fdd0625ac5b3411b49f492d2f828ca9fcaa'
            '248aee05a468848dda2c40185f49714ebe8b789913dd7bb49018d69f1f3606f7'
            '94b560d753ac31709e9ffb3771759561979e061772de1789f7dfe102fe01ca8c')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../fix-tests-with-proxies.patch
  # Workaround removal of DEFAULT_TOPIC_POLICY from moto.sns.models
  # See: https://github.com/spulec/moto/pull/2517
  patch -Np1 -i ../$pkgname-moto-1.3.14.diff

  # Disable dependency pinning
  # Upstream tracking issue: https://github.com/aio-libs/aiobotocore/issues/670
  rm -v tests/test_patches.py
  sed -i -r "s#'(botocore.*),<.*',#'\1',#" setup.py
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=build/lib pytest -v -m moto tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
