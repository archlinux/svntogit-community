# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
pkgver=0.10.3
pkgrel=1
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-async_generator)
checkdepends=(python-flask python-moto python-pytest python-pytest-asyncio)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/aio-libs/aiobotocore/archive/$pkgver.tar.gz"
        moto-1.3.8.patch)
sha256sums=('5af3a575dee8f6d4bda36a237807edc7e864cc0e45c0b20dc07521191ee04759'
            '7953b9cf8b4256cf0020098a978ef5c48097dfbaafe1c2cbf22d590ed1b07e5c')

prepare() {
  cd $_pkgname-$pkgver

  # used in a skipped test only
  sed -i '/from dill.source import getsource/d' tests/test_patches.py

  patch -Np1 -i ../moto-1.3.8.patch
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # test_patches works only for pinned versions of dependencies
  PYTHONPATH=build/lib pytest -v -m moto -k 'not test_patches' tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
