# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: cclin <cclinet@outlook.com>

pkgname=python-aiofiles
pkgver=0.7.0
pkgrel=2
pkgdesc='File support for asyncio'
arch=(any)
license=(APACHE)
url=https://github.com/Tinche/aiofiles
makedepends=(
  git
  python-pip
  python-poetry
)
checkdepends=(
  python-coverage
  python-pytest-asyncio
  python-pytest
  python-pytest-cov
  python-tox
)
_tag=469c1a8b94ccccf25516706c88dde2e69ea45f3c
source=(git+https://github.com/Tinche/aiofiles.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd aiofiles
  git describe --tags | sed 's/^v//'
}

build() {
  cd aiofiles
  poetry build --format wheel
}

check() {
  cd aiofiles
  tox -e py310
}

package() {
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="${pkgdir}" --ignore-installed --no-deps aiofiles/dist/*.whl
  install -Dm 644 aiofiles/LICENSE -t "${pkgdir}"/usr/share/licenses/python-aiofiles/
}

# vim: ts=2 sw=2 et:
