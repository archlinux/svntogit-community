# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=python-aiohttp-socks
pkgver=0.7.0
pkgrel=1
pkgdesc="SOCKS proxy connector for aiohttp. SOCKS4(a) and SOCKS5 are supported."
arch=('any')
url="https://github.com/romis2012/aiohttp-socks/"
license=('Apache')
depends=('python-aiohttp' 'python-python-socks')
makedepends=('git' 'python' 'python-setuptools')
checkdepends=('python-flask' 'python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/romis2012/aiohttp-socks?signed#tag=v${pkgver}")
sha256sums=('SKIP')
validpgpkeys=('706A8935FED55F3BFB7C188B5EEAD7400A5D80ED') # Roman Snegirev (maintainer)

check() {
    cd "${pkgname//python-}"
    pytest
}

build() {
    cd "${pkgname//python-}"
    python setup.py build

}

package() {
    cd "${pkgname//python-}"
    python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}
