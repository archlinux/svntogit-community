# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=aiomysql
pkgname=python-$_pkgname
pkgver=0.1.1
pkgrel=3
pkgdesc='library for accessing a MySQL database from the asyncio'
arch=('any')
url='https://github.com/aio-libs/aiomysql'
license=('MIT')
depends=('python' 'python-pymysql')
makedepends=('python-build' 'python-installer' 'python-wheel'
             'python-setuptools' 'python-setuptools-scm')
optdepends=('python-sqlalchemy: SQLAlchemy support')
#checkdepends=('python-pytest' 'python-ipdb' 'python-docker' 'python-sqlalchemy' 'python-uvloop')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('b83a91465caf756f8a3046651d90b5a39d9b2714d96d8bb1c6df4fa6b47189c24979d88901d7f7fb1d2dd235c4a19e2d9ed3f73c8ace42950d12e1cdbb0e46ac')

prepare() {
  # remove the use of python-setuptools-scm-git-archive: https://github.com/aio-libs/aiomysql/pull/872
  sed -e '/setuptools_scm_git_archive/d' -i $_pkgname-$pkgver/pyproject.toml
}

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

## NOTE: most tests require a running mysql instance
#check() {
#  cd $_pkgname-$pkgver
#
#  python -m pytest
#}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
