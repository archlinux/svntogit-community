# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aioredis
pkgver=2.0.0
pkgrel=4
pkgdesc="asyncio (PEP 3156) Redis client library"
url="https://github.com/aio-libs/aioredis-py"
license=('MIT')
arch=('any')
depends=('python-async-timeout' 'python-hiredis' 'python-typing-extensions')
makedepends=('python-setuptools')
checkdepends=('python-pytest-asyncio' 'pifpaf' 'redis')
source=("https://github.com/aio-libs/aioredis-py/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e65f0eb568c2a87bbd35169ab83ad898819597b32467c80e924da13aab355278733506e73b2eed2230a3f080ab0bf681a77ba89bbb3d78952949dabf762a3b65')

build() {
  cd aioredis-py-$pkgver
  python setup.py build
}

check() {
  cd aioredis-py-$pkgver
  pifpaf run redis pytest
}

package() {
  cd aioredis-py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
