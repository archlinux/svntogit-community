# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aioresponses
pkgver=0.6.1
pkgrel=3
pkgdesc="A helper to mock/fake web requests in python aiohttp package."
url="https://github.com/pnuckowski/aioresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp')
makedepends=('python-pbr')
checkdepends=('python-pytest-runner' 'python-ddt' 'python-asynctest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pnuckowski/aioresponses/archive/$pkgver.tar.gz")
sha512sums=('e1d397bf0aca459322cffa0fb07989c155d4a433c62600e5c995b21a259fb84a52e1f88fd5c8b544458a645d658651501e68cd7e81e215dac3762cab59b42cac')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd aioresponses-$pkgver
  python setup.py build
}

check() {
  cd aioresponses-$pkgver
  python setup.py pytest
}

package() {
  cd aioresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
