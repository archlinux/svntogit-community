# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aioresponses
pkgver=0.7.0
pkgrel=1
pkgdesc="A helper to mock/fake web requests in python aiohttp package."
url="https://github.com/pnuckowski/aioresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp')
makedepends=('python-pbr')
checkdepends=('python-pytest-runner' 'python-ddt' 'python-asynctest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pnuckowski/aioresponses/archive/$pkgver.tar.gz")
sha512sums=('a32264acb424ddb113f03afb4ec9611bd5592f0c0bde24509439feae6812eff88b38734efc66cc601ef28111149c55ec9ace7fe8d8dd9e5a622b3940f3b64405')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd aioresponses-$pkgver
  python setup.py build
}

check() {
  cd aioresponses-$pkgver
  python setup.py pytest
}

package() {
  cd aioresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
