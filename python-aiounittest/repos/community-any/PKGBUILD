# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aiounittest
pkgver=1.4.1
pkgrel=1
pkgdesc="Test asyncio code more easily"
url="https://github.com/kwarunek/aiounittest"
license=('MIT')
arch=('any')
depends=('python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://github.com/kwarunek/aiounittest/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7d47ac912b9f1656033986f014ca9c5146e4eb21550957e784ad87bd2a2a7c8d291f149c8df068612ac5bf4d275d09e5fb9ea3ded90d638b53ee770d499b1065')

build() {
  cd aiounittest-$pkgver
  python setup.py build
}

check() {
  cd aiounittest-$pkgver
  nosetests3
}

package() {
  cd aiounittest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
