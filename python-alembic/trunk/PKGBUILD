# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: googol <googol@posteo.de>
# Contributor: Oliver Mangold <o.mangold at gmail dot com>

pkgbase=python-alembic
pkgname=('python-alembic' 'python2-alembic')
pkgver=1.4.1
pkgrel=1
pkgdesc='Lightweight database migration tool for usage with SQLAlchemy'
url='https://bitbucket.org/zzzeek/alembic'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil'
             'python2-setuptools' 'python2-mako' 'python2-sqlalchemy' 'python2-editor' 'python2-dateutil')
checkdepends=('python-pytest' 'python-mock' 'python2-pytest' 'python2-mock')
source=(https://pypi.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz{,.asc})
sha512sums=('a3333c83533e8bba5fb41a1bf438806728db9597071da2afa8e8e4f246d7d5b13d081d1df7d4e8cd17a67ba2a62900808c324e2f27de7d556f9afebea5cb6ecb'
            'SKIP')
b2sums=('d7f42452b9fbb1fb7bcf63805db16988ea83bfdb33133f2d8f01d40b926c0176ed96625643d75421e38d1af72600f77824a4fa9d503e8a4108998b60913be0d9'
        'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1') # Michael Bayer <classic@zzzcomputing.com>

prepare() {
  cp -a alembic-${pkgver}{,-py2}
}

build() {
  echo 'Building python...'
  (cd alembic-${pkgver}
    python setup.py build
  )
  echo 'Building python2...'
  (cd alembic-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  echo 'Checking python...'
  (cd alembic-${pkgver}
    py.test
  )
  echo 'Checking python2...'
  (cd alembic-${pkgver}-py2
    py.test2
  )
}

package_python-alembic() {
  depends=('python' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
  cd alembic-${pkgver}
  python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-alembic() {
  depends=('python2' 'python2-mako' 'python2-sqlalchemy' 'python2-editor' 'python2-dateutil')
  cd alembic-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv "${pkgdir}/usr/bin/alembic"{,2}
}

# vim: ts=2 sw=2 et:
