# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aniso8601
pkgver=9.0.0
pkgrel=1
pkgdesc="A library for parsing ISO 8601 strings."
url="https://bitbucket.org/nielsenb/aniso8601"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-dateutil' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/nielsenb/aniso8601/get/v$pkgver.tar.gz")
sha512sums=('9fa0a151d3f98108e675ef4f3813eb574c93a8995994bc282dc71268eab74a1f921285f61d31793db31a81c66aa5709aa3bb3f78636dc0249139b12add79ee06')

prepare() {
  mv nielsenb-aniso8601-* aniso8601-$pkgver
}

build() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py pytest
}

package() {
  cd aniso8601-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
