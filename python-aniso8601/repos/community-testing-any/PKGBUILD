# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aniso8601
pkgver=8.0.0
pkgrel=5
pkgdesc="A library for parsing ISO 8601 strings."
url="https://bitbucket.org/nielsenb/aniso8601"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-dateutil' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/nielsenb/aniso8601/get/v$pkgver.tar.gz")
sha512sums=('111bf07955795cf418971d4c9d776b91e3e4aa276d6dda78581f1d42b49e30ed67bb0f4fdbc2efeebaf4dbdfed2afe78084dc350dcda0d41afd92ccb821a2e1d')

prepare() {
  mv nielsenb-aniso8601-* aniso8601-$pkgver
}

build() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/aniso8601-$pkgver
  python setup.py pytest
}

package() {
  cd aniso8601-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
