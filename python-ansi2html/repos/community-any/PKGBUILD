# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=python-ansi2html
_libname=${pkgname/python-/}
pkgver=1.8.0
pkgrel=1
pkgdesc="Convert text with ANSI color codes to HTML"
arch=('any')
url="https://pypi.python.org/pypi/ansi2html"
license=('GPL')
depends=('python-six' 'python-setuptools' 'python-wheel' 'python-pip')
makedepends=(python-build python-installer python-wheel python-setuptools-scm python-setuptools-scm-git-archive)
source=("https://files.pythonhosted.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz")
md5sums=('21a2e083e288dfe73538bbef1f4291ef')
sha256sums=('38b82a298482a1fa2613f0f9c9beb3db72a8f832eeac58eb2e47bf32cd37f6d5')
validpgpkeys=(94504C3AE11DD197920058ABA90ED7DE971095FF)

build() {
    cd "$_libname-$pkgver"
    python -m build --wheel --no-isolation
}

package() {
    cd "$_libname-$pkgver"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
