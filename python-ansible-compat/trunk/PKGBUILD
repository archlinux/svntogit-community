# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-compat
pkgname=python-ansible-compat
pkgver=1.0.0
pkgrel=2
pkgdesc="Functions that help interacting with various versions of Ansible"
arch=(any)
url="https://github.com/ansible-community/ansible-compat"
license=(MIT)
depends=(python-subprocess-tee python-yaml)
makedepends=(python-build python-installer python-setuptools-scm python-setuptools-scm-git-archive python-wheel)
checkdepends=(ansible python-flaky python-pytest python-pytest-mock)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('2ade2acbd1d7868eeb602c682afe92bc8c05480333205c328b4c2a1b09f970a4')
b2sums=('56eb32314e310777e454a5f5e020cc4dfc485e6d5720d45e99734ce8cf238377f1ff443f9f78f5b9de947944564a7001294907c0e186fe94cd4561d4d348723e')

build() {
  cd "${_name}-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v -c /dev/null
}

package() {
  cd "${_name}-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
