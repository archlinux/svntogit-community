# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-anyio
pkgver=3.0.1
pkgrel=1
pkgdesc='High level compatibility layer for multiple asynchronous event loop implementations'
arch=(any)
url='https://github.com/agronholm/anyio'
license=(MIT)
depends=(python python-idna python-sniffio)
makedepends=(python-setuptools python-setuptools-scm)
checkdepends=(python-pytest python-trustme python-hypothesis python-uvloop
              python-curio python-trio)
optdepends=(
  'python-curio: curio backend'
  'python-trio: trio backend'
)
source=(https://github.com/agronholm/anyio/archive/$pkgver/anyio-$pkgver.tar.gz)
sha256sums=('45f4169148897135b3e4471a803acea443edc3e18654861dcbacd9faa182f49b')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd anyio-$pkgver
  python setup.py build
}

check() {
  cd anyio-$pkgver
  # Install to a temporary root as the test suite requires the entry point for
  # its pytest plugin
  pyver=$(python -c "import sys; print('{}.{}'.format(*sys.version_info[:2]))")
  python setup.py install --root="$PWD/tmp_install" --skip-build
  PYTHONPATH="$PWD/tmp_install/usr/lib/python$pyver/site-packages" pytest
}

package() {
  cd anyio-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
