# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgname=argon2_cffi
pkgname=python-argon2_cffi
pkgver=19.1.0
pkgrel=2
pkgdesc="CFFI-based Argon2 Bindings for Python"
arch=('x86_64')
url="https://github.com/hynek/argon2_cffi"
license=('MIT')
depends=(argon2 python-cffi python-six)
makedepends=(git python-setuptools)
_commit=0ccdbe2af4c7139f0abbb6a03bcb9f7605b16dca  # tags/19.1.0^0
source=("git+https://github.com/hynek/argon2_cffi#commit=$_commit"
        "git+https://github.com/p-h-c/phc-winner-argon2")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname
  git submodule init
  git config --local submodule.extras/libargon2.url "$srcdir/phc-winner-argon2"
  git submodule update
  cd ..
}

build() {
  cd $_pkgname
  python3 setup.py build
}

package() {
  cd $_pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
