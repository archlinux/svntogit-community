# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aspectlib
pkgver=1.4.2
pkgrel=6
pkgdesc="Development library for quickly writing configurable applications and daemons"
arch=('any')
license=('BSD')
url="https://github.com/ionelmc/python-aspectlib"
depends=('python-fields')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-tornado' 'python-process-tests'
              'python-profilestats' 'python-pytest') # 'mysql-python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-aspectlib/archive/v$pkgver.tar.gz")
sha512sums=('fadb90b625ce2faac8f6d29ee28efaee09b4338c3c516c79d9e8753b7fb2ff45e0033174c5c5302f47f1f9e332824f37d56667fb05e66f85b808e8aa3a086f57')

build() {
  cd "$srcdir"/python-aspectlib-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-aspectlib-$pkgver
  LC_CTYPE=en_US.UTF-8 py.test || warning "https://github.com/ionelmc/python-aspectlib/issues/11"
}

package() {
  cd python-aspectlib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
