# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-ass
pkgname=('python-ass' 'python2-ass')
pkgver=0.4.2
_commit=c8ffa0b1008c2b2262a2ee7c0f8881f0201a4c8d
pkgrel=2
pkgdesc='A library for parsing and manipulating Advanced SubStation Alpha subtitle files.'
arch=('any')
url='http://github.com/rfw/python-ass'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/rfw/python-ass.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a python-ass{,-py2}
}

build() {
  cd "$srcdir"/python-ass
  python setup.py build

  cd "$srcdir"/python-ass-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-ass
  python test.py

  cd "$srcdir"/python-ass-py2
  python2 test.py
}

package_python-ass() {
  depends=('python-setuptools')

  cd python-ass
  python setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-ass() {
  depends=('python2-setuptools')

  cd python-ass-py2
  python2 setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
