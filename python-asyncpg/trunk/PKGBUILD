# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=asyncpg
pkgname=python-$_pkgname
pkgver=0.21.0
pkgrel=1
pkgdesc='A fast PostgreSQL Database Client Library for Python/asyncio'
arch=('x86_64')
url='https://github.com/MagicStack/asyncpg'
license=('Apache')
depends=('python')
makedepends=('git' 'python-setuptools' 'cython')
checkdepends=('python-pytest-runner' 'python-uvloop' 'postgresql')
source=("git+$url.git#tag=v$pkgver"
        'git+https://github.com/MagicStack/py-pgproto.git')
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd $_pkgname

  git submodule init
  git config submodule.asyncpg/pgproto.url "$srcdir"/py-pgproto
  git submodule update

  sed -i "s|'Cython==.*'|'Cython'|" setup.py
}

build() {
  cd $_pkgname

  python setup.py build_py build_ext
}

check() {
  cd $_pkgname

  python setup.py pytest
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
