# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Anatol Pomozov

pkgbase=python-autobahn
pkgname=(python-autobahn python2-autobahn)
pkgver=18.11.2
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things'
arch=(any)
url='http://autobahn.ws/python/'
license=(MIT)
makedepends=(python-setuptools python2-setuptools python-twisted python-six python-txaio python2-twisted python2-six python2-txaio)
checkdepends=(python2-unittest2)
source=(https://pypi.io/packages/source/a/autobahn/autobahn-$pkgver.tar.gz)
sha256sums=('43262e500aaf8b89fb5f0b60ab13163500ee877908cdd6861208546d95c6dba0')

build() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$srcdir/autobahn-$pkgver"
  #It requires python-unittest2 in [community]
  #python setup.py test
  #python2 setup.py test
}

package_python-autobahn() {
  depends=(python python-twisted python-six python-txaio)

  cd "$srcdir/autobahn-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-autobahn() {
  depends=(python2 python2-twisted python2-six python2-txaio)

  cd "$srcdir/autobahn-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
