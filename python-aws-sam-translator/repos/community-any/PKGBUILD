# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
# https://github.com/aws/serverless-application-model/releases
pkgver=1.50.0
pkgrel=1
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/aws/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema)
makedepends=(python-setuptools)
# python-yaml is needed by samtranslator.yaml_helper, while the latter is used in tests only
checkdepends=(python-pytest python-pytest-env python-yaml python-parameterized)
# Upstream intentionally exclude tests from sdist
# https://github.com/aws/serverless-application-model/pull/381#issuecomment-382143094
source=("https://github.com/aws/serverless-application-model/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-jsonschema-4.patch"::"https://github.com/aws/serverless-application-model/pull/2441.patch")
sha256sums=('ab9362f9a6de211f13fe72678154ecd8847d6d0cd9471b7b943a8af0c054c78f'
            '7bba093258d9d7768112463f9a6fbe63bb78e060081d9585637582caf0da1d20')

prepare() {
  cd serverless-application-model-$pkgver
  # skip pytest-coverage
  sed -i -r 's#--cov\S*\s+\S+##g' pytest.ini
  patch -Np1 -i ../$pkgname-jsonschema-4.patch
}

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver
  pytest tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
