# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
pkgver=1.35.0
pkgrel=1
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/aws/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-yaml python-mock python-parameterized)
# Upstream intentionally exclude tests from sdist
# https://github.com/aws/serverless-application-model/pull/381#issuecomment-382143094
source=("https://github.com/aws/serverless-application-model/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "no-install-integration-tests.diff")
sha256sums=('07515a95a46542615cd48d1d7efcfaa88c7b86714b3d3efdf98cb3934a96fb5b'
            '44545f773c5b8ea750f2aa03a1fb12bfe289bacd0f7c1da0b7e47fd53d8b0c18')

prepare() {
  cd serverless-application-model-$pkgver
  # skip pytest-coverage
  sed -i '/addopts/d' pytest.ini

  patch -Np1 -i ../no-install-integration-tests.diff
}

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver
  pytest tests
}

package() {
  cd serverless-application-model-$pkgver
  # Work around https://bugs.python.org/issue34033
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
