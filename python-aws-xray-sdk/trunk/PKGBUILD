# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-aws-xray-sdk
_pkgname=aws-xray-sdk-python
pkgver=2.11.0
_commit=b37ae1100350780a3862297fe9d2c364bb39510f
pkgrel=1
pkgdesc='AWS X-Ray SDK for Python'
arch=(any)
url='https://github.com/aws/aws-xray-sdk-python'
license=(Apache)
depends=(python python-botocore python-wrapt)
# See extensions in https://github.com/aws/aws-xray-sdk-python/tree/master/aws_xray_sdk/ext
optdepends=(python-aiobotocore python-aiohttp python-bottle python-django
            python-flask python-flask-sqlalchemy python-httpx python-mysql-connector
            python-pg8000 python-psycopg2 python-pymongo python-pymysql
            python-pynamodb python-requests python-sqlalchemy)
makedepends=(git python-setuptools ${optdepends[@]})
checkdepends=(python-pytest python-pytest-asyncio python-testing.postgresql
              python-webtest python-django-fake-model python-mock python-pytest-benchmark)
source=("git+https://github.com/aws/aws-xray-sdk-python.git#commit=$_commit")
sha256sums=('SKIP')

_backports=(
  # The following three are from https://github.com/aws/aws-xray-sdk-python/pull/364 (ongoing)
  # Use fixture event_loop instead of the deprecated fixture loop
  72dcbf97a27a44e87123630fe78ff828eafa4d4c
  # Do not pass event loop to asyncio.sleep() and wait() on Python 3.8+
  dde50155e7a912aef80199a1d0c2e9900002af5f
  # Do not pass coroutine objects directly to asyncio.wait()
  b3d605d145dda82b9bcaa2fdac7eb593770886b2
)

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $_pkgname

  # this loop is stolen from core/systemd :)
  local _c
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"
    git cherry-pick -n "${_c}"
  done

  cat > pytest.ini <<EOF
[pytest]
asyncio_mode = auto
EOF
}

build() {
  cd $_pkgname
  python setup.py build
}

check() {
  cd $_pkgname

  # See setenv= in upstream tox.ini
  export DJANGO_SETTINGS_MODULE=tests.ext.django.app.settings
  export AWS_SECRET_ACCESS_KEY=fake_key
  export AWS_ACCESS_KEY_ID=fake_id

  # * the test suite for aiohttp uses test_client fixture, which is
  #   dropped in the latest pytest-aiohttp
  # * the test suite for pymysql uses testing.mysqld, which is not
  #   compatible with MariaDB [1]
  # * tests fail with pg8000 > 1.20.0 and upstream explicitly
  #   states no support [2]
  # [1] https://github.com/tk0miya/testing.mysqld/issues/3
  # [2] https://github.com/aws/aws-xray-sdk-python/pull/324
  pytest -v tests --ignore tests/ext/aiohttp \
                  --ignore tests/ext/pg8000 \
                  --ignore tests/ext/pymysql
}

package() {
  cd $_pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
