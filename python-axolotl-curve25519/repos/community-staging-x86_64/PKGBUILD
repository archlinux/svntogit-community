# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexandre Bouvier <contact AT amb DOT tf>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: Tommaso Sardelli <lacapannadelloziotom AT gmail DOT com>
# Contributor: Philipp Joram <phijor AT t-online DOT de>

pkgbase=python-axolotl-curve25519
pkgname=('python-axolotl-curve25519' 'python2-axolotl-curve25519')
_pkgver=0.4.1-2
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc='Python wrapper for curve25519 library with ed25519 signatures'
url='https://github.com/tgalal/python-axolotl-curve25519'
arch=('x86_64')
license=('GPL3')
makedepends=('zlib' 'glibc' 'python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/tgalal/python-axolotl-curve25519/archive/${_pkgver}.tar.gz)
sha256sums=('cc8de0c140831519e45598b082fdef0e939fd30930be442e38bdec13dc16aab5')
sha512sums=('cbc7c6caa47a9a811640c247a1be727d7b1b68bcdb4c5336e02b4d1eaf9fd2c57b7438b0da466a379a1c0f3f146756b9b7eea3c9b7945ce88478d4bf0b8a1e0d')

prepare() {
  cp -a ${pkgbase}-${_pkgver}{,-py2}
}

build() {
  msg "Building python..."
  (cd ${pkgbase}-${_pkgver}
    python setup.py build
  )
  msg "Building python2..."
  (cd ${pkgbase}-${_pkgver}-py2
    python2 setup.py build
  )
}

package_python-axolotl-curve25519() {
  depends=('python' 'zlib' 'glibc')
  cd ${pkgbase}-${_pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-axolotl-curve25519() {
  depends=('python2' 'zlib' 'glibc')
  cd ${pkgbase}-${_pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
