# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Contributor: Alexander Diana <alexander@rouk.org>

_pypiname=bcrypt
pkgbase=python-bcrypt
pkgname=("python-bcrypt" "python2-bcrypt")
pkgver=3.1.3
pkgrel=1
pkgdesc="Modern password hashing for your software and your servers"
arch=('i686' 'x86_64')
url="https://github.com/pyca/bcrypt"
license=('APACHE')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi'
             'python-six' 'python2-six' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/pyca/bcrypt.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a "$srcdir"/bcrypt{,-py2}

  export LC_CTYPE=en_US.UTF-8 
}

build() {
  cd "$srcdir"/bcrypt
  python setup.py build

  cd "$srcdir"/bcrypt-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/bcrypt
  python setup.py ptr

  cd "$srcdir"/bcrypt-py2
  python2 setup.py ptr
}

package_python-bcrypt() {
  depends=('python-cffi' 'python-six')

  cd bcrypt
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 "$srcdir"/bcrypt/LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

package_python2-bcrypt() {
  depends=('python2-cffi' 'python2-six')

  cd bcrypt-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 "$srcdir"/bcrypt/LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
