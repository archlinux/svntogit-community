# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=python-beautifulsoup4
pkgname=('python-beautifulsoup4' 'python2-beautifulsoup4')
pkgver=4.9.0
pkgrel=1
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping"
arch=('any')
url="https://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=('python-setuptools' 'python2-setuptools' 'python-soupsieve' 'python2-soupsieve')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/b/beautifulsoup4/beautifulsoup4-$pkgver.tar.gz")
sha512sums=('8c8d3c23f9e76955e465c6d343addb5165480f59d785755f6550aa3e77e8efe0c3dd4a2f310d485162dc046b05f797a8a6aee96a515d59659c820993e0ee37d7')

prepare() {
  cp -a beautifulsoup4-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/beautifulsoup4-$pkgver
  python setup.py build

  cd "$srcdir"/beautifulsoup4-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/beautifulsoup4-$pkgver/build
  py.test

  cd "$srcdir"/beautifulsoup4-$pkgver-py2/build
  py.test2
}

package_python-beautifulsoup4() {
  depends=('python-soupsieve')
  optdepends=('python-chardet: to autodetect character encodings'
              'python-lxml: alternative HTML parser'
              'python-html5lib: alternative HTML parser')

  cd beautifulsoup4-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-beautifulsoup4() {
  depends=('python2-soupsieve')
  optdepends=('python2-chardet: to autodetect character encodings'
              'python2-lxml: alternative HTML parser'
              'python2-html5lib: alternative HTML parser')

  cd beautifulsoup4-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
