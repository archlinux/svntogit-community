# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

pkgname=python-black
pkgver=21.4b2
pkgrel=1
pkgdesc='Uncompromising Python code formatter'
arch=('any')
url=https://github.com/psf/black
license=('MIT')
depends=('python' 'python-appdirs' 'python-click' 'python-mypy_extensions'
         'python-pathspec' 'python-regex' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-aiohttp' 'python-aiohttp-cors' 'python-typed-ast')
optdepends=('python-aiohttp: for the blackd HTTP server'
            'python-aiohttp-cors: for the blackd HTTP server'
            'python-colorama: for colored diffs'
            'python-typed-ast: to format Python 2 code')
source=("https://files.pythonhosted.org/packages/source/b/black/black-$pkgver.tar.gz")
sha256sums=('fc9bcf3b482b05c1f35f6a882c079dc01b9c7795827532f4cc43c0ec88067bbc')
b2sums=('1fb806e6b2a03204ad488294afaa1b4a1f9d9a4ff9e7c55a7aa08b3c4749f11e7592959e40de5a084594c34d81b5fb6b0e4d0c502ac9dc7c6b20f232cd311edc')

build() {
  cd "black-$pkgver"
  python setup.py build
}

check() {
  cd "black-$pkgver"
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" python -m unittest tests/test_black.py
}

package() {
  cd "black-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
