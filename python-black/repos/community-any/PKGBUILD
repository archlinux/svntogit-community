# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

pkgname=python-black
pkgver=23.1.0
pkgrel=2
pkgdesc='Uncompromising Python code formatter'
arch=('any')
url=https://github.com/psf/black
license=('MIT')
depends=('python' 'python-click' 'python-mypy_extensions' 'python-packaging'
         'python-pathspec' 'python-platformdirs' 'python-tomli')
makedepends=('python-build' 'python-hatch-fancy-pypi-readme' 'python-hatch-vcs'
             'python-hatchling' 'python-installer')
checkdepends=('ipython' 'python-aiohttp' 'python-parameterized' 'python-pytest'
              'python-typed-ast' 'python-tokenize-rt')
optdepends=('ipython: for Jupyter notebook support'
            'python-tokenize-rt: for Jupyter notebook support'
            'python-aiohttp: for the blackd HTTP server'
            'python-colorama: for colored diffs')
source=("https://files.pythonhosted.org/packages/source/b/black/black-$pkgver.tar.gz")
sha256sums=('b0bd97bea8903f5a2ba7219257a44e3f1f9d00073d6cc1add68f0beec69692ac')
b2sums=('de6c4156431eb53c9b73aed95ae8bc5a71865497e17502332587c18f179db0685c2267a9bbaf7e48c45fadc85e4bfc7c7454a21fdb108d22287928374053f5dd')

build() {
  cd "black-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "black-$pkgver"
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  # https://github.com/psf/black/issues/3251#issuecomment-1236413890
  ulimit -n 2048
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  cd "black-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/black-$pkgver.dist-info/licenses/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Completions
  install -vd \
    "$pkgdir/usr/share/"{bash-completion/completions,fish/vendor_completions.d,zsh/site-functions}
  _BLACK_COMPLETE=bash_source test-env/bin/black > \
    "$pkgdir/usr/share/bash-completion/completions/black"
  _BLACK_COMPLETE=fish_source test-env/bin/black > \
    "$pkgdir/usr/share/fish/vendor_completions.d/black.fish"
  _BLACK_COMPLETE=zsh_source test-env/bin/black > \
    "$pkgdir/usr/share/zsh/site-functions/_black"
  _BLACKD_COMPLETE=bash_source test-env/bin/blackd > \
    "$pkgdir/usr/share/bash-completion/completions/blackd"
  _BLACKD_COMPLETE=fish_source test-env/bin/blackd > \
    "$pkgdir/usr/share/fish/vendor_completions.d/blackd.fish"
  _BLACKD_COMPLETE=zsh_source test-env/bin/blackd > \
    "$pkgdir/usr/share/zsh/site-functions/_blackd"

  # Vim plugin
  install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/plugin" plugin/black.vim
  install -Dm644 -t "$pkgdir/usr/share/vim/vimfiles/autoload" autoload/black.vim
}

# vim:set ts=2 sw=2 et:
