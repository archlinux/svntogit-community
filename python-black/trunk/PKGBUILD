# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

pkgname=python-black
pkgver=19.3b0
pkgrel=1
pkgdesc='Uncompromising Python code formatter'
arch=('any')
url=https://github.com/ambv/black
license=('MIT')
depends=('python' 'python-appdirs' 'python-attrs' 'python-click' 'python-setuptools'
         'python-toml')
checkdepends=('python-aiohttp')
optdepends=('python-aiohttp: for the blackd HTTP server')
source=("https://files.pythonhosted.org/packages/source/b/black/black-$pkgver.tar.gz")
sha512sums=('db241d13c76b0c6fd218d9a741adce3c28447140add1346e540d6ba9238fe87bbd9b159fc9c36c685734da57a001144b4dc3a63c868a267aee36ceb4faa1fbc1')

build() {
  cd "black-$pkgver"
  python setup.py build
}

check() {
  cd "black-$pkgver"
  python -m unittest tests/test_black.py
}

package() {
  cd "black-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
