# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgbase=python-bleach
pkgname=(python-bleach python2-bleach)
pkgver=3.1.0
pkgrel=3

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="https://pypi.org/project/bleach/"
arch=('any')
license=('Apache')
makedepends=('python'
             'python-setuptools'
             'python-webencodings'
             'python2'
             'python2-setuptools'
              'python2-webencodings')
checkdepends=('python-pytest'
              'python2-pytest')

source=(https://pypi.org/packages/source/b/bleach/bleach-$pkgver.tar.gz
        pytest5.patch)

sha256sums=('3fdf7f77adcf649c9911387df51254b813185e32b2c6619f690b593a617e19fa'
            'a1d3dea566e7c56540b83bcd006c730f6c71fe531570fa2c5cccf0a917070bc6')

prepare() {
	# https://github.com/mozilla/bleach/pull/483
	patch -Np1 -d bleach-$pkgver <pytest5.patch
	cp -a bleach-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py build

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py build
}

check() {
	cd "$srcdir"/bleach-$pkgver
	python -m pytest

	cd "$srcdir"/bleach-$pkgver-python2
	python2 -m pytest
}

package_python-bleach() {
	depends=('python-webencodings')

	cd "$srcdir"/bleach-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-bleach() {
	depends=('python2-webencodings')

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1
}
