# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgbase=python-blessed
pkgname=('python-blessed' 'python2-blessed')
pkgver=1.14.2
pkgrel=2
pkgdesc='A thin, practical wrapper around terminal styling, screen positioning, and keyboard input'
arch=('any')
url='https://github.com/jquast/blessed'
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-blessed-${pkgver}.tar.gz::https://github.com/jquast/blessed/archive/${pkgver}.tar.gz")
sha256sums=('6c375f28e8a4d85d03dd46e3dd058a9cd2279d51da018bca0c0f6b5d98b1ee34')

prepare() {
  cp -r {,python-}blessed-${pkgver}
  mv {,python2-}blessed-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-blessed-${pkgver}
    ${py} setup.py build
    popd
  done
}
package_python-blessed() {
  depends=('python-six' 'python-wcwidth')

  cd python-blessed-${pkgver}

  python setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

package_python2-blessed() {
  depends=('python2-six' 'python2-wcwidth')

  cd python2-blessed-${pkgver}

  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
