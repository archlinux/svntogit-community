# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Philipp Schmidt <philschmidt at gmx dot net>
# Contributor: Stefan Tatschner <stefan.tatschner at gmail dot com>

pkgbase=python-blinker
pkgname=('python-blinker' 'python2-blinker')
pkgver=1.3
pkgrel=4
pkgdesc="Fast, simple object-to-object and broadcast signaling"
arch=('any')
url="http://discorporate.us/projects/Blinker/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("http://pypi.python.org/packages/source/b/blinker/blinker-$pkgver.tar.gz")
md5sums=('66e9688f2d287593a0e698cd8a5fbc57')

prepare() {
  cp -a blinker-$pkgver{,-py2}
}

check() {
  cd "$srcdir/blinker-$pkgver"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests3

  cd "$srcdir/blinker-$pkgver-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2
}

package_python-blinker() {
  depends=('python')

  cd "$srcdir/blinker-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-blinker() {
  depends=('python2')

  cd "$srcdir/blinker-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
