# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Chris Baker <baker.chris.3@gmail.com>

pkgbase=python-blist
pkgname=('python-blist' 'python2-blist')
_pkgname=blist
pkgver=1.3.6
pkgrel=6
pkgdesc='List-like type with better asymptotic performance'
url='http://stutzbachenterprises.com/blist'
arch=('x86_64')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/DanielStutzbach/blist/archive/v${pkgver}.tar.gz
        python3.7.patch)
sha512sums=('f46ded57aeb2a33fc5450420210d015aa37c2fc91034a771dfb1a184ef2655d8e5dc97bee61139059e6211569fcb7358f972071d3830733e9dbb8908e1b0e14c'
            'ecf08ebbfd39a069e8cd93723fefe3d0ee223d618f8748032927416dd6c813396fa271b20dd74fdaed0369b19046b4d2954b70c77f72258cc5551699eaa1281f')

prepare() {
  (cd $_pkgname-$pkgver; patch -p1 -i ../python3.7.patch)
  cp -ra ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    python setup.py test
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-blist() {
  depends=('python')
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-blist() {
  depends=('python2')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
