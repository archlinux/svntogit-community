# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-blosc
pkgver=1.10.4
pkgrel=1
pkgdesc="A Python wrapper for the extremely fast Blosc compression library"
arch=('x86_64')
url="http://python-blosc.blosc.org/"
license=('BSD')
depends=('python' 'blosc')
optdepends=('python-numpy: for tests and benchmarks')
makedepends=('python-scikit-build' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Blosc/python-blosc/archive/v${pkgver}.tar.gz")
sha256sums=('2fdb29a624d96d0f47b22aec4a144814d5b9c5a1cdaa51c802e73155d5677746')

prepare() {
  sed -e '/c-blosc/d' -e 's|blosc_static|blosc|' \
    -i $pkgname-$pkgver/blosc/CMakeLists.txt # Use system blosc
}
 
build() {
  cd "$pkgname-$pkgver"
  BLOSC_DIR=/usr
  export BLOSC_DIR
  python setup.py build_clib
  python setup.py build_ext --inplace
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
