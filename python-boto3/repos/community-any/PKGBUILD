# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-boto3
pkgname=('python-boto3' 'python2-boto3')
pkgver=1.9.86
pkgrel=1
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
makedepends=('python-setuptools' 'python2-setuptools')
# checkdepends=('python-coverage' 'python-nose' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('b78f2138d92edb2db4675e9b8610c129800053642a6abd7d133f36ede6e28ce1e99703124220e4df01aa19a18d1919301db090cfc77e44dd829cf45b8adeb622')

prepare() {
  cp -r "boto3-$pkgver" "python-boto3-$pkgver"
  cp -r "boto3-$pkgver" "python2-boto3-$pkgver"
}

build() {
  cd "$srcdir/python-boto3-$pkgver"
  python setup.py build

  cd "$srcdir/python2-boto3-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir"/boto3-$pkgver
  # scripts/ci/run-tests
}

package_python-boto3() {
  depends=('python-botocore' 'python-jmespath' 'python-s3transfer')
  cd "$srcdir/python-boto3-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-boto3() {
  depends=('python2-botocore' 'python2-jmespath' 'python2-s3transfer')
  cd "$srcdir/python2-boto3-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
