# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-boto3
pkgname=('python-boto3' 'python2-boto3')
pkgver=1.7.16
pkgrel=1
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
makedepends=('python-setuptools' 'python2-setuptools')
# checkdepends=('python-coverage' 'python-nose' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('942c4629bd8d428bc4902dce1680580d98e2c515f9075ba862dc4f377da078e7a6575c8eeb374a7452e5f392ffc06aa7430cabf52beb420eb6df612211012411')

prepare() {
  cp -r "boto3-$pkgver" "python-boto3-$pkgver"
  cp -r "boto3-$pkgver" "python2-boto3-$pkgver"
}

build() {
  cd "$srcdir/python-boto3-$pkgver"
  python setup.py build

  cd "$srcdir/python2-boto3-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir"/boto3-$pkgver
  # scripts/ci/run-tests
}

package_python-boto3() {
  depends=('python-botocore' 'python-jmespath' 'python-s3transfer')
  cd "$srcdir/python-boto3-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-boto3() {
  depends=('python2-botocore' 'python2-jmespath' 'python2-s3transfer')
  cd "$srcdir/python2-boto3-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
