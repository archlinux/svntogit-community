# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Ainola
# Contributor: Chris Fordham

pkgname=python-botocore
pkgver=1.13.28
pkgrel=1
pkgdesc='A low-level interface to a growing number of Amazon Web Services'
arch=('any')
url="https://github.com/boto/botocore"
license=('Apache')
depends=('python-dateutil' 'python-jmespath' 'python-docutils' 'python-urllib3')
makedepends=('python-setuptools')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
        $pkgname-cap-dateutil-version.patch::https://github.com/boto/botocore/commit/e87e7a745f.patch)
md5sums=('3bf94275368fce05f9c8d3c4d41c9247'
         '11307e5c6eddd77cc5ea4be17550a4a7')

prepare() {
  cd botocore-$pkgver
  patch -Rp1 -i ../$pkgname-cap-dateutil-version.patch
}

build() {
  cd botocore-$pkgver
  python setup.py build
}

package() {
  cd botocore-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
