# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgname=python-bottleneck
pkgver=1.3.4
pkgrel=1
pkgdesc="Fast NumPy array functions written in Cython"
arch=(x86_64)
url="https://github.com/kwgoodman/bottleneck"
license=(BSD)
depends=(python-numpy)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-fix-missing-tests-data.patch::${url}/commit/20b060155b1fc9e12c00f502970a3dd609de3d47.patch)
sha256sums=('f33f5c7bfa922911d4439ff332e5f9a1863587aefdb38b159802e3356293284d'
            'aec4bcc1901664662649a656afb03c5c57846fd3c5e9432038816548617a044c')

prepare() {
  cd bottleneck-${pkgver}
  patch -p1 < ../${pkgname}-fix-missing-tests-data.patch
}

build() {
  cd bottleneck-${pkgver}
  python setup.py build
}

check() {
  cd bottleneck-${pkgver}
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --skip-build
  test-env/bin/python /usr/bin/pytest -vv --color=yes --pyargs bottleneck
}

package() {
  cd bottleneck-${pkgver}
  python setup.py install --skip-build --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
