# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgname=python-breathe
pkgver=4.13.1
pkgrel=3
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output."
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
depends=('python' 'python-setuptools' 'python-sphinx' 'python-docutils' 'python-six')
makedepends=('python' 'git' 'python-setuptools')
checkdepends=('python-nose' 'python-sphinx')
BUILDENV=('!check')
source=("git+https://github.com/michaeljones/breathe#tag=v${pkgver}?signed")
sha256sums=('SKIP')
validpgpkeys=('79DC0FAF18C31807E82E1A6B9EF1DC92B5F301BD') # Melvin Vermeeren <mail@mel.vin>

# FIXME: tests are disabled for now, as sphinx 2.1.0+ break them
check() {
  cd breathe

  PYTHONPATH=$PWD make test
}

build() {
  cd breathe
  python setup.py build
}

package_python-breathe() {
  cd "breathe"
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
