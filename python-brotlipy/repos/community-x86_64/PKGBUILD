# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-brotlipy
pkgname=('python-brotlipy' 'python2-brotlipy')
pkgver=0.7.0
pkgrel=4
pkgdesc='Python binding to the Brotli library'
arch=('x86_64')
license=('MIT')
url='https://github.com/python-hyper/brotlipy'
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi' 'python2-enum34' 'brotli06')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-hypothesis'
              'python2-hypothesis' 'brotli06-testdata')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-hyper/brotlipy/archive/v$pkgver.tar.gz"
        shared-brotli.patch)
sha512sums=('bec8183cf3cc8ba9e594d1a86a8a33d88a6ac8e9c6a6287c837bda3726183ff1b486f2c49da4e489b5b3cb51b57dd5816413b59419920c2ced48435221f1beaf'
            'fc472dfb857d223f1b40edb8182a34c22ed1a8ff02953de934a0cf3b223e072699b100e63bf3ba1848b312743dc302b781b71fcafb3117223439f65f97e334de')

prepare() {
  (cd brotlipy-$pkgver; patch -p1 -i ../shared-brotli.patch)
  sed -i 's|^TEST_DATA_DIR = .*|TEST_DATA_DIR = "/usr/share/brotli06/testdata"|' brotlipy-$pkgver/test/conftest.py

  cp -a "$srcdir"/brotlipy-$pkgver{,-py2}

  export USE_SHARED_BROTLI=1
  export CFLAGS="-I/usr/include/brotli06 -L/usr/lib/brotli06"
}

build() {
  cd "$srcdir"/brotlipy-$pkgver
  python setup.py build

  cd "$srcdir"/brotlipy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/brotlipy-$pkgver
  python setup.py pytest

  cd "$srcdir"/brotlipy-$pkgver-py2
  python2 setup.py pytest
}

package_python-brotlipy() {
  depends=('brotli06' 'python-cffi')

  cd brotlipy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-brotlipy() {
  depends=('brotli06' 'python2-cffi' 'python2-enum34')

  cd brotlipy-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
