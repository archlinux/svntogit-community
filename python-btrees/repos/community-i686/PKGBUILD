# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-btrees
pkgname=('python-btrees' 'python2-btrees')
pkgver=4.4.1
pkgrel=1
pkgdesc='A fancy and practical functional tools'
arch=('i686' 'x86_64')
license=('ZPL')
url='https://github.com/zopefoundation/btrees'
makedepends=('python-setuptools' 'python2-setuptools' 'python-persistent' 'python2-persistent'
             'python-zope-interface' 'python2-zope-interface')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-transaction'
              'python2-transaction')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('06586c17585b63030474bf73b1ee3af0d171b043719a5a0be86cd47a844b509e70e3c370569c9babd8103884eb6d2c9f6d9f3c89ffae443688e233a25f0bb9d4')

prepare() {
  cp -a BTrees-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/BTrees-$pkgver
  python setup.py build

  cd "$srcdir"/BTrees-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/BTrees-$pkgver
  python setup.py pytest

  cd "$srcdir"/BTrees-$pkgver-py2
  python2 setup.py pytest
}

package_python-btrees() {
  depends=('python-persistent' 'python-zope-interface')

  cd BTrees-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-btrees() {
  depends=('python2-persistent' 'python2-zope-interface')

  cd BTrees-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
