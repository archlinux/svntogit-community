# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-btrees
pkgver=4.9.2
pkgrel=2
pkgdesc='A fancy and practical functional tools'
arch=('x86_64')
license=('ZPL')
url='https://github.com/zopefoundation/btrees'
depends=('python-persistent' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-transaction' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('860fceed41ea26c68d865f5c57687a2a773304cc64bdffc9f3b21fc01dedb7e103943177af3f2368a310d070e0e406d915b5aac33bddfc3e07e187d0c892a523')

build() {
  cd BTrees-$pkgver
  python setup.py build
}

check() {
  cd BTrees-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10" python -m zope.testrunner --test-path=src
}

package() {
  cd BTrees-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
