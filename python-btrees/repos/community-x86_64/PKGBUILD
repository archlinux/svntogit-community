# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-btrees
pkgname=('python-btrees' 'python2-btrees')
pkgver=4.5.0
pkgrel=1
pkgdesc='A fancy and practical functional tools'
arch=('x86_64')
license=('ZPL')
url='https://github.com/zopefoundation/btrees'
makedepends=('python-setuptools' 'python2-setuptools' 'python-persistent' 'python2-persistent'
             'python-zope-interface' 'python2-zope-interface')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-transaction'
              'python2-transaction' 'python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('c50e8e1d67c0bf52bc3b33fa31bc32a4ed8f7257ce211c5155f08fcbe37b93b3e2d760634176135f1114ad569d14080c38e8cc591761100efd651d3ef417efb9')

prepare() {
  cp -a BTrees-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/BTrees-$pkgver
  python setup.py build

  cd "$srcdir"/BTrees-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/BTrees-$pkgver
  python setup.py pytest

  cd "$srcdir"/BTrees-$pkgver-py2
  python2 setup.py pytest
}

package_python-btrees() {
  depends=('python-persistent' 'python-zope-interface')

  cd BTrees-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-btrees() {
  depends=('python2-persistent' 'python2-zope-interface')

  cd BTrees-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
