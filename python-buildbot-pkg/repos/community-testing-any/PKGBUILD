# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=python-buildbot-pkg
pkgdesc='Utilities and common code for building and testing www plugins'
pkgver=1.8.0
# `git rev-parse v$pkgver`
_tag_rev=5b1106e9e2898cff174e7dd4f03dfd80a5c79f16
pkgrel=1
arch=(any)
url='https://buildbot.net'
license=(GPL2)
# Use Node.js 8 as www plugins do not build with Node.js 10.14.2 and 11 (https://github.com/buildbot/buildbot/issues/4382)
depends=(python-setuptools nodejs-lts-carbon yarn python2)
makedepends=('git')
source=("git+https://github.com/buildbot/buildbot?signed#tag=$_tag_rev")
sha256sums=('SKIP')
validpgpkeys=(
  '390EB159056ED56F66AB1092AECD456B4D2531FC'  # Pierre Tardy <tardyp@gmail.com>
)

build() {
  cd buildbot/pkg
  python setup.py build
}

package() {
  cd buildbot/pkg
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
