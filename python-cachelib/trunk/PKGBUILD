# Maintainer:
# Contributor: Filipe Nascimento <flipee at tuta dot io>
# Contributor: Julien Nicoulaud <julien DOT nicoulaud AT gmail DOT com>

pkgname=python-cachelib
_name=${pkgname#python-}
pkgver=0.9.0
pkgrel=1
pkgdesc='A collection of cache libraries in the same API interface. Extracted from werkzeug'
arch=(any)
url='https://github.com/pallets/cachelib'
license=(BSD)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest-xprocess)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('38222cc7c1b79a23606de5c2607f4925779e37cdcea1c2ad21b8bae94b5425a5')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  PYTHONPATH="$PWD"/src \
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
