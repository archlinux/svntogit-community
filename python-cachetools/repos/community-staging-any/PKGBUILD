# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cachetools
pkgname=(python-cachetools python2-cachetools)
pkgver=3.1.1
pkgrel=3
pkgdesc="Extensible memoizing collections and decorators"
url="https://github.com/tkem/cachetools"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tkem/cachetools/archive/v$pkgver.tar.gz")
sha512sums=('3765f2a4dbca7fa1da92be6f68f3d4c1202b558ca6d7a5f05b8bb9c13cbb9ebeef19d4370b29d0239dc606656ce74b3db4d3df5d5ae36ce323d94eae3f18aedc')

prepare() {
  cp -a cachetools-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py build

  cd "$srcdir"/cachetools-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py pytest

  cd "$srcdir"/cachetools-$pkgver-py2
  python2 setup.py pytest
}

package_python-cachetools() {
  depends=('python')

  cd cachetools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-cachetools() {
  depends=('python2')

  cd cachetools-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
