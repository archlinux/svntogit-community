# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgbase=python-cairocffi
pkgname=(python-cairocffi python2-cairocffi)
pkgver=0.9.0
pkgrel=1
pkgdesc="cairocffi is a CFFI-based drop-in replacement for Pycairo, a set of Python bindings and object-oriented API for cairo."
arch=('any')
url="https://cairocffi.readthedocs.io/"
license=('BSD')
makedepends=('python-cffi' 'python2-cffi' 'python-setuptools' 'python2-setuptools' 'cairo'
             'gdk-pixbuf2' 'python-xcffib' 'python2-xcffib')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Kozea/cairocffi/archive/v${pkgver}.tar.gz")
sha512sums=('a1a4d2ccb5885798f8889bbc181f6325fc0fff88065d01dd1dc2936dbd5ac71e959bb28516864ffd9244de13fba87c2c99028cea708476e8cf378f5538f5496c')

prepare() {
  cp -a cairocffi-$pkgver{,-py2}
}

build() {
  cd "$srcdir/cairocffi-$pkgver"
  python setup.py build

  cd "$srcdir/cairocffi-$pkgver-py2"
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.utf8

  cd "$srcdir/cairocffi-$pkgver"
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir/cairocffi-$pkgver-py2"
  python2 setup.py pytest || warning "Tests failed"
}

package_python-cairocffi() {
  depends=('python-cffi' 'cairo' 'python-xcffib')
  optdepends=('gdk-pixbuf2: for cairocffi.pixbuf')

  cd "$srcdir/cairocffi-$pkgver"
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-cairocffi() {
  depends=('python2-cffi' 'cairo' 'python2-xcffib')
  optdepends=('gdk-pixbuf2: for cairocffi.pixbuf')

  cd "$srcdir/cairocffi-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
