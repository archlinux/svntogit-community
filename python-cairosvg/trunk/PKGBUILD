# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: razer <razer[at]neuf[dot]fr>
# Contributor: pzl <alsoelp@gmail.com>
# Contributor: Dennis Fink <dennis.fink@c3l.lu>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>

pkgname=python-cairosvg
pkgver=2.1.3
pkgrel=2
pkgdesc="A Simple SVG Converter for Cairo"
arch=('any')
url="https://cairosvg.org/"
license=('LGPL3')
depends=('python-cairocffi' 'python-cssselect2' 'python-defusedxml' 'python-pillow'
         'python-tinycss2')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-isort'
              'python2-pytest-isort' 'python-pytest-flake8' 'python2-pytest-flake8'
              'python-pytest-cov' 'python2-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/CairoSVG/archive/$pkgver.tar.gz")
sha512sums=('2fe079be6e471ab28bbcd16d8dba00729ffc920163aa6a0fabf1223db21685a25fc03118e41f059e6164a3c1afd5375e05022954846fd75bc6b76ad122e90ef5')

prepare() {
  cd CairoSVG-$pkgver
  rmdir test/cairosvg_reference
  cp -a "$srcdir"/CairoSVG-$pkgver{,-copy}
  mv "$srcdir"/CairoSVG-$pkgver-copy test/cairosvg_reference
}

build() {
  cd CairoSVG-$pkgver
  python setup.py build
}

check() {
  cd CairoSVG-$pkgver
  python setup.py test
}

package() {
  cd CairoSVG-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
