# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: razer <razer[at]neuf[dot]fr>
# Contributor: pzl <alsoelp@gmail.com>
# Contributor: Dennis Fink <dennis.fink@c3l.lu>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>

pkgname=(python-cairosvg python2-cairosvg)
pkgver=1.0.22
pkgrel=1
pkgdesc="A Simple SVG Converter for Cairo"
arch=('any')
url="http://cairosvg.org/"
license=('LGPL3')
makedepends=('python-cairocffi' 'python2-cairocffi' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-pillow' 'python2-pillow' 'python-lxml'
              'python2-lxml' 'python-tinycss' 'python2-tinycss' 'python-cssselect'
              'python2-cssselect' 'gnu-free-fonts')
source=("git+https://github.com/Kozea/CairoSVG.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a CairoSVG{,-py2}
}

build() {
  cd "$srcdir/CairoSVG"
  python setup.py build

  cd "$srcdir/CairoSVG-py2"
  python2 setup.py build
}

check() {(
  cd "$srcdir/CairoSVG"
  nosetests3

  cd "$srcdir/CairoSVG-py2"
  nosetests2
  ) || warning "Tests failed"
  # Tests require special fonts that are not in the repos
}

package_python-cairosvg() {
  depends=('python-cairocffi')
  optdepends=('python-lxml: To use lxml to parse SVG files'
              'python-tinycss: Apply CSS not included in the style attribute of the tags'
              'python-cssselect: Apply CSS not included in the style attribute of the tags'
              'python-pillow: To handle embedded raster images other than PNG')

  cd "$srcdir/CairoSVG"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-cairosvg() {
  depends=('python2-cairocffi')
  optdepends=('python2-lxml: To use lxml to parse SVG files'
              'python2-tinycss: Apply CSS not included in the style attribute of the tags'
              'python2-cssselect: Apply CSS not included in the style attribute of the tags'
              'python2-pillow: To handle embedded raster images other than PNG')

  cd "$srcdir/CairoSVG-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mv "$pkgdir"/usr/bin/cairosvg{,2}
}
