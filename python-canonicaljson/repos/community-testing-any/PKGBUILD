# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-canonicaljson
pkgname=(python-canonicaljson python2-canonicaljson)
pkgver=1.1.4
pkgrel=2

pkgdesc='Canonical JSON'
url='https://pypi.python.org/pypi/canonicaljson/'
arch=('any')
license=('Apache')

makedepends=('python-setuptools'
             'python-simplejson'
             'python-frozendict'

             'python2-setuptools'
             'python2-simplejson'
             'python2-frozendict')

source=("python-canonicaljson-$pkgver.tar.gz::https://github.com/matrix-org/python-canonicaljson/archive/v${pkgver}.tar.gz")

md5sums=('3f29b4eab8219076532ab34315843634')

prepare() {
	cp -a python-canonicaljson-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/python-canonicaljson-$pkgver
	python setup.py build

	cd "$srcdir"/python-canonicaljson-$pkgver-python2
	python2 setup.py build
}

check() {
	cd "$srcdir"/python-canonicaljson-$pkgver
	python setup.py test

	cd "$srcdir"/python-canonicaljson-$pkgver-python2
	python2 setup.py test
}

package_python-canonicaljson() {
	depends=('python' 'python-simplejson' 'python-frozendict')

	cd python-canonicaljson-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-canonicaljson() {
	depends=('python2' 'python2-simplejson' 'python2-frozendict')

	cd python-canonicaljson-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
