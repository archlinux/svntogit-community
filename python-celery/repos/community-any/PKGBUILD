# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin Kolenda <marcinkolenda419@gmail.com>
# Contributor: Valentin Haloiu <vially.ichb+aur@gmail.com>
# Contributor: Lukas Linhart <bugs@almad.net>
# Contributor: Marco Elver <marco.elver AT gmail.com>
# Contributor: apkawa <apkawa@gmail.com>

pkgbase=python-celery
pkgname=('python-celery' 'python2-celery')
pkgver=4.2.0
pkgrel=1
pkgdesc='Distributed Asynchronous Task Queue'
arch=('any')
url='http://celeryproject.org/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-kombu' 'python2-kombu'
             'python-billiard' 'python2-billiard' 'python-pytz' 'python2-pytz')
checkdepends=('python-pytest32' 'python2-pytest32' 'python-pytest-runner' 'python2-pytest-runner'
              'python-case' 'python2-case' 'python-pyopenssl' 'python2-pyopenssl' 'python-gevent'
              'python2-gevent' 'python2-librabbitmq' 'python-pymongo' 'python2-pymongo'
              'python-msgpack' 'python2-msgpack' 'python-pyro' 'python2-pyro' 'python-redis'
              'python2-redis' 'python-sqlalchemy' 'python2-sqlalchemy' 'python-boto' 'python2-boto'
              'python-yaml' 'python2-yaml' 'python-pyzmq' 'python2-pyzmq' 'python-eventlet'
              'python2-eventlet')
source=("https://pypi.io/packages/source/c/celery/celery-$pkgver.tar.gz"
        celery@.service celery2@.service
        celery.tmpfiles.d celery2.tmpfiles.d)
options=('!emptydirs')
sha512sums=('ad7d18c318b6383e2a44a03f55f44d30f4d2fb6f3d05529376f0b05cbc28a8acbf3961a8d24a039e46917f439c1679d1f55a5957e4bc9587a6643a5c766ca487'
            '3c6c9dbdacca2de12d49c03526b34797fd867c14d04115af0d1fda64d2848a5d3f64ceb6d284be319a81d932dc86e69c157b6f5f859f2fa213fc2f43a6052c65'
            '1227803e56d5ea7879e097609b8f85633d6c0af4fa3607b98b4f30ffad093b0f3c3e7c54d965f44e954b6d9642a08e767836a9b837e225772d61dffa2042577c'
            '67279b75c3b44d065811c9c90aee006296164000912d5bb97c74956b26ee4ad4f0847e846052a896d379848b869c849300367e676d3f689cf29e3a0c7ae5310b'
            'ffeb86b07440de7943343d5a9348980a45d414f42dd29fd78f70e3680600e5dfb6a3a48748746e3af00f42aad9ba1cd8efeb8b2e81e86fb6febde16429b51399')

prepare() {
  cp -a celery-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/celery-$pkgver
  python setup.py build

  cd "$srcdir"/celery-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/celery-$pkgver
  python setup.py pytest

  cd "$srcdir"/celery-$pkgver-py2
  python2 setup.py pytest
}

package_python-celery() {
  depends=('python-kombu' 'python-billiard' 'python-pytz')
  optdepends=('python-pyopenssl: for celery.security'
              'python-pymongo: for celery.backends.mongodb'
              'python-msgpack: for using the msgpack serializer'
              'python-pyro: for using Pyro4 message transport'
              'python-redis: for celery.backends.redis'
              'python-sqlalchemy: for celery.backends.database'
              'python-boto: for SQS transport'
              'python-yaml: for using the yaml serializer'
              'python-pyzmq: for using ZeroMQ transport')

  cd "$srcdir"/celery-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 "$srcdir"/celery@.service "$pkgdir"/usr/lib/systemd/system/celery@.service
  install -Dm644 "$srcdir"/celery.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/celery.conf
}

package_python2-celery() {
  depends=('python2-kombu' 'python2-billiard' 'python2-pytz')
  optdepends=('python2-pyopenssl: for celery.security'
              'python2-gevent: for celery.concurrency.gevent'
              'python2-librabbitmq: optimized client for RabbitMQ'
              'python2-pymongo: for celery.backends.mongodb'
              'python2-msgpack: for using the msgpack serializer'
              'python2-pyro: for using Pyro4 message transport'
              'python2-redis: for celery.backends.redis'
              'python2-sqlalchemy: for celery.backends.database'
              'python2-boto: for SQS transport'
              'python2-yaml: for using the yaml serializer'
              'python2-pyzmq: for using ZeroMQ transport')

  cd "$srcdir"/celery-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 "$srcdir"/celery2@.service "$pkgdir"/usr/lib/systemd/system/celery2@.service
  install -Dm644 "$srcdir"/celery2.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/celery2.conf

  for _exe in "$pkgdir"/usr/bin/*; do
    mv "$_exe"{,2}
  done
}
