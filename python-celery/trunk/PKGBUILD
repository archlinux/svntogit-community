# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin Kolenda <marcinkolenda419@gmail.com>
# Contributor: Valentin Haloiu <vially.ichb+aur@gmail.com>
# Contributor: Lukas Linhart <bugs@almad.net>
# Contributor: Marco Elver <marco.elver AT gmail.com>
# Contributor: apkawa <apkawa@gmail.com>

pkgname=python-celery
pkgver=4.4.1
pkgrel=1
pkgdesc='Distributed Asynchronous Task Queue'
arch=('any')
url='http://celeryproject.org/'
license=('BSD')
depends=('python-kombu' 'python-billiard' 'python-pytz' 'python-vine')
optdepends=('python-cryptography: for celery.security'
            'python-pymongo: for celery.backends.mongodb'
            'python-msgpack: for using the msgpack serializer'
            'python-pyro: for using Pyro4 message transport'
            'python-redis: for celery.backends.redis'
            'python-sqlalchemy: for celery.backends.database'
            'python-boto3: for SQS transport'
            'python-yaml: for using the yaml serializer'
            'python-pyzmq: for using ZeroMQ transport')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-runner' 'python-case' 'python-cryptography'
              'python-gevent' 'python-pymongo' 'python-msgpack' 'python-pyro' 'python-redis'
              'python-sqlalchemy' 'python-boto3' 'python-yaml' 'python-pyzmq' 'python-eventlet'
              'python-moto' 'python-pre-commit')
source=("https://pypi.io/packages/source/c/celery/celery-$pkgver.tar.gz"
        celery@.service celery.tmpfiles.d)
options=('!emptydirs')
sha512sums=('91b0b4a5b6536665f73ccda09e8efa170f022e0201dcc615b45d4dcc68d486fe2485a717cb670fbc3f633721f6d3ff551f8f9fc3f1c605b4d7292cb0c1a5831d'
            '3c6c9dbdacca2de12d49c03526b34797fd867c14d04115af0d1fda64d2848a5d3f64ceb6d284be319a81d932dc86e69c157b6f5f859f2fa213fc2f43a6052c65'
            '67279b75c3b44d065811c9c90aee006296164000912d5bb97c74956b26ee4ad4f0847e846052a896d379848b869c849300367e676d3f689cf29e3a0c7ae5310b')

prepare() {
  cd celery-$pkgver
  sed -i -e 's/==/>=/' requirements/test.txt

  sed -i -e '/from pytest import PytestUnknownMarkWarning/a \    from _pytest.outcomes import Failed' \
         -e 's/ValueError/Failed/' t/unit/contrib/test_pytest.py
}

build() {
  cd celery-$pkgver
  python setup.py build
}

check() {
  cd celery-$pkgver
  # TODO
  python setup.py pytest --addopts "--deselect t/unit/security/test_security.py::test_security::test_setup_security__default_app"
}

package() {
  cd celery-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 "$srcdir"/celery@.service "$pkgdir"/usr/lib/systemd/system/celery@.service
  install -Dm644 "$srcdir"/celery.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/celery.conf
}
