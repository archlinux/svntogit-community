# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=cftime
pkgname=python-${_pkg}
pkgver=1.2.1
pkgrel=3
pkgdesc="Time-handling functionality from python-netcdf4"
arch=(x86_64)
url="https://unidata.github.io/cftime"
license=(MIT)
depends=(python-numpy)
makedepends=(cython python-setuptools)
checkdepends=(python-pytest python-pytest-cov)
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(https://github.com/Unidata/cftime/archive/v${pkgver}rel/${pkgname}-${pkgver}.tar.gz)
sha256sums=('e174523551625599a5f4c6d76c145af70518bdff833b8797a667d536ad737360')

build() {
    cd ${_pkg}-${pkgver}rel
    python setup.py build
}

check() {
    cd ${_pkg}-${pkgver}rel
    local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
    PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-${python_version}" py.test
}

package() {
    cd ${_pkg}-${pkgver}rel
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
