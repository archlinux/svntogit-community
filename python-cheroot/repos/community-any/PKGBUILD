# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgbase='python-cheroot'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cheroot' 'python2-cheroot')
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=6.3.2
pkgrel=1
arch=('any')
url="https://github.com/cherrypy/cheroot"
license=('MIT')
makedepends=('python-six' 'python2-six' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python2-backports.functools_lru_cache'
  'python2-backports.unittest_mock' 'tree')
source=("${pkgbase}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/11/d1/5be0e55bf606978c98a92f286bdf99606eaef636d2356d4947f8bf49084f/cheroot-6.3.2.tar.gz"
        "LICENSE"
        "cheroot-fix-setup-python3.patch")
sha512sums=('bae65749319fbcd3f713926efbd5aefc150fec592ac6f73e83027f1e69cb5b4a6fb1051f199220568f2b704055d3dc70abfe030870c1d5e85ab7113dda7cd086'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944'
            '5ec0001c803682f142e3eb1ebcf10614b3d4f2e64dfea874a868b7702a79b429ae86aea5c7b90711d573d5cc25e3296c3b64dc55598d926c06de59d35d8f04bc')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p1 -N -i "${srcdir}/cheroot-fix-setup-python3.patch"
}

package_python-cheroot() {
  depends=('python-six')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cheroot() {
  depends=('python2-six' 'python2-backports.functools_lru_cache'
           'python2-backports.unittest_mock')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/cheroot" "${pkgdir}/usr/bin/cheroot2"
}

# vim:set ts=2 sw=2 et:
