# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgbase='python-cheroot'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cheroot' 'python2-cheroot')
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=8.4.8
pkgrel=2
arch=('any')
url="https://github.com/cherrypy/cheroot"
license=('MIT')
makedepends=('python-six' 'python2-six' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python2-backports.functools_lru_cache'
  'python2-backports.unittest_mock' 'tree' 'python-tox' 'python2-tox'
  'python-jaraco' 'python2-jaraco' 'git')
checkdepends=('python-pytest' 'python-pytest-xdist'
              'python-requests' 'python-pyopenssl' 'python-trustme'
              'python2-pytest' 'python2-pytest-xdist'
              'python2-requests' 'python2-pyopenssl')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        "LICENSE"
        "disable-broken-tests.patch"
        "disable-broken-testmon-stuff.patch")
sha512sums=('fa0362d29adbcf0b06038d209e33d722ff2f0766d8f2dd46c8ca217195ef5b30bb51400a9615f11cc136683b146ab2c21c3f1974cb4e7ad55488762e31da40f5'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944'
            '488766eeb0ce4e1c95e1848a4d89aeb190317c634b2894dcc696cb4af64bae748554154ea76cc34cf0b666d507c42e631d0e8759a6ed7669fe49bf7f8ef5b9a9'
            '67d81874da0f0dca04356155f849908e187c2cf8c268f477c4a3d30c767cc485bc33c6358507f97477a2f630df44deb3f39dbd9a6c38bb28c7cd222bb556226d')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p1 -N -i "${srcdir}/disable-broken-tests.patch"
  patch -p1 -N -i "${srcdir}/disable-broken-testmon-stuff.patch"
  # don't use python-coverage
  sed -i '/^    --cov/d' pytest.ini

  # git-archive support is not needed since we use PyPI tarballs
  sed -i '/setuptools_scm_git_archive/d' setup.cfg

  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  # tox doesn't really provide any meaningful results for downstream packaging
  # TODO: properly run the tests with distro packages

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  # tox doesn't really provide any meaningful results for downstream packaging
  # TODO: properly run the tests with distro packages
}

package_python-cheroot() {
  depends=('python-six' 'python-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cheroot() {
  depends=('python2-six' 'python2-backports.functools_lru_cache'
           'python2-backports.unittest_mock' 'python2-selectors2'
           'python2-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/cheroot" "${pkgdir}/usr/bin/cheroot2"
}

# vim:set ts=2 sw=2 et:
