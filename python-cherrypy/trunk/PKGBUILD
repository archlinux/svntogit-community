# Maintainer:  Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgbase='python-cherrypy'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cherrypy' 'python2-cherrypy')
pkgver=14.2.0
pkgrel=1
pkgdesc="A pythonic, object-oriented web development framework"
arch=('any')
url="http://www.cherrypy.org"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python-tox' 'python2-tox' 'python-six'
  'python2-six' 'python-cheroot' 'python2-cheroot' 'python-portend'
  'python2-portend')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/${_pkgbase}/${_pkgbase}/archive/v$pkgver.tar.gz")
sha512sums=('a9b6fc466af4b8df31aae10094ad0414cdac4e59159b0d9a0634660f161669648e2b21b929f08bc965747f389b0a1da3aa15d86addddb8540a6caf34a0ef0cb1')

prepare() {
  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"

  # disable broken tests
  rm "${srcdir}/${_pkgbase}-${pkgver}/${_pkgbase}/test"/test_{objectmapping.py,request_obj.py}
  rm "${srcdir}/${_pkgbase}-${pkgver}-py2/${_pkgbase}/test"/test_{encoding.py,objectmapping.py,request_obj.py}
}

build() {

  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py build

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  # fix incorrect shebang
  sed -i 's|#!/usr/bin/python$|#!/usr/bin/python2|' cherrypy/test/sessiondemo.py
  python2 ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  tox

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  tox2
}

package_python-cherrypy() {
  depends=('python' 'python-six' 'python-cheroot' 'python-portend' 'python-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}"

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_python2-cherrypy() {
  depends=('python2' 'python2-six' 'python2-cheroot' 'python2-portend' 'python2-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"

  python2 ./setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}/usr/bin/cherryd" "${pkgdir}/usr/bin/cherryd2"

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
