# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>
pkgname=('python-cherrypy' 'python2-cherrypy')
pkgver=8.1.2
pkgrel=2
pkgdesc="A pythonic, object-oriented web development framework"
arch=('any')
url="http://www.cherrypy.org"
license=('BSD')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/d9/ec/a9cd68e57af9fead92158028d32cc79f1908fd81f486d8a3826e9847ada6/CherryPy-8.1.2.tar.gz")
md5sums=('553f054152c2adbe60738682ed5390c5')

build() {
  cp -r CherryPy-${pkgver} CherryPy-${pkgver}-py2

  cd CherryPy-${pkgver}
  python ./setup.py build

  cd "${srcdir}/CherryPy-${pkgver}-py2"
  python2 ./setup.py build
}

package_python-cherrypy() {
  depends=('python' 'python-six')

  cd CherryPy-${pkgver}

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 cherrypy/LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python-cherrypy/LICENSE.txt"
}

package_python2-cherrypy() {
  depends=('python2')

  cd CherryPy-${pkgver}-py2

  python2 ./setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}/usr/bin/cherryd" "${pkgdir}/usr/bin/cherryd2"

  install -Dm644 cherrypy/LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python2-cherrypy/LICENSE.txt"
}

check() {
  cd CherryPy-${pkgver}
#  python2 ./setup.py test

  cd ../CherryPy-${pkgver}
#  python3 ./setup.py check
}
