# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Xeonacid <h.dwwwwww at gmail dot com>

_pyname=cle
pkgname=python-${_pyname}
pkgver=9.2.27
_testbinaries_ver=9.2.27
pkgrel=2
pkgdesc='Loads binaries and their libraries, resolves imports and provides an abstraction of process memory'
url='https://github.com/angr/cle'
arch=('any')
license=('custom:BSD')
depends=('python' 'python-pyelftools' 'python-cffi' 'python-pyvex' 'python-pefile' 'python-sortedcontainers')
optdepends=(
  'python-claripy: symbolic constraints'
  'python-minidump: minidump'
  'python-pyxbe: xbe'
  'python-arpy: ar'
  'python-pyaxmlparser: apk'
)
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-minidump' 'python-pyxbe' 'python-arpy' 'python-claripy' 'python-pyaxmlparser')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/angr/binaries/archive/v${_testbinaries_ver}/angr-binaries-${_testbinaries_ver}.tar.gz")
sha512sums=('24e2b699c4036e5a94138761e79b02dbee080fb72bb6d109c5de9f2b6971687ba9fc059fc8dd15d70f23299601cbbd466d8c6dcac1735dabbf1905397b605dce'
            '2e790b3d6f60145349e673f0491e0a5ba4be2c78cbb82b7b1245bb2c35f7f555656d2997b8b02c214ef2b61e5ab841e19ab284934ab74f63bb8ce278d6e57a4f')
b2sums=('4afac6fc8b9aa73527b7b65a8051113f4478bf0dc338b40f4ab9b9a83bc6c1a403ef8b3bfc3239633b9a36317ab9cb2197469d8c40d80ff040f2bf8361209d28'
        '12b3257464c7e64dd5f4b227cb4f73723687fdad15cf9119847d005213c796591ee705cd8d1546e3440dedf3f628fdc199f39aa25b07e0e823b6088eb75f4219')

prepare() {
  ln -s binaries-${_testbinaries_ver} binaries
}

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
