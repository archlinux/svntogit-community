# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=click-threading
pkgname=("python-${_name}" "python2-${_name}")
pkgver=0.4.4
pkgrel=4
pkgdesc="Multithreaded Click apps made easy"
arch=("any")
url="https://pypi.python.org/pypi/click-threading"
license=("MIT")
makedepends=("python-setuptools" "python2-setuptools")
checkdepends=("python-pytest" "python-click" "python2-pytest" "python2-click"
'python2-futures')
source=("${_name}-${pkgver}.tar.gz::https://github.com/click-contrib/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('4a7af23be600cab43dcda267d9d9d3ed0cc885f9829f2412844ac9cd3f0f41d1bea3f157f9e37cc3333c4b0a3d65ab4525edcaeec767850efab5cc037e95e532')

prepare() {
  mv "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build

  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  # Needed for the tests, especially when building in clean chroot
  [[ ${LANG} = C ]] && export LANG=en_US.UTF8
  cd "${pkgname[0]}-${pkgver}"
  export PYTHONPATH=build/lib:${PYTHONPATH}
  py.test

  cd ../"${pkgname[1]}-${pkgver}"
  export PYTHONPATH=build/lib:${PYTHONPATH}
  py.test2
}

package_python-click-threading() {
  depends=("python-click")
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  install -vDm644 README.rst "${pkgdir}/usr/share/doc/${pkgname[0]}/README.rst"
}

package_python2-click-threading() {
  depends=("python2-click" 'python2-futures')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  install -vDm644 README.rst "${pkgdir}/usr/share/doc/${pkgname[1]}/README.rst"
}

