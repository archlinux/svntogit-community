# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cliff
pkgname=(python-cliff python2-cliff)
pkgver=2.5.0
pkgrel=1
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="http://docs.openstack.org/developer/cliff"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-cmd2' 'python2-cmd2' 'python-prettytable' 'python2-prettytable'
             'python-pyparsing' 'python2-pyparsing' 'python-stevedore'
             'python2-stevedore' 'python-unicodecsv' 'python2-unicodecsv' 'python-yaml'
             'python2-yaml')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz")
sha512sums=('e9395cdd8b2ca614a06373207e6b5672c950fc1f12185428c1369ab2e911fc4a848f064128cbb15ec67c25eed0aca48cccab990757110cf60f51417ac64f4e88')

prepare() {
  cp -a cliff-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/cliff-$pkgver
  python setup.py build

  cd "$srcdir"/cliff-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cliff-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests3 -d

  cd "$srcdir"/cliff-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2 -d
}

package_python-cliff() {
  depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
           'python-stevedore' 'python-unicodecsv' 'python-yaml')

  cd "$srcdir"/cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cliff() {
  depends=('python2-six' 'python2-pbr' 'python2-cmd2' 'python2-prettytable' 'python2-pyparsing'
           'python2-stevedore' 'python2-unicodecsv' 'python2-yaml')

  cd "$srcdir"/cliff-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
