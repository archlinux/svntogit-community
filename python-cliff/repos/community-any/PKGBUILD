# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cliff
pkgname=(python-cliff python2-cliff)
pkgver=2.10.0
pkgrel=1
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="http://docs.openstack.org/developer/cliff"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-cmd2' 'python2-cmd2' 'python-prettytable' 'python2-prettytable'
             'python-pyparsing' 'python2-pyparsing' 'python-stevedore'
             'python2-stevedore' 'python2-unicodecsv' 'python-yaml' 'python2-yaml')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-mock' 'python2-mock'
              'python-sphinx' 'python2-sphinx' 'python-oslosphinx' 'python2-oslosphinx'
              'python-testscenarios' 'python2-testscenarios')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz")
sha512sums=('8ecdf7fb82f9481b0e7639df4d5f1a8ebd087dc732629e8009387105d714ecef857da1d59f8cfcee02f0a3c83797b1585b3556a5abe180bbfdfbf3e04928b663')

prepare() {
  cp -a cliff-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/cliff-$pkgver
  python setup.py build

  cd "$srcdir"/cliff-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cliff-$pkgver
  python setup.py testr

  cd "$srcdir"/cliff-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-cliff() {
  depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
           'python-stevedore' 'python-yaml')

  cd "$srcdir"/cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cliff() {
  depends=('python2-six' 'python2-pbr' 'python2-cmd2' 'python2-prettytable' 'python2-pyparsing'
           'python2-stevedore' 'python2-unicodecsv' 'python2-yaml')

  cd "$srcdir"/cliff-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
