# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cloudflare
pkgname=('python-cloudflare' 'python2-cloudflare')
pkgver=1.8.1
pkgrel=1
pkgdesc='Python wrapper for the Cloudflare v4 API'
arch=('any')
license=('MIT')
url='https://github.com/cloudflare/python-cloudflare'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-future' 'python2-future' 'python-yaml' 'python2-yaml')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cloudflare/python-cloudflare/archive/$pkgver.tar.gz")
sha512sums=('5f15a3a47ada87761743e88a8a6ada3c9a788e1994e16609c6bde9ee1777db9d56e7d85fce039596a0fa88a9ebee926da34a08177bbe35e70c2171f07aa1b590')

prepare() {
  cp -a python-cloudflare-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-cloudflare-$pkgver
  python setup.py build

  cd "$srcdir"/python-cloudflare-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-cloudflare-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest tests/test1.py

  cd "$srcdir"/python-cloudflare-$pkgver-py2
  PYTHONPATH="$PWD/build/lib" pytest2 tests/test1.py
}

package_python-cloudflare() {
  depends=('python-requests' 'python-future' 'python-yaml')

  cd python-cloudflare-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm -r "$pkgdir"/usr/lib/python3.6/site-packages/examples
}

package_python2-cloudflare() {
  depends=('python2-requests' 'python2-future' 'python2-yaml')

  cd python-cloudflare-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/cli4{,-python2}

  rm -r "$pkgdir"/usr/lib/python2.7/site-packages/examples
}

# vim:set ts=2 sw=2 et:
