# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Chris Warrick <aur@chriswarrick.com>

pkgbase=python-cloudpickle
pkgname=('python-cloudpickle' 'python2-cloudpickle')
_pyname=cloudpickle
pkgver=0.5.2
pkgrel=2
pkgdesc='Extended pickling support for Python objects'
arch=('any')
url='https://github.com/cloudpipe/cloudpickle'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha256sums=('b0e63dd89ed5285171a570186751bc9b84493675e99e12789e9a5dc5490ef554')

prepare() {
  # Prepare sources for python2 build
  cp -r "${_pyname}-${pkgver}" "python2-cloudpickle-${pkgver}"
}

package_python-cloudpickle() {
  depends=('python-setuptools')

  cd "${_pyname}-${pkgver}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cloudpickle() {
  depends=('python2-setuptools')

  cd "${pkgname}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

