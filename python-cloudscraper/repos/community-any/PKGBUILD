# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=cloudscraper
pkgname=python-cloudscraper
pkgver=1.2.58
pkgrel=2
pkgdesc="Python module to bypass Cloudflare's anti-bot page"
arch=('any')
url="https://github.com/VeNoMouS/${_pkgname}"
license=('MIT')
depends=('python-pyparsing' 'python-requests' 'python-requests-toolbelt')
optdepends=('python-js2py: alternative interpreter/solver'
            'nodejs: alternative interpreter/solver')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-responses' 'python-js2py' 'nodejs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('9fc3562818e58f98d4e3ca874de5ff24d072c554b5a627ce9e38d394bb209c7f')
b2sums=('b3e002d559ebdbb07686d74fe9967776f832397758653c2d6fe8ba766239d5744608fe6f321a0284c1da02a1ddb8257d7ce1931bb5adf71096e4151dbb9cb2a2')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest -k 'not test_getCookieString_challenge_js_challenge1_16_05_2020'
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
