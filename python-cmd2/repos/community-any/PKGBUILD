# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cmd2
pkgver=1.2.1
pkgrel=1
pkgdesc="A tool for building interactive command line apps"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
depends=('python-attrs' 'python-colorama' 'python-pyperclip' 'python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-pytest-mock' 'vi')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('8b2c9880c0eac250ecf0f3189d16d0675cbce83004dbc33525dabeed59c431c05370d8a4929c4c557d5b433316739c34164dd6ed10c75e131891432eada981a7')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd cmd2-$pkgver
  python setup.py build
}

check() {
  cd cmd2-$pkgver
  python setup.py pytest
}

package() {
  cd cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
