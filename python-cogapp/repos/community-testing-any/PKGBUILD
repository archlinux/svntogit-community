# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cogapp
pkgver=3.2.0
pkgrel=3
pkgdesc="A code generator for executing Python snippets in source files"
url="https://nedbatchelder.com/code/cog"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/c/cogapp/cogapp-$pkgver.tar.gz")
sha512sums=('4afb62f73e64a0c64e512941145c3c53ad22519e7522b16fc1271f39a55c7e70202f4c5c9e70146163c4291ffd95f8dfddd117b250e4b66485aef7ed4c5cdef8')

build() {
  cd cogapp-$pkgver
  python setup.py build
}

check() {
  cd cogapp-$pkgver
  python setup.py pytest
}

package() {
  cd cogapp-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
