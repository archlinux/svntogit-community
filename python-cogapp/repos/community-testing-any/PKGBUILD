# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cogapp
pkgver=3.0.0
pkgrel=1
pkgdesc="A code generator for executing Python snippets in source files"
url="http://nedbatchelder.com/code/cog"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/c/cogapp/cogapp-$pkgver.tar.gz")
sha512sums=('7d70ee08da24b005cf838252c55e16c53e6517d01a3e99d85fb2e92b7a9233f515ce952cb233fc9fb210e31b38c88bfe46043b189234f132ec625430e62b83bb')

build() {
  cd cogapp-$pkgver
  python setup.py build
}

check() {
  cd cogapp-$pkgver
  python setup.py pytest
}

package() {
  cd cogapp-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
