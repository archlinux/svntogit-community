# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-configargparse
pkgname=('python-configargparse' 'python2-configargparse')
pkgver=0.9.3
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/zorro3/ConfigArgParse'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/C/ConfigArgParse/ConfigArgParse-$pkgver.tar.gz")
sha256sums=('141c57112e1f8eb7e594a9820e95af897a7fa2d186cef5cff7e08cb3f7252829')

prepare() {
  cp -a ConfigArgParse-$pkgver{,-py2}
}

check() {
  # Tests not included in release tarball
  return

  cd "$srcdir/ConfigArgParse-$pkgver"
  python setup.py test

  cd "$srcdir/ConfigArgParse-$pkgver-py2"
  python2 setup.py test
}

package_python-configargparse() {
  depends=('python')
  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-configargparse() {
  depends=('python2')
  cd ConfigArgParse-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
