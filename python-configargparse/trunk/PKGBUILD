# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configargparse
pkgver=1.4.1
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/bw2/ConfigArgParse'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-tests' 'python-yaml')
optdepends=('python-yaml: for YAML support')
source=("https://github.com/bw2/ConfigArgParse/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('304d5981c5685188edb22a63966d25a8943e4c443f2aefc323492f141885f50657e9f9200514df65683f1e13e64173d4def80cdbebcff941c3ee66ff4af0cbd4')

build() {
  cd ConfigArgParse-$pkgver
  python setup.py build
}

check() {
  cd ConfigArgParse-$pkgver
  python setup.py test
}

package() {
  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
