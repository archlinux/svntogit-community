# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-configargparse
pkgname=('python-configargparse' 'python2-configargparse')
pkgver=0.12.0
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/zorro3/ConfigArgParse'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-yaml' 'python2-yaml')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bw2/ConfigArgParse/archive/$pkgver.tar.gz")
sha512sums=('c8499241c3fb13bc7734e1d63bee1cedce909c6800595265e61f8d086f985b08a01ce8312bc47db04402bb4070d41884fe8eedd73d9c5ef7c7a159236639792d')

prepare() {
  cp -a ConfigArgParse-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/ConfigArgParse-$pkgver
  python setup.py test

  cd "$srcdir"/ConfigArgParse-$pkgver-py2
  python2 setup.py test
}

package_python-configargparse() {
  depends=('python')
  optdepends=('python-yaml: for YAML support')

  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-configargparse() {
  depends=('python2')
  optdepends=('python2-yaml: for YAML support')

  cd ConfigArgParse-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
