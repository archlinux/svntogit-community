# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.com.ve>
# Contributor: Stefan Husmann  <stefan-husmann@t-online.de>
# Contributor: Manuel C.

pkgname=python-configobj
pkgver=5.0.6.r110.g3e2f4cc
pkgrel=1
pkgdesc='Simple config file reader and writer'
arch=(any)
url='https://github.com/DiffSK/configobj'
license=(BSD)
depends=(python-six)
makedepends=(git python-setuptools)
source=("git+$url#commit=3e2f4cca81b60171e6b3e23619f6f4929a13322e") # latest master, ref FS#68893
sha256sums=(SKIP)

pkgver() {
  cd configobj
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd configobj
  # Avoid the need for a local configobj install just for VERSION
  local _version=$(grep -oP "__version__ = '\K[^']+" src/configobj/_version.py)
  sed -i "/from validate/d;s/VERSION/\"$_version\"/" setup_validate.py
}

package() {
  cd configobj
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  python setup_validate.py install --root="$pkgdir" --optimize=1
}
