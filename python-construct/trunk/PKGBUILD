# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-construct
pkgver=2.10.56
pkgrel=3
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-arrow' 'python-numpy' 'python-pytest-runner' 'python-pytest-benchmark'
              'python-ruamel-yaml')
optdepends=('python-arrow: for Timestamp'
            'python-numpy: for array serialization with Numpy protocol'
            'python-ruamel-yaml: for KaitaiStruct (KSY) exporter')
source=("$pkgname-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('cc9aa126c26a017a3fb7b78a6b8f2056194028a44c8ed615a6dc4a445d50f0463e2de0b80810ab5a252070925a56b5a61dbb601a3de9e445d861356231a146d5')

build() {
  cd construct-$pkgver
  python setup.py build
}

check() {
  cd construct-$pkgver
  python setup.py pytest
}

package() {
  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
