# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-copr
pkgver=1.114
pkgrel=1
pkgdesc="Python client for copr service"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-marshmallow' 'python-requests' 'python-requests-toolbelt' 'python-six' 'python-munch')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=("https://pagure.io/copr/copr/archive/python-copr-$pkgver-1/copr-python-copr-$pkgver-1.tar.gz")
sha512sums=('0c8201fb3877981a98e7e0d0aa2cd762ac29ae2241b809ff08c5043d02f52d8359affbf47ebd66121ac7372caf62c8e5a8e94577b21031c8e5dcd114d5c12c70')

build() {
  cd copr-python-copr-$pkgver-1/python
  python setup.py build
}

check() {
  cd copr-python-copr-$pkgver-1/python
  python -m pytest
}

package() {
  cd copr-python-copr-$pkgver-1/python
  python setup.py install --root="$pkgdir" --optimize=1
}
