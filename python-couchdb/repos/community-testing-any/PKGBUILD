# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Byron Clark <byron@theclarkfamily.name>
# Contributor: Guan 'kuno' Qing <neokuno AT gmail DOT com>

pkgbase=python-couchdb
pkgname=(python-couchdb python2-couchdb)
pkgver=1.2
_commit=44ec56ced41d507fce4237d01ea467f7505f58e1
pkgrel=1
pkgdesc="Python library for working with CouchDB"
url="https://github.com/djc/couchdb-python/"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pifpaf' 'couchdb')
source=("git+https://github.com/djc/couchdb-python.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a couchdb-python{,-py2}

  cd "$srcdir"/couchdb-python-py2
  sed -i 's/python /python2 /' Makefile
  find . -name "*.py" -exec \
    sed -i '1s|env python$|env python2|' {} \;
}

build() {
  cd "$srcdir"/couchdb-python
  python setup.py build

  cd "$srcdir"/couchdb-python-py2
  python2 setup.py build
}

check() {
  # New couchdb doesn't provide /usr/bin/couchdb
  export PATH="$PATH:/usr/lib/couchdb/bin"

  cd "$srcdir"/couchdb-python
  pifpaf run couchdb python setup.py test || warning "python 3 tests failed"

  cd "$srcdir"/couchdb-python-py2
  pifpaf run couchdb python2 setup.py test
}

package_python-couchdb() {
  depends=('python-setuptools')

  cd couchdb-python
  python setup.py install --root="$pkgdir" --optimize=1

  install -D -m644 "COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_python2-couchdb() {
  depends=('python2-setuptools')

  cd couchdb-python-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -D -m644 "COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  for name in couchdb-dump couchdb-load couchdb-replicate couchpy couchdb-load-design-doc; do
    mv "$pkgdir/usr/bin/$name"{,2}
  done
}

# vim:ts=2:sw=2:et
