# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

_pkgname=coveragepy
pkgname=python-coverage
pkgver=6.1.1
pkgrel=3
pkgdesc="A tool for measuring code coverage of Python programs"
arch=('x86_64')
url="https://nedbatchelder.com/code/coverage/"
license=('Apache')
depends=('python')
optdepends=('python-tomli: for pyproject.toml support')
makedepends=('python-setuptools')
checkdepends=('python-flaky' 'python-hypothesis' 'python-mock'
              'python-pycontracts' 'python-pytest' 'python-pytest-xdist' 'python-tomli'
              'python-unittest-mixins' 'python-virtualenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nedbat/coveragepy/archive/$pkgver.tar.gz")
sha512sums=('bfef3b70b76b12ea08cd7c6751d25c172bf72e28ff5df5b1812f79582b6c2ef447bce234c0ebe289474c292dec631ec95482c6f406210f22c5af3ea9e4d63d50')
b2sums=('fe0241b1c10d030d21b9daed86bfc5a07646fc96e222964c4150ef11b20d986b49b5e6063ebc36587924c532d4283165fdfc944c4065387fa344c00e74cabb79')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  (
    virtualenv "test_dir" --system-site-packages
    . "test_dir/bin/activate"
    export PYTHONPATH="/usr/lib/python$python_version/site-packages:$PYTHONPATH"
    python setup.py --quiet clean develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  ) || echo "Known failing tests."
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="$pkgdir"
}
