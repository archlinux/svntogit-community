# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

pkgname=(python-coverage python2-coverage)
pkgver=4.2
pkgrel=2
pkgdesc="A tool for measuring code coverage of Python programs"
arch=('i686' 'x86_64')
url="http://nedbatchelder.com/code/coverage/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'mercurial')
checkdepends=('python-mock' 'python2-mock' 'python-nose' 'python2-nose' 'python-pycontracts'
              'python2-pycontracts' 'python-greenlet' 'python2-greenlet' 'python-virtualenv'
              'python2-virtualenv' 'python-pyenchant' 'python2-pyenchant' 'python-pylint'
              'python2-pylint' 'python-unittest-mixins' 'python2-unittest-mixins')
source=("hg+https://bitbucket.org/ned/coveragepy#tag=coverage-$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a coveragepy{,-py2}
}

check() {
  # Hack entry points by installing it

  export LC_CTYPE=en_US.UTF-8

  (
    cd "$srcdir/coveragepy"
    virtualenv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py --quiet develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  )

  (
    cd "$srcdir/coveragepy-py2"
    virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
    . "$srcdir/pyvenv-py2/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python2.7/site-packages"
    python setup.py --quiet develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  ) || warning "Python 2 tests failed"
}

package_python-coverage() {
  depends=('python')

  cd coveragepy
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-coverage() {
  depends=('python2')

  cd coveragepy
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir/usr/bin/coverage" "$pkgdir/usr/bin/coverage2"
}
