# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-cram
pkgname=('python-cram' 'python2-cram')
pkgver=0.7
pkgrel=3
pkgdesc='Functional tests for command line applications'
arch=('any')
url='https://bitheap.org/cram/'
license=('GPL')
makedepends=('python' 'python2' 'git')
source=("https://bitheap.org/cram/cram-$pkgver.tar.gz")
md5sums=('2ea37ada5190526b9bcaac5e4099221c')

prepare() {
  cp -a cram-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cram-$pkgver
  python setup.py build

  cd "$srcdir"/cram-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cram-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python scripts/cram tests

  cd "$srcdir"/cram-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 scripts/cram tests
}

package_python-cram() {
  depends=('python')

  cd cram-$pkgver
  python setup.py install --root="$pkgdir" -O1
}

package_python2-cram() {
  depends=('python2')

  cd cram-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1

  mv "$pkgdir"/usr/bin/cram{,2}
}
