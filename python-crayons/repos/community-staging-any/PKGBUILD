# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-crayons
pkgname=('python-crayons' 'python2-crayons')
pkgver=0.1.2
pkgrel=2
pkgdesc='TextUI colors for Python'
arch=('any')
license=('MIT')
url='https://github.com/kennethreitz/crayons'
makedepends=('python-setuptools' 'python2-setuptools' 'python-colorama' 'python2-colorama')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kennethreitz/crayons/archive/v$pkgver.tar.gz")
sha512sums=('5da863c9f1b0f2c8713c6e0412701309ff9e52df782151b9aad4d2a852e2fa1220f96ddee9ced1aed6500fa2b67e6d3bbc53d980c51f77739888d826c6f5c275')

prepare() {
  cp -a crayons-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/crayons-$pkgver
  python setup.py build

  cd "$srcdir"/crayons-$pkgver-py2
  python2 setup.py build
}

package_python-crayons() {
  depends=('python-colorama')

  cd crayons-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  # install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-crayons() {
  depends=('python2-colorama')

  cd crayons-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  # install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
