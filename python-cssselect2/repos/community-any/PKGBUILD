# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-cssselect2
pkgname=(python-cssselect2 python2-cssselect2)
pkgver=0.2.1
pkgrel=2
pkgdesc="CSS selectors for Python ElementTree"
url="https://pypi.org/project/cssselect2/"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-tinycss2' 'python2-tinycss2')
checkdepends=('python-pytest-flake8' 'python2-pytest-flake8' 'python-pytest-cov'
              'python2-pytest-cov' 'python-pytest-isort' 'python2-pytest-isort'
              'python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/c/cssselect2/cssselect2-$pkgver.tar.gz")
md5sums=('bfa61b1e917d1e93ebccf0f27903087d')

prepare() {
  cp -a cssselect2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cssselect2-$pkgver
  python setup.py build

  cd "$srcdir"/cssselect2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cssselect2-$pkgver
  python setup.py pytest

  cd "$srcdir"/cssselect2-$pkgver-py2
  python2 setup.py pytest
}

package_python-cssselect2() {
  depends=('python-tinycss2')

  cd cssselect2-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-cssselect2() {
  depends=('python2-tinycss2')

  cd cssselect2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
