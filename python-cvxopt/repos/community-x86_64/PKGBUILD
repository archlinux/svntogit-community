# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: BinkyTheClown <binky_at_archlinux_dot_us>
# Contributor: Julien Ugon <bzklrm at gmail dot com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Gustavao A. Gomez Farhat <gustavo_gomez_farhat_at_gmail_dot_com>

pkgbase=python-cvxopt
pkgname=('python2-cvxopt' 'python-cvxopt')
pkgver=1.2.1
pkgrel=1
pkgdesc="A free software package for convex optimization written in Python"
url="http://cvxopt.org/"
arch=('x86_64')
license=('GPL3')
makedepends=('gsl' 'fftw' 'glpk' 'dsdp' 'python-setuptools' 'python2-setuptools' 'suitesparse')
optdepends=('gsl: for custom random number generators'
            'fftw: for FFTW interface'
            'glpk: solver for linear cone and PWL programming problems'
            'dsdp: solver for linear cone semidefinite programming problems')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cvxopt/cvxopt/archive/${pkgver}.tar.gz")
sha1sums=('0141a4b4771dd49e188c3c3247d789428b792b55')


prepare() {
  cd "$srcdir"
  cp -a cvxopt-${pkgver} cvxopt-py2-${pkgver}
}

build() {
  cd "$srcdir"/cvxopt-$pkgver

  for lib in GSL FFTW GLPK DSDP; do
    eval "export $'CVXOPT_BUILD_$lib'=1"
  done

  msg "Building Python2"
  cd "$srcdir"/cvxopt-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/cvxopt-${pkgver}
  python setup.py build
}

package_python2-cvxopt() {
  depends=("python2" "suitesparse")

  cd "$srcdir"/cvxopt-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python-cvxopt() {
  depends=("python" "suitesparse")

  cd "$srcdir"/cvxopt-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
