# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Julien Duponchelle <julien@gns3.net>

pkgbase=python-cx_freeze
pkgname=('python-cx_freeze' 'python2-cx_freeze')
pkgver=4.3.4
pkgrel=3
pkgdesc='Utilities for freezing Python scripts into executables'
arch=('x86_64' 'i686')
url='http://cx-freeze.sourceforge.net/'
license=('PSF')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/c/cx_Freeze/cx_Freeze-$pkgver.tar.gz"
        'python35.patch')
md5sums=('5bd662af9aa36e5432e9144da51c6378'
         '92b761ef5f225646c14a261794787f33')

prepare() {
  cd "cx_Freeze-$pkgver"
  patch -p1 -i ../python35.patch
}

package_python-cx_freeze() {
  depends=('python')
  conflicts=('cx_freeze' 'python2-cx_freeze')

  cd "cx_Freeze-$pkgver"
  python setup.py install --root "$pkgdir" --optimize 1
}

package_python2-cx_freeze() {
  depends=('python2')
  conflicts=('cx_freeze' 'python-cx_freeze')
  replaces=('cx_freeze')

  cd "cx_Freeze-$pkgver"
  python2 setup.py install --root "$pkgdir" --optimize 1

  # Python 2 fix
  find "$pkgdir" -name qotd.py -exec sed -i '0,/on/s//on2/' {} \;
}

# vim:set ts=2 sw=2 et:
