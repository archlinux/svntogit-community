# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Julien Duponchelle <julien@gns3.net>

pkgbase=python-cx_freeze
pkgname=('python-cx_freeze' 'python2-cx_freeze')
pkgver=5.1.1
pkgrel=3
pkgdesc='Utilities for freezing Python scripts into executables'
arch=('x86_64')
url='https://anthony-tuininga.github.io/cx_Freeze'
license=('PSF')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-openpyxl' 'python2-openpyxl' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/anthony-tuininga/cx_Freeze/archive/$pkgver.tar.gz"
        python3.7.patch)
sha512sums=('95d70c23678080fec96df78658aeaaeb81ce0660ce242a363699d6b82d926d021519f9b67dc458b0d2e3c89230a582cfec6862a2346bf0deacc636ce125c0c96'
            '9009d617213bcf57124b32b1680fa1a2282885ff27fdc6c14482c3db34b2859d2e6cdc724c114a2ccd3a90f11c83e3c3837d1df58d13a041ab1d2439804cb1d4')

prepare() {
  cd cx_Freeze-$pkgver
  patch -p1 -i ../python3.7.patch
}

build() {
  cd cx_Freeze-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd cx_Freeze-$pkgver
  python setup.py nosetests || warning "Tests failed"
  python2 setup.py nosetests
}

package_python-cx_freeze() {
  depends=('python')
  conflicts=('cx_freeze' 'python2-cx_freeze')

  cd cx_Freeze-$pkgver
  python setup.py install --root "$pkgdir" --optimize 1 --skip-build
}

package_python2-cx_freeze() {
  depends=('python2')
  conflicts=('cx_freeze' 'python-cx_freeze')
  replaces=('cx_freeze')

  cd cx_Freeze-$pkgver
  python2 setup.py install --root "$pkgdir" --optimize 1 --skip-build

  # Python 2 fix
  find "$pkgdir" -name qotd.py -exec sed -i '0,/on/s//on2/' {} \;
}
