# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-daemonize
pkgname=(python-daemonize python2-daemonize)
pkgver=2.4.7
pkgrel=3

pkgdesc='Library to enable your code run as a daemon process on Unix-like systems'
url='https://pypi.python.org/pypi/daemonize/'
arch=('any')
license=('MIT')

makedepends=('python-setuptools'
             'python2-setuptools')

source=("https://pypi.org/packages/source/d/daemonize/daemonize-$pkgver.tar.gz")

md5sums=('6759005b12dfeea0d4305f8536b4b0c2')

prepare() {
	cp -a daemonize-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/daemonize-$pkgver
	python setup.py build

	cd "$srcdir"/daemonize-$pkgver-python2
	python2 setup.py build
}

package_python-daemonize() {
	depends=('python')

	cd daemonize-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-daemonize() {
	depends=('python2')

	cd daemonize-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
