# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=2.0.0
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/jd/daiquiri'
depends=('python-json-logger')
makedepends=('python-pbr')
checkdepends=('python-testrepository' 'python-testtools' 'python-json-logger' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('9220a616ede2f0a672fdd715fb9884b8366696b41e15d21cdfbf42b35ccae75b5b0f46009921591e33ba74d05425d3bdc4211726ce3cc975ce28f61ad7ad5c30')

export PBR_VERSION=$pkgver

build() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py testr
}

package() {
  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
