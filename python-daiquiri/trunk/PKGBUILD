# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=3.2.0
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/Mergifyio/daiquiri'
depends=('python-json-logger')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Mergifyio/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('1fcf2e0457c067309774d2a5e48003179ad04e406469a70e29431db45e401a9ed50f98cfc964402c2fd9063b57fadbe4f0f2ac83a3b01fe65fdc80548c870f29')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd daiquiri-$pkgver
  python setup.py build
}

check() {
  cd daiquiri-$pkgver
  python -m pytest
}

package() {
  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
