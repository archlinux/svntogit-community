# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python-dateutil
pkgname=('python-dateutil' 'python2-dateutil')
pkgver=2.8.0
pkgrel=1
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('BSD' 'Apache')
url="https://github.com/dateutil/dateutil"
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-six' 'python2-six')
checkdepends=('python-pytest' 'python-freezegun' 'python-hypothesis'
              'python2-pytest' 'python2-freezegun' 'python2-hypothesis')
source=("https://files.pythonhosted.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('c89805f6f4d64db21ed966fda138f8a5ed7a4fdbc1a8ee329ce1b74e3c74da9e'
            'SKIP')
validpgpkeys=('6B49ACBADCF6BD1CA20667ABCD54FCE3D964BEFB') # Paul Ganssle <paul@ganssle.io>

prepare() {
  cd "$srcdir"

  cp -r  python-dateutil-$pkgver python2-dateutil-$pkgver
}

build() {
  cd "$srcdir"/python-dateutil-$pkgver
  python setup.py build

  cd "$srcdir"/python2-dateutil-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-dateutil-$pkgver
  pytest

  cd "$srcdir"/python2-dateutil-$pkgver
  pytest2
}

package_python-dateutil() {
depends=('python-six')
  cd "$srcdir"/python-dateutil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-dateutil() {
depends=('python2-six')
  cd "$srcdir"/python2-dateutil-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
