# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-debtcollector
pkgver=2.0.1
pkgrel=1
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='https://docs.openstack.org/developer/debtcollector'
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/debtcollector/archive/$pkgver.tar.gz")
sha512sums=('0c75fbdb682daff6daa2e714657e7d979690fb205aa31576e766497624a9251675f83f71b1d408a662ac61608ad09321f038ef4c5793b881a0978dd2a2a5cfc5')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py build
}

check() {
  cd debtcollector-$pkgver
  stestr run
}

package() {
  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
