# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-debtcollector
pkgname=('python-debtcollector' 'python2-debtcollector')
pkgver=1.22.0
pkgrel=3
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='https://docs.openstack.org/developer/debtcollector'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-wrapt' 'python2-wrapt' 'python2-funcsigs')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/debtcollector/archive/$pkgver.tar.gz")
sha512sums=('bbf6a6cfbf7bfac4e08181e5fd9d661f3c51d9a414ded9f6446c39589103bcddb212b1778bf2fd9bf47123df2481b4c59ef1318f106ca6a77a8c749d4f7d9169')

prepare() {
  cp -a debtcollector-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py build

  cd "$srcdir"/debtcollector-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/debtcollector-$pkgver
  stestr run

  cd "$srcdir"/debtcollector-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-debtcollector() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')

  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-debtcollector() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-wrapt' 'python2-funcsigs')

  cd debtcollector-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
