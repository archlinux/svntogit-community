# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Pierre Chapuis <catwell at archlinux dot us>

pkgname=('python-decorator' 'python2-decorator')
pkgver=4.0.5
pkgrel=1
pkgdesc='Python Decorator module'
arch=('any')
url='http://pypi.python.org/pypi/decorator'
license=('BSD')
makedepends=('python2' 'python' 'python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/d/decorator/decorator-${pkgver}.tar.gz"
        'LICENSE.txt' 'print_unittest.patch')
md5sums=('1dc49f53cede079788fa8db0e1c561df'
         '0ca76d2c707f09dbb04acc425ea1a08b'
         'ce7b145ff6b667a84d7e9004095a9c15')

build() {
  # Make python and python2 builds possible
  cp -r decorator-$pkgver decorator2-$pkgver

  cd "$srcdir/decorator-$pkgver"

  python3 setup.py build

  cd "$srcdir/decorator2-$pkgver"

  python2 setup.py build
}

package_python2-decorator() {
depends=('python2')
replaces=('python-decorator<=3.3.2-1')
  cd "${srcdir}/decorator2-${pkgver}"

  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 "${srcdir}/LICENSE.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
package_python-decorator() {
depends=('python')
  cd "${srcdir}/decorator-${pkgver}"

  python3 setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 "${srcdir}/LICENSE.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

check() {
  cd "$srcdir/decorator-$pkgver"
  patch -Np1 -i $srcdir/print_unittest.patch

  python3 setup.py test

  cd "$srcdir/decorator2-$pkgver"

  python2 setup.py test
}
