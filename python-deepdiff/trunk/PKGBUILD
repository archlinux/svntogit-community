# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deepdiff
pkgver=6.0.0
pkgrel=1
pkgdesc="Deep Difference and Search of any Python object/data."
url="https://github.com/seperman/deepdiff"
license=('MIT')
arch=('any')
depends=('python-ordered-set')
optdepends=('python-click: for cli'
            'python-yaml: for cli'
            'python-toml: for cli'
            'python-clevercsv: for cli')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-clevercsv' 'python-click' 'python-jsonpickle' 'python-numpy'
              'python-toml' 'python-yaml')
source=("https://github.com/seperman/deepdiff/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7a17db64aaf69662f0da09d9bb5f93fe122971d45fdc25bae06ca6ec6b6be3b7df7ef705a0e3ced9aeb2fdf40328afba8b4b6cb55ddbcdba31cf39905bfcc8b7')

build() {
  cd deepdiff-$pkgver
  python setup.py build
}

check() {
  cd deepdiff-$pkgver
  pytest tests
}

package() {
  cd deepdiff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
