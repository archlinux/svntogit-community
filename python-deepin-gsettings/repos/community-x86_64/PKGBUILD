# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=python-deepin-gsettings
pkgname=python2-deepin-gsettings
pkgver=0.1.20131016
_commit=a64de3ac195fe8d5878a07f2e862a058d49ce16d
pkgrel=1
pkgdesc='deepin gsettings python bindings'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/deepin-gsettings"
license=('GPL3')
depends=('python2' 'glib2')
makedepends=('python2-setuptools' 'git')
checkdepends=('gnome-control-center')
provides=('deepin-gsettings')
replaces=('deepin-gsettings')
conflicts=('deepin-gsettings')
source=("git+https://github.com/linuxdeepin/deepin-gsettings.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd deepin-gsettings

  # fix python version
  # find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

  # fix tests by using another key to test, since the old one was deprecated
  sed -e 's|motion-threshold|drag-threshold|' \
      -e 's|idle-dim-battery|idle-dim|g' \
      -i example.py
}

build(){
  cd deepin-gsettings
  python2 setup.py build
}

check() {
  cd deepin-gsettings
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 example.py
}

package() {
  cd deepin-gsettings
  python2 setup.py install --root="$pkgdir" --optimize=1
}
