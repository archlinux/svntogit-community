# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=python-deepin-ui
pkgname=python2-deepin-ui
pkgver=1.0.4
pkgrel=1
pkgdesc='UI toolkit for Linux Deepin'
arch=('any')
url="https://gitcafe.com/Deepin/deepin-ui"
license=('GPL3')
groups=('deepin')
depends=('python2-deepin-utils' 'python2-deepin-gsettings' 'pygtk'
         'python2-scipy' 'glib2' 'python2-cairo' 'python2-pillow'
         'python2-xlib' 'pywebkitgtk' 'libsoup')
makedepends=('deepin-gettext-tools' 'python2-setuptools' 'webkitgtk' 'git')
provides=('deepin-ui')
replaces=('deepin-ui')
conflicts=('deepin-ui')
source=("git+https://github.com/linuxdeepin/deepin-ui.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd deepin-ui

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build(){
  cd deepin-ui
  python2 setup.py build

  deepin-generate-mo dtk/tools/locale_config.ini
}

package_python2-deepin-ui() {
  cd deepin-ui
  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -dm 755 "${pkgdir}"/usr/share/locale
  cp -r dtk/locale/mo/* "${pkgdir}"/usr/share/locale/
}
