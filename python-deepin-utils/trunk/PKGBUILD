# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=python-deepin-utils
pkgname=python2-deepin-utils
pkgver=0.1.20140509
_commit=8aaf2a6f002eeba5506c11c64b25d1404de78744
pkgrel=1
pkgdesc='Basic utils for all project in Linux Deepin'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/deepin-utils"
groups=('deepin')
depends=('pygtk' 'python2-cairo' 'freetype2' 'webkitgtk2'
         'python2-xlib' 'pywebkitgtk')
makedepends=('python2-setuptools' 'git')
license=('GPL3')
provides=('deepin-utils')
replaces=('deepin-utils')
conflicts=('deepin-utils')
source=("git+https://github.com/linuxdeepin/deepin-utils.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd deepin-utils
  python2 setup.py build
}

package_python2-deepin-utils() {
  cd deepin-utils
  python2 setup.py install --root="$pkgdir" --optimize=1
}
