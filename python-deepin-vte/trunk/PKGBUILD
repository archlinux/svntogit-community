# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-deepin-vte
pkgname=python2-deepin-vte
pkgver=0.28.2.20130904
_commit=02cee65aeb952e3298f12be2008fce00b924c32b
pkgrel=1
pkgdesc="Python VTE module patched for deepin"
arch=('i686' 'x86_64')
url=("https://github.com/linuxdeepin/deepin-vte")
license=('GPL')
depends=('pygtk')
makedepends=('git' 'intltool' 'pygobject2-devel' 'gobject-introspection')
source=("git+https://github.com/felixonmars/python-vte.git#commit=$_commit"
        make_alt_work.patch)
md5sums=('SKIP'
         '17da7295eb41faf0a6b74e805165b5ee')

prepare() {
  cd python-vte
  patch -Np1 -i ../make_alt_work.patch
  rm -r doc/reference/html
}

build() {
  cd python-vte
  export CFLAGS="$CFLAGS -fno-strict-aliasing"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=2.0 --disable-gnome-pty-helper
  make
}

package_python2-deepin-vte() {
  cd python-vte
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
