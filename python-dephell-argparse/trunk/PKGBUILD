# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_pkgname=dephell_argparse
pkgname=python-dephell-argparse
pkgver=0.1.2
pkgrel=1
pkgdesc='Argparse with groups, commands, colors, and fuzzy matching'
arch=(any)
license=('MIT')
url='https://github.com/dephell/dephell_argparse'
depends=(python)
makedepends=(python-dephell)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::https://github.com/dephell/dephell_argparse/archive/v.$pkgver.tar.gz)
sha512sums=('8750cfcc484956a16209b1fc539a031a43fb308fa06a90442b54f486fee69ef09da0f5262072260a9b8de2654d45c14aa971a1aaa59093759bf06dd9b65f65a4')

prepare() {
  cd ${_pkgname}-v.${pkgver}
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd ${_pkgname}-v.${pkgver}
  python3 setup.py build
}

check() {
  cd ${_pkgname}-v.${pkgver}
  python -m pytest tests
}

package() {
  cd ${_pkgname}-v.${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
