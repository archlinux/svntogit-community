# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_discover
pkgname=python-dephell-discover
pkgver=0.2.8
pkgrel=2
pkgdesc="Find project modules and data files (packages and package_data for setup.py)"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('70347d511074c19a3facb16cd87f98c7077b76f3dfb6f4fc49e03392a81cb97d')
b2sums=('3e739975095e4358ab8931a24fd7949b6b25faaeaeaf94f3a89f9b81d1eb6d0368c391aa440e9d498d5449cd0155a0524a5e7ca62cfa71239bdc83a60264afda')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
