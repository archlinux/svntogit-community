# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_discover
pkgname=python-dephell-discover
pkgver=0.2.7
pkgrel=1
pkgdesc="Find project modules and data files (packages and package_data for setup.py)"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('ab949ba595feccf39c08f01c2e3e4b5b50f29148c78cfe2fb05394b4bb76d834')
b2sums=('93a00109c83b7cf904ff98f4cbce4c3cc6e88f3f24aacfa42b36a9bbfc6ee470f3862b66b365efe30ab704aeac1d9afc0215d7421d01e53cf8e6285eff0e7fa9')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
