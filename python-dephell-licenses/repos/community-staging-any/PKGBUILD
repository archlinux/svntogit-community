# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell-licenses
pkgname=python-dephell-licenses
pkgver=0.1.6
pkgrel=3
pkgdesc="Manage OSS licenses: retrieve information, generate"
arch=('any')
url="https://github.com/dephell/${_pkgname/-/_}"
license=('MIT')
depends=('python-attrs' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
         dephell-licenses-fix-test.patch)
sha256sums=('e190c8c2bd63a1524943c734ead82877591356585c9fb4f85f80e156b68ca999'
            '74bcf5f1503485138f052ac3af2180da16bca7ebe629f16c049d5033756f271b')
b2sums=('e04ecf71c05220e9b4891028fc7ef061debd3fdf7400366d26ab9bb4e2f4f3b271a150bb55e5073122f01c1223314fa0e57e60cdbe4361d257c5abfbd6d86e7c'
        '8ca7ab788d9159ab03cfabb8673b18608fa5de5aab322ddd70d18558cd70f8d9030e0a60c3b2843f83b4c734805c3b4a77c97e9ffc2b131d5d3e152e79285105')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # fix wrong test
    patch -p1 -i ../dephell-licenses-fix-test.patch
    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
