# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_markers
pkgname=python-dephell-markers
pkgver=1.0.1
pkgrel=6
pkgdesc="Work with environment markers (PEP-496)"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs' 'python-dephell-specifier' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('a9fcf0a4acb59e9fe261d3304a25f33b4b743598f83825888ab0359b75e5e96f')
b2sums=('f5dbc8d8851de24e447f7aa1d1c2022b1cb5ce42ed2ab7319fc7b065ab68de46f8a38cbefe94e46096deca42f7136e9a5a0af8a44c5ce19bcd5808c59e7ba17f')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
