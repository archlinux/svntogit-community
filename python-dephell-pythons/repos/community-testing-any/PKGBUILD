# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_pythons
pkgname=python-dephell-pythons
pkgver=0.1.12
pkgrel=3
pkgdesc="Work with python versions"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs' 'python-dephell-specifier' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('6831c41204d6487dc2a1042ea2dc4ae57dbf36cf58d24b247eb2bf31b310d8a5')
b2sums=('4bf5f8fa9cf0201be9e09ae56fa196c2920959733f976e9cad5e02cb8f3b2638509caa9efdfea285424ee6e45243ff6c5dbdf85cea7d0f8d2eb94980feb33853')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
