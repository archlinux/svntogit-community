# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_pythons
pkgname=python-dephell-pythons
pkgver=0.1.15
pkgrel=3
pkgdesc="Work with python versions"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs' 'python-dephell-specifier' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        py39.patch)
sha256sums=('804c29afa2147322aa23e791f591d0204fd1e9983afa7d91e1d1452fc7be1c5c'
            'f12e9736ef37020e8d2883a450b184922dc5beddd953eeca68fae2d0a335fd3c')
b2sums=('49a892c221b188407938d0254e7336a9d63697acb6106fb82ecbaef2ce7e81b62a1570431ec0a064d58562e359045406ec206bcd0590548f3afa82153b05b06e'
        '3a0ae636ffd37dd3e7b5aeb0d6d9209328606b50e02a1684b021854e609ad59f98805ef665e94b223d52cd3bdb8a08c62eade57539d8a6dd9c19fcc097271ff4')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}
    patch -Np1 -i ../py39.patch
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
