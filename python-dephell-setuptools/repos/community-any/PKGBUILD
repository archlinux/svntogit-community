# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_setuptools
pkgname=python-dephell-setuptools
pkgver=0.2.1
pkgrel=4
pkgdesc="Extract meta information from setup.py"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('bd4d4d063b5ad718bed87b6e1d901feadc60f0122bfdb87f8bb9e124b5a5d5d9')
b2sums=('e1d04abef1508bf5e8d8aa54890b85cd3f4f88ca57c5263a92e42ee07666fbd7ea7032a8e21dc1b7d951c152da508ce0271e071ef65d1cf6297637ebb58c770b')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
