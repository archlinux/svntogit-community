# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_setuptools
pkgname=python-dephell-setuptools
pkgver=0.2.4
pkgrel=2
pkgdesc="Extract meta information from setup.py"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('663629e1ebf7b20bf7e372ee2a2e7ebf1a15aeb3bc6d46ad32e1bcb21044ca29')
b2sums=('db637f00d59e931c9604a5f5755a9987d00771bc601fd3f8af39876320ddaeeea668e08fd66bfba3c61636dfb945e13986f6b4a205952e596701f8fd223b3135')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
