# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_specifier
pkgname=python-dephell-specifier
pkgver=0.1.7
pkgrel=1
pkgdesc="Work with version specifiers"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('5e7235ec357fcd4c6e038b4c1996a9564374d36edb2223bf81f9ac41df3c22e0')
b2sums=('e154a7041b73552a8357fbe371e1aae82b2a254f94b391fe48479391242a172dbe7f770cb4c9305ea833534dfa8f197c9912bda468b6abd26b57d500513d5e9c')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
