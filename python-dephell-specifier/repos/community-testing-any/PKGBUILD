# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_specifier
pkgname=python-dephell-specifier
pkgver=0.2.1
pkgrel=3
pkgdesc="Work with version specifiers"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('c2ec7e62a2406960e19eb6db0f11918d82af64995ee1f26fe00d013791ae9758')
b2sums=('8ede7f342e841f430c6ba58f8c43cae3b6c5e2607fcb71d8cdca1237009ed5022f83de419e59bfc1c94eaec6e8a7be59bd753fcda185bec70cc4f282303002c0')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
