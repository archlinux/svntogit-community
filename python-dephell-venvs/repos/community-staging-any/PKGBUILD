# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_venvs
pkgname=python-dephell-venvs
pkgver=0.1.16
pkgrel=6
pkgdesc="Manage Python virtual environments"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs' 'python-dephell-pythons')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        $pkgname-py39.patch::https://github.com/dephell/dephell_venvs/commit/1dbd550b3481.patch)
sha256sums=('3675c0a905a5c29516937c686713d9387b8f8d3f3243dccec5e203e294c9b549'
            'a186c90984bc7574e3595d691cbc2c9abeca75b17a32b8de8c2b5f344eed5c84')
b2sums=('7e422393772902dacfe8d8eb26257065b39fc76ea0b58f0c057f9290ec911117a2d7c45303091cbe9c61f1c8928b4c5243e7e21cde880b87245756fedeb438d3'
        '52a5dde6e2601ea6ab1501254359d5a3fe6f9e2fba445af790569207579dff284989b3a92bae306f6baa0d750cbd9abdd629c9f12580007908e89678cee880d3')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    patch -Np1 -i ../$pkgname-py39.patch

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
