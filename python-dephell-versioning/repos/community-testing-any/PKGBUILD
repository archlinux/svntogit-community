# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_versioning
pkgname=python-dephell-versioning
pkgver=0.1.1
pkgrel=3
pkgdesc="Bump project version like a pro"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('c438a39662fdf88b00196b56ece4e542db8fa9b16ee5c164a834451240026848')
b2sums=('2574400f4638ef9d3c3484a821f6dfe17a430e4a4d03ba43c7e203012f18fd2bf066649527f72fbaab36c7fdbd9e5e262083dde0ca29699262520083878807d7')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
