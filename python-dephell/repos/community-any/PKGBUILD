# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell
pkgname=python-dephell
pkgver=0.7.8
pkgrel=2
pkgdesc="universal Python project management: convert between formats, build, manage venvs"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('ISC')
_deps=('aiohttp' 'appdirs' 'attrs' 'bowler' 'cerberus' 'dephell-archive' 'dephell-discover'
       'dephell-licenses' 'dephell-links' 'dephell-markers' 'dephell-pythons'
       'dephell-shells' 'dephell-specifier' 'dephell-venvs' 'dephell-versioning'
       'docker' 'dockerpty' 'flatdict' 'html5lib' 'jinja' 'packaging'
       'pip' 'pygments' 'requests' 'ruamel-yaml' 'setuptools' 'tabulate' 'tomlkit' 'yaspin')
depends=("${_deps[@]/#/python-}" 'm2r')
checkdepends=('git' 'python-pytest' 'python-aioresponses' 'python-requests-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "https://github.com/dephell/dephell/commit/d9b3c96472ef95df9cbc1959e1a9dd87a0a9a9bc.patch"
        "0001-Do-not-override-the-system-SSL-certificates-with-the.patch")
sha256sums=('2d85e8788594994ed8403a70b049467682be61a6c2017c5f5c35c98efb836ebb'
            '05d5146bf3fe26790b429ec539f7131bf07e3e36b5545597a5b9cae5db6715d3'
            '4dc2412354c0d96956f88b2e5474652e4a87108588905510d96674ddf939b4c1')
b2sums=('427b35f58553bfe895d2cbc03fd9e4fa22b9e33ecc527b150f757eb0fa21cb8f37c792a2cf1e429a4fa49eb5b41c8edf58180e0741d8e6d6aa82a97f4c20ff56'
        '56971049c6a1c0231334845bbc3cd0949f00fd7c0c8ccd86922d4abce686af2186f1e1e24246359f5f8e394084229b5ddd8900e9001fde4eb2a73b7f0f7c5536'
        'f379265aecef50dd45026f777f68725348083c8fb8aa57e14733883067d52aa1ea39e1affc2f4b84e7574e64695bb84a1c13ca5a0f691e089ae5a9438eedab07')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # bad certifi
    patch -p1 -i ../0001-Do-not-override-the-system-SSL-certificates-with-the.patch
    # backport fix for https://github.com/dephell/dephell/issues/257
    patch -p1 -i ../d9b3c96472ef95df9cbc1959e1a9dd87a0a9a9bc.patch

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # skip git tests, which rely on being run from dephell's own git repo
    python -m pytest -k 'not test_git_git'
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

    # make shell completions
    python -c 'from dephell.actions._autocomplete import make_bash_autocomplete as comp; print(comp())' | \
        install -Dm644 /dev/stdin "${pkgdir}"/usr/share/bash-completion/completions/dephell

    # rewrite zsh completion to support autoloading
    {   printf '#compdef dephell\n'
        python -c 'from dephell.actions._autocomplete import make_zsh_autocomplete as comp; print(comp())' | \
            sed 's/^compdef _dephell dephell$/_dephell/'
    } | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/zsh/site-functions/_dephell
}
