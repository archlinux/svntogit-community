# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell
pkgname=python-dephell
pkgver=0.7.4
pkgrel=1
pkgdesc="universal Python project management: convert between formats, build, manage venvs"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('ISC')
_deps=('aiohttp' 'appdirs' 'attrs' 'cerberus' 'dephell-archive' 'dephell-discover'
       'dephell-licenses' 'dephell-links' 'dephell-markers' 'dephell-pythons'
       'dephell-shells' 'dephell-specifier' 'dephell-venvs' 'jinja' 'packaging'
       'pip' 'requests' 'setuptools' 'tomlkit' 'yaml' 'yaspin')
depends=("${_deps[@]/#/python-}" 'm2r')
checkdepends=('python-pytest' 'python-aioresponses' 'python-requests-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('4e21ccaaa8366c68d7b2351ad9f06cabc381f09031878f8a880698b67885883e')
b2sums=('8e532ae03fc2e841add6b3ea4a864cdfeafb385cdd8c1b72dc23258e9a48a49d8bd194306202807b055e3143ed51b30e84a19d26fcb0d5ccf38bd3ca5025d2cc')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # skip git tests, which rely on being run from dephell's own git repo
    python -m pytest -k 'not test_git_git'
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

    # make shell completions
    python -c 'from dephell.actions._autocomplete import make_bash_autocomplete as comp; print(comp())' | \
        install -Dm644 /dev/stdin "${pkgdir}"/usr/share/bash-completion/completions/dephell

    # rewrite zsh completion to support autoloading
    {   printf '#compdef dephell\n'
        python -c 'from dephell.actions._autocomplete import make_zsh_autocomplete as comp; print(comp())' | \
            sed 's/^compdef _dephell dephell$/_dephell/'
    } | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/zsh/site-functions/_dephell
}
