# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell
pkgname=python-dephell
pkgver=0.8.2
pkgrel=1
pkgdesc="universal Python project management: convert between formats, build, manage venvs"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('ISC')
_d_deps=('archive' 'argparse' 'changelogs' 'discover' 'licenses' 'links' 'markers'
         'pythons' 'setuptools' 'shells' 'specifier' 'venvs' 'versioning')
_deps=('aiohttp' 'appdirs' 'attrs' 'bowler' 'cerberus' 'colorama' "${_d_deps[@]/#/dephell-}"
       'html5lib' 'jinja' 'packaging'
       'pip' 'pygments' 'requests' 'ruamel-yaml' 'tabulate' 'tomlkit' 'yaspin')
depends=("${_deps[@]/#/python-}" 'm2r')
checkdepends=('git' 'python-pytest' 'python-aioresponses' 'python-requests-mock')
optdepends=('python-aiofiles: speed up file writes for files downloaded from warehouse'
            'python-docker: for the docker subcommand'
            'python-dockerpty: for the docker subcommand'
            'python-gnupg: for the package verify subcommand'
            'python-graphviz: print deps tree as a graph'
            'autopep8: make setup.py converter produce formatted pep8 output'
            'yapf: make setup.py converter produce google yapf-formatted output')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "0001-Do-not-override-the-system-SSL-certificates-with-the.patch")
sha256sums=('44896e11545c242b9933ad897468dea0041b84785df41eb39935b7aa931a48a1'
            '1a1157ba7660db1c72e729fd17460a78ed24521e85643ce0097be709bfb48cc5')
b2sums=('0bb41521d221e2f97352b1b44fbeec9c48f06b05b7f359cdc18eba93d89d5b879aed013c81df2dfdfa27c9b1bac952e67b83ec38e0a6a00f7b07ac8242c1d6a3'
        'c5bcd919af73efae8f606231003215af778b326617313e04f9bd0cfd653c20a870363a2fae69c9b23f55bf5a58880c972178b04a13d5e414ab42e35939bc5786')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # bad certifi
    patch -p1 -i ../0001-Do-not-override-the-system-SSL-certificates-with-the.patch

    # don't lock pip version to below pip 20:
    # https://github.com/dephell/dephell/pull/363#issuecomment-606150965
    # because https://github.com/pypa/pip/issues/7629 seems insufficient
    # to hold up non-PyPY platforms :/
    sed -i 's/pip<=19.3.1,>=18.0/pip/' setup.py
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # skip git tests, which rely on being run from dephell's own git repo
    python -m pytest -k 'not test_git_git'
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

    # make shell completions
    python -c 'from dephell.actions._autocomplete import make_bash_autocomplete as comp; print(comp())' | \
        install -Dm644 /dev/stdin "${pkgdir}"/usr/share/bash-completion/completions/dephell

    # rewrite zsh completion to support autoloading
    {   printf '#compdef dephell\n'
        python -c 'from dephell.actions._autocomplete import make_zsh_autocomplete as comp; print(comp())' | \
            sed 's/^compdef _dephell dephell$/_dephell/'
    } | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/zsh/site-functions/_dephell
}
