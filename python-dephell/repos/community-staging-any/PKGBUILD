# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell
pkgname=python-dephell
pkgver=0.7.7
pkgrel=3
pkgdesc="universal Python project management: convert between formats, build, manage venvs"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('ISC')
_deps=('aiohttp' 'appdirs' 'attrs' 'bowler' 'cerberus' 'dephell-archive' 'dephell-discover'
       'dephell-licenses' 'dephell-links' 'dephell-markers' 'dephell-pythons'
       'dephell-shells' 'dephell-specifier' 'dephell-venvs' 'dephell-versioning'
       'docker' 'dockerpty' 'html5lib' 'jinja' 'packaging'
       'pip' 'requests' 'setuptools' 'tomlkit' 'yaml' 'yaspin')
depends=("${_deps[@]/#/python-}" 'm2r')
checkdepends=('git' 'python-pytest' 'python-aioresponses' 'python-requests-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "${url}/pull/250/commits/5cc501da4f3b1873b86d3da606f03503fc79b23f.patch"
        "0001-Do-not-override-the-system-SSL-certificates-with-the.patch")
sha256sums=('cd3beb4d0827360ce78303d71985fb612406e965fd3b65538550315482f3ffc9'
            'b250879d9110558dcb2ad4a649587adaf48038760f562e4b89552dd51adba614'
            '4dc2412354c0d96956f88b2e5474652e4a87108588905510d96674ddf939b4c1')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # fix tests: https://github.com/dephell/dephell/pull/250
    patch -p1 -i ../5cc501da4f3b1873b86d3da606f03503fc79b23f.patch
    # bad certifi
    patch -p1 -i ../0001-Do-not-override-the-system-SSL-certificates-with-the.patch

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # skip git tests, which rely on being run from dephell's own git repo
    python -m pytest -k 'not test_git_git'
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

    # make shell completions
    python -c 'from dephell.actions._autocomplete import make_bash_autocomplete as comp; print(comp())' | \
        install -Dm644 /dev/stdin "${pkgdir}"/usr/share/bash-completion/completions/dephell

    # rewrite zsh completion to support autoloading
    {   printf '#compdef dephell\n'
        python -c 'from dephell.actions._autocomplete import make_zsh_autocomplete as comp; print(comp())' | \
            sed 's/^compdef _dephell dephell$/_dephell/'
    } | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/zsh/site-functions/_dephell
}
