# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell
pkgname=python-dephell
pkgver=0.8.0
pkgrel=1
pkgdesc="universal Python project management: convert between formats, build, manage venvs"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('ISC')
_deps=('aiohttp' 'appdirs' 'attrs' 'bowler' 'cerberus' 'dephell-archive' 'dephell-argparse' 'dephell-discover'
       'dephell-licenses' 'dephell-links' 'dephell-markers' 'dephell-pythons' 'dephell-setuptools'
       'dephell-shells' 'dephell-specifier' 'dephell-venvs' 'dephell-versioning'
       'docker' 'dockerpty' 'flatdict' 'html5lib' 'jinja' 'packaging'
       'pip' 'pygments' 'requests' 'ruamel-yaml' 'setuptools' 'tabulate' 'tomlkit' 'yaspin')
depends=("${_deps[@]/#/python-}" 'm2r')
checkdepends=('git' 'python-pytest' 'python-aioresponses' 'python-requests-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "0001-Do-not-override-the-system-SSL-certificates-with-the.patch")
sha256sums=('2ba41eb46c2915d6c4b2c0cf5ae90a6cc553b3754710b38cbe011f588bd8beb5'
            '4dc2412354c0d96956f88b2e5474652e4a87108588905510d96674ddf939b4c1')
b2sums=('e57b3b13d82ba755485a9b5754472fc46221423cfc8d984bac6c580d93202ff0ef39d2f4cc6a900a86f0a28fc12dbc23eb6354ac9a61517f9c33085c330ba3e4'
        'f379265aecef50dd45026f777f68725348083c8fb8aa57e14733883067d52aa1ea39e1affc2f4b84e7574e64695bb84a1c13ca5a0f691e089ae5a9438eedab07')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # bad certifi
    patch -p1 -i ../0001-Do-not-override-the-system-SSL-certificates-with-the.patch

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # skip git tests, which rely on being run from dephell's own git repo
    python -m pytest -k 'not test_git_git'
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

    # make shell completions
    python -c 'from dephell.actions._autocomplete import make_bash_autocomplete as comp; print(comp())' | \
        install -Dm644 /dev/stdin "${pkgdir}"/usr/share/bash-completion/completions/dephell

    # rewrite zsh completion to support autoloading
    {   printf '#compdef dephell\n'
        python -c 'from dephell.actions._autocomplete import make_zsh_autocomplete as comp; print(comp())' | \
            sed 's/^compdef _dephell dephell$/_dephell/'
    } | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/zsh/site-functions/_dephell
}
