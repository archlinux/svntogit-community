# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-deprecation
pkgname=('python-deprecation' 'python2-deprecation')
pkgver=1.0.1
pkgrel=1
pkgdesc="A library to handle automated deprecations"
arch=('any')
license=('Apache')
url="http://deprecation.readthedocs.io"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-unittest2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/briancurtin/deprecation/archive/$pkgver.tar.gz")
sha512sums=('522ef7daca49f3174f54f53d3405ed81dc20ab4292d74a2fc7fffbbdc9f7f05610a201a6698f0ac9172d64e37927b2c2e23a1bf475b5b5291dd9bcc60484161b')

prepare() {
  cp -a deprecation-$pkgver{,-py2}

  sed -i 's/unittest2/unittest/g' deprecation-$pkgver/tests/*.py

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/deprecation-$pkgver
  python setup.py build

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/deprecation-$pkgver
  python -m unittest discover

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 -m unittest discover
}

package_python-deprecation() {
  depends=('python')

  cd deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-deprecation() {
  depends=('python2')

  cd deprecation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
