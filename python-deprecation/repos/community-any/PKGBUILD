# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-deprecation
pkgver=2.0.7
pkgrel=2
pkgdesc="A library to handle automated deprecations"
arch=('any')
license=('Apache')
url="https://deprecation.readthedocs.io"
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/d/deprecation/deprecation-$pkgver.tar.gz")
sha512sums=('4412e2e95f4aa4b3d5598346a6649c8d945d7401ac2d0fba3b6f6eef567403c8454ebe537e8de646903cd0c2308f48ecb949dae3015ba7617edd2fa3a26ed429')

prepare() {
  sed -i 's/unittest2/unittest/g' deprecation-$pkgver/tests/*.py

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd deprecation-$pkgver
  python setup.py build
}

check() {
  cd deprecation-$pkgver
  python -m unittest discover
}

package() {
  cd deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
