# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=deprecation
pkgbase=python-deprecation
pkgname=('python-deprecation' 'python2-deprecation')
pkgver=2.0.7
pkgrel=1
pkgdesc="A library to handle automated deprecations"
arch=('any')
license=('Apache')
url="https://deprecation.readthedocs.io"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-unittest2')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4412e2e95f4aa4b3d5598346a6649c8d945d7401ac2d0fba3b6f6eef567403c8454ebe537e8de646903cd0c2308f48ecb949dae3015ba7617edd2fa3a26ed429')

prepare() {
  cp -a deprecation-$pkgver{,-py2}

  sed -i 's/unittest2/unittest/g' deprecation-$pkgver/tests/*.py

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/deprecation-$pkgver
  python setup.py build

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/deprecation-$pkgver
  python -m unittest discover

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 -m unittest discover
}

package_python-deprecation() {
  depends=('python')

  cd deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-deprecation() {
  depends=('python2')

  cd deprecation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
