# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=deprecation
pkgbase=python-deprecation
pkgname=('python-deprecation' 'python2-deprecation')
pkgver=2.0.6
pkgrel=1
pkgdesc="A library to handle automated deprecations"
arch=('any')
license=('Apache')
url="https://deprecation.readthedocs.io"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-unittest2')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('69dcf5e07cfc1c746564e03063088cce65690e2b34cffe327d59edad24f00d23cf9904089ab9ff5e67dce2b6b02ed617e53959be4a907074440ddb778576aa2f'
            'SKIP')
validpgpkeys=('E62866A3562575D95B570AF58EB71A70831B56D1') # Brian Curtin (brian.curtin) <brian@python.org>

prepare() {
  cp -a deprecation-$pkgver{,-py2}

  sed -i 's/unittest2/unittest/g' deprecation-$pkgver/tests/*.py

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/deprecation-$pkgver
  python setup.py build

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/deprecation-$pkgver
  python -m unittest discover

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 -m unittest discover
}

package_python-deprecation() {
  depends=('python')

  cd deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-deprecation() {
  depends=('python2')

  cd deprecation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
