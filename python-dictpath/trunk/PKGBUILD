# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-dictpath
_pipname=dictpath
pkgver=0.1.3
pkgrel=4
pkgdesc='Object-oriented dictionary paths'
arch=(any)
url='https://github.com/p1c2u/dictpath'
license=(BSD)
depends=(python-six)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest-cov python-pytest-flake8)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('751cde3b76b176d25f961b90c423a11a4d5ede9bd09ab0d64a85abb738c190d8')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  pytest -vv
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
