# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-digitalocean
pkgname=('python-digitalocean' 'python2-digitalocean')
pkgver=1.13.2
pkgrel=2
pkgdesc='digitalocean.com API to manage Droplets and Images'
arch=('any')
license=('LGPL')
url='https://github.com/koalalorenzo/python-digitalocean'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-jsonpickle' 'python2-jsonpickle')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-responses' 'python2-responses')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/koalalorenzo/python-digitalocean/archive/v$pkgver.tar.gz")
sha512sums=('6ccc559d755c96b84c311b8f8798388e7e6c1f4e344568093f5c81902c703b9b05e6a143a59c5935d049e407ec11df326e0cf2192524f74829857c1013a12bbc')

prepare() {
  cp -a python-digitalocean-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-digitalocean-$pkgver
  python setup.py build

  cd "$srcdir"/python-digitalocean-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-digitalocean-$pkgver
  python setup.py pytest

  cd "$srcdir"/python-digitalocean-$pkgver-py2
  python2 setup.py pytest
}

package_python-digitalocean() {
  depends=('python-requests' 'python-jsonpickle')

  cd python-digitalocean-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-digitalocean() {
  depends=('python2-requests' 'python2-jsonpickle')

  cd python-digitalocean-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
