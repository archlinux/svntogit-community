# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Guillaume Hayot <ghayot@postblue.info>
# Contributor: Arvedui <arvedui@posteo.de>
# Contributor: Marc Plano-Lesay <marc.planolesay@gmail.com>
# Contributor: Joost Bremmer <toost.b@gmail.com>

pkgname=python-discogs-client
pkgver=2.6
pkgrel=2
pkgdesc='Python Client for the Discogs API'
arch=(any)
url=https://github.com/joalla/discogs_client
license=(BSD)
depends=(
  python-dateutil
  python-six
  python-requests
  python-oauthlib
)
makedepends=(
  git
  python-setuptools
)
_tag=25eb053a7351bafc9b05ea5948378ecceb22f744
source=(git+https://github.com/joalla/discogs_client.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd discogs_client
  git describe --tags | sed 's/^v//'
}

build() {
  cd discogs_client
  python setup.py build
}

package() {
  cd discogs_client
  python setup.py install --root=$pkgdir --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-discogs-client
}

# vim: set ts=2 sw=2 et:
