# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=distributed
pkgname=python-${_pkg}
pkgver=2022.03.0
pkgrel=1
pkgdesc="Distributed task scheduler for Dask"
arch=(x86_64)
url="https://distributed.dask.org/"
license=(BSD)
depends=(
    python
    python-click
    python-cloudpickle
    python-dask
    python-jinja
    python-msgpack
    python-packaging
    python-psutil
    python-sortedcontainers
    python-tblib
    python-toolz
    python-tornado
    python-yaml
    python-zict
)
makedepends=(cython)
checkdepends=(ipython
    python-pytest
    python-pytest-asyncio
    python-pytest-repeat
    python-pytest-timeout
    python-flaky
    python-blosc
    python-cryptography
    python-distributed
    python-fsspec
    python-h5py
    python-ipykernel
    python-ipywidgets
    python-joblib
    python-jsonschema
    python-jupyter_client
    python-lz4
    python-netcdf4
    python-numpy
    python-pandas
    python-paramiko
    python-prometheus_client
    python-pytorch
    python-requests
    python-scipy
    python-snappy
    python-zstandard
)
# No tests in PyPi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(https://github.com/dask/distributed/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('615df296e593bc636ed584c6b13ce2f05f29af8aac74d398993da2e81fd164b7')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build --with-cython
}

# Timing out, no time to investigate
#check() {
#  cd ${_pkg}-${pkgver}
#  pytest distributed -vvrsfE -m "not avoid_ci" --color=yes
#}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build --with-cython
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
