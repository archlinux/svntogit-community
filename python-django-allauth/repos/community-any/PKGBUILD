# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.46.0
pkgrel=1
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=('any')
url="https://github.com/pennersr/django-allauth"
license=('MIT')
depends=('python-django' 'python-openid' 'python-requests'
'python-requests-oauthlib' 'python-pyjwt')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=(
  "${_name}-${pkgver}.tar.gz::https://github.com/pennersr/${_name}/archive/${pkgver}.tar.gz"
  "${pkgname}-0.46.0-python-pyjwt-2.2.0.patch"
)
sha512sums=('cb7f2195861fcd54cb8f9aa1b0af3915dc038a3cf4fff3d0e0c74ee5602f4dbdc386a9583a1d3814d398fa5c67c888aa3a2d1ae9525581ff91814cad023d4e17'
            '57671f180534c7372534077f0ae74de23afd99ecb8e39767fb65134133c88863e6f6da64ca3e62ed91880a811b235ec79f7e7bab30da4a4e70c61d949d2a34b3')
b2sums=('67fc03fa8e2377bc87a9a52cd4f51cb33be3d91ba9079a82150ff15059efd1e74a49b3b4c17d879a0528a287e29d8747fbc9e243c865c0e3ee9c81afe0ecaa3f'
        '18c4749d2ba8334b4ba060499a42c15fe0ad925ec4480fece7a934302a05abf0a44c3674f472ea953dc31323e30216b036603b72dff2c0ceb4f4ab7be6e10448')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"

  cd "$pkgname-$pkgver"
  # remove unused jwt.decode() parameter
  # https://github.com/pennersr/django-allauth/issues/2977
  patch -Np1 -i ../"${pkgname}-0.46.0-python-pyjwt-2.2.0.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python manage.py test allauth
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
