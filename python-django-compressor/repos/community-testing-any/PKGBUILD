# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django_compressor
pkgname=python-django-compressor
pkgver=2.4.1
pkgrel=3
pkgdesc="Compresses linked and inline javascript or CSS into a single cached file"
arch=('any')
url="https://github.com/django-compressor/django-compressor"
license=('Apache' 'BSD' 'MIT')
depends=('python-django-appconf' 'python-rcssmin' 'python-rjsmin' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-beautifulsoup4' 'python-brotli' 'python-calmjs'
'python-coverage' 'python-csscompressor' 'python-django-classy-tags'
'python-django-sekizai' 'python-html5lib' 'python-jinja' 'python-lxml'
'python-mock' 'slimit')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d59fc8b97f9f87b26e53a9822a9b467df77449cc480a7145d7fedf020a85516dee6f3bc04519fc2e2b751134fb10ce4ddbd4ffe394b5817b973db8d90b9ccda7')
b2sums=('455938c328c787e6ca48b3c413f005f9ba3cfc186587b821afb804c0de6259702060d4493e34f240bd721e547ce496a1b0de7a478eb8c9b7ac8e4ba49be755fa')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  sed -e 's/rcssmin ==/rcssmin >=/' \
      -e 's/rjsmin ==/rjsmin >=/' \
      -i setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

# # tests are broken because of python-beautifulsoup4:
# # https://github.com/django-compressor/django-compressor/issues/998
# check() {
#   cd "$pkgname-$pkgver"
#   export PYTHONPATH="build:${PYTHONPATH}"
#   django-admin test --settings=compressor.test_settings compressor
# }

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
