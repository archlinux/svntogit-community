# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-crispy-forms
pkgname=python-django-crispy-forms
pkgver=1.13.0
pkgrel=1
pkgdesc="The best way to have DRY Django forms"
arch=('any')
url="https://github.com/django-crispy-forms/django-crispy-forms"
license=('MIT')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('a52709470df3e4f32671cfa488135cea8947f726f6874bc2943f4c4f132c08d01fb6bc18a1b78e2756e381815d80c140c5e99c4db8ea1dffe7943b0ed1c03ecf')
b2sums=('6efe657d897a4178683e79c1548cc598d59e95bddf4092bf3c3790e28d67e5f0c2d860574f14fd41104e737751197adc0f100fcf93f18d8129e2768ccb4c08df')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=crispy_forms.tests.test_settings pytest -v crispy_forms/tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {CONTRIBUTORS.txt,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
