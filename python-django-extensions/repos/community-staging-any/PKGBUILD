# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Audet <ichimonji10@gmail.com>
# Contributor: Dominik Kozaczko <dominik@kozaczko.info>
# Contributor: Alper Kanat <alperkanat@raptiye.org>
# Contributor: Apkawa <apkawa@gmail.com>
# Contributor: Schnouki <thomas.jost@gmail.com>

pkgbase=python-django-extensions
pkgname=('python-django-extensions' 'python2-django-extensions')
pkgver=2.0.7
pkgrel=2
pkgdesc='A collection of custom extensions for the Django Framework'
arch=('any')
url='http://github.com/django-extensions/django-extensions'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('graphviz: graph_models extension')
source=("django-extensions-${pkgver}.tar.gz::https://github.com/django-extensions/django-extensions/archive/${pkgver}.tar.gz")
sha256sums=('4430da44bfef4130ba7672e613e12333fe865e34656f8be2dd67fb0ccac584bc')

prepare() {
  cp -r {,python-}django-extensions-${pkgver}
  mv {,python2-}django-extensions-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-django-extensions-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-django-extensions() {
  depends=('python-django' 'python-six')
  optdepends+=('python-werkzeug: runserver_plus extension'
               'ipython: shell_plus extension')

  cd python-django-extensions-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-django-extensions/
}

package_python2-django-extensions() {
  depends=('python2-django' 'python2-six')
  optdepends+=('python2-werkzeug: runserver_plus extension'
               'ipython2: shell_plus extensions')

  cd python2-django-extensions-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python2-django-extensions/
}

# vim: ts=2 sw=2 et:
