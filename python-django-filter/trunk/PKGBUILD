# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-filter
pkgname=python-django-filter
pkgver=22.1
pkgrel=1
pkgdesc="A generic system for filtering Django QuerySets based on user selections"
arch=(any)
url="https://github.com/carltongibson/django-filter"
license=(BSD)
depends=(python-django)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-django-crispy-forms python-django-rest-framework
python-mock python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('0b92186cc6831351b22a7d0d8c447ec3fbd25db03402b47cdae650c8e4f9a80f384c49540bcacf1f15bfe07f27b77e0950d9ec69fab4b839f23a89486d611be9')
b2sums=('3dba7579d527ee742cc89698a8c0812f12b82c807a02060d7acd7b280dc748f5bf4eada60ace1ebac2153bc6058ef3f73cce99de173e4e356b7ff19c84c4e421')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python runtests.py
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,{CHANGES,README}.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
