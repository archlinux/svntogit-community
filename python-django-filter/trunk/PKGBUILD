# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-filter
pkgname=python-django-filter
pkgver=21.1
pkgrel=1
pkgdesc="A generic system for filtering Django QuerySets based on user selections"
arch=('any')
url="https://github.com/carltongibson/django-filter"
license=('BSD')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-django-crispy-forms' 'python-django-rest-framework'
'python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('df2b02bfa93874d9684a26394778203b827c6459ab8cc8c581677e6ccfeedf1323085f53fb52cfc3e0e0a3d27f86de655366bcd4be361502ce86e65b4a4e84d0')
b2sums=('f0ae3119a1dfa466e6630f7da44db1531172991b137fe9420ed533a7b3d4177f689f07fed115566854bd40e5e23f4e9f2f73be32188af2c067a81a95fbccc5a3')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python runtests.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,{CHANGES,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
