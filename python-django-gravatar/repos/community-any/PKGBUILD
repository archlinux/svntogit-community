# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-gravatar2
pkgname=python-django-gravatar
pkgver=1.4.3
pkgrel=1
pkgdesc="Essential Gravatar support for Django"
arch=('any')
url="https://github.com/twaddington/django-gravatar"
license=('MIT')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f95e3ddf39ccb6782a26d04fb22cded935735403684964f79c6fec3047b613c5ee39cafbcecd049a4207ada065c66a9ae9dc622c71852f0e57983004aace136c')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python example_project/manage.py test --verbosity=2 django_gravatar
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
