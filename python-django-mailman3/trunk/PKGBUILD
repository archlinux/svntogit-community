# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-mailman3
pkgname=python-django-mailman3
pkgver=1.3.7
pkgrel=1
pkgdesc="Libraries and templates to Django-based interfaces interacting with Mailman"
arch=('any')
url="https://gitlab.com/mailman/django-mailman3"
license=('GPL3')
depends=('python-django' 'python-django-allauth' 'python-django-gravatar'
'python-mailmanclient' 'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('923ca67cc813f59ee9e84b639c4a0b2cfe5b39b63a1a19c5b28c7727540a0ca1f1ee9ecfa12d91a314ee3d7cdb3535c939b9a240281ba277b9af276f18be28e8'
            'SKIP')
b2sums=('cabaabd9514b28caf571af56b966b37893d582b3fc61b945096dd4b898635b321b239d88082c496f9b972a42bd773354e081e397418bc07c219596e7f9e0edda'
        'SKIP')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  django-admin.py test --settings=django_mailman3.tests.settings_test django_mailman3
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
