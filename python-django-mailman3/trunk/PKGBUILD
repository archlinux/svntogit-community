# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-mailman3
pkgname=python-django-mailman3
pkgver=1.3.4
pkgrel=3
pkgdesc="Libraries and templates to Django-based interfaces interacting with Mailman"
arch=('any')
url="https://gitlab.com/mailman/django-mailman3"
license=('GPL3')
depends=('python-django' 'python-django-allauth' 'python-django-gravatar'
'python-mailmanclient' 'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('0b28636caec452efeda2b1b44f1d5ad44740ab0c3ea7b03d7d2e6bf420b1a788d5858e5c827824ef6baf2a463232ec53f404d3de0c22cf6ee17065236f7fcd91'
            'SKIP')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  django-admin.py test --settings=django_mailman3.tests.settings_test django_mailman3
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
