# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=python-django-modeltranslation
pkgname=('python-django-modeltranslation' 'python2-django-modeltranslation')
pkgver=0.12.2
pkgrel=2
pkgdesc='Translates Django models using a registration approach'
arch=('any')
url='https://github.com/deschler/django-modeltranslation'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("django-modeltranslation-${pkgver}.tar.gz::https://github.com/deschler/django-modeltranslation/archive/${pkgver}.tar.gz")
sha256sums=('cc17012514941689f62de943bb06c91bd96fe994b7548a80f504aa8f20f01a41')

prepare() {
  cp -r {,python-}django-modeltranslation-${pkgver}
  mv {,python2-}django-modeltranslation-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-django-modeltranslation-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-django-modeltranslation() {
  depends=('python-django')

  cd python-django-modeltranslation-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python-django-modeltranslation/
}

package_python2-django-modeltranslation() {
  depends=('python2-django')

  cd python2-django-modeltranslation-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python2-django-modeltranslation/
}

# vim: ts=2 sw=2 et:
