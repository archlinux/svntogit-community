# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-dkim
pkgver=0.9.1
_branch=${pkgver%.*}
pkgrel=1
pkgdesc='Python library that implements DKIM and ARC email signing and verification'
arch=('any')
url=https://launchpad.net/dkimpy
license=('custom')
depends=('python-authres' 'python-dnspython' 'python-pynacl' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/d/dkimpy/dkimpy-$pkgver.tar.gz"
        'LICENSE-dkimpy-0.9.0::https://git.launchpad.net/dkimpy/plain/LICENSE?id=0.9.0')
sha512sums=('5667c5b7752081f5831c26eebdf5d05bec8ff6c82299506e6ca31603ba0d04aa37b908aa07e97877f298263aeb652fdc0b8d54330a263902033dc0647dc44c3f'
            '20a36385eabb3f758fee79d66033db77601ee352ea6f28c16395b1a7798e7804e1e9c3bc6326d939ece8f30e047d4ac95df25755f00a69e95de5f7528c16d5dd')

build() {
  cd dkimpy-$pkgver
  python setup.py build
}

check() {
  cd dkimpy-$pkgver
  python -m unittest dkim.tests.test_suite
}

package() {
  cd dkimpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 ../LICENSE-dkimpy-0.9.0 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
