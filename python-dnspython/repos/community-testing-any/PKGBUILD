# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Mathijs Kadijk <maccain13@gmail.com>

_name=dnspython
pkgname=python-dnspython
# IMPORTANT: stay on 1.16 for https://github.com/eventlet/eventlet/issues/619
pkgver=1.16.0
pkgrel=2
epoch=1
pkgdesc="A DNS toolkit for Python"
arch=('any')
url="http://www.dnspython.org"
license=('ISC')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-idna' 'python-pycryptodome' 'python-ecdsa') # 'python-trio')
optdepends=('python-ecdsa: DNSSEC support'
            'python-pycryptodome: DNSSEC support'
            'python-idna: support for updated IDNA 2008')
#            'python-trio: async support'
#            'python-sniffio: async support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.zip")
sha256sums=('36c5e8e38d4369a08b6780b7f27d790a292b2b08eea01607865bf0936c558e01')
validpgpkeys=('A580DEE052FEC78D8ACF383DF24B3AFC8CA2F5C7') # Bob Halley <halley@dnspython.org>

build() {
    cd dnspython-${pkgver}

    python setup.py build
}

check() {
    cd dnspython-${pkgver}

    python setup.py test
}

package() {
    cd dnspython-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
