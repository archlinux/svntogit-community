# $Id$
# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hideaki Takahashi <mymelo+aur@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.pm>

pkgbase=python-docker-pycreds
pkgname=(python-docker-pycreds python2-docker-pycreds)
pkgver=0.3.0
pkgrel=2
pkgdesc="Python bindings for the docker credentials store API"
arch=('any')
license=('Apache')
url="https://github.com/shin-/dockerpy-creds"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'flake8' 'python2-flake8'
              'python-pytest-cov' 'python2-pytest-cov')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/shin-/dockerpy-creds/archive/$pkgver.tar.gz")
sha512sums=('daff736677f46c1aceb87090bd6a248b5e1537907dcf7afba735274ce100aaee35a39958aeb7d6d18d7de66d9e8f5bf168cd9d9dc14f4f59485c3c1bc0d9adbf')

prepare() {
  sed -i 's/==.*$//' dockerpy-creds-$pkgver/test-requirements.txt

  cp -a dockerpy-creds-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/dockerpy-creds-$pkgver
  python setup.py build

  cd "$srcdir"/dockerpy-creds-$pkgver-py2
  python2 setup.py build
}

# We don't have docker-credential-secretservice
check_disabled() {
  cd "$srcdir"/dockerpy-creds-$pkgver
  python setup.py pytest

  cd "$srcdir"/dockerpy-creds-$pkgver-py2
  python2 setup.py pytest
}

package_python-docker-pycreds() {
  depends=('python-six')

  cd dockerpy-creds-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-docker-pycreds() {
  depends=('python2-six')

  cd dockerpy-creds-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
