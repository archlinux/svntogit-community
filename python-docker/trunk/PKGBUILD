# $Id$
# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hideaki Takahashi <mymelo+aur@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgbase=python-docker
pkgname=(python-docker python2-docker)
pkgver=3.5.0
pkgrel=1
pkgdesc="A Python library for the Docker Engine API"
arch=('any')
url="https://github.com/docker/docker-py"
license=("Apache")
makedepends=('python-pip' 'python2-pip' 'python-requests' 'python2-requests' 'python-six'
             'python2-six' 'python-websocket-client' 'python2-websocket-client'
             'python2-backports.ssl_match_hostname' 'python2-ipaddress' 'python-docker-pycreds'
             'python2-docker-pycreds')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-mock' 'python2-mock'
              'flake8' 'python2-flake8')
checkdepends_x86_64=('docker')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/docker/docker-py/archive/$pkgver.tar.gz")
sha512sums=('c5ebc99df66d49f53c39ff897bd95afcbb92ced3ec6fd61148b6775cddf9e830454b1da5ac79ea0bba76f143f35f29dec66e7db5c6df33ca5157d3fe40cd4e93')

prepare() {
  sed -i 's/==.*$//' docker-py-$pkgver/{,test-}requirements.txt

  cp -a docker-py-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/docker-py-$pkgver
  python setup.py build

  cd "$srcdir"/docker-py-$pkgver-py2
  python2 setup.py build
}

check() {
  # Skipping integration tests as docker needs root to run:
  # docker daemon -H fd:// -p "$srcdir/docker.pid"

  cd "$srcdir"/docker-py-$pkgver
  py.test --cov=docker tests/unit

  cd "$srcdir"/docker-py-$pkgver-py2
  py.test2 --cov=docker tests/unit

  # kill $(<"$srcdir/docker.pid")
}

package_python-docker() {
  depends=('python-requests' 'python-six' 'python-websocket-client' 'python-docker-pycreds')
  provides=('python-docker-py')
  conflicts=('python-docker-py')
  replaces=('python-docker-py')

  cd docker-py-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-docker() {
  depends=('python2-requests' 'python2-six' 'python2-websocket-client' 'python2-docker-pycreds'
           'python2-backports.ssl_match_hostname' 'python2-ipaddress')
  provides=('python2-docker-py')
  conflicts=('python2-docker-py')
  replaces=('python2-docker-py')

  cd docker-py-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
}
