# $Id$
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor : Rohan Dhruva (rohandhruva at gmail dot com)

pkgname=python-docs
pkgver=3.6.6
pkgrel=1
pkgdesc="Set of HTML documentation for python"
arch=('any')
url="http://docs.python.org/py3k/"
license=('GPL')
options=('docs')
source=(https://sources.archlinux.org/other/community/python-docs/python-$pkgver-docs-html.tar.bz2)
sha256sums=('5db928eb9d0cbdebaf11b62f3df3a8473bf7e8e9eecad92b4e4364f993502694')

package() {
  mkdir -p "$pkgdir"/usr/share/doc/python/html
  cp -rf "$srcdir"/python-$pkgver-docs-html/* "$pkgdir"/usr/share/doc/python/html/
  find "$pkgdir"/usr/share/doc/python/html/ -type f -exec chmod 0644 {} \;
  find "$pkgdir"/usr/share/doc/python/html/ -type d -exec chmod 0755 {} \;
  install -dm0755 "$pkgdir"/etc/profile.d
  echo "export PYTHONDOCS=/usr/share/doc/python/html/library" >"$pkgdir"/etc/profile.d/$pkgname.sh
  echo "setenv PYTHONDOCS /usr/share/doc/python/html/library" >"$pkgdir"/etc/profile.d/$pkgname.csh
}
