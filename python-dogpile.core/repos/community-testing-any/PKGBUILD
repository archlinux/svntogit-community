# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sibren Vasse <arch at sibrenvasse.nl>
# Contributor: Quentin Stievenart <acieroid@awesom.eu>

pkgbase=python-dogpile.core
pkgname=(python-dogpile.core python2-dogpile.core)
_pypiname=dogpile.core
pkgver=0.4.1
pkgrel=4
pkgdesc="A 'dogpile' lock, typically used as a component of a larger caching solution"
arch=('any')
url="http://pypi.python.org/pypi/dogpile.core"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("http://pypi.python.org/packages/source/d/$_pypiname/$_pypiname-$pkgver.tar.gz")
md5sums=('01cb19f52bba3e95c9b560f39341f045')

prepare() {
  cp -a "$_pypiname-$pkgver"{,-py2}
}

build() {
  cd $_pypiname-$pkgver
  python setup.py build

  cd ../$_pypiname-$pkgver-py2
  python2 setup.py build
}

check() {
  cd $_pypiname-$pkgver
  nosetests3

  cd ../$_pypiname-$pkgver-py2
  nosetests2
}

package_python-dogpile.core() {
  depends=('python')

  cd "$srcdir/$_pypiname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-dogpile.core() {
  depends=('python2')

  cd "$srcdir/$_pypiname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
