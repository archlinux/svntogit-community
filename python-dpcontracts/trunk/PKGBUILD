# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-dpcontracts
pkgver=0.6.0
pkgrel=1
pkgdesc="A simple implementation of contracts for Python"
url="https://github.com/deadpixi/contracts"
license=('LGPL')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/d/dpcontracts/dpcontracts-$pkgver.tar.gz"
        https://github.com/deadpixi/contracts/commit/a70f208959542cfb5e599d41cdc16da510179a55.patch)
sha512sums=('451b541c87ee2ccad3ab18f2328ffffff0fd422e137af8227b7c264f7b91b4270de077f79596008f746a37aef1e5762278b209b8923a6126a74a3b32225d4dd5'
            'd3022db1dd519b1b0fa45994259e50696cde726926412551b0eed7d833466d7c7d9e23da09c53af3f2e18efa9950bd3e1ecc9e39a02421fe47453bfa5139f830')

prepare() {
  cd dpcontracts-$pkgver
  patch -p1 -i ../a70f208959542cfb5e599d41cdc16da510179a55.patch || :
}

build() {
  cd dpcontracts-$pkgver
  python setup.py build
}

check() {
  cd dpcontracts-$pkgver
  python setup.py pytest --addopts README.rst
}

package() {
  cd dpcontracts-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
