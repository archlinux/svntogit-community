# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Tim Diels <tim@timdiels.be>

pkgbase=python-dropbox
_pkgname=dropbox-sdk-python
pkgname=(python-dropbox python2-dropbox)
pkgver=9.4.0
pkgrel=2
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://www.dropbox.com/developers/core/sdks/python"
arch=('any')
license=('MIT')
makedepends=(python-requests python2-requests python-urllib3 python2-urllib3
             python-setuptools python2-setuptools python-pytest-runner python2-pytest-runner)
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('977212f9d41221509b80ac91935e406659e31f3611e80330cea7122a351dc758573ba9fad7b3cd7a95fea3839514f99e02223fb0e25ab18d052f5af378eddbf2')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
  python2 setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd "$srcdir/$_pkgname-$pkgver"
#
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#  python2 setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#}

package_python-dropbox() {
  depends=(python python-urllib3 python-requests)

  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-dropbox() {
  depends=(python2 python2-urllib3 python2-requests)

  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
