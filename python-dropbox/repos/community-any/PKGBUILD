# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Andrey Mivrenik <gim at fastmail dot fm>
# Contributor: Tim Diels <tim@timdiels.be>

pkgname=python-dropbox
_pkgname=dropbox-sdk-python
pkgver=11.22.0
pkgrel=1
pkgdesc="Python SDK for Dropbox Core APIs"
url="https://github.com/dropbox/dropbox-sdk-python"
arch=('any')
license=('MIT')
depends=(python python-urllib3 python-requests)
makedepends=(python-setuptools python-pytest-runner)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dropbox/$_pkgname/archive/v$pkgver.tar.gz")
sha512sums=('b403a49a1fef084e1cafaa41e289bfabc5c414a086c0c6058e57c9f7610d52590b513ea929307312d518f5a850c43c18c8bdc850670a5e27b1f9ae12d0d925a9')

prepare() {
  sed -e 's|pytest-runner == 5.2.0|pytest-runner|' -i $_pkgname-$pkgver/setup.py # Unversion dependency
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py build
}

# requires API token in $DROPBOX_TOKEN
#check() {
#  cd "$srcdir/$_pkgname-$pkgver"
#
#  python setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#  python2 setup.py pytest --addopts "-k 'not TestDropboxTeam'"
#}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
