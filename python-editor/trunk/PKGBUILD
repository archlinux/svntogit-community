# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michael Straube <straubem@gmx.de>
# Contributor: Kevin Brubeck Unhammer <unhammer@fsfe.org>

pkgbase=python-editor
pkgname=('python-editor' 'python2-editor')
pkgver=1.0.4
pkgrel=1
pkgdesc='Programmatically open an editor, capture the result'
url='https://github.com/fmoo/python-editor'
arch=('any')
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/fmoo/python-editor/archive/${pkgver}.tar.gz)
sha256sums=('61a4758919668b8ffe5eb389a86a4c2e8c2f789c02b9c6aee0ddf3c4ebfe3fd3')
sha512sums=('2a01ffa5967be31ec425ddb9cd2c57f327b61547f011e1510005a7b6cab30ba01fe81e18cb38e7c4f39796350701997d11a5059568b987355878671a5299eaf5')

prepare() {
  cp -a ${pkgbase}-${pkgver}{,-py2}
  sed 's|python|python2|' -i ${pkgbase}-${pkgver}-py2/editor.py
}

build() {
  (cd ${pkgbase}-${pkgver}
    python setup.py build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-editor() {
  depends=('python')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-editor() {
  depends=('python2')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
