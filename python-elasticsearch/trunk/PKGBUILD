# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elasticsearch
pkgver=7.8.0
pkgrel=1
arch=('any')
pkgdesc='Python client for Elasticsearch'
url='https://github.com/elastic/elasticsearch-py'
license=('Apache')
depends=('python-urllib3')
makedepends=('python-setuptools' 'git')
checkdepends=('python-requests' 'python-mock' 'python-yaml' 'python-pytest' 'python-numpy'
              'python-pandas' 'python-aiohttp' 'python-pytest-asyncio')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elastic/elasticsearch-py/archive/$pkgver.tar.gz"
        git+https://github.com/elastic/elasticsearch.git)
sha512sums=('94d65c0a3e8d509214ed7ad63ebd47da6cdb53e4966694f23f2071f6c21106e6be634bbcf1253f6dae37c5528fe9b564e34660aef155caf6659ed572f6cceadb'
            'SKIP')

prepare() {
  sed -i 's/, "certifi"//' elasticsearch-py-$pkgver/setup.py
  sed -i 's|CA_CERTS = None|CA_CERTS = "/etc/ssl/certs/ca-certificates.crt"|' elasticsearch-py-$pkgver/elasticsearch/{connection/http_urllib3.py,_async/http_aiohttp.py}
}

build() {
  cd elasticsearch-py-$pkgver
  python setup.py build
}

check() {
  cd elasticsearch-py-$pkgver
  # Skip tests that requires a running elasticsearch server
  TEST_ES_REPO="$srcdir"/elasticsearch pytest --deselect test_elasticsearch/test_async/test_server
}

package() {
  cd elasticsearch-py-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
