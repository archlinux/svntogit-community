# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=2.5.1
pkgrel=1
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/elementpath/archive/v$pkgver.tar.gz")
sha512sums=('35c272a9f3dc2ddb1ab6b1aaa67588c1e3fcc5765a2edfd4dae47346b7b45f209b38a501a24f0c37b5ca0d933928c13b8608f33a2aadc57e1e4eeb928a491ab8')

build() {
  cd elementpath-$pkgver
  python setup.py build
}

check() {
  cd elementpath-$pkgver
  python -m pytest
}

package() {
  cd elementpath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
