# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=2.1.2
pkgrel=1
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/elementpath/archive/v$pkgver.tar.gz")
sha512sums=('f6a6ad638f598ce7778afd3fb4fdb0988ebc675e22b873163bdfc6d02c905f4d89379075b12e19a715f02af180dfdddc92844bc1ecc70ede9ff1040cfc07dbc1')

build() {
  cd elementpath-$pkgver
  python setup.py build
}

check() {
  cd elementpath-$pkgver
  python -m pytest
}

package() {
  cd elementpath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
