# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=1.2.0
pkgrel=1
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-xmlschema')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/elementpath/archive/v$pkgver.tar.gz")
sha512sums=('25a63eb40f0258116dbbe866f250cdb7370178575197f81a599126d2153146449645316e71908c15aa78073e7c77324fb3aa5558869d9da9f2721552ca132244')

build() {
  cd elementpath-$pkgver
  python setup.py build
}

check() {
  cd elementpath-$pkgver
  python setup.py pytest || warning "missing it_IT locale for testing"
}

package() {
  cd elementpath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
