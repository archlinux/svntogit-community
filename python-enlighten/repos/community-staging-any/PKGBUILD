# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=python-enlighten
pkgver=1.11.2
pkgrel=2
pkgdesc='Progress bar for Python console apps'
url=https://github.com/Rockhopper-Technologies/enlighten
arch=(any)
license=(MPL2)
depends=(
  python-blessed
  python-prefixed
)
makedepends=(
  git
  python-setuptools
)
_tag=be19ddbdfb6e76f1e703f0d54a2b26c124c881e1
source=(git+https://github.com/Rockhopper-Technologies/enlighten.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd enlighten
  git describe --tags
}

build() {
  cd enlighten
  python setup.py build
}

package() {
  cd enlighten
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
