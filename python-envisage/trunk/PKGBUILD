# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-envisage
pkgname=('python2-envisage' 'python-envisage')
pkgver=4.5.1
pkgrel=0
pkgdesc="Extensible Application Framework"
arch=('any')
url="https://github.com/enthought/envisage"
license=('BSD')
makedepends=('python-setuptools' 'python-apptools' 'python2-setuptools' 'python2-apptools')

source=("envisage-$pkgver.tar.gz::https://github.com/enthought/envisage/archive/${pkgver}.tar.gz")
md5sums=('827537cf5d07a1676955d731fc1dc888')

prepare() {
  cd "$srcdir"
  cp -a envisage-${pkgver} envisage-py2-${pkgver}
}

build() {
  cd "$srcdir"/envisage-py2-$pkgver

  msg "Building Python2"
  python2 setup.py build

  cd "$srcdir"/envisage-$pkgver

  msg "Building Python3"
  python setup.py build
}

package_python2-envisage() {
  depends=('python2-apptools')
  optdepends=('ipython2: for IPython shell plugin')
  cd "$srcdir"/envisage-py2-$pkgver

  python2 setup.py install --root="$pkgdir"/  --optimize=1

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-envisage() {
  depends=('python-apptools')
  optdepends=('ipython: for IPython shell plugin')
  cd "$srcdir"/envisage-$pkgver

  python setup.py install --root="$pkgdir"/  --optimize=1

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
