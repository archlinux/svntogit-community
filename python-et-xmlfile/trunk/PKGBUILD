# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-et-xmlfile
pkgname=('python2-et-xmlfile' 'python-et-xmlfile')
pkgver=1.0.1
pkgrel=3
pkgdesc="A low memory library for creating large XML files."
arch=('any')
url="http://et-xmlfile.readthedocs.org/"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python-lxml' 'python2-lxml')
source=("https://pypi.python.org/packages/source/e/et_xmlfile/et_xmlfile-${pkgver}.tar.gz"
        "LICENCE")
md5sums=('f47940fd9d556375420b2e276476cfaf'
         'd5feddd06c61a04aea40b7aa1707e625')

prepare() {
  cd "$srcdir"
  cp -a et_xmlfile-${pkgver} et_xmlfile-py2-${pkgver}
  cd et_xmlfile-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/et_xmlfile-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/et_xmlfile-${pkgver}
  python setup.py build
}

package_python2-et-xmlfile() {
  depends=('python2' 'python2-lxml')

  cd "$srcdir"/et_xmlfile-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

package_python-et-xmlfile() {
  depends=('python' 'python-lxml')

  cd "$srcdir"/et_xmlfile-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

