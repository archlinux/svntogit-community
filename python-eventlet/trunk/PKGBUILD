# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-eventlet
pkgname=('python-eventlet' 'python2-eventlet')
_pyname=eventlet
pkgver=0.25.0
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='https://eventlet.net'
arch=('any')
license=('MIT')
makedepends=('python-greenlet' 'python-setuptools' 'python-sphinx' 'python-monotonic' 'python-dnspython'
             'python2-greenlet' 'python2-setuptools' 'python2-sphinx' 'python2-monotonic' 'python2-dnspython' 'python2-subprocess32')
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq'
              'python2-psycopg2' 'python2-nose' 'python2-pyopenssl' 'python2-httplib2' 'python2-mock' 'python2-pyzmq')
options=('!makeflags')
source=(https://github.com/eventlet/eventlet/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha512sums=('e490cbf06f01d0bb1809ec2c66ff718a9404f844f153caffc54317aebb24042d096b9c930fe035e5c2a59b186d344628741aae02f625a0dc2596de52b4a3cbcc')

prepare() {
  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i ${_pyname}-${pkgver}/tests/*_test.py
  cp -a ${_pyname}-${pkgver}{,-py2}
  sed 's|python|python2|' -i ${_pyname}-${pkgver}-py2/examples/*
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -C doc text
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -C doc text
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    nosetests -sv tests
  )
  (cd ${_pyname}-${pkgver}-py2
    nosetests2 -sv tests
  )
}

package_python-eventlet() {
  depends=('python' 'python-greenlet' 'python-monotonic' 'python-dnspython')
  optdepends=('python-psycopg2: non-blocking PostgreSQL support'
              'python-pyopenssl: non-blocking SSL support'
              'python-httplib2: non-blocking HTTP support'
              'python-pyzmq: non-blocking ZeroMQ support'
              'python-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-eventlet() {
  depends=('python2' 'python2-greenlet' 'python2-monotonic' 'python2-dnspython' 'python2-subprocess32')
  optdepends=('python2-psycopg2: non-blocking PostgreSQL support'
              'python2-pyopenssl: non-blocking SSL support'
              'python2-httplib2: non-blocking HTTP support'
              'python2-pyzmq: non-blocking ZeroMQ support'
              'python2-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
