# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pypiname=exam
pkgbase=python-exam
pkgname=('python-exam' 'python2-exam')
pkgver=0.10.6
pkgrel=3
pkgdesc="Helpers for better testing"
arch=('any')
url="https://pypi.python.org/pypi/exam"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-mock' 'python2-mock' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/Fluxx/exam.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a exam{,-py2}
}

build() {
  cd "$srcdir/exam"
  python setup.py build

  cd "$srcdir/exam-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/exam"
  python setup.py test

  cd "$srcdir/exam-py2"
  python2 setup.py test
}

package_python-exam() {
  depends=('python-mock')

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-exam() {
  depends=('python2-mock')

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
