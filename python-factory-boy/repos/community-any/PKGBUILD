# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-factory-boy
pkgver=3.1.0
pkgrel=3
pkgdesc="A versatile test fixtures replacement based on thoughtbot's factory_bot for Ruby"
url="https://github.com/FactoryBoy/factory_boy"
license=('MIT')
arch=('any')
depends=('python-faker')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-flask' 'python-django' 'python-sqlalchemy'
              'python-flask-sqlalchemy' 'python-mongoengine' 'python-mongomock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FactoryBoy/factory_boy/archive/$pkgver.tar.gz")
sha512sums=('6fff7b26f1a8a2c533591699527f40d922f287663fe4fab3ce38204651d57bd6b287a4e01e5e0195cf5337cc48b25db0e97f64acd5b5b3799f3814f14da8e5a5')

prepare() {
  cd factory_boy-$pkgver
  # Use mongomock
  sed -i '/serverselectiontimeoutms/a is_mock=True' tests/test_mongoengine.py
}

build() {
  cd factory_boy-$pkgver
  python setup.py build
}

check() {
  cd factory_boy-$pkgver
  python setup.py pytest
}

package() {
  cd factory_boy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
