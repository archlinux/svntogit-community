# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgbase=python-fake-factory
pkgname=(python-fake-factory python2-fake-factory)
pkgver=0.5.3
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='http://www.joke2k.net/faker/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-ukpostcodeparser' 'python2-ukpostcodeparser'
              'python-mock' 'python2-mock')
source=("git+https://github.com/joke2k/faker.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  # Fix test discover
  echo -e '[pytest]\npython_files = *.py' > faker/pytest.ini

  cp -a faker{,-py2}
}

build() {
  cd "$srcdir/faker"
  python setup.py build

  cd "$srcdir/faker-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/faker"
  py.test faker/tests

  cd "$srcdir/faker-py2"
  py.test2 faker/tests
}

package_python-fake-factory() {
  depends=('python-setuptools')

  cd "${srcdir}/faker"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python2-fake-factory() {
  depends=('python2-setuptools')

  cd "${srcdir}/faker-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  mv "${pkgdir}/usr/bin/faker"{,2}
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
