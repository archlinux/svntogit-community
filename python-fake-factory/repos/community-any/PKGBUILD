# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgbase=python-fake-factory
pkgname=(python-fake-factory python2-fake-factory)
pkgver=0.5.10
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='http://www.joke2k.net/faker/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-dateutil' 'python2-dateutil' 'git')
checkdepends=('python-ukpostcodeparser' 'python2-ukpostcodeparser' 'python-nose' 'python2-nose'
              'python-mock' 'python2-mock' 'python-email-validator' 'python2-email-validator'
              'python2-ipaddress')
source=("git+https://github.com/joke2k/faker.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a faker{,-py2}
}

build() {
  cd "$srcdir"/faker
  python setup.py build

  cd "$srcdir"/faker-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/faker
  nosetests3

  cd "$srcdir"/faker-py2
  nosetests2
}

package_python-fake-factory() {
  depends=('python-setuptools' 'python-dateutil')

  cd "$srcdir"/faker
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}

package_python2-fake-factory() {
  depends=('python2-setuptools' 'python2-dateutil' 'python2-ipaddress')

  cd "$srcdir"/faker-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/faker{,2}
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
