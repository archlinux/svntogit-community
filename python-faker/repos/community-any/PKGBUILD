# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgname=python-faker
pkgver=9.6.0
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='https://faker.readthedocs.io/en/master/'
license=('MIT')
provides=("python-fake-factory=$pkgver")
conflicts=('python-fake-factory')
replaces=('python-fake-factory')
depends=('python-dateutil' 'python-text-unidecode')
makedepends=('python-setuptools' 'python-pytest')
checkdepends=('python-validators' 'python-ukpostcodeparser' 'python-random2'
              'python-freezegun' 'python-pillow')
source=("$pkgname-$pkgver.tar.gz::https://github.com/joke2k/faker/archive/v$pkgver.tar.gz")
sha512sums=('aa5eba1e8b7d7611578c929eb0e0e1fa929d648609583dc896262bfe283ddf8a867f8170f86414b8d3729542a6b03889c3400f0dc277b9c0007182a2be3b30a2')

prepare() {
  sed -i -e 's/==/>=/' faker-$pkgver/setup.py
}

build() {
  cd faker-$pkgver
  python setup.py build
}

check() {
  cd faker-$pkgver
  python setup.py egg_info
  python -m pytest
}

package() {
  cd faker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}
