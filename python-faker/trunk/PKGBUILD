# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgbase=python-faker
pkgname=(python-faker python2-faker)
pkgver=0.7.8
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='http://www.joke2k.net/faker/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-dateutil' 'python2-dateutil' 'git')
checkdepends=('python-ukpostcodeparser' 'python2-ukpostcodeparser' 'python-nose' 'python2-nose'
              'python-mock' 'python2-mock' 'python-email-validator' 'python2-email-validator'
              'python2-ipaddress')
source=("git+https://github.com/joke2k/faker.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a faker{,-py2}
}

build() {
  cd "$srcdir"/faker
  python setup.py build

  cd "$srcdir"/faker-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/faker
  nosetests3

  cd "$srcdir"/faker-py2
  nosetests2
}

package_python-faker() {
  depends=('python-setuptools' 'python-dateutil')
  provides=("python-fake-factory=$pkgver")
  conflicts=('python-fake-factory')
  replaces=('python-fake-factory')

  cd "$srcdir"/faker
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}

package_python2-faker() {
  depends=('python2-setuptools' 'python2-dateutil' 'python2-ipaddress')
  provides=("python2-fake-factory=$pkgver")
  conflicts=('python2-fake-factory')
  replaces=('python2-fake-factory')

  cd "$srcdir"/faker-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/faker{,2}
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
