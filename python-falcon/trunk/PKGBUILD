# Maintainer: David Runge <dvzrv@archlinux.org>

_name=falcon
pkgname=python-falcon
pkgver=3.1.0
pkgrel=1
pkgdesc="Web API framework for fast and reliable microservices, proxies, and app backends"
arch=(x86_64)
url="https://falconframework.org/"
license=(Apache)
depends=(python)
makedepends=(cython python-build python-installer python-setuptools python-wheel)
# TODO: add python-daphne
checkdepends=(gunicorn hypercorn python-aiofiles python-cbor2 python-httpx
python-jsonschema python-mimeparse python-msgpack python-mujson python-orjson
python-pecan python-pytest python-pytest-asyncio python-pytest-runner
python-rapidjson python-requests python-testtools python-ujson
python-websockets python-yaml uvicorn)
optdepends=(
  'python-django: for falcon-bench'
  'python-flask: for falcon-bench'
  'python-pecan: for falcon-bench'
  'python-bottle: for falcon-bench'
  'python-pprofile: for falcon-bench'
  'python-vmprof: for falcon-bench'
)
# not all required files for tests contained in pypi sdist tarball: https://github.com/falconry/falcon/issues/2051
# source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
source=($_name-$pkgver.tar.gz::https://github.com/falconry/$_name/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('9da76dd92dfce1611ef681588d8e0e2fc7e906b5c0bc4b60a4620e2e9907564783763b7776c9cc2ccb296116c985e527b2f429520a1c601567cf4eb15e98f8cc')
b2sums=('183623c8d7e8b86ed9915cbf3757fd526f7f3a8a97ae5fdf86294bcc65052c5c827d4a920d93e9fa0198a8d255c7b56ecff465a36d9b0ea31581abd87d55ccd0')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd $_name-$pkgver
  export PYTHONPATH="build/lib.linux-$CARCH-$python_version:$PWD:$PYTHONPATH"
  pytest -v --ignore "tests/asgi/test_cythonized_asgi.py" tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
