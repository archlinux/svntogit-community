# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Tomislav Ivek <tomislav.ivek@gmail.com>

pkgname=('python-fasteners' 'python2-fasteners')
pkgver=0.14.1
pkgrel=6
pkgdesc="A python package that provides useful locks."
arch=('any')
url="https://github.com/harlowja/fasteners"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/harlowja/fasteners/archive/$pkgver.tar.gz")
sha512sums=('954d692b7f43563ba1413854c8dec2f5ff98757346c55aaa5f39e9aa777fb746bf4f9506beab6cfa149d5646db4b3de58d35bcef95e3128096c3346c9fd8015c')

build() {
  cd "$srcdir/fasteners-$pkgver"
  python setup.py build
}

package_python-fasteners() {
  depends=('python-six' 'python-monotonic')

  cd "$srcdir/fasteners-$pkgver"
  python setup.py install --optimize=1 --root=${pkgdir}
}

package_python2-fasteners() {
  depends=('python2-six' 'python2-monotonic')

  cd "$srcdir/fasteners-$pkgver"
  python2 setup.py install --optimize=1 --root=${pkgdir}
}
