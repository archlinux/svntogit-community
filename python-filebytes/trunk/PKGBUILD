# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-filebytes
pkgname=('python-filebytes' 'python2-filebytes')
_pyname=${pkgbase/python-/}
pkgver=0.9.21
pkgrel=3
pkgdesc='Library to read and edit executable binary files'
url='https://github.com/sashs/filebytes'
arch=('any')
license=('GPL2')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=(https://github.com/sashs/filebytes/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha512sums=('98eee1f44f2de440d6fb2b1eb80e1f928e24af384e943f76c5ff96e0647df3c2b707fdf4852ecd4534b7c0e5293858017fc30338b4eb1e92ab6aefc716f2a802')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
  sed -r 's|env python$|env python2|' -i ${_pyname}-${pkgver}-py2/samples/*
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-filebytes() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

package_python2-filebytes() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

# vim: ts=2 sw=2 et:
