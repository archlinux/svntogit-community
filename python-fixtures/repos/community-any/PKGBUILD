# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fixtures
pkgname=('python-fixtures' 'python2-fixtures')
pkgver=3.0.0
pkgrel=7
pkgdesc="Fixtures, reusable state for writing clean tests and more"
arch=('any')
license=('Apache')
url="https://launchpad.net/python-fixtures"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-six' 'python2-six' 'python-testtools' 'python2-testtools')
checkdepends=('python-subunit' 'python-mock')
source=("https://pypi.io/packages/source/f/fixtures/fixtures-$pkgver.tar.gz")
sha512sums=('e52ba864c42d15a8baa60583761006b2dd78e3cd5f3f043f5702346aef428b933d62e8fa5889b7cb4196e4072acc5df3b7ffa39702a1b782887bdc109299b695')

prepare() {
  cp -a fixtures-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/fixtures-$pkgver
  python setup.py build

  cd "$srcdir"/fixtures-$pkgver-py2
  python2 setup.py build
}

check() {
  cd fixtures-$pkgver
  python -m testtools.run fixtures.test_suite
}

package_python-fixtures() {
  depends=('python-pbr' 'python-six' 'python-testtools')

  cd fixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-fixtures() {
  depends=('python2-pbr' 'python2-six' 'python2-testtools')

  cd fixtures-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
