# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

_name=flake8-black
pkgname=python-flake8-black
pkgver=0.3.4
pkgrel=1
pkgdesc='Flake8 plugin to call black as a code style validator'
arch=('any')
url=https://github.com/peterjc/flake8-black
license=('MIT')
depends=('flake8' 'python-black' 'python-tomli')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('7f96a4c80a828d09f1d550724e16aabb2adacd6a5f8e0bb051df422fc63d2183')
b2sums=('bf32f49239d89d199dbbf82690c59722a8efe6bdd8fa634a7649176703530fe800ba021cb5dedf56a588b47293f3c635ec44dceb3c236af1fefa7c71c4a25f57')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  source test-env/bin/activate
  python -m installer dist/*.whl
  cd tests
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="../build:$site_packages" ./run_tests.sh
  deactivate
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/flake8_black-$pkgver.dist-info/LICENSE.rst \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
