# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flake8-blind-except
pkgname=('python-flake8-blind-except' 'python2-flake8-blind-except')
pkgver=0.1.1
pkgrel=2
pkgdesc='A flake8 extension that checks for blind except: statements'
arch=('any')
license=('MIT')
url='https://github.com/elijahandrews/flake8-blind-except'
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/elijahandrews/flake8-blind-except.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a flake8-blind-except{,-py2}
}

build() {
  cd "$srcdir"/flake8-blind-except
  python setup.py build

  cd "$srcdir"/flake8-blind-except-py2
  python2 setup.py build
}

package_python-flake8-blind-except() {
  depends=('python-setuptools')

  cd "$srcdir"/flake8-blind-except
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flake8-blind-except() {
  depends=('python2-setuptools')

  cd "$srcdir"/flake8-blind-except-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
