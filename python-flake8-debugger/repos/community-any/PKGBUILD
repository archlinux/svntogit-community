# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flake8-debugger
pkgname=('python-flake8-debugger' 'python2-flake8-debugger')
pkgver=3.0.0
pkgrel=1
pkgdesc='ipdb/pdb statement checker plugin for flake8'
arch=('any')
license=('MIT')
url='https://github.com/JBKahn/flake8-debugger'
makedepends=('python-setuptools' 'python2-setuptools' 'flake8' 'python2-flake8'
             'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/JBKahn/flake8-debugger/archive/$pkgver.tar.gz")
sha512sums=('82b8d3378ff1b6b63d02f3602d2db113d5c2e818a9da741b8867487308a7d8119f0871ac9e89a269dd4b1097c9381f171e53a83ec94e7685fcf5203c812820dd')

prepare() {
  cp -a flake8-debugger-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py build

  cd "$srcdir"/flake8-debugger-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py test

  cd "$srcdir"/flake8-debugger-$pkgver-py2
  python2 setup.py test
}

package_python-flake8-debugger() {
  depends=('flake8')

  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-flake8-debugger() {
  depends=('python2-flake8')

  cd "$srcdir"/flake8-debugger-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
