# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flake8-debugger
pkgname=('python-flake8-debugger' 'python2-flake8-debugger')
pkgver=3.1.0
pkgrel=2
pkgdesc='ipdb/pdb statement checker plugin for flake8'
arch=('any')
license=('MIT')
url='https://github.com/JBKahn/flake8-debugger'
makedepends=('python-setuptools' 'python2-setuptools' 'flake8' 'python2-flake8'
             'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/JBKahn/flake8-debugger/archive/$pkgver.tar.gz")
sha512sums=('487864ba8b717978e91fb9b76dc43b14e4c0efa322a6a032c9592d65da9f6eb87e624ec05a731263dd63560ab8508d4f0e05b86e83c3d101ae34638719abdbb7')

prepare() {
  cp -a flake8-debugger-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py build

  cd "$srcdir"/flake8-debugger-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py test

  cd "$srcdir"/flake8-debugger-$pkgver-py2
  python2 setup.py test
}

package_python-flake8-debugger() {
  depends=('flake8')

  cd "$srcdir"/flake8-debugger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-flake8-debugger() {
  depends=('python2-flake8')

  cd "$srcdir"/flake8-debugger-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
