# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Moritz Lipp <mlq@pwmt.org>

_pipname=Flask-AutoIndex
pkgbase=python-flask-autoindex
pkgname=(python2-flask-autoindex python-flask-autoindex)
pkgver=0.5
pkgrel=1
pkgdesc="Flask-AutoIndex generates an index page for your Flask application automatically"
arch=('any')
url="http://packages.python.org/Flask-AutoIndex/"
license=('BSD')
makedepends=('python2-setuptools' 'python2-flask' 'python-setuptools' 'python-flask')
source=("https://pypi.python.org/packages/source/${_pipname:0:1}/$_pipname/$_pipname-0.5.tar.gz")
md5sums=('24984602365704737468bb4d2586a739')

prepare() {
    cp -R $_pipname-$pkgver python2-$_pipname-$pkgver
}

package_python2-flask-autoindex() {
depends=('python2-flask')

    cd python2-$_pipname-$pkgver
    python2 setup.py install --root="$pkgdir/" --optimize=1

    mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
    install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

package_python-flask-autoindex() {
depends=('python-flask')

    cd $_pipname-$pkgver 
    python setup.py install --root="$pkgdir/" --optimize=1

    mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
    install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

