# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: djanku <djanku (at) email (dot) cz>

_pipname=Flask-Babel
pkgname=python-flask-babel
pkgver=2.0.0
pkgrel=1
pkgdesc="Adds i18n/l10n support to Flask applications with the help of the Babel library."
arch=(any)
url="https://pypi.python.org/pypi/Flask-Babel"
license=(BSD)
depends=(python-flask python-babel python-pytz python-speaklater python-jinja)
makedepends=(python-setuptools)
source=("https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz")
sha256sums=('f9faf45cdb2e1a32ea2ec14403587d4295108f35017a7821a2b1acb8cfd9257d')

build() {
  cd $_pipname-$pkgver 
  python setup.py build
}

package() {
  cd $_pipname-$pkgver 
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
