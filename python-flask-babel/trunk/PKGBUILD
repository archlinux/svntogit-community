# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: djanku <djanku (at) email (dot) cz>

_pipname=Flask-Babel
pkgbase=python-flask-babel
pkgname=(python2-flask-babel python-flask-babel)
pkgver=0.11.2
pkgrel=2
pkgdesc="Adds i18n/l10n support to Flask applications with the help of the Babel library."
arch=(any)
url="http://pypi.python.org/pypi/Flask-Babel"
license=(BSD)
makedepends=(python2-setuptools python-setuptools)
source=("https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz")
sha256sums=('c0d75710bd4b0fe866f9f2347de6e19208712f9cec006436b4c1c15d4cb0c939')

prepare() {
    cp -R $_pipname-$pkgver python2-$_pipname-$pkgver
}

package_python2-flask-babel() {
  depends=(python2-flask python2-babel python2-pytz python2-speaklater python2-jinja)

  cd python2-$_pipname-$pkgver
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

package_python-flask-babel() {
  depends=(python-flask python-babel python-pytz python-speaklater python-jinja)

  cd $_pipname-$pkgver 
  python setup.py install --root="$pkgdir/" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

