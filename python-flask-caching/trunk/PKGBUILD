# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-caching
pkgver=1.7.2
pkgrel=2
pkgdesc="Adds caching support to your Flask application"
url="https://github.com/sh4nks/flask-caching"
license=('BSD')
arch=('any')
depends=('python-flask')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-xprocess' 'python-pylibmc'
              'python-redis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sh4nks/flask-caching/archive/v$pkgver.tar.gz")
sha512sums=('4667fbabb2d65335348fbbb6b0c738984d71b4d210ab7c2955e196a73ba834499febe10eac8aa732ad5164e4c96371dbab82bc08b41ce3cf6b6750b0bd078291')

prepare() {
  cd flask-caching-$pkgver
  sed -i 's/< *2.6/<3/' setup.py
}

build() {
  cd flask-caching-$pkgver
  python setup.py build
}

check() {
  cd flask-caching-$pkgver
  python setup.py pytest
}

package() {
  cd flask-caching-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
