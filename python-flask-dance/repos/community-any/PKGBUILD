# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-dance
pkgver=5.0.0
pkgrel=1
pkgdesc="Doing the OAuth dance with style using Flask, requests, and oauthlib"
url="https://github.com/singingwolfboy/flask-dance"
license=('MIT')
arch=('any')
depends=('python-requests' 'python-oauthlib' 'python-requests-oauthlib' 'python-flask'
         'python-urlobject' 'python-werkzeug')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-sphinx' 'python-pytest-cov' 'python-mock'
              'python-responses' 'python-freezegun' 'python-sqlalchemy' 'python-sqlalchemy-utils'
              'python-flask-sqlalchemy' 'python-flask-login' 'python-flask-caching'
              'python-flask-sphinx-themes' 'python-sphinxcontrib-seqdiag' 'python-betamax'
              'python-pyjwt' 'python-pytest-mock')
source=("https://github.com/singingwolfboy/flask-dance/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2b96b9755d42bb2af4e1e34147fee6651b3664ed470bfa3e6cd6cb07dcc04c5d78ef012e472491aef34ca48a90ad275e53844e2a0054b9f0cd59caff9a569cc8')

prepare() {
  cd flask-dance-$pkgver
  sed -i 's/sign_rsa_sha1/_sign_rsa/' tests/contrib/test_jira.py
}

build() {
  cd flask-dance-$pkgver
  python setup.py build
}

check() {
  cd flask-dance-$pkgver
  # New oauthlib related failure. The sed above didn't fix it cleanly.
  python setup.py pytest --addopts '--deselect tests/contrib/test_jira.py::test_load_from_config'
}

package() {
  cd flask-dance-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
