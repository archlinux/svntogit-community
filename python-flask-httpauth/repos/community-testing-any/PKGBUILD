# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-flask-httpauth
_pkgname=Flask-HTTPAuth
pkgver=3.3.0
pkgrel=4
pkgdesc='Basic, Digest and Token HTTP authentication for Flask routes'
url='https://github.com/miguelgrinberg/Flask-HTTPAuth'
arch=('any')
license=('MIT')
depends=('python' 'python-flask')
checkdepends=('python-pytest')
source=(https://github.com/miguelgrinberg/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1303351a278799eb885afc9160d4569f73d13212cab2defbc818df6c39ad9284703da703f52c63fb032096a9773f429216e47cc26513730389585761b8f9953b')
b2sums=('d6e259fcc06a92e2a8f065118f32fd72a11ca2d3a12ebb641a461cdd8554cd954799e2e4ddcd9f7ac904accbc226f5a19374e150e50ca30d65e3f5f3009182d6')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  pytest tests/
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
