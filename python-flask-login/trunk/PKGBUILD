# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flask-login
pkgname=('python2-flask-login' 'python-flask-login')
pkgver=0.4.1
pkgrel=2
pkgdesc="User session management for Flask."
arch=('any')
url="https://pythonhosted.org/Flask-Login/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-flask' 'python2-flask')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-blinker'
              'python2-blinker' 'python-semantic-version' 'python2-semantic-version')
source=("$pkgname-$pkgver.tar.gz::https://github.com/maxcountryman/flask-login/archive/$pkgver.tar.gz")
sha512sums=('c87a2948ac37439ddd76f3f11584bdd5910cb00e96a7400343cf4dadc5a0f9bc84d40bdc068d2e8c05bd5a510046e18473e9ad71502c5039a5f05b371ce9bb4c')

prepare() {
  cp -a flask-login-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/flask-login-$pkgver
  nosetests3

  cd "$srcdir"/flask-login-$pkgver-py2
  # https://github.com/maxcountryman/flask-login/issues/232
  nosetests2 || warning "Tests failed"
}

package_python-flask-login() {
  depends=('python-flask')

  cd flask-login-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

package_python2-flask-login() {
  depends=('python2-flask')

  cd flask-login-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
