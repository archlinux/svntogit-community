# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=flask-principal
pkgbase=python-flask-principal
pkgname=('python-flask-principal' 'python2-flask-principal')
pkgver=0.4.0
pkgrel=3
pkgdesc='Identity management for Flask applications'
url='https://pythonhosted.org/Flask-Principal/'
arch=('any')
license=('MIT')
makedepends=('python-flask' 'python-blinker' 'python-sphinx' 'python-setuptools'
             'python2-flask' 'python2-blinker' 'python2-sphinx' 'python2-setuptools')
checkdepends=('python-nose'
              'python2-nose')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/mattupstate/flask-principal/archive/${pkgver}.tar.gz)
sha256sums=('ed8c58943460d7d54c29463e2fe98ae4969d3818c0f59b36e9b2649128db96c9')
sha512sums=('ed8cb28c4e8d936de96db0bf9f7cb45b253dc204c4b8f8dd8022ef1552592ff6324b4a33d3ee862794a6e20eb8c32a0365e7b9397d427da5022c5ded3dfa308a')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
  python2 setup.py build
  sphinx-build2 -b text docs docs/_build-py2/text
  sphinx-build2 -b man docs docs/_build-py2/man
}

check() {
  cd ${_pkgname}-${pkgver}
  nosetests
  nosetests2
}

package_python-flask-principal() {
  depends=('python' 'python-flask')
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-principal.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-principal() {
  depends=('python2' 'python2-flask')
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build-py2/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build-py2/man/flask-principal.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
