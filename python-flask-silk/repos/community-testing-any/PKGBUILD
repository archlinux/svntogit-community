# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=Flask-Silk
pkgbase=python-flask-silk
pkgname=('python-flask-silk' 'python2-flask-silk')
pkgver=0.2
pkgrel=4
pkgdesc='Adds silk icons to your Flask application or blueprint, or extension'
url='https://pythonhosted.org/Flask-Silk/index.html'
arch=('any')
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/F/Flask-Silk/Flask-Silk-$pkgver.tar.gz")
sha256sums=('80a21faf09fe257443a4fbbf8cd3f6c793c567c87ff784751a1c38d2e18b5fbe')

prepare() {
    cp -R $_pipname-$pkgver python2-$_pipname-$pkgver
}

package_python2-flask-silk() {
depends=('python2-flask')

    cd python2-$_pipname-$pkgver
    python2 setup.py install --root="$pkgdir/" --optimize=1

    mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
    install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

package_python-flask-silk() {
depends=('python-flask')

    cd $_pipname-$pkgver 
    python setup.py install --root="$pkgdir/" --optimize=1

    mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
    install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

