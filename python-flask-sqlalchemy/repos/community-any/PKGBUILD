# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Serge Victor <arch@random.re>
# Contributor: Kevin Azzam <aur@azz.am>

_pyname=flask-sqlalchemy
pkgbase=python-flask-sqlalchemy
pkgname=('python-flask-sqlalchemy' 'python2-flask-sqlalchemy')
pkgver=2.3.2
pkgrel=2
pkgdesc='Adds SQLAlchemy support to your Flask application'
url='http://flask-sqlalchemy.pocoo.org/'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python-sphinx' 'python-flask' 'python-sqlalchemy'
             'python2-setuptools' 'python2-sphinx' 'python2-flask' 'python2-sqlalchemy')
checkdepends=('python-pytest' 'python-blinker'
              'python2-pytest' 'python2-blinker')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mitsuhiko/flask-sqlalchemy/archive/${pkgver}.tar.gz)
sha512sums=('0dda35dbdd0a2bd87aeaad72c44bdbc80f47fcda519c072f235fe0cc5d64d46278fa6d4cedce66e0731035423f3adb5fc2c20d631d70cc1b3c3e2289bdd1cbd1')

prepare() {
  sed -r "s|(release =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -j1 -C docs man text SPHINXBUILD=sphinx-build PYTHONPATH=..
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -j1 -C docs man text SPHINXBUILD=sphinx-build2 PYTHONPATH=..
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    PYTHONPATH=".:${PYTHONPATH}" py.test
  )
  (cd ${_pyname}-${pkgver}-py2
    PYTHONPATH=".:${PYTHONPATH}" py.test2
  )
}

package_python-flask-sqlalchemy() {
  depends=('python-flask' 'python-sqlalchemy')
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-sqlalchemy() {
  depends=('python2-flask' 'python2-sqlalchemy')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
