# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Tarmo Heiskanen <turskii@gmail.com>

pkgname=python-flask
pkgver=2.0.0
pkgrel=2
pkgdesc='Micro webdevelopment framework for Python'
url='http://flask.pocoo.org/'
arch=('any')
license=('custom:BSD')
depends=('python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
makedepends=('python-setuptools'  'python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pallets/flask/archive/${pkgver}.tar.gz")
sha512sums=('648c96b877ddcd260346b96c16706fc590de21932b68e15f0e169cc03b01681e2ff8b68561aa4477c5012c0620161b4fd2ac07223145bf819d1f1867e0a3e7af')

build() {
  cd "flask-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py build
}

# check() {
#   cd "flask-$pkgver"
#   python setup.py test
# }

package() {
  cd "flask-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
