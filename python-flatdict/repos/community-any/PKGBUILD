# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=flatdict
pkgname=python-flatdict
pkgver=3.4.0
pkgrel=1
pkgdesc="Interact with nested dicts as a single level dict with delimited keys."
arch=('any')
url="https://flatdict.readthedocs.io"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gmr/flatdict/archive/${pkgver}.tar.gz")
sha256sums=('d9728a5ef91e9936f9c4218b22c0a6666277b041c4bf32f63ccc7b82ebe64fd0')
b2sums=('83883b1a19d2de473805c466d603137958e0c2d8ec5007d7f1bbd0e02a2aeb4e7dc673b3fd390140db48b2e2713a6912e15edf934047c5a1acbccad4b0d5ac5a')

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    nosetests
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
