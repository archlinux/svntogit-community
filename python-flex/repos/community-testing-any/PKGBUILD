# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flex
pkgname=(python-flex python2-flex)
pkgver=6.13.2
pkgrel=2
pkgdesc="Swagger schema validator"
url="https://github.com/pipermerriam/flex"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'python-yaml'
             'python2-yaml' 'python-validate-email' 'python2-validate-email' 'python-rfc3987'
             'python2-rfc3987' 'python-requests' 'python2-requests' 'python-strict-rfc3339'
             'python2-strict-rfc3339' 'python-click' 'python2-click' 'python-jsonpointer'
             'python2-jsonpointer')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-factory-boy'
              'python2-factory-boy' 'python-responses' 'python2-responses' 'python-pytest-httpbin'
              'python2-pytest-httpbin')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pipermerriam/flex/archive/v$pkgver.tar.gz")
sha512sums=('9883dcaed83ed80eb19e017dce18ee8335484ae1b8a6c893c0a2950984c0657e8c1cb90e0a0c9785d0934a0a9c765e4f12fc3258ebc7f732d052c2fda9a2e50f')

prepare() {
  sed -i 's/<2/<3/' flex-$pkgver/setup.py
  cp -a flex-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flex-$pkgver
  python setup.py build

  cd "$srcdir"/flex-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flex-$pkgver
  python setup.py pytest

  cd "$srcdir"/flex-$pkgver-py2
  python2 setup.py pytest
}

package_python-flex() {
  depends=('python-six' 'python-yaml' 'python-validate-email' 'python-rfc3987' 'python-requests'
           'python-strict-rfc3339' 'python-click' 'python-jsonpointer')

  cd flex-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flex() {
  depends=('python2-six' 'python2-yaml' 'python2-validate-email' 'python2-rfc3987' 'python2-requests'
           'python2-strict-rfc3339' 'python2-click' 'python2-jsonpointer')

  cd flex-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/swagger-flex{,2}
}
