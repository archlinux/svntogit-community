# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Francois Garillot <francois[@]garillot.net>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=python2-flickrapi
_pkgname=flickrapi
pkgver=1.4.4
pkgrel=1
pkgdesc="The official Python interface to the Flickr API"
arch=('any')
url="http://stuvel.eu/flickrapi"
license=('Python')
depends=('python2')
makedepends=('python2-docutils' 'python2-setuptools')
checkdepends=('python2-nose')
source=("http://pypi.python.org/packages/source/f/$_pkgname/$_pkgname-$pkgver.zip")

prepare() {
  cd $_pkgname-$pkgver
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}

build() {
  cd $_pkgname-$pkgver
  python2 setup.py build
}

check() {
  cd $_pkgname-$pkgver
  nosetests2
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('3af0e38ca9368b43d84449cecc8065145e822223e086dd07fb9bdd5af23e55f16961c9e87c867c0a965064d2aac9ee022ca2e0bfb291991439ab53ae425c3bd9')
