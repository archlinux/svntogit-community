# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=flit
pkgbase=python-$_pkgname
pkgname=($pkgbase{,-core})
pkgver=3.2.0
pkgrel=3
pkgdesc='Simplified packaging of Python modules'
arch=('any')
url='https://github.com/takluyver/flit'
license=('BSD')
depends=('python' 'python-toml')
makedepends=('python-setuptools' 'python-requests' 'python-docutils')
checkdepends=('python-pytest' 'python-testpath' 'python-responses')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('b40768fb4b0b2a2e3116ac790dfa629da5a57cda34b10abb8ce6a8febcc3436795d5122dcfa3db2c6d07d493f00391d2ca7300112561c8ff2a0b04d77c879662')

prepare() {
  cd $_pkgname-$pkgver

  # regenerate the dist without the "I don't support people using setup.py" flag
  PYTHONPATH="$PWD/flit_core/" python -m flit build --format sdist
  bsdtar --strip-components 1 -xvf \
    dist/${_pkgname}-${pkgver}.tar.gz ${_pkgname}-${pkgver}/setup.py

  cd flit_core

  # flit_core.buildapi does not ever generate setup.py, only `flit build` does???
  # since we still rely on them... patch the self builder to import from the flit directory's subclass
  sed -i 's/from .sdist import SdistBuilder/from flit.sdist import SdistBuilder/' flit_core/build_thyself.py
  PYTHONPATH="$PWD/../" python ./build_dists.py
  bsdtar --strip-components 1 -xvf \
    dist/flit_core-${pkgver}.tar.gz flit_core-${pkgver}/setup.py
}

build() {
  cd $_pkgname-$pkgver

  # flit doesn't either support people invoking setup.py outside of pip. The
  # undocumented setuptools.launch reimplements the same thing, but less well
  # done. Still enough for flit to build.
  python -m setuptools.launch setup.py build

  cd flit_core

  python -m setuptools.launch setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=flit_core python -m pytest
}

package_python-flit() {
  depends+=('python-flit-core' 'python-requests' 'python-docutils')
  provides=($_pkgname)
  conflicts=($_pkgname)

  cd $_pkgname-$pkgver

  python -m setuptools.launch setup.py install --root="$pkgdir" --optimize=1 --skip-build
  cp -a flit/license_templates "$pkgdir"/usr/lib/python3.9/site-packages/flit/

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-flit-core() {
  pkgdesc+=' (core backend)'

  cd $_pkgname-$pkgver/flit_core

  python -m setuptools.launch setup.py install --root="$pkgdir" --optimize=1 --skip-build
  # installing tests to a subdir of your project might not conflict the toplevel, but
  # don't do it anyway...
  rm -r "$pkgdir"/usr/lib/python*/site-packages/flit_core/tests/

  install -Dm 644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
