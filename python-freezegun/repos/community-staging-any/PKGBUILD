# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-freezegun
pkgname=('python-freezegun' 'python2-freezegun')
pkgver=0.3.8
pkgrel=2
pkgdesc="Let your Python tests travel through time"
arch=('any')
license=('Apache')
url="https://github.com/spulec/freezegun"
makedepends=('python-setuptools' 'python2-setuptools' 'python-dateutil' 'python2-dateutil' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock'
              'python-coverage' 'python2-coverage' 'python-dateutil' 'python2-dateutil')
source=("git+https://github.com/spulec/freezegun.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a freezegun{,-py2}
}

build() {
  cd "$srcdir"/freezegun
  python setup.py build

  cd "$srcdir"/freezegun-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/freezegun
  nosetests3

  cd "$srcdir"/freezegun-py2
  nosetests2
}

package_python-freezegun() {
  depends=('python-dateutil')

  cd freezegun
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-freezegun() {
  depends=('python2-dateutil')

  cd freezegun-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
