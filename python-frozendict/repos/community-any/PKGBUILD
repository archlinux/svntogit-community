# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-frozendict
pkgname=(python-frozendict python2-frozendict)
pkgver=1.2
pkgrel=3

pkgdesc='An immutable dictionary'
url='https://pypi.python.org/pypi/frozendict/'
arch=('any')
license=('MIT')

makedepends=('python-setuptools'
             'python2-setuptools')

source=("https://pypi.org/packages/source/f/frozendict/frozendict-$pkgver.tar.gz")

md5sums=('483ce6ddfe68ca197bc123aaafebf9ab')

prepare() {
	cp -a frozendict-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/frozendict-$pkgver
	python setup.py build

	cd "$srcdir"/frozendict-$pkgver-python2
	python2 setup.py build
}

package_python-frozendict() {
	depends=('python' 'python-setuptools')

	cd frozendict-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-frozendict() {
	depends=('python2' 'python2-setuptools')

	cd frozendict-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
