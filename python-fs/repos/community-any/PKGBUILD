# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fs
pkgname=('python-fs' 'python2-fs')
pkgver=0.5.2
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('custom:BSD')
url="http://pypi.python.org/pypi/fs/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'fuse' 'python2-paramiko')
source=("https://pypi.python.org/packages/source/f/fs/fs-$pkgver.tar.gz")
sha512sums=('4589fa81c16a6351ec7aa22fca7973f44d16732be1bee32c8ba359f9f0a2b3d99497b457f6339f5d0e0f24ce292963ee5db114c92e05eb78836e31a0dc1bb95e')

prepare() {
  cp -a fs-$pkgver{,-py2}
}

build() {
  cd "$srcdir/fs-$pkgver"
  python setup.py build

  cd "$srcdir/fs-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/fs-$pkgver"
  LC_CTYPE=en_US.utf8 python setup.py ptr || warning "Tests failed"

  cd "$srcdir/fs-$pkgver-py2"
  LC_CTYPE=en_US.utf8 python2 setup.py ptr || warning "Tests failed"
}

package_python-fs() {
  depends=('python-setuptools' 'python-six')

  cd fs-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-fs() {
  depends=('python2-setuptools' 'python2-six')

  cd fs-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1

  for f in fscat fscp fsinfo fsls fsmkdir fsmount fsmv fsrm fsserve fstree; do
    mv "$pkgdir/usr/bin/$f"{,2}
  done
}
