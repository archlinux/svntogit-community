# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fs
pkgver=2.4.10
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('BSD')
url="https://pypi.python.org/pypi/fs/"
depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')
checkdepends=('python-nose' 'fuse2' 'python-mock' 'python-pyftpdlib' 'python-psutil'
              'python-pysendfile')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyFilesystem/pyfilesystem2/archive/v$pkgver.tar.gz")
sha512sums=('ebb252e6c7f67fcce4fe1c98405e4c2892860d68e1b015d44b9ba62ae33445fbe7050270762ef19035bceb9fd0881aefe709e75ec1b5169a8aeb5c23b960036c')

build() {
  cd pyfilesystem2-$pkgver
  python setup.py build
}

check() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" nosetests3 tests
}

package() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
