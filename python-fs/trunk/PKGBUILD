# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fs
pkgver=2.4.13
pkgrel=4
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('BSD')
url="https://pypi.python.org/pypi/fs/"
depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')
checkdepends=('python-pytest-runner' 'fuse2' 'python-pyftpdlib' 'python-psutil' 'python-pysendfile')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyFilesystem/pyfilesystem2/archive/v$pkgver.tar.gz")
sha512sums=('c07bf6a8d373a11019709ada392153dbf953399631fa388433f9b7b73d9ae1072ea70b8ed9896752161a4737a399520f030b7875dc23137f418268c5acc24f48')

build() {
  cd pyfilesystem2-$pkgver
  python setup.py build
}

check() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" python setup.py pytest
}

package() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
