# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fs
pkgver=2.4.12
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('BSD')
url="https://pypi.python.org/pypi/fs/"
depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')
checkdepends=('python-pytest-runner' 'fuse2' 'python-mock' 'python-pyftpdlib' 'python-psutil'
              'python-pysendfile')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyFilesystem/pyfilesystem2/archive/v$pkgver.tar.gz")
sha512sums=('8b4727ea21cb9e1b69e31688c46f0732bb1536490549e353c1f88ab0c794b867c5212d1003b094cc86f562108ab4a7ee9dbc7e9aa5dc1349371de583277eb93d')

build() {
  cd pyfilesystem2-$pkgver
  python setup.py build
}

check() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" python setup.py pytest
}

package() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
