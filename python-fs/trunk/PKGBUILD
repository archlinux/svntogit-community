# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fs
pkgname=('python-fs' 'python2-fs')
pkgver=2.0.25
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('custom:BSD')
url="http://pypi.python.org/pypi/fs/"
makedepends=('python-appdirs' 'python2-appdirs' 'python2-enum34' 'python-pytz' 'python2-pytz'
             'python2-scandir' 'python-setuptools' 'python2-setuptools' 'python-six' 'python2-six'
             'python2-typing' 'python2-backports.os')
checkdepends=('python-nose' 'python2-nose' 'fuse2' 'python-mock' 'python2-mock' 'python-pyftpdlib'
              'python2-pyftpdlib' 'python-psutil' 'python2-psutil' 'python-pysendfile'
              'python2-pysendfile')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyFilesystem/pyfilesystem2/archive/v$pkgver.tar.gz")
sha512sums=('951c19d06cf341028b15599e2fc84a75cf92c1504767af871a61e63e8b539cb47ca74f935aec515fb2c09fdf539f1a63f9edf2c6bd7978a7e99507ac91e72994')

prepare() {
  cp -a pyfilesystem2-$pkgver{,-py2}

  sed -e '/enum34/d' -e '/scandir/d' -i pyfilesystem2-$pkgver/setup.py
}

build() {
  cd "$srcdir"/pyfilesystem2-$pkgver
  python setup.py build

  cd "$srcdir"/pyfilesystem2-$pkgver-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/pyfilesystem2-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" nosetests3 tests

  cd "$srcdir"/pyfilesystem2-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" nosetests2 tests
}

package_python-fs() {
  depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')

  cd pyfilesystem2-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-fs() {
  depends=('python2-appdirs' 'python2-enum34' 'python2-pytz' 'python2-scandir' 'python2-setuptools'
           'python2-six' 'python2-typing' 'python2-backports.os')

  cd pyfilesystem2-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
