# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fs
pkgname=('python-fs' 'python2-fs')
pkgver=2.0.2
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('custom:BSD')
url="http://pypi.python.org/pypi/fs/"
makedepends=('python-appdirs' 'python2-appdirs' 'python2-enum34' 'python-pytz' 'python2-pytz'
             'python2-scandir' 'python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-nose' 'python2-nose' 'fuse2' 'python-mock' 'python2-mock' 'python-pyftpdlib'
              'python2-pyftpdlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyFilesystem/pyfilesystem2/archive/v$pkgver.tar.gz")
sha512sums=('2d6e0ed514cf28227c082a78d3ca45fb7be6037122e3998374b7efee747d8a233b4855b3287287185d72bfd4ce7a6e640aa0061128e6981d67a3aea93c857645')

prepare() {
  cp -a pyfilesystem2-$pkgver{,-py2}

  sed -e '/enum34/d' -e '/scandir/d' -i pyfilesystem2-$pkgver/setup.py
}

build() {
  cd "$srcdir"/pyfilesystem2-$pkgver
  python setup.py build

  cd "$srcdir"/pyfilesystem2-$pkgver-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/pyfilesystem2-$pkgver
  nosetests3 -a "!slow" tests

  cd "$srcdir"/pyfilesystem2-$pkgver-py2
  nosetests2 -a "!slow" tests
}

package_python-fs() {
  depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')

  cd pyfilesystem2-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-fs() {
  depends=('python2-appdirs' 'python2-enum34' 'python2-pytz' 'python2-scandir' 'python2-setuptools'
           'python2-six')

  cd pyfilesystem2-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
