# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=furl
pkgbase=python-furl
pkgname=('python-furl' 'python2-furl')
pkgver=1.1
pkgrel=2
pkgdesc="A small Python library that makes parsing and manipulating URLs easy"
url="https://github.com/gruns/furl"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pycodestyle' 'python2-pycodestyle')
license=('custom:Unlicense')
arch=('any')
source=("https://pypi.python.org/packages/source/f/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('5af4acc5736f8841fd4abc002fc4c9668da2e3655f197773e603242ca29c40021dc388c760959fea7847cf1be71d5b367d56eaf263c84b171659af0632364a9c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-furl() {
  depends=('python-six' 'python-orderedmultidict')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" -vDm644 {API,README}.md
}

package_python2-furl() {
  depends=('python2-six' 'python2-orderedmultidict')
  cd "${pkgname[0]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" -vDm644 {API,README}.md
}
