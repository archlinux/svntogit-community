# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>

pkgbase=python-fuse
pkgname=(python-fuse python2-fuse)
pkgver=0.3.1
pkgrel=2
arch=('x86_64')
pkgdesc="Python bindings for FUSE"
url="https://github.com/libfuse/python-fuse"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'fuse2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/libfuse/python-fuse/archive/v$pkgver.tar.gz")
sha512sums=('88c01c1f9ca3cd9738711a621b782ec190adaf068a79fdea2c362d865c38fea73706dab48b8346b21502d6b4d404403a261f96e2570254809976a3018060e188')

build() {
  cd python-fuse-$pkgver
  python setup.py build
  python2 setup.py build
}

package_python-fuse() {
  depends=('fuse2' 'python')

  cd python-fuse-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python2-fuse() {
  depends=('fuse2' 'python2')

  cd python-fuse-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
