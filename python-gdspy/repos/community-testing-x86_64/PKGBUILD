# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdspy
pkgver=1.6.1
pkgrel=2
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdspy"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/heitzmann/gdspy/archive/v$pkgver.tar.gz")
sha512sums=('7b1e1a163029401b3fe1c94d1ac172b0541920d10445f4b4672852e4512c96d83e2343064f1cb4ae8b1f517c065fe196ad1e338db7bda35f8ca655a334300ba6')

build() {
  cd gdspy-$pkgver
  python setup.py build
}

check() {
  cd gdspy-$pkgver
  python setup.py pytest
}

package() {
  cd gdspy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
