# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdspy
pkgver=1.6.6
pkgrel=1
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdspy"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/heitzmann/gdspy/archive/v$pkgver.tar.gz")
sha512sums=('fad77202872403806561f7a39524bbb0cd6880d0b5e7ef30bca5ec1322987783dc400b97c8e82966488ba64eb34f9086297ade6b8dfc3f3aaacde2283e9ffd96')

build() {
  cd gdspy-$pkgver
  python setup.py build
}

check() {
  cd gdspy-$pkgver
  python setup.py pytest
}

package() {
  cd gdspy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
