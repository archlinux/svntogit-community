# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdspy
pkgver=1.4
pkgrel=1
pkgdesc="Return the gdspy true value of an iterable"
url="https://github.com/heitzmann/gdspy"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/heitzmann/gdspy/archive/v$pkgver.tar.gz")
sha512sums=('68f7746b0f4d4b64df14985bf0930e9de9dfb70032d51555ba9407cca6757f58e83cff2325c4fe19f66e21b08c888a98878a17cace23ff9353b56a074bd6a395')

build() {
  cd gdspy-$pkgver
  python setup.py build
}

check() {
  cd gdspy-$pkgver
  python setup.py pytest
}

package() {
  cd gdspy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
