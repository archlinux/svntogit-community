# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdstk
pkgver=0.9.29
_commit=077809b9fb4f0958c3f3f2752da08bc1e9bf0527
pkgrel=2
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdstk"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('git' 'python-setuptools' 'cmake')
checkdepends=('python-pytest-runner')
source=("git+https://github.com/heitzmann/gdstk.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd gdstk
  python setup.py build
}

check() {
  cd gdstk
  python setup.py pytest
}

package() {
  cd gdstk
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
