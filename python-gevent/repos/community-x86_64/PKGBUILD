# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-gevent
pkgver=21.12.0
pkgrel=1
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
depends=('c-ares' 'libev' 'python-greenlet' 'python-setuptools' 'python-zope-event' 'python-zope-interface')
makedepends=('cython')
checkdepends=('lsof' 'python-perf' 'python-objgraph' 'python-dnspython' 'python-requests')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('91cc1a9a2c6c1789cbb045d5ee3f23e4115b7d06738e28bf410d7a2db74b6ec9bde0f2ba963c6e93a746febab1483145ec77d53482ac0fa1bb2b7e3deb4b4a4e')

export EMBED=0

build() {
  cd gevent-$pkgver

  CYTHON=cython \
  PYTHON=python \
  python setup.py build
}

check() {
  (
    cd "$srcdir"/gevent-$pkgver
    python -m venv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py install
    python -m gevent.tests
  ) || echo "Tests failed"
}

package() {
  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
