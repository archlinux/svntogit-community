# $Id: PKGBUILD 70305 2012-05-04 08:21:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-gevent
pkgname=(python-gevent python2-gevent)
pkgver=1.2.2
pkgrel=1
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
makedepends=('python-greenlet' 'python2-greenlet' 'cython' 'cython2')
checkdepends=('lsof')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('4ec12e3e4feae6e90ad9c54265c7b748ebd0c010511738130156d14b8c7e84b8922ea0033218cd44cb8bbbfe5e5b30babdd8f9869e0c91bfb2552f935938f5c4')

prepare() {
  sed -i 's/C.UTF-8/en_US.UTF-8/' gevent-$pkgver/Makefile
  cp -a gevent-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/gevent-$pkgver

  EMBED=1 \
  CYTHON=cython \
  PYTHON=python \
  python setup.py build

  cd "$srcdir"/gevent-$pkgver-py2

  EMBED=1 \
  CYTHON=cython2 \
  PYTHON=python2 \
  python2 setup.py build
}

check() {
  cd "$srcdir"/gevent-$pkgver
  PYTHON=python PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" make fulltoxtest || warning "Tests failed"

  cd "$srcdir"/gevent-$pkgver-py2
  PYTHON=python2 PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" make fulltoxtest || warning "Tests failed"
}

package_python-gevent() {
  depends=('python-greenlet')

  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gevent() {
  depends=('python2-greenlet')

  cd gevent-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
