# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-gevent
pkgver=22.08.0
pkgrel=1
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
depends=('c-ares' 'libev' 'python-greenlet' 'python-setuptools' 'python-zope-event' 'python-zope-interface')
makedepends=('cython')
checkdepends=('lsof' 'python-perf' 'python-objgraph' 'python-dnspython' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('0655e36f7cbb4b6570e176a1d807ab0e2761a7812edf1367b459ecd6296fea601798fd0ba92dc418fadc0ab0b542df9f1478c81283750f3522b2c4c89dae8caf')

export EMBED=0

build() {
  cd gevent-$pkgver

  CYTHON=cython \
  PYTHON=python \
  python setup.py build
}

check() {
  (
    cd gevent-$pkgver
    python -m venv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py install
    python -m gevent.tests
  ) || echo "Tests failed"
}

package() {
  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
