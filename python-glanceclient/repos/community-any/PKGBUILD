# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-glanceclient
pkgver=3.2.0
pkgrel=1
pkgdesc="OpenStack Image API Client Library"
arch=('any')
url="https://docs.openstack.org/developer/python-glanceclient"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
         'python-requests' 'python-warlock' 'python-oslo-utils' 'python-oslo-i18n' 'python-wrapt'
         'python-pyopenssl')
makedepends=('python-setuptools' 'python-pbr')
checkdepends=('python-stestr' 'python-ddt' 'python-mock' 'python-requests-mock' 'python-testscenarios')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-glanceclient/archive/$pkgver.tar.gz")
sha512sums=('f20a3b3bc2df8664e71824ca690a9b5ba93aa4d267b5e90a892f436f8cf7b3237b993f609ff9015ab67aef6be5d9d3a00b488d3d928816d97b43b8ff8f08d350')

export PBR_VERSION=$pkgver

build() {
  cd python-glanceclient-$pkgver
  python setup.py build
}

check() {
  cd python-glanceclient-$pkgver
  stestr run
}

package() {
  cd python-glanceclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
