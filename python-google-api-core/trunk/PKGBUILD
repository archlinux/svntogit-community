# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=python-google-api-core
pkgver=2.3.2
pkgrel=2
pkgdesc="Google API client core library"
arch=('any')
url="https://github.com/googleapis/googleapis"
license=('Apache')
depends=('python-setuptools' 'python-pytz' 'python-requests' 'python-googleapis-common-protos' 'python-google-auth')
optdepends=('python-grpcio: for grpc support')
source=("https://files.pythonhosted.org/packages/source/g/google-api-core/google-api-core-${pkgver}.tar.gz")
sha256sums=('c8889f45cf58deca522888ae1d39b2a25e93e7d1b019ae8cee6456d5c726a40c')

build() {
  cd "google-api-core-${pkgver}"
  python setup.py build
}

package() {
  cd "google-api-core-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
  chmod -R a+r "$pkgdir"/usr/lib/python*/site-packages/google_api_core*.egg-info/
}
