# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=2.3.3
pkgrel=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('python-cachetools' 'python-certifi' 'python-pyasn1-modules' 'python-rsa'
         'python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses' 'python-pyopenssl' 'python-pyu2f')
source=("https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('34c3ccdef0a966eb3455e523941ef0ea301892b1dd5c620774d876a014fac7607817d9c89bfdee320c61e3b7c2679f94343aa50e56c03452a21c8a793a724995')

prepare() {
  sed -i 's|cachetools>=2.0.0,<5.0|cachetools>=2.0.0|' google-auth-library-python-$pkgver/setup.py
  sed -i 's|utils.int_from_bytes|int.from_bytes|' google-auth-library-python-$pkgver/google/auth/crypt/es256.py
}

build() {
  cd google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  cd google-auth-library-python-$pkgver
  python -m pytest tests
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
