# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=1.14.1
pkgrel=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-setuptools'
         'python-cachetools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses' 'python-pyopenssl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver.tar.gz")
sha512sums=('237c34abe40688f7556d330f77ef9f6dc5ea984bd706bbf692f2abb93de8141701c447fdefb384524e7f2b69a1786e3cdeafc5b544260b98b791665854c3dfbf')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         -e 's|import certifi|pass|' \
         google-auth-library-python-$pkgver/google/auth/transport/{urllib3,requests}.py
}

build() {
  cd google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd google-auth-library-python-$pkgver
  python -m pytest tests --deselect tests/transport/test_urllib3.py::test__make_default_http_without_certifi
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
