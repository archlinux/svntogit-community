# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-google-auth
pkgname=(python-google-auth python2-google-auth)
pkgver=1.6.3
pkgrel=2
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
makedepends=('ca-certificates' 'python-setuptools' 'python2-setuptools' 'python-pyasn1-modules'
             'python2-pyasn1-modules' 'python-rsa' 'python2-rsa' 'python-six' 'python2-six'
             'python-cachetools' 'python2-cachetools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-requests' 'python2-requests' 'python-flask' 'python2-flask'
              'python-oauth2client' 'python2-oauth2client' 'python-pytest-localserver'
              'python2-pytest-localserver' 'python-cryptography' 'python2-cryptography')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver.tar.gz"
        'pytest5.patch')
sha512sums=('3becc5301d982532d59bf6c3d0c2ebe6ea8f53b6ebdbf71d2dfc622f7933c3472102af62e43c9292c7361d458e812e8af71c7d426efbfff282f3db96de1d3374'
            '3df316f27b81b128798807dac69add8d99fcbad0ab157763cdc7d3c81535ee4a57b5dcec96e37a56e106dbb1f033ba83972c3073d18bd028b0124fc95aa56daa')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         google-auth-library-python-$pkgver/google/auth/transport/urllib3.py
  patch -Np1 -d google-auth-library-python-$pkgver <pytest5.patch
  cp -a google-auth-library-python-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/google-auth-library-python-$pkgver
  python setup.py build

  cd "$srcdir"/google-auth-library-python-$pkgver-py2
  python2 setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd "$srcdir"/google-auth-library-python-$pkgver
  python setup.py pytest --addopts "tests -k 'not test__make_default_http_without_certifi'"

  cd "$srcdir"/google-auth-library-python-$pkgver-py2
  python2 setup.py pytest --addopts "tests -k 'not test__make_default_http_without_certifi'"
}

package_python-google-auth() {
  depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-six' 'python-cachetools')

  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-google-auth() {
  depends=('ca-certificates' 'python2-pyasn1-modules' 'python2-rsa' 'python2-six' 'python2-cachetools')

  cd google-auth-library-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
