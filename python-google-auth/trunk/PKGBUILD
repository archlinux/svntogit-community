# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=1.11.3
pkgrel=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-setuptools'
         'python-cachetools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver.tar.gz")
sha512sums=('fa5959d93314d431778bca83a1ae33e189e0e74237600e69ce1df5bc0374aba3f1b6751bb4ad4ec1f75fbbe25e4aa76cad552131ec8222df72adff31c55bf300')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         google-auth-library-python-$pkgver/google/auth/transport/urllib3.py
}

build() {
  cd google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd google-auth-library-python-$pkgver
  python -m pytest tests --deselect tests/transport/test_urllib3.py::test__make_default_http_without_certifi
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
