# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>

pkgname=python-graphene
pkgver=3.0.0b6
pkgrel=3
pkgdesc='GraphQL framework for Python'
arch=(any)
url=https://graphene-python.org/
license=(MIT)
depends=(
  python-aniso8601
  python-graphql-core
  python-graphql-relay
  python-six
)
makedepends=(
  git
  python-setuptools
)
_tag=84582eb3749a3f56968facfc4c7d41f5887a9b0a
source=(git+https://github.com/graphql-python/graphene.git#tag=${_tag})
sha512sums=(SKIP)

pkgver() {
  cd graphene

  git describe --tags | sed 's/^v//'
}

build() {
  cd graphene

  python setup.py build
}

package() {
  cd graphene

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-graphene
}

# vim: ts=2 sw=2 et:
