# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-graphql-core
pkgver=3.2.0
pkgrel=1
pkgdesc='GraphQL base implementation for Python'
url=https://github.com/graphql-python/graphql-core
arch=(any)
license=(MIT)
depends=(
  python-six
  python-promise
  python-rx
)
makedepends=(
  git
  python-setuptools
)
_tag=66e5ae9d4936c3d28dfe00290d8316f2acdefd30
source=(git+https://github.com/graphql-python/graphql-core.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd graphql-core

  git describe --tags | sed 's/^v//'
}

build() {
  cd graphql-core

  python setup.py build
}

package() {
  cd graphql-core

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-graphql-core/
}

# vim: ts=2 sw=2 et:
