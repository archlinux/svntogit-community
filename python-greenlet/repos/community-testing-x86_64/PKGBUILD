# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-greenlet
pkgver=1.1.2
pkgrel=3
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('x86_64')
url="https://pypi.org/project/greenlet/"
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz")
sha512sums=('e7bcc1bfffc80fcaee735ee69f6f53e8f68bd4d3ab8ad6ed813684ffad72340a347e4810f18cc32d9c3948b5c995aefc3a53ce4ae60324093dae12042d33183b')

build() {
	cd "$srcdir"/greenlet-$pkgver
	python setup.py build
}

check() {
    cd "$srcdir"/greenlet-$pkgver
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10" python -m unittest discover -v greenlet.tests
}

package() {
    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}
