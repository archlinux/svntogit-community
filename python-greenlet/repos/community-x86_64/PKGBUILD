# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-greenlet
pkgver=1.0.0
pkgrel=2
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('x86_64')
url="https://pypi.org/project/greenlet/"
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz")
sha512sums=('7b615792c340e0eb8ca61bd09bde48ed15110d97254910bd8598312e15adb7267ba795e9e6b52f0998db27d8413d2e1aa9e977511c0d09cd3a5583b37475f882')

build() {
	cd "$srcdir"/greenlet-$pkgver
	python setup.py build
}

check() {
    cd "$srcdir"/greenlet-$pkgver
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" python -m unittest discover -v greenlet.tests
}

package() {
    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}
