# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-greenlet
pkgname=(python-greenlet python2-greenlet)
pkgver=0.4.15
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('x86_64')
url="https://pypi.org/project/greenlet/"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz")
sha512sums=('a3b7856aadc988fe153f5cf62552dd6219358f35ee2ca136e5eb5c9871cb7545986753af299e6b3e95877e9aa564559e95c548785f78e680766630b047a1ec89')

prepare() {
    cp -a greenlet-$pkgver{,-py2}
}

build() {
	cd "$srcdir"/greenlet-$pkgver
	python setup.py build

    cd "$srcdir"/greenlet-$pkgver-py2
    python2 setup.py build
}

check() {
    cd "$srcdir"/greenlet-$pkgver
    python setup.py test

    cd "$srcdir"/greenlet-$pkgver-py2
    python2 setup.py test
}

package_python-greenlet() {
    depends=('python')

    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}

package_python2-greenlet() {
    depends=('python2')

	cd greenlet-$pkgver-py2
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}
