# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-greenlet
pkgname=(python-greenlet python2-greenlet)
pkgver=0.4.7
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
url="http://pypi.python.org/pypi/greenlet"
makedepends=('python' 'python2')
checkdepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/g/greenlet/greenlet-${pkgver}.zip")
arch=('i686' 'x86_64')

prepare() {
    cp -a greenlet-$pkgver greenlet-$pkgver-py2
}

build() {
	cd greenlet-$pkgver
	python setup.py build

    cd ../greenlet-$pkgver-py2
    python2 setup.py build
}

check() {
    cd greenlet-$pkgver
    python setup.py test

    cd ../greenlet-$pkgver-py2
    python2 setup.py test
}

package_python-greenlet() {
    depends=('python')

    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir/usr/share/licenses/$pkgname/LICENSE.PSF"
}

package_python2-greenlet() {
    depends=('python2')

	cd greenlet-$pkgver-py2
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE.PSF "$pkgdir/usr/share/licenses/$pkgname/LICENSE.PSF"
}

sha512sums=('f2a080b9782bc3f91cf7971b97cb00b9d48d6021e01d413c96128ff32165fbd310ec8e9a1a00d03946e3c637162acd7eb6ebf44425580bd2e7511c3cd15fb192')
