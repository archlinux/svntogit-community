# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-greenlet
pkgname=(python-greenlet python2-greenlet)
pkgver=0.4.10
pkgrel=1
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
url="http://pypi.python.org/pypi/greenlet"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/g/greenlet/greenlet-${pkgver}.zip")
arch=('i686' 'x86_64')

prepare() {
    cp -a greenlet-$pkgver greenlet-$pkgver-py2
}

build() {
	cd greenlet-$pkgver
	python setup.py build

    cd ../greenlet-$pkgver-py2
    python2 setup.py build
}

check() {
    cd greenlet-$pkgver
    python setup.py test

    cd ../greenlet-$pkgver-py2
    python2 setup.py test
}

package_python-greenlet() {
    depends=('python')

    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir/usr/share/licenses/$pkgname/LICENSE.PSF"
}

package_python2-greenlet() {
    depends=('python2')

	cd greenlet-$pkgver-py2
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE.PSF "$pkgdir/usr/share/licenses/$pkgname/LICENSE.PSF"
}

sha512sums=('7fc78b14dca079f838647fd99878850cf9e83af781c985362146a11f71c5a700fbac8148273cd1c0ec4ac6ead4cf5bd389e001b8fc1225e87e9b51919d7ecc6c')
