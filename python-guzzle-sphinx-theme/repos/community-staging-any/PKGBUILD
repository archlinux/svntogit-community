# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com

pkgname=python-guzzle-sphinx-theme
_pkgname=guzzle_sphinx_theme
pkgver=0.7.11
pkgrel=4
pkgdesc='Sphinx theme used by Guzzle'
arch=('any')
url="https://github.com/guzzle/${_pkgname}"
license=('custom')
makedepends=('python-distribute')
depends=('python' 'python-sphinx>=1.1.3')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('f5f0e3d541c09ace912e0fa75dafd46f5a54314a929913c7de9709e677d5aa09')

build() {
  cd "${_pkgname}-$pkgver"
  python setup.py build
}

package() {
  cd "${_pkgname}-$pkgver"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dpm644 'README.rst' 'requirements.txt' -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -Dpm644 'LICENSE' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
