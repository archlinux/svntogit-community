# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-h2
pkgname=('python-h2' 'python2-h2')
pkgver=3.0.1
pkgrel=2
pkgdesc="HTTP/2 State-Machine based protocol implementation"
arch=('any')
license=('MIT')
url="http://hyper.rtfd.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-hpack' 'python2-hpack'
             'python-hyperframe' 'python2-hyperframe' 'python2-enum34')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-hypothesis'
              'python2-hypothesis')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-hyper/hyper-h2/archive/v$pkgver.tar.gz")
sha512sums=('5d98b4fb654c20f6b9e6918bb71b88a42a3027bae795f31c32c2b5cc4428774457c44a194f9c819f99b9cfd01753a6e6d8edb6c162020ff463602452d8e9057c')

prepare() {
  cp -a hyper-h2-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/hyper-h2-$pkgver
  python setup.py build

  cd "$srcdir"/hyper-h2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hyper-h2-$pkgver
  python setup.py pytest

  cd "$srcdir"/hyper-h2-$pkgver-py2
  python2 setup.py pytest
}

package_python-h2() {
  depends=('python-hpack' 'python-hyperframe')

  cd hyper-h2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-h2() {
  depends=('python2-hpack' 'python2-hyperframe' 'python2-enum34')

  cd hyper-h2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
