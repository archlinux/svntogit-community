# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Joey Dumont <joey.dumont@gmail.com>

_pkg=h5py
_mpi=openmpi
pkgbase=python-${_pkg}-${_mpi}
pkgname=("python-${_pkg}-${_mpi}" "python2-${_pkg}-${_mpi}")
pkgver=2.7.0
pkgrel=3
pkgdesc="General-purpose Python bindings for the HDF5 library (${_mpi} version)"
url="http://www.h5py.org/"
arch=('i686' 'x86_64')
license=('BSD')
makedepends=("hdf5-${_mpi}" 'cython' 'cython2' 'python-numpy' 'python2-numpy' 'python-six' 'python2-six'
             'python-pkgconfig' 'python2-pkgconfig' 'python-mpi4py' 'python2-mpi4py')
checkdepends=('inetutils')
source=("https://pypi.io/packages/source/h/${_pkg}/${_pkg}-${pkgver}.tar.gz"{,.asc})
md5sums=('f62937f40f68d3b128b3941be239dd93' 'SKIP')
validpgpkeys=('AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7') # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>

prepare() {
    # Remove RPATH
    sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" ${_pkg}-${pkgver}/setup_build.py

    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    export CC=mpicc
    cd ${_pkg}-${pkgver}
    python setup.py configure --mpi 
    python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py configure --mpi
    python2 setup.py build
}

check() {
    cd ${_pkg}-${pkgver}
    python setup.py test

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py test
}

package_python-h5py-openmpi() {
    depends=('hdf5-openmpi' 'python-numpy' 'python-six' 'cython' 'python-mpi4py')
    conflicts=('python-h5py')
    provides=('python-h5py')

    cd ${_pkg}-${pkgver}
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1

    install -D licenses/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-h5py-openmpi() {
    depends=('hdf5-openmpi' 'python2-numpy' 'python2-six' 'cython2' 'python2-mpi4py')
    conflicts=('python2-h5py')
    provides=('python2-h5py')

    cd ${_pkg}-${pkgver}-py2
    python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1

    install -D licenses/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
