# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Rich Li <rich@dranek.com>
# Contributor: Sebastien Binet <binet@lblbox>

pkgbase=python-h5py
pkgname=(python-h5py python2-h5py)
pkgver=2.4.0
pkgrel=1
pkgdesc="General-purpose Python bindings for the HDF5 library"
url="http://www.h5py.org/"
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('cython' 'cython2' 'hdf5' 'python-numpy' 'python2-numpy' 'python-six' 'python2-six')
source=("https://pypi.python.org/packages/source/h/h5py/h5py-$pkgver.tar.gz"
        DOCS_LICENSE.txt)
md5sums=('80c9a94ae31f84885cc2ebe1323d6758'
         '747a856995bb271af44733b2fdab642b')

prepare() {
  cp -a h5py-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/h5py-$pkgver
  python setup.py build

  cd "$srcdir"/h5py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/h5py-$pkgver
  python setup.py test

  cd "$srcdir"/h5py-$pkgver-py2
  python2 setup.py test
}

package_python-h5py() {
  depends=('hdf5' 'python-numpy' 'python-six' 'cython')
  conflicts=('hdf5-openmpi' 'python-h5py-openmpi')

  cd "$srcdir"/h5py-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"/ --optimize=1

  install -D "$srcdir/DOCS_LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-h5py() {
  depends=('hdf5' 'python2-numpy' 'python2-six' 'cython2')
  conflicts=('hdf5-openmpi' 'python2-h5py-openmpi')

  cd "$srcdir"/h5py-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir"/ --optimize=1

  install -D "$srcdir/DOCS_LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
