# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hacking
pkgver=3.0.0
pkgrel=2
arch=('any')
pkgdesc='OpenStack Hacking Guideline Enforcement'
url='https://docs.openstack.org/developer/hacking'
license=('Apache')
depends=('python-six' 'flake8')
makedepends=('python-setuptools' 'python-pbr' 'flake8')
checkdepends=('python-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack-dev/hacking/archive/$pkgver.tar.gz")
sha512sums=('cdb1b8edc4dd0ffc4aee38df31b43a7f89fc4b36a9289459c3ff18db2a7759d0146ad2d03d0570f5cd51489614097df7acf5bd2fd0aac4f5a92259fd44003ebc')

export PBR_VERSION=$pkgver

build() {
  cd "$srcdir"/hacking-$pkgver
  python setup.py build
}

check() {
  cd hacking-$pkgver
  stestr run
}

package_python-hacking() {
  cd hacking-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
