# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hacking
pkgver=3.1.0
pkgrel=1
arch=('any')
pkgdesc='OpenStack Hacking Guideline Enforcement'
url='https://docs.openstack.org/developer/hacking'
license=('Apache')
depends=('flake8')
makedepends=('python-pbr' 'python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/hacking/archive/$pkgver.tar.gz")
sha512sums=('ebe34824ad8416a3e2af74c2e80363b373e9bfc9341eb0d8ad87bc3fce11b098539511ab097b8d9a886171df975084ba6774c8e1fbc81f3bc30fe375ef541a04')

export PBR_VERSION=$pkgver

prepare() {
  cd hacking-$pkgver
  sed -i 's/<.*//' requirements.txt
}

build() {
  cd "$srcdir"/hacking-$pkgver
  python setup.py build
}

check() {
  cd hacking-$pkgver
  stestr run
}

package_python-hacking() {
  cd hacking-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
