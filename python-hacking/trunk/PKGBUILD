# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hacking
pkgname=('python-hacking' 'python2-hacking')
pkgver=3.0.0
pkgrel=1
arch=('any')
pkgdesc='OpenStack Hacking Guideline Enforcement'
url='https://docs.openstack.org/developer/hacking'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'flake8' 'python2-flake8')
checkdepends=('python-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack-dev/hacking/archive/$pkgver.tar.gz")
sha512sums=('cdb1b8edc4dd0ffc4aee38df31b43a7f89fc4b36a9289459c3ff18db2a7759d0146ad2d03d0570f5cd51489614097df7acf5bd2fd0aac4f5a92259fd44003ebc')

export PBR_VERSION=$pkgver

prepare() {
  cp -a hacking-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/hacking-$pkgver
  python setup.py build

  cd "$srcdir"/hacking-$pkgver-py2
  python2 setup.py build
}

check() {
  cd hacking-$pkgver
  stestr run
}

package_python-hacking() {
  depends=('python-six' 'flake8')

  cd hacking-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-hacking() {
  depends=('python2-six' 'python2-flake8')

  cd hacking-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
