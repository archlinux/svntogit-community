# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-harparser
pkgname=('python-harparser' 'python2-harparser')
pkgver=0.2
_commit=d50dba66ecb1913cde2babc4e844d5ef543cdca7
pkgrel=2
pkgdesc="Python HAR Parser Utility"
arch=('any')
license=('MIT')
url="https://github.com/JustusW/harparser"
makedepends=('python' 'python2' 'git')
source=("git+https://github.com/JustusW/harparser.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a harparser{,-py2}
}

build() {
  cd "$srcdir"/harparser
  python setup.py build

  cd "$srcdir"/harparser-py2
  python2 setup.py build
}

package_python-harparser() {
  depends=('python')

  cd harparser
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-harparser() {
  depends=('python2')

  cd harparser-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
