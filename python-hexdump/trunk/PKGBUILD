# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: knucker <www.knucker[at]gmail.com>

pkgbase=python-hexdump
pkgname=('python-hexdump' 'python2-hexdump')
_pyname=${pkgbase/python-/}
_hghash=7450ca489143
pkgver=3.2
pkgrel=3
pkgdesc='Library and tool to work with hex and binary data'
url='https://bitbucket.org/techtonik/hexdump'
arch=('any')
license=('Public Domain')
makedepends=('python' 'python2')
checkdepends=('python-tox')
source=(${pkgbase}-${pkgver}.tar.bz2::https://bitbucket.org/techtonik/${_pyname}/get/${pkgver}.tar.bz2)
sha512sums=('1739a050ccea46b8568cf624d4cf32d7205e52b06b43ce196e53528e202516e727498029ebb0e2a4cdce27332b4989e26a6b9e3c49cbce7ab4497dad410131ee')

prepare() {
  cd "techtonik-${_pyname}-${_hghash}"
  sed 's|py34|py3|g' -i tox.ini
}

check() {
  cd "techtonik-${_pyname}-${_hghash}"
  tox
}

package_python-hexdump() {
  depends=('python')

  cd "techtonik-${_pyname}-${_hghash}"
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

package_python2-hexdump() {
  depends=('python2')

  cd "techtonik-${_pyname}-${_hghash}"
  sed -r 's|(/usr/bin/env python)|\12|' -i hexdump.py
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

# vim: ts=2 sw=2 et:
