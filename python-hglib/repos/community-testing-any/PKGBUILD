# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Erik Johnson <palehose at gmail dot com>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgbase=python-hglib
pkgname=('python2-hglib' 'python-hglib')
pkgver=2.2
pkgrel=2
arch=('any')
url="https://pypi.python.org/pypi/python-hglib"
license=('MIT')
makedepends=('python2' 'mercurial' 'python')
checkdepends=('python-nose' 'python2-nose')
source=(https://pypi.python.org/packages/78/4d/62747caf1368ef440dc5ed162e1b21ef3a276b60cf2109640a5a22ea560b/${pkgbase}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('FAD61395F642FC2B33C84B9A205781AC682A2D72') # Matthew Mackall <mpm@selenic.com>
sha256sums=('ce678914d6a6b69b340cec8e2f92da7272ea015ef5973cde74d2c5010b8a9015'
            'SKIP')

prepare() {
  cp -rf "${pkgbase}-${pkgver}" "${pkgbase}-${pkgver}-2"
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python test.py

  cd "${srcdir}/${pkgbase}-${pkgver}-2"
  python2 test.py
}

package_python2-hglib() {
  pkgdesc="A library with a fast, convenient interface to Mercurial. It uses Mercurial's command server for communication with hg."
  depends=('python2' 'mercurial')
  cd "${srcdir}/${pkgbase}-${pkgver}-2"
  python2 setup.py install --root=${pkgdir} --optimize=1
}

package_python-hglib() {
  pkgdesc="A library with a fast, convenient interface to Mercurial. It uses Mercurial's command server for communication with hg."
  depends=('python' 'mercurial')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root=${pkgdir} --optimize=1
}
