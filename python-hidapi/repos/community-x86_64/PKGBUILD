# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Kevin Azzam <arch@kevin.azz.am>

pkgbase=python-hidapi
pkgname=('python-hidapi' 'python2-hidapi')
_pipname=hidapi
pkgver=0.7.99.21
_pkgver=${pkgver%.*}.post${pkgver##*.}
pkgrel=2
pkgdesc="A Cython interface to the hidapi from signal11/hidapi"
arch=('x86_64')
makedepends=('cython2' 'python2-setuptools' 'cython' 'python-setuptools' 'libusb' 'udev')
url="https://github.com/trezor/cython-hidapi"
license=('custom')
source=(https://pypi.org/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$_pkgver.tar.gz)
sha256sums=('e0be1aa6566979266a8fc845ab0e18613f4918cf2c977fe67050f5dc7e2a9a97')


prepare() {
  cp -R $_pipname-$_pkgver python2-$_pipname-$_pkgver
}

build() {
  cd "$_pipname-$_pkgver"
  python setup.py build

  cd "../python2-$_pipname-$_pkgver"
  python2 setup.py build
}


package_python-hidapi() {
  depends=('libusb' 'python')
  cd $_pipname-$_pkgver
  python setup.py install --root="$pkgdir/" --optimize=1

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  echo "You are free to use cython-hidapi code for any purpose." > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-hidapi() {
  depends=('libusb' 'python2')
  cd "python2-$_pipname-$_pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  echo "You are free to use cython-hidapi code for any purpose." > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
