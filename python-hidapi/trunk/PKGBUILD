# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Kevin Azzam <arch@kevin.azz.am>

pkgbase=python-hidapi
pkgname=('python-hidapi' 'python2-hidapi')
_pipname=hidapi
pkgver=0.7.99.20
_pkgver=${pkgver%.*}.post${pkgver##*.}
pkgrel=1
pkgdesc="A Cython interface to the hidapi from signal11/hidapi"
arch=('i686' 'x86_64')
makedepends=('cython2' 'python2-setuptools' 'cython' 'python-setuptools' 'libusb')
url="https://github.com/trezor/cython-hidapi"
license=('custom')
source=(https://pypi.org/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$_pkgver.tar.gz)
sha256sums=('5f88fd3e79deeeaacd2f266e641434a342d25b779778393191c863dd4f2dffcc')


prepare() {
  cp -R $_pipname-$_pkgver python2-$_pipname-$_pkgver
}

build() {
  cd "$_pipname-$_pkgver"
  python setup.py build

  cd "../python2-$_pipname-$_pkgver"
  python2 setup.py build
}


package_python-hidapi() {
  depends=('libusb' 'python')
  cd $_pipname-$_pkgver
  python setup.py install --root="$pkgdir/" --optimize=1

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  echo "You are free to use cython-hidapi code for any purpose." > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-hidapi() {
  depends=('libusb' 'python2')
  cd "python2-$_pipname-$_pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -dm 755 "$pkgdir/usr/share/licenses/$pkgname"
  echo "You are free to use cython-hidapi code for any purpose." > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
