# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hpack
pkgname=(python-hpack python2-hpack)
pkgver=2.3.0
pkgrel=2
pkgdesc="Pure-Python HPACK header compression"
arch=('any')
url="http://hyper.rtfd.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-hypothesis' 'python2-hypothesis')
source=("git+https://github.com/Lukasa/hpack.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a hpack{,-py2}
}

build() {
  cd "$srcdir"/hpack
  python setup.py build

  cd "$srcdir"/hpack-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hpack
  python setup.py ptr

  cd "$srcdir"/hpack-py2
  python2 setup.py ptr
}

package_python-hpack() {
  depends=('python')

  cd hpack
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hpack() {
  depends=('python2')

  cd hpack-py2
  python2 setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
