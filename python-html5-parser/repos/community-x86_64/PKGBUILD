# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=html5-parser
pkgname=python-html5-parser
pkgver=0.4.10
pkgrel=1
pkgdesc="Fast C based HTML 5 parsing for python"
arch=('x86_64')
url="https://github.com/kovidgoyal/${_pkgname}"
license=('Apache')
depends=('python-chardet' 'python-lxml')
makedepends=('python-setuptools')
checkdepends=('python-beautifulsoup4')
optdepends=('python-beautifulsoup4: to use the soup treebuilder')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('f9294418c0da95c2d5facc19d3dc32941093a6b8e3b3e4b36cc7b5a1697fbca4'
            'SKIP')
b2sums=('5759c872e65080ea059b81e632ed70a94e6677ef01af1e50ab1822128cc75ccbd3361c9d4e5e94fc4cc78996b3dbde97ffe435f083c492df2815c67d929cabc7'
        'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
