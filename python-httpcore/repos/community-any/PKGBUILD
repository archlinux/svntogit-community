# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=httpcore
pkgname=python-httpcore
# https://github.com/encode/httpcore/blob/master/CHANGELOG.md
pkgver=0.16.1
pkgrel=1
pkgdesc="A minimal HTTP client"
arch=('any')
url="https://github.com/encode/${_pkgname}"
license=('BSD')
depends=('python-anyio' 'python-h11' 'python-sniffio' 'python-certifi')
optdepends=(
  'python-h2: for HTTP/2 support'
  'python-socksio: for SOCKS support'
  'python-trio: for trio backend'
)
makedepends=('python-setuptools' 'python-h2')
checkdepends=('python-pytest-httpbin' 'python-pytest-trio' 'python-socksio')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('a160a02b6c33cdedc1cea6b72cca7e8742ac97333b6d1a3931711f10e397d5e0ba03b2c191e0a7700a1ecd667cbcac4246ef29450da8472ebcc7f19602d8b39d')

prepare() {
    cd ${_pkgname}-${pkgver}

    # disable -Werror, which often causes failures due to newer dependencies in Arch
    sed -i '/\berror\b/d' setup.cfg
}

build() {
    cd ${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd ${_pkgname}-${pkgver}

    # raise open files limits, many tests will fail otherwise
    ulimit -S -n 4096

    python -m pytest
}

package() {
    cd ${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
