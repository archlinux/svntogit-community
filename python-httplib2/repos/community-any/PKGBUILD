# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>
# Contributor: Anton Eliasson <devel@antoneliasson.se>

pkgbase=python-httplib2
pkgname=(python-httplib2 python2-httplib2)
pkgver=0.17.3
pkgrel=1
pkgdesc='Comprehensive HTTP client library, supporting many features'
url='https://github.com/httplib2/httplib2'
license=('MIT')
arch=('any')
makedepends=('ca-certificates' 'python-setuptools' 'python2-setuptools')
checkdepends=('flake8' 'python-mock' 'python-pytest-cov' 'python-pytest-forked'
              'python-pytest-timeout' 'python-pytest-xdist' 'python-pytest-randomly'
              'python-future')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/httplib2/httplib2/archive/v$pkgver.tar.gz"
        'cert.patch')
sha512sums=('a1af9c9e42584c4d14cf37f8a3cac9dc2bb9e03a47711e8736db02b798791585afe9515eaf3492a643a38fb3416ee1a21324c6a2ce8227e76a2f8f8ae062cad8'
            '503719fb07b9ff1f06f252b73804787759139098dba990a7dcd9e9b3602f7271d2251e703993636b249b3597c9a240c721c536a4fe31526ec4ba9441f6c8da56')

prepare() {
  cd httplib2-$pkgver
  patch -p0 -i "$srcdir"/cert.patch
  sed -i 's/==/>=/' requirements-test.txt

  cp -a "$srcdir"/httplib2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/httplib2-$pkgver
  python setup.py build

  cd "$srcdir"/httplib2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd httplib2-$pkgver
  python setup.py test
}

package_python-httplib2() {
  depends=('python' 'ca-certificates')

  cd httplib2-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-httplib2() {
  depends=('python2' 'ca-certificates')

  cd httplib2-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
