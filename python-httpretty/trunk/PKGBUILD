# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-httpretty
pkgname=("python-httpretty" "python2-httpretty")
pkgver=0.8.0
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/httpretty"
license=('MIT')
makedepends=('python-urllib3' 'python2-urllib3')
checkdepends=('python-nose' 'python2-nose' 'python-coverage' 'python2-coverage' 'python-mock' 'python2-mock'
              'python-sure' 'python2-sure' 'python-tornado' 'python2-tornado' 'python-requests' 'python2-requests'
              'python-httplib2' 'python2-httplib2')
source=("https://pypi.python.org/packages/source/h/httpretty/httpretty-$pkgver.tar.gz")
md5sums=('2d5ea5205c358fa489e6de6fc74c00fa')

prepare() {
  cp -r "httpretty-$pkgver"{,-py2}
}

check() {
  cd httpretty-$pkgver
  # Disabled due to hanging
  #nosetests3 -s tests

  cd ../httpretty-$pkgver-py2
  nosetests2 -s tests || :  # Failing some tests
}

package_python-httpretty() {
  depends=('python')
  optdepends=('python-urllib3: for recording actual requests')

  cd httpretty-$pkgver
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python-httpretty/COPYING"
}

package_python2-httpretty() {
  depends=('python2')
  optdepends=('python2-urllib3: for recording actual requests')

  cd httpretty-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python2-httpretty/COPYING"
}

