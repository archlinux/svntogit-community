# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hunter
pkgver=3.3.3
pkgrel=1
arch=('x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD')
url='https://github.com/ionelmc/python-hunter'
depends=('python-colorama' 'python-cymem' 'python-manhole')
optdepends=('gdb: for remote support')
makedepends=('python-pip' 'python-wheel' 'cython')
checkdepends=('python-pytest-benchmark' 'python-virtualenv' 'python-pytest' 'gdb'
              'python-process-tests' 'python-aspectlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-hunter/archive/v$pkgver.tar.gz")
sha512sums=('661b0f33653a6175f2172e45386c822552464a06129e2f5c8d4b88f6142a4aecc69bd657dc98ef7ffbb3d93cde537aa3a2e5f68bbced6f03b6514d39cfb99df4')

prepare() {
  cd python-hunter-$pkgver
  rm src/hunter/*.c
  rm -r src/hunter/vendor

  sed -i 's/from .vendor.colorama/from colorama/' src/hunter/{actions,util}.py
  sed -i 's/from .vendor._cymem.cymem/from cymem.cymem/' src/hunter/_event.pyx
}

build() {
  cd "$srcdir"/python-hunter-$pkgver
  python setup.py build
}

check() {
  # Hack to process .pth files properly
  # ptrace needed to pass the remote tests

  cd "$srcdir"/python-hunter-$pkgver
  python tests/setup.py build_ext --inplace
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" PATH="$srcdir/pyvenv/bin:$PATH" pytest
  ) || :

  # Clean up to prevent it from being installed
  rm build/lib.linux-$CARCH-3.9/*.so
}

package() {
  cd python-hunter-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
