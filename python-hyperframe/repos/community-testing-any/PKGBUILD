# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hyperframe
pkgname=('python-hyperframe' 'python2-hyperframe')
pkgver=5.1.0
pkgrel=2
pkgdesc="HTTP/2 framing layer for Python"
arch=('any')
license=('MIT')
url="http://hyper.rtfd.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-hyper/hyperframe/archive/v$pkgver.tar.gz")
sha512sums=('982d5151bf88027ad2eb39b3c02e3193dcae3250d5bad1f014ec1ea5e598305a1ec4e0bae8e41e958a897dd7ba4c61461997c1203c428a2434d158cc06eae487')

prepare() {
  cp -a hyperframe-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py build

  cd "$srcdir"/hyperframe-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py pytest

  cd "$srcdir"/hyperframe-$pkgver-py2
  python2 setup.py pytest
}

package_python-hyperframe() {
  depends=('python')

  cd hyperframe-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hyperframe() {
  depends=('python2')

  cd hyperframe-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
