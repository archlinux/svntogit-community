# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hyperframe
pkgname=('python-hyperframe' 'python2-hyperframe')
pkgver=5.2.0
pkgrel=1
pkgdesc="HTTP/2 framing layer for Python"
arch=('any')
license=('MIT')
url="https://hyper.rtfd.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-hyper/hyperframe/archive/v$pkgver.tar.gz")
sha512sums=('e85dc5d3354f19edde87e826d6b95530817a2e5a080e00d44314856c053f877c4d0741363d014e8e85d7451af32e4c5698ef409d21ff631a510f1c9f468d0c4b')

prepare() {
  cp -a hyperframe-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py build

  cd "$srcdir"/hyperframe-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py pytest

  cd "$srcdir"/hyperframe-$pkgver-py2
  python2 setup.py pytest
}

package_python-hyperframe() {
  depends=('python')

  cd hyperframe-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hyperframe() {
  depends=('python2')

  cd hyperframe-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
