# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hypothesis
pkgname=('python-hypothesis' 'python2-hypothesis')
pkgver=3.6.1
_commit=7ed4892866960154d5dffc40f32340e385434128
pkgrel=2
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python2-enum34' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'flake8' 'python2-flake8' 'python-pytz'
              'python2-pytz' 'python-numpy' 'python2-numpy' 'python-fake-factory'
              'python2-fake-factory' 'python-flaky' 'python2-flaky' 'python-pytest-benchmark'
              'python2-pytest-benchmark' 'python-django' 'python2-django')
source=("git+https://github.com/DRMacIver/hypothesis.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  sed -i 's/@pytest.mark.hookwrapper/@pytest.hookimpl(hookwrapper=True)/' hypothesis/src/hypothesis/extra/pytestplugin.py

  cp -a hypothesis{,-py2}

  rm -r hypothesis/tests/py2
  rm -r hypothesis-py2/tests/py3
}

build() {
  cd "$srcdir"/hypothesis
  python setup.py build

  cd "$srcdir"/hypothesis-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/hypothesis
  mv tests/django ../
  python setup.py ptr
  mv ../django tests/
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m tests.django.manage test tests.django

  cd "$srcdir"/hypothesis-py2
  mv tests/django ../
  python2 setup.py ptr
  mv ../django tests/
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m tests.django.manage test tests.django
}

package_python-hypothesis() {
  depends=('python')
  optdepends=('python-pytz: for datetime and django module'
              'python-fake-factory: for fakefactory and django module'
              'python-django: for django module'
              'python-numpy: for numpy module'
              'python-pytest: for pytest module')

  cd hypothesis
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-hypothesis() {
  depends=('python2-enum34')
  optdepends=('python2-pytz: for datetime and django module'
              'python2-fake-factory: for fakefactory and django module'
              'python2-django: for django module'
              'python2-numpy: for numpy module'
              'python2-pytest: for pytest module')

  cd hypothesis-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
