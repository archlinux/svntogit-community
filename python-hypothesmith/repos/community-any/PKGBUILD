# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesmith
pkgver=0.1.6
_commit=40947c2e590f06ea5f3b88c6e75d8a98e9443c63
pkgrel=1
pkgdesc="Hypothesis strategies for generating Python programs, something like CSmith"
url="https://github.com/Zac-HD/hypothesmith"
license=('MPL')
arch=('any')
depends=('python-hypothesis' 'python-lark-parser' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-black' 'python-parso' 'python-pytest-cov')
source=("https://github.com/Zac-HD/hypothesmith/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('c93341aaa3397c46331474b6b5b48efab1b50a8cf4e7359b77fa60c9e4952341fc0e8a8c3becca44599ef9b5ed05748072a0022ba79def82a2b78e0dd44e165d')

build() {
  cd hypothesmith-$_commit
  python setup.py build
}

check() {
  cd hypothesmith-$_commit
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd hypothesmith-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}
