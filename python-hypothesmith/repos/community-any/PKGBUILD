# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesmith
pkgver=0.1.7
pkgrel=1
pkgdesc="Hypothesis strategies for generating Python programs, something like CSmith"
url="https://github.com/Zac-HD/hypothesmith"
license=('MPL')
arch=('any')
depends=('python-hypothesis' 'python-lark-parser' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-black' 'python-parso' 'python-pytest-cov')
source=("https://pypi.io/packages/source/h/hypothesmith/hypothesmith-$pkgver.tar.gz")
sha512sums=('2cd24ad00489c086f9ed687ba3824216628d66c7f3eac7dc36f13117110d9a8f9e791a4be0ee7987aa1bd04d6593ccff000d3e6cc7776b905dfdda79ce20cb71')

build() {
  cd hypothesmith-$pkgver
  python setup.py build
}

check() {
  cd hypothesmith-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd hypothesmith-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
