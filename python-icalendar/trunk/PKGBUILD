# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgname=python-icalendar
pkgver=5.0.1
pkgrel=1
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=(any)
url="https://github.com/collective/icalendar"
license=(BSD)
depends=(python-dateutil python-pytz)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('35faeae20e58d0fe26e33c04bbfd08b424192d61ae3e1da1d7611ba06ab33570')
b2sums=('5cc2e17e583caa2f35d5b4723da1975d784bfcbd5620108428cc0d671002620bf0e2fbc72c83440866e3ef31c5e3136781d45dee69ea15be00c42abb6b4d5d22')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v src/$_name/tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  ln -s "$site_packages/$_name-$pkgver.dist-info/LICENSE.rst" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"

  rm -frv "$pkgdir/$site_packages/$_name/tests/"
}
