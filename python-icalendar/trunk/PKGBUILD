# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgbase=python-icalendar
pkgname=('python-icalendar' 'python2-icalendar')
pkgver=4.0.1
pkgrel=1
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=('any')
url="https://github.com/collective/icalendar"
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-dateutil' 'python-pytz' 'python2-dateutil' 'python2-pytz')
source=("${_name}-${pkgver}.tar.gz::https://github.com/collective/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('98a2f006819f589b9d7b2c9a16e8344092fd6b013f8ad32362a2820f68d352cc67796c8044c4c720e6910bb7d735e035d81b9c1239d7c25b7ec00fcb5c987c0d')

prepare() {
  # Disable failing test (https://github.com/collective/icalendar/issues/260)
  sed -i 's/test_create_america_new_york/disabled_&/' \
    "${_name}-${pkgver}"/src/icalendar/tests/test_timezoned.py
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python -m unittest discover src
  cd ../"${pkgname[1]}-${pkgver}"
  python2 -m unittest discover src
}

package_python-icalendar() {
  depends=('python-dateutil' 'python-pytz')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}/" \
    -vDm644 {CHANGES,CONTRIBUTING,README}.rst
  install -vDm644 LICENSE.rst \
    "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python2-icalendar() {
  depends=('python2-dateutil' 'python2-pytz')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  mv -v "${pkgdir}/usr/bin/${_name}" "${pkgdir}/usr/bin/${_name}2"
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}/" \
    -vDm644 {CHANGES,CONTRIBUTING,README}.rst
  install -vDm644 LICENSE.rst \
    "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
}

# vim:set ts=2 sw=2 et:

