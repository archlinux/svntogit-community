# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.3.5
pkgrel=3
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/pre-commit/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance-s' 'python-pytest')
optdepends=('python-editdistance-s: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/pre-commit/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('89d40df2fae167a348c14b5fa20cf3a3fc83e1f42a6fabbeb02935f873a5a0366cfddf51c358f35f1bf15846b357647d229a180afec4e71fdf6ba44c0d109bd6')
b2sums=('9fac1817c173fac8b82fd805c5c5602d964cccd001e1d7dc6d7350c72b4f436613e47989fdd2e6f866d56c52871a4d1aa848d1761b03cf728446d483276bd6a6')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
