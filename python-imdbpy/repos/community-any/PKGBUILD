# Maintainer: David Runge <dave@sleepmap.de>

_name=IMDbPY
pkgname=python-imdbpy
pkgver=6.8
pkgrel=3
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('64b0ab95ca848530d9df3d0da2319497cb1af779d229cb64c1bf59db3cd3e42331cfa854a24222cab5558b9d9af8616098d794756dece463bfefdb3ebdaf42c5')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix install folder for docs and config
  sed -e "s/'doc'/'share\/doc\/python-imdbpy\/'/" \
      -e "s/'etc'/'\/etc'/" -i setup.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  # flaky tests are flaky: https://github.com/alberanid/imdbpy/issues/240
  py.test -k 'not test_movie_genres_if_single_should_be_a_list_of_genre_names \
              and not test_movie_runtimes_with_countries_should_include_context \
              and not test_movie_full_credits_for_tv_show \
              and not test_person_height_should_be_in_inches_and_meters \
              and not test_movie_sound_mix_if_multiple_with_notes_should_include_notes \
              and not test_selected_unreleased_movie_should_have_correct_state'
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
