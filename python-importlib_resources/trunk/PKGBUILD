# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.1.4
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=('any')
url="https://github.com/python/importlib_resources"
license=('Apache')
depends=('python')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest' 'python-tests')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('ede757b0ee4a246a3446a165f4c8e56edf326f90464f01d853b6c04042371ff1edc4e41f388c6ea6973302a183cf1c04ee21335342886747c991ba0b38c7aec3')
b2sums=('e88cbe34dfd9b251fc78f2aba173f3eb657462777cb66454fbc1735575c6cac6a868f5e40206fb8925aa8c7f708d72624693cd772f2335231bc5da4f417d622e')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
