# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>

pkgbase=python-inet_diag
pkgname=('python-inet_diag' 'python2-inet_diag')
pkgver=0.2.1
pkgrel=4
pkgdesc="Python bindings for the inet_diag kernel interface"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/libs/python/python-inet_diag/python-inet_diag.git/"
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://git.kernel.org/pub/scm/libs/python/${pkgbase}/${pkgbase}.git/snapshot/${pkgbase}-${pkgver}.tar.gz")
sha512sums=('fe9c5a51d0b29abdb765990db37ab6f399aa5f46fa068655892181fea413d41a07486af7509540518a765d03fb4981a8a8784e5aa8b0367bb65bc36ef136be32')

prepare() {
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
  cd "${pkgname[0]}-${pkgver}"
  sed -e '1s/python2/python/' -i setup.py
  cd ../"${pkgname[1]}-${pkgver}"
  sed -e '1s/python/&2/' -i *.py
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

package_python-inet_diag() {
  depends=('python')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm775 psk.py "${pkgdir}/usr/bin/psk"
  install -vDm775 pss.py "${pkgdir}/usr/bin/pss"
}

package_python2-inet_diag() {
  depends=('python2')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm775 psk.py "${pkgdir}/usr/bin/psk2"
  install -vDm775 pss.py "${pkgdir}/usr/bin/pss2"
}
# vim:set ts=2 sw=2 et:

