# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>

pkgname=python-inet_diag
pkgver=0.2.1
pkgrel=4
pkgdesc="Python bindings for the inet_diag kernel interface"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/libs/python/python-inet_diag/python-inet_diag.git/"
license=('GPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://git.kernel.org/pub/scm/libs/python/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('fe9c5a51d0b29abdb765990db37ab6f399aa5f46fa068655892181fea413d41a07486af7509540518a765d03fb4981a8a8784e5aa8b0367bb65bc36ef136be32')

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -e '1s/python2/python/' -i setup.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package_python-inet_diag() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm 775 psk.py "${pkgdir}/usr/bin/psk"
  install -vDm 775 pss.py "${pkgdir}/usr/bin/pss"
}
