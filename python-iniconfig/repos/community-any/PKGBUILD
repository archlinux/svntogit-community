# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-iniconfig
pkgver=2.0.0
_commit=93f5930e668c0d1ddf4597e38dd0dea4e2665e7a
pkgrel=4
pkgdesc="brain-dead simple config-ini parsing"
url="https://github.com/RonnyPfannschmidt/iniconfig"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-hatchling' 'python-hatch-vcs')
checkdepends=('python-pytest')
source=("git+https://github.com/RonnyPfannschmidt/iniconfig.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd iniconfig
  python -m build -nw
}

check() {
  cd iniconfig
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd iniconfig
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
