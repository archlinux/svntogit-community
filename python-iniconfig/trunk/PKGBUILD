# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-iniconfig
pkgname=(python-iniconfig python2-iniconfig)
pkgver=1.0.0
pkgrel=3
pkgdesc="brain-dead simple config-ini parsing"
url="https://github.com/RonnyPfannschmidt/iniconfig"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
             'python2-setuptools-scm')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/RonnyPfannschmidt/iniconfig/archive/v$pkgver.tar.gz"
        'pytest5.patch')
sha512sums=('d8048af40145a5312784c5d1a9afaa052528ffdef56e47aa9be7f727d946426a43b8e934edce6b46024b83771c151ef544787e8422c74adeda2ccc8913aedd5c'
            'db500dcdbc358db130569c8f7a199e34b37a4f0693dc99064fdcc4f7968048f118f942bed84332c4399fea9f64d1d84ed5e3502ba26e48e7290d2ca31ea87638')

prepare() {
  patch -Np1 -d iniconfig-$pkgver <pytest5.patch
  cp -a iniconfig-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/iniconfig-$pkgver
  python setup.py build

  cd "$srcdir"/iniconfig-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/iniconfig-$pkgver
  python setup.py pytest

  cd "$srcdir"/iniconfig-$pkgver-py2
  python2 setup.py pytest
}

package_python-iniconfig() {
  depends=('python')

  cd iniconfig-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-iniconfig() {
  depends=('python2')

  cd iniconfig-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
