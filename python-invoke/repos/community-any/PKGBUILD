# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.4.0
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('a6479780bd9e297c500b1f695bc5aa423000d4732a40d77ab1a175823f3a7dcf538cd040904fc62937c7df746850f92b276eb3513db0413bd74a30404d3ee395')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
}

build() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
