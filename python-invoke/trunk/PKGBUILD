# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.7.0
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python-fluidity' 'python-lexicon' 'python-six' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz"
        invoke-devendor.patch)
sha512sums=('18754e8e6e6310df19065ddf73b2adadf2164fa919412604ae522b78b67c22f778a9b08d673ad762466aea251edc1f92963df350e0905dec6b0b89b8a8aa9501'
            'c5e414990f7a36455ff28288f90cccd62e570775523ea307f4ed2b7cdc7b9b0c1ee2d2eb046a048843fd2f016aed0a3cf270355297a1d73119f83edf8bc37be9')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
  rm -r invoke-$pkgver/invoke/vendor

  patch -d invoke-$pkgver -p1 < invoke-devendor.patch
}

build() {
  cd invoke-$pkgver
  python setup.py build
}

check() {
  cd invoke-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest tests
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
