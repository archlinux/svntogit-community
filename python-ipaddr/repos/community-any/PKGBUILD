# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Liu <net147@gmail.com>

pkgbase=python-ipaddr
pkgname=('python-ipaddr' 'python2-ipaddr')
pkgver=2.1.10
pkgrel=1
pkgdesc="An IPv4/IPv6 manipulation library in Python"
arch=('any')
url="http://code.google.com/p/ipaddr-py/"
license=('APACHE')
makedepends=('python' 'python2' 'python-distribute' 'python2-distribute')
source=("http://ipaddr-py.googlecode.com/files/ipaddr-${pkgver}.tar.gz"
        "python3.patch")
md5sums=('f315ac829218e9735c5d772d59a3e0e7'
         '030b8f2479eb223b369b07c1239377b2')

build() {
  cd "${srcdir}/ipaddr-${pkgver}"
  patch -Np1 -i "${srcdir}/python3.patch"
}

package_python-ipaddr() {
  depends=('python')
  cd "${srcdir}/ipaddr-${pkgver}"
  python setup.py install --root="${pkgdir}" -O1
}

package_python2-ipaddr() {
  depends=('python2')
  cd "${srcdir}/ipaddr-${pkgver}"
  python2 setup.py install --root="${pkgdir}" -O1
}

# vim:set ts=2 sw=2 et:
