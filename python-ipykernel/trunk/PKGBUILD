# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-ipykernel
pkgver=6.9.2
pkgrel=1
pkgdesc="The ipython kernel for Jupyter"
arch=('any')
url="https://pypi.org/project/ipykernel/"
license=('BSD')
depends=('python-traitlets' 'python-tornado' 'ipython' 'python-jupyter_client' 'python-debugpy')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-mock' 'python-flaky' 'python-ipyparallel')
source=("ipykernel-$pkgver.tgz::https://github.com/ipython/ipykernel/archive/v$pkgver.tar.gz")
md5sums=('3eca3a689db1ee79c6252ac29cfb6352')

# dep cycle with python-jupyter_client

build() {
  cd ipykernel-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ipykernel-$pkgver
  pytest
}

package() {
  cd ipykernel-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
