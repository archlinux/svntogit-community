# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-ipywidgets
_pipname=ipywidgets
pkgver=7.7.1
pkgrel=1
pkgdesc="IPython HTML widgets for Jupyter"
arch=('any')
url="https://github.com/ipython/ipywidgets"
license=('BSD')
depends=('python-ipykernel' 'python-jsonschema' 'python-ipython-genutils')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
md5sums=('d31f524b9ab382ce92a6a1a4c678b077')

build() {
  cd ipywidgets-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ipywidgets-$pkgver
  pytest -v
}

package() {
  cd ipywidgets-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
