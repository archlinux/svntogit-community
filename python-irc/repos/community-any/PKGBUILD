# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Pierre Chapuis <catwell@archlinux.us>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgbase=python-irc
pkgname=('python-irc' 'python2-irc')
pkgver=8.9.1
pkgrel=1
pkgdesc="IRC (Internet Relay Chat) protocol client library for Python"
depends=('python-six')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
arch=('any')
url="http://pypi.python.org/pypi/irc"
license=('LGPL')
install='python-irc.install'
source=(http://pypi.python.org/packages/source/i/irc/irc-$pkgver.zip
        jaraco.py)
md5sums=('b19439f713a33d13585302e15c45bc26'
         '90a2ebb75d5b5263ea8f688f024a93d5')

build() {
  cd "$srcdir"
  # todo, convince author not to use his own misc lib
  cp jaraco.py "irc-$pkgver/irc/"
  pushd "irc-$pkgver"
  sed -i 's/jaraco.util.itertools/irc.jaraco/' irc/client.py
  sed -i '25i irc/jaraco.py' irc.egg-info/SOURCES.txt
  popd
  cp -a "irc-$pkgver" "irc2-$pkgver"
}

package_python2-irc()
{
  depends=('python2-six')
  install='python2-irc.install'

  cd "$srcdir/irc2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
}

package_python-irc()
{
  depends=('python-six')
  install='python-irc.install'

  cd "$srcdir/irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python3 setup.py install --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  find "$pkgdir/" -type d -empty -delete
}

check()
{
  cd "$srcdir/irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python3 setup.py test 

  cd "$srcdir/irc2-$pkgver"
  python2 setup.py test 
}
