# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-itemadapter
pkgver=0.1.1
pkgrel=3
pkgdesc="Common interface for data container classes"
url="https://github.com/scrapy/itemadapter"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-attrs' 'python-pytest' 'scrapy')
source=("https://github.com/scrapy/itemadapter/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b3ab8cbee8d75bf6b98c8438125585e1aeee4e94aa203e1fba864446c48c3015e8778078e56ba8b3709fec92e57c40bf0d3a5b0c82cc65f42eff0a77e95c0bc8')

build() {
  cd itemadapter-$pkgver
  python setup.py build
}

check() {
  cd itemadapter-$pkgver
  pytest
}

package() {
  cd itemadapter-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
