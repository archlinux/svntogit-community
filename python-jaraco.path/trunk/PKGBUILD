# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.path
pkgver=3.4.0
_commit=697b5423fa86e2c66c86304705557f96b42b77cc
pkgrel=5
pkgdesc="Miscellaneous path functions"
url="https://github.com/jaraco/jaraco.path"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-enabler' 'python-pytest-mypy')
source=("git+https://github.com/jaraco/jaraco.path.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd jaraco.path
  python -m build -wn
}

check() {
  cd jaraco.path
  python -m pytest
}

package() {
  cd jaraco.path
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
