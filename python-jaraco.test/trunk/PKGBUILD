# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jaraco.test
pkgver=5.0.0
pkgrel=4
pkgdesc="Testing support by jaraco"
url="https://github.com/jaraco/jaraco.test"
license=('MIT')
arch=('any')
depends=('python-jaraco.collections' 'python-jaraco.context' 'python-jaraco.functools' 'python-toml'
         'python-more-itertools')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-checkdocs' 'python-pytest-cov'
              'python-pytest-enabler' 'python-pytest-flake8' 'python-pytest-mypy'
              'python-pip')
source=("https://github.com/jaraco/jaraco.test/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5a5c3268b570269f5d18166c9c90973e18acbd14f382c08a8576f8612fc477d739d1e8714aa9cbdf0cb2da9551a0a19da33976ed443449007eb6b300a055f987')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd jaraco.test-$pkgver
  python setup.py build
}

check() {
  cd jaraco.test-$pkgver
  python -m pytest
}

package() {
  cd jaraco.test-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
