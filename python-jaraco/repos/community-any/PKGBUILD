# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jaraco
pkgver=2021.03.28
pkgrel=3
pkgdesc='A gaggle of idiosyncratic and questionable wrappers for the stdlib.'
arch=('any')
url='https://github.com/jaraco?page=1&tab=repositories&utf8=%E2%9C%93&q=jaraco'
license=('MIT')
depends=('python-six' 'python-pytz' 'python-more-itertools' 'python-toml')
makedepends=('python-setuptools-scm')
provides=('python-jaraco-text'
          'python-jaraco-functools'
          'python-jaraco-collections'
          'python-jaraco-classes'
          'python-jaraco-stream'
          'python-jaraco-logging'
          'python-jaraco-itertools')

# These should not exist and do not deserve so many packages.
# Furthermore, try to make a replacement for python-irc so we can nuke it all.
# And now cherrypy depends on this too :-(
_text=3.5.0
_functools=3.3.0
_collections=3.3.0
_classes=3.2.1
_stream=3.0.2
_logging=3.1.0
_itertools=6.0.1
_pyhost="https://files.pythonhosted.org/packages/source"

source=("$_pyhost/j/jaraco.text/jaraco.text-$_text.tar.gz"
        "$_pyhost/j/jaraco.functools/jaraco.functools-$_functools.tar.gz"
        "$_pyhost/j/jaraco.collections/jaraco.collections-$_collections.tar.gz"
        "$_pyhost/j/jaraco.classes/jaraco.classes-$_classes.tar.gz"
        "$_pyhost/j/jaraco.stream/jaraco.stream-$_stream.tar.gz"
        "$_pyhost/j/jaraco.logging/jaraco.logging-$_logging.tar.gz"
        "$_pyhost/j/jaraco.itertools/jaraco.itertools-$_itertools.tar.gz")
sha512sums=('da566f1a6d8343d05c51ca3f4635212094ddbc84fe9b460dd7630030a9b5bb28c97ccb0de5928da50230a182985c7e5bce8903d4019ce5afb89067ad938dd152'
            'fc7c5a11a3411692066f4437d76ace7e8a00af2152256ea9f730254b577e74163342298d5b33d472c744cbad5dd316b56345d05aa93ef6f82bc92d6aadb2f2ec'
            '23fea645226bcfbd5f034877aff7846960ad99d65d451bdbf5b8abb166356074d07780ea8e2e25d31fd03343929596dcd64a9ec949c1ff056c11862e043a8b7e'
            '4c85cfdd1da88de01596867365147be6cadd7e1d230798683236fdfde579747e3309aac95ae4f3ef86c91bb4493f3096c4ecc14e3db764ef308aab41ead88e23'
            'fce486caa9f27f3cfe7775858892d2865fe1c1656a8d369ef9321057716f25d7a737abf2a28fe7827efbeba2daa3b7992ec4fd18807751bf1fee6f48b238ab01'
            'ce9a9cbad6d6bb448615eac986d361f9c6ff477b1209e85433f12a49055a9e30125cd49bebcf915a08b8efe0732439a8e1d336e63f6454369b3e846a9a1383ba'
            '60a4bf4335bd159b1b2ed532f4b72c47984a8e2afc03bafb7ccb53095da787cecbcb6207bb2c521266285bd5bf0550f1a78dd5323266bde6e00937481599a506')

prepare() {
  # remove inflect dep
  cd "$srcdir/jaraco.itertools-$_itertools"
  sed -i "s/'inflect',//" setup.py
  sed -i "s/inflect//"    jaraco.itertools.egg-info/requires.txt
  sed -i "s/inflect//"    setup.cfg
  sed -i 's/import inflect//' jaraco/itertools.py
  sed -i 's/ord = .*$/ord = str(self.sliceArgs[2])+"th"/' jaraco/itertools.py
}

package() {
  cd "$srcdir/jaraco.text-$_text"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.functools-$_functools"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.collections-$_collections"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.classes-$_classes"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.stream-$_stream"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.logging-$_logging"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  cd "$srcdir/jaraco.itertools-$_itertools"
  python3 setup.py install --root="$pkgdir/" --optimize=0

  #rm "$pkgdir/usr/bin/calc-prorate"
  #rmdir "$pkgdir/usr/bin"
  # None of these packages have a license file?  Fix later.
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
