# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Mariusz Szczepa≈Ñczyk <mszczepanczyk@gmail.com>

pkgbase=python-jdcal
pkgname=('python2-jdcal' 'python-jdcal')
pkgver=1.2
pkgrel=1
pkgdesc="Julian dates, from proleptic Gregorian and Julian calendars"
arch=('any')
url="https://github.com/phn/jdcal"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("http://pypi.python.org/packages/source/j/jdcal/jdcal-${pkgver}.tar.gz")
md5sums=('ab8d5ba300fd1eb01514f363d19b1eb9')

build() {
  cd "$srcdir"
  cp -a jdcal-${pkgver} jdcal-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/jdcal-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/jdcal-${pkgver}
  python setup.py build
}

package_python2-jdcal() {
  depends=('python2')

  cd "$srcdir"/jdcal-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-jdcal() {
  depends=('python')

  cd "$srcdir"/jdcal-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

