# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Mariusz Szczepa≈Ñczyk <mszczepanczyk@gmail.com>

pkgbase=python-jdcal
pkgname=('python2-jdcal' 'python-jdcal')
pkgver=1.0
pkgrel=0
pkgdesc="Julian dates, from proleptic Gregorian and Julian calendars"
arch=('any')
url="https://github.com/phn/jdcal"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("http://pypi.python.org/packages/source/j/jdcal/jdcal-${pkgver}.tar.gz"
        "LICENSE")
md5sums=('ae4a28210426f575232cb5a48d478d08'
         'a933325008e394f91f4dfab09a4929fd')

build() {
  cd "$srcdir"
  cp -a jdcal-${pkgver} jdcal-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/jdcal-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/jdcal-${pkgver}
  python setup.py build
}

package_python2-jdcal() {
  depends=('python2')

  cd "$srcdir"/jdcal-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-jdcal() {
  depends=('python')

  cd "$srcdir"/jdcal-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

