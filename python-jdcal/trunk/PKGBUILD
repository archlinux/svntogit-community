# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Mariusz Szczepa≈Ñczyk <mszczepanczyk@gmail.com>

pkgbase=python-jdcal
pkgname=('python2-jdcal' 'python-jdcal')
_pkghash="9b/fa/40beb2aa43a13f740dd5be367a10a03270043787833409c61b79e69f1dfd"
pkgver=1.3
pkgrel=2
pkgdesc="Julian dates, from proleptic Gregorian and Julian calendars"
arch=('any')
url="https://github.com/phn/jdcal"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/${_pkghash}/jdcal-${pkgver}.tar.gz")
md5sums=("885ba61d28992f26acffec131bd2a17e")

build() {
  cd "$srcdir"
  cp -a jdcal-${pkgver} jdcal-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/jdcal-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/jdcal-${pkgver}
  python setup.py build
}

package_python2-jdcal() {
  depends=('python2')

  cd "$srcdir"/jdcal-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-jdcal() {
  depends=('python')

  cd "$srcdir"/jdcal-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

