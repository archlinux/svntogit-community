# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-jellyfish
pkgver=0.8.9
pkgrel=3
pkgdesc='A python library for doing approximate and phonetic matching of strings'
arch=(x86_64)
url='https://github.com/jamesturk/jellyfish'
license=(BSD)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=c3f9e2d3ed76899214159906af104e0841cd415a
source=(
  git+https://github.com/jamesturk/jellyfish.git#tag=${_tag}
  git+https://github.com/jamesturk/cjellyfish.git
)
sha256sums=(
  SKIP
  SKIP
)

prepare() {
  cd jellyfish

  for submodule in cjellyfish; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule}
    git submodule update ${submodule}
  done
}

pkgver() {
  cd jellyfish

  git describe --tags | sed 's/^v//'
}

build() {
  cd jellyfish

  python setup.py build
}

package() {
  cd jellyfish

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-jellyfish/
}

# vim: ts=2 sw=2 et:
