# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pypiname=jieba
pkgname=(python-jieba python2-jieba)
pkgver=0.38
pkgrel=2
pkgdesc="Chinese Words Segementation Utilities"
arch=('any')
url='https://github.com/fxsjy/jieba'
license=('MIT')
makedepends=('python' 'python2')
source=("https://pypi.io/packages/source/j/jieba/jieba-$pkgver.zip"
        LICENSE)
sha512sums=('52bfde29f97210f7e0dd766cc83f17961644e71593c2180a9b2867c5f4f42cad87b193ac3050aa3bc922fcee36ddcd48dd41e981206f39368ca30b60eae721de'
            '840a892dcc1ae83b4d3eda1aa005d08ce158c28911621f43afa0c214c269a72bedd909d60b2cdbfa2a545259451abde798713a25d1b1d9f9c06cceb56bc214dd')

prepare() {
  cp -a jieba-$pkgver{,-py2}
}

package_python-jieba() {
  depends=('python')

  cd "$srcdir/jieba-$pkgver"
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jieba() {
  depends=('python2')

  cd "$srcdir/jieba-$pkgver-py2"
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
