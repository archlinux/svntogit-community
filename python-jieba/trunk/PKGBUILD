# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jieba
pkgname=(python-jieba python2-jieba)
pkgver=0.39
pkgrel=2
pkgdesc="Chinese Words Segementation Utilities"
arch=('any')
url='https://github.com/fxsjy/jieba'
license=('MIT')
makedepends=('python' 'python2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/fxsjy/jieba/archive/v$pkgver.tar.gz")
sha512sums=('2d8d25604265c8952dbed1a584548490b6b4f183cc3f09956c689c240d8b545f8f8aa09b5e25620539c45e08a77ac2be85f0ad228ab3c4100a1962b11caf5ea5')

prepare() {
  cp -a jieba-$pkgver{,-py2}
}

package_python-jieba() {
  depends=('python')

  cd jieba-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jieba() {
  depends=('python2')

  cd jieba-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
