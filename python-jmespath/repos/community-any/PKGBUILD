# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Chris Fordham <chris [at] fordham-nagy [dot] id [dot] au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/python-jmespath/PKGBUILD

pkgbase=python-jmespath
pkgname=(python-jmespath python2-jmespath)
pkgver=0.9.2
pkgrel=1
pkgdesc='A query language for JSON'
arch=('any')
url="https://github.com/jmespath/jmespath"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-py' 'python2-py')
source=("git+https://github.com/jmespath/jmespath.py.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a jmespath.py{,-py2}
}

build() {
  cd "$srcdir/jmespath.py"
  python setup.py build

  cd "$srcdir/jmespath.py-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/jmespath.py"
  LC_CTYPE=en_US.utf8 nosetests3

  cd "$srcdir/jmespath.py-py2"
  LC_CTYPE=en_US.utf8 nosetests2
}

package_python-jmespath() {
  depends=('python')

  cd $srcdir/jmespath.py
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-jmespath() {
  depends=('python2')

  cd $srcdir/jmespath.py-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/jp{,2}.py
}
