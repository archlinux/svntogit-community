# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Chris Fordham <chris [at] fordham-nagy [dot] id [dot] au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/python-jmespath/PKGBUILD

pkgbase=python-jmespath
pkgname=(python-jmespath python2-jmespath)
pkgver=0.9.3
pkgrel=2
pkgdesc='A query language for JSON'
arch=('any')
url="https://github.com/jmespath/jmespath"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-py' 'python2-py')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jmespath/jmespath.py/archive/$pkgver.tar.gz")
sha512sums=('3ca646337e96d3defef45d89f48dbbaa2739a6a18a926b6cf37222bf6db8e6838d0a183fbfb5d4e31be02f9e0de495024b1dba8a69cec995a5285209486c870f')

prepare() {
  cp -a jmespath.py-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/jmespath.py-$pkgver
  python setup.py build

  cd "$srcdir"/jmespath.py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/jmespath.py-$pkgver
  LC_CTYPE=en_US.utf8 nosetests3

  cd "$srcdir"/jmespath.py-$pkgver-py2
  LC_CTYPE=en_US.utf8 nosetests2
}

package_python-jmespath() {
  depends=('python')

  cd jmespath.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-jmespath() {
  depends=('python2')

  cd jmespath.py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/jp{,2}.py
}
