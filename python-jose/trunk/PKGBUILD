# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jose
pkgname=('python-jose' 'python2-jose')
pkgver=1.4.0
pkgrel=1
pkgdesc="A JOSE implementation in Python"
arch=('any')
license=('MIT')
url="https://github.com/mpdavis/python-jose"
makedepends=('python-setuptools' 'python2-setuptools' 'python-crypto' 'python2-crypto' 'python-six'
             'python2-six' 'python-ecdsa' 'python2-ecdsa' 'python-future' 'python2-future')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-cryptography'
              'python2-cryptography')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mpdavis/python-jose/archive/$pkgver.tar.gz")
sha512sums=('8ce37d3eb339f782995ae3595aae4a335ab50b2f5e837c7654a306172d3e3a5a0215895473adae5853e4af7e2ba33e520ea8ab6a22708b1a75e8edf043603180')

prepare() {
  cp -a python-jose-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-jose-$pkgver
  python setup.py build

  cd "$srcdir"/python-jose-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-jose-$pkgver
  python setup.py pytest

  cd "$srcdir"/python-jose-$pkgver-py2
  python2 setup.py pytest
}

package_python-jose() {
  depends=('python-crypto' 'python-six' 'python-ecdsa' 'python-future')

  cd python-jose-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jose() {
  depends=('python2-crypto' 'python2-six' 'python2-ecdsa' 'python2-future')

  cd python-jose-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
