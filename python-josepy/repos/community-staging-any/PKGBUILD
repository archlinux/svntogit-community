# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-josepy
pkgver=1.5.0
pkgrel=3
pkgdesc="JOSE protocol implementation in Python"
url="https://github.com/certbot/josepy"
license=('PerlArtistic')
arch=('any')
depends=('python-cryptography' 'python-pyopenssl' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-coverage' 'python-pytest-isort' 'python-pytest-cache'
              'python-pytest-cov' 'flake8' 'python-pytest-flake8' 'python-mock')
source=("https://pypi.io/packages/source/j/josepy/josepy-$pkgver.tar.gz")
sha512sums=('39ace649ae4fcefee3eb7fa749001645711de39dc17fd47f1ebf52981929e21bee9b6316d09c61b5478d3d652afc0659e7d5110d1e222e8dcbdc2bb831cb2d8d')

prepare() {
  # https://github.com/certbot/josepy/issues/24
  sed -i 's/flake8-ignore = E501/flake8-ignore = E501 W504/' josepy-$pkgver/pytest.ini
}

build() {
  cd josepy-$pkgver
  python setup.py build
}

check() {
  cd josepy-$pkgver
  python setup.py pytest
}

package() {
  cd josepy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
