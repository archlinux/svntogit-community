# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Mubashshir <ahmubashshir@gmail.com>

_name=jq.py
pkgname=python-jq
pkgver=1.2.3
pkgrel=1
pkgdesc='Python bindings for jq'
arch=('x86_64')
url=https://github.com/mwilliamson/jq.py
license=('BSD')
depends=('jq' 'python')
makedepends=('cython' 'python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz"
        'jq-py-setup.patch')
b2sums=('72ebe4137d21bfd06dce337da1c7bcbfaa10ea55d06d128c901daa605557d7f6bd3bc219242127dfd36908089f312a6120306028bccb66e03274194e179175ee'
        '3424a91e0e6330e8d25e167b862554e466f469cdeb5672d4d4e5ff4d4475bfe9c7a575f3c93cd03687e10cc6afac2bb94294c0fef50feec03eaa55684e2907bc')

# Devendor
prepare() {
  cd $_name-$pkgver
  sed -i '/certifi\|requests/d' pyproject.toml
  patch --forward --strip=1 --input=../jq-py-setup.patch
}

build() {
  cd $_name-$pkgver
  cython jq.pyx
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/jq-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
