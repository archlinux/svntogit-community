# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-js2py
pkgver=0.68
_commit=04241ba58f850cf586c55c25ac033561b9bb82cb
pkgrel=1
pkgdesc="JavaScript to Python Translator & JavaScript interpreter written in 100% pure Python"
url="https://github.com/PiotrDabkowski/Js2Py"
license=('MIT')
arch=('any')
depends=('python-tzlocal' 'python-six' 'python-pyjsparser')
makedepends=('python-setuptools' 'python-tzlocal'  'python-six' 'python-pyjsparser' )
checkdepends=('npm' 'python-numpy')
source=("$pkgname-$_commit.tar.gz::https://github.com/PiotrDabkowski/Js2Py/archive/$_commit.tar.gz")
sha512sums=('47b07804d1909b37d86855accd8c6f8f54587816c564c7eabad9fcf3c2bec898e333430275604d449cc1026146d28113934233a30c8ea0e39ca6c18621d3a646')

prepare() {
  mv Js2Py-{$_commit,$pkgver}
}

build() {
  cd Js2Py-$pkgver
  python setup.py build
}

check() {
  cd Js2Py-$pkgver
  python simple_test.py
}

package() {
  cd Js2Py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
