# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-js2py
pkgver=0.71
_commit=5f665f60083a9796ec33861240ce31d6d2b844b6
pkgrel=4
pkgdesc="JavaScript to Python Translator & JavaScript interpreter written in 100% pure Python"
url="https://github.com/PiotrDabkowski/Js2Py"
license=('MIT')
arch=('any')
depends=('python-six' 'python-pyjsparser')
makedepends=('python-setuptools' 'python-tzlocal'  'python-six' 'python-pyjsparser')
checkdepends=('npm' 'python-numpy')
optdepends=('python-tzlocal: for local timezone support')
source=("$pkgname-$_commit.tar.gz::https://github.com/PiotrDabkowski/Js2Py/archive/$_commit.tar.gz"
        "js2py-opt-tzlocal.patch::https://github.com/PiotrDabkowski/Js2Py/pull/293.patch")
sha512sums=('75bae61619d4766117f3c163d36427f3019825ecb08b745aee7c3b6f2d33a4ea8507ca180b63c0a8e575a5f9491eb64d6963acbb912edc7b934a676b38814597'
            'db4fce5b0b26cc89346347719f7feb8da65119ef54504f6475d7a52f0dbb6483c26ff259a1b5c54c98422057e64debad5d2d92585e1acfd1a1c67f301d6a328c')

prepare() {
  mv Js2Py-{$_commit,$pkgver}
  cd Js2Py-$pkgver
  # https://github.com/PiotrDabkowski/Js2Py/pull/293
  patch --forward --strip=1 --input=../js2py-opt-tzlocal.patch
}

build() {
  cd Js2Py-$pkgver
  python setup.py build
}

check() {
  cd Js2Py-$pkgver
  python simple_test.py
}

package() {
  cd Js2Py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
