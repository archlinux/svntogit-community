# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-js2py
pkgver=0.67
_commit=12739ed5978953f16fb5201a98c026bdb6fc1003
pkgrel=2
pkgdesc="JavaScript to Python Translator & JavaScript interpreter written in 100% pure Python"
url="https://github.com/PiotrDabkowski/Js2Py"
license=('MIT')
arch=('any')
depends=('python-tzlocal' 'python-six' 'python-pyjsparser')
makedepends=('python-setuptools' 'python-tzlocal'  'python-six' 'python-pyjsparser' )
checkdepends=('npm' 'python-numpy')
source=("$pkgname-$_commit.tar.gz::https://github.com/PiotrDabkowski/Js2Py/archive/$_commit.tar.gz")
sha512sums=('09e7feea092f350d5ba983716940acbc6d4b0b8da595770f1e6e5eaee1556ddc1b73b098094f4ebaedd86a9ae1ab15b37bb5ef48458487bccd9c1df4c6bd0fbb')

prepare() {
  mv Js2Py-{$_commit,$pkgver}
}

build() {
  cd Js2Py-$pkgver
  python setup.py build
}

check() {
  cd Js2Py-$pkgver
  python simple_test.py
}

package() {
  cd Js2Py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
