# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-json-logger
pkgname=('python-json-logger' 'python2-json-logger')
pkgver=0.1.8
_commit=1a464798a033d3f8034c05811dbdf097ec4b0201
pkgrel=1
pkgdesc='A python library adding a json log formatter'
arch=('any')
license=('BSD')
url='https://github.com/madzak/python-json-logger'
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/madzak/python-json-logger/archive/$_commit.tar.gz")
sha512sums=('a0331493aa2861db28669e344f9c13be247ddd84ed6a33b2af2719a49f089230c42d5f66ca2fd776f9584ae2dbdb0f6cf27c575f270c3e1727ddc71da7167ded')

prepare() {
  mv python-json-logger-$_commit python-json-logger-$pkgver
  cp -a python-json-logger-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-json-logger-$pkgver
  python setup.py build

  cd "$srcdir"/python-json-logger-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-json-logger-$pkgver
  python setup.py test

  cd "$srcdir"/python-json-logger-$pkgver-py2
  python2 setup.py test
}

package_python-json-logger() {
  depends=('python-setuptools')

  cd python-json-logger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-json-logger() {
  depends=('python2-setuptools')

  cd python-json-logger-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
