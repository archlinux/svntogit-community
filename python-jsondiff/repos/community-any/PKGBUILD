# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-jsondiff
_pkgname=jsondiff
pkgver=1.2.0
pkgrel=1
pkgdesc='Diff JSON and JSON-like structures in Python'
arch=(any)
url='https://github.com/xlwings/jsondiff'
license=(MIT)
depends=(python python-setuptools)
checkdepends=(python-nose python-nose-random)
source=("https://github.com/xlwings/jsondiff/archive/v$pkgver/jsondiff-v$pkgver.tar.gz")
sha256sums=('445b2d519c006e0ba10e1a71f596b7cb3f5d3c62849f3b9a6780b5e9653da92f')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  nosetests -v tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  # FS#60911, https://github.com/xlwings/jsondiff/issues/24
  rm "$pkgdir"/usr/bin/jsondiff
}
