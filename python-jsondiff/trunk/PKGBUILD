# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-jsondiff
_pkgname=jsondiff
pkgver=1.1.2
pkgrel=4
pkgdesc='Diff JSON and JSON-like structures in Python'
arch=(any)
url='https://github.com/ZoomerAnalytics/jsondiff'
license=(MIT)
depends=(python)
checkdepends=(python-nose python-nose-random)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/ZoomerAnalytics/jsondiff/archive/$pkgver.tar.gz")
sha256sums=('8e5edaa604c4a96d9ffe7fea6f39ec91bdddaee78fae7c6ff91cbb8e105b70c1')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver
  nosetests -v tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  # FS#60911
  rm -r "$pkgdir"/usr/bin
}
