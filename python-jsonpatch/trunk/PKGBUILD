# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgbase=python-jsonpatch
_pkgname=jsonpatch
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=1.9
pkgrel=1
pkgdesc="Python object model built on JSON schema and JSON patch"
arch=(any)
url=https://github.com/stefankoegl/python-json-patch
license=(BSD)
makedepends=(python2-setuptools python2-jsonschema python-setuptools python-jsonschema)
options=(!emptydirs)
source=(http://pypi.python.org/packages/source/j/${_pkgname#*-}/${_pkgname#*-}-$pkgver.tar.gz)
sha256sums=('e997076450992aa7af2f4ae6c3e7767d390ddb6746979c74fd2092bb8fbdf5b2')
sha512sums=('9f27def8519677dbc6d45287b6938ccfb9179f7a315871adeb7198e1568876a391625c060c7901b7acd51af0ed10427a61d54d899719ef1a073b2e93591904ed')

prepare(){
    cp -a $_pkgname-$pkgver $_pkgname-$pkgver-2
    find $_pkgname-$pkgver-2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
    LANG=en_US.UTF-8
    cd $srcdir/$_pkgname-$pkgver
    python setup.py build
    cd $srcdir/$_pkgname-$pkgver-2
    python2 setup.py build
}

package_python-jsonpatch() {
    cd $srcdir/$_pkgname-$pkgver
    depends=(python-jsonschema)
    install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
    python setup.py install --prefix=/usr --root="$pkgdir"
}
package_python2-jsonpatch() {
    cd $srcdir/$_pkgname-$pkgver-2
    depends=(python2-jsonschema)
    install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
    python2 setup.py install --prefix=/usr --root="$pkgdir"
    rm $pkgdir/usr/bin/json{diff,patch}
}
