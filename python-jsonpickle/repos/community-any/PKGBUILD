# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsonpickle
pkgname=(python-jsonpickle python2-jsonpickle)
_pypiname=jsonpickle
pkgver=0.9.2
pkgrel=3
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="http://jsonpickle.github.io/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-numpy' 'python2-numpy' 'python-pymongo' 'python2-pymongo'
              'python-feedparser' 'python2-feedparser' 'python-coverage' 'python2-coverage' 'python2-enum34'
              'python-simplejson' 'python2-simplejson')
license=('BSD')
arch=('any')
source=("https://pypi.python.org/packages/source/j/${_pypiname}/${_pypiname}-${pkgver}.tar.gz")
md5sums=('579a44c19ae55af83cb7b0437dfbe7e3')

prepare() {
  cp -a $_pypiname-$pkgver{,-py2}
}

build() {
  cd "$srcdir/${_pypiname}-${pkgver}"
  python setup.py build

  cd "$srcdir/${_pypiname}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/${_pypiname}-${pkgver}"
  nosetests3

  cd "$srcdir/${_pypiname}-${pkgver}-py2"
  nosetests2
}

package_python-jsonpickle() {
  depends=('python')

  cd $srcdir/${_pypiname}-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-jsonpickle() {
  depends=('python2')

  cd $srcdir/${_pypiname}-${pkgver}-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
