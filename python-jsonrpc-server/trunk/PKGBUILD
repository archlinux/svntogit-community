# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: fenuks

pkgname=python-jsonrpc-server
pkgver=0.1.2
pkgrel=2
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-jsonrpc-server"
license=("MIT")
depends=("python" "python-future")
makedepends=("python" "python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes" "python-mock")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('09b418e3b1ba9032aecc7aefdd185511dd230fb8dacf18ec195d14dfd89d9e54')

build() {
    cd "${pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${pkgname}-${pkgver}"
    py.test
}

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
