# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-jupyter_client
pkgname=(python-jupyter_client python2-jupyter_client)
pkgver=4.4.0
pkgrel=2
pkgdesc="Jupyter protocol implementation and client libraries"
arch=('any')
url="https://pypi.python.org/pypi/jupyter_client"
license=('BSD')
depends=('python-ipykernel' 'python-traitlets' 'python-pyzmq'
         'python-jupyter_core')
makedepends=('python-setuptools' 'python2-setuptools'
             'python2-ipykernel' 'python2-traitlets' 'python2-pyzmq'
             'python2-jupyter_core')

source=("$pkgbase-$pkgver.tgz::https://github.com/jupyter/jupyter_client/archive/$pkgver.tar.gz")
md5sums=('a0bd6fe6ba7c504fbc962a88a2a56a90')

prepare() {
  cd "$srcdir"
  cp -r jupyter_client-$pkgver python2-jupyter_client-$pkgver
}

build() {
  cd "$srcdir"
}

package_python-jupyter_client() {
  cd "$srcdir/jupyter_client-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jupyter_client() {
  depends=('python2-ipykernel' 'python2-traitlets' 'python2-pyzmq'
           'python2-jupyter_core')

  cd "$srcdir/python2-jupyter_client-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr"
  rm bin/jupyter-kernelspec
  rmdir bin
}

