# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jupyter_client
pkgver=7.3.3
pkgrel=1
pkgdesc="Jupyter protocol implementation and client libraries"
arch=('any')
url="https://pypi.python.org/pypi/jupyter_client"
license=('BSD')
depends=('python-ipykernel' 'python-traitlets' 'python-pyzmq'
         'python-jupyter_core' 'python-dateutil' 'python-entrypoints' 'python-nest-asyncio')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest' 'python-async_generator' 'python-pytest-asyncio' 'python-pytest-timeout')
#source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/jupyter_client/archive/$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/j/jupyter_client/jupyter_client-$pkgver.tar.gz")
sha256sums=('9e80defc0058712bf2b30327bad582f9734668aa85e90256dcba116325ce3060')

# dep cycle with python-ipykernel

build() {
  cd jupyter_client-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd jupyter_client-$pkgver
  pytest -vv
}

package() {
  cd jupyter_client-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
