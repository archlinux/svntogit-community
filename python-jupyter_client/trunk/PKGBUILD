# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-jupyter_client
pkgname=(python-jupyter_client python2-jupyter_client)
pkgver=5.2.3
pkgrel=1
pkgdesc="Jupyter protocol implementation and client libraries"
arch=('any')
url="https://pypi.python.org/pypi/jupyter_client"
license=('BSD')
depends=('python-ipykernel' 'python-traitlets' 'python-pyzmq'
         'python-jupyter_core' 'python-dateutil')
makedepends=('python-setuptools' 'python2-setuptools'
             'python2-ipykernel' 'python2-traitlets' 'python2-pyzmq'
             'python2-jupyter_core')
source=("$pkgbase-$pkgver.tgz::https://github.com/jupyter/jupyter_client/archive/$pkgver.tar.gz")
md5sums=('411e5aaf0db1e1b6334a1a8c461a62f5')

# dep cycle with python-ipykernel

prepare() {
  cd "$srcdir"
  cp -r jupyter_client-$pkgver python2-jupyter_client-$pkgver
}

build() {
  cd "$srcdir"
}

package_python-jupyter_client() {
  cd "$srcdir/jupyter_client-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jupyter_client() {
  depends=('python2-ipykernel' 'python2-traitlets' 'python2-pyzmq'
           'python2-jupyter_core' 'python2-dateutil')

  cd "$srcdir/python2-jupyter_client-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr"
  rm bin/jupyter-*
  rmdir bin
}

