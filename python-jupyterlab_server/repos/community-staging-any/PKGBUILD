# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Guillaume Duboc <guilduboc@gmail.com>

_pipname=jupyterlab_server
pkgname=python-$_pipname
pkgver=2.22.0
pkgrel=2
pkgdesc='Launch an application built using JupyterLab'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(jupyter-server python-requests python-json5 python-babel python-ruamel-yaml)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest-tornasync python-pytest-cov python-pytest-timeout python-pytest-jupyter
              python-openapi-core python-strict-rfc3339 python-parse python-requests-mock
              python-jupyter-server-terminals)
conflicts=(jupyterlab_server)
replaces=(jupyterlab_server)
source=(https://pypi.io/packages/source/j/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('0f9f6752b0c534a7b22a6542b984fa6a2c18ab4d4e0a4c79f191138506a9a75f')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  pytest -v  -k 'not test_translation_api.py' \
    -W ignore::pytest.PytestAssertRewriteWarning \
    -W ignore::DeprecationWarning \
    -W ignore::pytest.PytestUnraisableExceptionWarning
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
