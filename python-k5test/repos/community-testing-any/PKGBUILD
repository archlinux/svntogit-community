# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-k5test
pkgver=0.10.3
pkgrel=2
pkgdesc='Library for setting up self-contained Kerberos 5 environments'
url='https://github.com/pythongssapi/k5test'
arch=('any')
license=('custom')
depends=('python' 'krb5')
makedepends=('python-setuptools')
source=("https://github.com/pythongssapi/k5test/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b0fa83f616b311871a0c39dda38d0070fd9a0e06380fc6b26fff76bb0647d6ef808be5b2a7a7ecbd597f674d1db4e0f1fdafa6156823c09e550668d141a2d765')
b2sums=('89478fb837fd4bbee591616ed15445d5292a676e47a48fa31504ffa32dc6fef985fb3ba5a4ff545b341fac0408d7e15694e064d5ab89da3836ef087bb1e88f7c')

build() {
  cd "k5test-${pkgver}"
  python setup.py build
}

package() {
  cd "k5test-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
