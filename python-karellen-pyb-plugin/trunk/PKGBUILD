# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-karellen-pyb-plugin
pkgver=0.0.1.20160930
_commit=41d012b2f093a31596610cf14d0e1da56402e45c
pkgrel=3
pkgdesc='Karellen PyBuilder Plugin'
arch=('any')
url='https://github.com/karellen/karellen-pyb-plugin'
license=('Apache')
depends=('python-pybuilder')
makedepends=('git' 'python-cram' 'flake8' 'python-sphinx' 'python-unittest-xml-reporting')
# checkdepends=('python-pyfix')
source=("git+https://github.com/karellen/karellen-pyb-plugin.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  sed -i '/use_plugin("pypi:pybuilder_header_plugin")/d' karellen-pyb-plugin/src/main/python/karellen_pyb_plugin/__init__.py
}

check() {
  cd karellen-pyb-plugin
  pyb -v analyze
}

package() {
  cd karellen-pyb-plugin
  pyb -v -o package
  cd target/dist/karellen-pyb-plugin-*
  python setup.py install --root="$pkgdir" -O1
}
