# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgname=python-keyrings-alt
epoch=1
pkgver=3.1
pkgrel=2
pkgdesc="Alternate keyring implementations"
arch=('any')
url="https://pypi.org/project/keyrings.alt/"
license=('MIT')
depends=('python-keyring')
optdepends=('libgnome-keyring: GNOME Integration'
            'python-gobject: GNOME Integration'
            'python-fs'
            'python-crypto: Encrypted file storage')
makedepends=('python-setuptools_scm')
checkdepends=('python-pycryptodome' 'libgnome-keyring' 'python-gobject' 'python-pytest-flake8'
              'python-pytest-runner')  # 'python-fs'
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/keyrings.alt/archive/$pkgver.tar.gz")
sha512sums=('29a552f526d33cabf9646c7c51236c74cbccc6a3f650d6ccec6d5beaa48c065dec7438debb0f2d2d6a48611590f6767ee442fb9e5a597bf428a70652ae79dab5')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  echo -e '\nflake8-ignore = W191 W503 W504' >> keyrings.alt-$pkgver/pytest.ini

  cp -a keyrings.alt-$pkgver{,-py2}
}

build() {
  cd keyrings.alt-$pkgver
  python setup.py build
}

check() {
  cd keyrings.alt-$pkgver
  python setup.py pytest
}

package() {
  cd keyrings.alt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
