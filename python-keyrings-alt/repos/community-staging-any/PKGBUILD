# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgbase=python-keyrings-alt
pkgname=('python-keyrings-alt' 'python2-keyrings-alt')
epoch=1
pkgver=1.3
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('any')
url="http://pypi.python.org/pypi/keyrings.alt"
license=('MIT')
source=("git+https://github.com/jaraco/keyrings.alt.git#tag=$pkgver")
makedepends=('python-setuptools_scm' 'python2-setuptools_scm' 'python-keyring' 'python2-keyring' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-fs' 'python2-fs' 'python-crypto' 'python2-crypto' 'libgnome-keyring' 
              'python-gobject' 'python2-gobject' 'python2-gdata')
md5sums=('SKIP')

prepare() {
  # Remove it from test_requirements
  sed -i '/python-keyczar/d' keyrings.alt/setup.py

  cp -a keyrings.alt{,-py2}
}

build() {
  cd "$srcdir"/keyrings.alt
  python setup.py build

  cd "$srcdir"/keyrings.alt-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/keyrings.alt
  python setup.py ptr

  cd "$srcdir"/keyrings.alt-py2
  python2 setup.py ptr
}

package_python-keyrings-alt() {
  depends=('python-keyring')
  optdepends=('libgnome-keyring: GNOME Integration'
              'python-gobject: GNOME Integration'
              'python-fs'
              'python-crypto: Encrypted file storage')

  cd "$srcdir"/keyrings.alt
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-keyrings-alt() {
  depends=('python2-keyring')
  optdepends=('libgnome-keyring: GNOME Integration'
              'python2-gobject: GNOME Integration'
              'python2-fs'
              'python2-gdata: Google Docs Integration'
              'python2-crypto: Encrypted file storage')

  cd "$srcdir"/keyrings.alt-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
