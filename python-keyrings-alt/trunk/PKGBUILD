# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgbase=python-keyrings-alt
pkgname=('python-keyrings-alt' 'python2-keyrings-alt')
epoch=1
pkgver=2.3
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('any')
url="http://pypi.python.org/pypi/keyrings.alt"
license=('MIT')
makedepends=('python-setuptools_scm' 'python2-setuptools_scm' 'python-keyring' 'python2-keyring')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python2-backports.unittest_mock'
              'python-crypto' 'python2-crypto' 'libgnome-keyring' 'python-gobject' 'python2-gobject'
              'python2-gdata')  # 'python-fs' 'python2-fs' Not compatible
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jaraco/keyrings.alt/archive/$pkgver.tar.gz")
sha512sums=('c2b9168e91297d260907cbd9d3ec635090747c0d4adf32313e3711b8ee0c33fa1b37a7e546acd920592efba3d517af45a4d925f1a9d48d2f28eff11cb5230606')

prepare() {
  # Remove it from test_requirements
  sed -i '/python-keyczar/d' keyrings.alt-$pkgver/setup.py

  # Set version for setuptools_scm
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  cp -a keyrings.alt-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/keyrings.alt-$pkgver
  python setup.py build

  cd "$srcdir"/keyrings.alt-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/keyrings.alt-$pkgver
  python setup.py pytest

  cd "$srcdir"/keyrings.alt-$pkgver-py2
  python2 setup.py pytest
}

package_python-keyrings-alt() {
  depends=('python-keyring')
  optdepends=('libgnome-keyring: GNOME Integration'
              'python-gobject: GNOME Integration'
              'python-fs'
              'python-crypto: Encrypted file storage')

  cd "$srcdir"/keyrings.alt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-keyrings-alt() {
  depends=('python2-keyring')
  optdepends=('libgnome-keyring: GNOME Integration'
              'python2-gobject: GNOME Integration'
              'python2-fs'
              'python2-gdata: Google Docs Integration'
              'python2-crypto: Encrypted file storage')

  cd "$srcdir"/keyrings.alt-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
