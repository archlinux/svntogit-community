# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgname=python-keyrings-alt
_pkgname=keyrings.alt
epoch=1
pkgver=3.4.0
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('any')
url='https://github.com/jaraco/keyrings.alt'
license=('MIT')
depends=('python-keyring')
makedepends=('python-setuptools_scm')
checkdepends=('libgnome-keyring' 'python-pycryptodome' 'python-gobject' 'python-keyring'
              'python-pytest-runner' 'python-pytest-flake8' 'python-pytest-black' 'python-pytest-cov')
optdepends=('libgnome-keyring: GNOME Integration'
            'python-gobject: GNOME Integration'
            'python-fs'
            'python-crypto: Encrypted file storage')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "fix-tests.patch::$url/commit/e645e3c39cc6c1ea971acee8d096faadffc77618.patch")
sha512sums=('eda62fd7a7625cca511789c21ab2db16455b7ea3675b971ed230b41aee8669092aab65aeb75c633bc19104ef869118087d341c350ad647d08def79217e5609a8'
            'e3dcb1ffc4159af3e3ff8ed40f3827f7071e1ea0618c1c1f5d9a8b356186e9b059354373a95eb8890ace51cb8f11af2a7490cf63aa68eb228ae6d8aac53f5887')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../fix-tests.patch
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
