# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-keystoneauth1
pkgname=(python-keystoneauth1 python2-keystoneauth1)
pkgver=3.17.1
pkgrel=2
pkgdesc="Authentication Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/developer/keystoneauth"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-iso8601'
             'python2-iso8601' 'python-os-service-types' 'python2-os-service-types' 'python-requests'
             'python2-requests' 'python-stevedore' 'python2-stevedore')
checkdepends=('python-requests-mock' 'python2-requests-mock' 'python-testscenarios'
              'python2-testscenarios' 'python-mock' 'python2-mock' 'python-lxml' 'python2-lxml'
              'python-oslo-config' 'python2-oslo-config' 'python-betamax' 'python2-betamax'
              'python-oslotest' 'python2-oslotest' 'python-oslo-utils' 'python2-oslo-utils'
              'python-requests-kerberos' 'python2-requests-kerberos' 'python-oauthlib'
              'python2-oauthlib' 'python-pycodestyle' 'python2-pycodestyle' 'python-stestr'
              'python2-stestr')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/keystoneauth/archive/$pkgver.tar.gz")
sha512sums=('ae9a230bc2f34aa30b9e3d0bb4cd4f8e2fe6c15bfa041ce2f1a9ddd280a23c0d287c87aaea4f3c4f1e2dcb6bd2549d8eb220648f0738803b9c207a9085be0f07')

prepare() {
  sed -i 's/pep8/pycodestyle/g' keystoneauth-$pkgver/keystoneauth1/tests/unit/test_hacking_checks.py

  cp -a keystoneauth-$pkgver{,-py2}

  sed -i 's/assertItemsEqual/assertCountEqual/' keystoneauth-$pkgver/keystoneauth1/tests/unit/extras/kerberos/*.py

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/keystoneauth-$pkgver
  python setup.py build

  cd "$srcdir"/keystoneauth-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/keystoneauth-$pkgver
  stestr run

  cd "$srcdir"/keystoneauth-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-keystoneauth1() {
  depends=('python-pbr' 'python-iso8601' 'python-stevedore' 'python-os-service-types' 'python-requests')
  optdepends=('python-requests-kerberos: for kerberos support'
              'python-lxml: for saml2 support'
              'python-oauthlib: for oauth1 support'
              'python-betamax: for betamax support'
              'python-fixtures: for betamax support'
              'python-mock: for betamax support')

  cd "$srcdir"/keystoneauth-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-keystoneauth1() {
  depends=('python2-pbr' 'python2-iso8601' 'python2-stevedore' 'python2-os-service-types' 'python2-requests')
  optdepends=('python2-requests-kerberos: for kerberos support'
              'python2-lxml: for saml2 support'
              'python2-oauthlib: for oauth1 support'
              'python2-betamax: for betamax support'
              'python2-fixtures: for betamax support'
              'python2-mock: for betamax support')

  cd "$srcdir"/keystoneauth-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
