# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-keystoneauth1
pkgver=5.1.2
_commit=6ee21bd722b3e1dbec3e5a211e32f10fb2a20603
pkgrel=2
pkgdesc="Authentication Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/developer/keystoneauth"
license=('Apache')
depends=('python-pbr' 'python-iso8601' 'python-stevedore' 'python-os-service-types' 'python-requests')
optdepends=('python-requests-kerberos: for kerberos support'
            'python-lxml: for saml2 support'
            'python-oauthlib: for oauth1 support'
            'python-betamax: for betamax support'
            'python-fixtures: for betamax support')
makedepends=('git')
checkdepends=('python-requests-mock' 'python-testscenarios' 'python-lxml'
              'python-oslo-config' 'python-betamax' 'python-oslotest' 'python-oslo-utils'
              'python-requests-kerberos' 'python-oauthlib' 'python-hacking')
source=("git+https://github.com/openstack/keystoneauth.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd keystoneauth
  python setup.py build
}

check() {
  cd keystoneauth
  stestr run
}

package() {
  cd keystoneauth
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
