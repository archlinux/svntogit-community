# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-keystoneauth1
pkgname=(python-keystoneauth1 python2-keystoneauth1)
pkgver=2.4.0
pkgrel=1
pkgdesc="Authentication Library for OpenStack Identity"
arch=('any')
url="http://docs.openstack.org/developer/keystoneauth"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-iso8601' 'python2-iso8601' 'python-positional' 'python2-positional'
             'python-requests' 'python2-requests' 'python-stevedore' 'python2-stevedore')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-requests-mock'
              'python2-requests-mock' 'python-testscenarios' 'python2-testscenarios'
              'python-mock' 'python2-mock' 'python-lxml' 'python2-lxml' 'pep8' 'python2-pep8'
              'python-oslo-config' 'python2-oslo-config' 'python-betamax' 'python2-betamax'
              'python-oslotest' 'python2-oslotest' 'python-oslo-utils' 'python2-oslo-utils'
              'python-requests-kerberos' 'python2-requests-kerberos')
source=("git+https://git.openstack.org/openstack/keystoneauth#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a keystoneauth{,-py2}
}

build() {
  cd "$srcdir"/keystoneauth
  python setup.py build

  cd "$srcdir"/keystoneauth-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/keystoneauth
  # Two failures in keystoneauth1.tests.unit.test_betamax_fixture.TestBetamaxFixture
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/keystoneauth-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-keystoneauth1() {
  depends=('python-pbr' 'python-iso8601' 'python-stevedore' 'python-positional' 'python-requests')

  cd "$srcdir"/keystoneauth
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-keystoneauth1() {
  depends=('python2-pbr' 'python2-iso8601' 'python2-stevedore' 'python2-positional' 'python2-requests')

  cd "$srcdir"/keystoneauth-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
