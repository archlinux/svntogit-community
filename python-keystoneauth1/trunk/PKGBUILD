# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-keystoneauth1
pkgname=(python-keystoneauth1 python2-keystoneauth1)
pkgver=3.0.0
pkgrel=1
pkgdesc="Authentication Library for OpenStack Identity"
arch=('any')
url="http://docs.openstack.org/developer/keystoneauth"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-iso8601'
             'python2-iso8601' 'python-positional' 'python2-positional' 'python-requests'
             'python2-requests' 'python-stevedore' 'python2-stevedore')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-requests-mock'
              'python2-requests-mock' 'python-testscenarios' 'python2-testscenarios'
              'python-mock' 'python2-mock' 'python-lxml' 'python2-lxml' 'python-oslo-config'
              'python2-oslo-config' 'python-betamax' 'python2-betamax' 'python-oslotest'
              'python2-oslotest' 'python-oslo-utils' 'python2-oslo-utils' 'python-requests-kerberos'
              'python2-requests-kerberos' 'python-oauthlib' 'python2-oauthlib' 'python-pycodestyle'
              'python2-pycodestyle')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/keystoneauth/archive/$pkgver.tar.gz")
sha512sums=('86e11cdce9d73604c835afa55f9e5faf2935a93ac79f9b028c15f16df46d457ccf1aa41d84b6eb09d541b59fb30c9fd00426dff2e6002bf334a23ebbaa4629c3')

prepare() {
  # Use pycodestyle
  sed -i 's/pep8/pycodestyle/g' keystoneauth-$pkgver/keystoneauth1/tests/unit/test_hacking_checks.py

  cp -a keystoneauth-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/keystoneauth-$pkgver
  python setup.py build

  cd "$srcdir"/keystoneauth-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/keystoneauth-$pkgver
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/keystoneauth-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-keystoneauth1() {
  depends=('python-pbr' 'python-iso8601' 'python-stevedore' 'python-positional' 'python-requests')
  optdepends=('python-requests-kerberos: for kerberos support'
              'python-lxml: for saml2 support'
              'python-oauthlib: for oauth1 support'
              'python-betamax: for betamax support'
              'python-fixtures: for betamax support'
              'python-mock: for betamax support')

  cd "$srcdir"/keystoneauth-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-keystoneauth1() {
  depends=('python2-pbr' 'python2-iso8601' 'python2-stevedore' 'python2-positional' 'python2-requests')
  optdepends=('python2-requests-kerberos: for kerberos support'
              'python2-lxml: for saml2 support'
              'python2-oauthlib: for oauth1 support'
              'python2-betamax: for betamax support'
              'python2-fixtures: for betamax support'
              'python2-mock: for betamax support')

  cd "$srcdir"/keystoneauth-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
