# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-keystoneclient
pkgname=(python-keystoneclient python2-keystoneclient)
pkgver=3.0.0
pkgrel=1
pkgdesc="Client Library for OpenStack Identity"
arch=('any')
url="http://docs.openstack.org/developer/python-keystoneclient"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-iso8601' 'python2-iso8601' 'python-prettytable' 'python2-prettytable'
             'python-debtcollector' 'python2-debtcollector' 'python-keystoneauth1'
             'python2-keystoneauth1' 'python-positional' 'python2-positional' 'python-oslo-utils'
             'python2-oslo-utils' 'python-oslo-i18n' 'python2-oslo-i18n'
             'python-oslo-serialization' 'python2-oslo-serialization' 'python-oslo-config'
             'python2-oslo-config' 'python-requests' 'python2-requests' 'python-stevedore'
             'python2-stevedore')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-testresources' 'python2-testresources'
              'python-requests-mock' 'python2-requests-mock' 'pep8' 'python2-pep8' 'python-lxml'
              'python2-lxml')
source=("git+https://git.openstack.org/openstack/python-keystoneclient#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a python-keystoneclient{,-py2}
}

build() {
  cd "$srcdir"/python-keystoneclient
  python setup.py build

  cd "$srcdir"/python-keystoneclient-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-keystoneclient
  python setup.py testr

  cd "$srcdir"/python-keystoneclient-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-keystoneclient() {
  depends=('python-six' 'python-pbr' 'python-iso8601' 'python-prettytable' 'python-debtcollector'
           'python-keystoneauth1' 'python-positional' 'python-oslo-utils' 'python-oslo-i18n'
           'python-oslo-serialization' 'python-oslo-config' 'python-requests' 'python-stevedore')

  cd "$srcdir"/python-keystoneclient
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-keystoneclient() {
  depends=('python2-six' 'python2-pbr' 'python2-iso8601' 'python2-prettytable' 'python2-debtcollector'
           'python2-keystoneauth1' 'python2-positional' 'python2-oslo-utils' 'python2-oslo-i18n'
           'python2-oslo-serialization' 'python2-oslo-config' 'python2-requests' 'python2-stevedore')

  cd "$srcdir"/python-keystoneclient-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
