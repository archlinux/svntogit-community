# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: lagrange <flkazemakase@gmail.com>
# Contributor: mid-kid <esteve.varela@gmail.com>

pkgname=python-kivy
pkgver=1.11.1
pkgrel=3
pkgdesc="A software library for rapid development of hardware-accelerated multitouch applications."
arch=('x86_64')
url="https://kivy.org/"
license=('MIT')
depends=('python-docutils' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-pytest' 'python-coverage' 'python-gobject' 'xorg-server-xvfb' 'opencv' 'python-pyenchant'
              'git' 'mtdev' 'xclip' 'xsel' 'gtk3' 'gst-plugins-base'
              'gst-plugins-good' 'gst-libav' 'python-dbus')
source=("https://pypi.io/packages/source/K/Kivy/Kivy-$pkgver.tar.gz"
        py38.patch)
sha512sums=('d6b34668d9ad9196edcb9a8307d6659868a8f72580018d873bc6e91823461c853c3c92758c0c3013b25faa177dd62ab5d12671411822112b13a9610e4eda3eb0'
            'a128a81d3f7124339feb65c93eff6023174ae67db31b5eb3787046c906b03fd155e768d0342d0e8df1a3d2c739e3a90d3d31d8f43d8cb15ffdf5aba97ceda593')

# For better metadata
export KIVY_USE_SETUPTOOLS=1

export LC_CTYPE=en_US.UTF-8

prepare() {
  # Remove max cython version limit
  sed -i 's/,<={max_version}//' Kivy-$pkgver/setup.py # Kivy-$pkgver/kivy/tools/packaging/cython_cfg.py
  patch -d Kivy-$pkgver/kivy -p1 -i ../../py38.patch
}

build() {
  cd "$srcdir"/Kivy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/Kivy-$pkgver
  python setup.py build_ext --inplace
  xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" pytest kivy/tests
}

package() {
  cd Kivy-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
