# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: lagrange <flkazemakase@gmail.com>
# Contributor: mid-kid <esteve.varela@gmail.com>

pkgbase=python-kivy
pkgname=('python-kivy' 'python2-kivy')
pkgver=1.10.0
pkgrel=1
pkgdesc="A software library for rapid development of hardware-accelerated multitouch applications."
arch=('x86_64')
url="http://kivy.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'gstreamer' 'sdl2_ttf'
             'sdl2_mixer' 'sdl2_image')
checkdepends=('python-nose' 'python2-nose' 'python2-mock' 'python-coverage' 'python2-coverage'
              'python-gobject' 'python2-gobject' 'xorg-server-xvfb' 'opencv' 'python-pyenchant'
              'python2-pyenchant' 'git' 'mtdev' 'xclip' 'xsel' 'gtk3' 'gst-plugins-base'
              'gst-plugins-good' 'python-dbus' 'python2-dbus')
source=("https://pypi.io/packages/source/K/Kivy/Kivy-$pkgver.tar.gz")
sha512sums=('9f6ee0214a3ef784f26cdf7298844e15858a15a81ff8aa399ad2620939a7304f5c08121800293b2977bd3ddaf3c86cfb50d3dce1e4e8b5a696e8400e4f88ced6')

prepare() {
  # For better metadata
  export KIVY_USE_SETUPTOOLS=1

  export LC_CTYPE=en_US.UTF-8

  cp -a Kivy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/Kivy-$pkgver
  python setup.py build build_ext --inplace

  cd "$srcdir"/Kivy-$pkgver-py2
  python2 setup.py build build_ext --inplace
}

check() {
  cd "$srcdir"/Kivy-$pkgver
  xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" nosetests3 kivy/tests

  cd "$srcdir"/Kivy-$pkgver-py2
  xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" nosetests2 kivy/tests
}

package_python-kivy() {
  depends=('python' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')

  cd Kivy-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-kivy() {
  depends=('python2' 'gstreamer' 'sdl2_ttf' 'sdl2_mixer' 'sdl2_image')

  cd Kivy-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/share/kivy{,2}-examples
}
