# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-kiwisolver
pkgver=1.3.2
pkgrel=3
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('PerlArtistic')
arch=('x86_64')
depends=('python')
makedepends=('python-cppy' 'python-pip')
checkdepends=('python-pytest-runner')
source=("https://github.com/nucleic/kiwi/archive/$pkgver.rc1/$pkgname-$pkgver.rc1.tar.gz")
sha512sums=('e9ef20756df587b49f7b8e52ff388db6e2e551548ae7d60b6916586fb35249b10bda895a7be7635b300e6100ec1c7daf7d641de6d26d9b2c36055ac6c176302b')

build() {
  cd kiwi-$pkgver.rc1
  python setup.py build
}

check() {
  cd kiwi-$pkgver.rc1
  python setup.py pytest
}

package() {
  cd kiwi-$pkgver.rc1/
  python3 setup.py install --root="$pkgdir" --optimize=1
}
