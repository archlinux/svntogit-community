# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-kiwisolver
pkgname=(python-kiwisolver python2-kiwisolver)
pkgver=1.1.0
pkgrel=3
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('PerlArtistic')
arch=('x86_64')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/nucleic/kiwi/archive/$pkgver.tar.gz")
sha512sums=('e26a951fdba2fb221effe7ed785010d5795695b30e19fb67a30dc2ac883ee21b1daec967ad89734309c101513cd17bb5971118591270f402b8d46098178aff25')

prepare() {
  cp -a kiwi-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/kiwi-$pkgver/
  python setup.py build

  cd "$srcdir"/kiwi-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/kiwi-$pkgver/
  python setup.py pytest

  cd "$srcdir"/kiwi-$pkgver-py2
  python2 setup.py pytest
}

package_python-kiwisolver() {
  depends=('python-setuptools')

  cd kiwi-$pkgver/
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-kiwisolver() {
  depends=('python2-setuptools')

  cd kiwi-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
