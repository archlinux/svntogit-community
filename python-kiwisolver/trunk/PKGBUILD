# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-kiwisolver
pkgver=1.4.4
pkgrel=1
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('PerlArtistic')
arch=('x86_64')
depends=('python')
makedepends=('python-cppy' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest-runner')
source=("https://github.com/nucleic/kiwi/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9a7ed487bb28f8c430543dc21ea59156078c129fd29b1382f1578368e72f312f4e813eb4501e4c9387647b42b085b93d4bd27d0e846f1396fa4ce68fd9e9ba5f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd kiwi-$pkgver
  python setup.py build
}

check() {
  cd kiwi-$pkgver
  PYTHONPATH="$PWD"/py \
  python setup.py pytest
}

package() {
  cd kiwi-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
