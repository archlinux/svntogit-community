# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Ian Beringer <ian@ianberinger.com>

pkgname=python-kubernetes
_pkgbase="${pkgname#python-}"
_kubernetes_base_commit=b4d3aad42dc23e7a6c0e5c032691f8dc385a786c
pkgver=23.6.0
pkgrel=2
license=('Apache')
pkgdesc='Python client for the kubernetes API'
arch=('any')
url='https://github.com/kubernetes-client/python'
depends=('python' 'python-certifi' 'python-six' 'python-dateutil'
         'python-urllib3' 'python-yaml' 'python-google-auth'
         'python-websocket-client' 'python-requests' 'python-requests-oauthlib'
         'python-adal')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pluggy' 'python-py' 'python-mock' 'python-isort' 'python-nose')
source=("${_pkgbase}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "${_pkgbase}-base-${_kubernetes_base_commit}.tar.gz::https://github.com/kubernetes-client/python-base/archive/${_kubernetes_base_commit}.tar.gz")
sha256sums=('432f994ccb9c53f67cb5027c83e753966843a4650c8dcf563a37651b1583cf97'
            'c76a4f51e662132da2d29cafafe6aeab1778d7e546dc7c918ec6f3e1d4943871')

prepare() {
  cd "${srcdir}"

  mv -v "python-${pkgver}" "${_pkgbase}-${pkgver}"
  mv -v "python-base-${_kubernetes_base_commit}" "${_pkgbase}-base-${_kubernetes_base_commit}"

  # their "base" has been split out to a git submodule, so include it via symlink
  rm -vrf "${_pkgbase}-${pkgver}/kubernetes/base"
  ln -vsf "${srcdir}/${_pkgbase}-base-${_kubernetes_base_commit}" "${_pkgbase}-${pkgver}/kubernetes/base"
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  export PYTHONPATH="${PWD}"
  py.test -vvv -s --ignore=kubernetes/e2e_test
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
