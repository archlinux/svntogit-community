# Maintainer: David Runge <dvzrv@archlinux.org>

_name=lazr.config
pkgname=python-lazr.config
pkgver=2.2.2
pkgrel=4
pkgdesc="Create configuration schemas, and process and validate configurations"
arch=('any')
url="https://launchpad.net/lazr.config"
license=('LGPL3')
depends=('python-lazr.delegates' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-nose')
provides=('python-lazr-config')
replaces=('python-lazr-config')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('3427d2be4b4763238bd8378ec5936d38d0b944074d56f6ada55d0e3f232fe0846630a0ef3900c169b58092d029297d7c937ea4ad349aaa2745ed25b71df9c7a0'
            'SKIP')
b2sums=('5bfee9ae556e6b4109fa43ca36cc2676fb805dc6b9d9f788224ccd29e7836313affa55dd031496c2a252c5e64b150fd8f21c2934da3699ba95e0968ee8022879'
        'SKIP')
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we do not care about coverage in testing
  sed -e '/coverage/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  nosetests -P lazr
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {HACKING,NEWS,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
