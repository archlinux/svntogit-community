# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgbase=python-ldap
pkgname=(python-ldap python2-ldap)
pkgver=3.2.0
pkgrel=1
pkgdesc="LDAP client API for Python"
url="https://python-ldap.readthedocs.io/"
arch=('x86_64')
license=('MIT')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools'
	     'python-coverage' 'python2-coverage')
checkdepends=('openldap' 'python-pyasn1' 'python-pyasn1-modules'
	      'python2-pyasn1' 'python2-pyasn1-modules')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/python-ldap/python-ldap/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('ac6736345f5cdcd76fe54ac3ec6182b222c59c68d04c57690e44e449871e5730')

prepare() {
  cp -a ${pkgbase}-${pkgbase}-$pkgver{,-py2}
}

build(){
  cd "$srcdir/$pkgbase-$pkgbase-$pkgver"
  python setup.py build

  cd "$srcdir/$pkgbase-$pkgbase-$pkgver-py2"
  python2 setup.py build
}

check(){
  cd "$srcdir/$pkgbase-$pkgbase-$pkgver"
  python -m coverage run --parallel setup.py test

  cd "$srcdir/$pkgbase-$pkgbase-$pkgver-py2"
  python2 -m coverage run --parallel setup.py test
}

package_python2-ldap() {
  depends=('python2' 'python2-pyasn1' 'python2-pyasn1-modules' 'libldap')
  cd "$srcdir/$pkgbase-$pkgbase-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}

package_python-ldap() {
  depends=('python' 'python-pyasn1' 'python-pyasn1-modules' 'libldap')
  cd "$pkgbase-$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

