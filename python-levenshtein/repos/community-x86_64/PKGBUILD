# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>
# Contributor: scj <scj archlinux us>

pkgname=python-levenshtein
pkgver=0.20.1
pkgrel=2
pkgdesc="Python extension for computing string edit distances and similarities"
url="https://pypi.python.org/pypi/python-Levenshtein/"
license=('GPL')
arch=('x86_64')
depends=('python-rapidfuzz')
makedepends=('python-scikit-build' 'cython' 'rapidfuzz-cpp')
checkdepends=('python-pytest')
#source=("https://files.pythonhosted.org/packages/source/L/Levenshtein/Levenshtein-$pkgver.tar.gz")
source=("https://github.com/maxbachmann/Levenshtein/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9f3ab4d0e9766980e30956b85142528af2263f557d7cd3de50684fcb9aa713cdd7fd011762d16b2e2f0fe0a54e217c6d8f45246a6ccd33a4ea132ce0674c0a0f')

build() {
  cd Levenshtein-$pkgver
  python setup.py build
}

check() {
  cd Levenshtein-$pkgver
  PYTHONPATH=`echo "$PWD"/_skbuild/linux-x86_64-*/setuptools/lib.linux-x86_64-*` \
  pytest
}

package() {
  cd Levenshtein-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
