# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.10.3
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python tmux)
makedepends=(python-build python-installer python-poetry)
checkdepends=(python-pytest procps-ng)
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('2c8f28ce8c313404caffa42e5fd4a1dfa5d48bdc48203f8db595cb97b44886d63608771f23d62d76a56513b7e4578e48a31cacd224eca92425f110efd0a5db69')
b2sums=('40d591ee1f8a6068947b61416d6247b2475cc99031a67c167dc527631e50075f18c46fe6fc16193867d83a1a735b0a23fbfbc77f4539f788f05375f1f68b77d2')

build() {
  cd "${_name}-${pkgver}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGES,README.md,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
