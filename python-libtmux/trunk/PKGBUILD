# Maintainer: David Runge <dave@sleepmap.de>
_name=libtmux
pkgname=python-libtmux
pkgver=0.8.2
pkgrel=1
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('MIT')
depends=('python' 'tmux')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/${_name}/archive/v${pkgver}.tar.gz")
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('e5f3768f4ddd746c5380a92899961be05b1b6c567d9333720b0fd9ed124131671bbecf1c04a7c4e658092424b6dc89dd656ba559385b0a6f673f9895dd6c829c')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python -m pytest
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGES,README.rst,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
