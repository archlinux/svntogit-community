# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=python-linux-procfs
pkgver=0.6.2
pkgrel=3
pkgdesc="Linux /proc abstraction classes for python"
arch=('any')
url="https://git.kernel.org/pub/scm/libs/python/python-linux-procfs/python-linux-procfs.git/"
license=('GPL2')
depends=('python-six')
makedepends=('python-setuptools')
source=("https://git.kernel.org/pub/scm/libs/python/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8af4ce0fcee8e2a9df234f8a73b8c33716bc378d0ae4f4cd991bd453f420f64024b3c085f2c63b9e6447988e4175c6e4b252567bb5576c7232f77c20301072b6')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix broken import (notified upstream about issue on mailing list)
  sed -e 's/utilist/.utilist/g' -i procfs/procfs.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build/:${PYTHONPATH}"
  python bitmasklist_test.py
}

package() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
}
