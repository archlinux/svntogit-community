# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>

pkgname=python-llfuse
pkgver=1.3.6
pkgrel=1
pkgdesc="A set of Python bindings for the low level FUSE API."
arch=('x86_64')
url="https://bitbucket.org/nikratio/python-llfuse/"
license=('LGPL')
depends=('python' 'fuse2')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-pytest')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/75/b4/5248459ec0e7e1608814915479cb13e5baf89034b572e3d74d5c9219dd31/llfuse-$pkgver.tar.bz2")
md5sums=('4996674fa327c6c93174c1f71961ac6c')

build() {
  cd "${srcdir}/llfuse-${pkgver}"
  python setup.py build_ext --inplace
}

check() {
  cd "${srcdir}/llfuse-${pkgver}"
  python -m pytest test/
}

package() {
  cd "${srcdir}/llfuse-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
}
