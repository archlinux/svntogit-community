# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=py-lmdb
pkgname=python-lmdb
pkgver=1.2.1
pkgrel=3
pkgdesc="Universal Python binding for the LMDB Lightning Database"
arch=(x86_64)
url="https://github.com/jnwatson/py-lmdb/"
license=('custom: OpenLDAP')
depends=(python python-cffi lmdb)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(${url}/archive/${_pkg}_${pkgver}.tar.gz)
sha256sums=('9456cb1430a2eff85c08e6d0abc5afb3da09d9d52ddea04eedc61d9e74721c13')

build() {
  cd ${_pkg}-${_pkg}_${pkgver}
  LMDB_FORCE_SYSTEM=1 python setup.py build
}

check() {
  cd ${_pkg}-${_pkg}_${pkgver}
  local _pyver
  _pyver="$(python -c 'import sys; print("%s.%s" %sys.version_info[0:2])')"
  export LMDB_FORCE_SYSTEM=1
  export PYTHONPATH="$(pwd)/build/lib.linux-${CARCH}-${_pyver}"
  pytest
}

package() {
  cd ${_pkg}-${_pkg}_${pkgver}
  LMDB_FORCE_SYSTEM=1 python setup.py install --root="$pkgdir" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
