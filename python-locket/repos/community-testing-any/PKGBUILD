# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=locket
pkgname=python-${_pkg}
pkgver=0.2.1
pkgrel=4
pkgdesc="File-based locks for Python"
arch=(any)
url="https://github.com/mwilliamson/locket.py"
license=(BSD)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(python-nose python-spur)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('cda50c4b8b759dd2661451897a1fc69c655467cd6654f5305eae4508e6c659d0')

build() {
  cd ${_pkg}.py-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}.py-${pkgver}
  PYTHONPATH="${PWD}/build/lib/" nosetests
}

package() {
  cd ${_pkg}.py-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
