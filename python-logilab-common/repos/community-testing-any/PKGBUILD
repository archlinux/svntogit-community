# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=python-logilab-common
pkgname=('python2-logilab-common' 'python-logilab-common')
pkgver=1.2.2
pkgrel=2
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-six' 'python2-six' 'python-pytz' 'python2-pytz' 'python2-egenix-mx-base')
source=("https://pypi.io/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz")
sha1sums=('ecad2d10c31dcf183c8bed87b6ec35e7ed397d27')

prepare() {
  cp -a logilab-common-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/logilab-common-$pkgver
  python setup.py build

  cd "$srcdir"/logilab-common-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/logilab-common-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python bin/pytest

  cd "$srcdir"/logilab-common-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 bin/pytest
}

package_python-logilab-common() {
  depends=('python')
  optdepends=('python-six: for logilab.common.testlib')

  cd "$srcdir"/logilab-common-$pkgver

  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"

  mv "$pkgdir"/usr/bin/pytest{,-logilab}
}

package_python2-logilab-common() {
  depends=('python2')
  optdepends=('python2-six: for logilab.common.testlib')

  cd "$srcdir"/logilab-common-$pkgver-py2

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"

  mv "$pkgdir"/usr/bin/pytest{,2-logilab}
}
