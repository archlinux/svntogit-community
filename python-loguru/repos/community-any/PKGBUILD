# Maintainer: David Runge <dvzrv@archlinux.org>

_name=loguru
pkgname=python-loguru
pkgver=0.6.0
pkgrel=3
pkgdesc="Python logging made (stupidly) simple"
arch=(any)
url="https://github.com/Delgan/loguru"
license=(MIT)
depends=(python)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-colorama
  python-freezegun
  python-pytest
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('066bd06758d0a513e9836fd9c6b5a75bfb3fd36841f4b996bc60b547a309d41c')
b2sums=('c1faed884b8831ef2e708539caa61d4d2a9c92b6465fcd22a31aa614d65ff384fbf82730e08a5c1da9026079f44bda2bd13b5602be9b0535970c03f8bf132798')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    --deselect tests/test_repr.py::test_function_without_name
    --deselect tests/test_repr.py::test_function_with_empty_name
    --deselect tests/test_filesink_rotation.py::test_time_rotation_reopening_native # https://github.com/Delgan/loguru/issues/658
  )

  cd $_name-$pkgver
  pytest -vv "${pytest_options[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 *.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
