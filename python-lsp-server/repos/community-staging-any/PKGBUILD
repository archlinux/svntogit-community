# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Platon Pronko <platon7pronko@gmail.com>

pkgname=python-lsp-server
pkgver=1.2.4
pkgrel=4
pkgdesc="Fork of the python-language-server project, maintained by the Spyder IDE team and the community"
arch=(any)
url="https://github.com/python-lsp/python-lsp-server"
license=(MIT)
depends=(python-jedi python-lsp-jsonrpc python-pluggy python-ujson python-setuptools)
checkdepends=(
    python-pytest
    python-flaky
    python-matplotlib
    python-mccabe
    python-numpy
    python-pandas
    python-pycodestyle
    python-pydocstyle
    python-pyflakes
    python-pylint
    python-pyqt5
    python-rope
    autopep8
    flake8
    yapf
)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-fix-pylint-2.10.patch::https://github.com/python-lsp/python-lsp-server/commit/e8cf8e5b160f227550574cba480e276a98248d44.patch)
sha256sums=('3e9102808f661df18a54e2ddd97285d4b3c4cbc0af6fed8439a1960617f74674'
            '1778835e6926a84cc44c16628463394f4a36a5abd532bba2aa87a133ad4f14fe')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../${pkgname}-fix-pylint-2.10.patch
  sed 's|flake8>=3.8.0,<4.0.0|flake8>=3.8.0|g' -i setup.py
  sed 's|pyflakes>=2.3.0,<2.4.0|pyflakes>=2.3.0|g' -i setup.py
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  sed -i '/addopts =$/,$d' setup.cfg
  pytest -v --color=yes
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir} --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
