# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=lupa
pkgname=python-lupa
pkgver=1.10
pkgrel=1
pkgdesc="Python wrapper around Lua and LuaJIT"
arch=('x86_64')
url="https://github.com/scoder/lupa"
license=('MIT')
depends=('lua53' 'python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('e2511b27f381f6fdb66ef40dcc518215038197431b241935678dfc3d51178231')
b2sums=('2620abad8d35e21425d9a5d4bf118754f759dd9dd7383459ef8196e304f7ec069c088ba09d281d4ba688eb91d80c4022f614f56813caf567087cfe4b45ac8c83')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # --no-bundle is not removed from argv if lua is properly detected, so the
    # only working safety net is to rm the bundled sources
    rm -rf third-party
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build --no-luajit
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
