# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-ly
pkgver=0.9.6
pkgrel=2
pkgdesc="Parse, manipulate or create documents in LilyPond format"
arch=('any')
url="https://github.com/frescobaldi/python-ly"
license=('GPL')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('76da1c895555c87eb58308db13347feb66e9090bb1f6cdc253a1eaf810439dbe4089e9e0453fa1e6e0e30d19a91e0a9d7d20ea3ceb0062c76ba16cdf3b389e3a')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  cd doc
  make man text
}

check() {
  cd "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  # docs
  install -vDm 644 {ChangeLog,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # text readme
  install -vDm 644 doc/build/text/* \
    -t "${pkgdir}/usr/share/doc/${pkgname}/text/"
  # man pages
  install -vDm 644 doc/build/man/* \
    -t "${pkgdir}/usr/share/man/man1/"
}
