# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-manhole
pkgname=('python-manhole' 'python2-manhole')
pkgver=1.6.0
pkgrel=1
arch=('any')
pkgdesc='Debugging manhole for python applications'
license=('BSD')
url='https://github.com/ionelmc/python-manhole'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-virtualenv' 'python2-virtualenv' 'python-pytest-runner'
              'python2-pytest-runner' 'python-process-tests' 'python2-process-tests'
              'python-requests' 'python2-requests' 'uwsgi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/python-manhole/archive/v$pkgver.tar.gz")
sha512sums=('179a87529e371c4480b6ab0ad6cc6cc9e3942f4be339cacaf1f1b100e447b8b1f2903a03879e9df3376c86ced4421aa5a16d934c9b250f91715b28d03dca000a')

prepare() {
  cp -a python-manhole-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-manhole-$pkgver
  python setup.py build

  cd "$srcdir"/python-manhole-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack to process .pth files properly

  cd "$srcdir"/python-manhole-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python setup.py pytest
  ) || warning "Tests failed"

  cd "$srcdir"/python-manhole-$pkgver-py2
  virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
  (
    . "$srcdir/pyvenv-py2/bin/activate"
    python2 setup.py install
    python2 setup.py pytest
  ) || warning "Tests failed"
}

package_python-manhole() {
  depends=('python')

  cd python-manhole-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-manhole() {
  depends=('python2')

  cd python-manhole-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/manhole-cli{,2}
}
