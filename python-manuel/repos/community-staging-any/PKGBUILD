# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-manuel
pkgname=('python-manuel' 'python2-manuel')
pkgver=1.10.1
pkgrel=2
pkgdesc="Manuel lets you build tested documentation"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/manuel"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-zope-testing' 'python2-zope-testing')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/benji-york/manuel/archive/$pkgver.tar.gz")
sha512sums=('7109852a80097a12237a363c91ac433dd2662e238ab4f20757b4d4b93019d0ada19069fd45a13a66128adf4dbc32f162d12a90879db2f2eb4748635118d75072')

build() {
  cd manuel-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd manuel-$pkgver
  python setup.py test
  python2 setup.py test
}

package_python-manuel() {
  depends=('python-setuptools')

  cd manuel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-manuel() {
  depends=('python2-setuptools')

  cd manuel-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
