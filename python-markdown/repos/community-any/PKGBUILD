# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrew Antle <andrew dot antle at gmail dot com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chaiwat Suttipongsakul <cwt at bashell dot com>

pkgbase=python-markdown
pkgname=('python-markdown' 'python2-markdown')
_pkgbasename=Markdown
pkgver=2.1.1
pkgrel=1
pkgdesc="Python implementation of John Gruber's Markdown."
arch=('any')
url='http://www.freewisdom.org/projects/python-markdown/'
license=('BSD')
depends=('python' 'python2')
source=("http://pypi.python.org/packages/source/M/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz")
md5sums=('3f82d30c488e4b88b8f86eb1c9ad0da7')

build() {
  cp -r $_pkgbasename-$pkgver $srcdir/python2-markdown
  cd $srcdir/python2-markdown
  find $srcdir/python2-markdown -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}


package_python-markdown() {
  cd $_pkgbasename-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 
}

package_python2-markdown() {
  cd $srcdir/python2-markdown
  python2 setup.py install --root="$pkgdir" --optimize=1 
}
