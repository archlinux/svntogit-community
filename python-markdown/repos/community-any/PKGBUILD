# $Id$
# Maintainer: Kyle Keen <keenerd#gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrew Antle <andrew dot antle at gmail dot com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chaiwat Suttipongsakul <cwt at bashell dot com>

pkgbase=python-markdown
pkgname=('python-markdown' 'python2-markdown')
_pkgbasename=Markdown
pkgver=2.1.1
pkgrel=5
pkgdesc="Python implementation of John Gruber's Markdown."
arch=('any')
url='http://freewisdom.org/projects/python-markdown/'
license=('BSD')
makedepends=('python' 'python2')
source=("http://pypi.python.org/packages/source/M/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz" 
        "markdown.py3.patch")
md5sums=('3f82d30c488e4b88b8f86eb1c9ad0da7'
         '19ed1619797f2a32b73bedf697f8a2b6')

build() {
  cp -r $_pkgbasename-$pkgver "$srcdir/python2-markdown"
  cd "$srcdir/python2-markdown"
  find "$srcdir/python2-markdown" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}


package_python-markdown() {
  depends=('python')
  cd $_pkgbasename-$pkgver
  patch -N markdown/__main__.py "$srcdir/markdown.py3.patch"
  python setup.py install --root="$pkgdir" --optimize=0
}

package_python2-markdown() {
  depends=('python2')
  cd "$srcdir/python2-markdown"
  python2 setup.py install --root="$pkgdir" --optimize=0
  mv "$pkgdir/usr/bin/markdown_py" "$pkgdir/usr/bin/markdown_py2"
}
