# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-marshmallow-sqlalchemy
pkgver=0.24.0
pkgrel=2
pkgdesc='SQLAlchemy integration with marshmallow'
url=https://marshmallow-sqlalchemy.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python-marshmallow
  python-sqlalchemy
)
makedepends=(
  git
  python-setuptools
)
_tag=45df83115f84abc55e037ce9c2d05aff3f7ded52
source=(git+https://github.com/marshmallow-code/marshmallow-sqlalchemy.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd marshmallow-sqlalchemy

  git describe --tags
}

build() {
  cd marshmallow-sqlalchemy

  python setup.py build
}

package() {
  cd marshmallow-sqlalchemy

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-marshmallow-sqlalchemy/
}

# vim: ts=2 sw=2 et:
