# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-marshmallow-sqlalchemy
pkgver=0.19.0
pkgrel=2
pkgdesc='SQLAlchemy integration with marshmallow'
url=https://marshmallow-sqlalchemy.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python-marshmallow
  python-sqlalchemy
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/marshmallow-code/marshmallow-sqlalchemy.git#tag=abf13441214e0ccd13c93cbcac493c065042cace)
sha256sums=(SKIP)

pkgver() {
  cd marshmallow-sqlalchemy

  git describe --tags
}

build() {
  cd marshmallow-sqlalchemy

  python setup.py build
}

package() {
  cd marshmallow-sqlalchemy

  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-marshmallow-sqlalchemy/
}

# vim: ts=2 sw=2 et:
