# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-marshmallow
pkgname=(python-marshmallow python2-marshmallow)
pkgver=2.15.4
pkgrel=1
pkgdesc="A lightweight library for converting complex datatypes to and from native Python datatypes."
url="https://github.com/marshmallow-code/marshmallow"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-simplejson'
              'python2-simplejson' 'python-pytz' 'python2-pytz' 'python-dateutil'
              'python2-dateutil')
source=("https://pypi.io/packages/source/m/marshmallow/marshmallow-$pkgver.tar.gz")
sha512sums=('5cbbf3c0e2d7e615b8944972ec312c5802be0c4f82ddca2bd8a08648c9d22a4334635e875cf1879f75cb763d43460533b795232f48c4b0990255144806b2a08d')

prepare() {
  cp -a marshmallow-$pkgver{,-py2}
  rm -r marshmallow-$pkgver-py2/tests/test_py3
}

build() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py build

  cd "$srcdir"/marshmallow-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py pytest

  cd "$srcdir"/marshmallow-$pkgver-py2
  python2 setup.py pytest
}

package_python-marshmallow() {
  depends=('python')

  cd marshmallow-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-marshmallow() {
  depends=('python2')

  cd marshmallow-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
