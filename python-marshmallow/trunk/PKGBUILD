# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-marshmallow
pkgname=(python-marshmallow python2-marshmallow)
pkgver=2.17.0
pkgrel=1
pkgdesc="A lightweight library for converting complex datatypes to and from native Python datatypes."
url="https://github.com/marshmallow-code/marshmallow"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-simplejson'
              'python2-simplejson' 'python-pytz' 'python2-pytz' 'python-dateutil'
              'python2-dateutil')
source=("https://pypi.io/packages/source/m/marshmallow/marshmallow-$pkgver.tar.gz")
sha512sums=('ec941f00e29fc49b6ae515307701df4194703c591386d2bf346ff3bf521d5b5281a3100343702957595e027074a05b8dc417f3a7ed23696e912002a20f61cb9e')

prepare() {
  cp -a marshmallow-$pkgver{,-py2}
  rm -r marshmallow-$pkgver-py2/tests/test_py3
}

build() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py build

  cd "$srcdir"/marshmallow-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py pytest

  cd "$srcdir"/marshmallow-$pkgver-py2
  python2 setup.py pytest
}

package_python-marshmallow() {
  depends=('python')

  cd marshmallow-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-marshmallow() {
  depends=('python2')

  cd marshmallow-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
