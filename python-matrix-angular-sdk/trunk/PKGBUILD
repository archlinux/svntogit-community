# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-matrix-angular-sdk
pkgname=(python-matrix-angular-sdk python2-matrix-angular-sdk)
pkgver=0.6.8
pkgrel=3

pkgdesc="AngularJS services for implementing the Client-Server API on Matrix"
url="https://github.com/matrix-org/matrix-angular-sdk"
arch=('any')
license=('Apache')

makedepends=('python'
             'python-setuptools'
             'python2'
             'python2-setuptools')

source=("python-matrix-angular-sdk-$pkgver.tar.gz::https://github.com/matrix-org/matrix-angular-sdk/archive/v$pkgver.tar.gz")

md5sums=('9cf2dd16d1fd34dc24c64a7efcdb2efd')

prepare() {
	cp -a matrix-angular-sdk-$pkgver{,-python2}
}

build() {
	cd matrix-angular-sdk-$pkgver
	python setup.py build 

	cd "$srcdir"/matrix-angular-sdk-$pkgver-python2
	python2 setup.py build 
}

package_python-matrix-angular-sdk() {
	depends=('python')

	cd matrix-angular-sdk-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build
}

package_python2-matrix-angular-sdk() {
	depends=('python2')

	cd matrix-angular-sdk-$pkgver-python2
	python2 setup.py install --root "$pkgdir" --optimize=1 --skip-build
}
