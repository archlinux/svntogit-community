# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>

pkgname=python-matrix-common
pkgver=1.0.0
pkgrel=1
pkgdesc="Common code for Synapse, Sydent and Sygnal"
arch=(any)
url="https://github.com/matrix-org/matrix-python-common"
license=('Apache')
makedepends=('python-setuptools' "python-dephell")
checkdepends=('python-twisted' 'python-service-identity' 'python-aiounittest')
source=($pkgname-$pkgver.tar.gz::"https://github.com/matrix-org/matrix-python-common/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('0050243ce3930962934f94fe1c990dc97ce803e79998ee7e6c7dafdf5ca4736c8c06fd4f52223b81f2ceb91a8bcf70f6488559f7935a9bd087c33a11430af8cd')
b2sums=('04a9ccbd6d66bffdc21dec013e839783abb89c8565de9d2b5957c2620aa7357756519f0d0d0ae05c8ada4e50b63488188f50b44308c1ea6fc3e55c03882e8610')

prepare() {
	cd "matrix-python-common-$pkgver"
	dephell deps convert --from pyproject.toml --to setup.py
}

build() {
	cd "matrix-python-common-$pkgver"
	python setup.py build
}

check() {
	cd "matrix-python-common-$pkgver"
	PYTHONPATH="$PWD" trial tests
}

package() {
	cd "matrix-python-common-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
