# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-matrix-nio
pkgver=0.19.0
_tag=52109a160765f1a9e7f78be1aed8a282a83515f1 # git rev-parse "$pkgver"
pkgrel=1
pkgdesc='Python Matrix client library, designed according to sans I/O principles'
arch=('any')
url='https://github.com/poljar/matrix-nio'
license=('ISC')
depends=('python' 'python-aiofiles' 'python-aiohttp' 'python-aiohttp-socks' 'python-atomicwrites' 
         'python-cachetools' 'python-future' 'python-h11' 'python-h2' 'python-jsonschema' 'python-logbook' 
         'python-olm' 'python-peewee' 'python-pycryptodome' 'python-unpaddedbase64')
makedepends=('git' 'python-dephell' 'python-setuptools')
checkdepends=('python-aioresponses' 'python-faker' 'python-hpack' 'python-hyperframe' 'python-hypothesis'
              'python-pytest' 'python-pytest-aiohttp' 'python-pytest-benchmark')
source=("git+$url.git?signed#tag=$_tag")
sha512sums=('SKIP')
validpgpkeys=('689A3B5BC6560AB4C99A2A0581314DA807EF4E22') # Damir JeliÄ‡ (poljar) <poljar@termina.org.uk>

pkgver() {
	cd "${pkgname#python-}"
	git describe | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
	cd "${pkgname#python-}"
	dephell deps convert --from pyproject.toml --to setup.py
}

build() {
	cd "${pkgname#python-}"
	python setup.py build
}

check() {
	cd "${pkgname#python-}"
	python -m pytest
}

package() {
	cd "${pkgname#python-}"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
