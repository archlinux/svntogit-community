# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-matrix-nio
pkgver=0.12.0
_tag=e2e6239ad44c9364440af7c73097c0ae13f868e6 # git show-ref "$pkgver"
pkgrel=1
pkgdesc='Python Matrix client library, designed according to sans I/O principles'
arch=('any')
url='https://github.com/poljar/matrix-nio'
license=('ISC')
depends=('python-aiofiles' 'python-aiohttp' 'python-atomicwrites' 'python-cachetools'
         'python-future' 'python-h11' 'python-h2' 'python-jsonschema' 'python-logbook' 'python-olm'
         'python-peewee' 'python-pycryptodome' 'python-unpaddedbase64')
makedepends=('git')
checkdepends=('python-aioresponses' 'python-faker' 'python-hpack' 'python-hyperframe' 'python-hypothesis'
              'python-pytest' 'python-pytest-aiohttp' 'python-pytest-benchmark')
source=("git+$url.git?signed#tag=$_tag")
sha512sums=('SKIP')
validpgpkeys=('689A3B5BC6560AB4C99A2A0581314DA807EF4E22') # Damir JeliÄ‡ (poljar) <poljar@termina.org.uk>

pkgver() {
	cd "${pkgname#python-}"
	git describe | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
	cd "${pkgname#python-}"
	python setup.py build
}

check() {
	cd "${pkgname#python-}"
	python -m pytest
}

package() {
	cd "${pkgname#python-}"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
