# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-matrix-nio
pkgver=0.20.0
_tag=b2bde370d41ae9a2a20f185af3d4349dbf7c6fa0 # git rev-parse "$pkgver"
pkgrel=1
pkgdesc='Python Matrix client library, designed according to sans I/O principles'
arch=('any')
url='https://github.com/poljar/matrix-nio'
license=('ISC')
depends=('python' 'python-aiofiles' 'python-aiohttp' 'python-aiohttp-socks' 'python-future' 'python-h11'
         'python-h2' 'python-jsonschema' 'python-logbook' 'python-pycryptodome' 'python-unpaddedbase64')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-aioresponses' 'python-atomicwrites' 'python-cachetools' 'python-faker' 'python-hpack'
              'python-hyperframe' 'python-hypothesis' 'python-olm' 'python-peewee' 'python-pytest'
              'python-pytest-aiohttp' 'python-pytest-asyncio' 'python-pytest-benchmark' 'python-setuptools')
optdepends=('python-atomicwrites: end-to-end encryption support'
            'python-cachetools: end-to-end encryption support'
            'python-peewee: end-to-end encryption support'
            'python-olm: end-to-end encryption support')
source=("git+$url.git?signed#tag=$_tag")
sha512sums=('SKIP')
validpgpkeys=('689A3B5BC6560AB4C99A2A0581314DA807EF4E22') # Damir JeliÄ‡ (poljar) <poljar@termina.org.uk>

pkgver() {
	cd "${pkgname#python-}"
	git describe | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
	cd "${pkgname#python-}"
	# Remove unnecessary dependency version pinning
	sed -i 's/= "^[0-9.]*"/= "*"/' pyproject.toml
}

build() {
	cd "${pkgname#python-}"
	python -m build --wheel --no-isolation
}

check() {
	cd "${pkgname#python-}"
	python -m pytest
}

package() {
	cd "${pkgname#python-}"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
