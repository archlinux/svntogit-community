# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-maxminddb
pkgver=1.5.1
_testdatacommit=9a56cf59da5c380f6014d042189ad8bce6f3dd89
pkgrel=1
pkgdesc="Reader for the MaxMind DB format"
url="https://github.com/maxmind/MaxMind-DB-Reader-python"
license=('Apache')
arch=('x86_64')
depends=('python' 'libmaxminddb')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/maxmind/MaxMind-DB-Reader-python/archive/v$pkgver.tar.gz"
        https://github.com/maxmind/MaxMind-DB/archive/$_testdatacommit.tar.gz)
sha512sums=('d62542127d44ad2740d213331a7d0be0fc5bc9687d06b2c42501e9456727c0b266207bf01450c170d55d0b969cfc3396f533646376ceca1a2070c11b3e47aea4'
            'c5340eca903585f4dfc0e5cde6d2ae1aa2064bad7a40b6ea859644cd1a2bbf6058af7d2b6ce6624a82fdf6ab3fbc57b9c71ccc8683293b620164697ca2b8e6ab')

prepare() {
  cd MaxMind-DB-Reader-python-$pkgver
  rmdir tests/data
  ln -s "$srcdir"/MaxMind-DB-$_testdatacommit tests/data
}

build() {
  cd MaxMind-DB-Reader-python-$pkgver
  python setup.py build
}

check() {
  cd MaxMind-DB-Reader-python-$pkgver
  python setup.py nosetests
}

package() {
  cd MaxMind-DB-Reader-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
