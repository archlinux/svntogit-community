# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>

pkgbase=python-mccabe
pkgname=(python-mccabe python2-mccabe)
_reponame=mccabe
pkgver=0.3
pkgrel=1
pkgdesc="McCabe complexity checker for Python"
arch=('any')
url="https://github.com/flintwork/mccabe"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/flintwork/${_reponame}/archive/${pkgver}.tar.gz")
md5sums=('c583f58ea28be12842c001473d77504d')

prepare() {
  cp -a ${_reponame}-${pkgver}{,-py2}
}

build() {
  cd ${_reponame}-${pkgver}
  python setup.py build

  cd ../${_reponame}-${pkgver}-py2
  python2 setup.py build
}

check() {
  cd ${_reponame}-${pkgver}
  python setup.py test

  cd ../${_reponame}-${pkgver}-py2
  python2 setup.py test
}

package_python-mccabe() {
  depends=('python')

  cd ${_reponame}-${pkgver}
  python setup.py install --root="${pkgdir}/" --prefix=/usr --skip-build --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-mccabe() {
  depends=('python2')

  cd ${_reponame}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}/" --prefix=/usr --skip-build --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
