# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=mdit-py-plugins
pkgname=python-mdit_py_plugins
pkgver=0.2.6
pkgrel=1
pkgdesc='Collection of core plugins for markdown-it-py'
arch=('any')
url='https://github.com/executablebooks/mdit-py-plugins'
license=('MIT')
depends=('python-markdown-it-py')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-regressions')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d123296d1c5851c87942620129c622c23c5924b685e4fbb8fef8ebe9572a298d4e696fcc8b15806b249ba341a2b01f773feccb45044842a843766d01be4664ee')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
