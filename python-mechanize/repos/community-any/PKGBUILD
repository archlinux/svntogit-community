# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

_pkgname=mechanize
pkgbase=python-mechanize
pkgname=python2-mechanize
pkgver=0.3.6
pkgrel=1
epoch=1
pkgdesc='Stateful programmatic web browsing in Python'
arch=('any')
url="https://github.com/python-mechanize/mechanize"
license=('BSD')
depends=('python2' 'python2-html5lib')
optdepends=('python2-html5-parser: faster parsing of HTML')
makedepends=('python2-distribute')
checkdepends=('python2-zope-interface' 'python2-twisted')
source=("https://pypi.io/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('654e705157156c1f316601ea4f7ab27e96713a8a4dabe1604e6cc8d48e0aa1e8'
            'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
  cd "mechanize-$pkgver"

  python2 setup.py build
}

check() {
  cd "mechanize-$pkgver"

  python2 run_tests.py
}

package_python2-mechanize() {
  cd "mechanize-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
