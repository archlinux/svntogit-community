# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mimeparse
pkgname=(python-mimeparse python2-mimeparse)
pkgver=0.1.4
pkgrel=4
pkgdesc='Module of basic functions for parsing mime-type names and matching them against a list of media-ranges'
arch=('any')
url="https://github.com/dbtsai/python-mimeparse"
license=('MIT')
makedepends=('python' 'python2')
source=("http://pypi.python.org/packages/source/p/$pkgbase/$pkgbase-$pkgver.tar.gz")
md5sums=('1d2816a16f17dcfe0c613da611fe7e13')

prepare() {
  cp -a $pkgbase-$pkgver{,-py2}
}

package_python-mimeparse() {
  depends=('python')

  cd $pkgbase-$pkgver
  python setup.py install -O1 --root "$pkgdir"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-mimeparse() {
  depends=('python2')

  cd $pkgbase-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
