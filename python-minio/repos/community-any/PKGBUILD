# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-minio
pkgver=7.1.12
pkgrel=1
pkgdesc='MinIO Python Library for Amazon S3 Compatible Cloud Storage'
arch=('any')
url='https://github.com/minio/minio-py'
license=('Apache')
depends=(
  'python'
  'python-certifi'
  'python-urllib3'
)
makedepends=(
  'git'
  'python-setuptools'
)
checkdepends=(
  'minio'
  'python-pytest'
  'python-mock'
)
_commit='f764d45fbcb6ac97882851961e5d58afe86655d8'
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags
}

prepare() {
  cd "$pkgname"

  ln -sf /usr/bin/minio tests/functional/minio
}

build() {
  cd "$pkgname"

  python setup.py build
}

check() {
  cd "$pkgname"

  python -m pytest

  ./run_functional_tests.sh
}

package() {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1
}
