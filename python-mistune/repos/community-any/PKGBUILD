# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: shmilee

pkgbase=python-mistune
_pkgname=mistune
pkgname=('python-mistune' 'python2-mistune')
pkgver=0.8.3
pkgrel=2
pkgdesc="The fastest markdown parser in pure Python with renderer feature."
arch=('any')
url="https://github.com/lepture/mistune"
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tgz::https://github.com/lepture/mistune/archive/v${pkgver}.tar.gz")
md5sums=('2f2cc1d1d6d58460a52e22778ef86d44')

prepare() {
  cd "$srcdir"
  cp -r "$srcdir/$_pkgname-$pkgver" "$srcdir/${_pkgname}2-$pkgver"
}

package_python-mistune() {
  cd "$srcdir/${_pkgname}-$pkgver"
  python setup.py install --root="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-mistune/LICENSE"
}

package_python2-mistune() {
  depends=('python2')
  cd "$srcdir/${_pkgname}2-$pkgver"
  python2 setup.py install --root="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-mistune/LICENSE"
}

# vim:set ts=2 sw=2 et:
