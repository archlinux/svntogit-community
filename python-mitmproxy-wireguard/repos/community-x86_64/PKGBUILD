# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-mitmproxy-wireguard
_pyname=mitmproxy_wireguard
pkgver=0.1.18
pkgrel=1
pkgdesc='WireGuard frontend for mitmproxy'
arch=('x86_64')
url='https://github.com/decathorpe/mitmproxy_wireguard'
license=('MIT')
depends=('python')
makedepends=('maturin' 'python-installer' 'cargo')
options=(!lto)
source=("https://github.com/decathorpe/mitmproxy_wireguard/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('55a09e5e156007ccc0bd6551f12d04c16b1d6644310b3695559074ac8de6eaff07016a4c4e8e9cdbf623a7ad37c3c81d82034e9a1620bf7a65a195483e16342f')

build() {
  cd ${_pyname}-${pkgver}
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_pyname}-${pkgver}
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  python -c 'import mitmproxy_wireguard'
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
