# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-mitmproxy-wireguard
_pyname=mitmproxy_wireguard
pkgver=0.1.19
pkgrel=1
pkgdesc='WireGuard frontend for mitmproxy'
arch=('x86_64')
url='https://github.com/decathorpe/mitmproxy_wireguard'
license=('MIT')
depends=('python')
makedepends=('maturin' 'python-installer' 'cargo')
options=(!lto)
source=("https://github.com/decathorpe/mitmproxy_wireguard/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8e9a15e56d5d6a545be1c80dc1bf138240a876407b4be74a990b993ab796bb55ea615b782b5fc2984a93c55125ce4577e2ea31d87886dc98a67c7c1a2e3cf15f')

build() {
  cd ${_pyname}-${pkgver}
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_pyname}-${pkgver}
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  python -c 'import mitmproxy_wireguard'
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
