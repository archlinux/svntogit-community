# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-mockito
pkgname=('python-mockito' 'python2-mockito')
pkgver=1.0.12
pkgrel=1
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools' 'python2-setuptools'
             'python2-funcsigs' 'git')
source=("git+https://github.com/kaste/mockito-python.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a mockito-python{,-py2}
}

build() {
  cd "$srcdir"/mockito-python
  python setup.py build

  cd "$srcdir"/mockito-python-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mockito-python
  py.test build/lib

  cd "$srcdir"/mockito-python-py2
  py.test2 build/lib
}

package_python-mockito() {
  depends=('python')

  cd mockito-python
  python setup.py install --root="$pkgdir" -O1
}

package_python2-mockito() {
  depends=('python2-funcsigs')

  cd mockito-python-py2
  python2 setup.py install --root="$pkgdir" -O1
}
