# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-mockito
pkgname=('python-mockito' 'python2-mockito')
pkgver=1.1.0
pkgrel=2
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools' 'python2-setuptools'
             'python2-funcsigs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kaste/mockito-python/archive/$pkgver.tar.gz")
sha512sums=('23988f4890a8e224f482d8b73a5dbe594931f14aa01337cac7f3a36d4d902d70a69fd21817a792aee53942043b8cdd409ce63fe9d46aa3c4761e1b8f28ca424c')

prepare() {
  cp -a mockito-python-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/mockito-python-$pkgver
  python setup.py build

  cd "$srcdir"/mockito-python-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mockito-python-$pkgver
  py.test build/lib

  cd "$srcdir"/mockito-python-$pkgver-py2
  py.test2 build/lib
}

package_python-mockito() {
  depends=('python')

  cd mockito-python-$pkgver
  python setup.py install --root="$pkgdir" -O1
}

package_python2-mockito() {
  depends=('python2-funcsigs')

  cd mockito-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
}
