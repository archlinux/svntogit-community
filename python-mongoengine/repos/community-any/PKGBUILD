# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-mongoengine
pkgver=0.19.0
pkgrel=1
pkgdesc="A Python Object-Document-Mapper for working with MongoDB"
url="https://github.com/MongoEngine/mongoengine"
license=('MIT')
arch=('any')
depends=('python-pymongo' 'python-six')
optdepends=('python-dateutil: for DateTimeField'
            'python-pillow: for ImageField / ImageGridFsProxy')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-blinker' 'python-pillow' 'python-mongomock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/MongoEngine/mongoengine/archive/v$pkgver.tar.gz")
sha512sums=('8b29f81b9bd83ac572980abe6b3621a42b9509f298f1717a5a044c37f6da6cb5487df3e64f2bd846e219ec4a5d083c4a981379c32272812798050f0503969bf4')

prepare() {
  sed -i 's/coverage==/coverage>=/' mongoengine-$pkgver/setup.py
  sed -e 's|mongodb://|mongomock://|g' \
      -e "s|connect(db='mongoenginetest')|connect(db='mongoenginetest', is_mock=True)|" \
      -i mongoengine-$pkgver/tests/*.py mongoengine-$pkgver/tests/*/*.py
}

build() {
  cd "$srcdir"/mongoengine-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/mongoengine-$pkgver
  python setup.py nosetests || warning "Tests failed"
}

package() {
  cd mongoengine-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
