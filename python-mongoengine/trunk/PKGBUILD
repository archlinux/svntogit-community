# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-mongoengine
pkgver=0.24.0
pkgrel=1
pkgdesc="A Python Object-Document-Mapper for working with MongoDB"
url="https://github.com/MongoEngine/mongoengine"
license=('MIT')
arch=('any')
depends=('python-pymongo')
optdepends=('python-dateutil: for DateTimeField'
            'python-pillow: for ImageField / ImageGridFsProxy')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest-cov' 'python-coverage' 'python-blinker' 'python-pillow' 'python-zipp'
              'python-mongomock')
source=("https://github.com/MongoEngine/mongoengine/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2644fa972868ff4e0326a2dff458b3be029e0eb8a617d8b1e92c88a53697e769b25117a1abec25988876a2c5911cd476d1024cda8b0cdacd31df6dc9e84a6fa4')

build() {
  cd mongoengine-$pkgver
  python -m build -nw
}

check() {
  cd mongoengine-$pkgver
  # No sane way to run it without mongodb
  # python -m pytest
}

package() {
  cd mongoengine-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
