# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mongomock
pkgname=(python-mongomock python2-mongomock)
pkgver=3.18.0
pkgrel=3
pkgdesc="Fake pymongo stub for testing simple MongoDB-dependent code"
url="https://github.com/mongomock/mongomock"
license=('BSD')
arch=('any')
makedepends=('python-pbr' 'python2-pbr' 'python-sentinels' 'python2-sentinels' 'python-six'
             'python2-six')
checkdepends=('python-nose' 'python2-nose' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mongomock/mongomock/archive/$pkgver.tar.gz")
sha512sums=('f4fb8acef8aa560e65adc40aec30ca42c4ab6349fb84117a82cd14b9c393d2105eebf527a0e26acad4f0a07e2c8de3896f9a94dfa36c73f4dc3e9534a0dbe449')

export PBR_VERSION=$pkgver

prepare() {
  cp -a mongomock-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/mongomock-$pkgver
  python setup.py build

  cd "$srcdir"/mongomock-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mongomock-$pkgver
  python setup.py nosetests

  cd "$srcdir"/mongomock-$pkgver-py2
  python2 setup.py nosetests
}

package_python-mongomock() {
  depends=('python-sentinels' 'python-six')

  cd mongomock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-mongomock() {
  depends=('python2-sentinels' 'python2-six')

  cd mongomock-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
