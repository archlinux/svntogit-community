# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
pkgver=2.0.7
pkgrel=1
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
# depends on python-setuptools as pkg_resources is used is several modules
# NOTE: Keep the order of dependencies matching setup.py
depends=(python-boto3 python-botocore python-cryptography python-requests python-xmltodict
         python-six python-werkzeug python-pytz python-dateutil python-responses
         # Below dependencies varied with Python versions
         python-jinja python-more-itertools python-setuptools python-zipp)
# See reqquirements-test.txt, excluding pytest-cov
checkdepends=(python-pytest python-sure python-freezegun
              # boto is vendored (#3503), but still imported in tests
              python-boto)
# Below are optional dependencies. The order matches _dep_* items in upstream `setup.py`.
# Note that _dep_python_jose_ecdsa_pin and _dep_idna are excluded as they are pins and
# not used by moto.
optdepends=(
  'python-yaml: for cloudformation, s3 and ssm'
  'python-jose: for apigateway and cognitoidp'
  'python-docker: for awslambda, batch, cloudformation, dynamodb2, dynamodbstreams, ec2, ses, sns, sqs and ssm'
  'python-jsondiff: for iotdata'
  'python-aws-xray-sdk: for xray'
  'python-cfn-lint: for cloudformation and ssm'
  'python-sshpubkeys: for ec2'
  'python-flask: for moto_server'
  'python-flask-cors: for moto_server'
)
checkdepends+=(python-yaml python-jose python-docker python-jsondiff python-aws-xray-sdk
               python-cfn-lint python-sshpubkeys python-flask python-flask-cors)
source=("https://files.pythonhosted.org/packages/source/m/moto/moto-${pkgver}.tar.gz"
        sdist-no-pyc.diff)
sha256sums=('4209ee3241df1160523a58a169d31d0c8aa10b7aa75894484de58cbfacf14523'
            '4193bd88380f6ab8dac49be37d940993dee4e31351ffd60c8167b7c8e5ec3208')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../sdist-no-pyc.diff

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#,?<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # XXX: there are too many tests that depend on docker/podman, and there is no simple way
  # other than listing one-by-one for exclusion. To make things worse, running docker/podman
  # in systemd-nspawn containers appears impossible without special configurations. I gave
  # it up and running podman _outside_ the container for testing.
  # $ systemctl --user start podman.socket
  # $ extra-x86_64-build -- -d "$XDG_RUNTIME_DIR/podman:/tmp/podman"
  PODMAN_SOCK=/tmp/podman/podman.sock
  if [ -S $PODMAN_SOCK ]; then
    DOCKER_HOST="unix://$PODMAN_SOCK" TZ=UTC pytest tests
  fi
}

package() {
  cd $_pkgname-$pkgver

  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
