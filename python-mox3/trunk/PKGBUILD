# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mox3
pkgname=('python-mox3' 'python2-mox3')
pkgver=0.15.0
pkgrel=1
arch=('any')
pkgdesc='Mock object framework for Python'
url='http://docs.openstack.org/developer/mox3'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-fixtures' 'python2-fixtures' 'git')
checkdepends=('python-testrepository' 'python2-testrepository')
source=("git+https://git.openstack.org/openstack/mox3#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a mox3{,-py2}
}

build() {
  cd "$srcdir"/mox3
  python setup.py build

  cd "$srcdir"/mox3-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mox3
  python setup.py testr

  cd "$srcdir"/mox3-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-mox3() {
  depends=('python-pbr' 'python-fixtures')

  cd mox3
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-mox3() {
  depends=('python2-pbr' 'python2-fixtures')

  cd mox3-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
