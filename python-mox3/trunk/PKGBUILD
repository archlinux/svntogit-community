# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mox3
pkgname=('python-mox3' 'python2-mox3')
pkgver=0.24.0
pkgrel=1
arch=('any')
pkgdesc='Mock object framework for Python'
url='http://docs.openstack.org/developer/mox3'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-fixtures' 'python2-fixtures')
checkdepends=('python-testrepository' 'python2-testrepository')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/mox3/archive/$pkgver.tar.gz")
sha512sums=('714ebbfa32c721cc677ca7947ffe2d8ea58606322d985264e342d994ebdcb2c301f92bf34df511e995b77ea6c547eb23826daf8af0b570260e91f84b5e68f8d0')

prepare() {
  # Python 3.6 fixes
  sed -i 's/, flags=4//' mox3-$pkgver/mox3/tests/test_mox.py

  cp -a mox3-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/mox3-$pkgver
  python setup.py build

  cd "$srcdir"/mox3-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mox3-$pkgver
  python setup.py testr

  cd "$srcdir"/mox3-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-mox3() {
  depends=('python-pbr' 'python-fixtures')

  cd mox3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-mox3() {
  depends=('python2-pbr' 'python2-fixtures')

  cd mox3-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
