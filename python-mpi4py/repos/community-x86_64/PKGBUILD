# $Id$
# Maintainer : St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgbase=python-mpi4py
pkgname=('python-mpi4py' 'python2-mpi4py')
pkgver=1.2.2
pkgrel=5
arch=('i686' 'x86_64')
url="http://mpi4py.scipy.org"
license=('BSD')
makedepends=('python' 'python2' 'python-distribute' 'python2-distribute' 'openmpi')
source=(http://mpi4py.googlecode.com/files/mpi4py-${pkgver}.tar.gz)
sha1sums=('e08d09257794a85d010a22ad9bda9d1c89a6258f')

build() {
    cd "${srcdir}"
    cp -a mpi4py-${pkgver}{,-python2}

    # Build python 3 module
    cd mpi4py-${pkgver}
    python setup.py build

    # Build python 2 module
    cd ../mpi4py-${pkgver}-python2
    python2 setup.py build
}

package_python-mpi4py() {
    pkgdesc="Python bindings of the Message Passing Interface (MPI)"
    depends=('python>=3.2' 'openmpi')

    cd "${srcdir}/mpi4py-${pkgver}"
    python setup.py install --root "${pkgdir}" --optimize=1 --skip-build 
}

package_python2-mpi4py() {
    pkgdesc="Python2 bindings of the Message Passing Interface (MPI)"
    depends=('python2>=2.7' 'openmpi')

    cd "${srcdir}/mpi4py-${pkgver}-python2"
    python2 setup.py install --root "${pkgdir}" --optimize=1 --skip-build 
}
