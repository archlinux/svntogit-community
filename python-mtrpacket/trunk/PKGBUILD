# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mtrpacket
pkgver=1.0.0
pkgrel=1
pkgdesc="Asynchronous network probes for Python"
url="https://github.com/matt-kimball/mtr-packet-python"
license=('MIT')
arch=('any')
depends=('mtr' 'python')
makedepends=('python-setuptools')
checkdepends=('gnu-netcat')
source=("https://github.com/matt-kimball/mtr-packet-python/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('15d201256f0ab372f71fefab32a6836194a180aa45d1b0460716c9bcd5ebc51aa241bcb982613f81ccda2384bef0e97decfebbe94b19f3f4a9e6a3deda62f89b')

build() {
  cd mtr-packet-python-$pkgver
  python setup.py build
}

check() {
  cd mtr-packet-python-$pkgver
  ./test.sh
}

package() {
  cd mtr-packet-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
