# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-munch
pkgname=(python-munch python2-munch)
pkgver=2.5.0
pkgrel=0
pkgdesc="A dot-accessible dictionary (a la JavaScript objects)"
url="https://github.com/Infinidat/munch"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-yaml' 'python2-yaml')
source=(https://pypi.io/packages/source/m/munch/munch-${pkgver}.tar.gz)
sha512sums=('7eca31111f40782a19b49535322bc56d723022d52b0842c4beff512f32fa415a6c75851eb84950358cb6a11693095cafe56364028e48385b11135de414edca21')

prepare() {
  cp -a munch-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/munch-$pkgver
  export PYTHONPATH=$PWD
  python setup.py build

  cd "$srcdir"/munch-$pkgver-py2
  export PYTHONPATH=$PWD
  python2 setup.py build
}

check() {
  cd "$srcdir"/munch-$pkgver
  pytest

  cd "$srcdir"/munch-$pkgver-py2
  pytest2
}

package_python-munch() {
  depends=('python-six')

  cd munch-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-munch() {
  depends=('python2-six')

  cd munch-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
