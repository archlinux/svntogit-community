# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgbase=python-musicbrainzngs
pkgname=('python-musicbrainzngs' 'python2-musicbrainzngs')
pkgver=0.6
pkgrel=3
pkgdesc="Python bindings for Musicbrainz' NGS webservice"
arch=('any')
url="https://github.com/alastair/python-musicbrainzngs"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-musicbrainzngs-${pkgver}.tar.gz::https://github.com/alastair/python-musicbrainzngs/archive/v${pkgver}.tar.gz")
sha256sums=('ec447bcab906fe7c4dbd714a1dff1b00adcd20d0011968df1a740e6b1fb09cb5')

prepare() {
  cp -r python{,2}-musicbrainzngs-${pkgver}
}
build() {
  for py in python{,2}; do
    pushd ${py}-musicbrainzngs-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-musicbrainzngs() {
  depends=('python')

  cd python-musicbrainzngs-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'

  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/python-musicbrainzngs/
}

package_python2-musicbrainzngs() {
  depends=('python2')

  cd python2-musicbrainzngs-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/python2-musicbrainzngs/
}

# vim: ts=2 sw=2 et:
