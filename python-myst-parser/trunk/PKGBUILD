# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=MyST-Parser
pkgname=python-myst-parser
pkgver=0.13.1
pkgrel=1
pkgdesc='An extended commonmark compliant parser, with bridges to docutils/sphinx'
arch=('any')
url='https://github.com/executablebooks/MyST-Parser'
license=('MIT')
depends=('python-markdown-it-py' 'python-mdit_py_plugins' 'python-yaml' 'python-docutils' 'python-sphinx' 'python-jinja')
optdepends=('python-linkify-it-py: linkify support')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-regressions' 'python-beautifulsoup4' 'python-linkify-it-py')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('4692aaa629bb9b663ff868c72fab3a1b3228cb1ff2136a27ca234b965209ca716386019e5a5d4158453015b2a5e8707ea39044406e124d001e0b1615380fa182')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
