# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=MyST-Parser
pkgname=python-myst-parser
pkgver=0.13.6
pkgrel=1
pkgdesc='An extended commonmark compliant parser, with bridges to docutils/sphinx'
arch=('any')
url='https://github.com/executablebooks/MyST-Parser'
license=('MIT')
depends=('python-markdown-it-py' 'python-mdit_py_plugins' 'python-yaml' 'python-docutils' 'python-sphinx' 'python-jinja')
optdepends=('python-linkify-it-py: linkify support')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-regressions' 'python-beautifulsoup4' 'python-linkify-it-py')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "fix-tests.patch::$url/pull/320.patch")
sha512sums=('c0f080e328c7c91fe110bc900dbbba560a61e1a8f6777d680d0a4ebd55c7d919cbf323a875967b614714ced45e1b45877bc01fbcb2dbd305006c6bd67530a138'
            'f2af70ed802ccdc6605aed98d7acaf3ebba50dbc870daa84a56654ecf77775767fd778f09d40af6e067610b32906de5f19243decf177dc79324b6746c43ea0c1')

prepare() {
  cd $_pkgname-$pkgver

  patch -p1 -i ../fix-tests.patch
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest -k 'not test_sphinx_builds'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
