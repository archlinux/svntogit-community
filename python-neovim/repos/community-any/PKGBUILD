# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgbase=python-neovim
pkgname=('python-neovim' 'python2-neovim')
_realname=python-client
pkgver=0.1.13
pkgrel=1
pkgdesc='Python client for Neovim'
url='https://github.com/neovim/python-client'
arch=('any')
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/neovim/python-client/archive/${pkgver}.tar.gz")
sha256sums=('b2f78226d0b3c57d1e53e26828f5fa5665c0655725fbadd43aea87f5ce93ef3b')

prepare() {
  cp -a ${_realname}-${pkgver}{,-python2}
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_realname}-${pkgver}-python2"
  python2 setup.py build
}

package_python-neovim() {
  pkgdesc='Python 3 client for neovim'
  depends=('neovim' 'python' 'python-msgpack' 'python-greenlet' 'python-click')

  cd "${srcdir}/${_realname}-${pkgver}"
  python setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
}

package_python2-neovim() {
  pkgdesc='Python 2 client for neovim'
  depends=('neovim' 'python2' 'python2-msgpack' 'python2-greenlet' 'python2-click' 'python2-trollius')

  cd "${srcdir}/${_realname}-${pkgver}-python2"
  python2 setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
}

# vim:set sw=2 sts=2 et:
