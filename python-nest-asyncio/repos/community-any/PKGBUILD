# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-nest-asyncio
pkgver=1.5.4
pkgrel=1
pkgdesc="Patch asyncio to allow nested event loops"
url="https://github.com/erdewit/nest_asyncio"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/erdewit/nest_asyncio/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('96050b450ce2dc1d9cb9f966fe1e971ce48652524f4d2d75bf2cb37755feb29d28b9661bd82fd1016781d6c79c027de04c516f5faccebf1040e398ea9998c87e')

build() {
  cd nest_asyncio-$pkgver
  python setup.py build
}

check() {
  cd nest_asyncio-$pkgver
  PYTHONPATH="$PWD/build/lib" python tests/nest_test.py
}

package() {
  cd nest_asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
