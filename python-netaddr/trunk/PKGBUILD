# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastien LEDUC <sebastien@sleduc.fr>
# Contributor: Gilles CHAUVIN <gcnweb at gmail dot com>

pkgbase=python-netaddr
pkgname=('python-netaddr' 'python2-netaddr')
_pyname=netaddr
pkgver=0.7.19
pkgrel=2
pkgdesc='Pure Python network address representation and manipulation library'
url='https://github.com/drkjam/netaddr'
arch=('any')
license=('BSD')
makedepends=('python-sphinx' 'python2-sphinx')
checkdepends=('python-pytest' 'python2-pytest')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/drkjam/${_pyname}/archive/${_pyname}-${pkgver}.tar.gz
        fix-${pkgver}-python3.patch::https://github.com/drkjam/netaddr/commit/2ab73f10be7069c9412e853d2d0caf29bd624012.patch
        pep479-return-instead-of-raise-StopIteration.patch)
sha512sums=('f07fb4de08aef4925dbc429b4ee93b25855273f083405d4e763c2fbc996ee35b09b87b10117040b073d45ac9f51512083116883ff48216e68aa1e429f83d5278'
            '8db4e6cbaf03b95092561dfcf881616920da2ab45f205d8bd29f1f6e9e7681a7d591c0a52dbdb772a41cac0b42d643c2dafcad22a659524a35cb6b06dd98d38d'
            'ff4b46184cc98c360ba377a72ead48a64fd78477080c7061652f79dc44d7b6803111cee885502b7d98e08d66d440e7b9afd65faa97674b2c487a2ba78e684e9d')

prepare() {
  cd ${_pyname}-${_pyname}-${pkgver}
  sed -r 's|#!/usr/bin/env python||g' -i netaddr/eui/ieee.py netaddr/ip/iana.py
  patch -p1 < "${srcdir}/fix-${pkgver}-python3.patch"
  patch -p1 < "${srcdir}/pep479-return-instead-of-raise-StopIteration.patch"
}

build() {
  cd ${_pyname}-${_pyname}-${pkgver}/docs
  make man text
}

check() {
  cd ${_pyname}-${_pyname}-${pkgver}
  LC_ALL='en_US.UTF-8' py.test
  LC_ALL='en_US.UTF-8' py.test2
}

package_python-netaddr() {
  depends=('python')
  optdepends=('ipython: netaddr interractive command support')

  cd ${_pyname}-${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 docs/build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/build/man/netaddr.1 "${pkgdir}/usr/share/man/man1/netaddr.1"
}

package_python2-netaddr() {
  depends=('python2')
  optdepends=('ipython2: netaddr interractive command supprt')

  cd ${_pyname}-${_pyname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  mv "${pkgdir}"/usr/bin/netaddr{,2}
  sed -r 's|(env python)|\12|g' -i "${pkgdir}/usr/bin/netaddr2"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 docs/build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/build/man/netaddr.1 "${pkgdir}/usr/share/man/man1/netaddr2.1"
}

# vim: ts=2 sw=2 et:
