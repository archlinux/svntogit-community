# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
# Contributor: richli <rich at dranek dot com>
# Contributor: rememberthemer <rememberthemer@_GMAIL_DOT_COM_>

_pkg=netCDF4
pkgname=python-${_pkg,,}
pkgver=1.5.4
pkgrel=3
pkgdesc="Python/NumPy interface to the netCDF C library"
arch=(x86_64)
url="https://unidata.github.io/netcdf4-python"
license=(MIT)
depends=(python-numpy python-cftime netcdf)
makedepends=(cython python-setuptools)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('941de6f3623b6474ecb4d043be5990690f7af4cf0d593b31be912627fe5aad03')

build() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py build
}

check() {
    cd ${_pkg}-${pkgver}/test
    PYTHONPATH="../build/lib.linux-${CARCH}-3.9" python -B ./run_all.py
}

package() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
