# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gergely Imreh <imrehg(at)gmail(dot)com>
# Contributor: Ghost of Kendo <ghostofkendo at gmail dot com>

pkgbase=python-netifaces
pkgname=(python-netifaces python2-netifaces)
pkgver=0.10.5
pkgrel=2
pkgdesc="Portable module to access network interface information in Python"
arch=('i686' 'x86_64')
url="http://alastairs-place.net/netifaces/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/n/netifaces/netifaces-${pkgver}.tar.gz")
md5sums=('5b4d1f1310ed279e6df27ef3a9b71519')

prepare() {
  cp -a "${srcdir}/netifaces-${pkgver}"{,-py2}
}

package_python-netifaces() {
  depends=('python')

  cd "${srcdir}/netifaces-${pkgver}"
  python setup.py install --root "${pkgdir}"

  # Install license, that is inside the readme file
  install -Dm644 README.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-netifaces() {
  depends=('python2')

  cd "${srcdir}/netifaces-${pkgver}-py2"
  python2 setup.py install --root "${pkgdir}"

  # Install license, that is inside the readme file
  install -Dm644 README.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
