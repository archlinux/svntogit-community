# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

pkgname=python-networkx
pkgver=2.8.5
pkgrel=1
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.github.io"
depends=('python-numpy' 'python-scipy' 'python-matplotlib' 'python-pandas')
optdepends=('python-lxml: for GraphML XML format'
            'python-pydot: for graph drawing and graph layout algorithms via GraphViz'
            'python-yaml: for YAML format reading and writing')
# python-pygraphviz: for graph drawing and graph layout algorithms via GraphViz
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-lxml' 'python-pydot' 'python-yaml')
source=("https://github.com/networkx/networkx/archive/networkx-$pkgver.tar.gz")
sha512sums=('bb96fb9a92e6cedc5d51fc3313968aa014e86014fc654f4b9f57e39fe815e669b1686fd559545c0f1001ad0656e62af6624bb91bbefad335351f1310109aa7f6')

build() {
  cd networkx-networkx-$pkgver
  python setup.py build
}

check() {
  cd networkx-networkx-$pkgver
  python setup.py pytest
}

package() {
  cd networkx-networkx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv "$pkgdir"/usr/share/doc/networkx-$pkgver "$pkgdir"/usr/share/doc/$pkgname
}
