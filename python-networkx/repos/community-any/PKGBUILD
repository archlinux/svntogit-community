# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

pkgname=python-networkx
pkgver=2.8.6
pkgrel=1
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.org/"
depends=('python-numpy' 'python-scipy' 'python-matplotlib' 'python-pandas')
optdepends=('python-lxml: for GraphML XML format'
            'python-pydot: for graph drawing and graph layout algorithms via GraphViz'
            'python-yaml: for YAML format reading and writing')
# python-pygraphviz: for graph drawing and graph layout algorithms via GraphViz
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-lxml' 'python-pydot' 'python-yaml')
source=("https://github.com/networkx/networkx/archive/networkx-$pkgver.tar.gz")
sha512sums=('527e8335004dc719fbff30da8f73916af6c5ddc265187ef663186c5ee4c99032c0f904357c588b3e33b060cc095d7acb44498d3597bbbfb3887dd01dfe9c716f')

build() {
  cd networkx-networkx-$pkgver
  python setup.py build
}

check() {
  cd networkx-networkx-$pkgver
  python setup.py pytest
}

package() {
  cd networkx-networkx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv "$pkgdir"/usr/share/doc/networkx-$pkgver "$pkgdir"/usr/share/doc/$pkgname
}
