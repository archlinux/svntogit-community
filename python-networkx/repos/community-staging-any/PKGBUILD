# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

pkgbase=python-networkx
pkgname=(python-networkx python2-networkx)
_pypiname=networkx
pkgver=2.1
pkgrel=3
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.github.io"
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator')
checkdepends=('python-nose' 'python2-nose')
source=("https://github.com/networkx/networkx/archive/${_pypiname}-${pkgver}.tar.gz"
        "0001-Fix-StopIteration-handling-which-breaks-in-python-3..patch")
sha512sums=('1db1d4e6e6add25509b79b24bcdc30e934b3be714898dcad8092db56908fe40eec47952336b9cbb3662cf5026c821c50db7a2d8578a4dda93585ee0aaf8210ec'
            'acd4f982f0773a7eaf0c8034a512b28d367fc9aee5a2d480461451f2d76ff88af2016b2d87b5f70c46af75b9f0d2306e73ab0ffbc5584003153cfbcdc3c72380')

prepare() {
  # backport fix for python3.7 testsuite: https://github.com/networkx/networkx/issues/3046
  patch -p1 -d ${_pypiname}-${_pypiname}-$pkgver < 0001-Fix-StopIteration-handling-which-breaks-in-python-3..patch
  cp -a ${_pypiname}-${_pypiname}-$pkgver{,-py2}
}

build() {
  cd "$srcdir/${_pypiname}-${_pypiname}-$pkgver"
  python3 setup.py build

  cd "$srcdir/${_pypiname}-${_pypiname}-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/${_pypiname}-${_pypiname}-$pkgver"
  python3 setup.py nosetests #|| true  # Tests fail randomly

  cd "$srcdir/${_pypiname}-${_pypiname}-$pkgver-py2"
  python2 setup.py nosetests
}

package_python-networkx() {
  depends=('python-decorator')
  optdepends=('python-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
              'python-scipy: Provides flexible drawing of graphs.'
              'python-pyparsing: Required for pydot, GML file reading.'
              'python-yaml: Required for YAML format reading and writing.')

  cd "$srcdir/${_pypiname}-${_pypiname}-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  mv "$pkgdir/usr/share/doc/${_pypiname}-$pkgver" "$pkgdir/usr/share/doc/$pkgname"
}

package_python2-networkx() {
  depends=('python2-decorator')
  optdepends=('python2-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
              'python2-scipy: Provides flexible drawing of graphs.'
              'python2-pydot: Provides graph drawing and graph layout algorithms.'
              'python2-pyparsing: Required for pydot, GML file reading.'
              'python2-yaml: Required for YAML format reading and writing.')

  cd "$srcdir/${_pypiname}-${_pypiname}-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  mv "$pkgdir/usr/share/doc/${_pypiname}-$pkgver" "$pkgdir/usr/share/doc/$pkgname"
}
