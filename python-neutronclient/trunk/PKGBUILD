# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-neutronclient
pkgver=7.2.0
pkgrel=1
pkgdesc="OpenStack Networking (Neutron) Client"
arch=('any')
url="https://docs.openstack.org/python-neutronclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-cliff' 'python-debtcollector' 'python-iso8601' 'python-netaddr'
         'python-osc-lib' 'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization'
         'python-oslo-utils' 'python-os-client-config' 'python-keystoneauth1'
         'python-keystoneclient' 'python-requests' 'python-simplejson')
checkdepends=('python-oslotest' 'python-osprofiler' 'python-requests-mock')
source=("https://github.com/openstack/python-neutronclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ba7776421c1102ff057fcf2017eb76cb3a15fb8946b509281d893906aa4685da164bf846a5ebe7a25d1546b3fc4ab6ca10a2c02cd1d39f4595aee8b611360fe6')

export PBR_VERSION=$pkgver

build() {
  cd python-neutronclient-$pkgver
  python setup.py build
}

check() {
  cd python-neutronclient-$pkgver
  # https://salsa.debian.org/openstack-team/clients/python-neutronclient/-/blob/debian/ussuri/debian/rules
  stestr run 'neutronclient\.tests\.unit\.(?!(.*osc\.v2\.fwaas\.test_firewallrule\.TestShowFirewallRule\.test_show_filtered_by_id_or_name.*|.*osc\.v2\.fwaas\.test_firewallrule\.TestListFirewallRule\.test_list_with_long_option.*|.*osc\.v2\.fwaas\.test_firewallrule\.TestCreateFirewallRule\.test_create_with_no_options.*|.*osc\.v2\.fwaas\.test_firewallrule\.TestCreateFirewallRule\.test_create_with_all_params_protocol_any.*))'
}

package() {
  cd python-neutronclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
