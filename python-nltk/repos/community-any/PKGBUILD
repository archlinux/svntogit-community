# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Chris Baker <baker.chris.3@gmail.com>

pkgbase=python-nltk
pkgname=('python2-nltk' 'python-nltk')
pkgver=3.2.1
pkgrel=2
pkgdesc='Natural language processing in Python'
arch=('any')
url='http://www.nltk.org/'
license=('APACHE')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/nltk/nltk#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -r nltk nltk-py2
  # python 2 fix
  cd nltk-py2
  find . -type f -exec sed -i 's:^#.*env python:#!/usr/bin/env python2:' {} +
}

build() {
  (cd nltk; python setup.py build)
  (cd nltk-py2; python2 setup.py build)
}

package_python-nltk() {
  optdepends=('nltk-data: test data'
              'python-numpy: used for calculations'
              'python-matplotlib: used for plotting')
  depends=('python-yaml')

  (cd nltk; python setup.py install --root="$pkgdir" --optimize=1)
}

package_python2-nltk() {
  optdepends=('nltk-data: test data'
              'python2-numpy: used for calculations'
              'python2-matplotlib: used for plotting')
  depends=('python2-yaml')

  (cd nltk-py2; python2 setup.py install --root="$pkgdir" --optimize=1)
}

# getver: pypi.python.org/pypi/nltk
# vim:set ts=2 sw=2 et:
