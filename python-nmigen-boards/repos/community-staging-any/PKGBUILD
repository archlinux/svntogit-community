# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nmigen-boards
pkgname=python-$_pkgname
_commit=d9264bf03b1a7ddb71017a2885383c4cec777f53
pkgver=r133.d9264bf
pkgrel=2
pkgdesc='Board and connector definition files for nMigen'
arch=('any')
url='https://github.com/nmigen/nmigen-boards'
license=('BSD')
depends=('python-nmigen')
makedepends=('git' 'python-setuptools-scm')
source=("git+$url#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname

  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd $_pkgname

  python setup.py build
}

package() {
  cd $_pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
