# Maintainer: David Runge <dave@sleepmap.de>
_name=nose-progressive
pkgname=python-nose-progressive
pkgver=1.5.2
pkgrel=1
pkgdesc="Give your tests a progress bar and smarter tracebacks in 3 lines"
arch=('any')
url="https://github.com/erikrose/nose-progressive"
license=('MIT')
depends=('python-blessings' 'python-nose' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('59abe0e5f060149046aa4e9b8ca622af1eb65db3b69631b5c6230c31ee9641f9f7b9bba7ed50709f5243c04a9bbf36fd5901601e3d93e9b68867fc37357b649e')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build/lib:${PYTHONPATH}
  nosetests noseprogressive || warning "Known failing test: https://github.com/erikrose/nose-progressive/issues/90"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
