# Maintainer: David Runge <dvzrv@archlinux.org>

_name=nose2
pkgname=python-nose2
pkgver=0.10.0
pkgrel=2
pkgdesc="The next generation of nicer testing for python"
arch=('any')
url="https://github.com/nose-devs/nose2"
license=('BSD')
depends=('python-coverage' 'python-six')
makedepends=('python-setuptools')
optdepends=('python-coverage: for coverage_plugin'
            'python-setuptools: for eggdiscovery plugin'
            'python-sphinx: for doc'
            'python-sphinx_rtd_theme: for doc'
            'python-mock: for doc')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('05bc26f23e2f2bd950ee7f5ff7720d3282eb27f6c147ace911bc6f1dbaa65eb076d84b3411dcbcad979a7f86e0cf97d6926de68e5ed0b0746cab58cf5773125e')
b2sums=('1dc7c461cac4b540e99a691ba6f972b81182872d74160d158bc7dd5bb789ff8c363bdc3841e5fd89e09bcf6728e09c69635054358155413ec7c07bbc0b814742')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  # license
  install -vDm 644 license.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
