# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

_name=nose2
pkgbase=python-nose2
pkgname=('python-nose2' 'python2-nose2')
pkgver=0.7.4
pkgrel=3
pkgdesc="The next generation of nicer testing for python"
arch=('any')
url="https://github.com/nose-devs/nose2"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname[0]}-${pkgver}.tar.gz::https://github.com/nose-devs/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('693ae9c9fff5b3add269579a6c29863b616a5d2f33044d655407c9e50cf2df4e8eed3be2c0f47cf71f222fabe3d52da7bd40492880c64107557b30a928606e4f')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

package_python-nose2() {
  depends=('python-coverage' 'python-setuptools' 'python-six')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 license.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README.rst}
}

package_python2-nose2() {
  depends=('python2-coverage' 'python2-mock' 'python2-setuptools' 'python2-six')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 license.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README.rst}
  # move duplicates
  mv -v "${pkgdir}/usr/bin/nose2" "${pkgdir}/usr/bin/nose22"
}

# vim:set ts=2 sw=2 et:
