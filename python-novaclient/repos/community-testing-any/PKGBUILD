# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-novaclient
pkgname=(python-novaclient python2-novaclient)
pkgver=7.0.0
pkgrel=2
pkgdesc="Client library for OpenStack Compute API"
arch=('any')
url="http://docs.openstack.org/developer/python-novaclient"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel' 'python-prettytable' 'python2-prettytable'
             'python-keystoneauth1' 'python2-keystoneauth1' 'python-requests'
             'python2-requests' 'python-iso8601' 'python2-iso8601' 'python-oslo-utils'
             'python2-oslo-utils' 'python-oslo-i18n' 'python2-oslo-i18n'
             'python-oslo-serialization' 'python2-oslo-serialization')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock'
              'python2-requests-mock')
source=("git+https://git.openstack.org/openstack/python-novaclient#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i '/simplejson/d' python-novaclient/requirements.txt
  cp -a python-novaclient{,-py2}
}

build() {
  cd "$srcdir"/python-novaclient
  python setup.py build

  cd "$srcdir"/python-novaclient-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-novaclient
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/python-novaclient-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-novaclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
           'python-requests' 'python-iso8601' 'python-oslo-utils' 'python-oslo-i18n'
           'python-oslo-serialization')

  cd "$srcdir"/python-novaclient
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-novaclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-prettytable' 'python2-keystoneauth1'
           'python2-requests' 'python2-iso8601' 'python2-oslo-utils' 'python2-oslo-i18n'
           'python2-oslo-serialization')

  cd "$srcdir"/python-novaclient-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mv "$pkgdir"/usr/bin/nova{,2}
}

# vim:set ts=2 sw=2 et:
