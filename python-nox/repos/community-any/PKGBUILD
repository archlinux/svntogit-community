# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nox
pkgname=python-$_pkgname
pkgver=2022.1.7
pkgrel=1
pkgdesc='Flexible test automation for Python'
arch=('any')
url='https://github.com/theacodes/nox'
license=('Apache')
depends=('python' 'python-argcomplete' 'python-colorlog' 'python-py' 'python-virtualenv' 'python-packaging')
optdepends=('python-jinja: tox_to_nox'
            'python-tox: tox_to_nox')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-jinja' 'python-tox')
source=("$_pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f71ab36b76a655fff9f2bc41fbb7b469f00de3891491882c71da2c170c2f457830c37e43cc57c14db6618034f926d99c8e346efe613ad30202b3d0386ec735a9')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  PYTHONPATH="build/lib" pytest
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
