# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-numpydoc
pkgver=1.1.0
pkgrel=1
pkgdesc="Numpy's Sphinx extensions"
url="https://github.com/numpy/numpydoc"
license=('Apache')
arch=('any')
depends=('python-sphinx' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-matplotlib' 'python-numpy' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/numpy/numpydoc/archive/v$pkgver.tar.gz")
sha512sums=('596757521b627fe415978234d87d4f0b603ff2b488b5ecf590d00a1e1682059f02190a84d188fe122f821f54ea8ab6117bd9410f449e78d52580a0c4ff7b9e54')

build() {
  cd numpydoc-$pkgver
  python setup.py build
}

check() {
  cd numpydoc-$pkgver
  python -m pytest
}

package() {
  cd numpydoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
