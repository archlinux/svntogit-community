# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Atlanis <emallson@archlinux.us>

pkgbase=python-oauth2client
pkgname=('python2-oauth2client' 'python-oauth2client')
pkgver=4.1.2
pkgrel=2
pkgdesc="A client library for OAuth 2.0"
arch=('any')
url="https://github.com/google/oauth2client"
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools' 'python-httplib2'
             'python-pyasn1' 'python-pyasn1-modules' 'python-rsa'
             'python-six' 'python2-httplib2' 'python2-pyasn1'
             'python2-pyasn1-modules' 'python2-rsa' 'python2-six')
source=("https://github.com/google/oauth2client/archive/v${pkgver}.tar.gz")
md5sums=('0b3a7e3949b634e7e50ad9b6e50e93ae')

prepare() {
  cd "$srcdir"

  cp -a oauth2client-${pkgver} oauth2client-py2-${pkgver}
  cd oauth2client-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/oauth2client-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/oauth2client-${pkgver}
  python setup.py build
}

package_python2-oauth2client() {
  depends=('python2-httplib2' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-rsa' 'python2-six')
  optdepends=('python2-gflags: for oauth2client.tools.run function')

  cd "$srcdir"/oauth2client-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python-oauth2client() {
  depends=('python-httplib2' 'python-pyasn1' 'python-pyasn1-modules' 'python-rsa' 'python-six')
  optdepends=('python-gflags: for oauth2client.tools.run function')

  cd "$srcdir"/oauth2client-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}

