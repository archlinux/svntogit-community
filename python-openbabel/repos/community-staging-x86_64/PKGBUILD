# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Michal Bozon <bozonm@vscht.cz>

pkgname=('python-openbabel' 'python2-openbabel')
pkgver=2.3.2
pkgrel=5
pkgdesc='Python bindings for the openbabel library'
arch=('x86_64' 'i686')
url='http://openbabel.org/wiki/Python'
license=('GPL')
depends=('gcc-libs' "openbabel=${pkgver}" 'eigen2')
makedepends=('swig' 'cmake' 'python-setuptools' 'python2-setuptools')
source=("http://downloads.sourceforge.net/openbabel/openbabel-$pkgver.tar.gz"
        openbabel-gcc5.patch
        openbabel-swig3.patch)
sha256sums=('4eaca26679aa6cc85ebf96af19191472ac63ca442c36b0427b369c3a25705188'
            '0b022f4adeb16c8531e13871f6c6d256c8939defb5be0fe32d0a28711f2d933e'
            '6c8fb33199d4b4fa03128278110275502266b915ca9c43ef0dc87f24a0cadf06')

prepare() {
  patch -d openbabel-$pkgver -Np1 < openbabel-gcc5.patch
  patch -d openbabel-$pkgver -Np1 < openbabel-swig3.patch

  cp -r openbabel-$pkgver python2-openbabel-$pkgver
}

build() {
  cd "$srcdir/openbabel-$pkgver"

  # Create babelconfig.h
  cmake .

  # Regenerate interface with the patched swig 2.0.2
  cd scripts
  swig -O -I../include/ -python -py3 -c++  openbabel-python.i
  mv openbabel-python_wrap.cxx python/openbabel-python.cpp
  mv openbabel.py python/

  cd python
  sed -i '19i os.path.join("/usr/include/openbabel-2.0"),' setup.py
  sed -i '20i os.path.join("/usr/include/eigen2"),' setup.py

  cd $srcdir/python2-openbabel-$pkgver/scripts/python
  sed -i '19i os.path.join("/usr/include/openbabel-2.0"),' setup.py
  sed -i '20i os.path.join("/usr/include/eigen2"),' setup.py
}

package_python-openbabel() {
  depends+=('python')

  cd "$srcdir/openbabel-$pkgver/scripts/python"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python2-openbabel() {
  depends+=('python2')

  cd "$srcdir/python2-openbabel-$pkgver/scripts/python"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
