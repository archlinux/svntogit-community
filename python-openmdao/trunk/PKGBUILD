# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openmdao
pkgver=3.2.1
pkgrel=1
pkgdesc="An open-source high-performance computing platform"
url="https://github.com/OpenMDAO/OpenMDAO"
license=('Apache')
arch=('any')
depends=('python-six' 'python-numpydoc' 'python-scipy' 'python-sqlitedict' 'python-pycodestyle'
         'python-pydocstyle' 'python-testflo' 'python-parameterized' 'python-pyparsing'
         'python-networkx' 'python-sphinx' 'python-redbaron' 'python-mock' 'python-requests-mock'
         'python-tornado' 'python-pydoe2')
checkdepends=('python-mpi4py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenMDAO/OpenMDAO/archive/$pkgver.tar.gz")
sha512sums=('4a4eb87a07a26156cac0a73976c7107404f2b7e282305f173d062a04388b428cfc680b4bb0b9b4d980c482e73c7fca8d37ce6a548248db009dc087179c2ca38c')

prepare() {
  cd OpenMDAO-$pkgver
  sed -e 's/from distutils.core/from setuptools/' \
      -e 's/==/>=/' \
      -i setup.py
}

build() {
  cd OpenMDAO-$pkgver
  python setup.py build
}

check() {
  cd OpenMDAO-$pkgver
  PYTHONPATH="$PWD/build/lib" testflo -n 16 --timeout=120 openmdao || warning "Tests failed"
}

package() {
  cd OpenMDAO-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
