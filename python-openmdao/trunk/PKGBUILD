# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openmdao
pkgver=3.12.0
pkgrel=1
pkgdesc="An open-source high-performance computing platform"
url="https://github.com/OpenMDAO/OpenMDAO"
license=('Apache')
arch=('any')
depends=('python-networkx' 'python-numpy' 'python-pydoe2' 'python-pyparsing' 'python-scipy'
         'python-requests')
optdepends=('python-matplotlib: for docs'
            'python-numpydoc: for docs'
            'python-redbaron: for docs'
            'python-sphinx: for docs'
            'python-tabulate: for docs and notebooks'
            'ipython: for docs and notebooks'
            'python-notebook: for notebooks'
            'python-bokeh: for visualization'
            'python-colorama: for visualization')
checkdepends=('python-mpi4py' 'python-parameterized' 'python-numpydoc' 'python-pycodestyle'
              'python-pydocstyle' 'python-testflo' 'python-websockets' 'python-pyppeteer'
              'python-aiounittest')
source=("https://github.com/OpenMDAO/OpenMDAO/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a3c3e0f8a35bb4cbdfe30d7b7013ee95fd605516af6b61c9eb52184cb15ea57dd8f8bb0be1f8083ddceae05c5697cc70977f60361bbeb5716d5020bed3483696')

prepare() {
  cd OpenMDAO-$pkgver
  sed -e 's/==/>=/' -i setup.py
}

build() {
  cd OpenMDAO-$pkgver
  python setup.py build
}

check() {
  cd OpenMDAO-$pkgver
  PYTHONPATH="$PWD/build/lib" testflo -n 16 --timeout=120 openmdao || echo "Tests failed"
}

package() {
  cd OpenMDAO-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
