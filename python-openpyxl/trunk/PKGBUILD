# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Nishit Joseph (reachjlight at gmail dot com)
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgbase=python-openpyxl
pkgname=('python2-openpyxl' 'python-openpyxl')
pkgver=2.2.2
pkgrel=1
pkgdesc="A Python library to read/write Excel 2007 xlsx/xlsm files"
arch=('any')
url="http://openpyxl.readthedocs.org/"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python-jdcal' 'python2-jdcal' 'python-lxml' 'python2-lxml')
source=("https://pypi.python.org/packages/source/o/openpyxl/openpyxl-${pkgver}.tar.gz"
        "LICENCE")
md5sums=('94668360e4a9726b4d0a3c5884df802e'
         'c8afe530744e932b892358e6eb5bea9b')

prepare() {
  cd "$srcdir"
  cp -a openpyxl-${pkgver} openpyxl-py2-${pkgver}
  cd openpyxl-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/openpyxl-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/openpyxl-${pkgver}
  python setup.py build
}

package_python2-openpyxl() {
  depends=('python2' 'python2-jdcal' 'python2-lxml')
  optdepends=('python2-pillow: needed to include images')

  cd "$srcdir"/openpyxl-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

package_python-openpyxl() {
  depends=('python' 'python-jdcal' 'python-lxml')
  optdepends=('python-pillow: needed to include images')

  cd "$srcdir"/openpyxl-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

