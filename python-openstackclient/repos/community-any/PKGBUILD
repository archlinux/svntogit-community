# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=5.7.0
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneclient'
         'python-keystoneauth1' 'python-osc-lib' 'python-oslo-utils' 'python-oslo-i18n'
         'python-openstacksdk' 'python-glanceclient' 'python-novaclient' 'python-cinderclient')
checkdepends=('python-ddt' 'python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-openstackclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('79436b302de9eea6800666830dc2c170b257c54e0e1c20b2d86ce607083f379dbaa1932af06b4c544040fe0cf22aa20414b5bfbf44a2dfa0e433c1be042e8aec')

export PBR_VERSION=$pkgver

build() {
  cd python-openstackclient-$pkgver
  python setup.py build
}

check() {
  cd python-openstackclient-$pkgver
  stestr run || echo "Tests failed: https://storyboard.openstack.org/#!/story/2003143"
}

package() {
  cd python-openstackclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
