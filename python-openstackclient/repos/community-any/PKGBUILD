# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=5.3.0
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneclient'
         'python-keystoneauth1' 'python-osc-lib' 'python-oslo-utils' 'python-oslo-i18n'
         'python-openstacksdk' 'python-glanceclient' 'python-novaclient' 'python-cinderclient')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-openstackclient/archive/$pkgver.tar.gz")
sha512sums=('b13f0e3186ffb6e3e234785b65c79e7d5a8e822d0d1c1d8421cdd33671a7ce3320f77b8f2d2cefe6e4a8eec132dd48d9e3690ae798bb90dc09be9706e1f881bc')

export PBR_VERSION=$pkgver

build() {
  cd python-openstackclient-$pkgver
  python setup.py build
}

check() {
  cd python-openstackclient-$pkgver
  stestr run || warning "Tests failed: https://storyboard.openstack.org/#!/story/2003143"
}

package() {
  cd python-openstackclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
