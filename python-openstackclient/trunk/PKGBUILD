# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-openstackclient
pkgname=(python-openstackclient python2-openstackclient)
pkgver=2.3.0
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="http://docs.openstack.org/developer/python-openstackclient"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel' 'python-cliff' 'python2-cliff'
             'python-keystoneclient' 'python2-keystoneclient' 'python-requests'
             'python2-requests' 'python-keystoneauth1' 'python2-keystoneauth1' 'python-oslo-utils'
             'python2-oslo-utils' 'python-oslo-i18n' 'python2-oslo-i18n' 'python-openstacksdk'
             'python2-openstacksdk' 'python-os-client-config' 'python2-os-client-config'
             'python-oslo-config' 'python2-oslo-config' 'python-glanceclient'
             'python2-glanceclient' 'python-novaclient' 'python2-novaclient'
             'python-cinderclient' 'python2-cinderclient' 'python-stevedore' 'python2-stevedore'
             'python-simplejson' 'python2-simplejson')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock' 'python2-requests-mock')
source=("git+https://git.openstack.org/openstack/python-openstackclient#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a python-openstackclient{,-py2}
}

build() {
  cd "$srcdir"/python-openstackclient
  python setup.py build

  cd "$srcdir"/python-openstackclient-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-openstackclient
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/python-openstackclient-py2
  PYTHON=python2 python2 setup.py testr || warning "Tests failed"
}

package_python-openstackclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneclient'
           'python-requests' 'python-keystoneauth1' 'python-oslo-utils' 'python-oslo-i18n'
           'python-openstacksdk' 'python-os-client-config' 'python-oslo-config'
           'python-glanceclient' 'python-novaclient' 'python-cinderclient' 'python-stevedore'
           'python-simplejson')

  cd "$srcdir"/python-openstackclient
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-openstackclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-cliff' 'python2-keystoneclient'
           'python2-requests' 'python2-keystoneauth1' 'python2-oslo-utils' 'python2-oslo-i18n'
           'python2-openstacksdk' 'python2-os-client-config' 'python2-oslo-config'
           'python2-glanceclient' 'python2-novaclient' 'python2-cinderclient' 'python2-stevedore'
           'python2-simplejson')

  cd "$srcdir"/python-openstackclient-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mv "$pkgdir"/usr/bin/openstack{,2}
}

# vim:set ts=2 sw=2 et:
