# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstacksdk
pkgver=0.99.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://developer.openstack.org/sdks/python/openstacksdk"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-yaml' 'python-jsonpatch' 'python-appdirs'
         'python-keystoneauth1' 'python-os-service-types' 'python-deprecation'
         'python-requestsexceptions' 'python-decorator' 'python-jmespath' 'python-iso8601'
         'python-netifaces' 'python-dogpile.cache' 'python-munch' 'python-cryptography')
makedepends=('python-setuptools')
checkdepends=('python-stestr' 'python-ddt' 'python-mock' 'python-requests-mock' 'python-oslotest'
              'python-jsonschema' 'python-oslo-config' 'python-prometheus_client' 'python-hacking')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/openstacksdk/archive/$pkgver.tar.gz")
sha512sums=('aa8afc1a72af8aecf647579c19670c7729f5cc21c3bf4d23073316f81924d36dd0e4e41a5fbf61e8e294414fe21c5fff668620914baa04b6311b333eac25450c')

export PBR_VERSION=$pkgver

prepare() {
  echo -e "\nglobal-include *.json" >> openstacksdk-$pkgver/MANIFEST.in
  sed -i 's/assertItemsEqual/assertCountEqual/' openstacksdk-$pkgver/openstack/tests/unit/*.py openstacksdk-$pkgver/openstack/tests/unit/*/*.py
}

build() {
  cd openstacksdk-$pkgver
  python setup.py build
}

check() {
  # From https://github.com/openstack/openstacksdk/blob/master/tox.ini
  export OS_LOG_CAPTURE=true OS_STDOUT_CAPTURE=true OS_STDERR_CAPTURE=true

  cd openstacksdk-$pkgver
  # TODO
  stestr run || :
}

package() {
  cd openstacksdk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
