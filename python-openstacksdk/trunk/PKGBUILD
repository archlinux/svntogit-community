# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-openstacksdk
pkgname=(python-openstacksdk python2-openstacksdk)
pkgver=0.9.14
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="http://developer.openstack.org/sdks/python/openstacksdk"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-iso8601' 'python2-iso8601' 'python-stevedore' 'python2-stevedore'
             'python-keystoneauth1' 'python2-keystoneauth1' 'python-os-client-config'
             'python2-os-client-config' 'python-oslo-utils' 'python2-oslo-utils'
             'python-deprecation' 'python2-deprecation')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-mock' 'python2-mock')
source=("git+https://git.openstack.org/openstack/python-openstacksdk#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a python-openstacksdk{,-py2}
}

build() {
  cd "$srcdir"/python-openstacksdk
  python setup.py build

  cd "$srcdir"/python-openstacksdk-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-openstacksdk
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/python-openstacksdk-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-openstacksdk() {
  depends=('python-six' 'python-pbr' 'python-iso8601' 'python-stevedore' 'python-keystoneauth1'
           'python-os-client-config' 'python-oslo-utils' 'python-deprecation')

  cd "$srcdir"/python-openstacksdk
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-openstacksdk() {
  depends=('python2-six' 'python2-pbr' 'python2-iso8601' 'python2-stevedore' 'python2-keystoneauth1'
           'python2-os-client-config' 'python2-oslo-utils' 'python-deprecation')

  cd "$srcdir"/python-openstacksdk-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
