# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=orderedmultidict
pkgbase=python-orderedmultidict
pkgname=('python-orderedmultidict' 'python2-orderedmultidict')
pkgver=1.0.1
pkgrel=1
pkgdesc="Ordered multivalue dictionary that retains method parity with dict."
url="https://github.com/gruns/orderedmultidict"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pycodestyle' 'python2-pycodestyle')
license=('custom')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('5a826bb46dfe9d7e6e2159bf545612a35dd0f1bb5817c1fd78e0f3f4019f9144b36be94af08f87e881a310f593091df09702d001624c377f559a30f64b219c24')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgbase}-${pkgver}"
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  python setup.py test
  python2 setup.py test
}

package_python-orderedmultidict() {
  depends=('python-six')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 {API,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-orderedmultidict() {
  depends=('python2-six')
  cd "${pkgbase}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 {API,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
