# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.8.3
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64)
url="https://github.com/ijl/orjson"
license=(Apache MIT)
depends=(python)
makedepends=(maturin python-installer rust)
checkdepends=(python-arrow python-pendulum python-psutil python-pytest
python-pytz python-xxhash)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/ijl/$_name/archive/$pkgver.tar.gz)
sha512sums=('3d111bf797958b8bca933340a016082aa802ea5b380bb007fbe9f48924e777aab62cec4f4f7e647c6b189687d13d6def5201162b5f6883022d7c8e9aff41ec0a')
b2sums=('f8adedfef9a8a2f07aab14e52d7d9b236cd28569ac1878abe8afb19896cd3eaedffa54e421ee48256bad95db20b33812f1da27df6b19cceab088a839513197ca')

build() {
  cd $_name-$pkgver
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE-* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
