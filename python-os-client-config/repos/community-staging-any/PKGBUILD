# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-os-client-config
pkgname=('python-os-client-config' 'python2-os-client-config')
pkgver=1.24.0
pkgrel=2
arch=('any')
pkgdesc='OpenStack Client Configuation Library'
url='https://github.com/openstack/os-client-config'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml' 'git'
             'python-appdirs' 'python2-appdirs' 'python-keystoneauth1' 'python2-keystoneauth1'
             'python-requestsexceptions' 'python2-requestsexceptions')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-oslotest' 'python2-oslotest'
              'python-jsonschema' 'python2-jsonschema' 'python-mock' 'python2-mock'
              'python-glanceclient' 'python2-glanceclient')
source=("git+https://github.com/openstack/os-client-config.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a os-client-config{,-py2}
}

build() {
  cd "$srcdir"/os-client-config
  python setup.py build

  cd "$srcdir"/os-client-config-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/os-client-config
  # One test failure in os_client_config.tests.test_config.TestConfig
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/os-client-config-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-os-client-config() {
  depends=('python-yaml' 'python-appdirs' 'python-keystoneauth1' 'python-requestsexceptions')

  cd os-client-config
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-os-client-config() {
  depends=('python2-yaml' 'python2-appdirs' 'python2-keystoneauth1' 'python2-requestsexceptions')

  cd os-client-config-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
