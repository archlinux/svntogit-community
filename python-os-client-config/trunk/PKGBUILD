# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-os-client-config
pkgname=('python-os-client-config' 'python2-os-client-config')
pkgver=1.28.0
pkgrel=2
arch=('any')
pkgdesc='OpenStack Client Configuation Library'
url='https://github.com/openstack/os-client-config'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml'
             'python-appdirs' 'python2-appdirs' 'python-keystoneauth1' 'python2-keystoneauth1'
             'python-requestsexceptions' 'python2-requestsexceptions')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-oslotest' 'python2-oslotest'
              'python-jsonschema' 'python2-jsonschema' 'python-mock' 'python2-mock'
              'python-glanceclient' 'python2-glanceclient')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/os-client-config/archive/$pkgver.tar.gz")
sha512sums=('fc7bcd785ae8cc38df2e46eaea2e16d7d87a2aef1f5959ae6c2d4c45edca6589e608547584893ba9ff10ad89a10628d6383f65ebff6cfac37921c3a8cb2db306')

prepare() {
  # FS#54781
  # https://bugs.launchpad.net/os-client-config/+bug/1707019
  sed -i "/^packages =/i package_data = os_client_config = *.json */*.json" os-client-config-$pkgver/setup.cfg

  cp -a os-client-config-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/os-client-config-$pkgver
  python setup.py build

  cd "$srcdir"/os-client-config-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/os-client-config-$pkgver
  # One test failure in os_client_config.tests.test_config.TestConfig
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/os-client-config-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-os-client-config() {
  depends=('python-yaml' 'python-appdirs' 'python-keystoneauth1' 'python-requestsexceptions')

  cd os-client-config-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-os-client-config() {
  depends=('python2-yaml' 'python2-appdirs' 'python2-keystoneauth1' 'python2-requestsexceptions')

  cd os-client-config-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
