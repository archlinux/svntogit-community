# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-os-client-config
pkgname=('python-os-client-config' 'python2-os-client-config')
pkgver=1.33.0
pkgrel=1
arch=('any')
pkgdesc='OpenStack Client Configuation Library'
url='https://github.com/openstack/os-client-config'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-openstacksdk' 'python2-openstacksdk')
checkdepends=('python-stestr' 'python-oslotest' 'python-jsonschema' 'python-mock' 'python-glanceclient')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/os-client-config/archive/$pkgver.tar.gz")
sha512sums=('0ca58456e90b532917f6e4a52f27ba8c434f48874e3f096ff7b34b7152ae49b914e2f47d20b876b8c2dc4e8357cd23aa149193d8aa438934742fc2686a89fbde')

prepare() {
  # FS#54781
  # https://bugs.launchpad.net/os-client-config/+bug/1707019
  sed -i "/^packages =/i package_data = os_client_config = *.json */*.json" os-client-config-$pkgver/setup.cfg

  cp -a os-client-config-$pkgver{,-py2}

  sed -i 's/assertItemsEqual/assertCountEqual/' os-client-config-$pkgver/os_client_config/tests/test_config.py

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/os-client-config-$pkgver
  python setup.py build

  cd "$srcdir"/os-client-config-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/os-client-config-$pkgver
  stestr run
}

package_python-os-client-config() {
  depends=('python-openstacksdk')

  cd os-client-config-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-os-client-config() {
  depends=('python2-openstacksdk')

  cd os-client-config-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
