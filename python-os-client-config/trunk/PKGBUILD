# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-os-client-config
pkgname=('python-os-client-config' 'python2-os-client-config')
pkgver=1.31.2
pkgrel=2
arch=('any')
pkgdesc='OpenStack Client Configuation Library'
url='https://github.com/openstack/os-client-config'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-openstacksdk' 'python2-openstacksdk')
checkdepends=('python-stestr' 'python2-stestr' 'python-oslotest' 'python2-oslotest'
              'python-jsonschema' 'python2-jsonschema' 'python-mock' 'python2-mock'
              'python-glanceclient' 'python2-glanceclient')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/os-client-config/archive/$pkgver.tar.gz")
sha512sums=('4007e2352f69ff294a644bcb82547614ce0026ad06440ec06b538f4f1f72bc5070e34db536e7ed11ad5f6cdb991f33b897c8dcba246b3a3aad9b612e9110e290')

prepare() {
  # FS#54781
  # https://bugs.launchpad.net/os-client-config/+bug/1707019
  sed -i "/^packages =/i package_data = os_client_config = *.json */*.json" os-client-config-$pkgver/setup.cfg

  sed -i 's/assertItemsEqual/assertCountEqual/' os-client-config-$pkgver/os_client_config/tests/test_config.py

  cp -a os-client-config-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/os-client-config-$pkgver
  python setup.py build

  cd "$srcdir"/os-client-config-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/os-client-config-$pkgver
  stestr run

  cd "$srcdir"/os-client-config-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-os-client-config() {
  depends=('python-openstacksdk')

  cd os-client-config-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-os-client-config() {
  depends=('python2-openstacksdk')

  cd os-client-config-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
