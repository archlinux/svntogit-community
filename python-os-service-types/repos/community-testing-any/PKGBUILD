# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-os-service-types
pkgver=1.7.0
_commit=0b2f4733eb1bbbd8880e7fa15a769a9bbf7338b4
pkgrel=12
pkgdesc="Python library for consuming OpenStack sevice-types-authority data"
arch=('any')
url="https://pypi.python.org/pypi/os-service-types/$pkgver"
license=('Apache')
depends=('python-pbr')
makedepends=('git')
checkdepends=('python-oslotest' 'python-requests-mock')
options=('!emptydirs')
source=("git+https://github.com/openstack/os-service-types.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd os-service-types
  python setup.py build
}

check() {
  cd os-service-types
  stestr run
}

package() {
  cd os-service-types
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
