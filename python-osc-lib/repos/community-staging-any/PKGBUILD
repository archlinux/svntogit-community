# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-osc-lib
pkgname=(python-osc-lib python2-osc-lib)
pkgver=1.11.0
pkgrel=1
pkgdesc="OpenStackClient Library"
arch=('any')
url="https://docs.openstack.org/osc-lib/latest/"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel' 'python-cliff' 'python2-cliff'
             'python-keystoneauth1' 'python2-keystoneauth1' 'python-os-client-config'
             'python2-os-client-config' 'python-oslo-i18n' 'python2-oslo-i18n' 'python-oslo-utils'
             'python2-oslo-utils' 'python-simplejson' 'python2-simplejson' 'python-stevedore'
             'python2-stevedore' 'python-openstacksdk' 'python2-openstacksdk')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock'
              'python2-requests-mock' 'python-testrepository' 'python2-testrepository')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/osc-lib/archive/$pkgver.tar.gz")
sha512sums=('6b837b99b6fb59e2db3964d0c0ac14f0f457c80bcffe958f8e1b258ada82d93e1738a2763805f0af99ed0951136f687c85907179cab813c0a75e2694476dbaea')

prepare() {
  cp -a osc-lib-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/osc-lib-$pkgver
  python setup.py build

  cd "$srcdir"/osc-lib-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/osc-lib-$pkgver
  stestr run || warning "Tests failed"

  cd "$srcdir"/osc-lib-$pkgver-py2
  PYTHON=python2 stestr2 run || warning "Tests failed"
}

package_python-osc-lib() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneauth1'
           'python-os-client-config' 'python-oslo-i18n' 'python-oslo-utils' 'python-stevedore'
           'python-simplejson' 'python-openstacksdk')

  cd osc-lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-osc-lib() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-cliff' 'python2-keystoneauth1'
           'python2-os-client-config' 'python2-oslo-i18n' 'python2-oslo-utils' 'python2-stevedore'
           'python2-simplejson' 'python2-openstacksdk')

  cd osc-lib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
