# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-concurrency
pkgname=(python-oslo-concurrency python2-oslo-concurrency)
pkgver=4.0.0
pkgrel=2
pkgdesc="OpenStack library for all concurrency-related code"
arch=('any')
url="https://pypi.python.org/pypi/oslo.concurrency/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-oslo-config'
             'python2-oslo-config' 'python-oslo-i18n' 'python2-oslo-i18n' 'python-oslo-utils'
             'python2-oslo-utils' 'python-fasteners' 'python2-fasteners' 'python2-enum34')
checkdepends=('python-mock' 'python-oslotest' 'python-oslo-config' 'python-eventlet')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.concurrency/archive/$pkgver.tar.gz")
sha512sums=('d75d84d4ce27b7418e358e5b5fdf0015511aa2caaddfed38f9eb4d1fdb4635c7df348c6cafbad26b3a0b9af8b41df898450f050e8899c4659c81b63ef8dfb45c')

export PBR_VERSION=$pkgver

prepare() {
  cp -a oslo.concurrency-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/oslo.concurrency-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.concurrency-$pkgver-py2
  python2 setup.py build
}

check() {
  cd oslo.concurrency-$pkgver
  stestr run || : # "1 failure about raising rlimit"
}

package_python-oslo-concurrency() {
  depends=('python-six' 'python-pbr' 'python-oslo-config' 'python-oslo-i18n' 'python-oslo-utils'
           'python-fasteners')

  cd "$srcdir"/oslo.concurrency-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-concurrency() {
  depends=('python2-six' 'python2-pbr' 'python2-oslo-config' 'python2-oslo-i18n'
           'python2-oslo-utils' 'python2-fasteners' 'python2-enum34')

  cd "$srcdir"/oslo.concurrency-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/lockutils-wrapper{,2}
}

# vim:set ts=2 sw=2 et:
