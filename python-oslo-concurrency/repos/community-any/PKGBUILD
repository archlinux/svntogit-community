# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-concurrency
pkgver=4.1.1
pkgrel=1
pkgdesc="OpenStack library for all concurrency-related code"
arch=('any')
url="https://pypi.python.org/pypi/oslo.concurrency/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-oslo-config' 'python-oslo-i18n' 'python-oslo-utils'
         'python-fasteners')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest' 'python-oslo-config' 'python-eventlet')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.concurrency/archive/$pkgver.tar.gz")
sha512sums=('98e86063dc6e85f29c7a8dd9b6a58fd93a0569e582b38d5b24574214db41987cba8d6d9af5a573fcf39b248eba5940267ef84444f4af56068e20ee8c8b90876b')

export PBR_VERSION=$pkgver

build() {
  cd oslo.concurrency-$pkgver
  python setup.py build
}

check() {
  cd oslo.concurrency-$pkgver
  stestr run || : # "1 failure about raising rlimit"
}

package() {
  cd oslo.concurrency-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
