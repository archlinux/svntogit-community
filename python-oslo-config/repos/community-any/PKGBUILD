# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-config
pkgver=8.0.1
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
         'python-oslo-i18n' 'python-rfc3986' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-sphinx' 'python-requests-mock' 'python-oslo-log')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.config/archive/$pkgver.tar.gz")
sha512sums=('7dcb4681622be4160bfc585964c92164f8f53ccef4f0e3ab0bf824eebb7cc1b0546b64888ee5a3cd784903c10cbdb34c1c2ab3efaef0b7dd693877f439cef6cf')

export PBR_VERSION=$pkgver

build() {
  cd oslo.config-$pkgver
  python setup.py build
}

check() {
  cd oslo.config-$pkgver
  stestr run
}

package() {
  cd oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
