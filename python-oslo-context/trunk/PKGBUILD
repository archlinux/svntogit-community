# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=3.0.2
pkgrel=1
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-debtcollector')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.context/archive/$pkgver.tar.gz")
sha512sums=('d0e5c1cc10c0799d501c1e82b5e134ae8e5e78b7700f3f60334a4544dd49b37fae3f06d818d2c583d2c2ceeb9843d9ed2e2b2ad21b146b194a02dc98847d2bf5')

export PBR_VERSION=$pkgver

build() {
  cd oslo.context-$pkgver
  python setup.py build
}

check() {
  cd oslo.context-$pkgver
  stestr run
}

package() {
  cd oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
