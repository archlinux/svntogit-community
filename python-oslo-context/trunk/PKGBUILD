# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-context
pkgname=(python-oslo-context python2-oslo-context)
pkgver=3.0.0
pkgrel=2
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-debtcollector'
             'python2-debtcollector')
checkdepends=('python-mock' 'python-oslotest')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.context/archive/$pkgver.tar.gz")
sha512sums=('7e8f1e4cb0d84466e6ce15052f136e922125cc5f434d0fb8ae109e743e8ed7cbd2caf6600b5ee70ef724c808483eb0331f738bee1625f1f931316ab56bf6143d')

export PBR_VERSION=$pkgver

prepare() {
  cp -a oslo.context-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/oslo.context-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.context-$pkgver-py2
  python2 setup.py build
}

check() {
  cd oslo.context-$pkgver
  stestr run
}

package_python-oslo-context() {
  depends=('python-pbr' 'python-debtcollector')

  cd "$srcdir"/oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-context() {
  depends=('python2-pbr' 'python2-debtcollector')

  cd "$srcdir"/oslo.context-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
