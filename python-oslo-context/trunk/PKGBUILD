# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=3.2.0
pkgrel=1
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-debtcollector')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.context/archive/$pkgver.tar.gz")
sha512sums=('4081e4ca905a0420edc1eaa27dcc878016beb81068e5d3ee07d6904f934937a29690b6c788fa37cb1347455860cc5412086d27933ce325513358bb2f1995bc88')

export PBR_VERSION=$pkgver

build() {
  cd oslo.context-$pkgver
  python setup.py build
}

check() {
  cd oslo.context-$pkgver
  stestr run
}

package() {
  cd oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
