# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-db
pkgname=(python-oslo-db python2-oslo-db)
pkgver=7.0.0
pkgrel=2
pkgdesc="OpenStack Database Pattern Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.db/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-alembic'
             'python2-alembic' 'python-debtcollector' 'python2-debtcollector' 'python-oslo-i18n'
             'python2-oslo-i18n' 'python-oslo-config' 'python2-oslo-config' 'python-oslo-utils'
             'python2-oslo-utils' 'python-sqlalchemy' 'python2-sqlalchemy'
             'python-sqlalchemy-migrate' 'python2-sqlalchemy-migrate' 'python-stevedore'
             'python2-stevedore')
checkdepends=('python-mock' 'python-testresources' 'python-oslotest' 'python-oslo-context')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.db/archive/$pkgver.tar.gz")
sha512sums=('4153179c93be845d984bf1612aff82c35873b6b2b85a16a97c9f192adbced1d9064d0e12bae416b543fe033c49fcbf27e02a4085b21ee76485fd37cd06f22d7c')

export PBR_VERSION=$pkgver

prepare() {
  cp -a oslo.db-$pkgver{,-py2}
  sed -i 's/import unittest2/import unittest as unittest2/' oslo.db-$pkgver/oslo_db/tests/sqlalchemy/test_async_eventlet.py
}

build() {
  cd "$srcdir"/oslo.db-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.db-$pkgver-py2
  python2 setup.py build
}

check() {
  cd oslo.db-$pkgver
  stestr run
}

package_python-oslo-db() {
  depends=('python-six' 'python-pbr' 'python-alembic' 'python-debtcollector' 'python-oslo-i18n'
           'python-oslo-config' 'python-oslo-utils' 'python-sqlalchemy' 'python-sqlalchemy-migrate'
           'python-stevedore')

  cd "$srcdir"/oslo.db-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-db() {
  depends=('python2-six' 'python2-pbr' 'python2-alembic' 'python2-debtcollector' 'python2-oslo-i18n'
           'python2-oslo-config' 'python2-oslo-utils' 'python2-sqlalchemy'
           'python2-sqlalchemy-migrate' 'python2-stevedore')

  cd "$srcdir"/oslo.db-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
