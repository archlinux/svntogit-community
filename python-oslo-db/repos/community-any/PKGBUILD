# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-db
pkgver=8.2.1
pkgrel=1
pkgdesc="OpenStack Database Pattern Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.db/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-alembic' 'python-debtcollector' 'python-oslo-i18n'
         'python-oslo-config' 'python-oslo-utils' 'python-sqlalchemy' 'python-sqlalchemy-migrate'
         'python-stevedore')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-testresources' 'python-oslotest' 'python-oslo-context')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.db/archive/$pkgver.tar.gz")
sha512sums=('cb27b7f2ed4a4ff20c811c753e11ce039bcfa5dd04317a234bd28b7d2ce707114cb0f6ed1dd2ffca74e8dccdd2fb686fb7c7d457591cab355d98f25d7bc39ac3')

export PBR_VERSION=$pkgver

prepare() {
  sed -i 's/import unittest2/import unittest as unittest2/' oslo.db-$pkgver/oslo_db/tests/sqlalchemy/test_async_eventlet.py
}

build() {
  cd oslo.db-$pkgver
  python setup.py build
}

check() {
  cd oslo.db-$pkgver
  stestr run
}

package() {
  cd oslo.db-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
