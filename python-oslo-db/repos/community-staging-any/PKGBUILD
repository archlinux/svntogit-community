# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-db
pkgname=(python-oslo-db python2-oslo-db)
pkgver=4.40.0
pkgrel=2
pkgdesc="OpenStack Database Pattern Library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.db/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-alembic'
             'python2-alembic' 'python-debtcollector' 'python2-debtcollector' 'python-oslo-i18n'
             'python2-oslo-i18n' 'python-oslo-config' 'python2-oslo-config' 'python-oslo-utils'
             'python2-oslo-utils' 'python-sqlalchemy' 'python2-sqlalchemy'
             'python-sqlalchemy-migrate' 'python2-sqlalchemy-migrate' 'python-stevedore'
             'python2-stevedore')
checkdepends=('python-mock' 'python2-mock' 'python-testrepository' 'python2-testrepository'
              'python-testresources' 'python2-testresources' 'python-oslotest' 'python2-oslotest'
              'python-oslo-context' 'python2-oslo-context' 'python2-unittest2')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.db/archive/$pkgver.tar.gz")
sha512sums=('47bde2fea3f7603e0052fe4ab649f93887f68f6425057e7e732092ed1f72a0b9917afbf9eab1bce92a40e6292fa64562ee117074744619126a794237627fb0b5')

prepare() {
  cp -a oslo.db-$pkgver{,-py2}
  sed -i 's/import unittest2/import unittest as unittest2/' oslo.db-$pkgver/oslo_db/tests/sqlalchemy/test_async_eventlet.py

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.db-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.db-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.db-$pkgver
  python setup.py testr

  cd "$srcdir"/oslo.db-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-db() {
  depends=('python-six' 'python-pbr' 'python-alembic' 'python-debtcollector' 'python-oslo-i18n'
           'python-oslo-config' 'python-oslo-utils' 'python-sqlalchemy' 'python-sqlalchemy-migrate'
           'python-stevedore')

  cd "$srcdir"/oslo.db-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-db() {
  depends=('python2-six' 'python2-pbr' 'python2-alembic' 'python2-debtcollector' 'python2-oslo-i18n'
           'python2-oslo-config' 'python2-oslo-utils' 'python2-sqlalchemy'
           'python2-sqlalchemy-migrate' 'python2-stevedore')

  cd "$srcdir"/oslo.db-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
