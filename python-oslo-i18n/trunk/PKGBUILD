# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-i18n
pkgver=5.0.1
pkgrel=1
pkgdesc="Oslo i18n library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.i18n/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel')
checkdepends=('python-mock' 'python-oslotest' 'python-oslo-config')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.i18n/archive/$pkgver.tar.gz")
sha512sums=('342d5a53503a10ddbc606adc9d633044e604acd45dfe3a424c24cfb70b0c46c3536ff8f05683d51dd97dc2a29bb5d910ccb881c62d2bbc0553ba1ed6ff04ae72')

export PBR_VERSION=$pkgver

build() {
  cd oslo.i18n-$pkgver
  python setup.py build
}

check() {
  cd oslo.i18n-$pkgver
  stestr run
}

package() {
  cd oslo.i18n-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
