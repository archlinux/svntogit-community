# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslo-serialization
pkgname=(python-oslo-serialization python2-oslo-serialization)
pkgver=2.29.1
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-msgpack' 'python2-msgpack' 'python-oslo-utils'
             'python2-oslo-utils' 'python-pytz' 'python2-pytz')
checkdepends=('python-oslotest' 'python2-oslotest' 'python2-ipaddress')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz")
sha512sums=('6ae30dbd7f6b3e9f9ffd7524eeb8aae42d37c0a0493730a7bc2f47e9f94e486a8bb74779c921ca1c1ca668c133026c9503ade1913c1084266cba6e11e8e5f72e')

export PBR_VERSION=$pkgver

prepare() {
  cp -a oslo.serialization-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/oslo.serialization-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.serialization-$pkgver
  stestr run

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-oslo-serialization() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
           'python-pytz')

  cd "$srcdir"/oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-serialization() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-msgpack' 'python2-oslo-utils'
           'python2-pytz')

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
