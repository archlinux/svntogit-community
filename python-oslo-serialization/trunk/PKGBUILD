# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslo-serialization
pkgname=(python-oslo-serialization python2-oslo-serialization)
pkgver=2.23.0
pkgrel=2
pkgdesc="Oslo Serialization library"
arch=('any')
url="http://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-msgpack' 'python2-msgpack' 'python-oslo-utils'
             'python2-oslo-utils' 'python-pytz' 'python2-pytz')
checkdepends=('python-oslotest' 'python2-oslotest' 'python2-ipaddress')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz"
        "https://github.com/openstack/oslo.serialization/commit/277730036f1d7b2edd3fe3adc626e1bf97d494cc.patch")
sha512sums=('3bff8da9a8c8c1e468f745d8937683d2f80bc18492b3f9492d9dfd1a2a543d890a33d0c3277e6b049fef3bd461f3b3b010550854f83acca433aedc47cd2ab355'
            'a24bb03b61d7eb1d7657e99f93c08482caf6104dc551cef87b9c00d8a9dbbc38d28370e42a338d282b0b6b289ed224aab21a43e068dc3d5883a2c84849586ccd')

export PBR_VERSION=$pkgver

prepare() {
  # https://bugs.launchpad.net/oslo.serialization/+bug/1743445
  patch -d oslo.serialization-$pkgver -p1 < 277730036f1d7b2edd3fe3adc626e1bf97d494cc.patch
  cp -a oslo.serialization-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/oslo.serialization-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.serialization-$pkgver
  python setup.py testr

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-serialization() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
           'python-pytz')

  cd "$srcdir"/oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-serialization() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-msgpack' 'python2-oslo-utils'
           'python2-pytz')

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
