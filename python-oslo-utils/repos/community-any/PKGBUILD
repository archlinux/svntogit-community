# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-utils
pkgname=(python-oslo-utils python2-oslo-utils)
pkgver=3.9.0
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python2-funcsigs'
             'python-iso8601' 'python2-iso8601' 'python-oslo-i18n' 'python2-oslo-i18n' 'python2-monotonic'
             'python-pytz' 'python2-pytz' 'python-netaddr' 'python2-netaddr' 'python-netifaces'
             'python2-netifaces' 'python-debtcollector' 'python2-debtcollector')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("git+https://git.openstack.org/openstack/oslo.utils#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a oslo.utils{,-py2}

  # Use python 3's standard monotonic function
  sed -i '/monotonic/d' oslo.utils/requirements.txt
  sed -i 's/from monotonic import monotonic/from time import monotonic/' oslo.utils/oslo_utils/timeutils.py
}

build() {
  cd "$srcdir"/oslo.utils
  python setup.py build

  cd "$srcdir"/oslo.utils-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.utils
  python setup.py testr

  cd "$srcdir"/oslo.utils-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-utils() {
  depends=('python-six' 'python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz'
           'python-netaddr' 'python-netifaces' 'python-debtcollector')

  cd "$srcdir"/oslo.utils
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-utils() {
  depends=('python2-six' 'python2-pbr' 'python2-iso8601' 'python2-oslo-i18n' 'python2-monotonic'
           'python2-pytz' 'python2-netaddr' 'python2-netifaces' 'python2-debtcollector'
           'python2-funcsigs')

  cd "$srcdir"/oslo.utils-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
