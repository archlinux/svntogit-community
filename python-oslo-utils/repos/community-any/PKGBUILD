# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-utils
pkgver=6.0.0
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
depends=('python-iso8601' 'python-oslo-i18n' 'python-pytz' 'python-netaddr'
         'python-netifaces' 'python-debtcollector' 'python-pyparsing' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-ddt' 'python-eventlet')
source=("https://github.com/openstack/oslo.utils/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ae5871a2d3ef3a8e30f5e82a5d7368e7e81d13e3f017aad46aee4fcab5122edf6243aa61f5ee1a668973d335e59f622233864a631f7da4eae0eeeebda4e93011')

export PBR_VERSION=$pkgver

build() {
  cd oslo.utils-$pkgver
  python setup.py build
}

check() {
  cd oslo.utils-$pkgver
  stestr run
}

package() {
  cd oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
