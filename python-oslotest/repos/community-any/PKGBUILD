# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslotest
pkgname=(python-oslotest python2-oslotest)
pkgver=3.2.0
pkgrel=1
pkgdesc="Oslo test framework"
arch=('any')
url="https://github.com/openstack/oslotest"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-fixtures'
             'python2-fixtures' 'python-subunit' 'python2-subunit' 'python-testrepository'
             'python2-testrepository' 'python-testscenarios' 'python2-testscenarios'
             'python-testtools' 'python2-testtools' 'python-mock' 'python2-mock' 'python-mox3'
             'python2-mox3' 'python-os-client-config' 'python2-os-client-config'
             'python-debtcollector' 'python2-debtcollector')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslotest/archive/$pkgver.tar.gz")
sha512sums=('1b5f0b96b126e014069be10f6e82ccfb903b991af0ef572c7ae9e248b9dd6bfaeaa8e2b179c87dd82d00d6b65c99040b7f28a9b3550459b641dbc911ef80c35f')

prepare() {
  cp -a oslotest-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslotest-$pkgver
  python setup.py build

  cd "$srcdir"/oslotest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslotest-$pkgver
  python setup.py testr

  cd "$srcdir"/oslotest-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslotest() {
  depends=('python-fixtures' 'python-subunit' 'python-testrepository' 'python-testscenarios'
           'python-testtools' 'python-mock' 'python-mox3' 'python-os-client-config'
           'python-debtcollector')

  cd "$srcdir"/oslotest-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslotest() {
  depends=('python2-fixtures' 'python2-subunit' 'python2-testrepository' 'python2-testscenarios'
           'python2-testtools' 'python2-mock' 'python2-mox3' 'python2-os-client-config'
           'python2-debtcollector')

  cd "$srcdir"/oslotest-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  for _exe in "$pkgdir"/usr/bin/*; do
    mv "$_exe"{,2}
  done
}

# vim:set ts=2 sw=2 et:
