# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslotest
pkgname=(python-oslotest python2-oslotest)
pkgver=3.7.0
pkgrel=1
pkgdesc="Oslo test framework"
arch=('any')
url="https://github.com/openstack/oslotest"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-fixtures'
             'python2-fixtures' 'python-subunit' 'python2-subunit' 'python-stestr'
             'python2-stestr' 'python-testscenarios' 'python2-testscenarios'
             'python-testtools' 'python2-testtools' 'python-mock' 'python2-mock' 'python-mox3'
             'python2-mox3' 'python-os-client-config' 'python2-os-client-config'
             'python-debtcollector' 'python2-debtcollector')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslotest/archive/$pkgver.tar.gz")
sha512sums=('f4845bdd4a7caccb7b1523a901123e18dedf2e9adc36df3911a7a089e3f3d2fd4450613c24679e665811c12a4b1a31bfd6fbb8f9afff72eb708f34f5003e8494')

prepare() {
  cp -a oslotest-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslotest-$pkgver
  python setup.py build

  cd "$srcdir"/oslotest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslotest-$pkgver
  stestr run

  cd "$srcdir"/oslotest-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-oslotest() {
  depends=('python-fixtures' 'python-subunit' 'python-stestr' 'python-testscenarios'
           'python-testtools' 'python-mock' 'python-mox3' 'python-os-client-config'
           'python-debtcollector')

  cd "$srcdir"/oslotest-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslotest() {
  depends=('python2-fixtures' 'python2-subunit' 'python2-stestr' 'python2-testscenarios'
           'python2-testtools' 'python2-mock' 'python2-mox3' 'python2-os-client-config'
           'python2-debtcollector')

  cd "$srcdir"/oslotest-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  for _exe in "$pkgdir"/usr/bin/*; do
    mv "$_exe"{,2}
  done
}

# vim:set ts=2 sw=2 et:
