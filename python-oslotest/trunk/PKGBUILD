# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslotest
pkgname=(python-oslotest python2-oslotest)
pkgver=2.5.0
pkgrel=1
pkgdesc="Oslo test framework"
arch=('any')
url="https://github.com/openstack/oslotest"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-fixtures' 'python2-fixtures' 'python-subunit' 'python2-subunit'
             'python-testrepository' 'python2-testrepository' 'python-testscenarios'
             'python2-testscenarios' 'python-testtools' 'python2-testtools'
             'python-mock' 'python2-mock' 'python-mox3' 'python2-mox3'
             'python-os-client-config' 'python2-os-client-config' 'python-debtcollector'
             'python2-debtcollector')
source=("git+https://github.com/openstack/oslotest.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a oslotest{,-py2}
}

build() {
  cd "$srcdir"/oslotest
  python setup.py build

  cd "$srcdir"/oslotest-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslotest
  python setup.py testr

  cd "$srcdir"/oslotest-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslotest() {
  depends=('python-fixtures' 'python-subunit' 'python-testrepository' 'python-testscenarios'
           'python-testtools' 'python-mock' 'python-mox3' 'python-os-client-config'
           'python-debtcollector')

  cd "$srcdir"/oslotest
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslotest() {
  depends=('python2-fixtures' 'python2-subunit' 'python2-testrepository' 'python2-testscenarios'
           'python2-testtools' 'python2-mock' 'python2-mox3' 'python2-os-client-config'
           'python2-debtcollector')

  cd "$srcdir"/oslotest-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  for _exe in "$pkgdir"/usr/bin/*; do
    mv "$_exe"{,2}
  done
}

# vim:set ts=2 sw=2 et:
