# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-osprofiler
pkgver=3.4.0
pkgrel=1
pkgdesc="OpenStack Profiler Library"
arch=('any')
url="https://docs.openstack.org/osprofiler/latest/"
license=('Apache')
depends=('python-netaddr' 'python-oslo-concurrency' 'python-oslo-serialization' 'python-oslo-utils'
         'python-prettytable' 'python-requests' 'python-six' 'python-webob')
checkdepends=('python-ddt' 'python-docutils' 'python-elasticsearch' 'python-jaeger-client'
              'python-pymongo' 'python-redis' 'python-stestr')
source=("https://github.com/openstack/osprofiler/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('dbe8f0a924e2ac2b59c657b201c0451419a6212fa091e6f50cef660d3ce5beef6e5477be698a3164eb57535f02ee9b5208aed64227ad82b7bbc3167cd31c2877')

export PBR_VERSION=$pkgver

build() {
  cd osprofiler-$pkgver
  python setup.py build
}

check() {
  cd osprofiler-$pkgver
  stestr run
}

package() {
  cd osprofiler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
