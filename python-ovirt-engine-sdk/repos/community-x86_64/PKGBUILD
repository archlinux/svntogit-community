# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgbase='python-ovirt-engine-sdk'
pkgname=('python-ovirt-engine-sdk' 'python2-ovirt-engine-sdk')
pkgver=4.2.6
pkgrel=1
pkgdesc='Python access to the oVirt Engine API'
arch=('x86_64')
url='http://ovirt.org/develop/release-management/features/infra/python-sdk'
makedepends=('python-setuptools' 'python2-setuptools' 'libxml2')
license=('custom')
source=("http://resources.ovirt.org/pub/ovirt-4.2/src/python-ovirt-engine-sdk4/ovirt-engine-sdk-python-${pkgver}.tar.gz")
sha512sums=('17ddfcda95f50a067b7193e345f8e8173334fab3c4506d134ccae66d14557d4b3861039cf0b6aa9fdcd889472b1b153ed39728128970d2f2e95956507fdb7563')

package_python-ovirt-engine-sdk() {
  depends=('python-pycurl' 'python-six')

  cd "${srcdir}/ovirt-engine-sdk-python-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-ovirt-engine-sdk() {
  depends=('python2-pycurl' 'python2-six' 'python2-enum34')

  cd "${srcdir}/ovirt-engine-sdk-python-${pkgver}"
  python2 setup.py install -O1 --root="${pkgdir}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
