# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Keith Hughitt <khughitt@umd.edu>

# TODO: move python-fastparque from AUR to community (Apache Parquet)
# TODO: move python-gcsfs from AUR to community (Google Cloud Storage access)
# TODO: move python-numba from AUR to community (Alternative execution engine)
# TODO: package python-pandas-gbq (Google BigQuery I/O)
# TODO: package python-pyreadstat (SPSS files reading)
# TODO: package python-pyxlsb (reading XLSB files)
# TODO: move python-s3fs from AUR to community (Amazon S3 access)
# TODO: move python-xarray from AUR to community (converting Panels to xarray)
# TODO: move python-pyarrow from AUR to community (Feather Format and Apache Parquet)

pkgname=python-pandas
pkgver=1.0.0
pkgrel=1
pkgdesc='High-performance, easy-to-use data structures and data analysis tools for Python'
arch=('x86_64')
url="https://pandas.pydata.org/"
license=('BSD')
depends=('python-numpy' 'python-dateutil' 'python-pytz' 'python-setuptools')
makedepends=('cython')
optdepends=('python-pandas-datareader: pandas.io.data replacement (recommended)'
            'python-numexpr: needed for accelerating certain numerical operations (recommended)'
            'python-bottleneck: needed for accelerating certain types of nan evaluations (recommended)'
            'python-beautifulsoup4: needed for read_html function'
            'python-jinja: needed for conditional HTML formatting'
            'python-pyqt5: needed for read_clipboard function (only one needed)'
            'python-pytables: needed for HDF5-based storage'
            'python-sqlalchemy: needed for SQL database support'
            'python-scipy: needed for miscellaneous statistical functions'
            'python-xlsxwriter: alternative Excel XLSX output'
            'python-blosc: for msgpack compression using blosc'
            'python-html5lib: needed for read_hmlt function (and/or python-lxml)'
            'python-lxml: needed for read_html function (and/or python-html5lib)'
            'python-matplotlib: needed for plotting'
            'python-openpyxl: needed for Excel XLSX input/output'
            'python-psycopg2: needed for PostgreSQL engine for sqlalchemy'
            'python-pymysql: needed for MySQL engine for sqlalchemy'
            'python-qtpy: needed for read_clipboard function (only one needed)'
            'python-tabulate: needed for printing in Markdown-friendly format'
            'xclip: needed for read_clipboard function (only one needed)'
            'python-xlrd: needed for Excel XLS input'
            'python-xlwt: needed for Excel XLS output'
            'xsel: needed for read_clipboard function (only one needed)'
            'zlib: needed for compression for msgpack')
source=("https://github.com/pandas-dev/pandas/releases/download/v${pkgver}/pandas-${pkgver}.tar.gz")
sha256sums=('3ea6cc86931f57f18b1240572216f09922d91b19ab8a01cf24734394a3db3bec')

build() {
  cd "$srcdir"/pandas-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package() {
  cd "$srcdir"/pandas-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
