# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=param
pkgname=python-${_pkg}
pkgver=1.11.1
pkgrel=1
pkgdesc="Make your Python code clearer and more reliable by declaring Parameters"
arch=(any)
url="https://param.holoviz.org/"
license=(BSD)
makedepends=(python-setuptools)
checkdepends=(python-nose python-numpy python-pandas ipython)
# No tests in Pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(https://github.com/holoviz/param/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('4fb0c481c3f99bf71c38bab552b9f332bfdda0fbf0b786915a10cad3359d1e2b')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PARAM_TEST_NUMPY=1 PARAM_TEST_PANDAS=1 PARAM_TEST_PANDAS=1 nosetests
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
