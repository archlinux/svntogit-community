# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parameterized
pkgver=0.7.4
pkgrel=3
pkgdesc="Parameterized testing with any Python test framework"
url="https://github.com/wolever/parameterized"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock')
source=("https://pypi.io/packages/source/p/parameterized/parameterized-$pkgver.tar.gz")
sha512sums=('71c51ad57900f5b1b6f4866c60ba4d23649df7740e7f7e735a2e0faf4810bc8dc552a1647da8c174f8a0b80db7de5727ce24dabc9fee6b4cfe12ee98c18945d7')

build() {
  cd parameterized-$pkgver
  python setup.py build
}

check() {
  cd parameterized-$pkgver
  python setup.py nosetests
}

package() {
  cd parameterized-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
