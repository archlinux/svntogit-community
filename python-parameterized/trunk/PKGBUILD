# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parameterized
pkgver=0.7.1
pkgrel=1
pkgdesc="Parameterized testing with any Python test framework"
url="https://github.com/wolever/parameterized"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock')
source=("https://pypi.io/packages/source/p/parameterized/parameterized-$pkgver.tar.gz"
        skip_Documentation_tests.patch)
sha512sums=('331752b6bf27f88921d39567a58900c0e78f1740056d93755fc7e4528a032207f81e4938302c27cdfd82fe2ad455c01100d91da5f222a11e4bcf0210de68d9c2'
            '70a2408e954c8d19dc80b8de472f0d83d8ae44a7360f72855236ce2c239dde417c9e1bad2ad09855c1538e99e228a4cc1a2e81c6d2edf9fa1718f7cb400f2d54')

prepare() {
  cd parameterized-$pkgver
  # https://github.com/wolever/parameterized/issues/84
  patch -p1 -i ../skip_Documentation_tests.patch
}

build() {
  cd parameterized-$pkgver
  python setup.py build
}

check() {
  cd parameterized-$pkgver
  python setup.py nosetests
}

package() {
  cd parameterized-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
