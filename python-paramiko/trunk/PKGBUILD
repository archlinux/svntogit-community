# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgbase=python-paramiko
pkgname=(python-paramiko python2-paramiko)
pkgver=1.15.2
pkgrel=1
pkgdesc="Python module that implements the SSH2 protocol"
url="https://github.com/paramiko/paramiko/"
license=('LGPL')
arch=('any')
makedepends=('python-ecdsa' 'python2-ecdsa' 'python-crypto' 'python2-crypto')
source=("http://pypi.python.org/packages/source/p/paramiko/paramiko-$pkgver.tar.gz")
md5sums=('6bbfb328fe816c3d3652ba6528cc8b4c')

prepare() {
  cp -a paramiko-$pkgver{,-py2}

  find paramiko-$pkgver-py2 -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
        -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

check() {
  cd "$srcdir/paramiko-$pkgver"
  # Workaround encoding issue
  LANG=en_US.UTF-8 python test.py || warning "Tests failed" # Fails after actual tests finished

  cd "$srcdir/paramiko-$pkgver-py2"
  LANG=en_US.UTF-8 python2 test.py
}

package_python-paramiko() {
  depends=('python-crypto' 'python-ecdsa')
  cd "$srcdir/paramiko-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py
}

package_python2-paramiko() {
  depends=('python2-crypto' 'python2-ecdsa')
  cd "$srcdir/paramiko-$pkgver-py2"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py
}

