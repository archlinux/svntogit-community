# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Daniel M. Capella <polycitizen@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgbase=python-parsedatetime
pkgname=('python-parsedatetime' 'python2-parsedatetime')
pkgver=1.5.20151107
_commit=cd5847e10d72d90d394f471f64f4e2d93de0b02d
pkgrel=1
pkgdesc="Parse human-readable date/time strings."
arch=('any')
license=('Apache')
url='http://github.com/bear/parsedatetime'
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-pyicu' 'python2-pyicu'
              'python-coverage' 'python2-coverage')
source=("git+https://github.com/bear/parsedatetime.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a parsedatetime{,-py2}
}

build() {
  cd "$srcdir/parsedatetime"
  python setup.py build

  cd "$srcdir/parsedatetime-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/parsedatetime"
  nosetests3

  cd "$srcdir/parsedatetime-py2"
  nosetests2
}

package_python-parsedatetime() {
  depends=('python')

  cd parsedatetime
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-parsedatetime() {
  depends=('python2')

  cd parsedatetime-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
