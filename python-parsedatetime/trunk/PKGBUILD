# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Daniel M. Capella <polycitizen@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgname=python-parsedatetime
pkgver=2.4
pkgrel=3
pkgdesc="Parse human-readable date/time strings."
arch=('any')
license=('Apache')
url='https://github.com/bear/parsedatetime'
depends=('python-future')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bear/parsedatetime/archive/v$pkgver.tar.gz")
sha512sums=('0fd8bfa3411476a20d8874b44036bc162735fb7865d39ca98b0a608daec39daac47100314279dbf66229ec333821492744f7505315d955f58216fdaa75cbf046')

build() {
  cd parsedatetime-$pkgver
  python setup.py build
}

check() {
  cd parsedatetime-$pkgver
  python setup.py pytest
}

package() {
  cd parsedatetime-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
