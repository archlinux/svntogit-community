# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parver
pkgver=0.3
pkgrel=1
pkgdesc="Parse and manipulate version numbers"
url="https://github.com/RazerM/parver"
license=('MIT')
arch=('any')
depends=('python-arpeggio' 'python-attrs' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis' 'python-pretend')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RazerM/parver/archive/$pkgver.tar.gz")
sha512sums=('0b31ee3c3fc24b056a97ccb9df10dec69ca66f4690b7f01ae84fbe583b77cf713b98d207eaf7a34c2192eecdf4077121154fe9b59aa83fe4836080cae729a90d')

build() {
  cd parver-$pkgver
  python setup.py build
}

check() {
  cd parver-$pkgver
  python setup.py pytest
}

package() {
  cd parver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
