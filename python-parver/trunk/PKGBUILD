# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parver
pkgver=0.2.1
pkgrel=1
pkgdesc="Parse and manipulate version numbers"
url="https://github.com/RazerM/parver"
license=('MIT')
arch=('any')
depends=('python-arpeggio' 'python-attrs' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis' 'python-pretend')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RazerM/parver/archive/$pkgver.tar.gz")
sha512sums=('674e97d5bef6bb02a659ecf5e1a78fa5d597436b656d0251db20fe330a151ae8de52a3f3f0d3732ccb624ad9093116c07837ba0cbe4ae7ae664b4d47ec3333c3')

build() {
  cd parver-$pkgver
  python setup.py build
}

check() {
  cd parver-$pkgver
  python setup.py pytest
}

package() {
  cd parver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
