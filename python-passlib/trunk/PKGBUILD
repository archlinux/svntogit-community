# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: onny <onny@project-insanity.org>

_pypiname=passlib
pkgbase=python-$_pypiname
pkgname=(python-$_pypiname python2-$_pypiname)
pkgver=1.6.2
pkgrel=1
pkgdesc="A password hashing library for Python"
arch=('any')
url=("https://code.google.com/p/passlib/")
license=('custom:BSD')
makedepends=('python' 'python2')
source=("https://${_pypiname}.googlecode.com/files/$_pypiname-${pkgver}.tar.gz")
sha512sums=('783fe4fbfd4dca9d5cd39c49140976fb91efe41303ae31fec0f6f47101071600ae1709f3c4950bfac0bc9cbe65a2508781153aa3788b6451794f2acc9a1dfc58')

prepare() {
  cp -r ${_pypiname}-${pkgver} python2-${_pypiname}-${pkgver}
}

package_python-passlib() {
  depends=("python")

  cd ${_pypiname}-${pkgver}
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-passlib() {
  depends=("python2")
  optdepends=("python2-m2crypto: accelerate PBKDF2-based hashes")

  cd python2-${_pypiname}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
