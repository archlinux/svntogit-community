# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Campbell <davekong@archlinux.us>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase=python-paste
pkgname=(python-paste python2-paste)
pkgver=2.0.2
pkgrel=2
pkgdesc="Tools for using a Web Server Gateway Interface stack"
arch=('any')
url="http://pythonpaste.org/index.html"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-nose' 'python2-nose')
optdepends=('python2-flup: WSGI utilities'
            'python-openid: Support for OpenID')
source=("http://pypi.python.org/packages/source/P/Paste/Paste-${pkgver}.tar.gz")
md5sums=('4bfc8a7eaf858f6309d2ac0f40fc951c')

prepare() {
  cp -a Paste-${pkgver}{,-py2}

  cd Paste-${pkgver}-py2
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      $(find . -name '*.py')
}

build() {
  cd "$srcdir/Paste-$pkgver"
  python setup.py build

  cd "$srcdir/Paste-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/Paste-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests3 || warning "Tests failed under Python 3"

  cd "$srcdir/Paste-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2
}

package_python-paste() {
  depends=('python-six')

  cd Paste-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/$pkgname/license.txt"
  chmod 644 "${pkgdir}"/usr/lib/python*/site-packages/paste/evalexception/media/MochiKit.packed.js
}

package_python2-paste() {
  depends=('python2-six')

  cd Paste-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 docs/license.txt "${pkgdir}/usr/share/licenses/$pkgname/license.txt"
  chmod 644 "${pkgdir}"/usr/lib/python*/site-packages/paste/evalexception/media/MochiKit.packed.js
}
