# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Campbell <davekong@archlinux.us>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgname=python-paste
pkgver=3.4.4
pkgrel=1
pkgdesc="Tools for using a Web Server Gateway Interface stack"
arch=('any')
url="http://pythonpaste.org/index.html"
license=('MIT')
depends=('python-six')
optdepends=('python-flup: WSGI utilities')
makedepends=('python-setuptools' 'python-six')
checkdepends=('python-pytest-runner' 'python-flup')
source=("https://files.pythonhosted.org/packages/source/P/Paste/Paste-${pkgver}.tar.gz")
sha512sums=('f977fa02b6800be27f703115b87987dcf8a1e4edd681f253b20fb363bfe625109b424ec6a0faba066e1d074f55a2ba336e17654f899b3100c9e3c3252d38b5a6')

build() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py pytest
}

package() {
  cd Paste-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
