# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path.py
pkgver=12.0.2
pkgrel=2
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
depends=('python-importlib-metadata')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/v$pkgver.tar.gz")
sha512sums=('8eb0f3046e8c642356dca9c51e3670475680d641d4d4252cfada48a2b179dbe22d05fa6c15782cd7913e30cb8d522cc681c47cf50e13fe8095f18f047bbf68ac')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path.py-$pkgver
  python setup.py build
}

check() {
  cd path.py-$pkgver
  python setup.py pytest
}

package() {
  cd path.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
