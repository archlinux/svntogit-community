# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-path.py
pkgname=('python-path.py' 'python2-path.py')
pkgver=10.5
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
makedepends=('python-setuptools-scm' 'python2-setuptools-scm')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/$pkgver.tar.gz")
sha512sums=('c8e8af7cf4a1498c8bd2bc0bc067bf79f26455d05968222dc47adb8112922ad1c6b23e40ecf5a196f1d5dc4b6d6172bf5c088abf61255e09a2aa9ee84fed249b')

prepare() {
  cp -a path.py-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py build

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py pytest

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py pytest
}

package_python-path.py() {
  depends=('python')

  cd path.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-path.py() {
  depends=('python2')

  cd path.py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
