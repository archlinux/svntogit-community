# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pathlib2
pkgname=python-$_pkgname
pkgver=2.3.3
pkgrel=2
pkgdesc='Backport of pathlib aiming to support the full stdlib Python API.'
arch=('any')
url='https://github.com/mcmtroffaes/pathlib2'
license=('MIT')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$_pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('c961ff6b5fd247de849e5c0170f9316d10ea5c37ac62841a31a261091ec6868eacd3a4fc35353d8eaa7b6ad88c5e25846c20bb568a3f6041621aab6d03c376a9')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm 644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

# vim: ts=2 sw=2 et:

