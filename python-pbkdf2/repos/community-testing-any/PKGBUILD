# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>

pkgbase=python-pbkdf2
pkgname=('python-pbkdf2' 'python2-pbkdf2')
pkgver=1.3
pkgrel=4
pkgdesc='Password-based key derivation function PBKDF2'
url='https://www.dlitz.net/software/python-pbkdf2/'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'python-crypto'
             'python2' 'python2-setuptools' 'python2-crypto')
checkdepends=('python2-pytest' 'python-pytest')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/dlitz/python-pbkdf2/archive/v${pkgver}.tar.gz)
sha256sums=('f35d36855c082c9cc258da555ea513c800b125c6b5f2fbf9cc4a45655579de5c')
sha512sums=('642372615a2ffc9cf352805789f836c42763c49cbf113d64524a55c6ff8a09f0fda36c92713387f27c2f6a2950ce89ae04dc9f92495cba904400685b148f947e')

prepare() {
  cp -a ${pkgbase}-${pkgver}{,-py2}
  cd ${pkgbase}-${pkgver}-py2
  find . -type f -name "*.py" -exec sed -i 's#/usr/bin/python#/usr/bin/env python2#g' {} +
}

build() {
  msg2 "Building python..."
  (cd ${pkgbase}-${pkgver}
    python setup.py build
  )
  msg2 "Building python2..."
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 "Checking python..."
  (cd ${pkgbase}-${pkgver}
    py.test
  )
  msg2 "Checking python2..."
  (cd ${pkgbase}-${pkgver}-py2
    py.test2
  )
}

package_python-pbkdf2() {
  depends=('python')
  optdepends=('python-crypto: to make use of PyCrypto`s HMAC and SHA')
  cd ${pkgbase}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-pbkdf2() {
  depends=('python2')
  optdepends=('python2-crypto: to make use of PyCrypto`s HMAC and SHA')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
