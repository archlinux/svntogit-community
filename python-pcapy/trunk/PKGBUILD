# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com

_pyname=pcapy
pkgbase=python-pcapy
pkgname=('python-pcapy' 'python2-pcapy')
pkgver=0.11.1
pkgrel=1
pkgdesc='Extension module that interfaces with the libpcap packet capture library'
url='https://github.com/CoreSecurity/pcapy'
arch=('x86_64')
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'libpcap')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/CoreSecurity/pcapy/archive/${pkgver}.tar.gz)
sha512sums=('60eebbeae84da33d7bd2eb2398687007a305e8986fcc183d8061fa50bbc0cdf9a66c17461a32a652ca39952dd0935b164bd98a200f82ffef00ffaa55409522a3')

prepare() {
  sed '/data_files/,/pcapytests/d' -i ${_pyname}-${pkgver}/setup.py
  cp -ra ${_pyname}-${pkgver}{,-py2}
}

build() {
  msg2 "Building python..."
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  msg2 "Building python2..."
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 "Checking python..."
  (cd ${_pyname}-${pkgver}/tests
    local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="../build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python pcapytests.py
  )
  msg2 "Checking python2..."
  (cd ${_pyname}-${pkgver}-py2/tests
    local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="../build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python2 pcapytests.py
  )
}

package_python-pcapy() {
  depends=('python' 'libpcap')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-pcapy() {
  depends=('python2' 'libpcap')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
