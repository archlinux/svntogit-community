# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pdoc
pkgname=('python-pdoc' 'python2-pdoc')
pkgver=0.3.2
pkgrel=3
pkgdesc='A simple program and library to auto generate API documentation for Python modules.'
arch=('any')
url='https://github.com/BurntSushi/pdoc'
license=('custom:Public Domain')
makedepends=('python-mako' 'python2-mako' 'python-markdown' 'python2-markdown' 'git')
source=("git+https://github.com/BurntSushi/pdoc.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i 's/markdown < 2.5/markdown/' pdoc/setup.py

  cp -a pdoc{,-py2}
}

build() {
  cd "$srcdir"/pdoc
  python setup.py build

  cd "$srcdir"/pdoc-py2
  python2 setup.py build
}

package_python-pdoc() {
  depends=('python-mako' 'python-markdown')

  cd pdoc
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 "$pkgdir"/usr/share/pdoc/UNLICENSE "$pkgdir"/usr/share/licenses/$pkgname/UNLICENSE
  rm -r "$pkgdir"/usr/share
}

package_python2-pdoc() {
  depends=('python2-mako' 'python2-markdown')

  cd pdoc-py2
  python2 setup.py install --root="$pkgdir" -O1

  mv "$pkgdir"/usr/bin/pdoc{,2}
  install -Dm644 "$pkgdir"/usr/share/pdoc/UNLICENSE "$pkgdir"/usr/share/licenses/$pkgname/UNLICENSE
  rm -r "$pkgdir"/usr/share
}
