# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: j605

pkgbase='python-perf'
pkgname=('python-perf' 'python2-perf')
pkgver=1.5.1
pkgrel=2
pkgdesc='Toolkit to run Python benchmarks'
url='https://github.com/haypo/perf'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'python-six'
             'python2' 'python2-setuptools' 'python2-six' 'python2-statistics')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/vstinner/perf/archive/${pkgver}.tar.gz)
sha256sums=('0b20382c819114bcbb7b6ff27b1259d44a4b68e5b791324ef9a12e0afaa2e294')
sha512sums=('6a0592a2d5456c0d3bd4f6b09cc9baf74e640eea77fad109c94e9d209f9449d68f3afbdfeb6bd99fafce8d6e9cd6d7bd6e850fb35c0c72d234244c2a5ca35906')

prepare() {
  cp -a perf-${pkgver}{,-py2}
  sed 's|pyperf=|pyperf2=|' -i perf-${pkgver}-py2/setup.py
}

build() {
  (cd perf-${pkgver}
    python setup.py build
  )
  (cd perf-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-perf(){
  depends=('python' 'python-setuptools' 'python-six')
  cd perf-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-perf() {
  depends=('python2' 'python2-setuptools' 'python2-six' 'python2-statistics')
  cd perf-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
