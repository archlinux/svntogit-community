# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.7.0
pkgrel=2
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
depends=('python-cffi' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("https://github.com/zopefoundation/persistent/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('be02fe2f64cf5ee273ac84491c390117c46bdffc4175658df39b68cb7848c2c9cfee9edb6edb17816349a777678905ef792e730b14a56bba0a2a3fba8974b6b0')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=.
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
