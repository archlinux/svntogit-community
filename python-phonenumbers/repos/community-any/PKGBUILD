# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgver=8.13.0
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1f8e78b3fa1418dec1cc89c02a7e8544847f343912be99d2b62aa49250aa0d89d67d27f0f9c40481ef6125872be7cc772de25bdd9ce26df512e7715f4f8281f2')

build() {
  cd python-phonenumbers-$pkgver/python
  python setup.py build
}

check() {
  cd python-phonenumbers-$pkgver/python
  python setup.py test
}

package() {
  cd python-phonenumbers-$pkgver/python
  python setup.py install -O1 --root "$pkgdir"
}
