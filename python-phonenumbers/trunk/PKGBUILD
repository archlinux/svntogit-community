# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-phonenumbers
pkgname=(python-phonenumbers python2-phonenumbers)
pkgver=8.8.10
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
makedepends=("python-setuptools" "python2-setuptools")
source=("$pkgbase-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('e012b78f1c7a135550b3f5ecdc28d33ea0a66d390d03e5ef75535d190481464c60d2dbca9f0521200f5bd53596585d08d428e2c1d0ffda2abd6a85c0d3865f60')

prepare() {
  cp -a python-phonenumbers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py build

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py test

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py test
}

package_python-phonenumbers() {
  depends=('python')

  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}

package_python2-phonenumbers() {
  depends=('python2')

  cd python-phonenumbers-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"
}
