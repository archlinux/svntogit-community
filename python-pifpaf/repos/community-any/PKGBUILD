# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pifpaf
pkgname=('python-pifpaf' 'python2-pifpaf')
pkgver=0.23.0
pkgrel=1
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-cliff'
             'python2-cliff' 'python-stevedore' 'python2-stevedore' 'python-six' 'python2-six'
             'python-fixtures' 'python2-fixtures' 'python-xattr' 'python2-xattr' 'git')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-requests' 'python2-requests'
              'python-testtools' 'python2-testtools' 'python-oslotest' 'python2-oslotest'
              'memcached' 'mongodb' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb')
# 'elasticsearch': test fails
# 'rabbitmq': test hangs
source=("git+https://github.com/jd/pifpaf.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a pifpaf{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/pifpaf
  python setup.py build

  cd "$srcdir"/pifpaf-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pifpaf
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr

  cd "$srcdir"/pifpaf-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHON=python2 python2 setup.py testr
}

package_python-pifpaf() {
  depends=('python-pbr' 'python-cliff' 'python-stevedore' 'python-six' 'python-fixtures'
           'python-xattr')

  cd "$srcdir"/pifpaf
  python setup.py install --root="$pkgdir"/ --optimize=1
}

package_python2-pifpaf() {
  depends=('python2-pbr' 'python2-cliff' 'python2-stevedore' 'python2-six' 'python2-fixtures'
           'python2-xattr')

  cd "$srcdir"/pifpaf-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1

  mv "$pkgdir"/usr/bin/pifpaf{,2}
}

# vim:set ts=2 sw=2 et:
