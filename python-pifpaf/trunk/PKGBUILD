# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pifpaf
pkgname=('python-pifpaf' 'python2-pifpaf')
pkgver=1.9.0
pkgrel=1
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-cliff'
             'python2-cliff' 'python-six' 'python2-six' 'python-daiquiri' 'python2-daiquiri'
             'python-fixtures' 'python2-fixtures' 'python-tenacity' 'python2-tenacity'
             'python-jinja' 'python2-jinja')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-requests' 'python2-requests'
              'python-testtools' 'python2-testtools' 'python-oslotest' 'python2-oslotest'
              'memcached' 'mongodb' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb' 'python-xattr' 'python2-xattr')
# 'elasticsearch': test fails
# 'rabbitmq': test hangs
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz")
sha512sums=('0818b0e4901ea5601e5892ebfb49207bd8f606b792f5e93b13a0e83f223a232eef285e244f977a8007328b070f5eb4e53810efc2cda5a97a31b68ae4d90abb69')

prepare() {
  cp -a pifpaf-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pifpaf-$pkgver
  python setup.py build

  cd "$srcdir"/pifpaf-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr

  cd "$srcdir"/pifpaf-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHON=python2 python2 setup.py testr
}

package_python-pifpaf() {
  depends=('python-pbr' 'python-cliff' 'python-six' 'python-fixtures' 'python-daiquiri'
           'python-tenacity' 'python-jinja')
  optdepends=('python-xattr: for ceph support')

  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pifpaf() {
  depends=('python2-pbr' 'python2-cliff' 'python2-six' 'python2-fixtures' 'python2-daiquiri'
           'python2-tenacity' 'python2-jinja')
  optdepends=('python2-xattr: for ceph support')

  cd pifpaf-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/pifpaf{,2}
}

# vim:set ts=2 sw=2 et:
