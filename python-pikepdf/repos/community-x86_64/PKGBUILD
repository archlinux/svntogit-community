# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=2.2.0
pkgrel=2
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-setuptools)
depends=(python-lxml python-pillow qpdf)
source=($pkgname-$pkgver.tar.gz::"https://github.com/pikepdf/pikepdf/archive/v$pkgver.tar.gz")
sha256sums=('1db63e03a7345354bac6d42b6bd69220dc58d9c4c105cc18471c5acfccea3f62')

build() {
  cd pikepdf-$pkgver
  python setup.py build
}

package() {
  cd pikepdf-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
