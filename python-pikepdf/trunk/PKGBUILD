# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=5.6.1
pkgrel=2
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-build python-installer python-wheel python-toml python-setuptools-scm)
depends=(python-lxml python-pillow python-deprecation python-typing_extensions python-packaging qpdf)
checkdepends=(python-pytest-xdist python-hypothesis)
source=(https://pypi.python.org/packages/source/p/pikepdf/pikepdf-$pkgver.tar.gz
        qpdf-11.0.0.diff::https://github.com/pikepdf/pikepdf/compare/master...feature/qpdf11.patch)
sha256sums=('03fb70ff4fada9cba864d04033f4166b5bf7fa85c90cdc05afd0ab87c9ccaa1a'
            'SKIP')

prepare () {
  cd pikepdf-$pkgver
  # apply qpdf-11 branch fixes:
  # https://github.com/pikepdf/pikepdf/compare/master...feature/qpdf11
  patch -Np1 -i ../qpdf-11.0.0.diff
}

build() {
  cd pikepdf-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd pikepdf-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd pikepdf-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
