# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=1.6.5
pkgrel=2
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-setuptools)
depends=(python-lxml qpdf)
source=($pkgname-$pkgver.tar.gz::"https://github.com/pikepdf/pikepdf/archive/v$pkgver.tar.gz")
sha256sums=('b0523fa9a04982999c1b57e840d1bb831be2a2cc0e737c7b2a40ad36ac23cd61')

build() {
  cd pikepdf-$pkgver
  python setup.py build
}

package() {
  cd pikepdf-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
