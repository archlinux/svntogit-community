# Maintainer: David Runge <dave@sleepmap.de>
_name=pilkit
pkgname=python-pilkit
pkgver=2.0
pkgrel=3
pkgdesc="A collection of utilities for working with PIL (the Python Imaging Library)"
arch=('any')
url="https://github.com/matthewwithanm/pilkit"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-nose' 'python-nose-progressive' 'python-pillow')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7618d7de125f2813b25d707b02b70ca9cb4e19105ed0eb3be3ff6b926a812ed2a609e54f8e86535641fc4baef265fae58d70290c14011eeea744e90066b7bdae')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
