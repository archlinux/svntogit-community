# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgname=python-pillow
pkgver=8.2.0
pkgrel=1
_appname=Pillow
_py3basever=3.9
pkgdesc="Python Imaging Library (PIL) fork."
arch=('x86_64')
url="https://python-pillow.github.io/"
license=('BSD')
depends=('python' 'lcms2' 'libtiff' 'openjpeg2' 'libimagequant' 'libxcb')
optdepends=('freetype2: for the ImageFont module'
            'libraqm: for complex text scripts'
            'libwebp: for webp images'
            'tk: for the ImageTK module'
            'python-olefile: OLE2 file support'
            'python-pyqt5: for the ImageQt module')
makedepends=('python-setuptools' 'freetype2' 'libraqm' 'libwebp' 'tk')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/P/$_appname/$_appname-$pkgver.tar.gz")
md5sums=('21c03274a9f59b9c00419852a8faebe7')

prepare() {
  cd "$srcdir/$_appname-$pkgver"
}

build() {
  cd "$srcdir/$_appname-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/$_appname-$pkgver"

  export PYTHONPATH="$PWD/build/lib.linux-$CARCH-${_py3basever%m}"
  python selftest.py
  pytest
}

package() {
  cd "$srcdir/$_appname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py3basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py3basever/" src/libImaging/*.h
}
