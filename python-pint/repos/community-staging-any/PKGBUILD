# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgbase=python-pint
pkgname=('python-pint' 'python2-pint')
pkgver=0.7.2
pkgrel=2
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="http://pint.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-numpy' 'python2-numpy' 'python-uncertainties' 'python2-uncertainties')
source=("https://pypi.python.org/packages/source/P/Pint/Pint-$pkgver.tar.gz")
md5sums=('1e57f7631bb456cdca53e0bdecbc4456')

prepare() {
  cp -a Pint-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/Pint-$pkgver
  python setup.py test

  cd "$srcdir"/Pint-$pkgver-py2
  python2 setup.py test
}

package_python-pint() {
  depends=('python')

  cd "$srcdir"/Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pint() {
  depends=('python2')

  cd "$srcdir"/Pint-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
