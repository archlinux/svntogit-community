# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgbase=python-pint
pkgname=('python-pint' 'python2-pint')
pkgver=0.8.1
pkgrel=2
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="http://pint.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-numpy' 'python2-numpy' 'python-uncertainties' 'python2-uncertainties')
source=("https://pypi.io/packages/source/P/Pint/Pint-$pkgver.tar.gz")
sha512sums=('e8c33c147e5a86da45d84823ab974070a44c2fa34ea48e81aa07ca6f70804c5a11b316b91c8471b4798ef3ac3ba5488b9c9c6a6ecea5c43f426348430f941cee')

prepare() {
  cp -a Pint-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/Pint-$pkgver
  python setup.py test || warning "https://github.com/hgrecco/pint/issues/577"

  cd "$srcdir"/Pint-$pkgver-py2
  python2 setup.py test || warning "https://github.com/hgrecco/pint/issues/577"
}

package_python-pint() {
  depends=('python')

  cd "$srcdir"/Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pint() {
  depends=('python2')

  cd "$srcdir"/Pint-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
