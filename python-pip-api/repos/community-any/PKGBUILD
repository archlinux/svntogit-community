# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-api
pkgver=0.0.26
pkgrel=1
pkgdesc="An unofficial, importable pip API"
url="https://github.com/di/pip-api"
license=('Apache')
arch=('any')
depends=('python-packaging' 'python-pyparsing' 'python-pip')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pretend' 'python-virtualenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/di/pip-api/archive/$pkgver.tar.gz")
sha512sums=('dc7a2faf1edef618b397ec80354483b3195211f8e3d33d3cf561f1162a42bd9f08c24eab559ce706784423a6e997fdd80298103f1dbd65ffd6fb06d5fe1fc04a')

prepare() {
  cd pip-api-$pkgver
  # Devendor
  sed -i 's/pip_api._vendor.//' pip_api/*.py tests/*.py

  rm -r pip_api/_vendor
}

build() {
  cd pip-api-$pkgver
  python setup.py build
}

check() {
  cd pip-api-$pkgver
  python setup.py pytest
}

package() {
  cd pip-api-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
