# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-shims
pkgver=0.5.1
pkgrel=1
pkgdesc="Compatibility shims for pip versions 8 thru current"
url="https://github.com/sarugaku/pip-shims"
license=('ISC')
arch=('any')
depends=('python-pip' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest-runner' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/pip-shims/archive/$pkgver.tar.gz")
sha512sums=('37abfac3682b4d3f26bb43902be86911a21b0487e275b6ce688aa3696ba4d51748dcba1cdc2743787f244fd6bfbc91e57c9f8ce4ae64e09ecf891f63484337fe')

build() {
  cd pip-shims-$pkgver
  python setup.py build
}

check() {
  cd pip-shims-$pkgver
  python setup.py pytest
}

package() {
  cd pip-shims-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
