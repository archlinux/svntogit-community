# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-shims
pkgver=0.4.0
pkgrel=1
pkgdesc="Compatibility shims for pip versions 8 thru current"
url="https://github.com/sarugaku/pip-shims"
license=('ISC')
arch=('any')
depends=('python-pip' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest-runner' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/pip-shims/archive/$pkgver.tar.gz")
sha512sums=('fdd66f723e5dfaa2db4f7a8fb6de458fa9c0ca2de60f244d2e458f3e5274a00a0a71c1a8563e593c3e96838a4da10686a53850f466ebed4d68e73c0bc22c4c0c')

build() {
  cd pip-shims-$pkgver
  python setup.py build
}

check() {
  cd pip-shims-$pkgver
  python setup.py pytest
}

package() {
  cd pip-shims-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
