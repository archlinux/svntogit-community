# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgname=python-pipenv
pkgver=2018.11.26
pkgrel=3
pkgdesc="Sacred Marriage of Pipfile, Pip, & Virtualenv."
url="https://docs.pipenv.org"
arch=('any')
license=('MIT')
depends=('python' 'python-pip' 'python-pew' 'python-virtualenv'
		 'python-virtualenv-clone' 'python-certifi')
makedepends=('python-setuptools')
#checkdepends=('python-pytest' 'python-pytest-runner'
#	      'python-pip' 'python-certifi' 'python-virtualenv'
#	      'python-virtualenv-clone' 'python-strict-rfc3339'
#	      'python-pytz' 'python-flaky' 'python-mock'
#	      'python2-pytest' 'python2-pytest-runner')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pypa/pipenv/archive/v${pkgver}.tar.gz"
        "0001-setup-don-t-setup_requires-on-things-used-to-run-dev.patch")
sha256sums=('b095395fa6763329fec54c8f6df38c35a39ec3b8b316a4418829e44d50cbc631'
            '878fd681571afe7e9664fcb06828f41e999d33dc31ae3b11cfbd93475fe8516d')

prepare() {
  patch -p1 -d pipenv-$pkgver < 0001-setup-don-t-setup_requires-on-things-used-to-run-dev.patch
}

build(){
  cd "$srcdir/pipenv-$pkgver"
  python setup.py build
}

#check(){
#  cd "$srcdir/pipenv-$pkgver"
#  python setup.py pytest
#
#  cd "$srcdir/pipenv-$pkgver-py2"
#  python2 setup.py pytest
#}

package_python-pipenv() {
  cd "pipenv-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  PIPENV_SHELL=bash python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/pipenv"
  PIPENV_SHELL=zsh  python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_pipenv"
  PIPENV_SHELL=fish python -m pipenv --completion | install -Dm644 /dev/stdin "${pkgdir}/usr/share/fish/vendor_completions.d/pipenv.fish"
}
