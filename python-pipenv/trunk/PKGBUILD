# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Maikel Wever <maikelwever@gmail.com>

pkgbase=python-pipenv
pkgname=(python-pipenv python2-pipenv)
pkgver=11.9.0
pkgrel=2
pkgdesc="Sacred Marriage of Pipfile, Pip, & Virtualenv."
url="https://docs.pipenv.org"
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pypa/pipenv/archive/v${pkgver}.tar.gz")
sha256sums=('fe2a2cdb76ddfaff4d415606ff24425e2323763dd95aeeb049f5187f98620ef7')

prepare() {
  cp -a pipenv-$pkgver{,-py2}
}

build(){
  cd "$srcdir/pipenv-$pkgver"
  python setup.py build

  cd "$srcdir/pipenv-$pkgver-py2"
  python2 setup.py build
}

package_python2-pipenv() {
  depends=('python2' 'python2-pip' 'python2-pew' 'python2-virtualenv'
  	   'python2-virtualenv-clone' 'python2-pathlib' 'python2-ordereddict'
  	   'python2-setuptools' 'python2-certifi')
  cd "pipenv-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/pipenv" "${pkgdir}/usr/bin/pipenv2"
  mv "${pkgdir}/usr/bin/pewtwo" "${pkgdir}/usr/bin/pewtwo2"
  mv "${pkgdir}/usr/bin/pipenv-resolver" "${pkgdir}/usr/bin/pipenv-resolver2"
}

package_python-pipenv() {
  depends=('python' 'python-pip' 'python-pew' 'python-virtualenv'
  	   'python-setuptools' 'python-virtualenv-clone' 'python-certifi')
  cd "pipenv-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

