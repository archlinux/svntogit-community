# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgname=python-pkginfo
pkgver=1.8.2
pkgrel=1
pkgdesc='Query metadatdata from sdists / bdists / installed packages'
arch=('any')
url="https://pypi.python.org/pypi/pkginfo"
license=('PSF')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-nose')
source=("https://pypi.io/packages/source/p/pkginfo/pkginfo-$pkgver.tar.gz")
sha512sums=('06164b6c17000cf25e72d39e4e025a395cdd09afaab6169180cf096bae5a1ff863930d3b368274b6a03458a9ae1c3cb996559c0b042807141435b1868189b5f6')

build() {
  cd pkginfo-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd pkginfo-$pkgver
  python setup.py nosetests
}

package_python-pkginfo() {
  cd pkginfo-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
