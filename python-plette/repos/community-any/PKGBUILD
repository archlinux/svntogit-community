# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plette
pkgver=0.2.3
pkgrel=1
pkgdesc="Structured Pipfile and Pipfile.lock models"
url="https://github.com/sarugaku/plette"
license=('ISC')
arch=('any')
depends=('python-six' 'python-tomlkit')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-cerberus')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/plette/archive/$pkgver.tar.gz")
sha512sums=('e3ebc43357dec17db50f9832ca24093dff9bb82f96c50ab5568ab67e1b92db69b230e9f929ed0c54edc4027be5e2b937ed9f733e490ef97442c6fc23b10f63b6')

build() {
  cd plette-$pkgver
  python setup.py build
}

check() {
  cd plette-$pkgver
  python setup.py pytest
}

package() {
  cd plette-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
