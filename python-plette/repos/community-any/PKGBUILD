# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plette
pkgver=0.4.0
pkgrel=1
pkgdesc="Structured Pipfile and Pipfile.lock models"
url="https://github.com/sarugaku/plette"
license=('ISC')
arch=('any')
depends=('python-tomlkit')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-cerberus')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/plette/archive/v$pkgver.tar.gz")
sha512sums=('b4bd6fac5f51884beadba2cba39265008b72059e2c8db708b3b0be2cd0b367a8f3564baa25674dc1cfbb6d12894a18d716e1d2ddeffb658937b010b28333a1f6')

build() {
  cd plette-$pkgver
  python -m build -nw
}

check() {
  cd plette-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd plette-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
