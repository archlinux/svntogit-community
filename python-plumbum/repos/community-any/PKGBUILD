# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-plumbum
_name=plumbum
pkgver=1.7.1
pkgrel=1
pkgdesc='A small yet feature-rich Python library for shell script-like programs, and more'
url='https://github.com/tomerfiliba/plumbum'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-setuptools-scm')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('d6648acce8e84045cd8c2fc4a90c427fd5ab5a39e49e7a1f10b20ffbca20cc42636183325501e5b577631e4f1c5a1b3e7cae12ec6b89ff853e5a50946c15f3c7')
b2sums=('28d9616c3bcb13f1e9cf9c47b4533c62caf630d7eab998e86023256b568f8f038444f8741bb902ed999f04f480bc09159aab075d184f497b00a452bfac57c996')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  PYTHONPATH=. python -c 'import plumbum'
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
