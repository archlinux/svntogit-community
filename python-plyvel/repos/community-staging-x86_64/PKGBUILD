# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=(python-plyvel python2-plyvel)
pkgver=1.0.4
pkgrel=2
pkgdesc="A fast and feature-rich Python interface to LevelDB"
arch=('x86_64')
license=('BSD')
url="https://github.com/wbolster/plyvel"
makedepends=('cython' 'cython2' 'python-setuptools' 'python2-setuptools' 'leveldb')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/p/plyvel/plyvel-$pkgver.tar.gz")
sha512sums=('ee397d4f7e2dac4e7b382841559895184dc0356b84da4ff4ccdad43c32aebcb1dcc5020230a5564c875ed5d4401cc8b009438ca3b2e0b50e6467e384f0366171')

prepare() {
  # Shipped egg-info has wrong permission etc
  (cd plyvel-$pkgver; make clean)

  cp -a plyvel-$pkgver{,-py2}

  sed -i 's/cython/cython2/' plyvel-$pkgver-py2/Makefile
}

build() {
  cd "$srcdir"/plyvel-$pkgver
  make cython
  python setup.py build

  cd "$srcdir"/plyvel-$pkgver-py2
  make cython2
  python2 setup.py build
}

check() {
  cd "$srcdir"/plyvel-$pkgver
  python setup.py pytest

  cd "$srcdir"/plyvel-$pkgver-py2
  python2 setup.py pytest
}

package_python-plyvel() {
  depends=('leveldb' 'python')

  cd plyvel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-plyvel() {
  depends=('leveldb' 'python2')

  cd plyvel-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
