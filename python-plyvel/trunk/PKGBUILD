# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=(python-plyvel python2-plyvel)
pkgver=0.9
pkgrel=3
pkgdesc="A fast and feature-rich Python interface to LevelDB"
arch=('i686' 'x86_64')
license=('BSD')
url="https://github.com/wbolster/plyvel"
makedepends=('cython' 'cython2' 'python-setuptools' 'python2-setuptools' 'leveldb')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=(https://pypi.python.org/packages/source/p/plyvel/plyvel-$pkgver.tar.gz)
sha256sums=('587d93681ae44936ae086b4b45486eb302e3853ba5af149aac3be9e9713998e9')

prepare() {
  # Shipped egg-info has wrong permission etc
  (cd plyvel-$pkgver; make clean)

  cp -a plyvel-$pkgver{,-py2}

  sed -i 's/cython/cython2/' plyvel-$pkgver-py2/Makefile
}

build() {
  cd "$srcdir/plyvel-$pkgver"
  make cython
  python setup.py build

  cd "$srcdir/plyvel-$pkgver-py2"
  make cython2
  python2 setup.py build
}

check() {
  cd "$srcdir/plyvel-$pkgver"
  python setup.py ptr

  cd "$srcdir/plyvel-$pkgver-py2"
  python2 setup.py ptr
}

package_python-plyvel() {
  depends=('leveldb' 'python')

  cd "$srcdir/plyvel-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

package_python2-plyvel() {
  depends=('leveldb' 'python2')

  cd "$srcdir/plyvel-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}
