# $Id$
# Maintainer: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: Michael Krauss <hippodriver at gmx dot net>
# Contributor: dsa

pkgbase=python-pmw
pkgname=('python2-pmw' 'python-pmw')
pkgver=2.0.1
pkgrel=4
pkgdesc="A toolkit for high-level compound widgets in Python using the Tkinter module"
arch=('any')
url="http://pmw.sourceforge.net/"
license=('MIT')
depends=('python' 'python2' 'tk')
source=(${pkgbase}-${pkgver}.tar.gz::https://pypi.python.org/packages/source/P/Pmw/Pmw-${pkgver}.tar.gz
        "LICENSE.txt")
md5sums=('8080b0fabc731ff236f97e88e13b3938'
         '28b047a148c1780eddcf4bed6b150123')
changelog="ChangeLog"

prepare() {
  cp -r "Pmw-${pkgver}" "Pmw-${pkgver}-py2"
  # change python to python2
  cd "Pmw-${pkgver}-py2"
  find . -name '*.py' -exec \
      sed -i "s|python|python2|g" {} \;
}

build() {
  cd "Pmw-${pkgver}"
  python setup.py build

  cd "../Pmw-${pkgver}-py2"
  python2 setup.py build
}

package_python-pmw() {
  depends=('python' 'tk')

  cd "Pmw-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "${srcdir}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pmw() {
  depends=('python2' 'tk')

  cd "Pmw-${pkgver}-py2"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "${srcdir}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
