# $Id$
# Maintainer: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: Michael Krauss <hippodriver at gmx dot net>
# Contributor: dsa

pkgbase=python-pmw
pkgname=('python2-pmw' 'python-pmw')
pkgver=2.0.0
pkgrel=1
pkgdesc="A toolkit for high-level compound widgets in Python using the Tkinter module"
arch=('i686' 'x86_64')
url="http://pmw.sourceforge.net/"
license=('MIT')
depends=('python' 'python2' 'tk')
source=(${pkgname}-${pkgver}.tar.gz::http://sourceforge.net/projects/pmw/files/Pmw-${pkgver}.tar.gz/download
        "LICENSE.txt")
md5sums=('c7c3f26c4f5abaa99807edefee578fc0'
         '28b047a148c1780eddcf4bed6b150123')

prepare() {
  cp -r "Pmw-${pkgver}" "Pmw-${pkgver}-py2"
  # change python to python2
  cd "${srcdir}/Pmw-${pkgver}-py2"
  sed -i "s|python|python2|g" "Pmw/Pmw_2_0_0/tests/All.py"
  sed -i "s|python|python2|g" "Pmw/Pmw_2_0_0/bin/bundlepmw.py"
  sed -i "s|python|python2|g" "Pmw/Pmw_2_0_0/demos/All.py"
  sed -i "s|python|python2|g" "Pmw/Pmw_1_3_3/tests/ManualTests.py"
  sed -i "s|python|python2|g" "Pmw/Pmw_1_3_3/tests/All.py"
  sed -i "s|python|python2|g" "Pmw/Pmw_1_3_3/bin/bundlepmw.py"
  sed -i "s|python|python2|g" "Pmw/Pmw_1_3_3/demos/All.py"
}

build() {
  cd "${srcdir}/Pmw-${pkgver}"
  python setup.py build

  cd "${srcdir}/Pmw-${pkgver}-py2"
  python2 setup.py build
}

package_python-pmw() {
  depends=('python' 'tk')

  cd "Pmw-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "${srcdir}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pmw() {
  depends=('python2' 'tk')

  cd "Pmw-${pkgver}-py2"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "${srcdir}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
