# $Id$
# Contributor: Esteban V. Carnevale <alfplayer@mailoo.com>

pkgbase=python-polib
pkgname=(python-polib python2-polib)
_pkgname=polib
pkgver=1.0.8
pkgrel=3
pkgdesc='A library to manipulate gettext files'
url='https://pypi.python.org/pypi/polib'
arch=('any')
license=('MIT')
depends=('python' 'python2')
source=("https://pypi.io/packages/source/p/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('5affe3d915eb5b4773f4ce164817e383eea0306115cdaf9b64008b3aea8202df')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
}

package_python-polib() {
  depends=('python')
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-polib() {
  depends=('python2')
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
