# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-positional
pkgname=(python-positional python2-positional)
pkgver=1.1.2
pkgrel=1
pkgdesc="Library to enforce positional or key-word arguments"
arch=('any')
url="https://github.com/morganfainberg/positional"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-wrapt'
             'python2-wrapt')
checkdepends=('python-testrepository' 'python2-testrepository')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/morganfainberg/positional/archive/$pkgver.tar.gz")
sha512sums=('29d6200d6f612daa933c3c99df7829e6a7ab04b278d22a0fd4075a6ad6aaea915a219ae179e904f2fd5d2d26c5cb580c0bd744be2e5ae44454b1af386599d84a')

prepare() {
  cp -a positional-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/positional-$pkgver
  python setup.py build

  cd "$srcdir"/positional-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/positional-$pkgver
  python setup.py testr

  cd "$srcdir"/positional-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-positional() {
  depends=('python-pbr' 'python-wrapt')

  cd "$srcdir"/positional-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-positional() {
  depends=('python2-pbr' 'python2-wrapt')

  cd "$srcdir"/positional-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
