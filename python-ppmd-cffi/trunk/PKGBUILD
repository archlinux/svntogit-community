# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=ppmd-cffi
pkgname=python-ppmd-cffi
pkgver=0.4.1
pkgrel=1
pkgdesc="PPMd compression/decompression library"
arch=('x86_64')
url="https://github.com/miurahr/ppmd"
license=('LGPL')
depends=('python-cffi')
makedepends=('python-setuptools-scm')
checkdepends=('python-hypothesis' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('db4f8d27909ffc32c7f9481b73764d828f6213d92245c668c7eafbd3f15a4ec6')
b2sums=('dd97270b2ce22ddc55020bbf1d8b2ec0681a9c1d9cd626241015bb26c9041d8911065444aa997e20c2822b285f84559f6af8c39f2c9e82dd1d336b9e17f962ce')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # thou shalt not force coverage reports on innocent unittest users
    sed -i '/^addopts/d' tox.ini
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    pythonpaths=("$PWD/build/lib.linux-$CARCH"-3*)
    PYTHONPATH="${pythonpaths[0]}" pytest -rsx
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
