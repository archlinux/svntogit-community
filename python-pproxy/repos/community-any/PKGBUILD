# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-pproxy
pkgver=2.4.6
# The commit that bumps version. Upstream does not use git tags [1], and the
# source tarball on PyPI does not include LICENSE
# https://github.com/qwj/python-proxy/issues/91
_commit=ba9c1557ce71ea6a3136ea8a71bfde80be86d0b5
pkgrel=2
pkgdesc="Versatile TCP/UDP asynchronous tunnel proxy implemented in Python 3 asyncio"
arch=('any')
url="https://github.com/qwj/python-proxy"
license=('MIT')
depends=(python)
makedepends=(python-setuptools)
optdepends=(
  'python-pycryptodome: accelerated cipher operations'
  'python-uvloop: faster asynchronous operations'
  'python-daemon: run the server as a daemon'
)
source=(https://github.com/qwj/python-proxy/archive/$_commit/pproxy-$_commit.tar.gz)
sha256sums=('4258a6d58ab60094be0b1b2c0aca5d862c2189a6396f89435d90e5e35af361a4')

build() {
  cd python-proxy-$_commit
  python setup.py build
}

package() {
  cd python-proxy-$_commit
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
