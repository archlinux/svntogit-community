# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-praw
pkgname=('python-praw' 'python2-praw')
pkgver=4.4.0
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="http://praw.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-prawcore' 'python2-prawcore'
             'python-update-checker' 'python2-update-checker' 'python-pytest-runner'
             'python2-pytest-runner' 'git')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-serializers'
              'python2-betamax-serializers' 'python-betamax-matchers' 'python2-betamax-matchers')
source=("git+https://github.com/praw-dev/praw.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  sed -e 's/betamax-matchers >=0.3.0, <0.4/betamax-matchers >=0.3.0/' \
      -i praw/setup.py
  cp -a praw{,-py2}
}

check() {
  cd "$srcdir"/praw
  python setup.py pytest

  cd "$srcdir"/praw-py2
  python2 setup.py pytest
}

package_python-praw() {
  depends=('python-prawcore' 'python-update-checker')

  cd praw
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-praw() {
  depends=('python2-prawcore' 'python2-update-checker')

  cd praw-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
