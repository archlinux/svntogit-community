# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-praw
pkgname=('python-praw' 'python2-praw')
pkgver=6.0.0
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="https://praw.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-prawcore' 'python2-prawcore'
             'python-update-checker' 'python2-update-checker' 'python-pytest-runner'
             'python2-pytest-runner')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-serializers'
              'python2-betamax-serializers' 'python-betamax-matchers' 'python2-betamax-matchers')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('a276fbd3635618094cdfd8824239630405551a632ec00457636466b9dbfb250e3d0362df82c3034c667a3ede91d15d16baadaad4f9f9dec15a35ef26fe36f974')

prepare() {
  cp -a praw-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/praw-$pkgver
  python setup.py build

  cd "$srcdir"/praw-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/praw-$pkgver
  python setup.py pytest

  cd "$srcdir"/praw-$pkgver-py2
  python2 setup.py pytest
}

package_python-praw() {
  depends=('python-prawcore' 'python-update-checker')

  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-praw() {
  depends=('python2-prawcore' 'python2-update-checker')

  cd praw-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
