# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-praw
pkgname=('python-praw' 'python2-praw')
pkgver=3.4.0
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="http://praw.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator'
             'python-requests' 'python2-requests' 'python-six' 'python2-six'
             'python-update_checker' 'python2-update_checker' 'git')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-matchers' 'python2-betamax-matchers'
              'python-betamax-serializers' 'python2-betamax-serializers')
source=("git+https://github.com/praw-dev/praw.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i 's/mock ==1.0.1/mock/' praw/setup.py
  cp -a praw{,-py2}
}

check() {
  cd "$srcdir/praw"
  python setup.py test

  cd "$srcdir/praw-py2"
  python2 setup.py test
}

package_python-praw() {
  depends=('python-decorator' 'python-requests' 'python-six' 'python-update_checker')

  cd "${srcdir}/praw"
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-praw() {
  depends=('python2-decorator' 'python2-requests' 'python2-six' 'python2-update_checker')

  cd "${srcdir}/praw-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1

  mv "$pkgdir/usr/bin/praw-multiprocess"{,2}
}
