# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-prctl
pkgver=1.7
pkgrel=5
pkgdesc='Python(ic) interface to the linux prctl syscall'
arch=('x86_64')
license=('GPL')
url='https://github.com/seveas/python-prctl'
depends=('python' 'libcap')
checkdepends=('iputils' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/seveas/python-prctl/archive/v$pkgver.tar.gz")
sha512sums=('f2a18fc8e9ef469722c6618d5dd6b7b558d4060783e6fb3a8dc868d9ed3fa21ade371f94a4538a24dca92a71f623a96638c96882d9cf0d3d499a9f12dae8538a')

build() {
  cd python-prctl-$pkgver
  python setup.py build
}

check() {
  cd python-prctl-$pkgver
  python -m unittest discover || echo "Tests failed"
}

package() {
  cd python-prctl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
