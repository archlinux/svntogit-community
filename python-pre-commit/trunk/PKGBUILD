# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pre-commit
pkgver=2.3.0
pkgrel=1
pkgdesc="A framework for managing and maintaining multi-language pre-commit hooks"
url="https://github.com/pre-commit/pre-commit"
license=('MIT')
arch=('any')
depends=('python-cfgv' 'python-identify' 'python-nodeenv' 'python-yaml' 'python-toml'
         'python-virtualenv')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'git' 'go-pie' 'python2' 'rubygems' 'rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pre-commit/pre-commit/archive/v$pkgver.tar.gz"
        pre-commit-pythonpath-fix.patch::https://github.com/pre-commit/pre-commit/pull/1369.patch)
sha512sums=('771defeb4d056e79b4e4dc419564d1b1e9855d1846c0476b76a7a394febf9eaae87e40350be23b4f8ac0952915b68367785e63b862566c3bb375c74bef3f8797'
            '9bafce326138c5e7a5b73a2fd10ac8b3dec2fae4eed93224210cb6f466effa2074dc3dbadeb16d997319b4e2f58f6fef47e05877fbfd06296f6868a4265b0fa7')

prepare() {
  cd pre-commit-$pkgver
  patch -p1 -i ../pre-commit-pythonpath-fix.patch
}

build() {
  cd pre-commit-$pkgver
  python setup.py build
}

check() {
  cd pre-commit-$pkgver
  git config --global user.email "pony@arch"
  git config --global user.name "Lucky Pony"
  git init
  export GIT_AUTHOR_NAME="Lucky Pony"
  export GIT_COMMITTER_NAME="Lucky Pony"
  export GIT_AUTHOR_EMAIL="pony@arch"
  export GIT_COMMITTER_EMAIL="pony@arch"
  export VIRTUALENV_NO_DOWNLOAD=1
  export PRE_COMMIT_NO_CONCURRENCY=1

  # Deselect conda because we don't have it!
  # test_run_a_ruby_hook: https://github.com/pre-commit/pre-commit/issues/1368
  python setup.py pytest --addopts "--deselect tests/repository_test.py::test_conda_hook \
                                    --deselect tests/repository_test.py::test_conda_with_additional_dependencies_hook \
                                    --deselect tests/repository_test.py::test_local_conda_additional_dependencies \
                                    --deselect tests/repository_test.py::test_run_a_ruby_hook"
}

package() {
  cd pre-commit-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
