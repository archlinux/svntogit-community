# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alan <alan1world@googlemail.com>

pkgname=python-prefixed
pkgver=0.7.0
pkgrel=2
pkgdesc='Prefixed alternative numeric library'
arch=(any)
url=https://github.com/Rockhopper-Technologies/prefixed
license=(MPL2)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=15e529d4b5fd943cde4ee80eb5010bdaa8e0611c
source=(git+https://github.com/Rockhopper-Technologies/prefixed.git#tag=${_tag})
b2sums=(SKIP)

build() {
  cd prefixed
  python -m build --wheel --no-isolation
}

package(){
  cd prefixed
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-prefixed/
}

# vim: ts=2 sw=2 et:
