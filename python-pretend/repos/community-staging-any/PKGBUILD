# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pretend
pkgname=('python-pretend' 'python2-pretend')
pkgver=1.0.9
pkgrel=2
pkgdesc="A library for stubbing in Python"
arch=('any')
license=('BSD')
url="https://github.com/alex/pretend"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/alex/pretend/archive/v$pkgver.tar.gz")
sha512sums=('976e3272557aecfb11e4147a868ea96e23546e277d57d726f41b945103bef3451039716e6bbc5082d691fa1fd331c06b3d0f8b254deb0510948153571326eaf0')

prepare() {
  cp -a pretend-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pretend-$pkgver
  python setup.py build

  cd "$srcdir"/pretend-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pretend-$pkgver
  python setup.py pytest

  cd "$srcdir"/pretend-$pkgver-py2
  python2 setup.py pytest
}

package_python-pretend() {
  depends=('python')

  cd pretend-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.rst"
}

package_python2-pretend() {
  depends=('python2')

  cd pretend-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.rst"
}
