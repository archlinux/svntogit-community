# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pretend
pkgname=('python-pretend' 'python2-pretend')
pkgver=1.0.8
pkgrel=4
pkgdesc="A library for stubbing in Python"
arch=('any')
license=('BSD')
url="https://github.com/alex/pretend"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/pretend/pretend-$pkgver.tar.gz")

prepare() {
  cp -a pretend-${pkgver}{,-py2}
}

package_python-pretend() {
  depends=('python')

  cd pretend-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.rst"
}

package_python2-pretend() {
  depends=('python2')

  cd pretend-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.rst "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.rst"
}

sha512sums=('25011216947289d340fb979ebc9e2c7f000571f006cf5ce15f10ff0e013164e4329a3a66933d202acf8ced498929cf9e5a2a3666aafa00f78348400615c5cec8')
