# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgname=prettytable
pkgname=python-prettytable
pkgver=1.0.0
pkgrel=1
pkgdesc="A simple Python library for easily displaying tabular data"
url="https://github.com/jazzband/prettytable"
arch=('any')
license=('BSD')
depends=('python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/p/prettyTable/prettytable-${pkgver}.tar.gz"
        '0001-Fix-column-spacing-with-xterm-reset-escape-sequence.patch')
sha512sums=('2b19d46e375fcc6f6f518d335e5d1e14eac854d0545000581496a13df03292a888ed05e7c9952afccc06706fc5258f7beb27c60291b336d586cebe432b784713'
            '86cf8be475f62b0dfaa2483a37293cefd61e97bfe3c1226076cacc199da69bc5ea7e561bc311dd1b65f808f4fab202a0d38f702568ab7fbc091f285d2e52c924')

export SETUPTOOLSA_SCM_PRETEND_VERSION=$pkgver

prepare() {
  pushd prettytable-$pkgver
  patch -p1 -i "$srcdir"/0001-Fix-column-spacing-with-xterm-reset-escape-sequence.patch
  popd
}

build() {
  cd prettytable-$pkgver
  python setup.py build
}

check() {
  cd prettytable-$pkgver
  pytest
}

package() {
  cd prettytable-$pkgver
  chmod 644 -R prettytable.egg-info
  python setup.py install --root="$pkgdir/"
}
