# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pypiname=process-tests
pkgbase=python-process-tests
pkgname=('python-process-tests' 'python2-process-tests')
pkgver=1.2.0
pkgrel=1
pkgdesc="Tools for testing processes"
arch=('any')
url="https://pypi.python.org/pypi/process-tests"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/p/process-tests/$_pypiname-$pkgver.tar.gz")
md5sums=('21284426c6dbc4319bf0da1839000516')

prepare() {
  cp -a process-tests-$pkgver{,-py2}
}

build() {
  cd "$srcdir/process-tests-$pkgver"
  python setup.py build

  cd "$srcdir/process-tests-$pkgver-py2"
  python2 setup.py build
}

package_python-process-tests() {
  depends=('python')

  cd "$srcdir/$_pypiname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-process-tests() {
  depends=('python2')

  cd "$srcdir/$_pypiname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
