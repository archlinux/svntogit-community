# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-process-tests
pkgname=('python-process-tests' 'python2-process-tests')
pkgver=2.0.0
pkgrel=2
pkgdesc="Tools for testing processes"
arch=('any')
url="https://pypi.python.org/pypi/process-tests"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/process-tests/process-tests-$pkgver.tar.gz")
sha512sums=('456238c9fe29dac2d03acb4f8b26d3801cf3cf997210a5de6634d9a86e8b6f2d29db4042ccc1721caa880b31f8d4131d5c121bab84033625a9ce0e80fd379f8a')

prepare() {
  cp -a process-tests-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/process-tests-$pkgver
  python setup.py build

  cd "$srcdir"/process-tests-$pkgver-py2
  python2 setup.py build
}

package_python-process-tests() {
  depends=('python')

  cd process-tests-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-process-tests() {
  depends=('python2')

  cd process-tests-$pkgver
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
