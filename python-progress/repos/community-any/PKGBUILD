# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Guillaume Brogi <gui-gui at netcourrier dot com>

_pkgname=progress
pkgbase=python-progress
pkgname=('python-progress' 'python2-progress')
pkgver=1.6
pkgrel=1
pkgdesc="Easy to use progress bars for Python"
arch=('any')
url="https://github.com/verigak/${_pkgname}"
license=('ISC')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('e4a0d9e4370cd09f29ecdf72bf84c7177caef8374a90a627c83a84e619eb0f3d')

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python test_progress.py
    python2 test_progress.py
}

package_python-progress() {
    depends=('python')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-progress() {
    depends=('python2')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
