# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-progressbar
pkgname=('python-progressbar' 'python2-progressbar')
pkgver=2.3
pkgrel=1
arch=('any')
url='http://code.google.com/p/python-progressbar/'
license=('LGPL')
makedepends=('python' 'python-distribute' 'python2' 'python2-distribute')
source=("http://python-progressbar.googlecode.com/files/progressbar-$pkgver.tar.gz")
md5sums=('4f904e94b783b4c6e71aa74fd2432c59')

package_python-progressbar() {
  pkgdesc='Text progressbar library for python'
  depends=('python')
  cd progressbar-$pkgver
  python setup.py install --root="$pkgdir"
}

package_python2-progressbar() {
  pkgdesc='Text progressbar library for python2'
  depends=('python2')
  cd progressbar-$pkgver
  python2 setup.py install --root="$pkgdir"
  sed -i '1s,^#!/usr/bin/python$,#!/usr/bin/python2,' \
    "$pkgdir"/usr/lib/python2.7/site-packages/progressbar/*.py
}

# vim:set ts=2 sw=2 ft=sh et:
