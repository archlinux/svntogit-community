# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-proxy.py
_name=${pkgname#python-}
pkgver=2.4.0
pkgrel=2
pkgdesc='Lightweight HTTP, HTTPS, HTTP2 and WebSockets proxy server'
arch=('any')
url='https://github.com/abhinavsingh/proxy.py'
license=('BSD')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm'
             'python-setuptools-scm-git-archive' 'python-wheel')
checkdepends=('python-httpx' 'python-pytest' 'python-pytest-asyncio' 'python-pytest-cov' 'python-pytest-mock')
optdepends=('python-httpx: Cloudflare DNS resolver plugin support'
            'python-paramiko: proxy over SSH tunnel support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz"
        "python-proxy.py-2.4.0_fix-asyncio-deprecationwarning.patch::$url/commit/e0cc90d057b44ef506b88362d71f2c707db11db9.patch")
sha256sums=('cc5d1b5655ef109297f9679c9c6a9c12c13889540ee6c70619308adcb632331b'
            'd7ffe35494b1943b41b4c8285043626c46323a706d99ceaf1ca268c6b91db5eb')

prepare() {
	cd "$_name-$pkgver"
	# Fix pytest-asyncio 0.17.0 DeprecationWarning (https://github.com/abhinavsingh/proxy.py/pull/998)
	patch --forward --strip=1 --input="$srcdir/python-proxy.py-2.4.0_fix-asyncio-deprecationwarning.patch"
}

build() {
	cd "$_name-$pkgver"
	python -m build --wheel --no-isolation
}

check() {
	cd "$_name-$pkgver"
	pytest
}

package() {
	cd "$_name-$pkgver"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"

	# Remove entry point usr/bin/proxy to avoid conflict with libproxy (FS#73325)
	rm -r "$pkgdir/usr/bin"
}
