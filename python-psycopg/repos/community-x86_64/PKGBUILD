# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-psycopg
pkgver=3.1.8
pkgrel=1
pkgdesc='PostgreSQL database adapter for Python'
arch=('x86_64')
url='https://www.psycopg.org/psycopg3/'
license=('LGPL3')
depends=('python' 'postgresql-libs')
makedepends=(
  'git'
  'cython'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
)
checkdepends=('python-pytest' 'python-pytest-asyncio')
_commit='1ec7ed57f3c13a7bce3b817168325b30dbc35e6b'
source=("$pkgname::git+https://github.com/psycopg/psycopg.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build(){
  cd "$pkgname"

  for subpackage in psycopg psycopg_c; do
    pushd "$subpackage"
    python -m build --wheel --no-isolation
    popd
  done
}

package(){
  cd "$pkgname"

  for subpackage in psycopg psycopg_c; do
    pushd "$subpackage"
    python -m installer --destdir="$pkgdir" dist/*.whl 
    popd
  done
}
