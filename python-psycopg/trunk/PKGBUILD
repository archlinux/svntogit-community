# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-psycopg
pkgver=3.1
pkgrel=1
pkgdesc='PostgreSQL database adapter for Python'
arch=('x86_64')
url='https://www.psycopg.org/psycopg3/'
license=('LGPL3')
depends=('python' 'postgresql-libs')
makedepends=(
  'git'
  'cython'
  'python-setuptools'
  'python-wheel'
  'python-pip'
)
checkdepends=('python-pytest' 'python-pytest-asyncio')
_commit='d8ede0ff2920a9a08ceed07ae4b07a05db903135'
source=("$pkgname::git+https://github.com/psycopg/psycopg.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"
}

build(){
  cd "$pkgname"

  for subpackage in psycopg psycopg_c
  do
    python "$subpackage/setup.py" build
  done
}

package(){
  cd "$pkgname"

  for subpackage in psycopg psycopg_c
  do
    python "$subpackage/setup.py" install --root="$pkgdir" --optimize=1 --skip-build
  done
}
