# $Id$
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-psycopg1
pkgname=python2-psycopg1
pkgver=1.1.21
pkgrel=9
pkgdesc="A PostgreSQL database adapter for Python"
arch=('x86_64')
url="http://initd.org/projects/psycopg1"
license=('GPL')
depends=('python2-egenix-mx-base' 'postgresql-libs')
makedepends=('postgresql')
conflicts=('python-psycopg1<=1.1.21-7')
replaces=('python-psycopg1<=1.1.21-7')
source=(http://initd.org/psycopg/tarballs/PSYCOPG-1-1/psycopg-$pkgver.tar.gz)
md5sums=('a31f79f68d6d32898d6f24e11369a106')
sha1sums=('02d8a8b5dd682306117896c7c63a9a1b518997b6')

build() {
  cd "$srcdir/psycopg-$pkgver"

  ./configure --prefix=/usr --with-python=/usr/bin/python2 \
              --with-mxdatetime-includes=/usr/lib/python2.7/site-packages/mx/DateTime/mxDateTime
  sed -e 's:\(echo "  install -m 555 $$mod \)\($(PY_MOD_DIR)\)\("; \\\):\1${DESTDIR}\2/$$mod\3:' \
		-e 's:\($(INSTALL)  -m 555 $$mod \)\($(PY_MOD_DIR)\)\(; \\\):\1${DESTDIR}\2/$$mod\3:' \
		-i Makefile
  make
}

package_python2-psycopg1() {
  cd "$srcdir/psycopg-$pkgver"

  make DESTDIR="$pkgdir" install
}
