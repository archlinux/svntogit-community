# $Id$
# Maintainer: Morten Linderud <foxboron@archlinux.org> 
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=python-psycopg2
pkgname=('python-psycopg2' 'python2-psycopg2')
pkgver=2.7.5
pkgrel=2
pkgdesc="A PostgreSQL database adapter for the Python programming language."
arch=('x86_64')
url="http://initd.org/psycopg/"
license=('LGPL3')
makedepends=('python2' 'python2-setuptools' 
             'python' 'python-setuptools' 'postgresql-libs>=8.4.1')
validpgpkeys=('8AD609956CF1899418E19A856013BD3AFCF957DE')
source=(http://initd.org/psycopg/tarballs/PSYCOPG-2-7/psycopg2-$pkgver.tar.gz{,.asc})
sha256sums=('eccf962d41ca46e6326b97c8fe0a6687b58dfc1a5f6540ed071ff1474cea749e'
            'SKIP')
prepare() {
  cp -a psycopg2-$pkgver{,-py2}
}

build(){
  cd "$srcdir/psycopg2-$pkgver"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
  python setup.py build

  cd "$srcdir/psycopg2-$pkgver-py2"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
  python2 setup.py build
}

package_python-psycopg2() {
  depends=('python' 'postgresql-libs>=8.4.1')

  cd "$srcdir/psycopg2-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-psycopg2() {
  depends=('python2' 'postgresql-libs>=8.4.1')

  cd "$srcdir/psycopg2-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
