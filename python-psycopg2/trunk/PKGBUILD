# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=python-psycopg2
pkgname=('python-psycopg2' 'python2-psycopg2')
pkgver=2.4.5
pkgrel=2
pkgdesc='PostgreSQL database adapter for the Python programming language'
arch=('i686' 'x86_64')
url="http://initd.org/psycopg/"
license=('LGPL3')
makedepends=('python2' 'python' 'postgresql-libs>=8.4.1')
source=(http://initd.org/psycopg/tarballs/PSYCOPG-2-4/psycopg2-$pkgver.tar.gz)
md5sums=('075e4df465e9a863f288d5bdf6e6887e')

build() {
  cd "$srcdir/psycopg2-$pkgver"
  sed -i 's/,PSYCOPG_DEBUG$//' setup.cfg
}

package_python-psycopg2() {
  depends=('python' 'postgresql-libs>=8.4.1')

  cd "$srcdir/psycopg2-$pkgver"
  python setup.py install --root="$pkgdir"
}

package_python2-psycopg2() {
  depends=('python2' 'postgresql-libs>=8.4.1')

  cd "$srcdir/psycopg2-$pkgver"
  python2 setup.py install --root="$pkgdir"
}
