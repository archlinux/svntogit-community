# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ptable
pkgver=0.9.2.20180730
_commit=bcfdb92811ae1f39e1065f31544710bf87d3bc21
pkgrel=1
pkgdesc="A simple Python library for easily displaying tabular data in a visually appealing ASCII table format"
url="https://github.com/kxxoling/PTable"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$_commit.tar.gz::https://github.com/kxxoling/PTable/archive/$_commit.tar.gz")
sha512sums=('1f066c85b61d0b370c4da509db2e07d40b573b7f3c4388036dee2fc03c82bcaecbb2871fca1c7760479e072b172bf0ace0543caec38f0d7a58322bb4b4fafd20')

build() {
  cd PTable-$_commit
  python setup.py build
}

check() {
  cd PTable-$_commit
  python setup.py nosetests
}

package() {
  cd PTable-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
