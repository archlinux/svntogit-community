# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastien Binet <binet@lblbox>

pkgbase=python-ptrace
pkgname=('python-ptrace' 'python2-ptrace')
pkgver=0.9.3
pkgrel=2
pkgdesc='Python binding of ptrace library to trace processes and syscalls'
url='https://github.com/haypo/python-ptrace'
arch=('any')
license=('GPL2')
makedepends=('python' 'python-sphinx'
             'python2' 'python2-sphinx')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/haypo/python-ptrace/archive/${pkgver}.tar.gz)
sha512sums=('8d033bfc30ebfb74cc8ff862f7de1144261dc1ab4e2bf74b729aa756f5d20c85bc87825dfc0f5ce1084795031141b4e9170930e41355f966ce2f9c6a95270a86')

prepare(){
  cp -a ${pkgbase}-${pkgver}{,-py2}
}

build() {
  msg "Building python..."
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py build
  make -j1 -C doc man text SPHINXBUILD=sphinx-build

  msg "Building python2..."
  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py build
  make -j1 -C doc man text SPHINXBUILD=sphinx-build2
}

check() {
  msg "Checking python..."
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python runtests.py

  msg "Checking python2..."
  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 runtests.py
}

package_python-ptrace() {
  depends=('python')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 doc/build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/build/man/${pkgbase}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-ptrace() {
  depends=('python2')
  cd "${srcdir}/${pkgbase}-${pkgver}-py2"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 doc/build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/build/man/${pkgbase}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  # Rename gdb and strace scripts to avoid names conflict
  mv "${pkgdir}/usr/bin/gdb.py" "${pkgdir}/usr/bin/gdb2.py"
  mv "${pkgdir}/usr/bin/strace.py" "${pkgdir}/usr/bin/strace2.py"
}

# vim: ts=2 sw=2 et:
