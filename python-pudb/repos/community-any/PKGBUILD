# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgbase=python-pudb
pkgname=(python-pudb python2-pudb)
pkgver=2014.1
pkgrel=1
pkgdesc="A full-screen, console-based Python debugger"
url="http://pypi.python.org/pypi/pudb"
arch=('any')
license=('MIT')
makedepends=('python-urwid' 'python-pygments' 'python2-urwid' 'python2-pygments')
source=("http://pypi.python.org/packages/source/p/pudb/pudb-${pkgver}.tar.gz")
md5sums=('8104db184c9553c568966cd67a7b1b2f')

prepare() {
  cp -a pudb-$pkgver{,-py2}

  sed -i 's,^#! /usr/bin/env python$,#! /usr/bin/env python2,' pudb-$pkgver-py2/pudb/debugger.py
}

package_python-pudb() {
  depends=('python-urwid' 'python-pygments')

  cd "pudb-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pudb() {
  depends=('python2-urwid' 'python2-pygments')

  cd "pudb-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: set ft=sh ts=2 sw=2 et:
