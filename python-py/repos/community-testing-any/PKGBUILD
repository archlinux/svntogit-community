# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.5.4
pkgrel=2
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="http://py.readthedocs.io/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
             'python2-setuptools-scm' 'python-iniconfig' 'python2-iniconfig'
             'python-apipkg' 'python2-apipkg')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/py/archive/$pkgver.tar.gz")
sha512sums=('e0d4ed98d8ccf1a126c69486a379221aec461a4a5d88564e3f656d7e30dd747583b8f1ed65a8b5344887617049433baa510f158ffbabe1dd2ef48600456221a3')

prepare() {
  rm -r py-$pkgver/py/_vendored_packages
  cp -a py-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/py-$pkgver
  python setup.py build

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py build
}

check() {
  # Known failure: https://github.com/pytest-dev/py/issues/104

  cd "$srcdir"/py-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-py() {
  depends=('python-iniconfig' 'python-apipkg')

  cd py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-py() {
  depends=('python2-iniconfig' 'python2-apipkg')

  cd py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
