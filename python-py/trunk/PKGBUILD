# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.4.19
pkgrel=1
pkgdesc="rapid testing and development utils"
arch=('any')
license=('MIT')
url="http://pylib.org/"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/py/py-$pkgver.tar.gz")

prepare() {
  cp -r py-${pkgver} python2-py-${pkgver}
}

package_python-py() {
  depends=('python')

  cd py-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-py() {
  depends=('python2')

  cd python2-py-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

sha512sums=('4be2a790d8ea7ea35f910b823ac421e480554ce0a7e4532dc730f26e653f407fa12fbf3106ca29ee0fd105704b75744fd2395416186abdd335691fba241fa6a1')
