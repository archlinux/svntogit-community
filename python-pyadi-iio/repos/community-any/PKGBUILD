# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pyadi-iio
pkgname=python-$_pkgname
pkgver=0.0.8
pkgrel=2
pkgdesc='Python interfaces for ADI hardware with IIO drivers'
arch=('any')
url='https://github.com/analogdevicesinc/pyadi-iio'
license=('custom')
depends=('libiio' 'python-numpy' 'python-paramiko')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('python-pytest-runner' 'python-scapy')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('97f6b241230f54ff4932f677b42b4fd33c2e2eb69bb27d638b4c63b44e991b61c40744b9db09fd26d7973e83eeec6089cba65bbe63894206b06c3e10dd8f1f3e')

build() {
  cd $_pkgname-$pkgver

  python setup.py build

  cd doc

  make html
}

# need liblibc.a
#check() {
#  cd $_pkgname-$pkgver
#
#  python setup.py pytest
#}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  # Remove tests
  rm -rf "$pkgdir"/usr/lib/python*/site-packages/test

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/build/html "$pkgdir"/usr/share/doc/$pkgname

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
