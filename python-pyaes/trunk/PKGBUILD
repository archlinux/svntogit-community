# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgbase=python-pyaes
pkgname=('python-pyaes' 'python2-pyaes')
_pipname=pyaes
pkgver=1.6.1
pkgrel=2
pkgdesc="Pure-Python Implementation of the AES block-cipher and common modes of operation"
arch=('any')
makedepends=('python2-setuptools' 'python-setuptools')
url="https://github.com/ricmoo/pyaes"
license=('MIT')
source=(https://pypi.org/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('02c1b1405c38d3c370b085fb952dd8bea3fadcee6411ad99f312cc129c536d8f')


prepare() {
  cp -R $_pipname-$pkgver python2-$_pipname-$pkgver
}

build() {
  cd "$_pipname-$pkgver"
  python setup.py build

  cd "../python2-$_pipname-$pkgver"
  python2 setup.py build
}


package_python-pyaes() {
  depends=('python')
  cd $_pipname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1

  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}

package_python2-pyaes() {
  depends=('python2')
  cd "python2-$_pipname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
