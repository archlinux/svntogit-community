# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyalsa
pkgname=python-pyalsa
pkgver=1.2.6
pkgrel=2
pkgdesc="Python binding for the ALSA library"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
groups=(alsa)
depends=(alsa-lib python)
makedepends=(python-setuptools)
source=(
  "https://www.alsa-project.org/files/pub/${_name}/${_name}-${pkgver}.tar.bz2"{,.sig}
)
sha512sums=('11b8eed822e0f8acc96ff0e10eeec0ba26b2997c7a0000b1d82561661f59931cd6b6aa6397b46c5944a0853344517878ede6c9ba13bea241cefc72b72bf1d9ff'
            'SKIP')
b2sums=('d142d5818527b30b333d2552045892854df25eb14a7983320fe56e30a6239d3fe947ef1f0e1202ed3b5aad7722002e0e5e82ab9f89564bb9640aca2dc03c7887'
        'SKIP')
validpgpkeys=(F04DF50737AC1A884C4B3D718380596DA6E59C91)  # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
}
