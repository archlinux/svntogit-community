# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyaml
pkgname=(python-pyaml python2-pyaml)
pkgver=19.4.1
pkgrel=3
pkgdesc="PyYAML-based module to produce pretty and readable YAML-serialized data"
url="https://pypi.python.org/pypi/pyaml"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml')
checkdepends=('python-unidecode' 'python2-unidecode')
source=("https://pypi.io/packages/source/p/pyaml/pyaml-$pkgver.tar.gz")
sha512sums=('85efae2ef2899cbb2d469175f014f9a0e6186fff0106dbba2b5960d83552ff0ab4a014d62a11feca7f4520fe297bebc61e876b46306456c26326c21ace469196')

prepare() {
  cp -a pyaml-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyaml-$pkgver
  python setup.py build

  cd "$srcdir"/pyaml-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyaml-$pkgver
  python setup.py test

  cd "$srcdir"/pyaml-$pkgver-py2
  python2 -m unittest pyaml.tests.dump
}

package_python-pyaml() {
  depends=('python-yaml')
  optdepends=('python-unidecode: Needed if same-id objects or recursion is used within serialized data')

  cd pyaml-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-pyaml() {
  depends=('python2-yaml')
  optdepends=('python2-unidecode: Needed if same-id objects or recursion is used within serialized data')

  cd pyaml-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
