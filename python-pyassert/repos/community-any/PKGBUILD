# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pyassert
pkgname=('python-pyassert' 'python2-pyassert')
pkgver=0.4.2
pkgrel=1
pkgdesc='https://github.com/pyclectic/pyassert'
arch=('any')
url='http://pyassert.com'
license=('Apache')
makedepends=('python-pybuilder' 'python2-pybuilder' 'python-mockito' 'python2-mockito'
             'python-coverage' 'python2-coverage' 'python-six' 'python2-six' 'git')
checkdepends=('python-unittest-xml-reporting' 'python2-unittest-xml-reporting' 'flake8'
              'python2-flake8' 'pychecker') # python3-pychecker is missing
source=("git+https://github.com/pyclectic/pyassert.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  # package name will be determined by directory name
  mkdir py2
  cp -a pyassert py2/
}

check() {
  cd "$srcdir"/pyassert
  pyb -v analyze || warning "ignoring flake8 warnings"

  cd "$srcdir"/py2/pyassert
  pyb2 -v analyze || warning "ignoring flake8 warnings"
}

package_python-pyassert() {
  depends=('python-six')

  cd pyassert
  pyb -v package
  cd target/dist/pyassert-$pkgver
  python setup.py install --root="$pkgdir" -O1
}

package_python2-pyassert() {
  depends=('python2-six')

  cd py2/pyassert
  pyb2 -v package
  cd target/dist/pyassert-$pkgver
  python2 setup.py install --root="$pkgdir" -O1
}
