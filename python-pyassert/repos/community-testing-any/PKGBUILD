# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pyassert
pkgver=0.4.2
pkgrel=5
pkgdesc='Rich assertions library for Python'
arch=('any')
url='https://github.com/pyclectic/pyassert'
license=('Apache')
depends=('python-six')
makedepends=('python-pybuilder' 'python-mockito' 'python-coverage' 'python-unittest-xml-reporting'
             'flake8' 'pychecker')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyclectic/pyassert/archive/v$pkgver.tar.gz")
sha512sums=('5a7a8078dad967913708f0bc18278ba760bbc098c1fc6f299b6a654206834dbebc82125c33456e498e5edfbf15599360c3cff807df56073eaa62330b6b494f09')

prepare() {
  # Pybuilder uses directory name to determine distribution name
  mv pyassert-$pkgver pyassert

  # BUILD FAILED - Missing plugin dependency 'pychecker':
  #   pip._internal.exceptions.DistributionNotFound:
  #     No matching distribution found for pychecker
  sed -i '/use_plugin("python.pychecker")/d' pyassert/build.py
}

check() {
  cd pyassert
  pyb -v analyze || warning "ignoring flake8 warnings"
}

package() {
  cd pyassert
  pyb -v package
  cd target/dist/pyassert-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
