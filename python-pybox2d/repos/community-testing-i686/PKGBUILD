# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgbase=python-pybox2d
pkgname=('python2-pybox2d')
pkgver=2.3.0
pkgrel=2
epoch=1
arch=('x86_64' 'i686')
url='https://github.com/pybox2d/pybox2d'
license=('GPL')
makedepends=('git' 'python2' 'swig')
source=("$pkgbase::git://github.com/pybox2d/pybox2d.git#commit=9c21fcc9a3")
md5sums=('SKIP')

build() {
  cd "$pkgbase"

  python2 setup.py setopt --command=build_ext --option=swig --set-value=swig
  python2 setup.py build
}

package_python2-pybox2d() {
  depends=('python2')
  pkgdesc='Python wrapper for Box2D'

  cd "$pkgbase"

  python2 setup.py install --root="$pkgdir" --optimize=1

  # Fixes the warning about a missing dependency on python
  sed -i 's:python:python2:g' \
    "$pkgdir/usr/lib/python2.7/site-packages/Box2D/__init__.py"
}

# vim: sw=2 ts=2 et:
