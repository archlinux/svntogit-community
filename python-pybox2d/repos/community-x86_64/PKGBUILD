# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

_pkgbase=pybox2d
pkgbase=python-pybox2d
pkgname=('python-pybox2d' 'python2-pybox2d')
pkgver=2.3.2
pkgrel=2
epoch=1
pkgdesc='Python wrapper for Box2D'
arch=('x86_64')
url='https://github.com/pybox2d/pybox2d'
license=('custom')
makedepends=('python-setuptools' 'python2-setuptools' 'swig')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pybox2d/$_pkgbase/archive/$pkgver.tar.gz")
sha256sums=('ca7111557df3ecda3cb48b6c5fb3290fa8b45b6226a34867d2a85cbee5747266')

prepare() {
	cp -a $_pkgbase-$pkgver{,-python2}
}

build() {
	cd $_pkgbase-$pkgver
	python setup.py setopt --command=build_ext --option=swig --set-value=swig
	python setup.py build

	cd ../$_pkgbase-$pkgver-python2
	python2 setup.py setopt --command=build_ext --option=swig --set-value=swig
	python2 setup.py build
}

package_python-pybox2d() {
	depends=('python')

	cd $_pkgbase-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pybox2d() {
	depends=('python2')

	cd $_pkgbase-$pkgver-python2
	python2 setup.py install --root "$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
