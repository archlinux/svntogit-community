# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pybuilder
pkgver=0.11.17
pkgrel=3
pkgdesc='An extensible, easy to use continuous build tool for Python'
arch=('any')
url='http://pybuilder.github.io'
license=('Apache')
# added dependencies of distutils plugin into pybuilder's depends
depends=('python-pip' 'python-pypandoc' 'python-tailer' 'python-tblib' 'python-twine' 'python-wheel')
makedepends=('python-cram' 'flake8' 'python-sphinx' 'python-pdoc' 'python-coverage'
             'python-pytddmon' 'python-unittest-xml-reporting' 'python-pyfix')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pybuilder/pybuilder/archive/v$pkgver.tar.gz")
sha512sums=('586018833a53bc28bc996d9b9117417f8fde8a1677d5914bb6536d073b39e63aef6fb61925360a99d48af1dbe6eece4a3ed6209f80346e8fc1c7ec0c00ffb793')

prepare() {
  cd pybuilder-$pkgver
  sed -i 's/"install_dependencies", //' setup.py
  sed -i 's/project.plugin_depends_on("pypandoc", ".*$/project.plugin_depends_on("pypandoc")/' src/main/python/pybuilder/plugins/python/distutils_plugin.py
}

check() {
  cd pybuilder-$pkgver
  python build.py -v analyze || warning "ignoring flake8 warnings"
}

package() {
  cd pybuilder-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
