# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pybuilder
pkgname=('python-pybuilder' 'python2-pybuilder')
pkgver=0.11.9
_commit=712c28954dbda6efb11293cc685a57e2a10c67da
pkgrel=1
pkgdesc='An extensible, easy to use continuous build tool for Python'
arch=('any')
url='http://pybuilder.github.io'
license=('Apache')
makedepends=('python-pip' 'python2-pip' 'python-tblib' 'python2-tblib' 'python-wheel'
             'python2-wheel' 'python2-mock' 'python-cram' 'python2-cram' 'flake8' 'python2-flake8'
             'python-sphinx' 'python2-sphinx' 'python-pdoc' 'python2-pdoc' 'python-coverage'
             'python2-coverage' 'python-pytddmon' 'python2-pytddmon' 'python2-snakefood'
             'python-unittest-xml-reporting' 'python2-unittest-xml-reporting' 'git')
checkdepends=('python-pyfix' 'python2-pyfix')
source=("git+https://github.com/pybuilder/pybuilder.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  sed -i 's/"install_dependencies", //' pybuilder/setup.py

  cp -a pybuilder{,-py2}
}

check() {
  cd "$srcdir"/pybuilder
  python build.py -v analyze || warning "ignoring flake8 warnings"

  cd "$srcdir"/pybuilder-py2
  python2 build.py -v analyze || warning "ignoring flake8 warnings"
}

package_python-pybuilder() {
  depends=('python-pip' 'python-tblib' 'python-wheel')

  cd pybuilder
  python setup.py install --root="$pkgdir" -O1
}

package_python2-pybuilder() {
  depends=('python2-pip' 'python2-tblib' 'python2-wheel')

  cd pybuilder-py2
  python2 setup.py install --root="$pkgdir" -O1

  mv "$pkgdir"/usr/bin/pyb{,2}
  mv "$pkgdir"/usr/bin/pyb_{,2}
}
