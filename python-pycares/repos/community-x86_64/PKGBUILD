# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pycares
pkgver=3.1.1
pkgrel=1
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('python-cffi') # c-ares is bundled
makedepends=('python-setuptools')
#checkdepends=('python-idna')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/p/pycares/pycares-$pkgver.tar.gz")
sha256sums=('18dfd4fd300f570d6c4536c1d987b7b7673b2a9d14346592c5d6ed716df0d104')

build() {
  cd pycares-$pkgver
  python setup.py build
}

# test_query_any fails https://github.com/saghul/pycares/issues/108
#check() {
#  cd pycares-$pkgver
#  mkdir -p temp
#  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
#  python setup.py install --skip-build --root=temp
#  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" python tests/tests.py
#}

package() {
  cd pycares-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
