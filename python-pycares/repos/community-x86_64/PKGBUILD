# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pycares
pkgver=4.0.0
pkgrel=1
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('c-ares' 'python-cffi')
makedepends=('python-setuptools')
#checkdepends=('python-idna')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/p/pycares/pycares-$pkgver.tar.gz")
sha256sums=('d0154fc5753b088758fbec9bc137e1b24bb84fc0c6a09725c8bac25a342311cd')
b2sums=('cf83efcd3b7f0c32dfd013c4211ae0bce2cbc8cfa49f295239f315b3217d17c2cf1f6e491c41b4b3ca4de8762405f560a62d1ef6252b961bc94fed2253b6a632')

build() {
  cd pycares-$pkgver
  PYCARES_USE_SYSTEM_LIB=1 python setup.py build
}

# Test failures: test_custom_resolvconf, test_getaddrinfo{,2..4},
#                gethostbyaddr{,6}, gethostbyname{,_small_timeout},
#                test_getnameinfo
#check() {
#  cd pycares-$pkgver
#  mkdir -p temp
#  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
#  python setup.py install --skip-build --root=temp
#  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" python tests/tests.py
#}

package() {
  cd pycares-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
