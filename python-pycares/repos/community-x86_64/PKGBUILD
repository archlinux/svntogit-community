# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pycares
pkgver=4.2.1
pkgrel=1
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('c-ares' 'python-cffi')
makedepends=('python-setuptools')
checkdepends=('python-idna')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/p/pycares/pycares-$pkgver.tar.gz")
sha256sums=('735b4f75fd0f595c4e9184da18cd87737f46bc81a64ea41f4edce2b6b68d46d2')
b2sums=('62caa1d40c936354aaa519ec5214f31f6ccf69724624d443c76d036e9c5c7a260854d203c66b718ef2c3ba82ad280079f46e969e12443800ce11d8f273e7ba56')

build() {
  cd pycares-$pkgver
  PYCARES_USE_SYSTEM_LIB=1 python setup.py build
}

# Test failures: test_custom_resolvconf, gethostbyaddr{,6}, test_getnameinfo
#check() {
#  cd pycares-$pkgver
#  mkdir -p temp
#  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
#  python setup.py install --root=temp --optimize=1 --skip-build
#  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" python tests/tests.py
#}

package() {
  cd pycares-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
