# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=pycares
pkgname=python-pycares
pkgver=4.2.2
pkgrel=1
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('c-ares' 'python-cffi')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-idna')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('e1f57a8004370080694bd6fb969a1ffc9171a59c6824d54f791c1b2e4d298385')
b2sums=('c5f0f3a3195f2b91ebcfbac5811a3c2959d502c9063ee29a1e1f73a70e32ec3ba25512856c4e810924fa14c1d3b9ebb1e15a9eea71627cd00e2e6c037ae672c1')

build() {
  cd $_name-$pkgver
  PYCARES_USE_SYSTEM_LIB=1 python -m build --wheel --skip-dependency-check --no-isolation
}

# Test failures: gethostbyaddr{,6}, test_getnameinfo
#check() {
#  cd $_name-$pkgver
#  python -m venv --system-site-packages test-env
#  test-env/bin/python -m installer dist/*.whl
#  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python tests/tests.py
#}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
