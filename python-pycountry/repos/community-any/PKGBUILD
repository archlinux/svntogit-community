# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor:  Pawel "kTT" Salata <rockplayer.pl@gmail.com>

pkgbase=python-pycountry
pkgname=(python-pycountry python2-pycountry)
pkgver=1.20
pkgrel=1
pkgdesc="ISO country, subdivision, language, currency and script definitions and their translations"
arch=('any')
url="http://pypi.python.org/pypi/pycountry"
license=('LGPL2.1')
makedepends=('python2-setuptools' 'python-setuptools')
source=("http://pypi.python.org/packages/source/p/pycountry/pycountry-${pkgver}.tar.gz")
md5sums=('efb021c3b42b40c324b80a85714515d6')

package_python2-pycountry() {
  depends=(python2-lxml)
  cd "${srcdir}/pycountry-${pkgver}"

  python2 setup.py install --root="$pkgdir/" --optimize=1
}

package_python-pycountry() {
  depends=(python-lxml)
  cd "${srcdir}/pycountry-${pkgver}"

  python setup.py install --root="$pkgdir/" --optimize=1
}

