# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname='python-pycryptodome'
pkgver=3.16.0
pkgrel=1
license=('BSD')
arch=('x86_64')
pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 3."
url='https://www.pycryptodome.org/'
depends=('python' 'gmp')
makedepends=('gmp' 'python-'{build,installer,wheel} 'python-setuptools')
conflicts=('python-crypto')
provides=('python-crypto')
replaces=('python-crypto')
source=("pycryptodome-${pkgver}.tar.gz::https://github.com/Legrandin/pycryptodome/archive/v${pkgver}.tar.gz")
sha512sums=('945c5bda213a08ba12a0ace8e4bf76730461f8dfce94fa426a97dfab846234ea06e121b176ae823d0f7da9316de50aafde6507a2f17efec4d4a0629d4d68d870')

build() {
  cd "$srcdir/pycryptodome-$pkgver"
  python -m build -wn
}

check() {
  cd "$srcdir/pycryptodome-$pkgver"
  python -m unittest discover
}

package() {
  cd "pycryptodome-$pkgver"
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"

  rm -r "$pkgdir"/usr/lib/python3.*/site-packages/Crypto/SelfTest/
}
