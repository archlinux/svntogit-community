# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname='python-pycryptodome'
pkgver=3.15.0
pkgrel=3
license=('BSD')
arch=('x86_64')
pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 3."
url='https://www.pycryptodome.org/'
depends=('python' 'gmp')
makedepends=('gmp' 'python-'{build,installer,wheel} 'python-setuptools')
conflicts=('python-crypto')
provides=('python-crypto')
replaces=('python-crypto')
source=("pycryptodome-${pkgver}.tar.gz::https://github.com/Legrandin/pycryptodome/archive/v${pkgver}.tar.gz")
sha512sums=('fc611174cfb4ca89fb457cc0d1220326ac813de5a83a959a95c1928a5220dc8c652655ac0a2b7d116b60cb4a8317959d8c2b2f3595e19b9b359a32beb042b58c')

build() {
  cd "$srcdir/pycryptodome-$pkgver"
  python -m build -wn
}

check() {
  cd "$srcdir/pycryptodome-$pkgver"
  python -m unittest discover
}

package() {
  cd "pycryptodome-$pkgver"
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"

  rm -r "$pkgdir"/usr/lib/python3.*/site-packages/Crypto/SelfTest/
}
