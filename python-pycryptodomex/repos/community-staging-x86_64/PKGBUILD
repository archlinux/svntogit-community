# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: BlackIkeEagle <ike DOT devolder AT gmail DOT com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgbase=python-pycryptodomex
pkgname=('python-pycryptodomex' 'python2-pycryptodomex')
pkgver=3.9.8
pkgrel=3

pkgdesc='A self-contained Python package of low-level cryptographic primitives'
url='https://www.pycryptodome.org/'
arch=('x86_64')
license=('BSD')

makedepends=('python-setuptools'
             'python2-setuptools')

source=("https://pypi.org/packages/source/p/pycryptodomex/pycryptodomex-$pkgver.tar.gz")
sha512sums=('bcc5fb27a89c00e6348eacc744a23d687d0b308d5c8ad8f6d955fe2b9e84288a592206336e7fef699800948f8ea7b1ac06d2bec60df0b9ff0fa32d0a05380869')

prepare() {
	cp -a pycryptodomex-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/pycryptodomex-$pkgver
	python setup.py build

	cd "$srcdir"/pycryptodomex-$pkgver-python2
	python2 setup.py build
}

package_python-pycryptodomex() {
	depends=('python')

	cd pycryptodomex-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-pycryptodomex() {
	depends=('python2')

	cd pycryptodomex-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

	install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
