# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyct
pkgname=python-${_pkg}
pkgver=0.4.8
pkgrel=3
pkgdesc="Python packaging Common Tasks"
arch=(any)
url="https://github.com/pyviz-dev/pyct"
license=(BSD)
depends=(python-param)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-requests python-yaml)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('23d7525b5a1567535c093aea4b9c33809415aa5f018dd77f6eb738b1226df6f7')

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build
}

check() {
    cd ${_pkg}-${pkgver}
    pytest pyct -v --pyargs
}

package() {
    cd ${_pkg}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
