# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pycuda
pkgname=('python-pycuda' 'pycuda-headers')
pkgver=2019.1.2
pkgrel=6
pkgdesc="Python wrapper for Nvidia CUDA"
arch=('x86_64')
url="https://mathema.tician.de/software/pycuda"
license=('MIT' 'Apache')
source=("https://pypi.io/packages/source/p/pycuda/pycuda-${pkgver}.tar.gz"
        LICENSE)
makedepends=('ctags' 'python-setuptools' 'python-numpy' 'mesa' 'cuda' 'boost' 'nvidia-utils')
sha512sums=('22829daa5923342eec400a24342b1d738226073bb676476e4af97629d9311426b5d2465e60811696e55401568b90e152ac96d877c6262c416099a609862db742'
            'ee0bef251b8b2c25cd5821932d506183655c04285ae7d01d12f86557812c5068568e9f38e9f6bffe28de870ec662ebe0ee9cce2b444883caaf66b4c877494130')

build() {
   cd "${srcdir}"

   _arch=''
   [[ "$CARCH" = "x86_64" ]] && _arch='64'

   cd "${srcdir}"/pycuda-${pkgver}
   python3 ./configure.py \
                  --cuda-root=/opt/cuda \
                  --cuda-inc-dir=/opt/cuda/include \
                  --cudadrv-lib-dir=/opt/cuda/lib${_arch} \
                  --cudart-lib-dir=/opt/cuda/lib${_arch} \
                  --no-use-shipped-boost \
                  --boost-python-libname=boost_python3
   make
}

package_python-pycuda() {
   depends=('cuda' 'nvidia-utils' 'python-numpy' 'python-pytools' 'python' 'boost-libs' 'pycuda-headers')
   cd "${srcdir}"/pycuda-${pkgver}
   python3 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

   install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

   rm -fr "${pkgdir}"/usr/include
}

package_pycuda-headers() {
   cd "${srcdir}"/pycuda-${pkgver}
   install -dm755 "${pkgdir}"/usr/include/pycuda

   for file in $(ls -1 src/cpp/*.hpp); do
      install -m644 ${file} "${pkgdir}"/usr/include/pycuda
   done

   install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
