# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contrinutor: shining <shiningxc@gmail.com>
# Contributor: phillid <dbphillipsnz _at_thingy_that_swirly_a_symbol gmaildott comm>

pkgbase=python-pydot
pkgname=(python-pydot python2-pydot)
pkgver=1.2.4
pkgrel=1
pkgdesc="Python interface to Graphviz's Dot"
arch=('any')
url="https://github.com/erocarrera/pydot"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing'
             'graphviz')
checkdepends=('python-chardet' 'python2-chardet')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/erocarrera/pydot/archive/v$pkgver.tar.gz")
sha512sums=('848b3d1128eddd747de98fc405b01e7d523140f0da262aa41299f43873d34ab10a8d012cef6d808da069372f6f662c046f454d7ed01d425f2787f233c09e35f4')

prepare() {
  cp -a pydot-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pydot-$pkgver
  python setup.py build

  cd "$srcdir"/pydot-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pydot-$pkgver/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python pydot_unittest.py

  cd "$srcdir"/pydot-$pkgver-py2/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python2 pydot_unittest.py
}

package_python-pydot() {
  depends=('python-pyparsing' 'graphviz')

  cd pydot-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pydot() {
  depends=('python2-pyparsing' 'graphviz')

  cd pydot-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
