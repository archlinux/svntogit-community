# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josh Holbrook <josh.holbrook@gmail.com>

pkgname=python-pyee
pkgver=8.2.2
pkgrel=1
pkgdesc="Port of node.js's EventEmitter to python"
arch=('any')
url=https://github.com/jfhbrook/pyee
license=('MIT')
depends=('python-trio' 'python-twisted')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest-asyncio' 'python-pytest-trio')
source=("https://files.pythonhosted.org/packages/source/p/pyee/pyee-$pkgver.tar.gz")
sha256sums=('5c7e60f8df95710dbe17550e16ce0153f83990c00ef744841b43f371ed53ebea')
b2sums=('f9bd02e1afc78bf51b9267b3a88e4f3ebbfb1342e105fa4175f6fafb4ed0163ef5abb8a006e1ac264f60dfd5ca062068b21c306c5eeead7d929bdd9304e36d14')

prepare() {
  cd pyee-$pkgver
  # Remove setup_requres list
  sed -i '/setup_requires/,/\],/d' setup.py
}

build() {
  cd pyee-$pkgver
  python setup.py build
}

check() {
  cd pyee-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PYTHONPATH="$PWD/temp/$sitepackages" pytest
}

package() {
  cd pyee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
