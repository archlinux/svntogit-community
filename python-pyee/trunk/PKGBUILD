# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josh Holbrook <josh.holbrook@gmail.com>

pkgname=python-pyee
pkgver=9.0.4
pkgrel=5
pkgdesc="Port of node.js's EventEmitter to python"
arch=('any')
url=https://github.com/jfhbrook/pyee
license=('MIT')
depends=('python-typing-extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-mock' 'python-pytest-asyncio' 'python-pytest-trio'
              'python-twisted')
optdepends=('python-trio' 'python-twisted')
source=("https://files.pythonhosted.org/packages/source/p/pyee/pyee-$pkgver.tar.gz")
sha256sums=('2770c4928abc721f46b705e6a72b0c59480c4a69c9a83ca0b00bb994f1ea4b32')
b2sums=('4402d50f5de27ccda64e731450d90cc62f80584e8603a0fda385f6b5598e905d6abaa760aa8e987443dc5990600535f2bd756334cf5b6d0e9d8bf59b4fcdba60')

prepare() {
  cd pyee-$pkgver
  # Remove setup_requires list
  sed -i '/setup_requires/,/\],/d' setup.py
}

build() {
  cd pyee-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd pyee-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd pyee-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/pyee-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
