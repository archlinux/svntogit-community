# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josh Holbrook <josh.holbrook@gmail.com>

pkgname=python-pyee
pkgver=8.1.0
pkgrel=3
pkgdesc="Port of node.js's EventEmitter to python"
arch=('any')
url=https://github.com/jfhbrook/pyee
license=('MIT')
depends=('python-trio' 'python-twisted')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest'{,-asyncio,-trio})
source=("https://files.pythonhosted.org/packages/source/p/pyee/pyee-$pkgver.tar.gz")
sha256sums=('92dacc5bd2bdb8f95aa8dd2585d47ca1c4840e2adb95ccf90034d64f725bfd31')
b2sums=('5e8553cd1e7fde1dec101e101611faa409c24743cce87d4c2a968207af33ee6af66fae1e2a34453375769d845be25f55c1e78c3beeb65381f5f4b257b15b45f1')

prepare() {
  cd pyee-$pkgver
  # Remove setup_requres list
  sed -i '/setup_requires/,/\],/d' setup.py
}

build() {
  cd pyee-$pkgver
  python setup.py build
}

check() {
  cd pyee-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" pytest
}

package() {
  cd pyee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
