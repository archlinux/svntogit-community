# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-pyfakefs
pkgver=4.3.2
pkgrel=1
pkgdesc='pyfakefs implements a fake file system that mocks the Python file system modules'
url=https://github.com/jmcgeheeiv/pyfakefs/
license=(APACHE)
arch=(any)
depends=(python-pytest)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-tox
)
_tag=bec380f07a9efa5a5f9cfd4d5eb5de8350cbe41c
source=(git+https://github.com/jmcgeheeiv/pyfakefs.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pyfakefs

  git describe --tags | sed 's/^v//'
}

build() {
  cd  pyfakefs

  python setup.py build
}

check() {
  cd  pyfakefs

  tox -e py39
}

package() {
  cd pyfakefs

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
