# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-pyfakefs
pkgver=3.7.1
pkgrel=1
pkgdesc='pyfakefs implements a fake file system that mocks the Python file system modules'
url=https://github.com/jmcgeheeiv/pyfakefs/
license=(APACHE)
arch=(any)
depends=(python-pytest)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-tox
)
source=(git+https://github.com/jmcgeheeiv/pyfakefs.git#tag=ad23e51855dd72fc2a88f0b00fc247359c26228e)
sha256sums=(SKIP)

pkgver() {
  cd pyfakefs

  git describe --tags | sed 's/^v//'
}

build() {
  cd  pyfakefs

  python setup.py build
}

check() {
  cd  pyfakefs

  tox -e py38
}

package() {
  cd pyfakefs

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
