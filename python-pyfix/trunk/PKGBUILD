# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pyfix
pkgname=('python-pyfix' 'python2-pyfix')
pkgver=0.2.3
_commit=403d51f426dda6a907c96d8d9d2e2942999b1f23
pkgrel=2
pkgdesc='A framework for writing automated software tests'
arch=('any')
url='https://github.com/pyclectic/pyfix'
license=('Apache')
makedepends=('python-pybuilder' 'python2-pybuilder' 'python-pyassert' 'python2-pyassert' 'git'
             'python-coverage' 'python2-coverage' 'python-mockito' 'python2-mockito'
             'python-unittest-xml-reporting' 'python2-unittest-xml-reporting' 'pychecker')
             # python3-pychecker is missing
source=("git+https://github.com/pyclectic/pyfix.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  # package name will be determined by directory name
  mkdir py2
  cp -a pyfix py2/
}

check() {
  cd "$srcdir"/pyfix
  pyb -v analyze || warning "broken tests"

  cd "$srcdir"/py2/pyfix
  pyb2 -v analyze || warning "broken tests"
}

package_python-pyfix() {
  depends=('python-pyassert')

  cd pyfix
  pyb -v -o package
  cd target/dist/pyfix-$pkgver
  python setup.py install --root="$pkgdir" -O1
}

package_python2-pyfix() {
  depends=('python2-pyassert')

  cd py2/pyfix
  pyb2 -v -o package
  cd target/dist/pyfix-$pkgver
  python2 setup.py install --root="$pkgdir" -O1
}
