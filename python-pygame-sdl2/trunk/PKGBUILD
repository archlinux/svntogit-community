# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Cravix <dr.neemous@gmail.com>

pkgbase=python-pygame-sdl2
pkgname=(
  python-pygame-sdl2
)
pkgver=2.1.0.r411.7f814e9
pkgrel=2
epoch=1
pkgdesc='Reimplementation of portions of the pygame API using SDL2'
arch=(x86_64)
url=https://github.com/renpy/pygame_sdl2
license=(
  LGPL2.1
  zlib
)
depends=(
  sdl2_image
  sdl2_mixer
  sdl2_ttf
  python
)
makedepends=(
  cython
  git
  python
  python-setuptools
)
_commit=7f814e9970558c14af648302431a034dfcad01f2
source=(
  git+https://github.com/renpy/pygame_sdl2.git#commit=${_commit}
)
b2sums=('SKIP')

prepare() {
  mv pygame_sdl2 python-pygame_sdl2
}

pkgver() {
  cd python-pygame_sdl2
  version="$(cat src/pygame_sdl2/version.py | grep '^vernum = ' | sed 's/^vernum = //; s/(//; s/)//; s/, /./g')"
  echo "${version}.r$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
  cd python-pygame_sdl2
  python setup.py build
}

package() {
  cd python-pygame_sdl2

  python setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1 --skip-build
  install -Dm 644 COPYING.ZLIB -t "${pkgdir}"/usr/share/licenses/python-pygame-sdl2/
}

# vim: ts=2 sw=2 et:
