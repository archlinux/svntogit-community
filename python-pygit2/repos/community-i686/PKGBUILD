# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgbase=python-pygit2
pkgname=(python-pygit2 python2-pygit2)
pkgver=0.23.0
pkgrel=2
pkgdesc='Python bindings for libgit2'
url="https://github.com/libgit2/pygit2"
depends=(libgit2)
makedepends=(python-cffi python2-cffi python-setuptools python2-setuptools git)
arch=(i686 x86_64)
license=('GPL2')
#source=("$pkgbase-$pkgver.tar.gz::https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz")
#md5sums=('a73eaf01c42527db7673d11fb5b37654')
source=('git+https://github.com/libgit2/pygit2.git#commit=25d02259df7b73ec220b2a0095ad4a2d7d525f96')
sha1sums=('SKIP')

package_python-pygit2() {
  cd "pygit2"
  depends+=(python python-cffi)
  replaces=(python-libgit2)
  conflicts=(python-libgit2)
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  cd "pygit2"
  depends+=(python2 python2-cffi)
  replaces=(python2-libgit2)
  conflicts=(python2-libgit2)
  python2 setup.py install --root="$pkgdir" --optimize=1
}
