# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.9.2
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2' 'libgit2.so' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('python-build' 'python-cffi' 'python-installer' 'python-setuptools' 'git' 'python-six' 'python-wheel')
checkdepends=('python-pytest-runner')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('17064af7a824d7e4481b4b909370085da047c0d706674d4d086215da4b3ef497')

prepare() {
	cd "pygit2-$pkgver"
	# Disable tests that do stuff online
	sed -i -e '/has_network/s/True/False/' test/utils.py
}

build() {
	cd "pygit2-$pkgver"
	python -m build -wn
}

check() {
	cd "pygit2-$pkgver"
	local _pyver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
	PYTHONPATH="$PWD/build/lib.linux-$CARCH-$_pyver" pytest
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python -m installer -d "${pkgdir}" dist/*.whl
}
