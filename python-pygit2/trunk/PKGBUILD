# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.7.0
pkgrel=2
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:1.3.0' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('python-cffi' 'python-setuptools' 'git' 'python-six')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz"::$url/archive/v$pkgver.tar.gz)
sha256sums=('8fbd1b91bd594f5f9cdd515e3a691ec148fd431838455487663d73387183d844')

prepare() {
	cd "pygit2-$pkgver"
	# Disable tests that do stuff online
	sed -i -e '/has_network/s/True/False/' test/utils.py
}

build() {
	cd "pygit2-$pkgver"
	python setup.py build
}

check() {
	cd "pygit2-$pkgver"
	python setup.py pytest
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python setup.py install --root="${pkgdir}" --optimize=1
}
