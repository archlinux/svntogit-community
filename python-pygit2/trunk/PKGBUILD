# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgbase=python-pygit2
pkgname=(python-pygit2 python2-pygit2)
pkgver=0.23.3
pkgrel=1
pkgdesc='Python bindings for libgit2'
url="https://github.com/libgit2/pygit2"
depends=(libgit2)
makedepends=(python-cffi python2-cffi python-setuptools python2-setuptools git)
arch=(i686 x86_64)
license=('GPL2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz")
md5sums=('0181e07b28a00872f6a8ef134b7a5d7c')

package_python-pygit2() {
  depends+=(python python-cffi)
  replaces=(python-libgit2)
  conflicts=(python-libgit2)

  cd "pygit2-$pkgver"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  depends+=(python2 python2-cffi)
  replaces=(python2-libgit2)
  conflicts=(python2-libgit2)

  cd "pygit2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
