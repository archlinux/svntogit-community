# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Otreblan <otreblain@gmail.com>

_name=pygls
pkgname=python-pygls
pkgver=0.11.2
pkgrel=3
pkgdesc='Pythonic generic implementation of the Language Server Protocol'
arch=('any')
url=https://github.com/openlawlibrary/pygls
license=('APACHE')
depends=('python-pydantic' 'python-typeguard')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm'
             'python-toml' 'python-wheel')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-asyncio')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('1ade26fc9cf0d7c0700fa5430e8dc8411b1108d1eb21565adbe480b64f721a84')
b2sums=('3c26a4067c9f55eec9d151eee4dd90ff7c18084ab89999b70c944fdf5b6f1222938db1743524f433eb152d75a4446b024cff85f1d16924938727808844091271')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
