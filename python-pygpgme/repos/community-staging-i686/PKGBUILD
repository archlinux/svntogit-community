# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>

pkgbase=python-pygpgme
pkgname=(python-pygpgme python2-pygpgme)
pkgver=0.3
pkgrel=4
pkgdesc="A Python interface to the gpgme GnuPG library"
arch=('i686' 'x86_64')
url='https://launchpad.net/pygpgme'
makedepends=('python' 'python2')
license=('LGPL')
source=("https://launchpad.net/pygpgme/trunk/${pkgver}/+download/pygpgme-${pkgver}.tar.gz")
md5sums=('d38355af73f0352cde3d410b25f34fd0')

package_python-pygpgme() {
  depends=('python' 'gpgme')

  cd "${srcdir}/pygpgme-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pygpgme() {
  depends=('python2' 'gpgme')

  cd "${srcdir}/pygpgme-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
