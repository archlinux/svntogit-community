# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgbase=python-pyicu
pkgname=('python-pyicu' 'python2-pyicu')
pkgver=1.9.2
pkgrel=2
pkgdesc="Python binding for ICU"
arch=('i686' 'x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'icu')
checkdepends=('python-fonttools' 'python2-fonttools')
source=("https://pypi.python.org/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('1964464f5385dc8f65c4b88b63cd9f34ced1ae365562386708d92fc5c40090a40554578bd5e90d0a996ca6f012b3d2a8ab6e0fd07968e94a0dfe18894e8f08f4')

prepare() {
  cp -a PyICU-$pkgver{,-py2}
}

build() {
  cd "$srcdir/PyICU-$pkgver"
  python setup.py build

  cd "$srcdir/PyICU-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/PyICU-$pkgver"
  python setup.py test

  cd "$srcdir/PyICU-$pkgver-py2"
  python2 setup.py test
}

package_python-pyicu() {
  depends=('python' 'icu')

  cd PyICU-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pyicu() {
  depends=('python2' 'icu')

  cd PyICU-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
