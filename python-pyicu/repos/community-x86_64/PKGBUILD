# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-pyicu
pkgver=2.7.4
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
depends=('python' 'icu')
makedepends=('python-setuptools')
checkdepends=('python-fonttools' 'python-pytest')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz"
        $pkgname-$pkgver-LICENSE::https://gitlab.pyicu.org/main/pyicu/-/raw/v$pkgver/LICENSE)
sha512sums=('b1252f04c162525b607f28704629389a0765961eb3b8d8c7e67a141f9ecbe6d420cfdd043bc945134db8df550cd58316c08a3ac9c30192e04b0710ad2bc21c1d'
            'ac2b6d1fe6124b84249426abbf4ecdb7c40ab3101ff6b91fa98776a8f27e6b0c255ed88724d19a77e9c89628aab7a16ede1416d5e2464bfe5d8f3ace59dc67f5')

prepare() {
  # https://gitlab.pyicu.org/main/pyicu/-/issues/148#note_785
  # next release after 2.7.2 will include this in the tarball
  mv -vn $pkgname-$pkgver-LICENSE PyICU-$pkgver/LICENSE
}

build() {
  cd PyICU-$pkgver
  python setup.py build
}

check() {
  cd PyICU-$pkgver
  python setup.py test
}

package() {
  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
