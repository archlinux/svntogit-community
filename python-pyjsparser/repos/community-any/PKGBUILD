# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyjsparser
pkgname=(python-pyjsparser python2-pyjsparser)
pkgver=2.5.2
_commit=61f38fd4b62125970424e4dcacee80f1df14857b
pkgrel=1
pkgdesc="Fast javascript parser (based on esprima.js)"
url="https://github.com/PiotrDabkowski/pyjsparser"
license=('MIT')
arch=('any')
makedepends=('python' 'python2')
source=("$pkgname-$_commit.tar.gz::https://github.com/PiotrDabkowski/pyjsparser/archive/$_commit.tar.gz")
sha512sums=('a91d11e1badd085c1efe72174b304354e8fd5ae922a8b451f36f9099522e1c95454d1ac580eed2a19d7ac48fce22011ed11ef1d9676f9b118c76285a5190c9e4')

prepare() {
  mv pyjsparser-{$_commit,$pkgver}
}

build() {
  cd pyjsparser-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd pyjsparser-$pkgver
  python test.py
  python2 test.py
}

package_python-pyjsparser() {
  depends=('python')

  cd pyjsparser-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyjsparser() {
  depends=('python2')

  cd pyjsparser-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
