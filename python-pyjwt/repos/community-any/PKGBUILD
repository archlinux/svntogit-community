# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyjwt
pkgname=('python-pyjwt' 'python2-pyjwt')
pkgver=1.4.0
pkgrel=2
pkgdesc='JSON Web Token implementation in Python'
arch=('any')
url='http://github.com/jpadilla/pyjwt'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-cov' 'python2-pytest-cov')
source=("git+https://github.com/jpadilla/pyjwt.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a pyjwt{,-py2}
}

build() {
  cd "$srcdir/pyjwt"
  python setup.py build

  cd "$srcdir/pyjwt-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pyjwt"
  python setup.py ptr

  cd "$srcdir/pyjwt-py2"
  python2 setup.py ptr || warning "Tests failed"
}

package_python-pyjwt() {
  depends=('python-setuptools')

  cd "$srcdir/pyjwt"
  python3 setup.py install --root="$pkgdir" -O1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pyjwt() {
  depends=('python2-setuptools')

  cd "$srcdir/pyjwt-py2"
  python2 setup.py install --root="$pkgdir" -O1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "$pkgdir/usr/bin/jwt"{,2}
}

