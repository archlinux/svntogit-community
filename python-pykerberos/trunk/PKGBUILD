# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pykerberos
pkgname=('python-pykerberos' 'python2-pykerberos')
pkgver=1.2.1
pkgrel=2
pkgdesc="High-level interface to Kerberos"
arch=('x86_64')
license=('Apache')
url="https://github.com/02strich/pykerberos"
makedepends=('python-setuptools' 'python2-setuptools' 'krb5')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/02strich/pykerberos/archive/v$pkgver.tar.gz")
sha512sums=('1926a531e9aaa3ee71c3cb97f1bdf12b64478b96d9206d01b140db079cb9222a7e5f6dd39309eb6074ce4b86a4311162293537d1365c30b2d8ea0e6df0b44404')

prepare() {
  cp -a pykerberos-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pykerberos-$pkgver
  python setup.py build

  cd "$srcdir"/pykerberos-$pkgver-py2
  python2 setup.py build
}

package_python-pykerberos() {
  depends=('python' 'krb5')

  cd pykerberos-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pykerberos() {
  depends=('python2' 'krb5')

  cd pykerberos-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
