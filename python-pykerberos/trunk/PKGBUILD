# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pykerberos
pkgname=('python-pykerberos' 'python2-pykerberos')
pkgver=1.1.14
pkgrel=1
pkgdesc="High-level interface to Kerberos"
arch=('x86_64')
license=('Apache')
url="https://github.com/02strich/pykerberos"
makedepends=('python-setuptools' 'python2-setuptools' 'krb5' 'git')
source=("git+https://github.com/02strich/pykerberos.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pykerberos{,-py2}
}

build() {
  cd "$srcdir"/pykerberos
  python setup.py build

  cd "$srcdir"/pykerberos-py2
  python2 setup.py build
}

package_python-pykerberos() {
  depends=('python' 'krb5')

  cd pykerberos
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pykerberos() {
  depends=('python2' 'krb5')

  cd pykerberos-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
