# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Buehler <account@buehler.de>

_name=pykwalify
pkgname=python-pykwalify
pkgver=1.7.0
pkgrel=6
pkgdesc='Python lib/cli for JSON/YAML schema validation'
arch=('any')
url=https://github.com/Grokzen/pykwalify
license=('MIT')
depends=('python-dateutil' 'python-docopt' 'python-setuptools' 'python-yaml')
checkdepends=('python-pytest' 'python-testfixtures')
optdepends=('python-ruamel-yaml')
provides=('pykwalify')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "$url/commit/49052dd010637e1403c8b2b1c3710926acd2b1a0.patch")
sha256sums=('7e8b39c5a3a10bc176682b3bd9a7422c39ca247482df198b402e8015defcceb2'
            '01a138fb894242709846fd06ee179202fec1fada7474730efd8d0eb8ee1db404')

prepare() {
  cd $_name-$pkgver
  patch --forward --strip=1 --input=../49052dd010637e1403c8b2b1c3710926acd2b1a0.patch
}

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
