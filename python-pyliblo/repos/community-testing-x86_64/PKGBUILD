# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

_name=pyliblo
pkgbase=python-pyliblo
pkgname=('python-pyliblo' 'python2-pyliblo')
pkgver=0.10.0
pkgrel=5
pkgdesc='Python wrapper for the liblo OSC library'
arch=('x86_64')
url="http://das.nasophon.de/pyliblo/"
license=('LGPL2.1')
makedepends=('cython' 'cython2' 'liblo')
source=("http://das.nasophon.de/download/${_name}-${pkgver}.tar.gz")
sha512sums=('d2a3d47f6c52d310104256d33fbd5d652ae6bc8cde32c2d3b2f3f407b8a4aba99028e57cd466804d266f63468a6c792760f28a43dbabcfa3330e0a852ee4d4e3')


prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-pyliblo() {
  depends=('liblo' 'python')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}/" \
    -vDm644 {NEWS,README}
}

package_python2-pyliblo() {
  depends=('liblo' 'python2')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}/" \
    -vDm644 {NEWS,README}
  # moving conflicting files
  mv -v "${pkgdir}/usr/bin/dump_osc" "${pkgdir}/usr/bin/dump_osc2"
  mv -v "${pkgdir}/usr/bin/send_osc" "${pkgdir}/usr/bin/send_osc2"
  mv -v "${pkgdir}/usr/share/man/man1/dump_osc.1" "${pkgdir}/usr/share/man/man1/dump_osc2.1"
  mv -v "${pkgdir}/usr/share/man/man1/send_osc.1" "${pkgdir}/usr/share/man/man1/send_osc2.1"
}
