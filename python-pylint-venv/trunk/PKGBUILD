# Maintainer:
# Contributor: Stijn Seghers <stijnseghers at gmail dot com>

pkgname=python-pylint-venv
_pyname=pylint-venv
pkgver=3.0.1
pkgrel=1
pkgdesc='Make pylint respect virtualenvs'
arch=(any)
url="https://github.com/jgosmann/$_pyname"
license=(MIT)
depends=(python-pylint python-virtualenv)
makedepends=(python-build python-installer python-wheel python-poetry-core)
source=(https://github.com/jgosmann/$_pyname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d28e2ba9310a879bcbaf5455f8e614e4b59457f3ad68c3bee5e694bac3490449')

build() {
    cd $_pyname-$pkgver
    python -m build --wheel --no-isolation
}

package() {
    cd $_pyname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
