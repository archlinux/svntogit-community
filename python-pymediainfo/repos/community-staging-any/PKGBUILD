# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pymediainfo
pkgname=python-pymediainfo
pkgver=4.3
pkgrel=3
pkgdesc="A Python wrapper around the MediaInfo library"
arch=('any')
url="https://github.com/sbraz/pymediainfo/"
license=('MIT')
depends=('libmediainfo' 'python')
makedepends=('mediainfo' 'python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('641b335761a7382b4bb7e559afbba1a9694a3b588bc329187b62d1d6cb769a6ff0c110033e4fb50ab39991687a6f728cab53d528ecedf0164c55a9ce31250fe8')
b2sums=('a085268c90fd963501b0f57f4e58e000c58db57e902bfedf13b67ae2b56334b333bb028f24f7593416ba956943650031a3ab0b6d6627c9ffd3728e70c34182bb')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
