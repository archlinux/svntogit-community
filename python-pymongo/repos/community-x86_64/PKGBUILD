# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgbase=python-pymongo
pkgname=('python-pymongo' 'python2-pymongo')
pkgver=2.6.3
pkgrel=2
pkgdesc='Python module for using MongoDB'
arch=('x86_64' 'i686')
license=('APACHE')
url='http://pypi.python.org/pypi/pymongo/'
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('mongodb: High-performance schema-free document-oriented database')
source=("http://pypi.python.org/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha256sums=('cabe1d785ad5db6ed8ff70dcb9c987958fc75400f066ec78911ca3f37184a4e2')

build() {
  cp -a "pymongo-$pkgver" "pymongo2-$pkgver"
}

package_python2-pymongo() {
  depends=('python2')

  cd "pymongo2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-pymongo() {
  depends=('python')

  cd "pymongo-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
