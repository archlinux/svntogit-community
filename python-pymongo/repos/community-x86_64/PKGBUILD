# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=python-pymongo
pkgver=3.11.1
pkgrel=1
pkgdesc='Python module for using MongoDB'
arch=('x86_64')
license=('APACHE')
url='https://pypi.python.org/pypi/pymongo/'
depends=('python')
makedepends=('python-setuptools')
source=("https://pypi.io/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha512sums=('1e40758aab6510bca1550625f747147c306d9ac5fd8806bc43c602b4820dba22877ff3083ff1e1bbd56002393a86e1b6d9bc8cd0d9b064804952d5d2583b7582')

build() {
  cd "$srcdir"/pymongo-$pkgver
  python setup.py build
}

package() {
  cd "pymongo-$pkgver"
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}

# vim:set ts=2 sw=2 et:
