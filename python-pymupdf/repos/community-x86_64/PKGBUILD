# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.21.0
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL3)
depends=(glibc gumbo-parser jbig2dec libmupdf openjpeg2 python)
makedepends=(freetype2 libjpeg-turbo python-distro python-build python-installer python-setuptools python-wheel swig systemd)
checkdepends=(python-fonttools python-pillow python-pytest)
optdepends=(
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
options=(debug)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
  $pkgname-1.21.0-fix_build.patch
)
sha512sums=('be540106b448cd49eebc9de9123a2f8c1a26037e5bd426b2c07742e1e91892c6c1c031d4fe1e3fcb166a1aa287098912184892d8bd5481982db6c2fc151f13ec'
            '654d320712803bb3a06928e19f4db30acc1e5548284e04dcb9e6634739f512157b83bbc91b7c5ef32d58855ddcb823fac45f8087b7e02238736ec02ce4c4c36c')
b2sums=('51d5419326116d9fa7551474422534dff96c5af0e986f2d2e5c2d7722c51eaf5d693b04f894fa332cd7103ac75cb31d11d68fd063f118aa4b1a13696706a6fb5'
        '387c599663e2e51ecef635747daa353f2507c8bd44a18513321400c6393493f8d936e25fd63ceaf976718cb530c99c42b5ec6608fcbbed2c9a1e36162a42d6f4')

prepare() {
  # fix issue with setup.py: https://github.com/pymupdf/PyMuPDF/issues/2039
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-1.21.0-fix_build.patch

  # remove bundled mupdf sources
  rm -frv $_name-$pkgver/*.tgz
}

build() {
  cd $_name-$pkgver
  # NOTE: build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=""
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"

  cd $_name-$pkgver
  mkdir -vp $_test_dir
  # install to test dir for testing
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  # disable broken test: https://github.com/pymupdf/PyMuPDF/issues/2040
  pytest -vv -c /dev/null tests/ -k 'not test_textbox3'
}

package() {
  depends+=(
    freetype2 libfreetype.so
    libjpeg-turbo libjpeg.so
  )

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
