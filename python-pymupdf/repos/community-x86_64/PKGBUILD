# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.20.2
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL3)
depends=(glibc gumbo-parser jbig2dec libmupdf openjpeg2 python)
makedepends=(freetype2 libjpeg-turbo python-distro python-build python-installer python-setuptools python-wheel swig systemd)
checkdepends=(python-fonttools python-pillow python-pytest)
optdepends=(
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
options=(debug)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('40a11fd0fcce29c553155e278d3489507dd2d7572c79b5819d145696e0a58d6fe935eef45754976af1b81a54958afa59b634af945f8f399faf4c07e8f3f76d99')
b2sums=('3b2715434479ece6a7551d09ddec651211a2a9b98abbd950240483762acf17d35f2e4ce95a12bc3009e4ea06c6e014131688ad81b2914d22902814cd2f38d8cb')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"

  cd $_name-$pkgver
  mkdir -vp $_test_dir
  # install to test dir for testing
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null tests/
}

package() {
  depends+=(libfreetype.so libjpeg.so)

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
