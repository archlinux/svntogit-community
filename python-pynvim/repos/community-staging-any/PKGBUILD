# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=python-pynvim
pkgver=0.3.2
pkgrel=6
pkgdesc='Python client for Neovim'
url='https://github.com/neovim/pynvim'
arch=('any')
license=('Apache')
depends=('python' 'python-msgpack' 'python-greenlet')
provides=('python-neovim')
makedepends=('python-setuptools')
replaces=('python-neovim')
source=("https://github.com/neovim/pynvim/archive/${pkgver}.tar.gz")
sha256sums=('0a44c540dd5790acfbc7e411b0ed4fb6c44914d866226ebebf370c24c2723dc2')

prepare() {
  sed -i "s/msgpack-python/msgpack/g" pynvim-${pkgver}/setup.py
}

build() {
  cd "${srcdir}/pynvim-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/pynvim-${pkgver}"
  python setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
}

# vim:set sw=2 sts=2 et:
