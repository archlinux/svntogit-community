# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgname=pyocr
pkgname=python-pyocr
pkgver=0.7
pkgrel=1
pkgdesc="Python wrapper for Tesseract and Cuneiform"
arch=(any)
url="https://gitlab.gnome.org/World/OpenPaperwork/pyocr"
license=(GPL3)
depends=(python-pillow python-six)
makedepends=(git python-setuptools)
optdepends=('cuneiform: OCR backend'
            'tesseract: OCR backend')
_commit=0ffeca803fca7c812560a6c0e34342e4833f1ab0  # tags/0.7^0
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/pyocr.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $_pkgname
  make
}

package() {
  cd $_pkgname
  python3 setup.py install --root="$pkgdir" --optimize=1
}
