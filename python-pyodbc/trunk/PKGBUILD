# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Robert Knauer <robert@privatdemail.net>

pkgbase=python-pyodbc
pkgname=(python-pyodbc python2-pyodbc)
_srcname=pyodbc
pkgver=4.0.19
pkgrel=1
pkgdesc='ODBC module for Python'
arch=('i686' 'x86_64')
url='https://github.com/mkleehammer/pyodbc'
license=('MIT')
makedepends=('python' 'python2' 'unixodbc')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/mkleehammer/pyodbc/archive/${pkgver}.tar.gz")
sha256sums=('c614ce073f545bbb91bad74ac45057b72d24643071f9f7ff190c95c39a8caa57')

prepare() {
	echo "Version: ${pkgver}.0" > "${srcdir}/${_srcname}-${pkgver}/PKG-INFO"
}

package_python-pyodbc() {
	pkgdesc='ODBC module for Python 3.x'
	depends=('python' 'unixodbc')

	cd "${srcdir}/${_srcname}-${pkgver}"

	python setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-pyodbc() {
	pkgdesc='ODBC module for Python 2.x'
	depends=('python2' 'unixodbc')

	cd "${srcdir}/${_srcname}-${pkgver}"

	python2 setup.py install --root="${pkgdir}/" --optimize=1
}

