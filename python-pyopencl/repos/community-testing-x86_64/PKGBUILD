# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pyopencl
pkgname=('python2-pyopencl' 'python-pyopencl' 'pyopencl-headers')
pkgver=2015.2.1
pkgrel=2
pkgdesc="A complete, object-oriented language binding of OpenCL to Python"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pyopencl"
license=('custom')
epoch=1
checkdepends=('python2-six' 'python-six' 'python-appdirs' 'python2-appdirs')
makedepends=('boost-libs' 'ctags' 'python2-setuptools' 'python-setuptools' 'libcl' 'opencl-headers' 'mesa' 'boost' 'python2-mako' 'python-mako' 'python-numpy' 'python2-numpy')
source=("http://pypi.python.org/packages/source/p/pyopencl/pyopencl-${pkgver}.tar.gz"
        'LICENSE.txt')
sha1sums=('f339b71798d477233a68c2eedf344539667eb2bb'
          '2e6966b3d9b15603ce2c3ff79eeadd63c5d066b7')

build() {
   cd "$srcdir"

   cp -a pyopencl-$pkgver{,-python2}

   cd "${srcdir}/pyopencl-${pkgver}"
   python3 ./configure.py --cl-enable-gl --python-exe=python3 # --boost-python-libname=boost_python3
   make

   cd "$srcdir/pyopencl-$pkgver-python2"
   python2 ./configure.py --cl-enable-gl --python-exe=python2 # --boost-python-libname=boost_python
   make
}

check(){
   cd "${srcdir}/pyopencl-${pkgver}"
#   python3 setup.py test

   cd "$srcdir/pyopencl-$pkgver-python2"
#   python2 setup.py test
}

package_python-pyopencl() {
   depends=('libcl' 'opencl-headers' 'mesa' 'boost-libs' 'python' 'python-numpy' 'python-mako' 'python-pytools' 'pyopencl-headers' 'python-setuptools' 'python-cffi')

   cd "${srcdir}/pyopencl-${pkgver}"
   python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

   rm -fr "${pkgdir}"/usr/include

   install -D -m644 $srcdir/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyopencl() {
   depends=('libcl' 'opencl-headers' 'mesa' 'boost-libs' 'python2' 'python2-numpy' 'python2-mako' 'python2-pytools' 'pyopencl-headers' 'python2-setuptools' 'python2-cffi')

   cd "${srcdir}/pyopencl-${pkgver}-python2"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

    rm -fr "${pkgdir}"/usr/include/

   install -D -m644 $srcdir/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_pyopencl-headers() {
   cd "${srcdir}/pyopencl-${pkgver}/"
   install -dm755 "${pkgdir}"/usr/include/pyopencl

  for file in pyopencl-airy.cl  pyopencl-bessel-j.cl  pyopencl-bessel-y.cl  pyopencl-complex.h  pyopencl-eval-tbl.cl  pyopencl-ranluxcl.cl; do
      install -m644 pyopencl/cl/${file} "${pkgdir}"/usr/include/pyopencl
   done
   install -D -m644 $srcdir/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
