# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsandrade@gmail.com>

pkgname=('python2-pyparallel' 'python-pyparallel')
pkgbase=python-pyparallel
pkgver=0.2
pkgrel=5
pkgdesc="This module encapsulates the access for the parallel port."
arch=('any')
url="http://pyserial.sourceforge.net/pyparallel.html"
license=('custom:PYTHON')
makedepends=('python2' 'python')
source=("http://downloads.sourceforge.net/sourceforge/pyserial/pyparallel-$pkgver.zip")
md5sums=('46b65592f0b2fa7094ca87bf053e93a7')

build() {
  true
}

package_python2-pyparallel() {
  depends=('python2')

  cd $srcdir/pyparallel-$pkgver
  #fix build FS#20459
  sed -i s/data_files\ =\ None/data_files\ =\ {}/ setup.py
  python2 setup.py install --root $pkgdir --optimize=1
  install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python-pyparallel() {
  depends=('python')

  cd $srcdir/pyparallel-$pkgver
  sed -i s/data_files\ =\ None/data_files\ =\ {}/ setup.py
  2to3 -w parallel/*.py
  sed -i s/string\\.split/str.split/ parallel/__init__.py
  python setup.py install --root $pkgdir --optimize=1
  install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}
