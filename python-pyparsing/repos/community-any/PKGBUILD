# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>
pkgbase=python-pyparsing
pkgname=(python-pyparsing python2-pyparsing)
pkgver=1.5.6
pkgrel=1
pkgdesc="General parsing module for Python"
arch=(any)
url="http://pyparsing.wikispaces.com"
license=('MIT')
makedepends=(python python2)
source=(http://downloads.sourceforge.net/pyparsing/pyparsing-$pkgver.tar.gz)

build() {
  true
}

package_python2-pyparsing() {
  depends=(python2)
  provides=(pyparsing)
  replaces=(pyparsing)
  conflicts=(pyparsing)
  cd "$srcdir/pyparsing-$pkgver"
  rm -rf build pyparsing.py
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-pyparsing/LICENSE"
}

package_python-pyparsing() {
  depends=(python)
  cd "$srcdir/pyparsing-$pkgver"
  rm -rf build pyparsing.py
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-pyparsing/LICENSE"
}
md5sums=('1e41cb219dae9fc353bd4cd47636b283')
