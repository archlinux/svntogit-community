# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com> 

pkgbase=python-pyperclip
pkgname=('python-pyperclip' 'python2-pyperclip')
_pypiname=pyperclip
pkgver=1.5.25
_commit=a3023e83a371e6751dd3361bac71fdd567395e3b
pkgrel=1
pkgdesc="A cross-platform clipboard module for Python"
arch=('any')
url="https://github.com/asweigart/pyperclip"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
# GTK/Qt tests crash xvfb, and Klipper tests require a running klipper
checkdepends=('xorg-server-xvfb' 'python-pytest' 'python2-pytest'
              'xsel' 'xclip')
source=("git+https://github.com/asweigart/pyperclip.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a $_pypiname{,-py2}
}

check() {
  cd "$srcdir/$_pypiname"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test

  cd "$srcdir/$_pypiname-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test2
}

package_python-pyperclip() {
  depends=('python')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'python-pyqt4: qt backend'
              'plasma-workspace: klipper backend')

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-pyperclip() {
  depends=('python2')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'python2-pyqt4: qt backend'
              'plasma-workspace: klipper backend'
              'pygtk: gtk backend')

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
