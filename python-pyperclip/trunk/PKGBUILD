# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com>

pkgbase=python-pyperclip
pkgname=('python-pyperclip' 'python2-pyperclip')
pkgver=1.6.0
_commit=2f11f6302492e2dfc6b1a91eb9d5c0a6b68a71d5
pkgrel=1
pkgdesc="A cross-platform clipboard module for Python"
arch=('any')
url="https://github.com/asweigart/pyperclip"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
# GTK/Qt tests crash xvfb, and Klipper tests require a running klipper
checkdepends=('xorg-server-xvfb' 'python-pytest' 'python2-pytest' 'xsel' 'xclip')
source=("$pkgbase-$_commit.tar.gz::https://github.com/asweigart/pyperclip/archive/$_commit.tar.gz")
sha512sums=('2383a5b7e13cc810522acd84b894b608c5eba74ea99a7d9e588ee3d9f5324a8d9a0023e6fe6d9d3163aeac8898fb3e0fa416589fbe99e12e9c7ba0e7fe3ba52d')

prepare() {
  mv pyperclip-{$_commit,$pkgver}
  cp -a pyperclip-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/pyperclip-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test

  cd "$srcdir"/pyperclip-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test2
}

package_python-pyperclip() {
  depends=('python')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'python-pyqt4: qt backend'
              'plasma-workspace: klipper backend')

  cd "$srcdir"/pyperclip-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyperclip() {
  depends=('python2')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'python2-pyqt4: qt backend'
              'plasma-workspace: klipper backend'
              'pygtk: gtk backend')

  cd "$srcdir"/pyperclip-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
