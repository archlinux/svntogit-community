# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com>

pkgbase=python-pyperclip
pkgname=('python-pyperclip' 'python2-pyperclip')
pkgver=1.7.0
_commit=09abcb63fa5a6ac9afa3b11def604b059b943b6b
pkgrel=4
pkgdesc="A cross-platform clipboard module for Python"
arch=('any')
url="https://github.com/asweigart/pyperclip"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
# GTK/Qt tests crash xvfb, and Klipper tests require a running klipper
checkdepends=('xorg-server-xvfb' 'python-pytest' 'python2-pytest' 'xsel' 'xclip')
source=("$pkgbase-$_commit.tar.gz::https://github.com/asweigart/pyperclip/archive/$_commit.tar.gz")
sha512sums=('a7749f93e48a2d52f8ed94a54aaabca5d95b7ec107531472d612ddf180a1769b9b62e6c1196e1a6d4947e212bb84954683e38fadd897e3c3573dabb665ef4bee')

prepare() {
  mv pyperclip-{$_commit,$pkgver}
}

build() {
  cd pyperclip-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd pyperclip-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" xvfb-run py.test2
}

package_python-pyperclip() {
  depends=('python')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'plasma-workspace: klipper backend')

  cd pyperclip-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyperclip() {
  depends=('python2')
  optdepends=('xclip: xclip backend'
              'xsel: xsel backend'
              'plasma-workspace: klipper backend'
              'pygtk: gtk backend')

  cd pyperclip-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
