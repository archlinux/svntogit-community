# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyphen
pkgname=python-pyphen
pkgver=0.11.0
pkgrel=3
pkgdesc="Pure Python module to hyphenate text"
arch=('any')
url="https://pyphen.org/"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('49f510cdc00af479054c64380de8fb5722e2c99a3cf0f882c4b78b82c80300b6cb28c53c593a091e358eae662b28a50ac6e63afb8d57f2adf5c03e5a7f879c68')
b2sums=('9da03e913915e2b4719dc4c00f0f0c808e5e81732021e5ae00e70b02c15edd5d97849618e5b2f2f8d9c685c92debad4952848a9573f82194aa34ac2133cd42d1')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  nosetests -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
