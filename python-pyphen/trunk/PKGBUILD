# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyphen
pkgname=python-pyphen
pkgver=0.12.0
pkgrel=1
pkgdesc="Pure Python module to hyphenate text"
arch=(any)
url="https://pyphen.org/"
license=(GPL2 LGPL2.1 MPL)
depends=(python)
makedepends=(python-build python-flit python-installer python-wheel)
checkdepends=(python-pytest)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('144218b8d5454fffc16de0c839c93a0badadd5c9b7867195072b77d9130440059ec84b5819d7eed12b65d2cc51cb7202226cb44ef6844f18fa428c4b3a1ef2b5')
b2sums=('979f29051e65ea1411ce013760f4d7facfa87daac1d5325976a8fed7d6335eebaa14bc2d0f7ff521d9cf2637cef3f820fa91d983f7fce61eaaa7b23496cc1041')

build() {
  cd "${_name}-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "${_name}-$pkgver"
  pytest -v -c /dev/null tests/
}

package() {
  cd "${_name}-$pkgver"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
