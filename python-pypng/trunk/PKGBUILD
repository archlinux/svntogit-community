# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pypng
pkgname=python-$_pkgname
pkgver=0.0.20
pkgrel=1
pkgdesc='Pure Python library for PNG image encoding/decoding'
arch=('any')
url='https://github.com/drj11/pypng'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgname-$pkgver.tar.gz")
sha512sums=('09a567ac9ab71858340ae170c46f3d7334b46db8c09d26284adc4eaa7bdf49888cc7a661b7fb80460bb2decb121bc59ef3d2fbb909b67713da023d76100704a1')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

# vim:set ts=2 sw=2 et:
