# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: robertfoster
# Contributor: Ista Zahn <istazahn[at]gmail[dot]com>

_name=pyppeteer
pkgname=python-pyppeteer
pkgver=0.2.6
pkgrel=1
pkgdesc='Headless chrome/chromium automation library'
arch=('any')
url=https://github.com/pyppeteer/pyppeteer
license=('MIT')
depends=('python-appdirs' 'python-importlib-metadata' 'python-pyee'
         'python-tqdm' 'python-urllib3' 'python-websockets')
makedepends=('python-build' 'python-install' 'python-poetry-core')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
b2sums=('30f6cffcf753d9d802bb2dba1b9b87fafe578b90014dace858d72e8f3e091047994b461050f19f29cf78da24dd95f7b107618b9d4f370800b82faf16ed7bedc1')

prepare() {
  cd $_name-$pkgver
  # Remove include list https://github.com/pypa/wheel/issues/92
  sed -i '/^include/,/]/d' pyproject.toml
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_name-$pkgver
  export PYTHONHASHSEED=0
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl

  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/*

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s $site_packages/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
