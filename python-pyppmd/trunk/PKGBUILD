# Maintainer:

_pkgname=pyppmd
pkgname=python-$_pkgname
pkgver=0.17.1
pkgrel=1
pkgdesc='PPMd compression/decompression library'
arch=(x86_64)
url='http://github.com/miurahr/pyppmd'
license=(LGPL)
depends=(python)
makedepends=(python-setuptools-scm)
checkdepends=(python-pytest-benchmark python-py-cpuinfo python-hypothesis)
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('ebc65d393d58b5463fdfbdf9b27e51b6aef9a9142052a50685a5dfdba91f5808')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH="$PWD"/`ls -d build/lib.*` pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
