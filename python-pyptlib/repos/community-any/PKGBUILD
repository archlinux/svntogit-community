# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: mutantmonkey <aur@mutantmonkey.in>

pkgbase=python-pyptlib
pkgname=('python2-pyptlib')
_pkgname=pyptlib
pkgver=0.0.6
pkgrel=1
pkgdesc="A python implementation of the Pluggable Transports for Circumvention specification for Tor"
arch=('any')
url="https://pypi.python.org/pypi/pyptlib"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/p/pyptlib/pyptlib-${pkgver}.tar.gz")
sha256sums=('b98472e3d9e8f4689d3913ca8f89afa5e6cc5383dcd8686987606166f9dac607')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' pyptlib/*.py pyptlib/util/*.py
  sed -i 's/\["python", "-m"/["python2", "-m"/' pyptlib/test/{test_util_subproc.py,util_subproc_main.py}
}

check() {
  cd "$srcdir/$_pkgname-$pkgver"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 setup.py test
}

package_python2-pyptlib() {
  cd "$srcdir/$_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
