# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Windel Bouwman <windel at windel dot nl>
# Contributor: Jakub Klinkovský <kuba.klinkovsky@gmail.com>

_pkgname=pyqtgraph
pkgname=python-$_pkgname
pkgver=0.11.0
pkgrel=3
pkgdesc='Scientific Graphics and GUI Library for Python'
arch=('any')
url='http://www.pyqtgraph.org'
license=('MIT')
makedepends=('python-setuptools')
depends=('python' 'qt5-python-bindings' 'python-numpy')
optdepends=('python-opengl: 3d graphics')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/pyqtgraph/pyqtgraph/archive/pyqtgraph-$pkgver.tar.gz")
sha512sums=('1ef7c3c28359a25861424cf7670dcfe471ba62786ebb0288af72a3f626af49ca51a87ac85fccacb7cadce49b6318efb895a7682fb93aa84ff9e2171134d8071b')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
