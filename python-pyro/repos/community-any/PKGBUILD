# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>

pkgbase=python-pyro
pkgname=("$pkgbase" 'python2-pyro')
pkgver=4.35
pkgrel=1
pkgdesc='Distributed object middleware for Python (RPC)'
url='http://pypi.python.org/pypi/Pyro4/'
arch=('any')
license=('MIT')
depends=('python' 'python2')
source=("https://pypi.python.org/packages/source/P/Pyro4/Pyro4-$pkgver.src.tar.gz")
sha256sums=('97ef658b96fa10bac3e01097b1e2b6630fea2b307081ec6f2ac00f85e6020178')

package_python-pyro() {
  depends=('python')
  cd "Pyro4-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyro() {
  depends=('python2')
  cd "Pyro4-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
