# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyspnego
pkgname=python-${_pkg}
pkgver=0.5.2
pkgrel=1
pkgdesc="SPNEGO Authentication library"
arch=(any)
url="https://github.com/jborean93/pyspnego"
license=(MIT)
depends=(python-cryptography)
makedepends=(python-setuptools)
checkdepends=(cython python-pytest python-pytest-mock python-gssapi python-krb5 python-ruamel-yaml)
optdepends=('python-gssapi: Kerberos support'
            'python-krb5: Kerberos support'
            'python-ruamel-yaml: YAML configuration')
# No tests in pypi tarball
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('28253f7c0b43ef9d90b2c90a494eea65824331ac41ec353bfbc28e15723d20ca')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}"/build/lib pytest -vv --color=yes
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
