# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgbase=python-pytables
pkgname=('python2-pytables' 'python-pytables')
pkgver=3.2.2
pkgrel=5
arch=("i686" "x86_64")
pkgdesc="A package for managing hierarchical datasets and designed to efficiently and easily cope with extremely large amounts of data"
url="http://www.pytables.org"
license=("BSD")
makedepends=('lzo' 'hdf5' 'python2-numexpr' 'python-numexpr' 'python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/t/tables/tables-$pkgver.tar.gz")
md5sums=('4a9792bba1a21f0853e7841d7c1beed8')

prepare() {
  cd "$srcdir"/tables-$pkgver

  # -march=native generates AVX instructions if the host supports it, which
  # then results in "SIGILL (Illegal instruction)" on machines lacking AVX
  # https://github.com/PyTables/PyTables/issues/458
  # https://github.com/PyTables/PyTables/pull/503
  sed -i 's/"-march=native",\?//' setup.py

  cd "$srcdir"
  cp -a tables-$pkgver tables-py2-$pkgver
}

build() {
  msg "Building Python2"
  cd "$srcdir"/tables-py2-$pkgver
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/tables-$pkgver
  python setup.py build
}

package_python2-pytables() {
  depends=('lzo' 'hdf5' 'python2-numexpr')

  cd "$srcdir"/tables-py2-${pkgver}
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # see FS#36015 and FS#44971
  mv "$pkgdir"/usr/bin/pt2to3{,-2.7}
  mv "$pkgdir"/usr/bin/ptdump{,-2.7}
  mv "$pkgdir"/usr/bin/ptrepack{,-2.7}
  mv "$pkgdir"/usr/bin/pttree{,-2.7}

  # see FS#45975
  _site_packages=`python2 -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod a+rx "${pkgdir}${_site_packages}"/*.egg-info
  chmod a+r "${pkgdir}${_site_packages}"/*.egg-info/*
}

package_python-pytables() {
  depends=('lzo' 'hdf5' 'python-numexpr')

  cd "$srcdir"/tables-${pkgver}
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # see FS#45975
  _site_packages=`python -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod a+rx "${pkgdir}${_site_packages}"/*.egg-info
  chmod a+r "${pkgdir}${_site_packages}"/*.egg-info/*
}

