# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyte
pkgname=('python-pyte' 'python2-pyte')
pkgver=0.7.0
pkgrel=1
pkgdesc='Simple VTXXX-compatible terminal emulator'
arch=('any')
license=('LGPL3')
url='https://github.com/selectel/pyte'
makedepends=('python-setuptools' 'python2-setuptools' 'python-wcwidth' 'python2-wcwidth')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/selectel/pyte/archive/$pkgver.tar.gz")
sha512sums=('dda5ed3e3eb0621041914ead2f4ab1578d0cdd9fe7a42a3635bd154ef59473daf08fd53f2233d97871b75c2462bee69270438e9f6b305d2ffb07bde88a8c909e')

prepare() {
  cp -a pyte-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyte-$pkgver
  python setup.py build

  cd "$srcdir"/pyte-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyte-$pkgver
  python setup.py pytest

  cd "$srcdir"/pyte-$pkgver-py2
  python2 setup.py pytest
}

package_python-pyte() {
  depends=('python-wcwidth')

  cd pyte-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pyte() {
  depends=('python2-wcwidth')

  cd pyte-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
