# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Christoph Brill <egore911@gmail.com>

_name=pytesseract
pkgname=python-pytesseract
pkgver=0.3.6
pkgrel=3
pkgdesc='Python wrapper for Google Tesseract'
arch=('any')
url=https://github.com/madmaze/pytesseract
license=('Apache')
depends=('python-pillow' 'python-setuptools' 'tesseract')
checkdepends=('python-pytest' 'tesseract-data-eng' 'tesseract-data-fra')
source=("$url/archive/v.$pkgver/$_name-v.$pkgver.tar.gz")
sha512sums=('002777ce3c9609455aca36e0e17f19ead32c5d48b8bc04081b63aa1520104572ecdf975a85fbf010e29bd6eb66db810f1b88ee8e852de274dc9fdea96acf80bf')

build() {
  cd $_name-v.$pkgver
  python setup.py build
}

check() {
  cd $_name-v.$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" pytest -k 'not test_image_to_data_common_output[dict]' # https://github.com/madmaze/pytesseract/issues/298
}

package() {
  cd $_name-v.$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
