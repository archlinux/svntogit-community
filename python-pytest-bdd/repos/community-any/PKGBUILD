# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-bdd
pkgver=6.1.0
pkgrel=1
pkgdesc='BDD library for the pytest runner'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-bdd'
depends=('python-mako' 'python-parse' 'python-parse-type' 'python-pytest' 'python-typing-extensions')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-setuptools')
source=("https://github.com/pytest-dev/pytest-bdd/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('957b39a9c7c48b8d5e42d301de875579d062744766c55ffa32aed78a5a8a5c9fd9bb3ba281183841bc1da19604659c94ecaa4487dfc7a6dd69b507f5fc9f2dc7')

build() {
  cd pytest-bdd-$pkgver
  python -m build -nw
}

check() {
  # Hack entry points by installing it

  cd pytest-bdd-$pkgver
  python -m installer -d tmp_install dist/*.whl
  PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages" pytest
}

package() {
  cd pytest-bdd-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
