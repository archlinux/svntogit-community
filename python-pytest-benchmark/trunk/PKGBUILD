# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-benchmark
pkgname=('python-pytest-benchmark' 'python2-pytest-benchmark')
pkgver=3.2.3
pkgrel=1
pkgdesc='A py.test fixture for benchmarking code'
arch=('any')
license=('BSD')
url='https://github.com/ionelmc/pytest-benchmark'
makedepends=('python-pytest' 'python2-pytest' 'python2-statistics' 'python2-pathlib'
             'python-py-cpuinfo' 'python2-py-cpuinfo')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-pygal' 'python2-pygal'
              'python-freezegun' 'python2-freezegun' 'mercurial' 'python-aspectlib'
              'python2-aspectlib' 'python-pytest-xdist' 'python2-pytest-xdist'
              'python-pytest-runner' 'python2-pytest-runner' 'python-elasticsearch'
              'python2-elasticsearch' 'python2-mock' 'git')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/pytest-benchmark/archive/v$pkgver.tar.gz")
sha512sums=('952dbe2a9af1ae5de966543eae3b4523a5418c8441785714cbb7a5d17697046f060befb4a40cf216526e2a9a63d91c598d65e65618bd78fc2d889c92cecbbe53')

prepare() {
  cp -a pytest-benchmark-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-benchmark-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-benchmark-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-benchmark-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py pytest --addopts="tests"

  cd "$srcdir"/pytest-benchmark-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" PATH="$PWD/tmp_install/usr/bin:$PATH" python2 setup.py pytest --addopts="tests"
}

package_python-pytest-benchmark() {
  depends=('python-pytest' 'python-py-cpuinfo')

  cd "$srcdir"/pytest-benchmark-$pkgver
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-benchmark() {
  depends=('python2-pytest' 'python2-py-cpuinfo' 'python2-statistics' 'python2-pathlib')

  cd "$srcdir"/pytest-benchmark-$pkgver-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/pytest-benchmark{,2}
  mv "$pkgdir"/usr/bin/py.test-benchmark{,2}
}

# vim:set ts=2 sw=2 et:
