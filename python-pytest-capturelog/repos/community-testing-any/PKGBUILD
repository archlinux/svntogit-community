# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-capturelog
pkgname=('python-pytest-capturelog' 'python2-pytest-capturelog')
_pypiname=pytest-capturelog
pkgver=0.7
_tag=RELEASE_0_7
pkgrel=4
pkgdesc='py.test plugin to capture log messages'
arch=('any')
license=('MIT')
url='https://bitbucket.org/memedough/pytest-capturelog/overview'
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools' 'python2-setuptools' 'mercurial')
source=("hg+https://felixonmars@bitbucket.org/memedough/pytest-capturelog#tag=$_tag")
md5sums=('SKIP')

prepare() {
  cp -a "${srcdir}/${_pypiname}"{,-py2}
}

build() {
  cd "$srcdir/$_pypiname"
  python setup.py build

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test || warning "Tests failed"

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2 || warning "Tests failed"
}

package_python-pytest-capturelog() {
  depends=('python-pytest')

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-pytest-capturelog() {
  depends=('python2-pytest')

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
# vim:set ts=2 sw=2 et:
