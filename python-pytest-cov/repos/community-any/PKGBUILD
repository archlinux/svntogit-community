# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastien LEDUC <sebastien@sleduc.fr>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgbase=python-pytest-cov
pkgname=python-pytest-cov
pkgver=2.12.1
pkgrel=3
pkgdesc='py.test plugin for coverage reporting with support for both centralised and distributed testing, including subprocesses and multiprocessing'
arch=('any')
license=('MIT')
url='https://github.com/schlamar/pytest-cov'
depends=('python-pytest' 'python-coverage')
makedepends=('python-setuptools')
checkdepends=('python-virtualenv' 'python-process-tests'
              'python-pytest-xdist' 'python-fields')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-cov/archive/v$pkgver.tar.gz")
sha512sums=('a9893c81b7c5829c68b71e086719939c82622788fa471e2ab61f27cfb0efcc2008f09ec586ea06dba99ec1a6bf8789b8e3531c2ea8fa6435d7e45712b5cb9ea4')

build() {
  cd "$srcdir"/pytest-cov-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-cov-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH:$PWD/tests" py.test || echo "Tests failed"
}

package_python-pytest-cov() {
  cd pytest-cov-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
