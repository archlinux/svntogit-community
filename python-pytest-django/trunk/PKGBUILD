# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-django
pkgname=('python-pytest-django' 'python2-pytest-django')
pkgver=3.2.1
pkgrel=1
pkgdesc="A Django plugin for py.test"
arch=('any')
license=('BSD')
url="https://github.com/pytest-dev/pytest-django"
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools-scm' 'python2-setuptools-scm')
checkdepends=('python-django' 'python2-django' 'python-pytest-xdist' 'python2-pytest-xdist')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-django/archive/$pkgver.tar.gz")
sha512sums=('231a885238b3e958c52c0f1443564e2db17fc17e574783ec2e5ae15003a633bebb8962a4b1122c2f9d8e296cd2ff8d823d0322a440b62b7edbb93b084b6ecfe9')

prepare() {
  sed -i 's/setuptools_scm==1.11.1/setuptools_scm/' pytest-django-$pkgver/setup.py

  cp -a pytest-django-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-django-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-django-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-django-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test tests

  cd "$srcdir"/pytest-django-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test2 tests
}

package_python-pytest-django() {
  depends=('python-pytest')

  cd pytest-django-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-django() {
  depends=('python2-pytest')    

  cd pytest-django-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
