# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-httpbin
pkgname=('python-pytest-httpbin' 'python2-pytest-httpbin')
pkgver=1.0.0
pkgrel=3
pkgdesc='A py.test fixture for httpbining code'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/pytest-httpbin'
makedepends=('python-pytest' 'python2-pytest' 'python-six' 'python2-six' 'httpbin' 'python2-httpbin')
checkdepends=('python-requests' 'python2-requests' 'python-pytest-runner' 'python2-pytest-runner'
              'python2-ipaddress')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kevin1024/pytest-httpbin/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('2794134cc005cb969752e0c1119459d3f1e106f679942880a6b9ff507f2ee31c8ac4146a9c01b9a920368ea516443954b1b7044f7b1c270fe251f7d36adf7661'
            '949cb93164ce92e0a5acc184cca2da5580e31def2d153771ebfa386030fa2da65bcd48670780096d2cefe3e631dc755fc3024ae417f2d1bb3333bd85a39baf22')

prepare() {
  cp -a pytest-httpbin-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-httpbin-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-httpbin-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-httpbin-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" python setup.py pytest

  cd "$srcdir"/pytest-httpbin-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" python2 setup.py pytest
}

package_python-pytest-httpbin() {
  depends=('python-pytest' 'python-six' 'httpbin')

  cd pytest-httpbin-$pkgver
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-httpbin() {
  depends=('python2-pytest' 'python2-six' 'python2-httpbin')

  cd pytest-httpbin-$pkgver-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
