# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-isort
pkgver=1.1.0
pkgrel=1
pkgdesc='pytest plugin to perform isort checks (import ordering)'
arch=('any')
license=('BSD')
url='https://github.com/moccu/pytest-isort'
depends=('python-pytest-cache' 'python-isort')
source=("$pkgname-$pkgver.tar.gz::https://github.com/moccu/pytest-isort/archive/$pkgver.tar.gz")
sha512sums=('4eb6438ff19501573d925839761fa4cd476fe37ddb6dc618a2d05116cc0ec7680e84cbc88c834329fd7457deb7f88e1e93e67d471192420e6bdf3693c6dc53a3')

build() {
  cd pytest-isort-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

# vim:set ts=2 sw=2 et:
