# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-isort
pkgver=1.0.0
pkgrel=1
pkgdesc='pytest plugin to perform isort checks (import ordering)'
arch=('any')
license=('BSD')
url='https://github.com/moccu/pytest-isort'
depends=('python-pytest-cache' 'python-isort')
source=("$pkgname-$pkgver.tar.gz::https://github.com/moccu/pytest-isort/archive/$pkgver.tar.gz")
sha512sums=('3aec53f72d8df58d8f7e07a63c9c986051261b87c090fb50707e5ec00946c675d7a1e6746c458c035c7359218c299f53f769ffe9c066e8ad5a7eaac268ddd9c6')

build() {
  cd pytest-isort-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

# vim:set ts=2 sw=2 et:
