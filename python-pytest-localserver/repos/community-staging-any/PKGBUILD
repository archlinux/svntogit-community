# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-localserver
pkgname=('python-pytest-localserver' 'python2-pytest-localserver')
pkgver=0.4.1
pkgrel=2
pkgdesc='py.test plugin to test server connections locally'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-localserver'
makedepends=('python-pytest' 'python2-pytest' 'python-werkzeug' 'python2-werkzeug')
checkdepends=('python-requests' 'python2-requests')
source=("$pkgbase-$pkgver.tar.gz::https://bitbucket.org/pytest-dev/pytest-localserver/get/$pkgver.tar.bz2")
sha512sums=('6f7f4645a2c493e6ea37f6b86c6e2b9614079621511a4316acaf02b31dbaa4c4d0286b1ce80fde918d21e6dfe3f3d380515bf7610dfba83b0091eaea3ffaf5ef')

prepare() {
  mv pytest-dev-pytest-localserver-* pytest-localserver-$pkgver

  cp -a pytest-localserver-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-localserver-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-localserver-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-localserver-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-localserver-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-localserver() {
  depends=('python-pytest' 'python-werkzeug')

  cd pytest-localserver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-localserver() {
  depends=('python2-pytest' 'python2-werkzeug')

  cd pytest-localserver-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
