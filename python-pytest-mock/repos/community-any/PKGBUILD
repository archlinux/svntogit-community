# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-mock
pkgname=('python-pytest-mock' 'python2-pytest-mock')
pkgver=0.6.0
pkgrel=1
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
makedepends=('python-pytest' 'python2-pytest' 'python2-mock')
#checkdepends=('python-coverage' 'python2-coverage')
source=("http://pypi.python.org/packages/source/p/pytest-mock/pytest-mock-$pkgver.zip")
sha512sums=('a89bcd8ea55f940e08979e2d839548cef06ae055cd65ca60e47d7a4f6a3909fcee93828ea7e8d4265503f7dc66bca68a44de0d3600cbf19b71c9767d07d4fda2')

prepare() {
  cp -a pytest-mock-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/pytest-mock-${pkgver}"
  python setup.py build

  cd "$srcdir/pytest-mock-${pkgver}-py2"
  python2 setup.py build
}

check() {
  # Tests not included

  cd "$srcdir/pytest-mock-${pkgver}"
  # coverage3 run --source=pytest_mock.py runtests.py

  cd "$srcdir/pytest-mock-${pkgver}-py2"
  # coverage2 run --source=pytest_mock.py runtests.py
}

package_python-pytest-mock() {
  depends=('python-pytest')

  cd pytest-mock-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pytest-mock() {
  depends=('python2-pytest' 'python2-mock')

  cd pytest-mock-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

