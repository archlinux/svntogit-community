# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-mock
pkgname=('python-pytest-mock' 'python2-pytest-mock')
pkgver=1.5.0
pkgrel=2
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
makedepends=('python-pytest' 'python2-pytest' 'python2-mock' 'git')
source=("git+https://github.com/pytest-dev/pytest-mock.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pytest-mock{,-py2}
}

build() {
  cd "$srcdir"/pytest-mock
  python setup.py build

  cd "$srcdir"/pytest-mock-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-mock
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH:$PWD/tests" py.test

  cd "$srcdir"/pytest-mock-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD/tests" py.test2
}

package_python-pytest-mock() {
  depends=('python-pytest')

  cd pytest-mock
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pytest-mock() {
  depends=('python2-pytest' 'python2-mock')

  cd pytest-mock-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
