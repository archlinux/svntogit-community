# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mock
pkgver=3.0.0
pkgrel=1
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
depends=('python-pytest')
makedepends=('python-setuptools-scm')
# TODO: figure out why they fail
#checkdepends=('python-pytest-asyncio')
source=("https://github.com/pytest-dev/pytest-mock/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8c967dd5aea1a26411a0b3d1347714551aad8fe5bab9ad3847d30d3a6a69dbfc5abd1e9eaaddae6771348715887dac68ce7c54bb553a4f8b5abb4abc9a7ad9ce')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-mock-$pkgver
  python setup.py build
}

check() {
  cd pytest-mock-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD:$PWD/src" pytest
}

package() {
  cd pytest-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
