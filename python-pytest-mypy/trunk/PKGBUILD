# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mypy
pkgver=0.10.3
pkgrel=1
pkgdesc='Mypy static type checker plugin for Pytest'
arch=('any')
license=('MIT')
url='https://github.com/dbader/pytest-mypy'
depends=('mypy' 'python-filelock' 'python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-xdist' 'python-pexpect')
source=("https://github.com/dbader/pytest-mypy/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('74e0a6759cedd745ea57c0025698b09fe1297fc9fa344b72130a04f43ee06b7f4ceedf8473948429086f15fbb69efed51f5c934a83343cf7b4c92a5b400e6795')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-mypy-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd pytest-mypy-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install$site_packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-mypy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
