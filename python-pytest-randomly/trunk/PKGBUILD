# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-randomly
pkgname=('python-pytest-randomly' 'python2-pytest-randomly')
pkgver=1.2.3
pkgrel=2
pkgdesc='Pytest plugin to randomly order tests and control random.seed'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-randomly'
makedepends=('python-pytest' 'python2-pytest')
checkdepends=('python-faker' 'python2-faker' 'python-numpy' 'python2-numpy' 'python-factory-boy'
              'python2-factory-boy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-randomly/archive/$pkgver.tar.gz")
sha512sums=('de4bba7d19c8e0d449174de186f0358a43a1e64a47f47de0ace1b00c6bffbc5d99328ba01f5286263e2c828008bd7f9f794d9628b6cc4e4a17c20e3fdbd85363')

prepare() {
  cp -a pytest-randomly-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-randomly-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-randomly-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-randomly-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-randomly-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-randomly() {
  depends=('python-pytest')

  cd pytest-randomly-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-randomly() {
  depends=('python2-pytest')

  cd pytest-randomly-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
