# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-randomly
pkgname=('python-pytest-randomly' 'python2-pytest-randomly')
pkgver=2.1.1
pkgrel=4
pkgdesc='Pytest plugin to randomly order tests and control random.seed'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-randomly'
makedepends=('python-pytest' 'python2-pytest')
checkdepends=('python-faker' 'python-numpy' 'python-factory-boy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-randomly/archive/$pkgver.tar.gz")
sha512sums=('4b5eb3d7f0032827a7a923fc587bafdcafab4faad399e5ac8f212359dd3d25ee4d6824ec5bd85cb232f3bab75c0bdb2eeaeca9e3d98716fbdcebab82c703fe34')

prepare() {
  cp -a pytest-randomly-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-randomly-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-randomly-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-randomly-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" py.test
}

package_python-pytest-randomly() {
  depends=('python-pytest')

  cd pytest-randomly-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-randomly() {
  depends=('python2-pytest')

  cd pytest-randomly-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
