# Maintainer: David Runge <dave@sleepmap.de>
_name=pytest-rerunfailures
pkgbase=python-pytest-rerunfailures
pkgname=('python-pytest-rerunfailures' 'python2-pytest-rerunfailures')
pkgver=6.0
pkgrel=1
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python2-mock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('654a6afea01e5d7e0f516a6427eee5bd2947757a0c05ee177521f607d8cc7e472e614267e414ce014528c4bd5de99a4bfaf5381d6c5dbf5abd9e7170a4c3fbc8')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  # 
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  export PYTHONPATH="build/lib:$PYTHONPATH"
  cd "${pkgname[0]}-${pkgver}"
  py.test test_pytest_rerunfailures.py
  cd ../"${pkgname[1]}-${pkgver}"
  py.test2 test_pytest_rerunfailures.py
}

package_python-pytest-rerunfailures() {
  depends=('python-pytest')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}

package_python2-pytest-rerunfailures() {
  depends=('python2-pytest')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}

# vim:set ts=2 sw=2 et:
