# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-runner
pkgname=('python-pytest-runner' 'python2-pytest-runner')
pkgver=2.7.1
pkgrel=1
pkgdesc="Invoke py.test as distutils command with dependency resolution"
arch=('any')
license=('MIT')
url="https://github.com/pytest-dev/pytest-runner"
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools_scm' 'python2-setuptools_scm' 'git')
source=("git+https://github.com/pytest-dev/pytest-runner.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pytest-runner{,-py2}
}

build() {
  cd "$srcdir/pytest-runner"
  python setup.py build

  cd "$srcdir/pytest-runner-py2"
  python2 setup.py build
}

package_python-pytest-runner() {
  depends=('python-pytest')

  cd pytest-runner
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pytest-runner() {
  depends=('python2-pytest')

  cd pytest-runner-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
