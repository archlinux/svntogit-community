# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=pytest-subtesthack
pkgname=python-${_name}
pkgver=0.1.1
pkgrel=6
pkgdesc="A hack to explicitly set up and tear down fixtures"
arch=('any')
url="https://github.com/untitaker/pytest-subtesthack"
license=('custom')
depends=('python-pytest' 'python-setuptools')
source=(${_name}-${pkgver}.tar.gz::"https://github.com/untitaker/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('0bf6425b535e65dc7f06f74493d10fbb6451e78a817174638661d7525f39f092f67db8da010228cbd54b400485e71e443118f91ebef660b9c00d2b354062a55f')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}

