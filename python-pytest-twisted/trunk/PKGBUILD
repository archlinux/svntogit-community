# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-twisted
pkgname=('python-pytest-twisted' 'python2-pytest-twisted')
pkgver=1.5
pkgrel=1
pkgdesc='A twisted plugin for py.test'
arch=('any')
license=('BSD')
url='http://github.com/schmir/pytest-twisted'
makedepends=('python-pytest' 'python2-pytest' 'python-greenlet' 'python2-greenlet'
             'python-decorator' 'python2-decorator' 'python-twisted' 'python2-twisted')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/schmir/pytest-twisted/archive/$pkgver.tar.gz")
sha512sums=('f6bee0fff47c064040938aee93a8ba4f3cf88222895c58d44c304d7e93605a547bfeaed3dc8a0412d41b4e75193eb3fb9e25168b98527fcdb8cb5bf54a474225')

prepare() {
  cp -a pytest-twisted-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-twisted-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-twisted-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-twisted() {
  depends=('python-pytest' 'python-greenlet' 'python-decorator' 'python-twisted')

  cd pytest-twisted-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pytest-twisted() {
  depends=('python2-pytest' 'python2-greenlet' 'python2-decorator' 'python2-twisted')

  cd pytest-twisted-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
