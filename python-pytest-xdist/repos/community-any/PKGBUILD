# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-xdist
pkgver=2.2.1
pkgrel=1
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
depends=('python-execnet' 'python-psutil' 'python-pytest-forked')
makedepends=('python-setuptools_scm')
checkdepends=('python-filelock')
source=("https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b33bfb578b2350e1ade72177771cdd25006270a26a2c7925c424f8d3772d810d28bc62bbcad76fd9139db7beb8938252f455659bf667c832436477ed241c2fab')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-xdist-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-xdist-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" pytest \
    --deselect testing/acceptance_test.py::TestWarnings::test_warning_captured_deprecated_in_pytest_6
}

package() {
  cd pytest-xdist-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
