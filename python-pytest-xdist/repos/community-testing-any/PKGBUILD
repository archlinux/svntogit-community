# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-xdist
pkgver=2.4.0
pkgrel=3
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
depends=('python-execnet' 'python-psutil' 'python-pytest-forked')
makedepends=('python-setuptools-scm')
checkdepends=('python-filelock')
source=("https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ccaaa76b18363891b45a9374c73199702e5a543d838ac86f65b3919881e45c327edccb9af5b9a5d85e35474b1863c75b4dda103feac63c3bd9536a8e9d437f80')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-xdist-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-xdist-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest \
    --deselect testing/acceptance_test.py::TestWarnings::test_warning_captured_deprecated_in_pytest_6
}

package() {
  cd pytest-xdist-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
