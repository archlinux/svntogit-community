# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=2.6.0
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'twisted' 'python-mock' 'python2-mock' 'python-yaml' 'python2-yaml' 'python-pexpect' 'python2-pexpect')
source=("http://pypi.python.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")
sha512sums=('c5a4b8fdf178832a05fe97259e0ae110c06547c7783c513fd8f631b1f4cc2d63def10beb05243c01a328e69576604560b6becd42d61c6226f9bff7ea5492fa33')

prepare() {
  cp -r pytest-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/pytest-${pkgver}"
  python setup.py build

  cd "$srcdir/pytest-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pytest-${pkgver}"
  python setup.py test

  cd "$srcdir/pytest-${pkgver}-py2"
  python2 setup.py test
}

package_python-pytest() {
  depends=('python' 'python-py' 'python-setuptools')

  cd pytest-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest() {
  depends=('python2' 'python2-py' 'python2-setuptools')

  cd pytest-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/py.test" "${pkgdir}/usr/bin/py.test2"
}

