# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=5.1.2
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
depends=('python-py' 'python-setuptools' 'python-pluggy' 'python-attrs' 'python-more-itertools'
         'python-atomicwrites' 'python-wcwidth' 'python-importlib-metadata')
makedepends=('python-setuptools-scm')
checkdepends=('lsof' 'python-nose' 'python-mock' 'python-requests' 'python-hypothesis'
              'python-argcomplete' 'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'python-numpy' 'python-twisted' 'python-pytest-xdist' 'python-decorator'
              'python-jinja' 'python-pexpect')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('5a88702b9d6822e073f16f21c8b7292afea93d7918074bb7f5c267f4954edd616a61dc3a2449e515cd87ed72a7de6c1444cda8faa02e840864441b8e55ce8fc8')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  # Skip test_collect_pyargs_with_testpaths because it mangles PYTHONPATH

  cd pytest-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD"/build/lib python src/pytest.py -k 'not test_collect_pyargs_with_testpaths'
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
