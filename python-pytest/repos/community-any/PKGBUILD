# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=2.4.2
pkgrel=3
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py')
source=("http://pypi.python.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")

prepare() {
  cp -r pytest-${pkgver} python2-pytest-${pkgver}
}

package_python-pytest() {
  depends=('python' 'python-py>=1.4.17')

  cd pytest-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest() {
  depends=('python2' 'python2-py>=1.4.17')

  cd python2-pytest-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/py.test" "${pkgdir}/usr/bin/py.test2"
}

sha512sums=('2ffbc74c536fa4542b9f9afb257428cde98648c76335bdc8d3fd515fd094410fe1e71e502918d780f1d8070bebf4e4c24b33cab04d0f48ca983d907bfc43d3db')
