# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=6.2.5
pkgrel=2
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-attrs' 'python-iniconfig' 'python-packaging' 'python-pluggy'
         'python-py' 'python-toml')
makedepends=('python-setuptools')
checkdepends=('python-argcomplete' 'python-hypothesis' 'python-mock'
              'python-nose' 'python-requests' 'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'lsof' 'python-asynctest' 'python-decorator' 'python-docutils'
              'python-jinja' 'python-numpy' 'python-pexpect' 'python-pygments'
              'python-pytest-xdist' 'python-twisted')
source=("https://files.pythonhosted.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")
sha256sums=('131b36680866a76e6781d13f101efb86cf674ebb9762eb70d3082b6f29889e89')
b2sums=('9dfc87279617be58353566009325f929e6fa53d54fd4c665a75f3b359ab5415972d745a680eb85ab21ca9eaec84450bc1decd50566b0ccdf90d7551f3d03ee70')

# Remove dep on setuptools_scm and workaround test failures
prepare() {
  cd pytest-$pkgver
  sed -i "/^\[metadata\]/a version = $pkgver
  /setuptools-scm/d" setup.cfg
  sed -i '/setuptools-scm\|wheel/d
  /\[tool.setuptools_scm\]/,/^$/d
  /_pytest.warning_types.PytestUnknownMarkWarning/a \    "ignore:Creating a LegacyVersion has been deprecated and will be removed in the next major release:DeprecationWarning",' pyproject.toml
}

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  cd pytest-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  test-env/bin/python -m pytest
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
