# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=5.0.1
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
depends=('python-py' 'python-setuptools' 'python-pluggy' 'python-attrs' 'python-more-itertools'
         'python-atomicwrites' 'python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('lsof' 'python-nose' 'python-mock' 'python-tox' 'python-yaml' 'python-pytest-xdist'
              'python-twisted' 'python-requests' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('738f8a3b38ad36be6a66ad036999a9b78023c40dd6c9cb9bd01a023841011ae276fec556eb6dabf35638a6c7cf49304c140d90d5174ded710e0f51b51145e5cb')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  # Skip test_collect_pyargs_with_testpaths because it mangles PYTHONPATH

  cd pytest-$pkgver
  PYTHONPATH="$PWD"/build/lib python src/pytest.py -k 'not test_collect_pyargs_with_testpaths'
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
