# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=6.2.3
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-attrs' 'python-iniconfig' 'python-more-itertools' 'python-packaging' 'python-pluggy'
         'python-py' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-argcomplete' 'python-hypothesis' 'python-mock' 'python-nose' 'python-requests'
              'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'python-decorator' 'python-docutils' 'python-jinja' 'python-numpy' 'python-pexpect'
              'python-pygments' 'python-twisted' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('af606d5465809fd3cfba8fd44b51af1a7571277696795818aaedaddec211d5d61ffbead01032bfe75fa0e08c581461db23dc6aa95f28fad7df634e5d12b6b14f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  sed -i '/_pytest.warning_types.PytestUnknownMarkWarning/a \    "ignore:Creating a LegacyVersion has been deprecated and will be removed in the next major release:DeprecationWarning"' pytest-$pkgver/pyproject.toml
}

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  cd pytest-$pkgver
  python setup.py egg_info
  # pdb tests: https://github.com/pytest-dev/pytest/issues/8161
  PYTHONPATH="$PWD"/build/lib python src/pytest/__main__.py
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
