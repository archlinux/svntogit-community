# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=3.0.7
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py' 'git'
             'python-pluggy' 'python2-pluggy')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-tox'
              'python2-tox' 'python-yaml' 'python2-yaml' # 'python-pexpect' 'python2-pexpect'
              'python-pytest-xdist' 'python2-pytest-xdist' 'python-twisted' 'python2-twisted'
              'python-requests' 'python2-requests' 'python-hypothesis' 'python2-hypothesis')
source=("git+https://github.com/pytest-dev/pytest.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  # Remove bundled pluggy - disabled for now as it will break tests
  # rm -r pytest/_pytest/vendored_packages
  # sed -i "s/'_pytest.vendored_packages'//" pytest/setup.py

  cp -a pytest{,-py2}
}

build() {
  cd "$srcdir"/pytest
  python setup.py build

  cd "$srcdir"/pytest-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytest
  python setup.py test

  cd "$srcdir"/pytest-py2
  python2 setup.py test
}

package_python-pytest() {
  depends=('python-py' 'python-setuptools') # 'python-pluggy')

  cd pytest
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest() {
  depends=('python2-py' 'python2-setuptools') # 'python2-pluggy')

  cd pytest-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/py.test{,2}
  mv "$pkgdir"/usr/bin/pytest{,2}
}
