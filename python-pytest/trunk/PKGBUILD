# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=5.4.3
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-py' 'python-setuptools' 'python-pluggy' 'python-attrs' 'python-more-itertools'
         'python-atomicwrites' 'python-wcwidth' 'python-importlib-metadata' 'python-pygments')
makedepends=('python-setuptools-scm')
checkdepends=('lsof' 'python-nose' 'python-mock' 'python-requests' 'python-hypothesis'
              'python-argcomplete' 'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'python-numpy' 'python-twisted' 'python-pytest-xdist' 'python-decorator'
              'python-jinja' 'python-pexpect')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('285ae5ce2b503acd15c97ae015fb7bb2fda5507e17c7c8e5b0b52526f939c818e3c8b1788290c11388cabdbb550bef4328cf3a57b4a60b42341a0470c335078f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  # Skip test_collect_pyargs_with_testpaths because it mangles PYTHONPATH

  cd pytest-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD"/build/lib python src/pytest/__main__.py -k 'not test_collect_pyargs_with_testpaths'
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
