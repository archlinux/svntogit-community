# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest32
pkgname=('python-pytest32' 'python2-pytest32')
pkgver=3.2.5
pkgrel=2
pkgdesc="Simple powerful testing with Python (Legacy 3.2.x version)"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py' 'python-pluggy'
             'python2-pluggy')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-tox'
              'python2-tox' 'python-yaml' 'python2-yaml' 'python-pytest-xdist'
              'python2-pytest-xdist' 'python-twisted' 'python2-twisted' 'python-requests'
              'python2-requests' 'python-hypothesis' 'python2-hypothesis')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('312043d7e14eeaf5dcd86890f43b8a198482246c5fa4034b116db17e494e82c9a2f29662b7e290a4c0db2128aeb69953c5c77b07969f9e76700a4d788dffa1b2')

prepare() {
  rm -r pytest-$pkgver/_pytest/vendored_packages
  sed -i "s/'_pytest.vendored_packages'//" pytest-$pkgver/setup.py
  sed -i 's/assert numentries == 0/assert numentries == 26/' pytest-$pkgver/testing/python/collect.py

  sed -i '/DeprecationWarning/a \    ignore:.*Support for __multicall__.*:DeprecationWarning' pytest-$pkgver/tox.ini
  sed -i '/DeprecationWarning/a \    ignore:.*get_result.*which forces correct exception handling.*:DeprecationWarning' pytest-$pkgver/tox.ini
  sed -i "/DeprecationWarning/a \    ignore:.*can't resolve package from __spec__ or __package__.*:ImportWarning" pytest-$pkgver/tox.ini

  cp -a pytest-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytest-$pkgver
  python setup.py test

  cd "$srcdir"/pytest-$pkgver-py2
  python2 setup.py test
}

package_python-pytest32() {
  depends=('python-py' 'python-setuptools' 'python-pluggy')
  provides=("python-pytest=$pkgver")
  conflicts=('python-pytest')

  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest32() {
  depends=('python2-py' 'python2-setuptools' 'python2-pluggy')
  provides=("python2-pytest=$pkgver")
  conflicts=('python2-pytest')

  cd pytest-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/py.test{,2}
  mv "$pkgdir"/usr/bin/pytest{,2}
}
