# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ashwin Vishnu <ashwinvis+arch at pr0t0nm4il dot com>
# Contributor: xantares

pkgname=python-pythran
pkgver=0.12.1
pkgrel=2
pkgdesc='Ahead of Time compiler for numeric kernels'
arch=(any)
url='https://pythran.readthedocs.io/'
license=(BSD)
depends=(python-networkx python-ply python-numpy python-beniget python-six xsimd boost)
makedepends=(python-setuptools python-pytest-runner)
source=(https://files.pythonhosted.org/packages/source/p/pythran/pythran-$pkgver.tar.gz)
sha256sums=('702c2701187cfb38f66c0c20cc85d04d0e156d260a8d92892da65947faa5360e')

build() {
  cd pythran-$pkgver
  python setup.py build
}

package() {
  cd pythran-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  rm -r "$pkgdir"/usr/lib/python*/site-packages/pythran/{boost,xsimd} # Remove bundled boost and xsimd
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
