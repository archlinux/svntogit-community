# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2015.1.2
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator' 'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('dff3f7b6ac1a532c5f1f842667b2d8ee3d08cd6197de224d32b73cf6d2d710016281e308c3d2d260c51d569905cc1951717fcf62a4db1562126a212ae59e54db')

prepare() {
   cp -a pytools-${pkgver}{,-python2}
}

build() {
   cd "$srcdir/pytools-$pkgver"
   python setup.py build

   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py build
}

check() {
   cd "$srcdir/pytools-$pkgver"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test

   cd "$srcdir/pytools-$pkgver-python2"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2
}

package_python2-pytools() {
   depends=('python2-six' 'python2-decorator' 'python2-appdirs')
   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python-pytools(){
   depends=('python-six' 'python-decorator' 'python-appdirs')
   cd "$srcdir/pytools-$pkgver"
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

