# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2014.2.1
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('fd4532b521dbdbec0ff0d6fd838427f05cce84ac1f7a836fde36445f4ce0532369f6f1746d552afcf2c4e83b35158520c3910fa2a08ff82e14effc059ea6bbfb')

prepare() {
   cp -a pytools-${pkgver}{,-python2}
}

build() {
   cd "$srcdir/pytools-$pkgver"
   python setup.py build

   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py build
}

check() {
   # Need pytools installed for running tests

   cd "$srcdir/pytools-$pkgver"
   #py.test

   cd "$srcdir/pytools-$pkgver-python2"
   #py.test2
}

package_python2-pytools() {
   depends=('python2' 'python2-decorator')
   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python-pytools(){
   depends=('python' 'python-decorator')
   cd "$srcdir/pytools-$pkgver"
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

