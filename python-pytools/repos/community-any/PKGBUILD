# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2014.3.5
pkgrel=2
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator' 'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('94d2b51cdd87dd11263fdc7d04040979240dea0e0090da0ff325112df9770aacb0849e20812b90f807024ad3815313bc344307a30add8f6795a70e7ebd6adfaa')

prepare() {
   cp -a pytools-${pkgver}{,-python2}
}

build() {
   cd "$srcdir/pytools-$pkgver"
   python setup.py build

   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py build
}

check() {
   cd "$srcdir/pytools-$pkgver"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test

   cd "$srcdir/pytools-$pkgver-python2"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2
}

package_python2-pytools() {
   depends=('python2-six' 'python2-decorator' 'python2-appdirs')
   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python-pytools(){
   depends=('python-six' 'python-decorator' 'python-appdirs')
   cd "$srcdir/pytools-$pkgver"
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

