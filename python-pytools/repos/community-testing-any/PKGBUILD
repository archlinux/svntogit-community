# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pytools
pkgver=2020.4.3
pkgrel=3
pkgdesc="A collection of tools for Python"
arch=('any')
url="https://mathema.tician.de/software/pytools"
license=('MIT')
depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('4f162114fee298812c0f63b0af1c4113fa1636b6dd4bb750b18decb60d7ad89adc2e460b9861c46ee1c8e11ca3ec437836ff173abe4436ba7784f18b1d6455a6')

build() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py pytest
}

package(){
  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
