# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2014.3
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('8c94e185ff6f22b2051518d209373db92d2c9f993e3de803928f588a43d893259bdc378b0d65faef27301aa794c7d86afeea8cf055e19401182f87bd119af137')

prepare() {
   cp -a pytools-${pkgver}{,-python2}
}

build() {
   cd "$srcdir/pytools-$pkgver"
   python setup.py build

   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py build
}

check() {
   # Need pytools installed for running tests

   cd "$srcdir/pytools-$pkgver"
   #py.test

   cd "$srcdir/pytools-$pkgver-python2"
   #py.test2
}

package_python2-pytools() {
   depends=('python2' 'python2-decorator')
   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python-pytools(){
   depends=('python' 'python-decorator')
   cd "$srcdir/pytools-$pkgver"
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

