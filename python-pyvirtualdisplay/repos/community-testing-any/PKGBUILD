# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=PyVirtualDisplay
pkgname=python-${_pkg,,}
pkgver=1.3.2
pkgrel=3
pkgdesc="Python wrapper for Xvfb, Xephyr and Xvnc"
arch=(any)
url="https://github.com/ponty/PyVirtualDisplay"
license=(BSD)
depends=(python-easyprocess)
makedepends=(python-setuptools)
checkdepends=(
    python-pytest python-pyscreenshot python-pillow
    python-entrypoint2 python-path.py xorg-server-xvfb xorg-server-xephyr
    xorg-xmessage xorg-xdpyinfo scrot
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
#source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3fa85a6e490e45eab64e6be19841e0ab15ec8054c97f162079a061da6a93eba0')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  # Test.test_slowshot and Test.test_slowshot_wrap are timing out
  PYTHONPATH="${PWD}/build/lib" pytest tests || echo "Tests failed"
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
