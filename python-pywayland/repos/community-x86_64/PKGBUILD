# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywayland
pkgname=python-pywayland
pkgver=0.4.13
pkgrel=1
pkgdesc="Python bindings for the libwayland library"
arch=(x86_64)
url="https://github.com/flacjacket/pywayland"
license=(Apache)
depends=(python-cffi wayland)
makedepends=(python-build python-installer python-setuptools wayland-protocols python-wheel)
checkdepends=(python-pytest)
options=(debug)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('7e4de2bcbbf996e9f43d5f551af2cbfa02b848f02e96020b5fe8e4c85e4566e6990653dc1344eea4c1473b3f3e1481cad4a9ea62e98508efb1b7c1589c2cee1a')
b2sums=('49456e892fef0f9324e55a3deeb91eaf3fcfeba41aded994a2ebac1e800caeccabf01be683f0b2ee8ea07759fb1296d864dc404ac640ddff02bed229b20b7ddf')

build() {
  cd $_name-$pkgver
  python pywayland/ffi_build.py
  python -m pywayland.scanner
  python -m build --wheel --no-isolation
}

check() {
  local _py_ver="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"

  cd $_name-$pkgver
  mkdir -vp temp
  export PYTHONPATH="build/lib.linux-$CARCH-$_py_ver:$PYTHONPATH"
  export XDG_RUNTIME_DIR="$PWD/temp"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
}
