# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Kyle Keen <keenerd at gmail dot com>

pkgname=python-pyzmq
_pkgname="${pkgname#python-}"
pkgver=24.0.1
pkgrel=2
pkgdesc='Python bindings for ZeroMQ'
arch=('x86_64')
url='https://zeromq.org/languages/python/'
license=('LGPL')
depends=('zeromq' 'python')
makedepends=('python-setuptools')
checkdepends=(
  'python-flaky'
  'python-gevent'
  'python-tornado'
  'python-pytest'
  'python-pytest-rerunfailures'
  'python-pytest-asyncio'
  'cython'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/zeromq/pyzmq/archive/v$pkgver.tar.gz")

sha512sums=('d4b6346e780f356941a624321f1619771fb2f0ef9f65a3c0a03e837ef081ece7ed59c03f57e8647ab1cd38cfc7404d597dd294768f9e9b890115b9eebc085484')
b2sums=('01928ba3c64a5afce8e7a97821658da2067a5f6c36c0173ab2f818e83a729939f346ef67bb9a39251abf365f5772abdf7e9e62afb4cc434b5603f474485a7427')

check() {
  cd "$_pkgname-$pkgver"

  # https://github.com/zeromq/pyzmq/issues/1806
  #python setup.py test

  python setup.py build_ext --inplace

  pytest -v \
    --deselect zmq/tests/test_poll.py::TestPoll::test_timeout \
    --deselect zmq/tests/test_poll.py::TestSelect::test_timeout \
    --deselect zmq/tests/test_poll.py::TestPollGreen::test_timeout \
    --deselect zmq/tests/test_retry_eintr.py::TestEINTRSysCall::test_retry_poll
}

package() {
  cd "$_pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
}
