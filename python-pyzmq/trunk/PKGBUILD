# $Id$
# Maintainer: Kyle Keen <keenerd at gmail dot com>
pkgbase=python-pyzmq
pkgname=(python-pyzmq python2-pyzmq)
pkgver=17.1.0
pkgrel=1
pkgdesc="Python bindings for zeromq, written in Cython"
arch=('x86_64')
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
depends=('zeromq' 'python')
makedepends=('python' 'python2')
checkdepends=('python-nose' 'python2-nose' 'python-tornado' 'python2-tornado'
    'python-pytest' 'python2-pytest')
#source=("https://github.com/zeromq/pyzmq/downloads/pyzmq-$pkgver.tar.gz")
#source=("https://pypi.python.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz")
md5sums=('aecdfc328193fbd81f6dc23228319943')

prepare() {
  cd "$srcdir"
  cp -a pyzmq-${pkgver} py2zmq-${pkgver}
}

check() {
  # tests currently don't pass for python 3.6
  cd "$srcdir/pyzmq-$pkgver"
  python3 setup.py build_ext --inplace
  python3 setup.py test
  cd "$srcdir/py2zmq-$pkgver"
  python2 setup.py build_ext --inplace
  python2 setup.py test
}

package_python-pyzmq() {
  pkgdesc="Python3 bindings for zeromq, written in Cython"
  cd "$srcdir/pyzmq-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=0
}

package_python2-pyzmq() {
  pkgdesc="Python2 bindings for zeromq, written in Cython"
  depends=('zeromq' 'python2')
  cd "$srcdir/py2zmq-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
}
