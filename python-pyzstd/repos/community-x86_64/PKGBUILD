# Maintainer:

_pkgname=pyzstd
pkgname=python-$_pkgname
pkgver=0.15.0
pkgrel=1
pkgdesc='Python bindings to Zstandard (zstd) compression library'
arch=(x86_64)
url='https://github.com/animalize/pyzstd'
license=(LGPL)
depends=(python zstd)
makedepends=(python-setuptools-scm)
checkdepends=(python-tests)
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('bf15a39cb3c9b662775e22ffa8c4da09fdde6a15ece5e0ed710b6d3b4329cf36')

build() {
  cd $_pkgname-$pkgver

  python setup.py build --dynamic-link-zstd
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH="$PWD"/`ls -d build/lib.*` python setup.py test
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
