# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: gmes78 <gmes.078 at gmail dot com>

pkgname=python-qasync
pkgver=0.9.5
pkgrel=1
pkgdesc="Python library for using asyncio in Qt-based applications"
arch=(any)
url="https://github.com/CabbageDevelopment/qasync"
license=('BSD')
depends=('python' 'qt5-python-bindings')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pyqt5' 'pyside2' 'xorg-server-xvfb')
source=("https://github.com/CabbageDevelopment/qasync/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4402f903a4e3e7447b5878d2da95fce9d12f89ad153f93e7ea1ede591451a9ffd1260e909dc2aef5d36cabc3c056a6245820ff77a4fbd8ed046c267fd6f5ac95')

build() {
  cd qasync-$pkgver
  python setup.py build
}

check() {
  cd qasync-$pkgver
  QT_API=PyQt5 xvfb-run python -m pytest
  QT_API=PySide2 xvfb-run python -m pytest
}

package() {
  cd qasync-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
