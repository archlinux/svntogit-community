# $Id$
# Maintainer: Martin Wimpress <code@flexion.org>

pkgbase=python-qrencode
pkgname=(python-qrencode python2-qrencode)
pkgver=1.01
pkgrel=9
pkgdesc="A simple wrapper for the C qrencode library"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/qrencode"
license=('Apache')
makedepends=('gcc' 'python' 'python2' 'python-pillow' 'python2-pillow' 'qrencode')
source=("http://pypi.python.org/packages/source/q/qrencode/qrencode-${pkgver}.tar.gz")
sha256sums=('fd8cdabd743e2db7a1ad332d4f0fe037e5a7807cb2f1d32da3e39439088bb610')

prepare() {
    cd "${srcdir}/qrencode-${pkgver}"
    sed -i 's/import Image/from PIL import Image/' qrencode/__init__.py
}

package_python-qrencode() {
    depends=('python' 'python-pillow' 'qrencode')
    cd "${srcdir}/qrencode-${pkgver}"
    python setup.py install --prefix=/usr --root="${pkgdir}"
}

package_python2-qrencode() {
    depends=('python2' 'python2-pillow' 'qrencode')
    cd "${srcdir}/qrencode-${pkgver}"
    python2 setup.py install --prefix=/usr --root="${pkgdir}"
}

