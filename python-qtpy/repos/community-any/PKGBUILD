# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Muflone http://www.muflone.com/contacts/english/

_pkgname=qtpy
pkgname=python-qtpy
pkgver=2.0.1
pkgrel=1
pkgdesc="Provides an uniform layer to support PyQt5 and PySide2 with a single codebase"
arch=(any)
url="https://github.com/spyder-ide/qtpy/"
license=(MIT)
depends=(python-packaging)
makedepends=(python-setuptools)
optdepends=('python-pyqt5: Qt5 Python bindings'
            'python-pyqt6: Qt6 Python bindings'
            'python-pyqt5-webengine: Qt5-WebEngine python bindings'
            'python-pyqt6-webengine: Qt6-WebEngine python bindings'
            'pyside2: PySide Qt5 Python bindings'
            'pyside6: PySide Qt6 Python bindings')
# The test suite does not support both stacks at the same time
checkdepends=(
    python-pytest
    python-pytest-qt
    python-mock
    python-pyqt5
    python-pyqt5-3d
    python-pyqt5-datavisualization
    python-pyqt5-webengine
    #python-pyqt6
    #python-pyqt6-3d
    #python-pyqt6-datavisualization
    #python-pyqt6-webengine
    pyside2
    #pyside6
    qt5-multimedia
    qt5-remoteobjects
    qt5-sensors
    qt5-serialport
    qt5-speech
    qt5-svg
    qt5-tools
    qt5-websockets
    qt5-xmlpatterns
    #qt6-multimedia
    #qt6-remoteobjects
    #qt6-sensors
    #qt6-serialport
    #qt6-svg
    #qt6-tools
    xorg-server-xvfb
)   
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('905e801ee0c5fa65c8e0cff1067f99f9c25f69014a5e0674011ce53894f7132b')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  sed -i 's|--cov=qtpy --cov-report=term-missing||' pytest.ini
  PYTHONPATH="${PWD}"/build/lib xvfb-run --auto-servernum pytest qtpy
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
