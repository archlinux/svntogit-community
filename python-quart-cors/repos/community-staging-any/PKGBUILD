# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-quart-cors
pkgver=0.2.0
pkgrel=2
pkgdesc='A Quart extension to provide Cross Origin Resource Sharing, access control, support'
url=https://gitlab.com/pgjones/quart-cors
arch=(any)
license=(MIT)
depends=(
  python
  python-quart
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
  python-pytest-sugar
  python-tox
)
source=(git+https://gitlab.com/pgjones/quart-cors.git#tag=${pkgver})
sha256sums=('SKIP')

build() {
  cd quart-cors

  python setup.py build
}

check() {
  cd quart-cors

  tox -e py38
}

package() {
  cd quart-cors

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-quart-cors/
}

# vim: ts=2 sw=2 et:
