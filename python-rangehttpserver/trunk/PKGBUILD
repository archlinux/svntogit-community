# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=RangeHTTPServer
pkgbase=python-rangehttpserver
pkgname=(python-rangehttpserver python2-rangehttpserver)
pkgver=1.2.0
pkgrel=3
pkgdesc='SimpleHTTPServer with support for Range requests'
url='https://github.com/danvk/RangeHTTPServer/'
arch=('any')
license=('Apache')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
checkdepends=('python-nose' 'python-requests'
              'python2-nose' 'python2-requests')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/danvk/RangeHTTPServer//archive/${pkgver}.tar.gz)
sha256sums=('17aad58bec8ccd95f9d5bc9cff2bf3597d002eff08e1ced558c8756b61467a94')
sha512sums=('5964687bb3e8524ab3eea0b71eaefe189f795ce73ba438510936480bbdfda3dcfafe97ace66b35b3b235864f71744fe20c8fb90f881628ad67a53162e22b59bb')

prepare() {
  cp -a ${_pkgname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    nosetests
  )
  (cd ${_pkgname}-${pkgver}-py2
    nosetests2
  )
}

package_python-rangehttpserver() {
  depends=('python')
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-rangehttpserver() {
  depends=('python2')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
