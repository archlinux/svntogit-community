# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Benjamin A. Shelton <zancarius@gmail.com>

pkgbase=python-raven
pkgname=(python-raven python2-raven)
pkgver=6.9.0
pkgrel=2
pkgdesc="Python client for Sentry"
arch=('any')
url="http://pypi.python.org/pypi/raven"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python2-contextlib2')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-bottle' 'python2-bottle'
              'python-celery' 'python2-celery' 'python-django' 'python2-django'
              'python-pycodestyle' 'python2-pycodestyle' 'python-mock' 'python2-mock' 'python-nose'
              'python2-nose' 'python2-gevent' 'python-pytz' 'python2-pytz' 'python-exam'
              'python2-exam' 'python-requests' 'python2-requests' 'python-tornado'
              'python2-tornado' 'python-paste' 'python2-paste' 'python-webob' 'python2-webob'
              'python-webtest' 'python2-webtest' 'python-anyjson' 'python2-anyjson'
              'python-flask' 'python2-flask' 'python-blinker' 'python2-blinker' 'python-logbook'
              'python2-logbook' 'python-pytest-django' 'python2-pytest-django' 'python2-webpy'
              'python-flask-login' 'python2-flask-login' 'python-pytest-timeout'
              'python2-pytest-timeout' 'python-zconfig' 'python2-zconfig' 'python-pytest-xdist'
              'python2-pytest-xdist')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/getsentry/raven-python/archive/$pkgver.tar.gz")
sha512sums=('a8ab761779b9c4cf06150a763d31f56caf1688210354deeb065950b82276b6f7d4c9daff65bfd9fa865093350a2c65b983f7d8662d3232ea3b0304a8027276a9')

prepare() {
  cp -a raven-python-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/raven-python-$pkgver
  python setup.py build

  cd "$srcdir"/raven-python-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack distribution check by installing it
  # "-p no:logging": https://github.com/getsentry/raven-python/issues/1164

  cd "$srcdir"/raven-python-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" \
    py.test -p no:logging tests

  cd "$srcdir"/raven-python-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" \
    py.test2 -p no:logging tests
}

package_python-raven() {
  depends=('python')
  optdepends=('python-setuptools: for "raven" script')

  cd "$srcdir"/raven-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-raven() {
  depends=('python2-contextlib2')
  optdepends=('python2-setuptools: for "raven2" script')

  cd "$srcdir"/raven-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/raven{,2}

  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
