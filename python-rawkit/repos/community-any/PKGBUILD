# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=rawkit
pkgbase=python-rawkit
pkgname=('python-rawkit' 'python2-rawkit')
pkgver=0.6.0
pkgrel=4
pkgdesc="ctypes based libraw bindings"
arch=('any')
url="https://rawkit.readthedocs.io/"
license=('MIT')
depends=('libraw')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-mock' 'python2-mock' 'python-pytest' 'python2-pytest')
source=("${_pkgname}-${pkgver}.tar.gz::https://github.com/photoshell/rawkit/archive/v${pkgver}.tar.gz"
        "https://github.com/photoshell/rawkit/pull/140/commits/663e90afa835d398aedd782c87b8cd0bff64bc9f.patch")
sha256sums=('c07971ddbca9568cef93753ebb06a72b5b622f9b827a5d8bf96f2a137cc4219e'
            '1297740c3825d41b394808b3be145d0f41effc415e5dbd6fd1e17e51328ce418')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # python 3.7
    patch -p1 -i ../663e90afa835d398aedd782c87b8cd0bff64bc9f.patch
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
    python2 -m pytest
}

package_python-rawkit() {
    depends+=('python')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-rawkit() {
    depends+=('python2')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
