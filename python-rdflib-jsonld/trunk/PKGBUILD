# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rdflib-jsonld
pkgver=0.4.0
pkgrel=1
pkgdesc="JSON-LD parser and serializer plugins for RDFLib"
url="https://github.com/RDFLib/rdflib-jsonld"
license=('BSD')
arch=('any')
depends=('python-rdflib')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RDFLib/rdflib-jsonld/archive/$pkgver.tar.gz")
sha512sums=('733c5aa570a917f0cb7f19d1aaad375a6b7c79519599fa159fcbbc00c863ec4c7ccff47b41ddd18d9dd25c3e57768b877f5e21630aba662cf43c2da2a764f62d')

build() {
  cd rdflib-jsonld-$pkgver
  python setup.py build
}

check() {
  cd rdflib-jsonld-$pkgver
  python setup.py nosetests
}

package() {
  cd rdflib-jsonld-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
