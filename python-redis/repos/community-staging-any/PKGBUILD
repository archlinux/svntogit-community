# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgbase=python-redis
pkgname=('python-redis' 'python2-redis')
_basename=redis
pkgver=2.10.5
pkgrel=2
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="http://github.com/andymccurdy/redis-py"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'redis')
source=("https://pypi.python.org/packages/source/r/${_basename}/${_basename}-${pkgver}.tar.gz")
md5sums=('3b26c2b9703b4b56b30a1ad508e31083')

prepare() {
  cp -a ${_basename}-${pkgver}{,-py2}
}

build() {
  cd ${_basename}-${pkgver}
  python setup.py build

  cd ../${_basename}-${pkgver}-py2
  python2 setup.py build
}

check() {
  _run() {
    rm -f "$srcdir/redis.pid"
    redis-server --daemonize yes --pidfile "$srcdir/redis.pid"
    "$@"
    kill $(cat "$srcdir/redis.pid")
    sleep 1 # :/
  }

  cd ${_basename}-${pkgver}
  _run py.test

  cd ../${_basename}-${pkgver}-py2
  _run py.test2
}

package_python-redis() {
  depends=('python')

  cd ${_basename}-${pkgver}
  python setup.py install --root=${pkgdir}/ --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-redis() {
  depends=('python2')

  cd ${_basename}-${pkgver}-py2
  python2 setup.py install --root=${pkgdir}/ --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
