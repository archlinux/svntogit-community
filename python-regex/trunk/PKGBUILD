# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgname=python-regex
pkgver=2020.10.15
pkgrel=1
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python')
depends=('python')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('d25f5cca0f3af6d425c9496953445bf5b288bb5b71afc2b8308ad194b714c159')
b2sums=('6e5dd6129b63e1c4e413df42d4fb910d7e79ab2d07f2a433296db49b4cc60fedf9bb4859a59e7dff40234261dfaa313516085a86acf95deb3b77393a966b8373')

build() {
  cd "regex-${pkgver}"

  python setup.py build
}

check() {
  cd "regex-${pkgver}"

  cd build/lib.linux-${CARCH}-3*/
  python -m unittest regex/test_regex.py
}

package() {
  cd "regex-${pkgver}"

  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  sed -n '1,/^$/p' regex_3/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
