# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgbase=python-regex
pkgname=('python-regex' 'python2-regex')
pkgver=2019.12.20
pkgrel=1
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python')
makedepends=('python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('106e25a841921d8259dcef2a42786caae35bc750fb996f830065b3dfaa67b77e')
b2sums=('c3dcadced20fbec453f93cd37d2f0bdae504e8cd00f75a5b8002c5773ceec4c203214724d5016367ff76238f055aa641a5038aca3a6a8838e961a424571017ed')

build() {
  cd "regex-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "regex-${pkgver}"

  pushd build/lib.linux-${CARCH}-3*/
  python -c 'from regex.test_regex import test_main; test_main()'
  popd

  pushd build/lib.linux-${CARCH}-2*/
  python2 -c 'from regex.test_regex import test_main; test_main()'
  popd
}

package_python2-regex() {
  depends=('python2')
  pkgdesc="Alternative python regular expression module. (python2 version)"

  cd "regex-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  sed -n '1,/^$/p' regex_2/regex/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-regex() {
  depends=('python')
  pkgdesc="Alternative python regular expression module. (python3 version)"

  cd "regex-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  sed -n '1,/^$/p' regex_3/regex/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
