# $Id$
# Maintainer:
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-reportlab
pkgname=(python2-reportlab python-reportlab)
_origname=reportlab
pkgver=3.2.0
pkgrel=2
pkgdesc="A proven industry-strength PDF generating solution"
arch=('i686' 'x86_64')
url="http://www.reportlab.org/rl_toolkit.html"
depends=('freetype2')
conflicts=('python-reportlab<=2.5-2')
makedepends=('python' 'python2')
checkdepends=('python2-pillow' 'python-pillow')
replaces=('python-reportlab<=2.5-2')
license=('CUSTOM')
source=("https://pypi.python.org/packages/source/r/reportlab/reportlab-$pkgver.tar.gz")
md5sums=('79d059e797c557aed4b40c68dd6c7eae')

build() {
    cd $srcdir
    cp -rf ${_origname}-$pkgver "${_origname}2-$pkgver"
}

package_python2-reportlab() {
    depends=('python2')
    cd "$srcdir/${_origname}2-$pkgver"
    python2 setup.py install --root=$pkgdir --optimize=1
    install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}

package_python-reportlab() {
    depends=('python3')
    cd "$srcdir/${_origname}-$pkgver"
    python3 setup.py install --root=$pkgdir --optimize=1
    install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}


check() {
    cd ${srcdir}/${_origname}-${pkgver}
    python2 setup.py tests
    python setup.py tests
}
