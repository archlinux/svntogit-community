# $Id$
# Maintainer:
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-reportlab
pkgname=(python2-reportlab python-reportlab)
_origname=reportlab
pkgver=3.4.0
pkgrel=1
pkgdesc="A proven industry-strength PDF generating solution"
arch=('x86_64')
url="http://www.reportlab.org/rl_toolkit.html"
depends=('freetype2')
conflicts=('python-reportlab<=2.5-2')
makedepends+=('python' 'python2' 'freetype2' 'python2-pillow' 'python-pillow')
replaces=('python-reportlab<=2.5-2')
license=('CUSTOM')
source=("https://pypi.python.org/packages/87/f9/53b34c58d3735a6df7d5c542bf4de60d699cfa6035e113ca08b3ecdcca3f/reportlab-3.4.0.tar.gz")
md5sums=('3f2522cf3b69cd84426c216619bbff53')

build() {
    cd $srcdir
    cp -rf ${_origname}-$pkgver "${_origname}2-$pkgver"
}

package_python2-reportlab() {
    depends+=('python2' 'python2-pip' 'python2-pillow')

    cd "$srcdir/${_origname}2-$pkgver"
    python2 setup.py install --root=$pkgdir --optimize=1
    install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}

package_python-reportlab() {
    depends+=('python3' 'python-pip' 'python-pillow')

    cd "$srcdir/${_origname}-$pkgver"
    python3 setup.py install --root=$pkgdir --optimize=1
    install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}


check() {
    cd ${srcdir}/${_origname}-${pkgver}
    python2 setup.py tests
    python setup.py tests
}
