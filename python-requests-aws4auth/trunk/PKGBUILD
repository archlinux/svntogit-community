# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

_pkgname=requests-aws4auth
pkgbase=python-requests-aws4auth
pkgname=(python-requests-aws4auth python2-requests-aws4auth)
pkgver=0.9
pkgrel=2
pkgdesc='Amazon Web Services version 4 authentication for the Python Requests module'
url='https://github.com/sam-washington/requests-aws4auth'
arch=('x86_64')
license=('MIT')
makedepends=('python-setuptools' 'python2-requests'
             'python2-setuptools' 'python-requests')
checkdepends=('python-pytest' 'python2-pytest')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sam-washington/requests-aws4auth/archive/${pkgver}.tar.gz)
sha512sums=('cf378f3ac97fe44000c17c15a5d75774e95dd9ddab7f4d550f676505de1641df79b0209750c376d51e9e49c7114b2b6891e292ab3e55313ee127c924217fe8a2')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed "s|'test/requests_aws4auth_test.py',||" -i setup.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  python2 setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python -m pytest -k 'not test_headers_amz_example'
  python2 -m pytest -k 'not test_headers_amz_example'
}

package_python-requests-aws4auth() {
  depends=('python' 'python-requests')
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE NOTICE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-requests-aws4auth() {
  depends=('python2' 'python2-requests')
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE NOTICE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
