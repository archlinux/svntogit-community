# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-requests-kerberos
pkgname=('python-requests-kerberos' 'python2-requests-kerberos')
pkgver=0.9.0
pkgrel=1
pkgdesc="A kerberos of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/requests/requests-kerberos"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-pykerberos' 'python2-pykerberos' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("git+https://github.com/requests/requests-kerberos.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a requests-kerberos{,-py2}
}

build() {
  cd "$srcdir"/requests-kerberos
  python setup.py build

  cd "$srcdir"/requests-kerberos-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/requests-kerberos
  py.test

  cd "$srcdir"/requests-kerberos-py2
  py.test2
}

package_python-requests-kerberos() {
  depends=('python-requests' 'python-pykerberos')

  cd "$srcdir"/requests-kerberos
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-requests-kerberos() {
  depends=('python2-requests' 'python2-pykerberos')

  cd "$srcdir"/requests-kerberos-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
