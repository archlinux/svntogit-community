# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-requests-kerberos
pkgname=('python-requests-kerberos' 'python2-requests-kerberos')
pkgver=0.12.0
pkgrel=1
pkgdesc="A kerberos of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/requests/requests-kerberos"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-pykerberos' 'python2-pykerberos' 'python-cryptography' 'python2-cryptography')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/requests/requests-kerberos/archive/v$pkgver.tar.gz")
sha512sums=('b925b5f9f6753e87368dce451927df09568d0d6126d4ae8514bbd99a794d983e32298b92b6239be9f95595483e2682259cf165c6997d17d9cea64a81d4af1a06')

prepare() {
  cp -a requests-kerberos-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/requests-kerberos-$pkgver
  python setup.py build

  cd "$srcdir"/requests-kerberos-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/requests-kerberos-$pkgver
  py.test

  cd "$srcdir"/requests-kerberos-$pkgver-py2
  py.test2
}

package_python-requests-kerberos() {
  depends=('python-requests' 'python-pykerberos' 'python-cryptography')

  cd "$srcdir"/requests-kerberos-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-requests-kerberos() {
  depends=('python2-requests' 'python2-pykerberos' 'python2-cryptography')

  cd "$srcdir"/requests-kerberos-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
