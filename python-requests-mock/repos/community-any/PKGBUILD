# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requests-mock
pkgver=1.7.0
pkgrel=4
pkgdesc="A mock of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/jamielennox/requests-mock"
license=('Apache')
depends=('python-requests' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-testrepository' 'python-mock' 'python-purl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jamielennox/requests-mock/archive/$pkgver.tar.gz")
sha512sums=('a5a2ccc6d23fd47f6b85b7b27c20dbdff303452bc34eef78f2b3316d020cdd21631d840fe798cb721aa1ddedc830b0284a8c042c9d18142513dc61bda1207cab')

export PBR_VERSION=$pkgver

build() {
  cd requests-mock-$pkgver
  python setup.py build
}

check() {
  cd requests-mock-$pkgver
  python setup.py testr
}

package() {
  cd requests-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
