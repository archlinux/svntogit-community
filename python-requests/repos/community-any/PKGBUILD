# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-requests
pkgname=(python-requests python2-requests)
pkgver=2.0.1
pkgrel=1
_libname=requests
pkgdesc="Python HTTP for Humans."
url="http://python-requests.org"
makedepends=('python-setuptools' 'python2-setuptools')
license=('Apache')
arch=('any')
source=(http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz
        certs.patch)

prepare() {
    cd "$srcdir/$_libname-$pkgver"
    patch -p0 -i "$srcdir/certs.patch"
    sed -r 's#(\W|^)requests/cacert\.pem(\W|$)##' -i MANIFEST.in
    rm -f requests/cacert.pem
}

build() {
    cd "$srcdir/$_libname-$pkgver"

    rm -rf ../buildpy3; mkdir ../buildpy3
    python setup.py build -b ../buildpy3

    rm -rf ../buildpy2; mkdir ../buildpy2
    python2 setup.py build -b ../buildpy2
    find ../buildpy2 -name \*.py -exec sed -r 's|^#!(.*)python$|#!\1python2|' -i {} +
}

check() {
    cd "$srcdir/$_libname-$pkgver"
    test -f "$(python -m requests.certs)"
}

package_python-requests() {
    depends=(python)

    cd "$srcdir/$_libname-$pkgver"
    rm -rf build; ln -s ../buildpy3 build
    python setup.py install --skip-build -O1 --root="$pkgdir"
    install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-requests() {
    depends=(python2)
    optdepends=('python2-ndg-httpsclient: HTTPS requests with SNI support'
                'python2-grequests: asynchronous requests with gevent'
                'python2-simplejson')

    cd "$srcdir/$_libname-$pkgver"
    rm -rf build; ln -s ../buildpy2 build
    python2 setup.py install --skip-build -O1 --root="$pkgdir"
}

sha256sums=('8cfddb97667c2a9edaf28b506d2479f1b8dc0631cbdcd0ea8c8864def59c698b'
            '55f8ae9c2a81c65fb2e8d94b23cff58524ddfbb31ea5180219e6a79db2975805')
