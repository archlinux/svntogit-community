# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-requestsexceptions
pkgname=(python-requestsexceptions python2-requestsexceptions)
pkgver=1.2.0
pkgrel=1
pkgdesc="Import exceptions from potentially bundled packages in requests."
arch=('any')
url="https://github.com/openstack-infra/requestsexceptions"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-requests' 'python2-requests')
source=("git+https://github.com/openstack-infra/requestsexceptions.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a requestsexceptions{,-py2}
}

build() {
  cd "$srcdir"/requestsexceptions
  python setup.py build

  cd "$srcdir"/requestsexceptions-py2
  python2 setup.py build
}

package_python-requestsexceptions() {
  depends=('python-pbr' 'python-requests')

  cd "$srcdir"/requestsexceptions
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-requestsexceptions() {
  depends=('python2-pbr' 'python2-requests')

  cd "$srcdir"/requestsexceptions-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
