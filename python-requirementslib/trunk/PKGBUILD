# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requirementslib
pkgver=2.0.1
pkgrel=1
pkgdesc="A tool for converting between pip-style and pipfile requirements"
url="https://github.com/sarugaku/requirementslib"
license=('MIT')
arch=('any')
depends=('python-attrs' 'python-cached-property' 'python-distlib'  'python-packaging'
         'python-pep517' 'python-pip' 'python-platformdirs' 'python-plette' 'python-cerberus'
         'python-requests' 'python-setuptools' 'python-tomlkit' 'python-vistir')
makedepends=('python-invoke' 'python-wheel' 'python-parver' 'git')
checkdepends=('python-pytest-sugar' 'python-pytest-timeout' 'python-pytest-xdist'
              'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/requirementslib/archive/$pkgver.tar.gz"
        "git+https://github.com/kennethreitz/requests.git#commit=64bde6582d9b49e9345d9b8df16aaa26dc372d13"
        "git+https://github.com/sarugaku/shellingham.git#commit=9abe7464dab5cc362fe08361619d3fb15f2e16ab"
        "git+https://github.com/pyinstaller/pyinstaller.git#commit=6d4cce1f8eb80c0bf49d35876d28daa55a05800f")
sha512sums=('7cd34c8114ebee6bd9179eaa27d12562a83c2b0062f0ae482bf0d4d48e860670811dd586be614dbfce16fb68423c65946b8c23ef07597249ab893ed5ceed1959'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd requirementslib-$pkgver

  sed -i 's/pyparsing<3.0.0/pyparsing/' setup.cfg

  cd tests/artifacts/git
  rmdir requests shellingham pyinstaller
  ln -s "$srcdir"/requests
  ln -s "$srcdir"/shellingham
  ln -s "$srcdir"/pyinstaller
}

build() {
  cd requirementslib-$pkgver
  python setup.py build
}

check() {
  cd requirementslib-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest || echo "Tests failed"
}

package() {
  cd requirementslib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
