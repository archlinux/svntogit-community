# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requirementslib
pkgver=1.5.14
pkgrel=1
pkgdesc="A tool for converting between pip-style and pipfile requirements"
url="https://github.com/sarugaku/requirementslib"
license=('MIT')
arch=('any')
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-distlib'
         'python-orderedmultidict' 'python-packaging' 'python-pep517' 'python-pip-shims'
         'python-plette' 'python-cerberus' 'python-dateutil' 'python-requests' 'python-setuptools'
         'python-tomlkit' 'python-vistir')
makedepends=('python-invoke' 'python-wheel' 'python-parver' 'git')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-sugar' 'python-pytest-timeout'
              'python-pytest-xdist' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/requirementslib/archive/$pkgver.tar.gz"
        "git+https://github.com/kennethreitz/requests.git#commit=64bde6582d9b49e9345d9b8df16aaa26dc372d13"
        "git+https://github.com/sarugaku/shellingham.git#commit=9abe7464dab5cc362fe08361619d3fb15f2e16ab")
sha512sums=('a98e33c13aaa367604eea729c1b5c9a7182eb9343599b2118d59004cc922b9dff706908f965c12b522795ace073ea9258d482d38250cdb013581867063f03ce8'
            'SKIP'
            'SKIP')

prepare() {
  cd requirementslib-$pkgver
  sed -i 's/pkg_resources.extern.//' src/requirementslib/models/*.py

  # https://github.com/sarugaku/requirementslib/issues/181
  sed -e 's/list(MARKER_OP)/MARKER_OP.exprs/' -e 's/list(VARIABLE)/VARIABLE.exprs/' -i tests/unit/strategies.py

  # https://github.com/sarugaku/requirementslib/issues/183
  sed -i 's/not isinstance(source\["verify_ssl"\], bool)/isinstance(source["verify_ssl"], str)/' -i src/requirementslib/models/pipfile.py

  cd tests/artifacts/git
  rmdir requests shellingham
  ln -s "$srcdir"/requests
  ln -s "$srcdir"/shellingham
}

build() {
  cd requirementslib-$pkgver
  python setup.py build
}

check() {
  cd requirementslib-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd requirementslib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
