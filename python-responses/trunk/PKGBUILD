# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.10.14
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'flake8' 'python-pytest-localserver')
source=("$pkgname-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz")
sha512sums=('93812e3df0c745f878f443ccbfa96efdc4750c2af477a90319bc36e75e327816cb30b7606598879e40df213b03d9df07b0da2d2685c08ded672526b2919001fd')

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
