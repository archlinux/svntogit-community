# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname='resumable-urlretrieve'
pkgname='python-resumable-urlretrieve'
pkgver=0.1.5
pkgrel=3
pkgdesc='Small library to fetch files over HTTP and resuming their download'
url='https://github.com/berdario/resumable-urlretrieve'
arch=('any')
license=('MIT')
depends=('python' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-rangehttpserver')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/berdario/resumable-urlretrieve/archive/${pkgver}.tar.gz)
sha256sums=('32755a77dac3a8f59a3439df9eb38291f1801cdfa398befe5a15dacc637552a4')
sha512sums=('55833a8a4725316289b62dda071b579a9e5b636a1747b1d555388ddba5c125233b46c71e726ac8d5a1fa9572ebcefab1492f24530c85f905e8e21d1339999574')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=. py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
