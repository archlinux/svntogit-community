# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=python-rfc3986
pkgver=1.3.2
pkgrel=4
pkgdesc="Validating URI References per RFC 3986"
arch=('any')
license=('Apache')
url="https://rfc3986.readthedocs.org/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/r/rfc3986/rfc3986-$pkgver.tar.gz")
sha512sums=('c086c55295d80ccf33dbfeff06059303119e7a407bfc405b7f75ec30413221ec58d3061d75fd3ca696b7acf9b1be1dfcc785e01280bf659a53be43ab1f300b15')

build() {
  cd rfc3986-$pkgver
  python setup.py build
}

check() {
  cd rfc3986-$pkgver
  python setup.py pytest
}

package() {
  cd rfc3986-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
