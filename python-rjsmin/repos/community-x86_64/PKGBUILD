# Maintainer: David Runge <dvzrv@archlinux.org>

_name=rjsmin
pkgname=python-rjsmin
pkgver=1.2.0
pkgrel=1
pkgdesc="Fast javascript minifier for Python"
arch=('x86_64')
url="https://github.com/ndparker/rjsmin"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('5645a35c75d215ca925f6595d6d1e1833551ec8eabd620b3874ad78bfa7edbacd692a8f6d4ca006aa4b9acee4371dfc4ad5213c9e895a32cbd67b986fe642df3')
b2sums=('daf2e42ee4244b04f439fb0dc9301f5bb10bfbe853cc18f206858a7cf9f9d6f7382975e14bea5ac22c3cb533ca38f26e1bf7a21d04a1ebd46457f4b6cf2a3fa2')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  export PYTHONPATH="build/lib.linux-${CARCH}-${python_version}/:${PYTHONPATH}"
  pytest -vv
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {docs/{CHANGES,DESCRIPTION},README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
