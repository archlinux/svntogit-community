# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rply
pkgver=0.7.7
pkgrel=5
pkgdesc="A pure Python Lex/Yacc that works with RPython"
arch=('any')
license=('BSD')
url="https://rply.readthedocs.org"
makedepends=('python-setuptools' 'python-appdirs')
checkdepends=('python-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/alex/rply/archive/v$pkgver.tar.gz")
sha512sums=('c01166b7df067a6a55d1b43caa7b5a349045e838899daf82e2ba70a2c2525fd68e95f11e5319a1fe36936e696352a37bdc13f132f739cd52759a9a9c91e317ba')

build() {
  cd "$srcdir"/rply-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/rply-$pkgver
  py.test
}

package() {
  depends=('python-appdirs')

  cd rply-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
