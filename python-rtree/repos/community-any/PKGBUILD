# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rtree
pkgver=1.0.1
pkgrel=1
pkgdesc="Rtree: spatial index for Python GIS"
url="https://toblerity.github.com/rtree/"
license=('LGPL')
arch=('any')
depends=('python' 'spatialindex')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-numpy')
source=("https://github.com/Toblerity/rtree/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('473ed3bb6457a9831e464b102e52c9ff8667192d2c26880d4cb0bd3bbec51549a532d99ea44650be900298a1ea9302a6aab9c97e0b67c025a0362738062836c7')

build() {
  cd rtree-$pkgver
  python setup.py build
}

check() {
  cd rtree-$pkgver
  python setup.py pytest
}

package() {
  cd rtree-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
