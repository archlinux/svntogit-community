# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rtree
pkgver=0.9.4
pkgrel=2
pkgdesc="Rtree: spatial index for Python GIS"
url="https://toblerity.github.com/rtree/"
license=('LGPL')
arch=('any')
depends=('spatialindex' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Toblerity/rtree/archive/$pkgver.tar.gz")
sha512sums=('42db0cfc03f25caccc8cb1c601484318b3f0801b8b1794a7b7c0182a916d4df361d9bbb27ea339e5714f49205a92f504ee146392ae2399ad9b74d374274bb2c3')

build() {
  cd rtree-$pkgver
  python setup.py build
}

check() {
  cd rtree-$pkgver
  python setup.py pytest
}

package() {
  cd rtree-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
