# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Samuel Littley <aur@toastwaffle.com>
# Contributor: rnons <remotenonsense at gmail dot com>

pkgname=python-ruamel-yaml
pkgver=0.13.6
pkgrel=1
pkgdesc="YAML parser/emitter that supports roundtrip preservation of comments, seq/map flow style, and map key order"
arch=('i686' 'x86_64')
url="https://bitbucket.org/ruamel/yaml"
license=("MIT")
makedepends=('python-setuptools' 'python-pip' 'mercurial')
checkdepends=('python-pytest')
source=("hg+https://bitbucket.org/ruamel/yaml#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "$srcdir"/yaml
  python setup.py build
}

check() {
  cd "$srcdir"/yaml
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" py.test _test/test_*.py
}

package() {
  depends=('python')

  cd yaml
  pip install . --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
