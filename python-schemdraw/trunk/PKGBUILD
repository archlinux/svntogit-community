# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sequencer <liujiuyang1994@gmail.com>

pkgname=python-schemdraw
pkgver=0.12
pkgrel=1
pkgdesc="Electrical circuit schematic drawing."
url="https://bitbucket.org/cdelker/schemdraw"
license=('BSD')
arch=('any')
depends=('python')
optdepends=('python-matplotlib: for matplotlib backend'
            'python-ziafont: for svg backend'
            'python-ziamath: for svg backend'
            'python-latex2mathml: for svg backend')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-nbval' 'python-matplotlib' 'python-ziafont'
              'python-ziamath' 'python-latex2mathml')
source=("$pkgname-$pkgver.tar.bz2::https://bitbucket.org/cdelker/schemdraw/get/$pkgver.tar.bz2")
sha512sums=('abeb82e52302e559078b969b31a8b4d3b34065380126159864fa53dbbd610a6c0261ab31b33a26f65e60a005ecd3489693ae57003059aa2be237c5b6b2613440')

prepare() {
  mv cdelker-schemdraw-* schemdraw-$pkgver
}

build() {
  cd schemdraw-$pkgver
  python setup.py build
}

check() {
  cd schemdraw-$pkgver
  python setup.py pytest --addopts --nbval-lax
}

package() {
  cd schemdraw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
