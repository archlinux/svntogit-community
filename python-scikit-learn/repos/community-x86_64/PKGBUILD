# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=python-scikit-learn
pkgver=1.1.1
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=(x86_64)
url="https://scikit-learn.org"
license=(BSD)
depends=(python-scipy python-joblib python-threadpoolctl)
optdepends=('python-matplotlib: plotting capabilities')
makedepends=(python-setuptools cython openmp)
options=(!emptydirs)
_archive=${pkgname#python-}-${pkgver}
source=(https://github.com/scikit-learn/scikit-learn/archive/${pkgver}/$_archive.tar.gz)
sha256sums=('568e621b9e1479b9ab952a9241db5af2ba3ab4f69d44b8aba3dd7648825e8e5a')

build() {
  cd $_archive
  python setup.py build
}

package() {
  cd $_archive
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 -t "${pkgdir}"/usr/share/licenses/${pkgname}/ COPYING

  # See FS#49651
  install -d "${pkgdir}"/usr/share/doc/${pkgname}
  cp -r doc/tutorial "${pkgdir}"/usr/share/doc/${pkgname}/tutorial
}
