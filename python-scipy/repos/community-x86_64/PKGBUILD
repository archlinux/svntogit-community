# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Bodor Dávid Gábor <david.gabor.bodor@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

_name=scipy
pkgname=python-scipy
pkgver=1.7.3
pkgrel=1
pkgdesc='Open-source software for mathematics, science, and engineering'
arch=(x86_64)
url='https://www.scipy.org/'
license=(BSD)
depends=(python-numpy)
provides=(scipy)
makedepends=(gcc-fortran python-setuptools cython python-pythran pybind11)
checkdepends=(python-pytest)
optdepends=('python-pillow: for image saving module')
source=(https://pypi.python.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('ab5875facfdef77e0a47d5fd39ea178b58e60e454a4c85aa1e52fcb80db7babf')

build() {
  cd scipy-${pkgver}
  python setup.py config_fc build
}

check() {
  cd scipy-${pkgver}
  python setup.py config_fc install \
    --prefix=/usr --root="$srcdir"/test --optimize=1
  export PYTHONPATH="$srcdir"/test/usr/lib/python3.9/site-packages
  cd ..
  python -c "from scipy import test; test('full')"
}

package() {
  cd scipy-$pkgver
  python3 setup.py config_fc install \
    --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
