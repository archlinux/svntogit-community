# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Bodor Dávid Gábor <david.gabor.bodor@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=('python-scipy' 'python2-scipy')
pkgver=0.12.0
pkgrel=1
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
arch=('i686' 'x86_64')
url="http://www.scipy.org/"
license=('BSD')
makedepends=('gcc-fortran' 'python-numpy' 'python2-numpy')
checkdepends=('python-nose' 'python2-nose')
source=("http://downloads.sourceforge.net/scipy/scipy-${pkgver}.tar.gz" )
# "http://pkgbuild.com/~jelle/0001-GEN-regenerate-C-sources-with-Cython-0.17.1.patch.xz")

build() {
  unset LDFLAGS
  unset FFLAGS

  # Changing the arithmetic parameter (Thanks to Fabrizio Castellano)
  sed -i "s/\#define\ UNK\ 1/\#define\ IBMPC\ 1/" \
    scipy-${pkgver}/scipy/special/cephes/mconf.h

  # 2 builds
  cp -r scipy-${pkgver} scipy-${pkgver}-py2

  # build for python3
  cd scipy-${pkgver}
#patch -Np1   -i $srcdir/0001-GEN-regenerate-C-sources-with-Cython-0.17.1.patch
  python3 setup.py config_fc --fcompiler=gnu95 build

  # build for python2
  cd ../scipy-${pkgver}-py2

  for file in $(find . -name '*.py' -print); do
       sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
       sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py config_fc --fcompiler=gnu95 build
}

check() {
  cd scipy-${pkgver}
# figure out how to run tests in the source tree easily
#  python -c "from scipy import test; test('full')"

  cd ../scipy-${pkgver}-py2
# figure out how to run tests in the source tree easily
#  python2 -c "from scipy import test; test('full')"
}

package_python-scipy() {
  depends=('python-numpy')
  provides=('python3-scipy' 'scipy')

  cd scipy-${pkgver}

  python3 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python-scipy/LICENSE"
}

package_python2-scipy() {
  depends=('python2-numpy')
  conflicts=('python-scipy<0.9.0')

  cd scipy-${pkgver}-py2

  python2 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python2-scipy/LICENSE"
}

# vim:set ts=2 sw=2 et:
md5sums=('8fb4da324649f655e8557ea92b998786')
