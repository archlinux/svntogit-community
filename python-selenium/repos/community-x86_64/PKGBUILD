# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgbase=python-selenium
pkgname=('python-selenium' 'python2-selenium')
pkgver=3.4.1
pkgrel=1
pkgdesc="Python binding for Selenium Remote Control"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/selenium"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('geckodriver: Firefox driver support')
source=('https://pypi.python.org/packages/f1/68/4d0990587b9495e2e15d6859a0f42faf90a3a41f12926bfde2e7647ffce2/selenium-3.4.1.tar.gz')
md5sums=('70ba2452cd12cbdf1ce41202f430df51')

package_python-selenium() {
  depends=('python')
  cd "$srcdir/selenium-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-selenium() {
  depends=('python2')
  cd "$srcdir/selenium-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# check() takes too many dependencies to run
