# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-service_identity
pkgname=('python-service_identity' 'python2-service_identity')
_pyname=${pkgbase/python-/}
pkgver=18.1.0
pkgrel=1
pkgdesc='Service Identity Verification for Python'
url='https://service-identity.readthedocs.io/'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'python-attrs' 'python-pyasn1'
             'python-pyasn1-modules' 'python-idna' 'python-sphinx'
             'python2' 'python2-setuptools' 'python2-attrs' 'python2-pyasn1'
             'python2-pyasn1-modules' 'python2-idna' 'python2-sphinx')
checkdepends=('python-pytest' 'python2-pytest')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/pyca/service_identity/archive/${pkgver}.tar.gz)
sha512sums=('0d1abf6ba1e725e923171fd3b809b2cf7ba757ca47f773e8c99a2e2265425b253549609c76955c4d67fcd2fd305d479a1d7541cb26811e94db26ab853d3d3441')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man

  python2 setup.py build
  sphinx-build2 -b text docs docs/_build-py2/text
  sphinx-build2 -b man docs docs/_build-py2/man
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=src py.test
  PYTHONPATH=src py.test2
}

package_python-service_identity() {
  depends=('python' 'python-attrs' 'python-pyasn1' 'python-pyasn1-modules' 'python-idna')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-service_identity() {
  depends=('python2' 'python2-attrs' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-idna')
  cd ${_pyname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
