# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-setuptools-git
pkgver=1.2
pkgrel=6
pkgdesc='Setuptools revision control system plugin for Git'
arch=('any')
license=('BSD')
url='https://github.com/msabramo/setuptools-git'
depends=('python-setuptools' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/msabramo/setuptools-git/archive/$pkgver.tar.gz")
sha512sums=('368d56003a440c5be00685356b0aef97fdea4a84a6d4e56d59964833b55de4916353e061f2eebf2de4dac2df4783a0a1b2d58ba88e1a93979b38a2b62900c1c1')

build() {
  cd "$srcdir"/setuptools-git-$pkgver
  python setup.py build
}

check() {
  git config --global user.email "deedbeaf"

  cd "$srcdir"/setuptools-git-$pkgver
  python setup.py test
}

package() {
  cd setuptools-git-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
