# Maintainer: David Runge <dvzrv@archlinux.org>

_name=setuptools-scm-git-archive
pkgname=python-setuptools-scm-git-archive
pkgver=1.1
pkgrel=2
pkgdesc="setuptools_scm plugin for git archives"
arch=('any')
url="https://github.com/changaco/setuptools_scm_git_archive/"
license=('MIT')
depends=('python-setuptools-scm')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name//-/_}-${pkgver}.tar.gz")
sha512sums=('948b6f950b358f375c60bf44ce6a464238d0d91d60c44677e708ec06f91a04e3d56d60a439384ff06eee56b5e408d76f6a886ddd8b95c2978ccb03ae27924cd0')
b2sums=('99aabdadc5872f9f406fb62dc598e40ac0e22e580db7a0284eac9666d032e96e503eb37750992d1162d4b2df4ea980ba636367d5497e321c5fb39ecbd4800c06')

prepare() {
  mv -v "${_name//-/_}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v tests.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
