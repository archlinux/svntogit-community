# Maintainer: David Runge <dvzrv@archlinux.org>

_name=setuptools-scm-git-archive
pkgname=python-setuptools-scm-git-archive
pkgver=1.2
pkgrel=1
pkgdesc="setuptools_scm plugin for git archives"
arch=(any)
url="https://github.com/changaco/setuptools_scm_git_archive/"
license=(MIT)
depends=(python-setuptools-scm)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/${_name//-/_}-$pkgver.tar.gz)
sha512sums=('ffe55c988c240e731f141247de55afbe3d07b537d70262221f4e98d5887a905f8f98d14ffcf1b6ca2684b770079e62948b75b8d926ae8c0e576f88647088498b')
b2sums=('d587c4bbf4e431806c9ab15c0c30a4061f2921ebb16393e268c58516d27ed2f35de7e4b0162bec0c86e17fd60f1c0fd29d757c1a1141f1b73c12efc688642e28')

build() {
  cd ${_name//-/_}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${_name//-/_}-$pkgver
  pytest -v tests.py
}

package() {
  cd ${_name//-/_}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
