# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgbase=python-setuptools_scm
pkgname=(python-setuptools_scm python2-setuptools_scm)
_pkgname=setuptools_scm
pkgver=1.5.5
pkgrel=2
pkgdesc="Handles managing your python package versions in scm metadata."
arch=('any')
url="https://github.com/pypa/setuptools_scm"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/pypa/setuptools_scm.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a $_pkgname{,-py2}
}

build() {
  cd "${srcdir}/${_pkgname}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-py2"
  python2 setup.py build
}

package_python-setuptools_scm() {
  depends=('python-setuptools')

  cd "${srcdir}/${_pkgname}"
  python setup.py egg_info
  python setup.py install --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-setuptools_scm() {
  depends=('python2-setuptools')

  cd "${srcdir}/${_pkgname}-py2"
  python2 setup.py egg_info
  python2 setup.py install --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
