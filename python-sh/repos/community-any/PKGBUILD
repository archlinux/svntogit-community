# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=python-sh
pkgver=1.13.1
pkgrel=1
pkgdesc='Subprocess interface that allows you to call any program as if it were a function'
arch=('any')
url='https://pypi.python.org/pypi/sh'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'lsof')
source=("https://pypi.io/packages/source/s/sh/sh-$pkgver.tar.gz")
sha512sums=('2d5a275abe3b7f1f1d26a97dd03bcbd6e2e05d1c75f26d5a629fead230d2fb2acd368b5473255526ba53c8fea4031112f272d917e820e294e49be9bbeb5d2116')

prepare() {
  cd sh-$pkgver
  sed -i 's|/bin/sleep|/usr/bin/sleep|' test.py
}

build() {
  cd sh-$pkgver
  python setup.py build
}

check() {
  cd sh-$pkgver
  python sh.py travis
}

package() {
  cd sh-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/python-sh/LICENSE
}

# vim:set ts=2 sw=2 et:
