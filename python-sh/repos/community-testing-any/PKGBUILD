# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=("python-sh" "python2-sh")
pkgver=1.12.14
pkgrel=2
pkgdesc='Subprocess interface that allows you to call any program as if it were a function'
arch=('any')
url='http://pypi.python.org/pypi/sh'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-coverage' 'python2-coverage' 'lsof')
source=("https://pypi.io/packages/source/s/sh/sh-$pkgver.tar.gz")
sha512sums=('5f2daa2666cf8efe90ba550c8ea5875f2a8812d787597c9d9b933f58feccd192e1ec0ab5695b756bdbfbf9748d106c2045be20838e680b31dac3f778970b5786')

prepare() {
  cp -a sh-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sh-$pkgver
  python setup.py build

  cd "$srcdir"/sh-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sh-$pkgver
  python sh.py travis

  cd "$srcdir"/sh-$pkgver-py2
  python2 sh.py travis
}

package_python-sh() {
  depends=('python')

  cd sh-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/python-sh/LICENSE
}

package_python2-sh() {
  depends=('python2')

  cd sh-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/python2-sh/LICENSE
}

# vim:set ts=2 sw=2 et:
