# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: David Moore <davidm@sjsoft.com>

pkgbase=python-simplejson
pkgname=('python-simplejson' 'python2-simplejson')
pkgver=3.12.0
pkgrel=1
pkgdesc='Simple, fast, extensible JSON encoder/decoder for Python'
license=('MIT')
arch=('x86_64' 'i686')
url='https://github.com/simplejson/simplejson'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/simplejson/simplejson/archive/v$pkgver.tar.gz")
sha512sums=('6e9f90d80b1df8d5193b586b7cb5e273dd9a52f0e95635e240319340f4e67fc2273ebdb6014a81d85f171797d103baff87aa17a0e484c07b55227883e710bbb1')

prepare() {
  cp -a simplejson-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/simplejson-$pkgver
  python setup.py build

  cd "$srcdir"/simplejson-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/simplejson-$pkgver
  python setup.py pytest

  cd "$srcdir"/simplejson-$pkgver-py2
  python2 setup.py pytest 
}

package_python-simplejson() {
  depends=('python')

  cd simplejson-$pkgver
  python setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir"/simplejson-$pkgver/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-simplejson() {
  depends=('python2')

  cd simplejson-$pkgver-py2
  python2 setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir"/simplejson-$pkgver/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
  
