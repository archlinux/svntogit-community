# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: David Moore <davidm@sjsoft.com>

pkgname=python-simplejson
pkgver=3.17.6
pkgrel=4
pkgdesc='Simple, fast, extensible JSON encoder/decoder for Python'
license=(MIT)
arch=(x86_64)
url='https://github.com/simplejson/simplejson'
depends=(python)
makedepends=(git python-setuptools)
checkdepends=(python-pytest-runner)
source=("git+$url#commit=02221b19672b1b35188080435c7360cd2d6af6fb") # tag: v3.17.6
b2sums=(SKIP)

build() {
  cd simplejson
  python setup.py build
}

check() {
  cd simplejson
  python setup.py pytest
}

package_python-simplejson() {
  cd simplejson
  python setup.py install --root="$pkgdir"
  install -Dm644 ../simplejson/LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
