# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: David Moore <davidm@sjsoft.com>

pkgbase=python-simplejson
pkgname=('python-simplejson' 'python2-simplejson')
pkgver=3.11.1
pkgrel=1
pkgdesc='Simple, fast, extensible JSON encoder/decoder for Python'
license=('MIT')
arch=('x86_64' 'i686')
url='https://github.com/simplejson/simplejson'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/simplejson/simplejson/archive/v$pkgver.tar.gz")
sha512sums=('bef87f3a7c695823a4e1792dc4db0d8e15e03c5c085bfef364c505a079036696005973f89dd5f80a5437e5e8a88f1f3274b37cf5b2479e58e52738c5e37b455d')

prepare() {
  cp -a simplejson-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/simplejson-$pkgver
  python setup.py build

  cd "$srcdir"/simplejson-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/simplejson-$pkgver
  python setup.py pytest

  cd "$srcdir"/simplejson-$pkgver-py2
  python2 setup.py pytest 
}

package_python-simplejson() {
  depends=('python')

  cd simplejson-$pkgver
  python setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir"/simplejson-$pkgver/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-simplejson() {
  depends=('python2')

  cd simplejson-$pkgver-py2
  python2 setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir"/simplejson-$pkgver/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
  
