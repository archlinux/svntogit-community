# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

_pkgname=simplespectral
pkgname=python-$_pkgname
pkgver=1.0.0
pkgrel=5
pkgdesc='Heavily simplified scipy.signal.spectral module'
arch=('any')
url='https://github.com/xmikos/simplespectral'
license=('MIT')
depends=('python' 'python-numpy')
makedepends=('python-setuptools')
optdepends=('python-scipy: faster FFT calculations with scipy.fftpack library')
replaces=('simplespectral')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmikos/simplespectral/archive/v$pkgver.tar.gz")
sha256sums=('4342741f7054c71f7c5fb9d38df49e048e0a4e20eb53cefd247e47937644cad0')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir"

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
