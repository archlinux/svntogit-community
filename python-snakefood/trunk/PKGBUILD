# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgbase=python-snakefood
pkgname=python2-snakefood
pkgver=1.4.0.20160605
_revision=6d55510bd30bf731ef9b4de5242733d12a478295
pkgrel=2
pkgdesc='Dependency Graphing for Python'
arch=('any')
url="http://furius.ca/snakefood"
license=('GPL')
depends=('python2-six')
makedepends=('mercurial')
checkdepends=('python2-pytest')
source=("hg+https://bitbucket.org/blais/snakefood#revision=$_revision")
sha256sums=('SKIP')

prepare() {
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' snakefood/bin/*
}

build() {
  cd snakefood
  python2 setup.py build
}

check() {
  cd snakefood
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 || warning "Tests are broken"
}

package_python2-snakefood() {
  cd snakefood
  python2 setup.py install --root="$pkgdir" --optimize=1
}
