# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-socketio
pkgver=5.2.1
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
depends=('python' 'python-six' 'python-engineio' 'python-bidict')
makedepends=('python-setuptools' 'python-sphinx' 'python-tornado')
optdepends=('python-tornado: tornado asyncio webserver')
checkdepends=('python-pytest' 'python-mock')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5e28e1038c99746308b2f8f2014c567c4cfc6fb236e953216578f690c5fc10cf72bfdaae0969c1c5f739827fe547032b021cd05baf6db6bcee20e925da889c4f')
b2sums=('47f32f7f6a666cd56c23df798827bb27f45f29358c8c8dc32154e19b084af2173d3402d90a0e3b6c9a2510c00dca31a3cf804a2c745ced99f6f16f0aa59a75b8')

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgbase}-${pkgver}
  py.test -k 'not test_logger'
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
