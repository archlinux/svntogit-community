# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-softlayer
pkgver=5.6.3
pkgrel=1
pkgdesc="A library for SoftLayer's API"
arch=('any')
license=('MIT')
url='https://github.com/softlayer/softlayer-python'
depends=('python-six' 'python-ptable' 'python-click' 'python-requests' 'python-prompt_toolkit'
         'python-pygments' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-testtools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/softlayer/softlayer-python/archive/v$pkgver.tar.gz")
sha512sums=('8c36421e37fd8de35a2a62698a256e88c0a0749940a1a1c155d78777f323e1ec38658f177ff8c12e52872995e94dc923ec12b7c2c118796bd58d04dd82b65817')

prepare() {
  cd softlayer-python-$pkgver
  sed -i 's/==/>=/' setup.py
}

build() {
  cd softlayer-python-$pkgver
  python setup.py build
}

check() {
  cd softlayer-python-$pkgver
  python setup.py pytest --addopts '-k "not test_edit_perms_on_bad"'
}

package() {
  cd softlayer-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
