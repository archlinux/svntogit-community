# $Id: PKGBUILD 141139 2015-09-20 03:14:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sortedcontainers
pkgname=('python-sortedcontainers' 'python2-sortedcontainers')
pkgver=1.5.3
pkgrel=1
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/grantjenks/sorted_containers.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a sorted_containers{,-py2}
}

build() {
  cd "$srcdir/sorted_containers"
  python setup.py build

  cd "$srcdir/sorted_containers-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/sorted_containers"
  nosetests3

  cd "$srcdir/sorted_containers-py2"
  nosetests2
}

package_python-sortedcontainers() {
  depends=('python')

  cd sorted_containers
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-sortedcontainers() {
  depends=('python2')

  cd sorted_containers-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
