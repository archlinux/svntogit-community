# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-soupsieve
pkgver=2.0.1
pkgrel=3
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-html5lib' 'python-lxml')
source=("https://github.com/facelessuser/soupsieve/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f4ef4deee81bb31952e1d3216250712f09b425e7a2253e15bd1b38cb74b680e421e7d086a8770737898f6b57867f07f2dd07b8a98108d05e338bce92b274b85f')

build() {
  cd soupsieve-$pkgver
  python setup.py build
}

check() {
  cd soupsieve-$pkgver
  pytest
}

package() {
  cd soupsieve-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
