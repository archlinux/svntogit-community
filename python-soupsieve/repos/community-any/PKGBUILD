# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-soupsieve
pkgver=2.3
pkgrel=1
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-html5lib' 'python-lxml')
source=("https://github.com/facelessuser/soupsieve/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c5d46703aba737c6eea7cc482311fd75dbaaa4929ebf3ed45b580d7e89d1d0be806608adbab974803acd8df5d74435efe1e149bce5cdbd74f77c90bba960859a')

build() {
  cd soupsieve-$pkgver
  python setup.py build
}

check() {
  cd soupsieve-$pkgver
  # https://gitlab.gnome.org/GNOME/libxml2/-/issues/312
  pytest \
    --deselect tests/test_extra/test_soup_contains.py::TestSoupContains::test_contains_cdata_html \
    --deselect tests/test_extra/test_soup_contains_own.py::TestSoupContainsOwn::test_contains_own_cdata_html
}

package() {
  cd soupsieve-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
