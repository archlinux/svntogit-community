# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-soupsieve
pkgver=2.2
pkgrel=1
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-html5lib' 'python-lxml')
source=("https://github.com/facelessuser/soupsieve/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('26402c831c19c9b133442dd8ea31363e1eadc3cafe172b933cdbc746df6dc9961e930d4fbe8399b847848bf3fcd5893d2449e912ed0cdf8766468997acf5ee87')

build() {
  cd soupsieve-$pkgver
  python setup.py build
}

check() {
  cd soupsieve-$pkgver
  pytest
}

package() {
  cd soupsieve-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
