# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgname=('python-south' 'python2-south')
pkgver=0.8.2
pkgrel=2
pkgdesc="Intelligent database migrations library for the Django web framework"
arch=('any')
url="http://south.aeracode.org/"
license=('APACHE')
makedepends=('python-setuptools' 'python2-setuptools')
source=(http://pypi.python.org/packages/source/S/South/South-$pkgver.tar.gz)
sha256sums=('a6f074f048a63a0d19cd1a6ae298377b4800351eca8e1294d33f709c5cdd5e5a')

package_python-south() {
  depends=('python-django')

  cd "$srcdir/South-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-south() {
  depends=('python2-django')
  conflicts=('python-south<=0.7.6-1')
  replaces=('python-south<=0.7.6-1')

  cd "$srcdir/South-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
