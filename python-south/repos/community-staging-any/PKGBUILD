# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgname=('python-south' 'python2-south')
pkgver=0.8.4
pkgrel=2
pkgdesc="Intelligent database migrations library for the Django web framework"
arch=('any')
url="http://south.aeracode.org/"
license=('APACHE')
makedepends=('python-setuptools' 'python2-setuptools')
source=(http://pypi.python.org/packages/source/S/South/South-$pkgver.tar.gz)
sha256sums=('5cf6bdc2967be8dd7845d3058e3af0a73408e5383c9effc3123bb48c3cc5c821')

package_python-south() {
  depends=('python-django')

  cd "$srcdir/South-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-south() {
  depends=('python2-django')
  conflicts=('python-south<=0.7.6-1')
  replaces=('python-south<=0.7.6-1')

  cd "$srcdir/South-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
