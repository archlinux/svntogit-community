# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgname=python-south
pkgver=0.7.5
pkgrel=1
pkgdesc="Intelligent database migrations library for the Django web framework"
arch=('any')
url="http://south.aeracode.org/"
license=('APACHE')
depends=('django')
makedepends=('python2-distribute')
provides=('south')
conflicts=('south')
replaces=('south')
source=(http://pypi.python.org/packages/source/S/South/South-$pkgver.tar.gz)
sha256sums=('7f0072806405f2b2be4ad231aa0f570cd309c7e9bb976e7326e99e15abfc6ab5')

build() {
  cd "$srcdir/South-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/South-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
