# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tiago Pierezan Camargo <tcamargo@gmail.com>

pkgname=('python-south' 'python2-south')
pkgver=1.0.2
pkgrel=3
pkgdesc="Intelligent database migrations library for the Django web framework"
arch=('any')
url="http://south.aeracode.org/"
license=('APACHE')
makedepends=('python-setuptools' 'python2-setuptools')
source=(https://pypi.python.org/packages/source/S/South/South-$pkgver.tar.gz)
sha256sums=('d360bd31898f9df59f6faa786551065bba45b35e7ee3c39b381b4fbfef7392f4')

package_python-south() {
  depends=('python-django')

  cd "$srcdir/South-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-south() {
  depends=('python2-django')
  conflicts=('python-south<=0.7.6-1')
  replaces=('python-south<=0.7.6-1')

  cd "$srcdir/South-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
