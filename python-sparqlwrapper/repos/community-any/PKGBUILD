# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sparqlwrapper
pkgname=("python-sparqlwrapper" "python2-sparqlwrapper")
pkgver=1.6.3
pkgrel=1
pkgdesc="SPARQL Endpoint interface to Python"
arch=('any')
url="http://rdflib.github.io/sparqlwrapper/"
license=('W3C')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/S/SPARQLWrapper/SPARQLWrapper-$pkgver.tar.gz")
md5sums=('831974c30da9c41c795e99fb33ac1bf0')

prepare() {
  cp -r SPARQLWrapper-$pkgver{,-py2}
}

package_python-sparqlwrapper() {
  depends=('python3')

  cd SPARQLWrapper-$pkgver
  LC_CTYPE=en_US.utf8 python3 setup.py install --root="${pkgdir}" --optimize=1
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-sparqlwrapper() {
  depends=('python2')

  cd SPARQLWrapper-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  #install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

