# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sparqlwrapper
pkgname=("python-sparqlwrapper" "python2-sparqlwrapper")
pkgver=1.8.0
pkgrel=1
pkgdesc="SPARQL Endpoint interface to Python"
arch=('any')
url="http://rdflib.github.io/sparqlwrapper/"
license=('W3C')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/RDFLib/sparqlwrapper.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a sparqlwrapper{,-py2}
}

build() {
  cd "$srcdir"/sparqlwrapper
  python setup.py build

  cd "$srcdir"/sparqlwrapper-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sparqlwrapper
  ./run_tests_py3.sh

  cd "$srcdir"/sparqlwrapper-py2
  nosetests2
}

package_python-sparqlwrapper() {
  depends=('python')

  cd sparqlwrapper
  LC_CTYPE=en_US.UTF-8 python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-sparqlwrapper() {
  depends=('python2')

  cd sparqlwrapper-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
