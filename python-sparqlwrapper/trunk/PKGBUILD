# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sparqlwrapper
pkgver=1.8.4
pkgrel=2
pkgdesc="SPARQL Endpoint interface to Python"
arch=('any')
url="https://rdflib.github.io/sparqlwrapper/"
license=('W3C')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RDFLib/sparqlwrapper/archive/$pkgver.tar.gz")
sha512sums=('b89fe07319a3108f29fe984c7ae7381c30db6f655da35c5d193b7365fde4dbbc4f7575defe0371aff6c5d6d626e36c15b6fa8685c1d12eb19ae73d3bd3650eac')

build() {
  cd "$srcdir"/sparqlwrapper-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/sparqlwrapper-$pkgver
  ./run_tests_py3.sh
}

package() {
  cd sparqlwrapper-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
