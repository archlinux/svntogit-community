# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=sphinx-argparse-cli
pkgname=python-$_pkgname
pkgver=1.7.0
pkgrel=2
pkgdesc='Render CLI arguments (sub-commands friendly) defined by argparse module'
arch=('any')
url='https://github.com/gaborbernat/sphinx-argparse-cli'
license=('MIT')
depends=('python' 'python-sphinx' 'python-docutils-stubs')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('0293245fdab15088b95ccfc5cc078c7a795f31772cba78899feadb32a740997e11998c58dc06524ad5109f277b48db64a9e9a087c99a843276dadd4b188652a0')

build() {
  cd $_pkgname-$pkgver

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=src pytest
}

package() {
  cd $_pkgname-$pkgver

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
