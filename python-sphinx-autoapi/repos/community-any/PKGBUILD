# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=1.5.1
pkgrel=3
pkgdesc="A new approach to API documentation in Sphinx."
arch=('any')
url="https://github.com/readthedocs/sphinx-autoapi"
license=('MIT')
depends=('python-astroid' 'python-jinja' 'python-sphinx' 'python-unidecode'
'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
# TODO: package python-go and python-dotnet
# optdepends=('python-go' 'python-dotnet')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('4d50878323b80b66abdc4046c1632636473414653bac450b58832b8ee9f6a9a6')
b2sums=('9d2ab0c49f3f53e90c6cc89d5a07b48571de45c964ec2479f1d24ebca99ff673448b4ed9ba5d96520e949410b450e6a8a6d0db5b084fd0d293bec5163c703b43')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
