# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=1.7.0
pkgrel=3
pkgdesc="A new approach to API documentation in Sphinx."
arch=('any')
url="https://github.com/readthedocs/sphinx-autoapi"
license=('MIT')
depends=('python-astroid' 'python-jinja' 'python-sphinx' 'python-unidecode'
'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
# TODO: package python-go and python-dotnet
# optdepends=('python-go' 'python-dotnet')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('48caa054a99c21156e9a1d26559281dc27f86ab8ef8bb6ef160f8cd9f4a0053d')
b2sums=('ffa310c8f29a7f434a6ae3a7efae9a8ea3b00c88d72c20ef271d45b50893e36282384b035c6fa57d0a54f69d3fdbbe27050b96fa163f3189355b45b5e2177727')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v

  # Remove created cache files for reproduciblity with rebuilderd
  rm -r tests{,/python}/__pycache__
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
