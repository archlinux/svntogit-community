# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=furo
pkgname=python-sphinx-$_pkgname
pkgver=2022.01.02
pkgrel=2
pkgdesc='A clean customizable documentation theme for Sphinx'
arch=('any')
url='https://github.com/pradyunsg/furo'
license=('MIT')
depends=('python-sphinx' 'python-pygments' 'python-beautifulsoup4')
makedepends=('python-build' 'python-pip' 'python-sphinx-theme-builder' 'python-flit-core'
             'nodejs-lts-gallium' 'npm' 'expac')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('dcd2bae18c28a8623b566ef79a996a76d9157a05627b4e9c930b0ea82154992b9d2321ffe1c62fc2d014d499b7ca1f71117bcea9a1e8879a0ef12d3818cdd290')

prepare() {
  cd $_pkgname-$pkgver
  # force use of system nodejs
  local node_ver=$(expac %v nodejs-lts-gallium | cut -d - -f 1)
  sed -i "s/16.13.0/$node_ver/" pyproject.toml
}

build() {
  cd $_pkgname-$pkgver

  python -m build -nw

  # docs disabled for now to unblock Python 3.10 update

  # sphinx needs this theme installed because it is uses it.
  # simply injecting the package to sys.path (via PYTHONPATH or similar)
  # is not enough because sphinx looks for themes in the registered
  # entrypoints, this means we actually have to install the package.
  # for this we will create a venv with access to system packages and
  # install the theme there, then we will build the documentation.

  #python -m venv --system-site-packages doc-env
  #doc-env/bin/python setup.py install --skip-build

  #doc-env/bin/python /usr/bin/sphinx-build -b dirhtml -v docs build/docs/html
}

package() {
  cd $_pkgname-$pkgver

  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl

  #install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  #cp -r -a --no-preserve=ownership build/docs/html "$pkgdir"/usr/share/doc/$pkgname
  #rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
