# $Id$
# Maintainer:
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgbase=python-sphinx_rtd_theme
pkgname=(python-sphinx_rtd_theme python2-sphinx_rtd_theme)
_pyname=sphinx_rtd_theme
pkgver=0.1.8
pkgrel=1
arch=(any)
pkgdesc="Python Sphinx Read The Docs Theme"
url='https://github.com/snide/sphinx_rtd_theme'
license=(MIT)
makedepends=(python-setuptools python2-setuptools)
source=("https://pypi.python.org/packages/source/s/${_pyname}/${_pyname}-${pkgver}.tar.gz")
md5sums=('713ce7c53239449bdd799385577329ee')

build() {
  cp -r $_pyname-$pkgver $_pyname-$pkgver-py2
  
  cd $_pyname-$pkgver
  python setup.py build

  cd "$srcdir"/$_pyname-$pkgver-py2
  python2 setup.py build
}

package_python-sphinx_rtd_theme() {
  cd $_pyname-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sphinx_rtd_theme() {
  cd $_pyname-${pkgver}-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

