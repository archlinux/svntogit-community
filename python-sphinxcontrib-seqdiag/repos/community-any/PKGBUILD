# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sphinxcontrib-seqdiag
pkgver=2.0.0
pkgrel=1
pkgdesc="A sphinx extension for embedding sequence diagram using seqdiag"
url="https://github.com/blockdiag/sphinxcontrib-seqdiag"
license=('BSD')
arch=('any')
depends=('blockdiag' 'seqdiag' 'python-sphinx')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-reportlab' 'python-sphinx-testing')
source=("$pkgname-$pkgver.tar.gz::https://github.com/blockdiag/sphinxcontrib-seqdiag/archive/$pkgver.tar.gz")
sha512sums=('c75ab1239ebbe3a16ade03de9bffcf7d4d2247f19745bc8f2d9fb9ea5c7f73befe19ed7249688e26d78827bdb5b53b8ac8157c4032cd3e904f539a70de11a4f6')

build() {
  cd sphinxcontrib-seqdiag-$pkgver
  python setup.py build
}

check() {
  cd sphinxcontrib-seqdiag-$pkgver
  python setup.py nosetests
}

package() {
  cd sphinxcontrib-seqdiag-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
