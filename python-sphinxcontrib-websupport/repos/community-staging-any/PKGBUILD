# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com

pkgbase=python-sphinxcontrib-websupport
pkgname=(python-sphinxcontrib-websupport python2-sphinxcontrib-websupport)
pkgver=1.1.2
pkgrel=2
pkgdesc="Sphinx API for Web Apps"
arch=('any')
url="https://www.sphinx-doc.org/"
license=('BSD')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=(https://pypi.org/packages/source/s/sphinxcontrib-websupport/sphinxcontrib-websupport-$pkgver.tar.gz)
sha512sums=('55754f6304f38abd50f410da164f401a1b9a1d289b19f0077a38f72ce11a2dd64eff0713bb99471e470ff15fbd6a0042d49bab14c92f82cdd61019adbb88397e')

prepare() {
  cp -a sphinxcontrib-websupport-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python setup.py build

  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py build
}

package_python-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sphinxcontrib-websupport() {
  cd "$srcdir"/sphinxcontrib-websupport-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
