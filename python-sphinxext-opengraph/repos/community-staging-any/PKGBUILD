# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

_name=sphinxext-opengraph
pkgname=python-$_name
pkgver=0.8.1
pkgrel=2
pkgdesc="Sphinx extension to generate unique OpenGraph metadata"
arch=('any')
url="https://sphinxext-opengraph.readthedocs.io/"
license=('BSD')
depends=('python-sphinx' 'python-matplotlib')
makedepends=('python-setuptools' 'python-setuptools-scm')
source=(https://files.pythonhosted.org/packages/source/s/$_name/$_name-$pkgver.tar.gz
        $pkgname-$pkgver-LICENSE.md::https://github.com/wpilibsuite/sphinxext-opengraph/raw/main/LICENSE.md)
sha256sums=('4e698b907ef9582cd0106bd50807106677fdab4dc5c31040be17c9afb6e17880'
            '26f37e2b2fdcbb20e9373f1a0a4d9f79d7c9a2ba5ef85f106df437148709da8b')

prepare() {
  cd $_name-$pkgver
  mv {../$pkgname-$pkgver-,}LICENSE.md
}

build() {
  cd $_name-$pkgver
  python setup.py build
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}

# vim:set ts=2 sw=2 et:
