# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

_name=sphinxext-opengraph
pkgname=python-$_name
pkgver=0.7.5
pkgrel=2
pkgdesc="Sphinx extension to generate unique OpenGraph metadata"
arch=('any')
url="https://sphinxext-opengraph.readthedocs.io/"
license=('BSD')
depends=('python-sphinx')
makedepends=('python-setuptools' 'python-setuptools-scm')
source=(https://files.pythonhosted.org/packages/source/s/$_name/$_name-$pkgver.tar.gz
        $pkgname-$pkgver-LICENSE.md::https://github.com/wpilibsuite/sphinxext-opengraph/raw/main/LICENSE.md)
sha256sums=('caf061fb3bea8d8f2228f7a1d55cb8f6809f2b5c806bf3600e21ce1a3cf906d1'
            '26f37e2b2fdcbb20e9373f1a0a4d9f79d7c9a2ba5ef85f106df437148709da8b')

prepare() {
  cd $_name-$pkgver
  mv {../$pkgname-$pkgver-,}LICENSE.md
}

build() {
  cd $_name-$pkgver
  python setup.py build
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}

# vim:set ts=2 sw=2 et:
