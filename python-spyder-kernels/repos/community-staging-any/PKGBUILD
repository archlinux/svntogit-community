# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=spyder-kernels
pkgname=python-${_pkg}
pkgver=2.1.3
pkgrel=2
pkgdesc="Jupyter Kernels for the Spyder console"
arch=(any)
url="https://github.com/spyder-ide/spyder-kernels/"
license=(MIT)
makedepends=(python python-setuptools)
depends=(
    python-cloudpickle
    python-ipykernel
    python-jupyter_client
    python-pyzmq
    python-wurlitzer
)
checkdepends=(
    cython
    python-dask
    python-distributed
    python-flaky
    python-matplotlib
    python-mock
    python-numpy
    python-pandas
    python-pillow
    python-pytest
    python-scipy
    python-xarray
    xorg-server-xvfb
)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('529d75968a3bcfe9c9389c8c703aea116a4d1c100b28f0135217c07186f7024e')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  xvfb-run --auto-servernum pytest spyder_kernels -x -v
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
