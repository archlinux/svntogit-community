# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org

_pkgname=sqlalchemy-continuum
pkgname=python-sqlalchemy-continuum
pkgver=1.3.9
pkgrel=3
pkgdesc='Versioning and auditing extension for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-continuum'
arch=('any')
license=('BSD')
depends=('python' 'python-sqlalchemy' 'python-sqlalchemy-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('sqlite' 'python-pytest' 'python-flexmock' 'python-psycopg2' 'python-mysql-connector'
              'python-six' 'python-anyjson' 'python-flask' 'python-flask-login' 'python-flask-sqlalchemy'
              'python-sqlalchemy-i18n')
optdepends=('python-flask: flask plugin'
            'python-flask-login: flask plugin'
            'python-flask-sqlalchemy: flask plugin'
            'python-sqlalchemy-i18n: internationalization')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kvesteri/sqlalchemy-continuum/archive/${pkgver}.tar.gz)
sha512sums=('f597f537999c59005e09fe6dad40d03bd9fb933f9ea88135888c4a848ecb61df05e4a2dcb9af5399ed84bc127af2c48e8fc2993e3a1d3d561cad6ca3d34319ec')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  make -C docs text man
}

check() {
  cd ${_pkgname}-${pkgver}/tests
  DB=sqlite pytest -k 'not test_flask'
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/sqlalchemy-continuum.1 -t "${pkgdir}/usr/share/man/man1"
  ln -sf /usr/share/man/man1/sqlalchemy-continuum.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
}

# vim: ts=2 sw=2 et:
