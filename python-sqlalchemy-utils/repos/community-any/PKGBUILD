# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=sqlalchemy-utils
pkgname=python-sqlalchemy-utils
pkgver=0.33.10
pkgrel=1
pkgdesc='Various utility functions, new data types and helpers for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-utils'
depends=('python' 'python-sqlalchemy' 'python-anyjson' 'python-babel'
         'python-arrow' 'python-intervals' 'python-phonenumbers' 'python-passlib'
         'python-colour' 'python-dateutil' 'python-furl' 'python-cryptography')
checkdepends=('python-pytest' 'python-mock' 'python-flexmock' 'python-jinja')
makedepends=('python')
license=('BSD')
arch=('any')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kvesteri/sqlalchemy-utils/archive/${pkgver}.tar.gz)
sha512sums=('4a4c8803f315d3549290465513dfe7309c0010e428fdf93f02a703825849a3f0c54e2b24de133373538b81d4480da8f4691d869bf72c488a69bc1e0f94cacec1')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

check() {
  cd ${_name}-${pkgver}
  # Tests require a postgres and MySQL db
  pytest --disable-pytest-warnings tests/test_models.py
}

# vim: ts=2 sw=2 et:
