# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=sqlalchemy-utils
pkgname=python-sqlalchemy-utils
pkgver=0.32.9
pkgrel=2
pkgdesc='Various utility functions, new data types and helpers for SQLAlchemy.'
url='https://github.com/kvesteri/sqlalchemy-utils'
depends=('python' 'python-sqlalchemy' 'python-anyjson' 'python-babel'
         'python-arrow' 'python-intervals' 'python-phonenumbers' 'python-passlib'
         'python-colour' 'python-dateutil' 'python-furl' 'python-cryptography')
checkdepends=('python-pytest' 'python-mock' 'python-flexmock' 'python-jinja')
makedepends=('python')
license=('BSD')
arch=('any')
source=("https://github.com/kvesteri/sqlalchemy-utils/archive/${pkgver}.tar.gz")
sha512sums=('ba266e258054a08e0da6c74d2d6106c2c0f34674283a56b6f1125434754b8bbb948d39b25bfbd133ca6cee72c074b1d5f109b5a18dd830902ad72a6b65307a6f')

build() {
  cd "${srcdir}/${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
}

check() {
  cd "${srcdir}/${_name}-${pkgver}"
  # Tests require a postgres and MySQL db
  pytest --disable-pytest-warnings tests/test_models.py
}
