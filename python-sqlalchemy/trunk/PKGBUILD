# $Id$ 
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-sqlalchemy
pkgname=('python-sqlalchemy' 'python2-sqlalchemy')
pkgver=0.7.8
pkgrel=1
arch=('i686' 'x86_64') # python2 package contain .so
url="http://www.sqlalchemy.org/"
license=('custom: MIT')
makedepends=('python' 'python2' 'python-distribute' 'python2-distribute' 'python-nose' 'python2-nose')
source=("http://downloads.sourceforge.net/sourceforge/sqlalchemy/SQLAlchemy-$pkgver.tar.gz")
md5sums=('a5346f007e79c08cccb62ccec267309a')

build() {
  cp -a SQLAlchemy-$pkgver SQLAlchemy2-$pkgver
  cd SQLAlchemy-$pkgver
  python setup.py build
  cd ../SQLAlchemy2-$pkgver
  python2 setup.py build
}

package_python-sqlalchemy() {
  pkgdesc='Python SQL toolkit and Object Relational Mapper'
  depends=('python')

  cd SQLAlchemy-${pkgver}
  python setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-sqlalchemy() {
  pkgdesc='Python 2 SQL toolkit and Object Relational Mapper'
  depends=('python2')

  cd SQLAlchemy2-$pkgver
  python2 setup.py install --root="$pkgdir"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 ft=sh et:
