# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=python-srcinfo
pkgver=0.0.8
pkgrel=3

pkgdesc="Simple .SRCINFO parsing library"
url='https://github.com/kyrias/python-srcinfo'
arch=('any')
license=('ISC')

depends=('python' 'python-setuptools' 'python-parse')
checkdepends=('python-nose')

source=("https://pypi.python.org/packages/source/s/srcinfo/srcinfo-$pkgver.tar.gz"
        "https://pypi.python.org/packages/source/s/srcinfo/srcinfo-$pkgver.tar.gz.asc")

validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5')
md5sums=('5905c054ac2cd080bd0dbf1df5253608'
         'SKIP')

check() {
	cd srcinfo-"$pkgver"
	python setup.py test
}

package() {
	cd srcinfo-"$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
