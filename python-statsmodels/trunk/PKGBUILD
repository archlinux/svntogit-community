# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

# TODO: create X-12-ARIMA or X-13ARIMA-SEATS (can be used for time-series analysis)

pkgname=python-statsmodels
pkgver=0.13.2
pkgrel=1
pkgdesc="Statistical computations and models for use with SciPy"
arch=('x86_64')
url="http://statsmodels.sourceforge.net/"
license=('BSD')
depends=('python-scipy' 'python-pandas' 'python-patsy')
makedepends=('python-setuptools' 'cython')
optdepends=('python-nose: needed for integrated test suite'
            'python-matplotlib: needed for plotting functions'
	    'python-joblib:  accelerate distributed estimation for certain models'
	    'jupyter: needed to run the notebooks')
source=("$pkgname-$pkgver.tar.gz::https://github.com/statsmodels/statsmodels/archive/v${pkgver}.tar.gz")
sha256sums=('2c37244a87d9116f9e7de134219fcecee947ffe43cb66b949ee6d0422edfb4cc')

build() {
  cd "$srcdir"/statsmodels-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/statsmodels-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
