# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-stestr
pkgver=3.0.0
pkgrel=1
pkgdesc="A test runner runner similar to testrepository"
url="https://stestr.readthedocs.io/en/latest/"
license=('Apache')
arch=('any')
depends=('python-future' 'python-cliff' 'python-pbr' 'python-subunit' 'python-fixtures'
         'python-testtools' 'python-yaml' 'python-voluptuous')
checkdepends=('python-hacking' 'python-sphinx' 'python-mock' 'python-subunit2sql' 'python-coverage'
              'python-ddt')
source=("https://pypi.io/packages/source/s/stestr/stestr-$pkgver.tar.gz")
sha512sums=('848dd6fb0329ec26bd586fd318f6e72017f11d237fd559717995b0a8f73c2c6667d50aeafb5c1bffc45f3dcdc617cb901d5eeb5e1fe3fc6cc2568820722091b3')

build() {
  cd stestr-$pkgver
  python setup.py build
}

check() {
  cd stestr-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  (
    export PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages"
    export PATH="$PWD/tmp_install/usr/bin:$PATH"
    stestr run
  )
}

package() {
  cd stestr-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
