# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-structlog
# https://www.structlog.org/en/stable/changelog.html
pkgver=21.2.0
pkgrel=3
pkgdesc="Structured Logging for Python"
url="https://www.structlog.org"
license=('Apache')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun' 'python-pretend' 'python-simplejson'
              'python-twisted' 'python-rapidjson' 'python-greenlet' 'python-pytest-asyncio')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hynek/structlog/archive/$pkgver.tar.gz")
sha512sums=('6ffcb9a5fe4596e66b3b77c13281de67d3cfcb150a984d2b0fbf240d3c5e8b66ed7ac6a98859a0541531e99c126b022bb14ac2d751754dddf4e8f03dae316194')

build() {
  cd structlog-$pkgver
  python setup.py build
}

check() {
  cd structlog-$pkgver
  PYTHONPATH="$PWD"/src pytest
}

package() {
  cd structlog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
