# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-structlog
pkgver=20.1.0
pkgrel=4
pkgdesc="Structured Logging for Python"
url="https://www.structlog.org"
license=('Apache')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun' 'python-pretend' 'python-simplejson'
              'python-twisted' 'python-rapidjson' 'python-greenlet' 'python-pytest-asyncio')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hynek/structlog/archive/$pkgver.tar.gz")
sha512sums=('69c390e3a0ce5356b1d08948e5159e0ecf230222b25bb3cc5e4ba4baa5d4e00c21a8d322ac1064f543d98acce2ed0927677886ecfd4cfd3cf8619191baed85fa')

build() {
  cd structlog-$pkgver
  python setup.py build
}

check() {
  cd structlog-$pkgver
  PYTHONPATH="$PWD"/src pytest
}

package() {
  cd structlog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
