# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-structlog
pkgver=19.2.0
pkgrel=1
pkgdesc="Structured Logging for Python"
url="https://www.structlog.org"
license=('Apache')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun' 'python-pretend' 'python-simplejson'
              'python-twisted' 'python-rapidjson' 'python-greenlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hynek/structlog/archive/$pkgver.tar.gz")
sha512sums=('a000601477c155545ff359b8cd4439508cbc28809aa7216395bf2bc85539da481224aa6f265f801ada0a52e03574957bf384718e6793efe697125bf467ffcd7a')

build() {
  cd structlog-$pkgver
  python setup.py build
}

check() {
  cd structlog-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd structlog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
