# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.2.13
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.python.org/packages/source/s/sure/sure-$pkgver.tar.gz"
        py3k-fix.patch)
sha512sums=('751ab46d0b13541374c8471c6e3bdeb48b4709fab5c33f0dd3894cb61ea10e3932f66596f219f442a26b8bc41b8015e0e830728b0fc48cf5f227df6bf632ce44'
            '90118b3c3148b6dd6904f94199fc7ae98fc805980132000e270e1b80a563620a1216f916e82c894cfe8cd0c68041ca091ebd7c0a8e64b1cf6dd3c84db3b0d558')

prepare() {
  cp -a "sure-$pkgver"{,-py2}

  cd sure-$pkgver
  patch -p1 -i ../py3k-fix.patch
}

check() {
  cd sure-$pkgver
  nosetests3

  cd ../sure-$pkgver-py2
  nosetests2
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure-$pkgver
  LC_CTYPE=en_US.UTF-8 python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-$pkgver-py2
  LC_CTYPE=en_US.UTF-8 python2 setup.py install --root="${pkgdir}" --optimize=1
}

