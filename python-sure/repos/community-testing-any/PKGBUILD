# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.4.0
pkgrel=2
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.io/packages/source/s/sure/sure-$pkgver.tar.gz")
sha512sums=('a68b18e1d13f0b008184812b8b11041fbcaaf7c22cf7f62542ad73eeb816af96af7881f758eaa960d2348f75d1b00359aee30867699e366667135a389433dcd9')

prepare() {
  cp -a "sure-$pkgver"{,-py2}
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure-$pkgver
  LC_CTYPE=en_US.UTF-8 python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-$pkgver-py2
  LC_CTYPE=en_US.UTF-8 python2 setup.py install --root="${pkgdir}" --optimize=1
}

