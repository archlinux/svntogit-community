# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.2.7
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.python.org/packages/source/s/sure/sure-$pkgver.tar.gz")
sha512sums=('408d7334c3b989e29f757a31e137cfda6ccc3b4e03ed0a38bc7d72a23ff3ced11e1112b5d2597e20653c034330ec69ec9e713787516283e71ca404b1b1e770e1')

prepare() {
  cp -r "sure-$pkgver"{,-py2}
}

check() {
  cd sure-$pkgver
  nosetests3

  cd ../sure-$pkgver-py2
  nosetests2
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure-$pkgver
  LC_CTYPE=en_US.UTF-8 python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-$pkgver-py2
  LC_CTYPE=en_US.UTF-8 python2 setup.py install --root="${pkgdir}" --optimize=1
}

