# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.2.24
pkgrel=2
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.python.org/packages/source/s/sure/sure-$pkgver.tar.gz")
sha512sums=('875d2cb220d8915737909df372bedc2266c5c948c6f5ea23f3de5ee57b0f1179d2a0ae3455228d71a4f0f4279c098bb4c142a862b55462d67fd425c0def49a8b')

prepare() {
  cp -a "sure-$pkgver"{,-py2}
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure-$pkgver
  LC_CTYPE=en_US.UTF-8 python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-$pkgver-py2
  LC_CTYPE=en_US.UTF-8 python2 setup.py install --root="${pkgdir}" --optimize=1
}

