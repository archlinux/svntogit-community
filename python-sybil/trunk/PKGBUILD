# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sybil
pkgname=('python-sybil' 'python2-sybil')
pkgver=1.0.8
pkgrel=2
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cjw296/sybil/archive/$pkgver.tar.gz")
sha512sums=('fa3b00d1af6b0ba7a0f527d3b64c45438025442057a201f6200c506c531ff8cd31b0c0caeb8f23fb7ad87befd2aecbad3ae3549a0b0106d2c2b75a2c0eee3adc')

prepare() {
  cp -a sybil-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py build

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py pytest

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py pytest
}

package_python-sybil() {
  depends=('python')

  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sybil() {
  depends=('python2')

  cd sybil-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
