# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Spies <lukas (at) photoqt _dot_ org>

pkgname=python-symengine
_pkgname=symengine.py
pkgver=0.8.1
pkgrel=2
arch=(x86_64)
pkgdesc='Python wrappers for SymEngine'
url='https://github.com/symengine/symengine.py'
depends=(python symengine)
makedepends=(cmake cython)
license=(MIT)
source=(https://github.com/symengine/symengine.py/archive/v$pkgver/$_pkgname-$pkgver.tar.gz)
sha256sums=('02fe79e6d5e9b39a1d4e6fee05a2c1d1b10fd032157c7738ed97e32406ffb087')

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $_pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
