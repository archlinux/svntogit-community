# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgbase=python-sympy
pkgname=('python2-sympy' 'python-sympy')
pkgver=0.7.6
pkgrel=2
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='http://sympy.org/en/index.html'
license=('BSD')
makedepends=('python2-mpmath' 'python-mpmath' 'git')
source=("git+https://github.com/sympy/sympy.git#tag=sympy-${pkgver}"
        sympy-0.7.6-strip-internal-mpmath.patch)
sha512sums=('SKIP'
            '0b3836580e742c4a22c0b7306a219ebf56eb87b62f7c119cbe56887e642a28097f5223af043874968183601c06c327ad2beade9f34066ab2fd49d365cbc2caec')

prepare() {
  cd sympy
  # FS#43210
  patch -p1 -i ../sympy-0.7.6-strip-internal-mpmath.patch

  cd "$srcdir"
  cp -a sympy py3-sympy

  sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
         -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
    sympy/sympy/mpmath/tests/{runtests.py,test_eigen.py,test_levin.py,test_eigen_symmetric.py} \
    sympy/sympy/mpmath/matrices/{eigen.py,eigen_symmetric.py} \
    sympy/sympy/utilities/tests/diagnose_imports.py sympy/sympy/benchmarks/bench_symbench.py
  sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python3|' \
         -e 's|#!/usr/bin/python|#!/usr/bin/python3|' \
    py3-sympy/sympy/mpmath/tests/{runtests.py,test_eigen.py,test_levin.py,test_eigen_symmetric.py} \
    py3-sympy/sympy/mpmath/matrices/{eigen.py,eigen_symmetric.py} \
    py3-sympy/sympy/utilities/tests/diagnose_imports.py py3-sympy/sympy/benchmarks/bench_symbench.py
}

build() {
  cd sympy
  python2 setup.py build

  cd ../py3-sympy
  python3 setup.py build
}

check() {
  cd sympy
  python2 setup.py test

  cd ../py3-sympy
  python3 setup.py test
}

package_python2-sympy() {
  depends=('python2-mpmath')
  optdepends=('python2-pyglet: plotting'
              'ipython2: user friendly interface for isympy')

  cd sympy

  python2 setup.py install --root "${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-sympy() {
  depends=('python-mpmath')
  optdepends=('ipython: user friendly interface for isympy')

  cd py3-sympy

  python3 setup.py install --root "${pkgdir}" --optimize=1

  # rename files that exists in both 'python2-sympy' and 'python-sympy'
  mv "${pkgdir}"/usr/bin/isympy{,-py3}
  mv "${pkgdir}"/usr/share/man/man1/isympy{,-py3}.1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
