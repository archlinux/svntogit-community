# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgbase=python-sympy
pkgname=('python-sympy' 'python2-sympy')
pkgver=1.4
pkgrel=1
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='http://sympy.org/en/index.html'
license=('BSD')
makedepends=('python2-mpmath' 'python-mpmath' 'python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sympy/sympy/archive/sympy-$pkgver.tar.gz")
sha512sums=('fdf2113478393ac71c08a440c97ded1d11e2cb630da237a4e32d03a54fda1bb51c6317a757e5d51683fc1ff7ed865cf3b4c02927bec5974eb68bacc5b88f865a')

prepare() {
  cp -a sympy-sympy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sympy-sympy-$pkgver
  python setup.py build

  cd "$srcdir"/sympy-sympy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sympy-sympy-$pkgver
  python setup.py test || warning "Tests failed"

  cd "$srcdir"/sympy-sympy-$pkgver-py2
  python2 setup.py test || warning "Tests failed"
}

package_python-sympy() {
  depends=('python-mpmath')
  optdepends=('ipython: user friendly interface for isympy')

  cd sympy-sympy-$pkgver

  python setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sympy() {
  depends=('python2-mpmath')
  optdepends=('python2-pyglet: plotting'
              'ipython2: user friendly interface for isympy')

  cd sympy-sympy-$pkgver-py2

  python2 setup.py install --root "$pkgdir" --optimize=1

  # rename files that exists in both 'python-sympy' and 'python2-sympy'
  mv "$pkgdir"/usr/bin/isympy{,2}
  mv "$pkgdir"/usr/share/man/man1/isympy{,2}.1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
