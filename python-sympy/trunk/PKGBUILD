# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgbase=python-sympy
pkgname=('python2-sympy' 'python-sympy')
pkgver=1.0rc1
_commit=323d4a54eb94f6b79f32dd69779bf1952097def9
pkgrel=1
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='http://sympy.org/en/index.html'
license=('BSD')
makedepends=('python2-mpmath' 'python-mpmath' 'python-setuptools' 'python2-setuptools' 'git')
#source=("git+https://github.com/sympy/sympy.git#tag=sympy-${pkgver}"
source=("git+https://github.com/sympy/sympy.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a sympy py3-sympy
}

build() {
  cd sympy
  python2 setup.py build

  cd ../py3-sympy
  python3 setup.py build
}

check() {
  # Broken, waiting for upstream fixes.

  cd sympy
  python2 setup.py test || warning "Exit code: $?"

  cd ../py3-sympy
  python3 setup.py test || warning "Exit code: $?"
}

package_python2-sympy() {
  depends=('python2-mpmath')
  optdepends=('python2-pyglet: plotting'
              'ipython2: user friendly interface for isympy')

  cd sympy

  python2 setup.py install --root "${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-sympy() {
  depends=('python-mpmath')
  optdepends=('ipython: user friendly interface for isympy')

  cd py3-sympy

  python3 setup.py install --root "${pkgdir}" --optimize=1

  # rename files that exists in both 'python2-sympy' and 'python-sympy'
  mv "${pkgdir}"/usr/bin/isympy{,-py3}
  mv "${pkgdir}"/usr/share/man/man1/isympy{,-py3}.1

  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
