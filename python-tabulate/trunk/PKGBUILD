# Maintainer: David Runge <dvzrv@archlinux.org>
_name=tabulate
pkgname=python-tabulate
pkgver=0.8.6
pkgrel=1
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
depends=('python-setuptools')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-pandas' 'python-wcwidth')
optdepends=('python-numpy: NumPy array support'
            'python-pandas: pandas.DataFrame support'
            'python-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('10e331db1f8d6ea921654c3955efc4929827b003328c4e2ae663748ce5d5a3e473fe7abb4606147c00aec60ff44d3115c353d6f0e1417305166343212d77600e')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  python setup.py test
}

package_python-tabulate() {
  cd "${_name}-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
