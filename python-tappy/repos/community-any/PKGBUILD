# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>

pkgname=python-tappy
_pyname=tap.py
pkgver=3.0
pkgrel=1
pkgdesc="Test Anything Protocol (TAP) tools for Python"
arch=(any)
url="https://pypi.python.org/pypi/tap.py"
license=('BSD')
depends=('python' 'python-babel' 'python-pyaml' 'python-more-itertools')
checkdepends=('python-tests')
makedepends=('python-mock')
options=(!emptydirs)
source=(https://files.pythonhosted.org/packages/source/t/${_pyname}/${_pyname}-${pkgver}.tar.gz)
sha256sums=('f5eeeeebfd64e53d32661752bb4c288589a3babbb96db3f391a4ec29f1359c70')

check() {
	cd "$_pyname-$pkgver"
	python setup.py test
}

package() {
	cd "$_pyname-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
