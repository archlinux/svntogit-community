# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-tblib
_pyname=tblib
pkgver=1.6.0
pkgrel=1
pkgdesc='Python traceback fiddling library'
url='https://github.com/ionelmc/python-tblib'
arch=('any')
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools' 'python-six')
source=(https://github.com/ionelmc/python-tblib/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('64d580f6172a31d9d54b9468f98d7d6103e0304c3a3c92d75347780fc16bcbc5e06c03ff14a714a105fa1cf3426f76d4a19f23dafd979111eb80b9113ce4eb7f')
b2sums=('154f7818626805a591c01a677c110d20aafb697221d257eea41c1e0cf3623968a353c087909a023e9cbc6d927f015e90904d2ecf7d6fd5b0dd6939948fd918c2')

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgbase}-${pkgver}
  python setup.py test
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
