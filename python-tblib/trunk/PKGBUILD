# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-tblib
pkgname=('python-tblib' 'python2-tblib')
_pyname=tblib
pkgver=1.3.2
pkgrel=2
pkgdesc='Python traceback fiddling library'
url='https://github.com/ionelmc/python-tblib'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/ionelmc/python-tblib/archive/v${pkgver}.tar.gz)
sha256sums=('6eec32b8bac0a30a5a079add4983653da5fa717e986a9c304cd9c7273d3c29bc')
sha512sums=('68f338024c574b2cc6bd144c2e1ade23f2e4f37866d81640ec655aea43f736156eca52728bfb6797caa6723c45ee62328172490ac6387be150c5d28d99f62784')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

check() {
  (cd ${pkgbase}-${pkgver}
    python setup.py test
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-tblib() {
  depends=('python-six')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-tblib() {
  depends=('python2-six')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
