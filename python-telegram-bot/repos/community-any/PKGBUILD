# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-telegram-bot
pkgver=12.8
pkgrel=2
pkgdesc="A pure Python interface for the Telegram Bot AP"
url="https://github.com/python-telegram-bot/python-telegram-bot"
license=('LGPL')
arch=('any')
depends=('python-cryptography' 'python-decorator' 'python-tornado' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-flaky'
              'python-pytest-timeout')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-telegram-bot/python-telegram-bot/archive/v$pkgver.tar.gz"
        fix-tests.patch)
sha512sums=('84c033b946e1d93b10c36c39f3681631917aa4642da1795c6ecad694a7d8871888c23fa80aaf3827b0bdc8de1abf341a8ecc8326c825efd8a873df7c2904c5f2'
            '3023cce38549b74a429a18168556e9641107d189fbe9c793e979964b094ad1b4863e19b2c400aeb44697afca0ce8a1de83c1f1bdd2dda6bf97b93817b2b41231')

prepare() {
  cd python-telegram-bot-$pkgver

  patch -Np1 -i ../fix-tests.patch

  sed -i '/certifi/d' telegram/__main__.py requirements.txt
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i telegram/utils/request.py tests/test_official.py
  sed -e 's/import telegram.vendor.ptb_urllib3.urllib3/import urllib3/' \
      -e 's/from telegram.vendor.ptb_urllib3.urllib3/from urllib3/' \
      -e 's/from telegram.vendor.ptb_urllib3 import urllib3/import urllib3/' \
      -i telegram/utils/request.py tests/test_{bot,official}.py
}

build() {
  cd python-telegram-bot-$pkgver
  python setup.py build
}

check() {
  cd python-telegram-bot-$pkgver
  python -m pytest
}

package() {
  cd python-telegram-bot-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
