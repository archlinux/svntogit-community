# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=6.2.0
pkgrel=3
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
depends=('python-six')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-typeguard')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/tenacity/archive/$pkgver.tar.gz")
sha512sums=('0cabbaeedf45ff5a6c8855b459a47d30e7c69fbfdde8b213878af9fa1f6bf576cc2f733629a599bdbb2e2e68efe3e687876d926070b87615f4c61e042965597d')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tenacity-$pkgver
  python setup.py build
}

check() {
  cd tenacity-$pkgver
  python setup.py pytest
}

package() {
  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
