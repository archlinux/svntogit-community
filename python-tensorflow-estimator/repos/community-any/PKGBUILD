# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Konstantin Gizdov (kgizdov) <arch@kge.pw>

pkgname=python-tensorflow-estimator
pkgver=2.0.0
_pkgver=2.0.0
pkgrel=1
pkgdesc="A high-level TensorFlow API that greatly simplifies machine learning programming"
url="https://github.com/tensorflow/estimator"
license=('APACHE')
arch=('any')
depends=('python-wrapt')
makedepends=('bazel' 'python-wheel' 'python-setuptools' 'python-pip' 'python-tensorflow')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/tensorflow/estimator/archive/v${_pkgver}.tar.gz")
sha512sums=('99bbdb4be9cd9f46128657b262fc6daa61a1dbfad6c430c43bb62ef99cefec10b10bf03090adcb21bdac0404e00084b4e5c4961761a52462afdeb105ef1e0c73')

build() {
  cd estimator-${_pkgver}

  bazel build //tensorflow_estimator/tools/pip_package:build_pip_package
  bazel-bin/tensorflow_estimator/tools/pip_package/build_pip_package ${srcdir}/estimator_pip
}

check() {
  cd estimator-${_pkgver}

  # Of course, upstream doesn't care about working tests...
  # Revisit this at some point.
  # bazel test //tensorflow_estimator/...
}

package() {
  cd estimator-${_pkgver}

  WHEEL_PACKAGE=$(find ${srcdir}/estimator_pip -name "tensor*.whl")
  pip install --ignore-installed --upgrade --root "$pkgdir"/ $WHEEL_PACKAGE --no-dependencies
}
