# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: getzze <getzze at gmail dot com>
pkgname=python-tensorflow-serving-api
_name=tensorflow_serving_api
pkgver=1.14.0rc0
_pkgver=1.14.0-rc0
pkgrel=1
pkgdesc="Flexible, high-performance serving system for machine learning models, designed for production environments"
arch=(any)
url="https://www.tensorflow.org/serving/"
license=('Apache')
depends=('python' 'python-grpcio' 'python-tensorflow' 'python-protobuf')
makedepends=('bazel' 'python-wheel' 'python-pip')
source=("https://github.com/tensorflow/serving/archive/${_pkgver}.tar.gz")
sha256sums=('af50117af512e4b9866f29741894f221fa18349bb156dbf26ac6549b75b6ad26')

build() {
  cd serving-*
  bazel build -c opt tensorflow_serving/tools/pip_package:build_pip_package
  bazel-bin/tensorflow_serving/tools/pip_package/build_pip_package /tmp/pip
}

package() {
  cd serving-*
  pip install --ignore-installed --upgrade --root "${pkgdir}"/ /tmp/pip/tensorflow_serving*.whl --no-dependencies
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
