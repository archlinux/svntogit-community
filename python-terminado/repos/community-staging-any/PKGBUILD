# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Will Shanks <wsha dot code at g mail dot com>

pkgbase=python-terminado
pkgname=(python-terminado python2-terminado)
_pkgname=terminado
pkgver=0.6
pkgrel=2
pkgdesc="Terminals served to term.js using Tornado websockets"
url="https://github.com/takluyver/terminado"
arch=('any')
license=('BSD')
depends=('python-tornado' 'python-ptyprocess')
makedepends=('python-setuptools' 'python2-setuptools'
             'python-tornado' 'python-ptyprocess'
             'python2-tornado' 'python2-ptyprocess')
source=("http://pypi.python.org/packages/source/t/$_pkgname/$_pkgname-$pkgver.tar.gz"
        'https://raw.githubusercontent.com/takluyver/terminado/master/LICENSE.txt')
md5sums=('5b6c65da27fe1ed07a9f80f0588cdaba'
         '242c3ea7df130edf77dc0e088ac1847c')

prepare() {
  cd "$srcdir"
  cp -r $_pkgname-$pkgver ${_pkgname}2-$pkgver
}

package_python-terminado() {
  cd "$srcdir/$_pkgname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 "$srcdir/LICENSE.txt" "$pkgdir/usr/share/licenses/python-$_pkgname/LICENSE"
}

package_python2-terminado() {
  depends=('python2-tornado' 'python2-ptyprocess')
  cd "$srcdir/${_pkgname}2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 "$srcdir/LICENSE.txt" "$pkgdir/usr/share/licenses/python2-$_pkgname/LICENSE"
}

