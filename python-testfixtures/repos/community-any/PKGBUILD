# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testfixtures
pkgname=('python-testfixtures' 'python2-testfixtures')
pkgver=4.13.5
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock'
              'python-manuel' 'python2-manuel' 'python-nose-fixes' 'python2-nose-fixes')
source=("git+https://github.com/Simplistix/testfixtures.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a testfixtures{,-py2}
}

build() {
  cd "$srcdir"/testfixtures
  python setup.py build

  cd "$srcdir"/testfixtures-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/testfixtures
  nosetests3 || warning "Tests failed"

  cd "$srcdir"/testfixtures-py2
  nosetests2
}

package_python-testfixtures() {
  depends=('python')

  cd testfixtures
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

package_python2-testfixtures() {
  depends=('python2')

  cd testfixtures-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
