# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testfixtures
pkgname=('python-testfixtures' 'python2-testfixtures')
pkgver=5.4.0
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-zope-component' 'python2-zope-component' 'python-sybil' 'python2-sybil'
              'python-pytest-django' 'python2-pytest-django' 'python-django' 'python2-django')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Simplistix/testfixtures/archive/$pkgver.tar.gz")
sha512sums=('a81af203b003e78c1da4ae858e4eca17732c297578132aa7d3aa50e63245a5ec2c8ea8256c41282e7b8405effad6b5f78dce6d9e61cea9e4697bf8aa438817c7')

prepare() {
  cp -a testfixtures-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py build

  cd "$srcdir"/testfixtures-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"

  cd "$srcdir"/testfixtures-$pkgver-py2
  python2 setup.py pytest --addopts "--ignore=build"
}

package_python-testfixtures() {
  depends=('python')

  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-testfixtures() {
  depends=('python2')

  cd testfixtures-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
