# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testresources
pkgname=('python-testresources' 'python2-testresources')
pkgver=2.0.1
pkgrel=3
pkgdesc="Testresources, a pyunit extension for managing expensive test resources"
arch=('any')
license=('Apache' 'BSD')
url="https://launchpad.net/testresources"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-testtools' 'python2-testtools')
source=("https://pypi.io/packages/source/t/testresources/testresources-$pkgver.tar.gz")
sha512sums=('0b23432b019400fe378e957d14a91083eef7fe1130a494b567a5274d69e4e8e09ab75861e5453c3f217507e144cf179c908f84dd1a807992d4bee0b8d32cb0f4')

prepare() {
  cp -a testresources-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/testresources-$pkgver
  python setup.py build

  cd "$srcdir"/testresources-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/testresources-$pkgver
  python -m testtools.run discover

  cd "$srcdir"/testresources-$pkgver-py2
  python2 -m testtools.run discover
}

package_python-testresources() {
  depends=('python')

  cd testresources-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-testresources() {
  depends=('python2')

  cd testresources-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
