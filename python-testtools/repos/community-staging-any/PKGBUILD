# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testtools
pkgname=('python-testtools' 'python2-testtools')
pkgver=2.2.0
pkgrel=2
pkgdesc="Extensions to the Python standard library unit testing framework"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/testtools"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyrsistent' 'python2-pyrsistent'
             'git' 'python-pbr' 'python2-pbr' 'python-extras' 'python2-extras'
             'python-fixtures' 'python2-fixtures' 'python-mimeparse' 'python2-mimeparse'
             'python2-unittest2' 'python2-traceback2')
checkdepends=('python-testscenarios' 'python2-testscenarios')
source=("git+https://github.com/testing-cabal/testtools.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a testtools{,-py2}

  # These are for python 2.x and old python 3.x only
  sed -i 's|unittest2|unittest|' testtools/testtools/tests/test_run.py testtools/testtools/tests/test_testsuite.py testtools/testtools/tests/twistedsupport/test_matchers.py testtools/testtools/tests/twistedsupport/test_deferred.py testtools/testtools/tests/twistedsupport/test_runtest.py
  sed -i '/unittest2>=1.0.0/d' testtools/requirements.txt
  sed -i 's|import linecache2 as linecache|import linecache|' testtools/testtools/tests/test_compat.py
  sed -i "s|traceback = try_import('traceback2')|import traceback|" testtools/testtools/content.py
  sed -i '/traceback2/d' testtools/requirements.txt
}

build() {
  cd "$srcdir/testtools"
  python setup.py build

  cd "$srcdir/testtools-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/testtools"
  python -m testtools.run testtools.tests.test_suite

  cd "$srcdir/testtools-py2"
  python2 -m testtools.run testtools.tests.test_suite
}

package_python-testtools() {
  depends=('python-pbr' 'python-extras' 'python-fixtures' 'python-pyrsistent' 'python-mimeparse')

  cd testtools
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-testtools() {
  depends=('python2-pbr' 'python2-extras' 'python2-fixtures' 'python2-pyrsistent' 'python2-mimeparse' 'python2-unittest2' 'python2-traceback2')

  cd testtools-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
