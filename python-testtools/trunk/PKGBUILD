# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testtools
pkgver=2.5.0
pkgrel=2
pkgdesc="Extensions to the Python standard library unit testing framework"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/testtools"
depends=('python-pbr' 'python-extras' 'python-fixtures')
makedepends=('python-setuptools')
checkdepends=('python-testscenarios')
source=("https://github.com/testing-cabal/testtools/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('81e2a31f34461aefa52a553966deb979e2cc8632c0fe2b87640925958d403598e45a3a16eb35d583894fac507670b349cebae002ae5376185e86032ebfb5da80')

export PBR_VERSION=$pkgver

build() {
  cd testtools-$pkgver
  python setup.py build
}

check() {
  cd testtools-$pkgver
  python -m testtools.run testtools.tests.test_suite
}

package() {
  cd testtools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
