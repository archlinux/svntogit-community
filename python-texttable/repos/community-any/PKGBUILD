# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: rich_o <rich_o@lavabit.com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

pkgname=(python-texttable python2-texttable)
pkgver=0.8.4
pkgrel=1
pkgdesc="Generate a formatted text table using ASCII characters"
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('LGPL')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/t/texttable/texttable-${pkgver}.tar.gz")
sha512sums=('18c128da721f25d4cf8b2f2d740c9ce2ded81135d2843e1d46d3e230b59820a7d6d1f7b3e0245cbc6208f3e16ac9181e0b1f56206e53acd9966616e539e91639')

prepare() {
  cp -a texttable-$pkgver{,-py2}
}

build() {
  cd "$srcdir/texttable-$pkgver"
  python setup.py build

  cd "$srcdir/texttable-$pkgver-py2"
  python2 setup.py build
}

package_python-texttable() {
  depends=('python')

  cd "$srcdir/texttable-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-texttable() {
  depends=('python2')

  cd "$srcdir/texttable-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
