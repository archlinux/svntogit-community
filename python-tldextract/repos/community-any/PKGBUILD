# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=2.2.1
pkgrel=1
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-mock' 'python-responses')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('db4f14e514c0e68f85f28226343d7ec91ceea2843ee01c9a2950f0972f365bf343640e07cfb118aff52bab975719be71d179fc6b3bd4da7eb62a47b62747cc0b')

prepare() {
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  python setup.py pytest
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # use the snapshot version, because generating a new one requires Internet access and root permission
  ln -s .tld_set_snapshot "$pkgdir"/usr/lib/python3.7/site-packages/tldextract/.tld_set
}

# vim:set sw=2 et:
