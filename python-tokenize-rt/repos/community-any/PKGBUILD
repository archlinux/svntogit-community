# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=tokenize-rt
pkgname=python-tokenize-rt
pkgver=4.1.0
pkgrel=1
pkgdesc='Wrapper around the stdlib `tokenize` which roundtrips'
arch=('any')
url=https://github.com/asottile/tokenize-rt
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('c3b2e16a375701f9ed328f456838d7e2058388776c2dd93be488663b9bb1c5e9940acac582ae46ddd8b8082484a8245a69af1c1560b55cfa03c3da314c0834ad')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --root=temp --optimize=1 --skip-build
  PYTHONPATH="$PWD/temp/$sitepackages" pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
