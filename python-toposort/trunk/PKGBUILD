# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-toposort
pkgver=1.6
pkgrel=1
pkgdesc='Implements a topological sort algorithm'
arch=('any')
license=('Apache')
url='https://gitlab.com/ericvsmith/toposort'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/t/toposort/toposort-$pkgver.tar.gz")
sha512sums=('8dc30b5d5a6f476756520236bc93840243690779783ec8f0b5ef0f72c64d774177b25d10d2e024cda968f81352abbc4f89f3069760fc3a86bd5ee387e7951928')

build() {
  cd toposort-$pkgver
  python setup.py build
}

check() {
  cd toposort-$pkgver
  python setup.py test
}

package() {
  cd toposort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
