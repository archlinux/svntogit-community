# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-toposort
pkgver=1.7
pkgrel=1
pkgdesc='Implements a topological sort algorithm'
arch=('any')
license=('Apache')
url='https://gitlab.com/ericvsmith/toposort'
depends=('python')
makedepends=('python-pyproject2setuppy')
source=("https://pypi.io/packages/source/t/toposort/toposort-$pkgver.tar.gz")
sha512sums=('a585d1c103b2162fa3f37f6b0ef60a8709f08d9543d7fab619af3718bf9f752a293e50061d370fd08872b2d8a8db504cb9d9c1cce5446885b46fe8e833813833')

build() {
  cd toposort-$pkgver
  python -m pyproject2setuppy.main build
}

check() {
  cd toposort-$pkgver
  make test
}

package() {
  cd toposort-$pkgver
  python -m pyproject2setuppy.main install --root="$pkgdir" --optimize=1
}
