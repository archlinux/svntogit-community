# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=('python-tornado' 'python2-tornado')
pkgver=3.2.1
pkgrel=1
pkgdesc='open source version of the scalable, non-blocking web server and tools'
arch=('i686' 'x86_64')
url='http://www.tornadoweb.org/'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python2-backports.ssl_match_hostname' 'git')
checkdepends=('python-pycurl' 'python2-pycurl' 'python-mock' 'python2-mock' 'twisted')
source=("git+https://github.com/facebook/tornado.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a tornado{,-py2}

  # python -> python2 rename
  find tornado-py2 -name '*py' -exec sed -e 's_#!/usr/bin/env python_&2_' -i {} \;
}

build() {
  cd tornado
  python setup.py build

  cd ../tornado-py2
  python2 setup.py build
}

check() {
  cd tornado
  python -m tornado.test.runtests || :
  python -m tornado.test.runtests --ioloop=tornado.platform.asyncio.AsyncIOLoop || :
  python -m tornado.test.runtests --ioloop=tornado.platform.select.SelectIOLoop || :

  cd ../tornado-py2
  python2 -m tornado.test.runtests || :
  python2 -m tornado.test.runtests --ioloop=tornado.platform.twisted.TwistedIOLoop || :
  python2 -m tornado.test.runtests --ioloop=tornado.platform.select.SelectIOLoop || :
}

package_python-tornado() {
  depends=('python')

  cd tornado
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-tornado() {
  depends=('python2' 'python2-backports.ssl_match_hostname')

  cd tornado-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

