# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch.de>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>

pkgbase=python-tox
pkgname=(python-tox python2-tox)
pkgver=3.20.1
pkgrel=4
pkgdesc='Python virtualenv management and testing tool'
arch=('any')
url='https://tox.readthedocs.io'
license=('GPL2')
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-py' 'python2-py' 'python-virtualenv'
             'python2-virtualenv' 'python-pluggy' 'python2-pluggy' 'python-toml' 'python2-toml'
             'python-filelock' 'python2-filelock' 'python-freezegun' 'python2-freezegun')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-mock'
              'python2-pytest-mock' 'python-flaky' 'python2-flaky' 'python2-pathlib2')
source=("https://pypi.io/packages/source/t/tox/tox-$pkgver.tar.gz"
        "0001-tests-do-not-depend-on-pathlib2-for-modern-python-17.patch")
sha512sums=('73d6b6ca98cc9de628038360fe0af50ecfac1e7949735fde49fd38d85c9fcc517d897eeb287da0388265e689941ef3b63956349b4be409197a164039cb1ba1ae'
            '602fff6eea913455e9b8ce7196d154960a4916bc395a99ed2660491134fbb3b48d6bbd30cbaf62b693ad0efdd50d78ca79ea72aa6162801c8d78543da5e36393')

prepare() {
  find tox-$pkgver -name "*.pyc" -delete

  # do not depend on pathlib2 on python3: https://github.com/tox-dev/tox/pull/1739
  cd "$srcdir"/tox-$pkgver
  patch -p1 -i ../0001-tests-do-not-depend-on-pathlib2-for-modern-python-17.patch
  cd ..

  cp -a tox-$pkgver{,-py2}
}

build() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/tox-$pkgver
  python setup.py build

  cd "$srcdir"/tox-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points

  (
    cd "$srcdir"/tox-$pkgver
    virtualenv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  )

  (
    cd "$srcdir"/tox-$pkgver-py2
    virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
    . "$srcdir/pyvenv-py2/bin/activate"
    python setup.py install
    python setup.py pytest
  )
}

package_python-tox() {
  depends=('python-py' 'python-virtualenv' 'python-setuptools' 'python-pluggy' 'python-toml'
           'python-filelock')

  cd "$srcdir"/tox-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  ln -s tox "$pkgdir"/usr/bin/tox3
}

package_python2-tox() {
  depends=('python2-py' 'python2-virtualenv' 'python2-setuptools' 'python2-pluggy' 'python2-toml'
           'python2-filelock')

  cd "$srcdir"/tox-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/tox{,2}
  mv "$pkgdir"/usr/bin/{tox-quickstart,tox2-quickstart}
}
