# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-tpm2-pytss
pkgver=1.1.0
_tag=17ed609956831a11576c2d7beaf956316e8f53d0 # git rev-parse "$pkgver"
pkgrel=1
pkgdesc='Python bindings for tpm2-tss'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-pytss'
license=('BSD')
depends=('python' 'python-asn1crypto' 'python-cffi' 'python-cryptography' 'tpm2-tss'
         'libtss2-esys.so' 'libtss2-fapi.so' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-tctildr.so')
makedepends=('git' 'python-build' 'python-installer' 'python-pkgconfig'
             'python-setuptools' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'swtpm')
source=("git+$url.git?signed#tag=$_tag")
sha512sums=('SKIP')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1') # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

pkgver() {
	cd "${pkgname#python-}"
	git describe | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
	cd "${pkgname#python-}"
	python -m build --wheel --no-isolation
}

check() {
	cd "${pkgname#python-}"
	cp build/lib.*/tpm2_pytss/_libtpm2_pytss.abi3.so tpm2_pytss
	cp build/lib.*/tpm2_pytss/internal/{type_mapping,versions}.py tpm2_pytss/internal
	python -B -m pytest
}

package() {
	cd "${pkgname#python-}"
	python -m installer --destdir="$pkgdir" dist/*.whl
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
