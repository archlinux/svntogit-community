# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-transaction
pkgver=3.0.0
pkgrel=3
pkgdesc='Transaction management for Python'
arch=('any')
license=('ZPL')
url='https://github.com/zopefoundation/transaction'
depends=('python-zope-interface')
makedepends=('python-setuptools' 'python-zope-interface')
checkdepends=('python-nose' 'python-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/transaction/archive/$pkgver.tar.gz")
sha512sums=('9f1c80fec205e8d13e8093c46dcd93fd27a5303bceca2b29e3d239f40c801355b131064fd695908cc166b8a4395d60d9deaec1a93cdbcd160417edd113c1fd8b')

build() {
  cd transaction-$pkgver
  python setup.py build
}

check() {
  cd transaction-$pkgver
  python setup.py nosetests
}

package() {
  cd transaction-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
