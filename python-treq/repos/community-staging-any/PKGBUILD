# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-treq
pkgver=20.9.0
pkgrel=3
pkgdesc="A requests-like API built on top of twisted.web's Agent"
arch=('any')
license=('MIT')
url='https://github.com/twisted/treq'
depends=('python-incremental' 'python-requests' 'python-pyopenssl' 'python-service-identity'
         'python-idna' 'python-attrs' 'python-twisted')
makedepends=('python-setuptools' 'httpbin')
checkdepends=('python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/twisted/treq/archive/release-$pkgver.tar.gz")
sha512sums=('65eb8d353d835d85cfeb0edadf6a91c9f215a70acf044d9e4a18a4a796a483543197f6b0ebaa89d33516305b2ea9d00bb0a3448db765135207f7a619cb4bd08a')

build() {
  cd treq-release-$pkgver
  python setup.py build
}

check() {
  cd treq-release-$pkgver
  PYTHONPATH="$PWD/build/lib" trial3 treq
}

package() {
  cd treq-release-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
