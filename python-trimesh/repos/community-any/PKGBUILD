# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-trimesh
pkgver=3.8.15
pkgrel=1
pkgdesc='Trimesh is a pure Python library for loading and using triangular meshes with an emphasis on watertight surfaces'
arch=('any')
url="https://github.com/mikedh/trimesh"
license=('MIT')
depends=(python python-setuptools python-numpy)
checkdepends=(python-scipy python-networkx python-rtree python-lxml
              python-shapely python-pillow python-sympy python-requests
              python-msgpack python-pyglet python-jsonschema
              python-svg.path python-collada)
optdepends=('python-networkx: graph operations'
            'python-pyglet: preview windows'
            'python-rtree: vector path handling'
            'python-scipy: convex hulls'
            'python-shapely: vector path handling'
            'python-pillow: load images'
            'python-jsonschema: validate JSON schemas like GLTF'
            'python-requests: network requests'
            'python-msgpack: serialize into msgpack'
            'python-lxml: handle XML better and faster than built- in XML'
            'python-sympy: do analytical math'
            'python-svg.path: handle SVG format path strings'
            'python-xxhash: hash ndarrays faster than built-in MD5/CRC'
            'python-collada: parse collada/dae/zae files')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mikedh/trimesh/archive/${pkgver}.tar.gz)
sha512sums=('0ac782340f7f68e9a4f9a0b3fac7724298fd2e42e20375363a99d40ad6b783f5a22d503793b3b4164adeb13f2ab74786eb0b096f178aabb66d35041a1e753764')

build() {
  cd trimesh-${pkgver}
  python setup.py build
}

check() {
  cd trimesh-${pkgver}
  python -m unittest discover tests
}

package() {
  cd trimesh-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 LICENSE.md "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
