# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-trio
pkgver=0.12.1
pkgrel=1
pkgdesc='A friendly Python library for async concurrency and I/O'
arch=(any)
url=https://github.com/python-trio/trio
license=(MIT)
depends=(
  python
  python-async_generator
  python-attrs
  python-idna
  python-outcome
  python-sniffio
  python-sortedcontainers
)
makedepends=(
  git
  python-setuptools
)
provides=(python-multio-provider)
source=(git+https://github.com/python-trio/trio.git#tag=v${pkgver})
sha256sums=('SKIP')

build() {
  cd trio

  python setup.py build
}

package() {
  cd trio

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-trio/
}

# vim: ts=2 sw=2 et:
