# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-trustme
_pyname=${pkgname/python-/}
pkgver=0.5.2
pkgrel=3
pkgdesc='Library for fake certificate authority (CA) to generate fake TLS certs'
url='https://trustme.readthedocs.io'
arch=('any')
license=('MIT')
depends=('python' 'python-cryptography')
makedepends=('python' 'python-setuptools' 'python-cryptography')
checkdepends=('python-pytest' 'python-pyopenssl' 'python-service_identity')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/python-trio/trustme/archive/v${pkgver}.tar.gz)
sha512sums=('f2afc6782daee036565c79aa86b176a734ed9d0bb7248a56c8be87806e54287ac579f26857e241ff4daff3f6a063afa9dbe927e0aa8439a37880fcf065afa526')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=. py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
