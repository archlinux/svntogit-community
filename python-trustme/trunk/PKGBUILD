# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-trustme
pkgname=('python-trustme' 'python2-trustme')
_pyname=${pkgbase/python-/}
pkgver=0.5.0
pkgrel=1
pkgdesc='Library for fake certificate authority (CA) to generate fake TLS certs'
url='https://trustme.readthedocs.io'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'python-cryptography'
             'python2' 'python2-setuptools' 'python2-cryptography')
checkdepends=('python-pytest' 'python-pyopenssl' 'python-service_identity')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/python-trio/trustme/archive/v${pkgver}.tar.gz)
sha512sums=('ea5dfb6c7cdaf086f9478147e2f9a4cd2e7c7d2059defbd2d660e6b7433ff81829771f8b0771c8a334354fa7ce8728f2f70eb374d01a9ac493be20cbbc740cff')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  python2 setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=. py.test
}

package_python-trustme() {
  depends=('python' 'python-cryptography')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-trustme() {
  depends=('python2' 'python2-cryptography')
  cd ${_pyname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
