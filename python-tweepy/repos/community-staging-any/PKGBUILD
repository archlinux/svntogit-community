# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-tweepy
_pypiname=tweepy
pkgver=3.10.0
pkgrel=2
pkgdesc='Python library for accessing the entire Twitter API'
url='https://www.tweepy.org/'
arch=('any')
license=('MIT')
depends=('python' 'python-requests' 'python-requests-oauthlib' 'python-six' 'python-pysocks')
makedepends=('python-setuptools' 'python-pip' 'python-requests' 'python-requests-oauthlib'
             'python-six' 'python-pysocks')
source=(https://github.com/tweepy/${_pypiname}/archive/v${pkgver}/${_pypiname}-${pkgver}.tar.gz)
sha256sums=('c29dea6ef56dd433a850b4a54214eeb61c555a81b4454bbba4376bf7a2f11c55')
sha512sums=('2a40875c5612902e4ff69e6fdf73e9fa10b93a80bf4160b16e44e2dae058a90f86569b3a4bcb67a7acdf5c9c3119ce7ca35a3ae64e393181b78fde10dfd975b5')

build() {
  cd ${_pypiname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
