# $Id$
# Maintainer: Anatol Pomozov

pkgbase=python-txaio
pkgname=(python-txaio python2-txaio)
pkgver=2.6.1
pkgrel=1
pkgdesc='Utilities to support code that runs unmodified on Twisted and asyncio'
arch=(any)
url='https://github.com/tavendo/txaio'
license=(MIT)
makedepends=(python-setuptools python2-setuptools python-twisted python2-twisted)
source=(https://pypi.io/packages/source/t/txaio/txaio-$pkgver.tar.gz)
sha1sums=('791d5ce2f4f0ab7188881c5eb7aedcb763b3afd1')

prepare() {
  cp -a txaio-$pkgver{,-py2}
}

build() {
  cd "$srcdir/txaio-$pkgver"
  python setup.py build

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/txaio-$pkgver"
  python setup.py test

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py test
}

package_python-txaio() {
  depends=(python python-six python-twisted)

  cd "$srcdir/txaio-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-txaio() {
  depends=(python2 python2-six python2-twisted)

  cd "$srcdir/txaio-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
