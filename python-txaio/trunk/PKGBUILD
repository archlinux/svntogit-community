# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov

pkgbase=python-txaio
pkgname=(python-txaio python2-txaio)
pkgver=2.8.2
pkgrel=1
pkgdesc='Compatibility API between asyncio/Twisted/Trollius'
arch=('any')
url='https://github.com/tavendo/txaio'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-twisted' 'python2-twisted')
source=("https://pypi.io/packages/source/t/txaio/txaio-$pkgver.tar.gz")
sha512sums=('8b60a64ebfa1208995bc42e6ccbc87c4c3cd4687cb431435c6450cff31e5d0dba468c3f3494902e666cb2ee6dea84ded458fd8f1b3b9c25421b440dcd3fc5884')

prepare() {
  cp -a txaio-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py build

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py test

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py test
}

package_python-txaio() {
  depends=('python-six')

  cd txaio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-txaio() {
  depends=('python2-six')

  cd txaio-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
