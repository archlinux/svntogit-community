# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=u-msgpack-python
pkgbase='python-u-msgpack'
pkgname=('python-u-msgpack' 'python2-u-msgpack')
pkgver=2.6.0
pkgrel=1
arch=('any')
url="https://github.com/vsergeev/${_pkgname}"
license=('MIT')
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('754edb07eaee39a9686a99823892e3a1be4e0948d9cc5c717946750c27643c9c')
b2sums=('a6599a85fcb386f91dce4d11fd8d10c6be75c1fe4e80756c58058bcb7c2934c2174afe65bc0b88e11c5c9e3f66b46c956c9209bb6faf74ab5c382bd665c412fc')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    pytest
    pytest2
}

package_python-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python"
    depends=('python')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-u-msgpack() {
    pkgdesc="portable, lightweight MessagePack serializer and deserializer for Python2"
    depends=('python2')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
