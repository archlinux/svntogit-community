# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-ukpostcodeparser
pkgname=('python-ukpostcodeparser' 'python2-ukpostcodeparser')
pkgver=1.0.3.20150130
_commit=5bf544de5db9e5251cc2b8c8e965e7485ebcbd5f
pkgrel=1
pkgdesc="UK Postcode parser"
arch=('any')
license=('MIT')
url="https://github.com/hamstah/ukpostcodeparser"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/hamstah/ukpostcodeparser.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a ukpostcodeparser{,-py2}
}

build() {
  cd "$srcdir/ukpostcodeparser"
  python setup.py build

  cd "$srcdir/ukpostcodeparser-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/ukpostcodeparser"
  python setup.py test

  cd "$srcdir/ukpostcodeparser-py2"
  python2 setup.py test
}

package_python-ukpostcodeparser() {
  depends=('python')

  cd ukpostcodeparser
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-ukpostcodeparser() {
  depends=('python2')

  cd ukpostcodeparser-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

