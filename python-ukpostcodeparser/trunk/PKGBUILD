# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-ukpostcodeparser
pkgname=('python-ukpostcodeparser' 'python2-ukpostcodeparser')
pkgver=1.1.2
pkgrel=2
pkgdesc="UK Postcode parser"
arch=('any')
license=('MIT')
url="https://github.com/hamstah/ukpostcodeparser"
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/hamstah/ukpostcodeparser/archive/v$pkgver.tar.gz")
sha512sums=('8113522b728c095f937a9f53f21c369130e3e63c4fc3546dbea4ad1c53648742fb0e766b1254c76ee65d69dec585f1a90caa7d39ca540de1765ff5f989a702de')

prepare() {
  cp -a ukpostcodeparser-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/ukpostcodeparser-$pkgver
  python setup.py build

  cd "$srcdir"/ukpostcodeparser-$pkgver-py2
  python2 setup.py build
}

check() {
  # Expected failure: https://github.com/hamstah/ukpostcodeparser/issues/8

  cd "$srcdir"/ukpostcodeparser-$pkgver
  python -m unittest discover -s ukpostcodeparser/test -p '*.py' || warning "Tests failed"

  cd "$srcdir"/ukpostcodeparser-$pkgver-py2
  python2 -m unittest discover -s ukpostcodeparser/test -p '*.py' || warning "Tests failed"
}

package_python-ukpostcodeparser() {
  depends=('python')

  cd ukpostcodeparser-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-ukpostcodeparser() {
  depends=('python2')

  cd ukpostcodeparser-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

