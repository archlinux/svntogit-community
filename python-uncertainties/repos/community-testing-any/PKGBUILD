# $Id: PKGBUILD 145754 2015-11-03 08:23:02Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-uncertainties
pkgname=('python-uncertainties' 'python2-uncertainties')
pkgver=3.0.2
pkgrel=2
pkgdesc="Transparent calculations with uncertainties on the quantities involved (aka error propagation); fast calculation of derivatives."
arch=('any')
license=('BSD')
url="https://github.com/lebigot/uncertainties"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-numpy' 'python2-numpy')
source=("git+https://github.com/lebigot/uncertainties.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a uncertainties{,-py2}
}

build() {
  cd "$srcdir"/uncertainties
  python setup.py build

  cd "$srcdir"/uncertainties-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/uncertainties
  python setup.py nosetests

  cd "$srcdir"/uncertainties-py2
  python2 setup.py nosetests
}

package_python-uncertainties() {
  depends=('python')
  optdepends=('python-numpy: additional support for NumPy arrays and matrices')

  cd uncertainties
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-uncertainties() {
  depends=('python2')
  optdepends=('python2-numpy: additional support for NumPy arrays and matrices')

  cd uncertainties-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
