# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-uncertainties
pkgver=3.1.2
pkgrel=2
pkgdesc="Transparent calculations with uncertainties on the quantities involved (aka error propagation); fast calculation of derivatives."
arch=('any')
license=('BSD')
url="https://github.com/lebigot/uncertainties"
depends=('python')
optdepends=('python-numpy: additional support for NumPy arrays and matrices')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lebigot/uncertainties/archive/$pkgver.tar.gz")
sha512sums=('78e8173f134138371834e7556c2eebc15bd2aa624addbdc77553997c269a88a51e80e00d89051cbbed8f709437b49e051f6df3a7b598e21bf0bf07bf05934ded')

build() {
  cd uncertainties-$pkgver
  python setup.py build
}

check() {
  cd uncertainties-$pkgver
  python setup.py nosetests
}

package() {
  cd uncertainties-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
