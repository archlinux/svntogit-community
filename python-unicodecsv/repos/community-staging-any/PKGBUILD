# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-unicodecsv
pkgname=(python-unicodecsv python2-unicodecsv)
pkgver=0.14.1
pkgrel=2
pkgdesc="Python2's stdlib csv module is nice, but it doesn't support unicode. This module is a drop-in replacement which *does*."
arch=('any')
url="https://github.com/jdunck/python-unicodecsv"
license=('BSD')
makedepends=('git' 'python-setuptools' 'python2-setuptools')
source=("git+https://github.com/jdunck/python-unicodecsv.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  sed -i 's/unittest2/unittest/' python-unicodecsv/{runtests.py,unicodecsv/test.py}
  sed -i '/unittest2/d' python-unicodecsv/setup.py
  cp -a python-unicodecsv{,-py2}
}

build() {
  cd "$srcdir"/python-unicodecsv
  python setup.py build

  cd "$srcdir"/python-unicodecsv-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-unicodecsv
  python setup.py test

  cd "$srcdir"/python-unicodecsv-py2
  python2 setup.py test
}

package_python-unicodecsv() {
  depends=('python')

  cd "$srcdir"/python-unicodecsv
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-unicodecsv() {
  depends=('python2')

  cd "$srcdir"/python-unicodecsv-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
