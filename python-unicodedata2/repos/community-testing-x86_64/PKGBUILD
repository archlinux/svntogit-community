# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com
# Contributor: Philipp A. <flying-sheep@web.de>

pkgname=python-unicodedata2
pkgver=13.0.0.post2
pkgrel=3
pkgdesc="unicodedata backport/updates to Python 3"
arch=('x86_64')
url="https://github.com/fonttools/unicodedata2"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fonttools/unicodedata2/archive/${pkgver/.post/-}.tar.gz"
        python310.patch)
sha256sums=('0bc40afc5938f3e1415524b724d3d4856dfcb8868d39e0b8aeced490494403cd'
            'e755dd4826b15a8514cae9102e9d33e8cba41841c24dcc2aff3960614ed0e755')

prepare() {
  cd "unicodedata2-${pkgver/.post/-}"
  patch -Np1 -i ../python310.patch
}

build() {
  cd "unicodedata2-${pkgver/.post/-}"

  python setup.py build
}

package() {
  cd "unicodedata2-${pkgver/.post/-}"

  python setup.py install --root="$pkgdir" --optimize=1
}
