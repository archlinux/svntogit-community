# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgbase=python-unidecode
pkgname=('python-unidecode' 'python2-unidecode')
pkgver=0.04.19
pkgrel=3
pkgdesc='ASCII transliterations of Unicode text'
arch=('any')
url='https://pypi.python.org/pypi/Unidecode/'
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
options=('!emptydirs')
source=("https://pypi.python.org/packages/source/U/Unidecode/Unidecode-${pkgver}.tar.gz")
sha256sums=('51477646a9169469e37e791b13ae65fcc75b7f7f570d0d3e514d077805c02e1e')

prepare() {
  for d in python{,2}-unidecode; do
    cp -r Unidecode-${pkgver} $d
  done
}

build() {
  for d in python{,2}-unidecode; do
    pushd $d
    ${d%-*} setup.py build
    popd
  done
}

package_python-unidecode() {
  depends=('python')

  cd python-unidecode

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-unidecode() {
  depends=('python2')

  cd python2-unidecode

  python2 setup.py install --root="${pkgdir}" --optimize='1'
  mv "${pkgdir}"/usr/bin/unidecode{,2}
}

# vim: ts=2 sw=2 et:
