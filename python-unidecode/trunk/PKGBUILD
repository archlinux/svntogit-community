# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgname=python-unidecode
pkgver=1.3.4
pkgrel=1
pkgdesc='ASCII transliterations of Unicode text'
arch=(any)
url=https://github.com/avian2/unidecode
license=(GPL)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=6f78d8596cd33d791f4c0b32fba74497afdf125c
source=(git+https://github.com/avian2/unidecode.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd unidecode

  git describe --tags | sed 's/^unidecode-//'
}

build() {
  cd unidecode

  python setup.py build
}

package() {
  cd unidecode

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
