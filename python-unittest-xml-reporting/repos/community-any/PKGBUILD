# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-unittest-xml-reporting
pkgver=3.0.4
pkgrel=1
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='https://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmlrunner/unittest-xml-reporting/archive/$pkgver.tar.gz")
sha512sums=('ef147e73f9c2311a919593775a359b2f168cc5647ecdd322afcd7849f1a17750909b82cf6e1b5c0fda0e0b3ec2970aeb4077d6006e060cadc4f8c0e22a06ba50')

build() {
  cd unittest-xml-reporting-$pkgver
  python setup.py build
}

check() {
  cd unittest-xml-reporting-$pkgver
  python setup.py test
}

package() {
  cd unittest-xml-reporting-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
