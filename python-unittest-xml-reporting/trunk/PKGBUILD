# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-unittest-xml-reporting
pkgname=('python-unittest-xml-reporting' 'python2-unittest-xml-reporting')
pkgver=2.1.0
pkgrel=2
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='http://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-lxml' 'python2-lxml')
source=("git+https://github.com/xmlrunner/unittest-xml-reporting.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a unittest-xml-reporting{,-py2}
}

build() {
  cd "$srcdir"/unittest-xml-reporting
  python setup.py build

  cd "$srcdir"/unittest-xml-reporting-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/unittest-xml-reporting
  python setup.py test

  cd "$srcdir"/unittest-xml-reporting-py2
  python2 setup.py test
}

package_python-unittest-xml-reporting() {
  depends=('python-six')

  cd unittest-xml-reporting
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-unittest-xml-reporting() {
  depends=('python2-six')

  cd unittest-xml-reporting-py2
  python2 setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
