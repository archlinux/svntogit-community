# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-update-checker
pkgname=('python-update-checker' 'python2-update-checker')
pkgver=0.16
pkgrel=2
pkgdesc="A python module that will check for package updates"
arch=('any')
license=('BSD')
url='https://github.com/bboe/update_checker'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
source=("https://pypi.io/packages/source/u/update_checker/update_checker-$pkgver.tar.gz")
md5sums=('dcd73326d7e4db4226575cca03d24e42')

prepare() {
  cp -a update_checker-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/update_checker-$pkgver
  python setup.py build

  cd "$srcdir"/update_checker-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/update_checker-$pkgver
  python setup.py test

  cd "$srcdir"/update_checker-$pkgver-py2
  python2 setup.py test
}

package_python-update-checker() {
  depends=('python-requests')
  provides=('python-update_checker')
  conflicts=('python-update_checker')
  replaces=('python-update_checker')

  cd update_checker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-update-checker() {
  depends=('python2-requests')
  provides=('python2-update_checker')
  conflicts=('python2-update_checker')
  replaces=('python2-update_checker')

  cd update_checker-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
