# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-update_checker
pkgname=('python-update_checker' 'python2-update_checker')
pkgver=0.11
pkgrel=2
pkgdesc="A python module that will check for package updates"
arch=('any')
license=('BSD')
url='https://github.com/bboe/update_checker'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
source=("https://pypi.python.org/packages/source/u/update_checker/update_checker-${pkgver}.tar.gz")
md5sums=('1daa54bac316be6624d7ee77373144bb')

prepare() {
  cp -a update_checker-$pkgver{,-py2}
}

check() {
  cd "$srcdir/update_checker-${pkgver}"
  python setup.py test

  cd "$srcdir/update_checker-${pkgver}-py2"
  python2 setup.py test
}

package_python-update_checker() {
  depends=('python-requests')

  cd "${srcdir}/update_checker-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python2-update_checker() {
  depends=('python2-requests')

  cd "${srcdir}/update_checker-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
