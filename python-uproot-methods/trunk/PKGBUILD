# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=uproot-methods
pkgname="python-${_pkgname}"
pkgver=0.10.1
pkgrel=1
pkgdesc="Pythonic mix-ins for ROOT classes"
arch=('any')
makedepends=('python-setuptools')
depends=('python-awkward' 'python-numpy')
checkdepends=('python-awkward' 'python-pytest')
url="https://github.com/scikit-hep/${_pkgname}"
license=('BSD')

source=("${pkgname}::${url}/archive/${pkgver}.zip")
sha256sums=('c15d427867ee469a68d2c50adfb4fc309ae53b660142d266c85543ee1333eec3')

get_pyver () {
    python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

build() {
    cd "${srcdir}/uproot3-methods-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/uproot3-methods-${pkgver}"
    pytest
}

package() {
    cd "${srcdir}/uproot3-methods-${pkgver}"

    python setup.py install --root="${pkgdir}/" --optimize=1

    install -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -D README.rst "${pkgdir}/usr/share/${pkgname}/README.rst"
    ln -s "/usr/lib/python$(get_pyver)/site-packages/uproot3_methods" "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/uproot_methods"
}
