# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Patrice Peterson <runiq at archlinux dot us>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: BorgHunter <borghunter at gmail dot com>

pkgname=(python-urllib3 python2-urllib3)
_pkgname=urllib3
pkgver=1.8
pkgrel=2
pkgdesc="HTTP library with thread-safe connection pooling and file post support"
arch=("any")
url="https://github.com/shazow/urllib3"
license=("MIT")
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-pyopenssl' 'python2-pyopenssl'
              'python-tornado' 'python2-tornado' 'python2-ndg-httpsclient' 'python-coverage' 'python2-coverage')
source=("git+https://github.com/shazow/$_pkgname.git#tag=$pkgver" 
        LICENSE)
md5sums=('SKIP'
         '350846ab4dd11ce105b570c15c1b0764')

prepare() {
  # Hack version requirements for test requirements to run tests - Disabled due to tests not runable
  #sed -e "s/==.*//" -i $_pkgname/test-requirements.txt

  cp -a $_pkgname{,-py2}
}

build() {
  cd "$srcdir/$_pkgname"
  python setup.py build

  cd "$srcdir/$_pkgname-py2"
  python2 setup.py build
}

check() {
  # Tests stuck

  cd "$srcdir/$_pkgname"
  #nosetests3 -e "pyopenssl"  # No ndg-httpsclient module to test with

  cd "$srcdir/$_pkgname-py2"
  #nosetests2
}

package_python-urllib3() {
  depends=('python')

  cd $_pkgname
  python setup.py install --root="${pkgdir}"
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-urllib3() {
  depends=('python2')

  cd $_pkgname-py2
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
