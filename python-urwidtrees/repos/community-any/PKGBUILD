# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ian Denhardt <ian@zenhack.net>

_pkgname=urwidtrees
pkgbase=python-$_pkgname
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=1.0.2
pkgrel=2
pkgdesc="Tree widgets for urwid."
arch=(any)
url="https://github.com/pazz/$_pkgname"
license=(GPL3)
makedepends=(python 'python-urwid>=1.1.0' python-sphinx
             python2 'python2-urwid>=1.1.0' python2-sphinx)
source=("$_pkgname-$pkgver.tar.gz::https://github.com/pazz/$_pkgname/archive/$pkgver.tar.gz")
md5sums=('07699e009203486f9c1e19c2fd5ef201')

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	python setup.py build
	make SPHINXBUILD=sphinx-build -C docs man html
}

package_python-urwidtrees() {
	depends=(python 'python-urwid>=1.1.0')
	cd "$srcdir/$_pkgname-$pkgver"

	python setup.py install --optimize=1 --root="$pkgdir"

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	cp -a docs/build/html/* "$pkgdir/usr/share/doc/$pkgname"
}

package_python2-urwidtrees() {
	depends=(python2 'python2-urwid>=1.1.0')
	cd "$srcdir/$_pkgname-$pkgver"

	python2 setup.py install --optimize=1 --root="$pkgdir"

	install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	cp -a docs/build/html/* "$pkgdir/usr/share/doc/$pkgname"
}
