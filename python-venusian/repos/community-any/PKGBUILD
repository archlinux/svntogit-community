# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=python-venusian
pkgname=('python-venusian' 'python2-venusian')
pkgver=1.1.0
pkgrel=2
pkgdesc="A library for deferring decorator actions"
arch=('any')
license=('custom:BSD')
url="http://pylonsproject.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/venusian/archive/$pkgver.tar.gz")
sha512sums=('a06b26111ace37c1243ac605f1684446247c6e077d15fa77e92c0913e1fc30a0ce339f725cce1414501345abccdfb75e4ac5727e7f4db9fe9757b45b9e29b1cb')

prepare() {
  cp -a venusian-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/venusian-$pkgver
  python setup.py build

  cd "$srcdir"/venusian-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/venusian-$pkgver
  python setup.py pytest

  cd "$srcdir"/venusian-$pkgver-py2
  python2 setup.py pytest
}

package_python-venusian() {
  depends=('python')

  cd venusian-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-venusian() {
  depends=('python2')

  cd venusian-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
