# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-visitor
pkgver=0.1.3
pkgrel=5
pkgdesc="A tiny pythonic visitor implementation"
url="https://github.com/mbr/visitor"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mbr/visitor/archive/$pkgver.tar.gz")
sha512sums=('414c77df8efcc22df5ee8073e02b8c5c28563954ef9b9a869c4ea3f7f1268a97929c1fe29142ba41e3a5acafea52599f6bf073aacd654643d7d8380cd4f41fbc')

build() {
  cd visitor-$pkgver
  python setup.py build
}

check() {
  cd visitor-$pkgver
  python setup.py pytest
}

package() {
  cd visitor-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
