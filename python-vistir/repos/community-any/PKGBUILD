# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vistir
pkgver=0.5.1
pkgrel=1
pkgdesc="Miscellaneous utilities for dealing with filesystems, paths, projects, subprocesses, and more"
url="https://github.com/sarugaku/vistir"
license=('BSD')
arch=('any')
depends=('python-colorama' 'python-requests' 'python-six')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-hypothesis-fspaths' 'python-pytest-cov'
              'python-pytest-timeout' 'python-yaspin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/vistir/archive/$pkgver.tar.gz")
sha512sums=('8f53ef0e8f2b1e0fe88d5ba0d5ddc86c4a3554915b34aa73d1aa3da3333a7ba808594b0e0c500f5427eef8d160f79bd177191727e2c7541dcf6b43c79a5b5c04')

build() {
  cd vistir-$pkgver
  python setup.py build
}

check() {
  cd vistir-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py pytest
}

package() {
  cd vistir-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
