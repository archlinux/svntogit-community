# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vistir
pkgver=0.5.0
pkgrel=1
pkgdesc="Miscellaneous utilities for dealing with filesystems, paths, projects, subprocesses, and more"
url="https://github.com/sarugaku/vistir"
license=('BSD')
arch=('any')
depends=('python-colorama' 'python-requests' 'python-six')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-hypothesis-fspaths' 'python-pytest-cov'
              'python-pytest-timeout' 'python-yaspin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/vistir/archive/$pkgver.tar.gz")
sha512sums=('6a2a87bfb7a3c1090e764eeb1ebf46f0155cff0270e9a88ffcc9cbb59f6a25c709213e77b06035da9fc55f810e4f94aa068dacb7050d0c46fd8ade9a36e70c9f')

build() {
  cd vistir-$pkgver
  python setup.py build
}

check() {
  cd vistir-$pkgver
  LC_CTYPE=en_US.UTF-8 python setup.py pytest
}

package() {
  cd vistir-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
