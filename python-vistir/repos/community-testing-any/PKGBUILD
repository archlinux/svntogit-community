# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vistir
pkgver=0.4.3
pkgrel=3
pkgdesc="Miscellaneous utilities for dealing with filesystems, paths, projects, subprocesses, and more"
url="https://github.com/sarugaku/vistir"
license=('BSD')
arch=('any')
depends=('python-colorama' 'python-requests' 'python-six')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-hypothesis-fspaths' 'python-pytest-cov'
              'python-pytest-timeout' 'python-yaspin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/vistir/archive/$pkgver.tar.gz")
sha512sums=('60da4b1886fc0642530f3d3159844a2ff9401952584d72acdb9fe1a8e3578f05ba5a784f3f77a7379dcb1d93f2aa5aecd48a392b8acea1126b8e5be99d3d2eec')

build() {
  cd vistir-$pkgver
  python setup.py build
}

check() {
  cd vistir-$pkgver
  python setup.py pytest
}

package() {
  cd vistir-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
