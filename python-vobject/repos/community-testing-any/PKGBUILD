# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>

_name=vobject
pkgbase=python-vobject
pkgname=('python-vobject' 'python2-vobject')
pkgver=0.9.5
pkgrel=2
pkgdesc='Module for parsing and generating vCard and vCalendar files'
url='https://eventable.github.io/vobject/'
license=('Apache')
arch=('any')
makedepends=('python-dateutil' 'python-setuptools' 'python2-dateutil' 'python2-setuptools')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/eventable/vobject/archive/${pkgver}.tar.gz")
sha512sums=('087b394ad6c48c359ee1cabb90cbc85cb305ed9e65199f33c19f386c7075b70273e5fddcf7fde5356b140ead7a581efd917594e4d493e720c4e1a632f797acc7')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}


package_python-vobject() {
  depends=('python-dateutil')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 README.md "${pkgdir}/usr/share/${pkgname[0]}/README.md"
}

package_python2-vobject() {
  depends=('python2-dateutil')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  mv -v "${pkgdir}/usr/bin/ics_diff" "${pkgdir}/usr/bin/ics_diff2"
  mv -v "${pkgdir}/usr/bin/change_tz" "${pkgdir}/usr/bin/change_tz2"
  install -vDm644 README.md "${pkgdir}/usr/share/${pkgname[1]}/README.md"
}

# vim:set ts=2 sw=2 et:

