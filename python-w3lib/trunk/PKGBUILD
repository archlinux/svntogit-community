# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bence Balint <balintbence1337@gmail.com>

pkgbase=python-w3lib
pkgname=('python-w3lib' 'python2-w3lib')
pkgver=1.18.0
pkgrel=1
pkgdesc="Library of web-related functions"
arch=('any')
license=('BSD')
url="http://w3lib.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/scrapy/w3lib/archive/v$pkgver.tar.gz")
sha512sums=('8409398fdf543e961d87317d6c8474d0522ac3d563808a75f52f6c5129e831af36ede33bf5235233e6c8c49931c7615d77db87288ae704de6940a713b0b08167')

prepare() {
  cp -a w3lib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/w3lib-$pkgver
  python setup.py build

  cd "$srcdir"/w3lib-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/w3lib-$pkgver
  py.test

  cd "$srcdir"/w3lib-$pkgver-py2
  py.test2
}

package_python-w3lib() {
  depends=('python-six')

  cd w3lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-w3lib() {
  depends=('python2-six')

  cd w3lib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
