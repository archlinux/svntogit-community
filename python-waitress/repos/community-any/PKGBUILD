# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-waitress
pkgname=('python-waitress' 'python2-waitress')
pkgver=1.4.3
pkgrel=1
pkgdesc="Waitress WSGI server"
arch=('any')
license=('ZPL')
url="https://github.com/Pylons/waitress"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/waitress/archive/v$pkgver.tar.gz")
sha512sums=('15454758dfd56ab6bdbe8f6bfe565a0a00419f032f7976203d5e700ee18729ff999492f29a552227f525bbd744363ed23cf9b414a718b162125f79b57b290b30')

prepare() {
  cp -a waitress-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/waitress-$pkgver
  python setup.py build

  cd "$srcdir"/waitress-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/waitress-$pkgver
  nosetests3

  cd "$srcdir"/waitress-$pkgver-py2
  nosetests2
}

package_python-waitress() {
  depends=('python-setuptools')

  cd waitress-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-waitress() {
  depends=('python2-setuptools')

  cd waitress-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/waitress-serve{,2}
}
