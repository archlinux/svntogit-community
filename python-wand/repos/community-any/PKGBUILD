# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wand
pkgver=0.5.7
pkgrel=2
pkgdesc="Ctypes-based simple MagickWand API binding for Python"
url="https://github.com/emcconville/wand"
license=('MIT')
arch=('any')
depends=('imagemagick' 'libxml2' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-memory-profiler' 'python-psutil'
              'ghostscript' 'librsvg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/emcconville/wand/archive/$pkgver.tar.gz")
sha512sums=('cdd30133dc35b87c9e956b61221d9f6ec711c8654d0b45b4e9f1ece30de7a374a01527260f08f5e16d6915546225d2bb72905da986abd1bdd29ef648de6451e6')

build() {
  cd wand-$pkgver
  python setup.py build
}

check() {
  cd wand-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd wand-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm "$pkgdir"/usr/README.rst
}
