# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-warlock
pkgname=('python-warlock' 'python2-warlock')
pkgver=1.3.0
pkgrel=3
pkgdesc="Python object model built on JSON schema and JSON patch."
arch=('any')
license=('Apache')
url="https://github.com/bcwaldon/warlock"
makedepends=('python-setuptools' 'python2-setuptools' 'python-jsonschema' 'python2-jsonschema'
             'python-jsonpatch' 'python2-jsonpatch' 'python-six' 'python2-six' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/bcwaldon/warlock.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a warlock{,-py2}
}

build() {
  cd "$srcdir"/warlock
  python setup.py build

  cd "$srcdir"/warlock-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/warlock
  python setup.py pytest

  cd "$srcdir"/warlock-py2
  python2 setup.py pytest
}

package_python-warlock() {
  depends=('python-jsonschema' 'python-jsonpatch' 'python-six')

  cd warlock
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-warlock() {
  depends=('python2-jsonschema' 'python2-jsonpatch' 'python2-six')

  cd warlock-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
