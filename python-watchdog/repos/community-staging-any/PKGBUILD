# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=('python-watchdog' 'python2-watchdog')
pkgver=0.8.3
pkgrel=6
pkgdesc="Python API and shell utilities to monitor file system events."
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pathtools' 'python2-pathtools'
             'python-yaml' 'python2-yaml' 'python-argh' 'python2-argh')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-pytest-timeout' 'python2-pytest-timeout')
source=(https://pypi.python.org/packages/source/w/watchdog/watchdog-${pkgver}.tar.gz)
sha256sums=('7e65882adb7746039b6f3876ee174952f8eaaa34491ba34333ddf1fe35de4162')

prepare() {
  cp -a watchdog-$pkgver{,-py2}
}

build() {
  cd "$srcdir/watchdog-$pkgver"
  python setup.py build

  cd "$srcdir/watchdog-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/watchdog-$pkgver"
  python setup.py test

  cd "$srcdir/watchdog-$pkgver-py2"
  python2 setup.py test
}

package_python-watchdog() {
  depends=('python-pathtools' 'python-yaml' 'python-argh')

  cd "$srcdir/watchdog-$pkgver"
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-watchdog() {
  depends=('python2-pathtools' 'python2-yaml' 'python2-argh')

  cd "$srcdir/watchdog-$pkgver-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1

  mv "$pkgdir/usr/bin/watchmedo"{,2}
}

# vim:ts=2:sw=2:et:
