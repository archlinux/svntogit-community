# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=2.1.8
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://github.com/gorakhargosh/watchdog"
license=('Apache')
depends=('python')
optdepends=('python-yaml: for watchmedo')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-timeout')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('3182cb28701403e799c3850e5cd44acf6dc1aa7e98b1c4bb22b21de98fa4372c74a041e2f3be8d10ae77fe00018e460b98ce2fa5b0779edb84d15f952c407030')

prepare() {
  cd watchdog-$pkgver
  sed -i '/--cov/d' setup.cfg
}

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  # Invokes sudo
  PYTHONPATH="$PWD/build/lib" pytest --deselect tests/test_inotify_buffer.py::test_unmount_watched_directory_filesystem
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
