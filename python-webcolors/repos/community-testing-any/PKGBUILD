# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-webcolors
pkgname=(python-webcolors python2-webcolors)
pkgver=1.8.1
pkgrel=2
pkgdesc="A library for working with color names and color value formats defined by the HTML and CSS specifications for use in documents on the Web"
url="https://github.com/ubernostrum/webcolors"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/w/webcolors/webcolors-$pkgver.tar.gz")
sha512sums=('45ddb4ec030206c3ff1edb7de6b8ec1b5f2f9ef5068f37a7c43b66531a2dc6b290cced2af1ca18794aaa9ba17f297022b2f9cc7a92a74ee9b68f758abf7c104e')

prepare() {
  cp -a webcolors-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/webcolors-$pkgver
  python setup.py build

  cd "$srcdir"/webcolors-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/webcolors-$pkgver
  py.test

  cd "$srcdir"/webcolors-$pkgver-py2
  py.test2
}

package_python-webcolors() {
  depends=('python')

  cd webcolors-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-webcolors() {
  depends=('python2')

  cd webcolors-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
