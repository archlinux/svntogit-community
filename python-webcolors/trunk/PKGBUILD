# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-webcolors
pkgname=(python-webcolors python2-webcolors)
pkgver=1.7
pkgrel=1
pkgdesc="A library for working with color names and color value formats defined by the HTML and CSS specifications for use in documents on the Web"
url="https://github.com/ubernostrum/webcolors"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/w/webcolors/webcolors-$pkgver.tar.gz")
sha512sums=('0541b0cd4871f36adb09bcdf345797916ef27364e83ac0e87f5b41e891801c5f4354dcf8ca7dd3e10407d6c867cdfc0aa59dacc508a4fec0407e78a0b435697e')

prepare() {
  cp -a webcolors-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/webcolors-$pkgver
  python setup.py build

  cd "$srcdir"/webcolors-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/webcolors-$pkgver
  py.test

  cd "$srcdir"/webcolors-$pkgver-py2
  py.test2
}

package_python-webcolors() {
  depends=('python')

  cd webcolors-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-webcolors() {
  depends=('python2')

  cd webcolors-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
