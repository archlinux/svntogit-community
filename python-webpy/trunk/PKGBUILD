# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Erol V. Aktay <e.aktay@gmail.com>

pkgbase=python-webpy
pkgname=python2-webpy
pkgver=0.37
pkgrel=2
pkgdesc='Simple but powerful web framework for python'
arch=('any')
url="http://webpy.org/"
license=('custom')
depends=('python2')
conflicts=('python-webpy<=0.37-1')
replaces=('python-webpy<=0.37-1')
optdepends=('python2-flup: deploy web applications via FastCGI (recommended)'
            'mod_wsgi: deploy web applications via an Apache module'
            'python2-psycopg2: use PostgreSQL as the backing database'
            'mysql-python: use MySQL as the backing database'
            'python2-mako: alternative templating engine')
source=(http://webpy.org/static/web.py-$pkgver.tar.gz)
md5sums=('93375e3f03e74d6bf5c5096a4962a8db')

package_python2-webpy() {
  cd "$srcdir/web.py-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
  install -d "$pkgdir/usr/share/licenses/${pkgname}"
  echo "web.py is in the public domain; you can use it for whatever purpose with absolutely no restrictions." \
    > "$pkgdir/usr/share/licenses/${pkgname}/LICENSE.txt"
}
