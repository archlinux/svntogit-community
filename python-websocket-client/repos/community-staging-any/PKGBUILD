# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

_pkgbase='websocket-client'
__pkgbase="${_pkgbase//-/_}"
pkgname=("python-${_pkgbase}" "python2-${_pkgbase}")
pkgver=0.56.0
pkgrel=3
pkgdesc="WebSocket client library for Python"
arch=(any)
url="https://github.com/${_pkgbase}/${_pkgbase}"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=("https://pypi.io/packages/source/w/${_pkgbase}/${__pkgbase}-${pkgver}.tar.gz")
sha512sums=('f61497b5fa6deea11d0e81c1c0d3092d1efba9bcdb3427a45f51b81059444a486a9aa6a4d046fc2aa805e855d2c8e68a4ba46669c05a45eb928251c50e2e3a50')

prepare() {
  # Not sure why the conditions are not working :/
  sed -i "s/'backports.ssl_match_hostname'//" "${srcdir}/${__pkgbase}-${pkgver}/setup.py"

  cp -a "${srcdir}/${__pkgbase}-${pkgver}"{,-py2}
}

build() {
  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${__pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py test

  cd "${srcdir}/${__pkgbase}-${pkgver}-py2"
  python2 setup.py test
}

package_python-websocket-client() {
  depends=('python-six')

  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-websocket-client() {
  depends=('python2-six')

  cd "${srcdir}/${__pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "$pkgdir"/usr/bin/wsdump{,2}.py
}

# vim:set ts=2 sw=2 ft=sh et:
