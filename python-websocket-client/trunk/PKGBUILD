# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

_pkgbase='websocket-client'
__pkgbase="${_pkgbase//-/_}"
pkgname=python-${_pkgbase}
pkgver=0.58.0
pkgrel=1
pkgdesc="WebSocket client library for Python"
arch=(any)
url="https://github.com/${_pkgbase}/${_pkgbase}"
license=('LGPL')
depends=('python-six')
makedepends=('python-setuptools')
source=("https://pypi.io/packages/source/w/${_pkgbase}/${__pkgbase}-${pkgver}.tar.gz")
sha512sums=('c587945831427a9bddef96be53d20fec7f55ffd7efe2b676dac96b8078ba446ecda85379a4edfc49c36de936516d3994cbd0515decb9b615cd969bde1117167a')

prepare() {
  # Not sure why the conditions are not working :/
  sed -i "s/'backports.ssl_match_hostname'//" "${srcdir}/${__pkgbase}-${pkgver}/setup.py"
}

build() {
  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py test
}

package() {
  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 ft=sh et:
