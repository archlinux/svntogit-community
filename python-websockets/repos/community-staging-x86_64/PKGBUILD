# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sherlock Holo <sherlockya@gmail.com>
# Contributor: user6553591 <Message on Reddit>

pkgname=python-websockets
pkgver=9.1
pkgrel=2
pkgdesc='Python implementation of the WebSocket Protocol (RFC 6455)'
url='https://github.com/aaugustin/websockets'
arch=('x86_64')
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx-autodoc-typehints' 'python-sphinxcontrib-trio')
source=(https://github.com/aaugustin/websockets/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ff2907a913ff0b8ced6dda72b6fc6512a2ece3ffded2e2dcb8ff6805de9ad504430cc1871c1f6e02ab8069e7b96b2e7609afaf3c9c25f9271b28e2daa72bd3ac')
b2sums=('cb3807f01e1a4c22a40bf5150d1e8b21ca786dc5bc4da8f3e6602971f3f7c79b57092f5ddfe3ee52ec00e7fd15f3a992fdbb94b5e26599f2621a7f51434ba6dd')

build() {
  cd websockets-${pkgver}
  python setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd websockets-${pkgver}
  python setup.py test
}

package() {
  cd websockets-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/websockets.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
