# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=wheezy.template
pkgname=python-wheezy-template
pkgver=3.1.0
pkgrel=4
pkgdesc="lightweight template library"
arch=('x86_64')
url="https://github.com/akornatskyy/${_pkgname}"
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-pytest')
#source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('a75692c8bc4aeb750a378192fe094f6c28901526c6ca754fa92f3783cdd7abcd')
b2sums=('6cbaa6e5656a0b3bf417ed500770c97f2207f97822ccdf624c87cb0f3c0d2fa3b1a2269641325c6d6138568aeff3bcc417e6e4896186e2fe7c1b31a15e2d7365')

build() {
    cd ${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd ${_pkgname}-${pkgver}

    PYTHONPATH="$PWD/src" pytest --import-mode=importlib
}

package() {
    cd ${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
