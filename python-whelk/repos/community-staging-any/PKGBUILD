# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-whelk
pkgname=('python-whelk' 'python2-whelk')
pkgver=2.7.1
pkgrel=2
pkgdesc="Easy access to shell commands from python"
arch=('any')
license=('custom:zlib')
url="https://pypi.python.org/pypi/whelk"
makedepends=('python' 'python2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/seveas/whelk/archive/$pkgver.tar.gz")
sha512sums=('39b73ed09a0d2442cdde5b8bae72805571a172c6acfa3f652c8cb125d3a400f2538fe6534179f95383d321e8975b6127115616372c60c736e8cd8ceba00ef093')

prepare() {
  cp -a whelk-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/whelk-$pkgver
  python setup.py build

  cd "$srcdir"/whelk-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/whelk-$pkgver
  python -munittest discover

  cd "$srcdir"/whelk-$pkgver-py2
  python2 -munittest discover
}

package_python-whelk() {
  depends=('python')

  cd whelk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-whelk() {
  depends=('python2')

  cd whelk-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
