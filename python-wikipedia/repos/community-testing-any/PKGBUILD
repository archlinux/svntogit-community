# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chuan Ji <jichuan89@gmail.com>

pkgname=python-wikipedia
pkgver=1.4.0
pkgrel=8
pkgdesc='A Pythonic wrapper for the Wikipedia API'
arch=(any)
url=https://github.com/goldsmith/Wikipedia
license=(MIT)
depends=(
  python-beautifulsoup4
  python-requests
)
makedepends=(python-setuptools)
source=(https://pypi.python.org/packages/source/w/wikipedia/wikipedia-${pkgver}.tar.gz)
sha256sums=(db0fad1829fdd441b1852306e9856398204dc0786d2996dd2e0c8bb8e26133b2)

build() {
  cd wikipedia-${pkgver}

  python setup.py build
}

package_python-wikipedia() {
  cd wikipedia-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-wikipedia/
}

# vim: ts=2 sw=2 et:
