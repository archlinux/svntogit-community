# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chuan Ji <jichuan89@gmail.com>

pkgbase=python-wikipedia
pkgname=('python-wikipedia' 'python2-wikipedia')
pkgver=1.4.0
pkgrel=5
pkgdesc='A Pythonic wrapper for the Wikipedia API'
arch=('any')
url='https://github.com/goldsmith/Wikipedia'
license=('MIT')
depends=('python-beautifulsoup4' 'python-requests')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/w/wikipedia/wikipedia-${pkgver}.tar.gz")
sha256sums=('db0fad1829fdd441b1852306e9856398204dc0786d2996dd2e0c8bb8e26133b2')

prepare() {
  cp -r wikipedia-${pkgver} python-wikipedia-${pkgver}
  mv wikipedia-${pkgver} python2-wikipedia-${pkgver}
}

build() {
  cd python-wikipedia-${pkgver}

  python setup.py build

  cd ../python2-wikipedia-${pkgver}

  python2 setup.py build
}

package_python-wikipedia() {
  cd python-wikipedia-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/licenses/python-wikipedia
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/python-wikipedia/

}

package_python2-wikipedia() {
  cd python2-wikipedia-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/licenses/python2-wikipedia
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/python2-wikipedia/
}

# vim: ts=2 sw=2 et:
