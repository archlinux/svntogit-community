# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: neithere <neithere at gmail>
# Contributor: mitsuse <mitsuse at gmail>

_pyname=wtforms
pkgname=python-wtforms
pkgver=2.3.3
pkgrel=1
pkgdesc='Flexible forms validation and rendering library for python web development'
url='https://wtforms.readthedocs.io/'
arch=('any')
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python-babel' 'python-sphinx'
             'python-pallets-sphinx-themes' 'python-sphinx-issues'
             'python-sphinxcontrib-log-cabinet')
source=(https://github.com/wtforms/wtforms/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('f2b825a85f46a5eca2c8ecb7e0d1945e9697ad98c430b3043952eb5df87b77e180ac26aea0ad10c134438ef8f9d9c8d00ffc5b38a9fa413c6c95d43a681456a2')
b2sums=('96a504e5a5334086372c81f3691edc0a598d3df9bb3eecce007daae1f445930e80aeea5c943e10500b2cba24ebff16e6e89ec31c9e6a1a33c5008297bbc1b064')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py compile_catalog build
  export PYTHONPATH=..
  make -C docs html SPHINXBUILD=sphinx-build
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  find "$pkgdir" -type f -name \*.po -delete
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/html"
}

# vim: ts=2 sw=2 et:
