# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: neithere <neithere at gmail>
# Contributor: mitsuse <mitsuse at gmail>

_pyname=wtforms
pkgbase=python-wtforms
pkgname=('python-wtforms' 'python2-wtforms')
pkgver=2.2.1
pkgrel=4
pkgdesc='Flexible forms validation and rendering library for python web development'
url='https://wtforms.readthedocs.io/'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python-babel' 'python-sphinx'
             'python2-setuptools' 'python-babel' 'python2-sphinx')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wtforms/wtforms/archive/${pkgver}.tar.gz)
sha512sums=('db989ff5352d6834fc51c0ed208de11c12a2c0ad90f73dfeaa8f9995edc7f840deb427f1ffc6167f1c24e44f2137ecbb27ee1113258a6c55ed256a603657ef97')

prepare() {
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build compile_catalog
    make -C docs html SPHINXBUILD=sphinx-build
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build compile_catalog
    make -C docs html SPHINXBUILD=sphinx-build2
  )
}

package_python-wtforms() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  find "$pkgdir" -type f -name \*.po -delete
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/html"
}

package_python2-wtforms() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1
  find "$pkgdir" -type f -name \*.po -delete
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}/html"
}

# vim: ts=2 sw=2 et:
