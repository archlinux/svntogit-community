# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=wurlitzer
pkgname=python-${_pkg}
pkgver=3.0.2
pkgrel=3
pkgdesc="Capture C-level stdout/stderr in Python"
arch=(any)
url="https://github.com/minrk/wurlitzer"
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-mock)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('36051ac530ddb461a86b6227c4b09d95f30a1d1043de2b4a592e97ae8a84fcdf')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  py.test test.py
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --root "${pkgdir}" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
