# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgbase=python-xapian
pkgname=('python-xapian' 'python2-xapian')
_realname=xapian-bindings
pkgver=1.4.5
epoch=1
pkgrel=5
pkgdesc="Python bindings for Xapian"
arch=('x86_64')
url="https://xapian.org/docs/bindings/python/"
license=('GPL2')
makedepends=('python-sphinx' 'python2-sphinx' "xapian-core=$epoch:$pkgver")
source=("http://oligarchy.co.uk/xapian/${pkgver}/${_realname}-${pkgver}.tar.xz"
        '0001-Support-sphinx-1.7-while-still-working-with-1.7.patch')
sha512sums=('e39ece495e25077990db3d06554306567c52c6f45ad55a65005cb97e18086ee18947e41d0d612157b891b61e015881385bcb082a03aa0c6452565e90d09e8275'
            'c3cdd7e08249003ab8b2dcd571d82d00585920f2610219dc501756c080d7f469147e7347cd3a06a5d6fbfcef3dd6f7af525b769cb5a7e79df911e27f2f941924')

prepare() {
  cd "${_realname}-${pkgver}"
  # patch breakage introduced by python-sphinx >= 1.7.0
  # https://bugs.archlinux.org/task/57567
  patch --verbose -Np2 -i "${srcdir}/0001-Support-sphinx-1.7-while-still-working-with-1.7.patch"
  autoreconf -vfi
  cd ..
  mv -v "${_realname}-$pkgver" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-$pkgver" "${pkgname[1]}-${pkgver}"
  cd "${pkgname[1]}-${pkgver}"
  # fix shebang of examples
  sed -i '1s/python/python2/' python/docs/examples/*.py
  export XAPIAN_CONFIG=/usr/bin/xapian-config
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  ./configure --prefix=/usr \
              --with-python3
  make
  cd ../"${pkgname[1]}-${pkgver}"
  PYTHON2=/usr/bin/python2
  ./configure --prefix=/usr \
              --with-python
  make
}

package_python-xapian() {
  pkgdesc="Python 3.x bindings for Xapian"
  depends=('python' "xapian-core=$epoch:$pkgver")
  cd "${pkgname[0]}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO}
}

package_python2-xapian() {
  pkgdesc="Python 2.x bindings for Xapian"
  depends=('python2' "xapian-core=$epoch:$pkgver")
  cd "${pkgname[1]}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO}
}

# vim:set ts=2 sw=2 et:
