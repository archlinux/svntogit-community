# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgname=python-xapian
_name=xapian-bindings
pkgver=1.4.19
pkgrel=1
epoch=1
pkgdesc="Python bindings for Xapian"
arch=('x86_64')
url="https://xapian.org/docs/bindings/python/"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'python' 'zlib')
makedepends=('util-linux-libs' 'python-sphinx' "xapian-core=$epoch:$pkgver")
source=("https://oligarchy.co.uk/xapian/${pkgver}/${_name}-${pkgver}.tar.xz"{,.asc})
sha512sums=('c3e353a192822be5d86d448b9d5e4c5eb15d5eb2d079560f7c457f8f7ca85d42e2b933891e784d384c188577f427ca1929c2b47732b9d637e36543e8b5b704d6'
            'SKIP')
b2sums=('46004a3449a3137db42164a8b4c5b8684d2eaf0edfa60fe7ca41d5565d1cc16b110ca8204100c9e654baa323a4c73536f80550b17989ce3ee4a132315c31a50e'
        'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${_name}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${_name}-${pkgver}"
  ./configure --prefix=/usr \
              --with-python3
  make
}

check() {
  cd "${_name}-${pkgver}"
  make check
}

package() {
  depends+=('libuuid.so' 'libxapian.so')
  cd "${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${_name}"
}
# vim:set ts=2 sw=2 et:
