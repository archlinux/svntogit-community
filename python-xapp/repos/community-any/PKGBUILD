# $Id$
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=python-xapp
pkgname=(python-xapp python2-xapp)
pkgver=1.2.0
pkgrel=2
pkgdesc="Python Xapp Library"
arch=('any')
url="https://github.com/linuxmint/python-xapp"
license=('LGPL')
depends=('python' 'python-setuptools'
	 'python2' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/linuxmint/$pkgbase/archive/$pkgver.tar.gz")
sha256sums=('a81e50b1a16efcd2db0db99afbeebf56a4dfd45b008ec5bc606db0adc7cb9ac0')

prepare() {
  cp -a $pkgbase-$pkgver{,-py2}
}

build(){
  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py build

  cd "$srcdir/$pkgbase-$pkgver-py2"
  python2 setup.py build
}

package_python-xapp() {
  depends=('python' 'python-psutil')
  cd $pkgbase-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1 --skip-build
}

package_python2-xapp() {
  depends=('python2' 'python2-psutil')
  cd $pkgbase-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1 --skip-build
}
