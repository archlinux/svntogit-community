# $Id$
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=python-xapp
pkgname=(python-xapp python2-xapp)
pkgver=1.0.1
pkgrel=1
pkgdesc="Python Xapp Library"
arch=('any')
url="https://github.com/linuxmint/python-xapp"
license=('LGPL')
depends=('python' 'python2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/linuxmint/$pkgbase/archive/$pkgver.tar.gz")
sha256sums=('da2ec46dc3fce97accd0ee6d9bd9d71d26c953b01de1a5336ba2b4b60f2f3b6b')

build() {
  cd $pkgbase-$pkgver
  python setup.py build
}

package_python-xapp() {
  depends=('python')
  cd $pkgbase-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-xapp() {
  depends=('python2')
  cd $pkgbase-$pkgver
  python2 setup.py install --root "$pkgdir" --optimize=1
}
