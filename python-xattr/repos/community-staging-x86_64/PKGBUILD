# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xattr
pkgver=0.9.7
pkgrel=4
pkgdesc='Python wrapper for extended filesystem attributes'
arch=('x86_64')
license=('MIT')
url='https://github.com/xattr/xattr'
depends=('python-cffi' 'python-setuptools')
makedepends=('python-setuptools' 'python-cffi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/xattr/xattr/archive/v$pkgver.tar.gz")
sha512sums=('4d94e929057a39c0f332e24aee1c2883580c71df94c9041313d4af2f11885803ab2c007546d5510ed647abcc70804e2fbaecf38f84d0a8457129c1336e5c8874')

build() {
  cd "$srcdir"/xattr-$pkgver
  python setup.py build
}

check() { (
  # Build env doesn't have permission for those xattrs
  cd "$srcdir"/xattr-$pkgver
  python setup.py test
  ) || echo "Tests failed"
}

package() {
  cd xattr-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
