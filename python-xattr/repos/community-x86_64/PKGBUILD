# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-xattr
pkgname=('python-xattr' 'python2-xattr')
pkgver=0.9.2
pkgrel=1
pkgdesc='Python wrapper for extended filesystem attributes'
arch=('i686' 'x86_64')
license=('MIT')
url='http://github.com/xattr/xattr'
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/xattr/xattr/archive/v$pkgver.tar.gz")
sha512sums=('bad56024f7634d19f806124334de5a87fcadee5a83a558c54b293d5dc71a4416106f5b3f8e6d9321b460489c2caf06d8427f1035d99a7da86b6dd16aeec52ca2')

prepare() {
  cp -a xattr-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/xattr-$pkgver
  python setup.py build

  cd "$srcdir"/xattr-$pkgver-py2
  python2 setup.py build
}

check() { (
  # Build env doesn't have permission for those xattrs
  cd "$srcdir"/xattr-$pkgver
  python setup.py test

  cd "$srcdir"/xattr-$pkgver-py2
  python2 setup.py test
  ) || warning "Tests failed"
}

package_python-xattr() {
  depends=('python-cffi')

  cd xattr-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-xattr() {
  depends=('python2-cffi')

  cd xattr-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt

  mv "$pkgdir"/usr/bin/xattr{,2}
}

# vim:set ts=2 sw=2 et:
