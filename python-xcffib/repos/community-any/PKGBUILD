# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgbase=python-xcffib
pkgname=(python-xcffib python2-xcffib)
pkgver=0.4.2
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi'
             'python-six' 'python2-six' 'libxcb' 'git' 'ghc' 'haskell-xcb-types'
             'haskell-language-python' 'haskell-filemanip' 'haskell-split'
             'haskell-mtl' 'haskell-attoparsec' 'haskell-hunit' 'cabal-install'
             'haskell-test-framework' 'haskell-test-framework-hunit'
             'haskell-optparse-applicative')
checkdepends=('flake8' 'python2-flake8' 'parallel' 'python-nose' 'python2-nose'
              'xorg-server-xvfb' 'xorg-xeyes')
source=("git+https://github.com/tych0/xcffib.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a xcffib{,-py2}

  sed -e 's/python /python2 /' \
      -e 's/nosetests /nosetests2 /' \
      -e 's/flake8 /flake8-python2 /' \
      -i xcffib-py2/Makefile
}

build() {
  cd "$srcdir/xcffib"
  make xcffib

  cd "$srcdir/xcffib-py2"
  make xcffib
}

check() {
  cd "$srcdir/xcffib"
  make check

  cd "$srcdir/xcffib-py2"
  make check
}

package_python-xcffib() {
  depends=('python-six' 'python-cffi' 'libxcb')

  cd "$srcdir/xcffib"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-xcffib() {
  depends=('python2-six' 'python2-cffi' 'libxcb')

  cd "$srcdir/xcffib-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
