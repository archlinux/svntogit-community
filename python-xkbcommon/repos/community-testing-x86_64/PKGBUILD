# Maintainer: David Runge <dvzrv@archlinux.org>

_name=xkbcommon
pkgname=python-xkbcommon
pkgver=0.4
pkgrel=3
pkgdesc="Python bindings for libxkbcommon using cffi"
arch=('x86_64')
url="https://github.com/sde1000/python-xkbcommon"
license=('MIT')
depends=('python-cffi' 'libxkbcommon')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fb2c272b8229c4457762d95a622f42838a222f715b1882ca41e5733a20a44d2bc1289218f72f24da3cb959b9628e7ceb73327b61263397207fecabbe0e6fce77')
b2sums=('6e43960a521b6366ded43527b15a7c24ae299f216754d2d582b91c1b2b26c1b9527a6d1e20a09533f00b69a3b0b43b196695304c0b2e83e814660c94c763f30e')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-${pkgver}"
}

build() {
  cd "$pkgname-${pkgver}"
  python "${_name}/ffi_build.py"
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-${pkgver}"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
