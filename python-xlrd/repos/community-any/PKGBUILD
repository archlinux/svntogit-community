# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: tocer <tocer.deng@gmail.com>
# Contributor: Piotr Beling <qwak@stud.ics.p.lodz.pl>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>

pkgbase=python-xlrd
pkgname=('python2-xlrd' 'python-xlrd')
pkgver=1.0.0
pkgrel=2
pkgdesc="Library for developers to extract data from Microsoft Excel (tm) spreadsheet files"
arch=('any')
url="http://www.python-excel.org/"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/python-excel/xlrd/archive/${pkgver}.tar.gz")
md5sums=('72dfa4a43b300878e2e14f4209410d0d')

build() {
  cd "$srcdir"
  cp -a xlrd-${pkgver} xlrd-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/xlrd-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/xlrd-${pkgver}
  python setup.py build
}

package_python2-xlrd() {
  depends=('python2')

  cd "$srcdir"/xlrd-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
  mv "$pkgdir/usr/bin/runxlrd"{,2}.py

  install -D -m644 "xlrd/licences.py" "$pkgdir/usr/share/licenses/$pkgname/licences.py"
}

package_python-xlrd() {
  depends=('python')

  cd "$srcdir"/xlrd-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "xlrd/licences.py" "$pkgdir/usr/share/licenses/$pkgname/licences.py"
}

