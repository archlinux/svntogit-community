# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-xlsxwriter
pkgver=3.0.2
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/RELEASE_${pkgver}.tar.gz")
sha512sums=('790eb7fb93d25987adf5d30bfa28e0fa137f809dc9a293edfe86105e0a948263759df85b5a0f9fb19b89c256a33b5b68179d95a210964b6f82345972ee9e6108')
b2sums=('748d710d3f953ae22154022a75f3a67898846914cab67302167ba34ba5e449041a95dbc984883e4ff58c078b940ebd429898109abb7a39f57ea499e5fcff83fa')

build() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py build
}

check() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python -m unittest discover
}

package() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
