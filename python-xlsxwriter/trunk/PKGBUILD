# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-xlsxwriter
pkgname=('python2-xlsxwriter' 'python-xlsxwriter')
pkgver=0.5.5
pkgrel=0
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/source/X/XlsxWriter/XlsxWriter-${pkgver}.tar.gz")
md5sums=('9ba874d43e039ae286c438a101d67539')

build() {
  cd "$srcdir"
  cp -a XlsxWriter-${pkgver} XlsxWriter-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/XlsxWriter-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/XlsxWriter-${pkgver}
  python setup.py build
}

package_python2-xlsxwriter() {
  depends=('python2')

  cd "$srcdir"/XlsxWriter-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-xlsxwriter() {
  depends=('python')

  cd "$srcdir"/XlsxWriter-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

