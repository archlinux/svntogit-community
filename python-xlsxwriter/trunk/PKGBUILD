# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-xlsxwriter
pkgname=('python2-xlsxwriter' 'python-xlsxwriter')
pkgver=1.1.0
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/jmcnamara/XlsxWriter/archive/RELEASE_${pkgver}.tar.gz")
sha1sums=('3853014d325f9faabd53c5a7b28360107b1db9fc')

prepare() {
  cd "$srcdir"
  cp -a XlsxWriter-RELEASE_${pkgver} XlsxWriter-py2-RELEASE_${pkgver}
}

build() {
  msg "Building Python2"
  cd "$srcdir"/XlsxWriter-py2-RELEASE_${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py build
}

package_python2-xlsxwriter() {
  depends=('python2')

  cd "$srcdir"/XlsxWriter-py2-RELEASE_${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir"/usr/bin/vba_extract{,2}.py
}

package_python-xlsxwriter() {
  depends=('python')

  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

