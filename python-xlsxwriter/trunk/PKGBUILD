# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-xlsxwriter
pkgname=('python2-xlsxwriter' 'python-xlsxwriter')
pkgver=1.1.5
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/jmcnamara/XlsxWriter/archive/RELEASE_${pkgver}.tar.gz")
sha256sums=('5b7c597f7e49c9cccd68538bc96ec0d43a83529c4ee3947e6f5c810f2a3719b7')

prepare() {
  cd "$srcdir"
  cp -a XlsxWriter-RELEASE_${pkgver} XlsxWriter-py2-RELEASE_${pkgver}
}

build() {
  # "Building Python2"
  cd "$srcdir"/XlsxWriter-py2-RELEASE_${pkgver}
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py build
}

package_python2-xlsxwriter() {
  depends=('python2')

  cd "$srcdir"/XlsxWriter-py2-RELEASE_${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir"/usr/bin/vba_extract{,2}.py
}

package_python-xlsxwriter() {
  depends=('python')

  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

