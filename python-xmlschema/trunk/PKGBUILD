# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=1.9.1
pkgrel=1
pkgdesc="An XML Schema validator and decoder"
url="https://github.com/sissaschool/xmlschema"
license=('MIT')
arch=('any')
depends=('python-elementpath')
makedepends=('python-pip')
checkdepends=('python-lxml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/xmlschema/archive/v$pkgver.tar.gz")
sha512sums=('bab6e3fb3bf462e13808c433cfe88d83b778053d8df13142511aea4174ed7439c9693d29b88961d38b49899990cd4078411d133f3cef5e7e512bd7398d4048ed')

prepare() {
  sed -i 's/~=/>=/' xmlschema-$pkgver/setup.py
}

build() {
  cd xmlschema-$pkgver
  python setup.py build
}

check() {
  cd xmlschema-$pkgver
  PYTHONPATH="$PWD/build/lib" python -m unittest
}

package() {
  cd xmlschema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
