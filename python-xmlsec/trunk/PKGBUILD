# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.10
pkgrel=1
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=('x86_64')
depends=('python-lxml' 'xmlsec')
makedepends=('python-pip' 'python-pkgconfig' 'python-setuptools-scm')
checkdepends=('python-hypothesis' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mehcode/python-xmlsec/archive/$pkgver.tar.gz")
sha512sums=('9274ba4b9d9190a8f0f580e530c5c1efc34e3452a1459cea27636e7c594390902e8555dd4df0c38c243214ccb246430f4f60b1022d7cc369a7d67c3f113c8c68')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd python-xmlsec-$pkgver
  python setup.py build
}

check() {
  cd python-xmlsec-$pkgver
  # https://github.com/mehcode/python-xmlsec/issues/84
  python setup.py pytest || :
}

package() {
  cd python-xmlsec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
