# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.8
pkgrel=1
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=('x86_64')
depends=('python-lxml' 'xmlsec')
makedepends=('python-pip' 'python-pkgconfig' 'python-setuptools-scm')
checkdepends=('python-hypothesis' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mehcode/python-xmlsec/archive/$pkgver.tar.gz")
sha512sums=('db1a80443b0f86f84eb6256a9d67827da023b989c6b24cdfc0588b34c7a120071758ccc7b21b0b105f0a6e879a4769a325794b8db82c0c41ec12004cd7ea185c')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd python-xmlsec-$pkgver
  python setup.py build
}

check() {
  cd python-xmlsec-$pkgver
  # https://github.com/mehcode/python-xmlsec/issues/84
  python setup.py pytest || :
}

package() {
  cd python-xmlsec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
