# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.11
pkgrel=1
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=('x86_64')
depends=('python-lxml' 'xmlsec')
makedepends=('python-pip' 'python-pkgconfig' 'python-setuptools-scm')
checkdepends=('python-hypothesis' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mehcode/python-xmlsec/archive/$pkgver.tar.gz")
sha512sums=('fe0144d67edcad366b1d0f4615983f5630afd8f22aa056f46d1e6564f1c3114c0a469675e64a06591540515b0d12b5bf9304845f5138b24e15acaf4e5b861961')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd python-xmlsec-$pkgver
  python setup.py build
}

check() {
  cd python-xmlsec-$pkgver
  # https://github.com/mehcode/python-xmlsec/issues/84
  python setup.py pytest || :
}

package() {
  cd python-xmlsec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
