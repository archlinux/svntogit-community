# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: PiC0 <guill.p.linux@gmail.com>

pkgbase=python-xmltodict
pkgname=('python-xmltodict' 'python2-xmltodict')
pkgver=0.11.0
pkgrel=2
pkgdesc="Python module that makes working with XML feel like working with JSON"
url="https://github.com/martinblech/xmltodict"
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/x/xmltodict/xmltodict-$pkgver.tar.gz")
md5sums=('9f955947db085485873ac68154e88069')

package_python-xmltodict () {
  pkgdesc+=" (py3)"
  depends=('python')

  cd "$srcdir/xmltodict-$pkgver"
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-xmltodict () {
  pkgdesc+=" (py2)"
  depends=('python2')

  cd "$srcdir/xmltodict-$pkgver"
  sed -i 's/env python$/&2/' xmltodict.py
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
