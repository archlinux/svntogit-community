# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-xpybutil
pkgname=(python-xpybutil python2-xpybutil)
pkgver=0.0.1.20151007
_commit=570f225e09b0f0fb05cee27e6660c19b5d0ee6bb
pkgrel=2
pkgdesc="An incomplete xcb-util port plus some extras"
arch=('any')
url="https://github.com/BurntSushi/xpybutil"
license=('custom:WTFPL')
makedepends=('git' 'python-xcffib' 'python2-xcffib')
source=("git+https://github.com/BurntSushi/xpybutil.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  cp -a xpybutil{,-py2}
}

build() {
  cd "$srcdir/xpybutil"
  python setup.py build

  cd "$srcdir/xpybutil-py2"
  python2 setup.py build
}

package_python-xpybutil() {
  depends=('python-xcffib')

  cd "${srcdir}/xpybutil"
  python setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  mv "$pkgdir/usr/share/doc/"{,python-}xpybutil
}

package_python2-xpybutil() {
  depends=('python2-xcffib')

  cd "${srcdir}/xpybutil-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  mv "$pkgdir/usr/share/doc/"{,python2-}xpybutil
}
