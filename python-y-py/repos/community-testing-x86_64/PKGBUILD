# Maintainer: Antonio Rojas <arojas@archlinux.com>

_pyname=y_py
pkgname=python-y-py
pkgver=0.6.0
pkgrel=2
pkgdesc='Python bindings for the Y-CRDT built from yrs'
arch=(x86_64)
url='https://github.com/y-crdt/ypy'
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-maturin)
checkdepends=(python-pytest)
source=(https://pypi.python.org/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('46836169f7dc2957df8513cfe4bc2009175b3a473e630af421a8e75ee1c48f98')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
