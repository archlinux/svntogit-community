# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>

pkgbase=python-yaml
pkgname=(python2-yaml python-yaml)
pkgver=3.12
pkgrel=2
pkgdesc="Python bindings for YAML, using fast libYAML library"
arch=('i686' 'x86_64')
url="http://pyyaml.org"
license=('MIT')
makedepends=('python' 'python2' 'libyaml')
install='python-yaml.install'
source=(http://pyyaml.org/download/pyyaml/PyYAML-$pkgver.tar.gz)
md5sums=('4c129761b661d181ebf7ff4eb2d79950')

build() {
  true
}

package_python2-yaml() {
  depends=('python2' 'libyaml')

  cd "$srcdir"/PyYAML-$pkgver
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -m644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-yaml() {
  depends=('python' 'libyaml')

  cd "$srcdir"/PyYAML-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -m644 -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
