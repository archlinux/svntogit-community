# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: arch3y <arch3y[at]archstrike[dot]org>

_gitname=yara-python
pkgbase=python-yara
pkgname=('python-yara' 'python2-yara')
pkgver=4.0.2
_gitcommit=a1ed3cb54e8361a24e185183fccd888d99b15ae5
pkgrel=2
pkgdesc='Tool aimed at helping malware researchers to identify and classify malware samples'
url='https://github.com/VirusTotal/yara-python'
arch=('x86_64')
license=('Apache')
makedepends=('git' 'yara' 'glibc'
             'python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=(${pkgbase}::git+"https://github.com/VirusTotal/${_gitname}#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgbase}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cp -a ${pkgbase}{,-py2}
}

build() {
  echo 'Building python...'
  (cd ${pkgbase}
    python setup.py build --dynamic-linking
  )
  echo 'Building python2...'
  (cd ${pkgbase}-py2
    python2 setup.py build --dynamic-linking
  )
}

check() {
  echo 'Checking python...'
  (cd ${pkgbase}
    local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python tests.py
  )
  echo 'Checking python2...'
  (cd ${pkgbase}-py2
    local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python2 tests.py
  )
}

package_python-yara() {
  depends=('python' 'yara' 'libyara.so' 'glibc')
  cd ${pkgbase}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

package_python2-yara() {
  depends=('python2' 'yara' 'libyara.so' 'glibc')
  cd ${pkgbase}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

# vim: ts=2 sw=2 et:
