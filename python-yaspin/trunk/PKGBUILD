# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-yaspin
pkgver=1.4.1
pkgrel=1
pkgdesc="Yet Another Terminal Spinner"
url="https://github.com/pavdmyt/yaspin"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-dephell')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pavdmyt/yaspin/archive/v$pkgver.tar.gz")
sha512sums=('a9b0dbfd98ae377c673159a267bcd6d6e7e4b23ff4a4dc18728a9ed2e1e9de3d117229982dd519311a0008c90910c64aa2d14875ea8bf721f91b170f401aa395')

prepare() {
  cd yaspin-$pkgver

  # poetry-generated setup.py is badly broken in several ways, including
  # distribution of tests in the built package as well as using distutils for
  # bad metadata. See https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd yaspin-$pkgver
  python setup.py build
}

check() {
  cd yaspin-$pkgver
  python setup.py pytest
}

package() {
  cd yaspin-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
