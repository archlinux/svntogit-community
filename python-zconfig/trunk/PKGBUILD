# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-zconfig
pkgname=(python-zconfig python2-zconfig)
pkgver=3.5.0
pkgrel=1
pkgdesc="Structured Configuration Library"
url="https://github.com/zopefoundation/ZConfig"
license=('ZPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner' 'python-manuel' 'python2-manuel'
              'python-docutils' 'python2-docutils' 'python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/ZConfig/archive/$pkgver.tar.gz")
sha512sums=('0aa5ad1344ac599a10513e1189d0c990717311b39cc6dd6a44315812dce66c09760979931839e3739089d30c69c626bf8259695e1afb4691d19662f41100ecee')

prepare() {
  cp -a ZConfig-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/ZConfig-$pkgver
  python setup.py build

  cd "$srcdir"/ZConfig-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/ZConfig-$pkgver
  nosetests3

  cd "$srcdir"/ZConfig-$pkgver-py2
  nosetests2
}

package_python-zconfig() {
  depends=('python')

  cd ZConfig-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zconfig() {
  depends=('python2')

  cd ZConfig-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/zconfig{,2}
  mv "$pkgdir"/usr/bin/zconfig{,2}_schema2html
}
