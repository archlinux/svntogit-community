# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-zconfig
pkgver=3.6.0
pkgrel=2
pkgdesc="Structured Configuration Library"
url="https://github.com/zopefoundation/ZConfig"
license=('ZPL')
arch=('any')
makedepends=('python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel' 'python-docutils' 'python-nose')
source=("https://github.com/zopefoundation/ZConfig/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fc5fac078b900d1642533d48ed42dca5d58634277aff919e6978d8dd1eec3b6fff637a794dc6e8033459a42b19cc65a7f6c88bfa85f48409fdaa3c3bdb60f0c3')

build() {
  cd ZConfig-$pkgver
  python setup.py build
}

check() {
  cd ZConfig-$pkgver
  nosetests3
}

package() {
  cd ZConfig-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
