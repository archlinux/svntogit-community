# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=3.4.0
pkgrel=1
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="http://docs.python-zeep.org"
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-defusedxml'
         'python-isodate' 'python-lxml' 'python-requests' 'python-requests-toolbelt' 'python-six'
         'python-pytz')
checkdepends=('python-pytest-runner' 'python-freezegun' 'python-mock' 'python-pretend'
              'python-pytest-cov' 'python-requests-mock' 'python-pytest-tornado'
              'python-aioresponses' 'python-xmlsec')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mvantellingen/python-zeep/archive/$pkgver.tar.gz")
sha512sums=('cd4062b44e750c526f777ee04f32b14e9950259a503d024eb1a4e16f74cceb5703a3660ac6d8010a5bf03c84d0bc59738fa680ff44ef61a2b20b653c6f45f9d5')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py

  sed -i 's/get_marker/get_closest_marker/' python-zeep-$pkgver/tests/conftest.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  python setup.py pytest || warning "https://github.com/mvantellingen/python-zeep/issues/724"
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
