# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=4.1.0
pkgrel=1
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="https://docs.python-zeep.org"
depends=('python-attrs' 'python-cached-property' 'python-isodate' 'python-lxml'
         'python-platformdirs' 'python-requests' 'python-requests-toolbelt' 'python-requests-file'
         'python-pytz')
checkdepends=('python-pytest-runner' 'python-freezegun' 'python-mock' 'python-pretend'
              'python-pytest-cov' 'python-requests-mock' 'python-pytest-httpx'
              'python-pytest-asyncio' 'python-aiohttp' 'python-aioresponses' 'python-xmlsec')
source=("https://github.com/mvantellingen/python-zeep/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('977b3072718112ad0ee69c51f9f0069bebce9508f0473e113c67016d57f640b5779a6ebf6601216f936db0b5e99cade15e235768afe4273be29b4248909dd304')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  python setup.py pytest
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
