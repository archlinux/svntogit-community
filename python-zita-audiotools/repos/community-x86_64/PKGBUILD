# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-audiotools
pkgname=python-zita-audiotools
pkgver=1.0.0
pkgrel=10
pkgdesc="Tools for audio processing"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL2' 'GPL3')
depends=('gcc-libs' 'glibc' 'python-numpy')
makedepends=('libsndfile' 'python-setuptools' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${_name}-${pkgver}.tar.bz2")
sha512sums=('c3ebd4c7bb5cb878066ec3de345cd8505c2606754a3ccd584f9ba8fe7e4492546cfa4fac3c4b1d508382952d4066d82d677adbfff4c990c59c0f04dc1d661cf6')
b2sums=('2415afc311c5e8c721467c1be8c79eb8be6abdd5310fef0ef656b42ef593775dbd8238f03203e7a5bb5d1b7103a0b932c337076e28c29ad5ef78b9446093e17d')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  depends+=('libsndfile.so' 'libzita-resampler.so')

  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
}
