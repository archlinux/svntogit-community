# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-jacktools
pkgname=python-zita-jacktools
pkgver=1.5.0
pkgrel=1
pkgdesc="Audio tools running as a Jack client"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'python-zita-audiotools')
makedepends=('fftw' 'jack' 'libsndfile' 'python-setuptools' 'zita-convolver'
'zita-jclient' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${_name}-${pkgver}.tar.bz2")
sha512sums=('13b22325949605b52da44e54d6daeb311457f1a76c3f4f7a3281aeb0352d54c931cba8816703c48da01d9c8b853428e07f781607629ce2024e53de31094d3aa6')
b2sums=('ebb069938a1eb86ed339443d811c7657dee067e1f14ee9c1f53112dd9602f04ea4beee2f555cac2fc0c3e244aefd0dbaae395efd8e64c83814436b7a675ba424')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  depends+=('libfftw3f.so' 'libjack.so' 'libsndfile.so' 'libzita-convolver.so'
  'libzita-jclient.so' 'libzita-resampler.so')

  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"

  # examples
  find examples -type f \
    \( -iname "*.py" \
    -or -iname "*README" \
    -or -iname "*presets" \
    \) \
    -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
