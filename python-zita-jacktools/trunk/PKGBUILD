# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-jacktools
pkgname=python-zita-jacktools
pkgver=1.5.3
pkgrel=2
pkgdesc="Audio tools running as a Jack client"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'python-zita-audiotools')
makedepends=('fftw' 'jack' 'libsndfile' 'python-setuptools' 'zita-convolver'
'zita-jclient' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${_name}-${pkgver}.tar.bz2")
sha512sums=('5783bdfa8d351b3792cffa2cb32b886ee5a636aa176ccaaa1d4dad56040ffba1befe3142be72f5eafccb8bd83def821b63251756a5dce0b6d7e470078446a647')
b2sums=('632eb4c9f5ffa20d68d4b001c75d352a1617764e8dd1c1cd9c025e3c7b725f35b2ddb230d58e1902ebe03e14e558156b9f48bfd514fb013d0fe58c2d8ebde826')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  depends+=('libfftw3f.so' 'libjack.so' 'libsndfile.so' 'libzita-convolver.so'
  'libzita-jclient.so' 'libzita-resampler.so')

  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"

  # examples
  find examples -type f \
    \( -iname "*.py" \
    -or -iname "*README" \
    -or -iname "*presets" \
    \) \
    -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
