# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgbase=python-zope-component
pkgname=('python-zope-component' 'python2-zope-component')
pkgver=4.5
pkgrel=1
pkgdesc="Represents the core of the Zope Component Architecture"
arch=('any')
url="https://github.com/zopefoundation/zope.component"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-deferredimport'
             'python2-zope-deferredimport' 'python-zope-deprecation' 'python2-zope-deprecation'
             'python-zope-event' 'python2-zope-event' 'python-zope-hookable' 'python2-zope-hookable'
             'python-zope-interface' 'python2-zope-interface')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner' 'python-persistent'
              'python2-persistent' 'python-zope-location' 'python2-zope-location'
              'python-zope-proxy' 'python2-zope-proxy' 'python-zope-security'
              'python2-zope-security' 'python-zope-configuration' 'python2-zope-configuration'
              'python-zope-i18nmessageid' 'python2-zope-i18nmessageid')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.component/archive/$pkgver.tar.gz")
sha512sums=('cca873bcb62b6eb60825c55fa8fe9704e505c9bec90f851921545d543c23e3995cc9d70ab9e7b8cc6b37853bbcffd09c93891f675ff113e68b56e23d8b28c912')

prepare() {
  # Build in separated dir to avoid polluting .pyc files
  cp -a zope.component-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.component-$pkgver
  python setup.py build

  cd "$srcdir"/zope.component-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.component-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src

  cd "$srcdir"/zope.component-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-component() {
  depends=('python-setuptools' 'python-zope-deferredimport' 'python-zope-deprecation'
           'python-zope-event' 'python-zope-hookable' 'python-zope-interface')

  cd zope.component-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-component() {
  depends=('python2-setuptools' 'python2-zope-deferredimport' 'python2-zope-deprecation'
           'python2-zope-event' 'python2-zope-hookable' 'python2-zope-interface')

  cd zope.component-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
