# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-configuration
pkgname=('python-zope-configuration' 'python2-zope-configuration')
pkgver=4.1.0
pkgrel=2
pkgdesc="Zope Configuration Markup Language (ZCML)"
arch=('any')
url="https://github.com/zopefoundation/zope.configuration"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-schema' 'python2-zope-schema'
             'python-zope-i18nmessageid' 'python2-zope-i18nmessageid')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.configuration/archive/$pkgver.tar.gz")
sha512sums=('fb882684a3fffa28bbce822c4bf8fc0c69b2a7dc7582743f466dd94a575564296d3c5710412ccd8e4ece736850bbe2be4965728b891133a93bf551726e685ac0')

prepare() {
  cp -a zope.configuration-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.configuration-$pkgver
  python setup.py build

  cd "$srcdir"/zope.configuration-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.configuration-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.configuration-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-configuration() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema'
           'python-zope-i18nmessageid')

  cd "$srcdir"/zope.configuration-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-configuration() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-schema'
           'python2-zope-i18nmessageid')

  cd "$srcdir"/zope.configuration-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
