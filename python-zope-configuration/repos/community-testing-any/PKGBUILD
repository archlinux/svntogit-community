# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-configuration' 'python2-zope-configuration')
pkgver=4.0.3
pkgrel=2
pkgdesc="Zope Configuration Markup Language (ZCML)"
arch=('any')
url="https://github.com/zopefoundation/zope.configuration"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-schema' 'python2-zope-schema'
             'python-zope-i18nmessageid' 'python2-zope-i18nmessageid' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/zopefoundation/zope.configuration.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.configuration{,-py2}
}

build() {
  cd "$srcdir/zope.configuration"
  python setup.py build

  cd "$srcdir/zope.configuration-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.configuration"
  nosetests3

  cd "$srcdir/zope.configuration-py2"
  nosetests2
}

package_python-zope-configuration() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema'
           'python-zope-i18nmessageid')

  cd "$srcdir/zope.configuration"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-configuration() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-schema'
           'python2-zope-i18nmessageid')

  cd "$srcdir/zope.configuration-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
