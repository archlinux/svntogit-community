# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-deprecation' 'python2-zope-deprecation')
pkgver=4.2.0
pkgrel=2
pkgdesc="Zope Deprecation Infrastructure"
arch=('any')
url="https://github.com/zopefoundation/zope.deprecation"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/zopefoundation/zope.deprecation.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.deprecation{,-py2}
}

build() {
  cd "$srcdir/zope.deprecation"
  python setup.py build

  cd "$srcdir/zope.deprecation-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.deprecation"
  nosetests3

  cd "$srcdir/zope.deprecation-py2"
  nosetests2
}

package_python-zope-deprecation() {
  depends=('python-setuptools')

  cd "$srcdir/zope.deprecation"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-deprecation() {
  depends=('python2-setuptools')

  cd "$srcdir/zope.deprecation-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
