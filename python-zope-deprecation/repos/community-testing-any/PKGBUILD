# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-deprecation
pkgname=('python-zope-deprecation' 'python2-zope-deprecation')
pkgver=4.3.0
pkgrel=2
pkgdesc="Zope Deprecation Infrastructure"
arch=('any')
url="https://github.com/zopefoundation/zope.deprecation"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.deprecation/archive/$pkgver.tar.gz")
sha512sums=('a1eb90897c39e0a3caf9012ee43a32894e3bc69e78cfa2d3d76f37b745d72e6746ac0f5269ac23a9aef569195fdfc100c1e61ad1b121e9a162cb1427f88d7e1e')

prepare() {
  cp -a zope.deprecation-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.deprecation-$pkgver
  python setup.py build

  cd "$srcdir"/zope.deprecation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.deprecation-$pkgver
  nosetests3

  cd "$srcdir"/zope.deprecation-$pkgver-py2
  nosetests2
}

package_python-zope-deprecation() {
  depends=('python-setuptools')

  cd zope.deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-deprecation() {
  depends=('python2-setuptools')

  cd zope.deprecation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
