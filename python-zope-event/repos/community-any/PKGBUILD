# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgbase=python-zope-event
pkgname=('python-zope-event' 'python2-zope-event')
pkgver=4.4
pkgrel=1
pkgdesc="Provides a simple event system"
arch=(any)
license=('ZPL')
url="https://github.com/zopefoundation/zope.event"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/zopefoundation/zope.event/archive/${pkgver}.tar.gz")
sha512sums=('653273d021a43dca6d77900a3e2928f1621fd9c47a536ddd186e00b601670a4ac72042a1eadf18cf9912609025b491cee41508e8eb53bf2dc2ada2c5d3d5bb70')
makedepends=('python-setuptools' 'python2-setuptools')

prepare() {
  cp -a zope.event-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/zope.event-${pkgver}"
  python setup.py build

  cd "${srcdir}/zope.event-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/zope.event-${pkgver}"
  python setup.py test

  cd "${srcdir}/zope.event-${pkgver}-py2"
  python2 setup.py test
}

package_python-zope-event() {
  depends=('python')
  cd "$srcdir/zope.event-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-event() {
  depends=('python2')
  cd "$srcdir/zope.event-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
