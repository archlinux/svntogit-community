# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgbase=python-zope-event
pkgname=('python-zope-event' 'python2-zope-event')
pkgver=4.3.0
pkgrel=2
pkgdesc="Provides a simple event system"
arch=(any)
license=('ZPL')
url="https://github.com/zopefoundation/zope.event"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/zopefoundation/zope.event/archive/${pkgver}.tar.gz")
sha512sums=('7c42074023f56395156ef7bb120b00972dca8dfa2c80dd0387cb6f58b22380e34f8a2bf7b2fe717941cbcf1997e1aaea9f500f124fceb1c664c90e6c55150ffa')
makedepends=('python-setuptools' 'python2-setuptools')

prepare() {
  cp -a zope.event-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/zope.event-${pkgver}"
  python setup.py build

  cd "${srcdir}/zope.event-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/zope.event-${pkgver}"
  python setup.py test

  cd "${srcdir}/zope.event-${pkgver}-py2"
  python2 setup.py test
}

package_python-zope-event() {
  depends=('python')
  cd "$srcdir/zope.event-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-event() {
  depends=('python2')
  cd "$srcdir/zope.event-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
