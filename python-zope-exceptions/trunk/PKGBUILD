# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-exceptions
pkgname=('python-zope-exceptions' 'python2-zope-exceptions')
pkgver=4.2.0
pkgrel=2
pkgdesc="Generic exceptions and implementations for Zope"
arch=('any')
url="https://github.com/zopefoundation/zope.exceptions"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.exceptions/archive/$pkgver.tar.gz")
sha512sums=('120ae57003426cd308f213885c53fd6d0e61a6ff08eeaa912c9a3ff9f4274a5dfafdc8016814cbef59f9d2722edac0a032286ef334b8dd452afed048cd8c21a5')

prepare() {
  cp -a zope.exceptions-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.exceptions-$pkgver
  python setup.py build

  cd "$srcdir"/zope.exceptions-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.exceptions-$pkgver
  python setup.py test || :

  cd "$srcdir"/zope.exceptions-$pkgver-py2
  python2 setup.py test
}

package_python-zope-exceptions() {
  depends=('python-setuptools' 'python-zope-interface')

  cd zope.exceptions-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-exceptions() {
  depends=('python2-setuptools' 'python2-zope-interface')

  cd zope.exceptions-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
