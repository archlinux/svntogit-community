# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-hookable
pkgname=('python-zope-hookable' 'python2-zope-hookable')
pkgver=4.1.0
pkgrel=1
pkgdesc="Represents the core of the Zope hookable Architecture"
arch=('i686' 'x86_64')
url="https://github.com/zopefoundation/zope.hookable"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.hookable/archive/$pkgver.tar.gz")
sha512sums=('1c5b8ca4e663d89fc1cf08a18fc0fdc13a40c7c500be0115f94f5768e3ea51600f1e9effbd4f29e185958a0c57875ea8aba1f10ecc31cce22b2dfcd137199bdd')

prepare() {
  cp -a zope.hookable-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py build

  cd "$srcdir"/zope.hookable-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.hookable-$pkgver
  nosetests3

  cd "$srcdir"/zope.hookable-$pkgver-py2
  nosetests2
}

package_python-zope-hookable() {
  depends=('python-setuptools')

  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-hookable() {
  depends=('python2-setuptools')

  cd "$srcdir"/zope.hookable-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
