# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-hookable
pkgname=('python-zope-hookable' 'python2-zope-hookable')
pkgver=4.2.0
pkgrel=2
pkgdesc="Represents the core of the Zope hookable Architecture"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.hookable"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.hookable/archive/$pkgver.tar.gz")
sha512sums=('aeeafff7e5c7cbe3d5944864278896f926086514e75be1d5956e32fc0a3769ac11b008c3855a0816084c7deb64f14c65db49852dac2beaf06420a304af6ebb69')

prepare() {
  cp -a zope.hookable-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py build

  cd "$srcdir"/zope.hookable-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py nosetests

  cd "$srcdir"/zope.hookable-$pkgver-py2
  python2 setup.py pytest
}

package_python-zope-hookable() {
  depends=('python-setuptools')

  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-hookable() {
  depends=('python2-setuptools')

  cd "$srcdir"/zope.hookable-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
