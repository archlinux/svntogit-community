# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-hookable
pkgver=5.2
pkgrel=1
pkgdesc="Represents the core of the Zope hookable Architecture"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.hookable"
license=('ZPL')
depends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.hookable/archive/$pkgver.tar.gz")
sha512sums=('7b7503636f30dd65a30c3ad959239a7b0e350d63c695cbbc49dc35382845720caa1ddd75e2198ffc486d24459fb3aea0e783e4f8db46ef8408b5864f9bb627c8')

build() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py nosetests
}

package() {
  cd "$srcdir"/zope.hookable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
