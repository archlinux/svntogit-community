# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-i18nmessageid
pkgver=5.1.1
pkgrel=1
pkgdesc="Message Identifiers for internationalization"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
depends=('python-setuptools')
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('b1afd92c9dc12d12b5e0bb8657f35bc464f81ebedcb4931d0a6ba7e4f4e1975e57cdf76b8344f94efdc41c1319d1aa66175deda145d34486a0b54fe02cb95222')

build() {
  cd zope.i18nmessageid-$pkgver
  python setup.py build
}

check() {
  cd zope.i18nmessageid-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.i18nmessageid-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
