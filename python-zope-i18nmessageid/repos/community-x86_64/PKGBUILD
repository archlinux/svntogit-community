# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-i18nmessageid
pkgname=('python-zope-i18nmessageid' 'python2-zope-i18nmessageid')
pkgver=4.3.1
pkgrel=1
pkgdesc="Message Identifiers for internationalization"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('0ad97114eb71c3aa25356f81c45d6f7d2278f379027e412b7671c3d605b65ea917b794cc533e238907c47c6bed7093711f370dc88aa51028fb9f4cc7c536ee63')

prepare() {
  cp -a zope.i18nmessageid-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py build

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.7:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-i18nmessageid() {
  depends=('python-setuptools')

  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-i18nmessageid() {
  depends=('python2-setuptools')

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
