# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-i18nmessageid
pkgver=5.1.0
pkgrel=1
pkgdesc="Message Identifiers for internationalization"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
depends=('python-setuptools')
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('4a60c37d10b0593cadc7885ef92ea106b8bf1c89df20388ec657859f95fe19d5c282bf8cd86c6e04ec267b4c826226f0f43e4f1d11883c29dbf4b024075e74dc')

build() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10:$PYTHONPATH" python -m zope.testrunner --test-path=src || echo "Tests failed"
}

package() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
