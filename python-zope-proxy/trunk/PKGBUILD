# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-proxy' 'python2-zope-proxy')
pkgver=4.2.0
pkgrel=1
pkgdesc="Generic Transparent Proxies"
arch=('i686' 'x86_64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-zope-security' 'python2-zope-security')
source=("git+https://github.com/zopefoundation/zope.proxy.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.proxy{,-py2}
}

build() {
  cd "$srcdir/zope.proxy"
  python setup.py build

  cd "$srcdir/zope.proxy-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.proxy"
  nosetests3

  cd "$srcdir/zope.proxy-py2"
  nosetests2
}

package_python-zope-proxy() {
  depends=('python-setuptools' 'python-zope-interface')

  cd "$srcdir/zope.proxy"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-proxy() {
  depends=('python2-setuptools' 'python2-zope-interface')

  cd "$srcdir/zope.proxy"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
