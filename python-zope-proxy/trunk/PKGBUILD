# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-proxy
pkgver=4.6.0
pkgrel=1
pkgdesc="Generic Transparent Proxies"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
checkdepends=('python-zope-security' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.proxy/archive/$pkgver.tar.gz")
sha512sums=('25eeb13110316923300bc8e972117d2aeb0a30c2efe9512620f7c972d8d55075de79d498caf361d39aa54952f3e89aeec8d034428172afd229babb89c4cce329')

build() {
  cd "$srcdir"/zope.proxy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.proxy-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
