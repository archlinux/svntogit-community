# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-schema
pkgver=6.0.0
pkgrel=3
pkgdesc="zope.interface extension for defining data schemas"
arch=('any')
url="https://github.com/zopefoundation/zope.schema"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')
checkdepends=('python-zope-testrunner' 'python-zope-testing' 'python-zope-i18nmessageid')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.schema/archive/$pkgver.tar.gz")
sha512sums=('f5dab14a088ab285a52eca9245fbe83860548e12e30113a9cef7fbb2cb88ed6edceec4c91529824be093ea07ba29608dfaa57587bc71f32e364504bab204ba71')

build() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.schema-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
