# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-security
pkgver=5.5
pkgrel=1
pkgdesc="Zope Security Framework"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.security"
license=('ZPL')
depends=('python-setuptools' 'python-zope-component' 'python-zope-i18nmessageid'
         'python-zope-interface' 'python-zope-location' 'python-zope-proxy' 'python-zope-schema')
checkdepends=('python-btrees' 'python-nose' 'python-zope-configuration' 'python-zope-testing'
              'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.security/archive/$pkgver.tar.gz")
sha512sums=('337bff1e103de26ca9511557160d55a0b0743a2a74200112855f79663d0de8b5c6e9cf02a59572cb104063c16330b5f8a3850ba97a5e3595ccaac72f4cd28db6')

build() {
  cd zope.security-$pkgver
  python setup.py build
}

check() {
  cd zope.security-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.security-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
