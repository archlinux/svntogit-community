# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-security
pkgver=5.3
pkgrel=1
pkgdesc="Zope Security Framework"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.security"
license=('ZPL')
depends=('python-setuptools' 'python-zope-component' 'python-zope-i18nmessageid'
         'python-zope-interface' 'python-zope-location' 'python-zope-proxy' 'python-zope-schema')
checkdepends=('python-btrees' 'python-nose' 'python-zope-configuration' 'python-zope-testing'
              'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.security/archive/$pkgver.tar.gz")
sha512sums=('51fd50d42baf8d6f3307ce03620dfb42412adbb86b6c8bf06afa36bed9aef81a8655c9a718eb5e408c6a6ba1e911f5e3bb6a8098cf2a64dcd516d5b0eadfaab5')

build() {
  cd zope.security-$pkgver
  python setup.py build
}

check() {
  cd zope.security-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.security-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
