# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-security
pkgver=4.3.1
pkgrel=3
pkgdesc="Zope Security Framework"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.security"
license=('ZPL')
depends=('python-setuptools' 'python-zope-component' 'python-zope-i18nmessageid'
         'python-zope-interface' 'python-zope-location' 'python-zope-proxy' 'python-zope-schema')
checkdepends=('python-btrees' 'python-nose' 'python-zope-configuration' 'python-zope-testing' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.security/archive/$pkgver.tar.gz")
sha512sums=('8d07683b978ac3fb2bc0f8b3f08d80c10443085a78cb33a8249a31773fa67f0f01e106ab0b076587513848fb884d77ad02322826cb0ff957c812ff81e341469a')

build() {
  cd "$srcdir"/zope.security-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.security-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"
}

package() {
  cd zope.security-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
