# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-testing' 'python2-zope-testing')
pkgver=4.6.0
pkgrel=2
pkgdesc="Zope testing helpers"
arch=('any')
url="https://github.com/zopefoundation/zope.testing"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'git' 'python-zope-exceptions'
             'python2-zope-exceptions')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/zopefoundation/zope.testing.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.testing{,-py2}
}

build() {
  cd "$srcdir/zope.testing"
  python setup.py build

  cd "$srcdir/zope.testing-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.testing"
  python setup.py test

  cd "$srcdir/zope.testing-py2"
  python2 setup.py test
}

package_python-zope-testing() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions')

  cd "$srcdir/zope.testing"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-testing() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-exceptions')

  cd "$srcdir/zope.testing-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
