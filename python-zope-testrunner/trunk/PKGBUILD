# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-testrunner
pkgname=('python-zope-testrunner' 'python2-zope-testrunner')
pkgver=5.2
pkgrel=1
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-exceptions' 'python2-zope-exceptions'
             'python-six' 'python2-six')
checkdepends=('python-zope-testing' 'python2-zope-testing' 'python-zope-testrunner'
              'python2-zope-testrunner' 'python-virtualenv' 'python2-virtualenv')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.testrunner/archive/$pkgver.tar.gz")
sha512sums=('08ba52fbde1cf4af5cc956e5e42e073b3ad72ff8275ca606a71bb42464c53fa714faef6bc6c48f07afb36a52d70744bd28aac583648483aadd8bad7a0d1ff690')

prepare() {
  cp -a zope.testrunner-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.testrunner-$pkgver
  python setup.py build

  cd "$srcdir"/zope.testrunner-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.testrunner-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python setup.py test
  )

  cd "$srcdir"/zope.testrunner-$pkgver-py2
  virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
  (
    . "$srcdir/pyvenv-py2/bin/activate"
    python2 setup.py develop
    python2 setup.py test
  )
}

package_python-zope-testrunner() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions' 'python-six')

  cd zope.testrunner-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-testrunner() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-exceptions' 'python2-six')

  cd zope.testrunner-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/zope-testrunner{,2}
}

# vim:set ts=2 sw=2 et:
