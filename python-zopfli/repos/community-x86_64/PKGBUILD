# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zopfli
pkgname=python-zopfli
pkgver=0.1.9
pkgrel=3
pkgdesc="Python bindings to zopfli"
arch=('x86_64')
url="https://github.com/obp/py-zopfli"
license=('Apache')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip")
sha512sums=('45e7dd3e4aac145ee4737ed6630708799e0606db201f52b42b5090855223652b1d7c5aed9761fe61518c936eaada6b466dccf7a9c6f79c8ca3e8b2d12856d5db')
b2sums=('fe251e1a3d85bccbeaa0e8cbe8e5efcdf77511dd617ad13eb2edd5504d7240bf869c6d28afc4d1ea2e0bb7b3c60c2d4640b240107a1d1ebc841a65e1bf2a2037')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _py_ver=$(python -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:$PYTHONPATH"
  python tests/tests.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
