# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zopfli
pkgname=python-zopfli
pkgver=0.2.1
pkgrel=1
pkgdesc="Python bindings to zopfli"
arch=(x86_64)
url="https://github.com/obp/py-zopfli"
license=(Apache)
depends=(python)
makedepends=(python-setuptools-scm)
checkdepends=(python-pytest)
options=(debug)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip")
sha512sums=('73f8be5209d62a2f26f70584827f540a441f00e98a5e621981f1b12bb7fe7481e2571746524d7a9fd0a16e1eac957941663cf05f6494e4527974ebb83791f987')
b2sums=('3671a3471cb4eed8ba8b2c9dfadb48c9e19a0dfc2342b44cf427e9dfdd2bca8331e66b7c088f4cd63c4778d37c79d97b01e764be79c36dcff7842dbfcd2087b0')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  local _py_ver=$(python -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")

  cd $_name-$pkgver
  export PYTHONPATH="build/lib.linux-$CARCH-$_py_ver:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python setup.py install --optimize=1 --root="$pkgdir"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
