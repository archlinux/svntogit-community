# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python2-btchip
pkgver=0.1.14
pkgrel=2
pkgdesc="Python library to communicate with BTChip dongle"
arch=('any')
depends=('python2-hidapi')
makedepends=('python2-setuptools')
url="https://github.com/LedgerHQ/btchip-python"
license=('Apache')
source=($pkgname-$pkgver.tar.gz::https://github.com/LedgerHQ/btchip-python/archive/v$pkgver.tar.gz
        remove-pyusb-dependency.patch)
sha256sums=('6adfa69f18bc3ecff07a877dd46eff62dc747bd8cac491d882ba1bcb16bd16d5'
            '36dd49db4931e5ef6010e5673d4aaa3adb671ca955db97787cd0ae5726538141')

prepare() {
  cd btchip-python-$pkgver

  # Disable useless (on linux) pyusb dependency
  patch -uNp1 -i ../remove-pyusb-dependency.patch
}

build() {
  cd btchip-python-$pkgver

  python2 setup.py build
}

package() {
  cd btchip-python-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1
}
