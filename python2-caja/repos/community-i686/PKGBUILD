# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=python2-caja
_pkgname=python-caja
pkgver=1.6.1
pkgrel=1
pkgdesc="Python binding for Caja, to allow Caja property page and menu item extensions to be written in Python."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('mate-file-manager' 'python2' 'python2-gobject')
makedepends=('mate-common')
options=('!emptydirs')
replaces=('python-caja')
provides=('python-caja')
source=("http://pub.mate-desktop.org/releases/1.6/${_pkgname}-${pkgver}.tar.xz")
sha1sums=('44c39d62c977b06e9a8f37cfdbd81c2f6eb10ff5')

prepare() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    sed -i 's/python-config/python2-config/' configure
}

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
