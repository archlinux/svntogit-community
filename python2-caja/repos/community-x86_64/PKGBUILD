# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=python2-caja
pkgver=1.22.1
pkgrel=1
pkgdesc="Python binding for Caja, to allow Caja property page and menu item extensions to be written in Python"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('caja' 'python2-gobject')
makedepends=('intltool')
conflicts=('python2-caja-gtk3')
replaces=('python2-caja-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/python-caja-${pkgver}.tar.xz")
sha256sums=('13ef4920047033e88dc1906a98c215e3b365159259cb8f04dcd92aa388de131e')

prepare() {
    	cd "python-caja-${pkgver}"
    	sed -i 's/python-config/python2-config/' configure
}

build() {
    	cd "python-caja-${pkgver}"
    	PYTHON=/usr/bin/python2 ./configure \
        	--prefix=/usr

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

   	 make
}

package() {
    	cd "python-caja-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
