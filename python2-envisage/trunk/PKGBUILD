# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python2-envisage
pkgver=4.4.0
pkgrel=2
pkgdesc="Extensible Application Framework"
arch=('any')
url="https://github.com/enthought/envisage"
license=('BSD')
depends=('python2-apptools')
makedepends=('python2-setuptools')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/envisage/archive/${pkgver}.tar.gz")
md5sums=('3448a251709907848f99304d259a4195')

build() {
  cd "$srcdir"/envisage-$pkgver

  # force selection of wxpython 2.8
  # (see https://github.com/enthought/pyface/issues/96)
  sed -e "s/^\(.*\)import wx$/\1import wxversion\n\1wxversion.select(\"2.8\")\n\1import wx/g" -i $(find . -name '*.py')

  python2 setup.py build
}

package() {
  cd "$srcdir"/envisage-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

