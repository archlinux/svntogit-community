# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: cdhotfire <cdhotfire@gmail.com>

pkgbase=python2-eyed3
pkgname=('python2-eyed3' 'python-eyed3')
pkgver=0.8
pkgrel=4
# 0.8.1 breaks with python-magic in our repos requires https://github.com/ahupp/python-magic
epoch=1
pkgdesc="A Python module and program for processing information about mp3 files"
arch=('any')
url="http://eyed3.nicfit.net/"
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=(https://files.pythonhosted.org/packages/source/e/eyeD3/eyeD3-$pkgver.tar.gz)
md5sums=('840626686e6b1bc6afca9eab99a0873a')
options=(!emptydirs)

package_python2-eyed3() {
  depends=('python2-pathlib' 'python2-setuptools')
  conflicts=('python-eyed3')

  cd $srcdir/eyeD3-$pkgver
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

package_python-eyed3() {
  depends=('python-setuptools')
  cd $srcdir/eyeD3-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}
