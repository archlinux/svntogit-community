# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Tarmo Heiskanen <turskii@gmail.com>

pkgname=python2-flask
pkgver=0.9
pkgrel=1
pkgdesc='Micro webdevelopment framework for Python'
url='http://flask.pocoo.org/'
arch=('any')
license=('custom:BSD')
depends=('python2-werkzeug' 'python2-jinja')
makedepends=('python2-distribute')
replaces=('python-flask')
conflicts=('python-flask')
source=("http://pypi.python.org/packages/source/F/Flask/Flask-${pkgver}.tar.gz")
md5sums=('4a89ef2b3ab0f151f781182bd0cc8933')

build() {
  cd Flask-${pkgver}

  python2 setup.py build
}

check() {
  cd Flask-${pkgver}

  python2 setup.py test
}

package() {
  cd Flask-${pkgver}

  python2 setup.py install --root=${pkgdir} --optimize=1

  install -d ${pkgdir}/usr/share/licenses/python2-flask
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/python2-flask
}
