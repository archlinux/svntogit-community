# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Francois Garillot <francois[@]garillot.net>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=python2-flickrapi
_pkgname=flickrapi
pkgver=1.4.2
pkgrel=3
pkgdesc="The official Python interface to the Flickr API"
arch=('any')
url="http://stuvel.eu/flickrapi"
license=('Python')
depends=('python2')
makedepends=('python2-docutils' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/f/$_pkgname/$_pkgname-$pkgver.zip"
        fix_setuptools.patch)

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  
  # Fix setuptools > 0.7b
  patch -p1 -i ../fix_setuptools.patch

  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -r "$pkgdir"/usr/{README,LICENSE,UPGRADING}
}

sha512sums=('e140fd0bdf04d3866988609b2c9d8cb841766aafe349f4cb050716c89912c24f4f656419b7b48e92bae0e1b94b4cd3482475d31171895a1aa52d9da1849f9d88'
            '2ab9de3dd3086cf823d397e541287cf04851397c349d608bc8efc7e2e2fc3fa4bc59f5201a4e83a4a2d8a45ad0b23c2c18f45449b3947b09338722c7dda8b6ca')
