# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: shadyabhi <abhijeet.1989@gmail.com>

pkgname=python2-google-api-python-client
pkgver=1.2
pkgrel=1
pkgdesc="Google API Client Library for Python"
arch=('any')
url="https://github.com/google/google-api-python-client"
license=('Apache')
depends=('python2-httplib2')
makedepends=('python2-setuptools')
provides=('python2-oauth2client' 'python2-uritemplate')
conflicts=('python2-oauth2client' 'python2-uritemplate')
optdepends=('python2-gflags: for oauth2client.tools.run function')
source=("https://pypi.python.org/packages/source/g/google-api-python-client/google-api-python-client-${pkgver}.tar.gz")
md5sums=('031c69eacdd25606782d045b17f54934')
 
build() {
  cd "$srcdir/google-api-python-client-$pkgver"
  python2 setup.py build
}
 
package() {
  cd "$srcdir/google-api-python-client-$pkgver"
 
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
 
  _site=`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()"`
  _version=`python2 -c "from sys import version_info; print '%d.%d'%(version_info.major,version_info.minor)"`
  _path="${pkgdir}/${_site}/google_api_python_client-${pkgver}-py${_version}.egg-info"
  chmod o+r -R "${_path}"
 
  find "$pkgdir" -name "*.py" -exec sed -i '1s/python[[:space:]]*$/python2/' {} \;
  find "$pkgdir" -name "*.py" -exec sed -i 's/python2.[1-7]/python2/' {} \;
}
