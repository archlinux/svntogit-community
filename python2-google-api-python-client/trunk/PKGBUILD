# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: shadyabhi <abhijeet.1989@gmail.com>

pkgname=python2-google-api-python-client
pkgver=1.3.1
pkgrel=2
pkgdesc="Google API Client Library for Python"
arch=('any')
url="https://github.com/google/google-api-python-client"
license=('Apache')
depends=('python2-httplib2' 'python2-oauth2client' 'python2-uritemplate')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/g/google-api-python-client/google-api-python-client-${pkgver}.tar.gz")
md5sums=('cc482e0c775ac240048fa51febb3f439')

prepare() {
  cd "$srcdir/google-api-python-client-$pkgver"

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|python2.[1-7]|python2|" \
      -i $(find . -name '*.py')
}

build() {
  cd "$srcdir/google-api-python-client-$pkgver"

  python2 setup.py build
}
 
package() {
  cd "$srcdir/google-api-python-client-$pkgver"
 
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
 
  _site=`python2 -c "from distutils.sysconfig import get_python_lib; print get_python_lib()"`
  _version=`python2 -c "from sys import version_info; print '%d.%d'%(version_info.major,version_info.minor)"`
  _path="${pkgdir}/${_site}/google_api_python_client-${pkgver}-py${_version}.egg-info"
  chmod o+r -R "${_path}"
}

