# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>

# TODO: Change pkgbase from python2-httplib2 to python-httplib2
pkgbase=python2-httplib2
pkgname=(python-httplib2 python2-httplib2)
_pkgbasename=httplib2
pkgver=0.8
pkgrel=2
pkgdesc='Comprehensive HTTP client library, supporting many features'
url='http://code.google.com/p/httplib2/'
license=('MIT')
arch=('any')
makedepends=('python' 'python2')
provides=('httplib2')
replaces=('httplib2')
source=("http://httplib2.googlecode.com/files/$_pkgbasename-$pkgver.tar.gz"
        'security.patch::http://httplib2.googlecode.com/issues/attachment?aid=2820000000&name=ssl_hostname.diff&token=6H5KCc5DdiALaB413U9cqHZPgMI%3A1378908484674')
sha256sums=('af689bc3cb10a95721eb0d0cf806202a699e7799dacd3497f63b48b2ba429c3f'
            'd29fa108291a24f0708f04867f0479247ecbdd1fcf1617588b6650038d95554a')

prepare() {
  # patch, ref https://bugs.archlinux.org/task/36839
  cd "$_pkgbasename-$pkgver"
  patch -p1 -i "$srcdir/security.patch"
  tail -24 README > LICENSE
  cd ..

  # prepare one build directory for each package
  cp -a "$_pkgbasename-$pkgver" "${pkgname[0]}-$pkgver"
  cp -a "$_pkgbasename-$pkgver" "${pkgname[1]}-$pkgver"
}

package_python-httplib2() {
  depends=('python')

  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-httplib2() {
  depends=('python2')

  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
