# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Morgan LEFIEUX <comete_AT_archlinuxfr.org>

pkgname=python2-ldap
pkgver=2.4.40
pkgrel=1
pkgdesc="Provides an object-oriented API to access LDAP directory servers from Python programs"
arch=('i686' 'x86_64')
url="http://python-ldap.sourceforge.net"
license=('custom')
depends=('python2' 'libldap>=2.4.26')
makedepends=('chrpath')
replaces=('python-ldap')
source=("https://pypi.python.org/packages/4a/d8/7d70a7469058a3987d224061a81d778951ac2b48220bdcc511e4b1b37176/python-ldap-$pkgver.tar.gz")
sha256sums=('202f2f4aeeed2333d4095e8122d066e502e9a64de30cb09ffae16d18c71053f4')

build() {
  cd "$srcdir"/python-ldap-$pkgver
  sed -i 's|<sasl.h>|<sasl/sasl.h>|' Modules/LDAPObject.c
  python2 setup.py build
}

package() {
  cd "$srcdir"/python-ldap-$pkgver
  python2 setup.py install --root "$pkgdir"
  install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  chrpath -d "$pkgdir"/usr/lib/python2.7/site-packages/_ldap.so
}
