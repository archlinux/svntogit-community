# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Morgan LEFIEUX <comete_AT_archlinuxfr.org>

pkgname=python2-ldap
pkgver=2.4.27
pkgrel=1
pkgdesc="Provides an object-oriented API to access LDAP directory servers from Python programs"
arch=('i686' 'x86_64')
url="http://python-ldap.sourceforge.net"
license=('custom')
depends=('python2' 'libldap>=2.4.26')
makedepends=('chrpath')
replaces=('python-ldap')
source=("https://pypi.python.org/packages/fc/99/9eed836fe4d916792994838df125da9c25c5f7c31abfbf6f0ab076e5f419/python-ldap-$pkgver.tar.gz")
md5sums=('3725db082e8624d4c797e62a92ee761a')

build() {
  cd $srcdir/python-ldap-$pkgver
  sed -i 's|<sasl.h>|<sasl/sasl.h>|' Modules/LDAPObject.c
  python2 setup.py build
}

package() {
  cd $srcdir/python-ldap-$pkgver
  python2 setup.py install --root $pkgdir
  install -D -m644 LICENCE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  chrpath -d $pkgdir/usr/lib/python2.7/site-packages/_ldap.so
}
