# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python2-matplotlib
pkgver=1.1.0
pkgrel=4
pkgdesc="A python plotting library, making publication quality plots"
arch=('i686' 'x86_64')
url="http://matplotlib.sourceforge.net/"
license=('custom')
backup=(usr/lib/python2.7/site-packages/matplotlib/mpl-data/matplotlibrc)
depends=('python2-dateutil' 'python2-pytz' 'python2-numpy' 'python2-pyparsing' 'python2-pyqt')
optdepends=('pygtk: for use with the GTK or GTKAgg backend'
            'tk: used by the TkAgg backend'
            'wxpython: for use with the WXAgg backend')
makedepends=('pygtk' 'tk' 'wxpython')
provides=('matplotlib' "python-matplotlib=${pkgver}")
replaces=('python-matplotlib')
conflicts=('python-matplotlib')
source=("http://downloads.sourceforge.net/matplotlib/matplotlib-${pkgver}.tar.gz"
         setup.cfg)
sha1sums=('435b4f04a7e099b79f66451d69ad0b5ce66030ae'
          'f074495fc965298e0372751b34c8edb319437c5f')

build() {
   cd "${srcdir}/matplotlib-${pkgver}"

   cp ../setup.cfg .

   # remove internal copy of pyparsing
   rm lib/matplotlib/pyparsing.py
   sed -i "s/matplotlib.pyparsing/pyparsing/g" lib/matplotlib/{mathtext,fontconfig_pattern}.py

   # remove internal copy of dateutil
   rm -r lib/dateutil

   # python2 fix
   for file in $(find . -name '*.py' -print); do
      sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python2|" \
             -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python2|" ${file}
   done

   python2 setup.py build
}

package() {
   cd "${srcdir}/matplotlib-${pkgver}"
   python2 setup.py install -O1 --skip-build --root "${pkgdir}" --prefix=/usr
    
   install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
   install -m 644 doc/users/license.rst "${pkgdir}/usr/share/licenses/${pkgname}"
}
