# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=python2-munkres
pkgver=1.0.5.4
pkgrel=3
pkgdesc='Munkres algorithm for the assignment problem'
arch=('any')
url='http://bmc.github.com/munkres/'
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("http://pypi.python.org/packages/source/m/${pkgname#*-}/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('105adad30659dbdf82815d3e12a74d4c01a3e0741d5ccbce194ee2ad4e5338d2')

prepare() {
  cd ${pkgname#*-}-${pkgver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname#*-}-${pkgver}

  python2 setup.py build
}

package() {
  cd ${pkgname#*-}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {,"${pkgdir}"/usr/share/licenses/${pkgname}/}LICENSE
}

# vim: ts=2 sw=2 et:
