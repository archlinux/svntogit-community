# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=python2-munkres
pkgver=1.0.7
pkgrel=1
pkgdesc='Munkres algorithm for the assignment problem'
arch=('any')
url='https://github.com/bmc/munkres'
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("http://pypi.python.org/packages/source/m/munkres/munkres-${pkgver}.tar.gz")
sha256sums=('334a5d849bcefed3e4c32ecfbca4ee8dfc8ef5f2f181334d6f7f4e100b71d6c4')

prepare() {
  cd munkres-${pkgver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd munkres-${pkgver}

  python2 setup.py build
}

package() {
  cd munkres-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/usr/share/licenses/python2-munkres
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/python2-munkres/
}

# vim: ts=2 sw=2 et:
