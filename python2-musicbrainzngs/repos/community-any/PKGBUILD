# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=python2-musicbrainzngs
pkgver=0.4
pkgrel=3
pkgdesc='Bindings for the MusicBrainz NGS service'
arch=('any')
url='https://github.com/alastair/python-musicbrainz-ngs'
license=('BSD')
depends=('python2')
makedepends=('python2-distribute')
source=("http://pypi.python.org/packages/source/m/${pkgname#*-}/${pkgname#*-}-$pkgver.tar.gz")
sha256sums=('1a1b7c3cce851c491d5ad71b501bbed2bd653098e33565a0534050d4b922265a')

build() {
  cd ${pkgname#*-}-$pkgver

  python2 setup.py build
}

package() {
  cd ${pkgname#*-}-$pkgver

  python2 setup.py install --root="${pkgdir}" --optimize=1
  rm -rf "${pkgdir}"/usr/lib/python2.7/site-packages/{docs,examples,test}

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {,"${pkgdir}"/usr/share/licenses/${pkgname}/}COPYING
}

# vim: ts=2 sw=2 et:
