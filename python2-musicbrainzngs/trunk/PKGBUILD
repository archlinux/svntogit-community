# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=python2-musicbrainzngs
pkgver=0.5
pkgrel=1
pkgdesc='Bindings for the MusicBrainz NGS service'
arch=('any')
url="https://github.com/alastair/${pkgname/2/}"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("${pkgname/2/}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('ea84abc60fcb5152418dd49e8fdecf3e68759304a71bef422c3b1376886c5b7a')

build() {
  cd ${pkgname/2/}-${pkgver}

  python2 setup.py build
}

package() {
  cd ${pkgname/2/}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
  rm -rf "${pkgdir}"/usr/lib/python2.7/site-packages/{docs,examples,test}

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {,"${pkgdir}"/usr/share/licenses/${pkgname}/}COPYING
}

# vim: ts=2 sw=2 et:
