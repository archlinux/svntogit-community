# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giorgio Gilestro <giorgio@gilest.ro>

pkgname=python2-nautilus
_pkgname=nautilus-python
pkgver=1.1
pkgrel=3
pkgdesc="Python bindings for the Nautilus Extension API"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/nautilus-python/"
license=('GPL')
depends=('nautilus' 'python2-gobject')
replaces=('python-nautilus')
provides=('python-nautilus')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.xz)
sha256sums=('7825e08ada8e131f829d0e7d4144dcfac9fad7bfd7c0600bf3985349c20c496b')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
