# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>

pkgname=python2-ndg-httpsclient
_pkgname=ndg_httpsclient
pkgver=0.4.0
pkgrel=1
pkgdesc="Provides enhanced HTTPS support for httplib and urllib2 using PyOpenSSL"
arch=(any)
url="https://pypi.python.org/pypi/ndg-httpsclient"
license=('BSD')
depends=('python2-pyopenssl' 'python2-pyasn1')
makedepends=('python2-setuptools')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/n/ndg-httpsclient/$_pkgname-$pkgver.tar.gz
        https://github.com/cedadev/ndg_httpsclient/raw/e53ddbcdf1619f636f06e745cf074761a0989d4f/LICENSE)
sha256sums=('e8c155fdebd9c4bcb0810b4ed01ae1987554b1ee034dd7532d7b8fdae38a6274'
            '6aebc0c215cb6b73660175704579011215a7ca8f4e35a6c129169de217faf7fb')

build() {
	cd "$srcdir"/$_pkgname-$pkgver
	python2 setup.py build
}

package() {
	cd "$srcdir"/$_pkgname-$pkgver
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
