# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>

pkgname=python2-ndg-httpsclient
_pkgname=ndg_httpsclient
pkgver=0.3.3
pkgrel=1
pkgdesc="Provides enhanced HTTPS support for httplib and urllib2 using PyOpenSSL"
arch=(any)
url="https://pypi.python.org/pypi/ndg-httpsclient"
license=('BSD')
depends=('python2-pyopenssl' 'python2-pyasn1')
makedepends=('python2-setuptools')
options=(!emptydirs)
source=(https://pypi.python.org/packages/source/n/ndg-httpsclient/$_pkgname-$pkgver.tar.gz
        https://github.com/cedadev/ndg_httpsclient/raw/e53ddbcdf1619f636f06e745cf074761a0989d4f/LICENSE)
sha256sums=('7b9601225d2f530663567d03326f28f22a4a6423e56ac1812d61fd20412633f2'
            '7be3995faaf78cebcf2cfcd77dbad990855d7a639243900d2208a824ced29e07')

build() {
	cd "$srcdir"/$_pkgname-$pkgver
	python2 setup.py build
}

package() {
	cd "$srcdir"/$_pkgname-$pkgver
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "$srcdir"/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
