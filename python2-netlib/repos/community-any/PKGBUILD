# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Nicolas Pouillard https://nicolaspouillard.fr

pkgname=python2-netlib
pkgver=0.15.1
pkgrel=3
pkgdesc="A collection of network utilities used by pathod and mitmproxy"
url="https://github.com/mitmproxy/netlib"
license=('MIT')
arch=('any')
depends=('python2-passlib' 'python2-pyasn1' 'python2-pyopenssl' 'python2-hpack')
makedepends=('python2-setuptools')
checkdepends=('python2-nose' 'python2-mock' 'pathod')
source=("http://pypi.python.org/packages/source/n/netlib/netlib-$pkgver.tar.gz")
sha256sums=('f55ad129b770fcf82786fb59d3e4f4291ea4c6dca4e4850bce1977bd9e99492e')

prepare() {
  cd netlib-$pkgver

  # Allow cryptography 1.2.*, hpack 2.1.*
  sed -e 's/cryptography>=1.1.1, <1.2/cryptography>=1.1.1, <1.3/' \
      -e 's/hpack>=2.0.1, <2.1/hpack>=2.0.1, <3.0/' \
      -i setup.py

  # Use system certificate store, and system ssl.match_hostname
  sed -e '/certifi/d' \
      -e '/backports.ssl_match_hostname/d' \
      -i setup.py
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -e 's/from backports import ssl_match_hostname/import ssl as ssl_match_hostname/' \
      -i netlib/tcp.py
}  

build() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/netlib-$pkgver"
  nosetests2
}

package() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/lib/python2.7/site-packages/"{test,netlib/}
}
