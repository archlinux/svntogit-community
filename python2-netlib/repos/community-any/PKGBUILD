# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Nicolas Pouillard https://nicolaspouillard.fr

pkgname=python2-netlib
pkgver=0.15
pkgrel=1
pkgdesc="A collection of network utilities used by pathod and mitmproxy"
url="https://github.com/mitmproxy/netlib"
license=('MIT')
arch=('any')
depends=('python2-passlib' 'python2-pyasn1' 'python2-pyopenssl' 'python2-hpack')
makedepends=('python2-setuptools')
checkdepends=('python2-nose' 'python2-mock' 'pathod')
source=("http://pypi.python.org/packages/source/n/netlib/netlib-$pkgver.tar.gz")
sha256sums=('5f5c02724a32f4d44401a9dccaf7d3bdf077a352d11dc5131bb61fb24299afa5')

prepare() {
  cd netlib-$pkgver

  # Use system certificate store, and system ssl.match_hostname
  sed -e '/certifi/d' \
      -e '/backports.ssl_match_hostname/d' \
      -i setup.py
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -e 's/from backports import ssl_match_hostname/import ssl as ssl_match_hostname/' \
      -i netlib/tcp.py
}  

build() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/netlib-$pkgver"
  nosetests2
}

package() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/usr/lib/python2.7/site-packages/"{test,netlib/}
}
