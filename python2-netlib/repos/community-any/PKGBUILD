# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Nicolas Pouillard https://nicolaspouillard.fr

pkgname=python2-netlib
pkgver=0.16
pkgrel=3
pkgdesc="A collection of network utilities used by pathod and mitmproxy"
url="https://github.com/mitmproxy/netlib"
license=('MIT')
arch=('any')
depends=('python2-passlib' 'python2-pyasn1' 'python2-pyopenssl' 'python2-hpack' 'python2-hyperframe' 'python2-six')
makedepends=('python2-setuptools')
checkdepends=('python2-mock' 'python2-pytest-runner')
source=("http://pypi.python.org/packages/source/n/netlib/netlib-$pkgver.tar.gz")
sha256sums=('c70ed1915a5662c9ffce4dc97d143209e009cf0035a2f692031a6c47e87e6002')

prepare() {
  cd netlib-$pkgver

  sed -e 's/cryptography>=1.2.2, <1.3/cryptography>=1.2.2, <1.4/' \
      -e 's/pyOpenSSL>=0.15.1, <0.16/pyOpenSSL>=0.15.1, <17/' \
      -i setup.py

  # Use system certificate store, and system ssl.match_hostname
  sed -e '/certifi/d' \
      -e '/backports.ssl_match_hostname/d' \
      -i setup.py
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -e 's/from backports import ssl_match_hostname/import ssl as ssl_match_hostname/' \
      -i netlib/tcp.py
}  

build() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py ptr
}

package() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
