# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Nicolas Pouillard https://nicolaspouillard.fr

pkgname=python2-netlib
pkgver=0.12.1
pkgrel=1
pkgdesc="A collection of network utilities used by pathod and mitmproxy"
url="https://github.com/mitmproxy/netlib"
license=('MIT')
arch=('i686' 'x86_64')
depends=('python2-passlib' 'python2-pyasn1' 'python2-pyopenssl' 'python2-hpack')
makedepends=('python2-setuptools')
checkdepends=('python2-nose' 'python2-mock' 'pathod')
source=("http://pypi.python.org/packages/source/n/netlib/netlib-$pkgver.tar.gz")
sha256sums=('090ccaa44f4369f0aa98a831e021277bcd45fdf7d7f00af282073cc7343ce79b')

build() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir/netlib-$pkgver"
  nosetests2
}

package() {
  cd "$srcdir/netlib-$pkgver"
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
