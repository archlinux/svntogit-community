# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Sebastien Binet <binet@lblbox>

pkgbase=python-numexpr
pkgname=('python2-numexpr' 'python-numexpr')
pkgver=2.1
pkgrel=1
pkgdesc="A JIT compiler for Python expressions"
url="http://code.google.com/p/numexpr/"
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('python2-distribute' 'python-distribute' 'python-numpy' 'python2-numpy')
source=(http://numexpr.googlecode.com/files/numexpr-$pkgver.tar.gz)
md5sums=('8c138e81fb4214d05453da0fc88bf0d5')

build() {
  cd "$srcdir"
  cp -a numexpr-$pkgver numexpr-py2-$pkgver

  msg "Building Python2"
  cd "$srcdir"/numexpr-py2-$pkgver
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/numexpr-$pkgver
  python setup.py build
}

package_python2-numexpr() {
  depends=('python2-numpy')

  cd "$srcdir"/numexpr-py2-$pkgver

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/python2|" \
    $(find "${pkgdir}" -name '*.py')

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-numexpr() {
  depends=('python-numpy')

  cd "$srcdir"/numexpr-$pkgver

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

