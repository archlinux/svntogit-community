# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: vae77 <fernandogrd@yahoo.com.br> 

pkgbase=python-pathlib
pkgname=(python-pathlib python2-pathlib)
pkgver=1.0.1
pkgrel=2
pkgdesc="This module offers a set of classes featuring all the common operations on paths in an easy, object-oriented way"
arch=('any')
url="http://pathlib.readthedocs.org/en/${pkgver}/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/pathlib/pathlib-${pkgver}.tar.gz")
md5sums=('5099ed48be9b1ee29b31c82819240537')

prepare() {
  cp -a pathlib-$pkgver{,-py2}
}

package_python-pathlib() {
  depends=('python')

  cd "$srcdir/pathlib-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-pathlib() {
  depends=('python2')

  cd "$srcdir/pathlib-$pkgver-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
