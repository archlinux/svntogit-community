# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

_pkgname=Pillow
pkgname=python2-pillow
pkgver=6.2.1
pkgrel=2
pkgdesc="Python Imaging Library (PIL) fork."
arch=('x86_64')
url="https://python-pillow.github.io/"
license=('BSD')
depends=('python2' 'lcms2' 'libtiff' 'openjpeg2' 'libimagequant')
makedepends=('python2-setuptools' 'freetype2' 'libraqm' 'libwebp' 'tk')
optdepends=('freetype2: for the ImageFont module'
            'ghostscript: for EPS images'
            'libraqm: for complex text scripts'
            'libwebp: for webp images'
            'tk: for the ImageTK module'
            'python2-olefile: OLE2 file support'
            'python2-pyqt5: for the ImageQt module')
checkdepends=('python2-pytest' 'xorg-server-xvfb' 'python2-olefile' 'python2-pyqt5'
              'python2-cffi' 'python2-numpy' 'ghostscript')
provides=('python2-imaging' 'python2-pillow6')
conflicts=('python2-imaging' 'python2-pillow6')
replaces=('python2-imaging' 'python2-pillow6')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('13acde283ece6ac69f62609fa108d316')
b2sums=('abcfc41d1598e224c1f9800011dca06e704bca51f7132f9113da2f486fe355fe3274b2242bb946a93c55e47cd095966c2c0551eda529eacc25215bdcb8791914')

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python2 setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    export PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7"
    python2 selftest.py
    # 99.6% of tests pass, just some font rendering giving trouble?
    xvfb-run python2 -m pytest -k "not test_variation_set_by_axes and not test_variation_set_by_name \
                                   and not test_complex_unicode_text"
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python2 setup.py install --root="${pkgdir}/" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -dm755 "${pkgdir}/usr/include/python2.7/"
    install -m644 -t "${pkgdir}/usr/include/python2.7/" src/libImaging/*.h
}
