# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alessandro Pezzoni <donbex at nerdshack dot com>

pkgbase=python2-pycdio
pkgname=(python-pycdio python2-pycdio)
_pypiname=pycdio
pkgver=0.21
pkgrel=2
pkgdesc="Python OO interface to libcdio (CD Input and Control library)"
url="https://pypi.python.org/pypi/pycdio"
arch=(x86_64)
license=(GPL)
makedepends=(libcdio python-setuptools python2-setuptools swig)
source=("https://pypi.io/packages/source/p/$_pypiname/$_pypiname-${pkgver}.tar.gz"
        libcdio2.diff)
sha512sums=('8f31fb02695f7933957bd705666fb9aa4c4c48646e8e7410c9f8b0136981f8bd50b8facd6380b9cbc478954708a1b2e7e9eb4dae2597c759046877ddad1d06c3'
            '38195f284df3365cb1cff34776bf9c856d93ff0438632e8b1e367ea6abbbf4d8cea41d847a2bf853a9ca4395ec4b199f8274c963095834a0a5304e6a4babc987')

prepare() {
  cd $_pypiname-$pkgver
  patch -Np1 -i ../libcdio2.diff

  cd ..
  cp -a $_pypiname-$pkgver python2
  mv $_pypiname-$pkgver python3
}

build() {
  cd python3
  python3 setup.py build

  cd ../python2
  python2 setup.py build
}

package_python-pycdio() {
  depends=(python3 libcdio)

  cd python3
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pycdio() {
  depends=(python2 libcdio)

  cd python2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set sw=2 sts=2 et:
