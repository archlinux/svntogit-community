# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python2-pyface
pkgver=4.3.0
pkgrel=1
pkgdesc="Traits-capable windowing framework"
arch=('any')
url="https://github.com/enthought/pyface"
license=('BSD')
depends=('python2-traits')
makedepends=('python2-distribute')
optdepends=('python2-pyqt: for Qt backend'
            'wxpython: for wx backend')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/pyface/archive/${pkgver}.tar.gz")
md5sums=('0ebc97074dc8d7170c03048108055759')

build() {
  cd "$srcdir"/pyface-$pkgver

  python2 setup.py build
}

package() {
  cd "$srcdir"/pyface-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

