# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python2-pyface
pkgver=4.5.0
pkgrel=2
pkgdesc="Traits-capable windowing framework"
arch=('any')
url="https://github.com/enthought/pyface"
license=('BSD')
depends=('python2-traits')
makedepends=('python2-setuptools')
optdepends=('python2-pyqt4: for Qt backend'
            'wxpython2.8: for wx backend')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/pyface/archive/${pkgver}.tar.gz" "pygments2.patch")
md5sums=('05ea27821ed63a457bf5d95892ac0cf0'
         'e874bda3e3871120c7c47bf0c632582f')

prepare() {
  cd "$srcdir"/pyface-$pkgver

  patch -p0 < ../pygments2.patch
}

build() {
  cd "$srcdir"/pyface-$pkgver

  # force selection of wxpython 2.8
  # (see https://github.com/enthought/pyface/issues/96)
  sed -e "s/^\(.*\)import wx$/\1import wxversion\n\1wxversion.select(\"2.8\")\n\1import wx/g" -i $(find . -name '*.py')

  python2 setup.py build
}

package() {
  cd "$srcdir"/pyface-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

