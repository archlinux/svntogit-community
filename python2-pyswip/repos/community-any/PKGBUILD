# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: p2k <patrick.schneider@uni-ulm.de>

pkgname=python2-pyswip
pkgver=0.2.3
pkgrel=3
pkgdesc='Python wrapper for SWI-Prolog'
arch=('any')
url='https://github.com/yuce/pyswip'
license=('MIT')
depends=('python2' 'swi-prolog')
makedepends=('python2-setuptools' 'git')
source=("git://github.com/yuce/pyswip#commit=72771d9")
sha256sums=('SKIP')

build() {
  cd pyswip

  python2 setup.py build
}

check() {
  cd pyswip

  python2 setup.py check
}

package() {
  cd pyswip

  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cp -R examples "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
