# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: p2k <patrick.schneider@uni-ulm.de>

pkgname=python2-pyswip
pkgver=0.2.3
pkgrel=2
pkgdesc='Python wrapper for SWI-Prolog'
arch=('any')
url='http://code.google.com/p/pyswip/'
license=('MIT')
source=("http://pyswip.googlecode.com/files/pyswip-$pkgver.tar.gz")
depends=('python2' 'swi-prolog')
makedepends=('python2-setuptools')
sha256sums=('dfaf68aeb04c9388d5f11311cacaab95f9a72a73f7a0595ac477e17545353b9d')

build() {
  cd "pyswip-$pkgver"

  python2 setup.py build
}

check() {
  cd "pyswip-$pkgver"

  python2 setup.py check
}

package() {
  cd "pyswip-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -R README examples "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
