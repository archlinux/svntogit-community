# $Id: PKGBUILD 231390 2017-05-30 17:11:33Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>
# Contributor: David Scholl <djscholl@gmail.com> 
# Contributor: David Pretty <david.pretty@gmail.com> 

pkgname=python2-rpy2
pkgver=2.8.6
pkgrel=2
pkgdesc="A very simple, yet robust, Python interface to the R Programming Language."
arch=(x86_64)
url="http://rpy.sourceforge.net/"
license=(MPL GPL LGPL)
depends=(python2-numpy r)
makedepends=(python2-setuptools)
source=("https://pypi.io/packages/source/r/rpy2/rpy2-$pkgver.tar.gz")
sha256sums=('004d13734a7b9a85cbc1e7a93ec87df741e28db1273ab5b0d9efaac04a9c5f98')

prepare() {
  cd rpy2-$pkgver
  sed -i "s:Rlapack:lapack:" setup.py
  sed -i "s:os.path.join(RHOME.strip(), 'include'):'/usr/include/R':" setup.py
  sed -i "s:os.path.join(RHOME.strip(), 'include'):'/usr/include/R':" setup.py
}

package() {
  cd rpy2-$pkgver

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i "$pkgdir"/usr/lib/python2.7/site-packages/rpy2/tests.py
}
