# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=python2-rst2pdf
pkgver=0.92
pkgrel=1
pkgdesc='Create PDFs from simple text markup, no LaTeX required'
arch=('any')
url='http://rst2pdf.googlecode.com/'
license=('MIT')
depends=('python2-distribute' 'python2-docutils' 'python-reportlab' 'python2-pygments')
optdepends=('uniconvertor: vector images support'
            'python-svglib: some SVG support'
            'inkscape: best SVG support'
            'python-wordaxe: hyphenation'
            'python-imaging: non-JPEG bitmap image formats')
source=("http://rst2pdf.googlecode.com/files/rst2pdf-$pkgver.tar.gz")
sha256sums=('bb84b457eb454bb1159547d09cd2071efc95a5036a2c5f98b21bec508838b511')

build() {
  cd "rst2pdf-$pkgver"

  python2 setup.py build
  cd doc 
  rst2man2 rst2pdf.txt rst2pdf.1
}

package() {
  cd "rst2pdf-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  install -Dm644 doc/rst2pdf.1 "$pkgdir/usr/share/man/man1/rst2pdf.1"
}

# vim:set ts=2 sw=2 et:
