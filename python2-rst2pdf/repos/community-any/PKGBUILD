# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=python2-rst2pdf
pkgver=0.93
pkgrel=9
pkgdesc='Create PDFs from simple text markup, no LaTeX required'
arch=('any')
url='https://github.com/ralsina/rst2pdf'
license=('MIT')
makedepends=('git' 'python2-setuptools')
depends=('pygmentize' 'python2-docutils' 'python2-pdfrw' 'python2-pygments' 'python2-reportlab')
optdepends=('inkscape: SVG support'
            'python2-pillow: non-JPEG bitmap image formats')
# previous optdeps: python2-svglib python2-wordaxe uniconvertor
source=("git+https://github.com/ralsina/rst2pdf.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd rst2pdf

  find . -name "*.py" -exec sed -i '0,/env python/s//env python2/' {} \;
  sed -i 's/import Image/from PIL import Image/' rst2pdf/math_flowable.py
}

build() {
  cd rst2pdf

  python2 setup.py build
  rst2man2 doc/rst2pdf.txt doc/rst2pdf.1
}

package() {
  cd rst2pdf

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 doc/rst2pdf.1 "$pkgdir/usr/share/man/man1/rst2pdf.1"
}

# getver: raw.githubusercontent.com/ralsina/rst2pdf/master/setup.py
# vim: ts=2 sw=2 et:
