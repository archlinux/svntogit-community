# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=python2-rst2pdf
pkgver=0.93
pkgrel=8
pkgdesc='Create PDFs from simple text markup, no LaTeX required'
arch=('any')
url='https://github.com/ralsina/rst2pdf'
license=('MIT')
makedepends=('python2-setuptools' 'git')
depends=('python2-docutils' 'python2-reportlab' 'python2-pygments' 'pygmentize'
         'python2-pdfrw')
optdepends=('uniconvertor: vector images support'
            #'python2-svglib: some SVG support'
            'inkscape: best SVG support'
            #'python2-wordaxe: hyphenation'
            'python2-pillow: non-JPEG bitmap image formats')
source=("git://github.com/ralsina/rst2pdf.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd rst2pdf

  find . -name "*.py" -exec sed -i '0,/env python/s//env python2/' {} \;
  sed -i 's/import Image/from PIL import Image/' rst2pdf/math_flowable.py
}

build() {
  cd rst2pdf

  python2 setup.py build
  cd doc 
  rst2man2 rst2pdf.txt rst2pdf.1
}

package() {
  cd rst2pdf

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 doc/rst2pdf.1 "$pkgdir/usr/share/man/man1/rst2pdf.1"
}

# getver: raw.githubusercontent.com/ralsina/rst2pdf/master/setup.py
# vim:set ts=2 sw=2 et:
