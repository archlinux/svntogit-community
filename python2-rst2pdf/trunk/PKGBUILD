# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=python2-rst2pdf
pkgver=0.93
pkgrel=2
pkgdesc='Create PDFs from simple text markup, no LaTeX required'
arch=('any')
url='http://rst2pdf.googlecode.com/'
license=('MIT')
depends=('python2-distribute' 'python2-docutils' 'python2-reportlab' 'python2-pygments' 'python2-pdfrw')
optdepends=('uniconvertor: vector images support'
            #'python2-svglib: some SVG support'
            'inkscape: best SVG support'
            #'python2-wordaxe: hyphenation'
            'python2-imaging: non-JPEG bitmap image formats')
source=("http://rst2pdf.googlecode.com/files/rst2pdf-$pkgver.tar.gz")
sha256sums=('3af44283ced19030a8286e8f8d71106ca6c78efe4bc95e9af42d678c363efcbc')

build() {
  cd "rst2pdf-$pkgver"

  python2 setup.py build
  cd doc 
  rst2man2 rst2pdf.txt rst2pdf.1
}

package() {
  cd "rst2pdf-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 doc/rst2pdf.1 "$pkgdir/usr/share/man/man1/rst2pdf.1"
}

# vim:set ts=2 sw=2 et:
