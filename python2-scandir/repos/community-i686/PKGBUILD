# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-scandir
pkgver=1.4
pkgrel=1
pkgdesc="A better directory iterator and faster os.walk()"
arch=('i686' 'x86_64')
url="https://github.com/benhoyt/scandir"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools' 'git')
source=("git+https://github.com/benhoyt/scandir.git#tag=v$pkgver")
sha256sums=('SKIP')

build() {
  cd scandir
  python2 setup.py build
}

check() {
  cd scandir
  LC_CTYPE=en_US.UTF-8 python2 test/run_tests.py
}

package() {
  cd scandir
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
