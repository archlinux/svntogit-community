# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-scandir
pkgver=1.6
pkgrel=1
pkgdesc="A better directory iterator and faster os.walk()"
arch=('i686' 'x86_64')
url="https://github.com/benhoyt/scandir"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benhoyt/scandir/archive/v$pkgver.tar.gz")
sha512sums=('a48a6120fd4e7912bfa7db1de3f5730c1614b48d25fd8bddff7e35cba8a14993d2bdeee6355d027ff0cdf31ce840f8f655bfb06a7a67cfee3e6d3e0636aba906')

build() {
  cd scandir-$pkgver
  python2 setup.py build
}

check() {
  cd scandir-$pkgver
  LC_CTYPE=en_US.UTF-8 python2 test/run_tests.py
}

package() {
  cd scandir-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
