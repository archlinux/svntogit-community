# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=python2-sqlobject
pkgver=1.6.3
pkgrel=1
pkgdesc='Provides an object interface to SQL databases'
arch=('any')
license=('LGPL')
url='http://sqlobject.org/'
depends=('python2-formencode')
makedepends=('python2-setuptools')
optdepends=('python2-psycopg2: PostgreSQL database support')
source=("https://pypi.python.org/packages/source/S/SQLObject/SQLObject-$pkgver.tar.gz")
sha256sums=('9e4d5be452d10cea43c3e5d6dfd3d17073f5bc10645dc477a6142aa18bb066ce')

prepare() {
  cd "SQLObject-$pkgver"

  find . -type f -exec \
    sed -i 's,^#!/usr/bin/env python,#!/usr/bin/env python2,' {} \;
}

package() {
  cd "SQLObject-$pkgver"

  python2 setup.py install --root="$pkgdir"

  echo "/usr/lib/python2.7/site-packages/SQLObject-$pkgver-py2.6.egg" > \
    "$pkgdir/usr/lib/python2.7/site-packages/$pkgname.pth"
}

# vim:set ts=2 sw=2 et:
