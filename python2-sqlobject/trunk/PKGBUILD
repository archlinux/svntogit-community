# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=python2-sqlobject
pkgver=2.1.0
pkgrel=1
pkgdesc='Provides an object interface to SQL databases'
arch=('any')
license=('LGPL')
url='http://sqlobject.org/'
depends=('python2-formencode')
makedepends=('python2-setuptools')
optdepends=('python2-psycopg2: PostgreSQL database support')
source=("https://pypi.python.org/packages/source/S/SQLObject/SQLObject-$pkgver.tar.gz")
sha256sums=('7c3b996592a0ed88359ec11fd6668e46874ae030b5903d7b2364d48f2758c4cc')

prepare() {
  cd "SQLObject-$pkgver"

  find . -type f -exec \
    sed -i 's,^#!/usr/bin/env python,#!/usr/bin/env python2,' {} \;
}

package() {
  cd "SQLObject-$pkgver"

  python2 setup.py install --root="$pkgdir"

  echo "/usr/lib/python2.7/site-packages/SQLObject-$pkgver-py2.6.egg" > \
    "$pkgdir/usr/lib/python2.7/site-packages/$pkgname.pth"

  # Removing documentation that conflicts with other packages
  rm -rf "$pkgdir/usr/lib/python2.7/site-packages/docs"
}

# vim:set ts=2 sw=2 et:
