# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgname=python2-sympy
pkgver=0.7.1
pkgrel=1
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='http://code.google.com/p/sympy'
license=('BSD')
depends=('python2')
optdepends=('pyglet:plotting')
source=("http://sympy.googlecode.com/files/sympy-${pkgver}.tar.gz")
md5sums=('3847b5ed5c1531bf3c1b849dc2337253')

build() {
  cd sympy-${pkgver}

  # python2 fix
  sed \
    -e 's_#!/usr/bin/python_#!/usr/bin/python2_' \
    -i sympy/galgebra/GA.py sympy/galgebra/tests/test_GA.py

  sed \
    -e 's_#!/usr/bin/env python_#!/usr/bin/env python2_' \
    -i sympy/mpmath/tests/runtests.py

  python2 setup.py build
}

check() {
  cd sympy-${pkgver}

  python2 setup.py test
}

package() {
  cd sympy-${pkgver}

  python2 setup.py install --root ${pkgdir} --optimize=1

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
