# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com
pkgname=python2-systemd
_pkgname=systemd
pkgver=221
pkgrel=1
pkgdesc="Python2 bindings for systemd"
arch=('x86_64' 'i686')
url="http://www.freedesktop.org/wiki/Software/systemd/"
license=('GPL')
depends=('python2' 'python2-lxml')
makedepends=('intltool' 'gperf')
source=("http://www.freedesktop.org/software/$_pkgname/$_pkgname-$pkgver.tar.xz")
md5sums=('b4d5a253841cf28a98b7ec99c45e3716')

build() {
	cd "$_pkgname-$pkgver"
    ./configure \
        --libexecdir=/usr/lib \
        --disable-audit \
        --disable-manpages \
        PYTHON=python2
	make CFLAGS="${CFLAGS} -fno-lto"
}

package(){
	cd "$_pkgname-$pkgver"
    depends+=("systemd>=$pkgver")
    make DESTDIR=$srcdir/root install
    install -dm755 $pkgdir/usr/lib/
    cp -dpr --no-preserve=ownership $srcdir/root/usr/lib/python2.7/ $pkgdir/usr/lib/python2.7/
}
