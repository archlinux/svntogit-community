# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Scott Horowitz <stonecrest@gmail.com>

pkgname=python2-tagpy
pkgver=0.94.8
pkgrel=3
pkgdesc='Python bindings for TagLib'
arch=('x86_64' 'i686')
url='http://pypi.python.org/pypi/tagpy'
license=('MIT')
depends=('python2' 'taglib' 'boost-libs')
makedepends=('python2-distribute' 'boost')
replaces=('tagpy')
conflicts=('tagpy')
source=("http://pypi.python.org/packages/source/t/tagpy/tagpy-$pkgver.tar.gz"
        'taglib1.7.patch')
sha256sums=('56eab8dd81510f8af18e19375a0ffd5cd25c8e555104be46a92c3dc08634a0dc'
            'f6cc34f8d4d1186270943cd7e1ad3c9478ae7064ab9b2cddf7760d493295cd26')

build() {
  cd "tagpy-$pkgver"

  patch -p1 -i "$srcdir/taglib1.7.patch"

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py') 
 
  ./configure.py \
    --taglib-lib-dir=/usr/lib/ \
    --taglib-inc-dir=/usr/include/taglib/ \
    --boost-inc-dir=/usr/include/boost/ \
    --boost-lib-dir=/usr/lib/ \
    --boost-python-libname=boost_python
  python2 setup.py build
}

check(){
  cd "tagpy-$pkgver"

  python2 setup.py test
}

package(){
  cd "tagpy-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
