# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Scott Horowitz <stonecrest@gmail.com>

pkgname=python2-tagpy
pkgver=2013.1
pkgrel=13
pkgdesc='Python bindings for TagLib'
arch=('x86_64')
url='https://pypi.python.org/pypi/tagpy'
license=('MIT')
depends=('python2' 'taglib' 'boost-libs')
makedepends=('python2-setuptools' 'boost')
replaces=('tagpy')
conflicts=('tagpy')
source=("https://files.pythonhosted.org/packages/source/t/tagpy/tagpy-$pkgver.tar.gz" "setup.patch")
sha256sums=('80481c78ce34878e7a1f3231acd781a3bf1b0b569c70ce0e8d63af58061152f4'
            'eb22100507005264b35d9bb5b08d0ccc89531508f958ac9fc656b0d84c6ce32d')

prepare() {
  cd "tagpy-$pkgver"
  patch -Np1 -i $srcdir/setup.patch

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py') 
}

build() {
  cd "tagpy-$pkgver"

  ./configure.py \
    --taglib-lib-dir=/usr/lib/ \
    --taglib-inc-dir=/usr/include/taglib/ \
    --boost-inc-dir=/usr/include/boost/ \
    --boost-lib-dir=/usr/lib/ \
    --boost-python-libname=boost_python
  python2 setup.py build
}

check(){
  cd "tagpy-$pkgver"

  python2 setup.py test
}

package(){
  cd "tagpy-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
