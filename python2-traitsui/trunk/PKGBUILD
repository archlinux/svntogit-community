# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python2-traitsui
pkgver=4.5.1
pkgrel=1
pkgdesc="Traits-capable user interfaces"
arch=('any')
url="https://github.com/enthought/traitsui"
license=('BSD')
depends=('python2-pyface')
makedepends=('python2-setuptools')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/traitsui/archive/${pkgver}.tar.gz" "wx28.patch")
md5sums=('4d60037fbe30b0f67ae2826710968a4d'
         '0baa7f7c9620871b5e472d033181db4d')

build() {
  cd "$srcdir"/traitsui-$pkgver

  # force selection of wxpython 2.8
  # (see https://github.com/enthought/pyface/issues/96)
  sed -e "s/^\(.*\)import wx$/\1import wxversion\n\1wxversion.select(\"2.8\")\n\1import wx/g" -i $(find . -name '*.py')
  patch -p0 < ../wx28.patch

  python2 setup.py build
}

package() {
  cd "$srcdir"/traitsui-$pkgver

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

