# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: David Moore <davidm@sjsoft.com>

pkgname=python2-xlwt
pkgver=0.7.5
pkgrel=2
pkgdesc="Library to create spreadsheet files compatible with MS Excel 97/2000/XP/2003 XLS files"
arch=('any')
url="http://www.python-excel.org/"
license=('BSD')
depends=('python2')
makedepends=('python2-setuptools')
source=("https://pypi.python.org/packages/source/x/xlwt/xlwt-${pkgver}.tar.gz")
md5sums=('59cb5efd55319465dfcd25e6a485f03c')

prepare() {
  cd "$srcdir/xlwt-$pkgver"

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  cd "$srcdir/xlwt-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/xlwt-$pkgver"

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 licences.py "$pkgdir/usr/share/licenses/$pkgname/licences.py"
}

