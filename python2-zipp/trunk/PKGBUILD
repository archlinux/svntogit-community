# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-zipp
pkgver=1.1.0
pkgrel=1
epoch=1
pkgdesc="Pathlib-compatible object wrapper for zip files"
url="https://github.com/jaraco/zipp"
license=('MIT')
arch=('any')
depends=('python2-contextlib2')
makedepends=('python2-setuptools-scm' 'python2-toml')
checkdepends=('python2-jaraco' 'python2-pathlib' 'python2-unittest2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/zipp/archive/v$pkgver.tar.gz")
sha512sums=('36190ec5182e0ffc03996a104215890555d11e15119c2066e5c06c4a4dd23287d7a00837ff7bc019232a2ca3e3398793d5fd5f6b67131fa2d9a6717f66ac4e8e')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd zipp-$pkgver
  python2 setup.py build
}

check() {
  cd zipp-$pkgver
  python2 setup.py test
}

package() {
  cd zipp-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
