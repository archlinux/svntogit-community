# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Shiba Ikumi <shibotto@gmail.com>
# Contributor: Archie <xMickael@ifrance.com>

pkgname=pytube
pkgver=0.0.11.5
pkgrel=3
pkgdesc="PyGTK GUI for the use of downloading and converting YouTube videos"
arch=('any')
url="http://www.gnomefiles.org/app.php/PyTube"
license=('GPL3')
depends=('pygtk' 'youtube-dl' 'mplayer' 'ffmpeg2theora' 'vorbis-tools' 'swftools' 'sox' 'python-gdata')
source=(ftp://ftp.archlinux.org/other/pytube/${pkgname}-${pkgver}.tar.gz)
md5sums=('8de9c1c8f99aa7dc339f4cfdfb7c3986')
sha1sums=('6ee2ff42c28f6371f544c3a43b3715797fdb933c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  find "${srcdir}" -name '*.py' -exec sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' {} \;
  find "${srcdir}" -name '*.py' -exec sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' {} \;
  sed -i 's|#/usr/bin/python|#!/usr/bin/python2|' gdata/auth.py
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -d "${pkgdir}"/usr/{bin,share/{${pkgname}{,/stream2hdd},applications,pixmaps}}
  install -Dm755 pytube "${pkgdir}/usr/bin/"
  install -Dm644 {*.png,pytubegui.glade} "${pkgdir}/usr/share/${pkgname}"
  install -Dm755 pytube.py "${pkgdir}/usr/share/${pkgname}"
  install -Dm644 stream2hdd/{*.py,*.pyc} "${pkgdir}/usr/share/${pkgname}/stream2hdd"
  install -Dm644 pytube.desktop "${pkgdir}/usr/share/applications/"
  install -Dm644 pytube.png "${pkgdir}/usr/share/pixmaps/"
}
