# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: examon <examon.mail[at]gmail[dot]com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Dwight Schauer <dschauer@ti.com>

pkgbase=pyudev
pkgname=('pyudev' 'python2-udev')
pkgver=0.15
pkgrel=5
arch=('any')
url='http://pyudev.readthedocs.org/en/latest/index.html'
license=('LGPL')
makedepends=('python-distribute' 'python2-distribute')
optdepends=('python2-pyqt: PyQt integration'
            'python2-gobject2: Glib integration'
            'wxpython: WX integration')
source=("http://pypi.python.org/packages/source/p/pyudev/pyudev-${pkgver}.tar.gz")
md5sums=('35d7295e71664bb630a1fa61ad11d6f6')

build() {
  cp -rf "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/python2-${pkgbase}-${pkgver}"
}

package_pyudev() {
  pkgdesc='A pure Python 3.x binding to libudev'
  depends=('python' 'udev')
  optdepends=('pyqt: PyQt integration'
              'python-gobject2: Glib integration')
  
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root "${pkgdir}" --optimize=1
}

package_python2-udev() {
  pkgdesc='A pure Python 2.x binding to libudev'
  depends=('python2' 'udev')
  optdepends=('python2-pyqt: PyQt integration'
              'python2-gobject2: Glib integration'
              'wxpython: WX integration')
  
  cd "${srcdir}/python2-${pkgbase}-${pkgver}"
  python2 setup.py install --root "${pkgdir}" --optimize=1
}
