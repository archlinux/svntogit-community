# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Jon Bergli Heier <snakebite@jvnv.net>

pkgname=('python2-pyxattr' 'python-pyxattr')
pkgbase=python-pyxattr
_pkgname=pyxattr
pkgver=0.5.0
pkgrel=2
pkgdesc="A python extension module that allows you to manipulate the extended attributes."
arch=('i686' 'x86_64')
url='http://pyxattr.sourceforge.net/'
license=('GPL')
makedepends=('python2' 'python' 'python2-distribute' 'python-distribute')
source=("http://downloads.sourceforge.net/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('0f7ab1e185087329e40f7de218517c84')

package_python2-pyxattr() {
  depends=('python2')
  provides=('pyxattr')
  conflicts=('pyxattr')
  replaces=('pyxattr')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
}

package_python-pyxattr() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}"
}
