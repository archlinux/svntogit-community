# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>

pkgname=q4wine
pkgver=1.3.5
pkgrel=2
pkgdesc="A Qt4 GUI for Wine"
arch=("i686" "x86_64")
url="http://sourceforge.net/projects/${pkgname}/"
license=("GPL3")
if [[ $CARCH == x86_64 ]]; then
  # in multilib repo catalyst is 1st and default, so hardcode lib32-mesa-libgl
  depends=("lib32-mesa-libgl" "qt5-base" "wine" "sqlite3" "which" "icoutils")
  makedepends=("cmake" "qt5-tools")
else
  depends=("qt5-base" "wine" "sqlite3" "which" "icoutils")
  makedepends=("cmake" "qt5-tools")
fi
optdepends=("winetricks" "fuseiso")
options=('!emptydirs')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/_/-}.tar.bz2)
sha256sums=('aa48b0ea8ff504e341b3352194380aa256f6ac8a6a301e1bbf30196eec8d310c')

build() {
  cd "$srcdir"/${pkgname}-${pkgver/_/-}
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_WINETRIKS=ON \
    -DLIBS_ENTRY_PATH=/usr/lib/$pkgname \
    -DQT5=ON \
    .
  make
}

package() {
if [[ $CARCH == x86_64 ]]; then
  # remove hardcoded lib32-mesa-libgl
  depends=(${depends[@]:1})
fi

  cd "$srcdir"/${pkgname}-${pkgver/_/-}
  make DESTDIR="$pkgdir" install
}
