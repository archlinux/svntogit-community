# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>

pkgname=qcachegrind
_pkgname=kcachegrind
pkgver=0.7.4
pkgrel=4
pkgdesc='Visualization of Performance Profiling Data'
arch=('i686' 'x86_64')
url='http://kcachegrind.sourceforge.net'
license=('GPL2')
depends=('qt5-base' 'desktop-file-utils' 'hicolor-icon-theme' 'libxkbcommon-x11')
optdepends=('graphviz: For drawing call graphs'
            'php: pprof2calltree'
            'python2: hotshot2calltree')
conflicts=('kdesdk-kcachegrind')
source=(http://kcachegrind.sourceforge.net/$_pkgname-$pkgver.tar.gz qt-5.5.patch)
md5sums=('a0be465c0f4acfa08bedafb3963a3193'
         '8e6873dec097dfee6e960845d9decf5b')
install=$pkgname.install

prepare() {
  # Fix build with Qt 5.5
  cd $_pkgname-$pkgver
  patch -p1 -i ../qt-5.5.patch
}

build() {
  cd $_pkgname-$pkgver
  qmake-qt5
  make
}

package() {
  cd $_pkgname-$pkgver
  install -Dm755 cgview/cgview "$pkgdir/usr/bin/cgview"
  install -Dm755 qcachegrind/qcachegrind "$pkgdir/usr/bin/qcachegrind"
  install -Dm755 converters/{dprof,hotshot,memprof,op,pprof}2calltree \
    "$pkgdir/usr/bin/"
  install -Dm644 qcachegrind/qcachegrind.desktop \
    "$pkgdir/usr/share/applications/qcachegrind.desktop"
  install -Dm644 kcachegrind/hi32-app-kcachegrind.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/kcachegrind.png"
  install -Dm644 kcachegrind/hi48-app-kcachegrind.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/kcachegrind.png"

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/hotshot2calltree
}
