# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=2.0.5.0
pkgrel=11
pkgdesc="A 2D CAD package based upon Qt"
arch=('i686' 'x86_64')
url="http://www.ribbonsoft.com/qcad.html"
license=('GPL2')
depends=('qt3')
makedepends=('glu')
options=(libtool)
source=(http://www.ribbonsoft.com/archives/$pkgname/$pkgname-$pkgver-1-community.src.tar.gz
        qcad.xpm
        QCad.desktop
        qcad-intptr.patch
        qcad.patch)
md5sums=('96b6a56027782aec953c9c4e64c5998c'
         'da32fec0d2fb85d96126bf28bb0ab9ff'
         'b4d1eb6724b4b41f191f1ab6fd859c39'
         '92f900fe1fd3a8f841232b587b49c7da'
         '7e6779b1e3b10da1eb4daf36d93f8479')
# http://ghost1227.com/files/misc/qm.tar.gz)

build() {
  cd ${srcdir}/${pkgname}-2.0.5.0-1-community.src

  [ "$CARCH" = "x86_64" ] && {
    patch -p1 < ../qcad-intptr.patch;
    sed -i '1,1i#include <stdint.h>' qcadlib/src/engine/rs_layer.cpp qcadlib/src/engine/rs_entity.cpp;
  }
  patch -p1 < ../qcad.patch
  sed -i "s:"-pedantic"::g" mkspecs/defs.pro

  cd scripts
  sed -i "s|INCLUDEPATH += ../include|INCLUDEPATH += ../include /usr/include/qt3|" ../*/src/*.pro
  sed -i "s|../../qcadlib/include|../../qcadlib/include /usr/include/qt3|" ../qcad/src/qcad.pro
  sed -i "s/qmake/qmake-qt3/" build_qcad.sh
  sed -i 's|eval $MAKE|sed -i "s/-lqt/-lqt-mt/" ../qcad/src/Makefile;eval $MAKE|'  build_qcad.sh
  QTDIR=/usr/lib/qt3 ./build_qcad.sh notrans
}

package() {
  cd ${srcdir}/${pkgname}-2.0.5.0-1-community.src/qcad

  install -D -m755 qcad ${pkgdir}/usr/bin/qcad
  install -d ${pkgdir}/usr/share/{qcad,pixmaps,applications}
  install -m644 ../../qcad.xpm ${pkgdir}/usr/share/pixmaps/qcad.xpm
  install -m644 ../../QCad.desktop \
    ${pkgdir}/usr/share/applications/QCad.desktop
  cp -r fonts patterns data library machines ${pkgdir}/usr/share/qcad
  #cp -r ${srcdir}/qm ${pkgdir}/usr/share/qcad/
}
