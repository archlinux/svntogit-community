# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=qcoro
pkgname=(qcoro-qt5 qcoro-qt6)
pkgver=0.6.0
pkgrel=1
pkgdesc='C++ Coroutines for Qt'
arch=(x86_64)
url='https://github.com/danvratil/qcoro'
license=(MIT)
makedepends=(cmake qt5-websockets qt6-websockets)
source=(https://github.com/danvratil/qcoro/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('26d6ea1103c51b895e93d27b59bee394f80db8fd9ca21e7c0056583b1938906d')

prepare() {
  sed -e 's/-Werror//' -i $pkgbase-$pkgver/CMakeLists.txt
}

build() {
  cmake -B build5 -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT_VERSION=5 \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build5

  cmake -B build6 -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_QT_VERSION=6 \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build6
}

package_qcoro-qt5() {
  depends=(qt5-websockets)
  DESTDIR="$pkgdir" cmake --install build5
  install -Dm644 $pkgbase-$pkgver/LICENSES/MIT.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_qcoro-qt6() {
  depends=(qt6-websockets)
  DESTDIR="$pkgdir" cmake --install build6
  install -Dm644 $pkgbase-$pkgver/LICENSES/MIT.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
