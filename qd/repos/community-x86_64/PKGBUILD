# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT CoM>
# Contributor: Xyne <ca dot archlinux at xyne, backwards>

pkgname=qd
pkgver=2.3.22
pkgrel=1
pkgdesc='C++/Fortran-90 double-double and quad-double library'
arch=(x86_64)
url="http://crd-legacy.lbl.gov/~dhbailey/mpdist"
license=(BSD)
makedepends=(gcc-fortran)
source=("http://crd-legacy.lbl.gov/~dhbailey/mpdist/$pkgname-$pkgver.tar.gz")
sha256sums=('ac99d0af29a30bb32968134bc7840378ef5b9f1e3e7d50c34c206f5810386241')

build() {
  cd $pkgname-$pkgver

  export FC=gfortran
  ./configure --prefix=/usr --enable-shared
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
