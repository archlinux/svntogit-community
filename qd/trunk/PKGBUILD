# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT CoM>
# Contributor: Xyne <ca dot archlinux at xyne, backwards>

pkgname=qd
pkgver=2.3.17
pkgrel=2
pkgdesc='C++/Fortran-90 double-double and quad-double library'
arch=(x86_64)
url="http://crd-legacy.lbl.gov/~dhbailey/mpdist"
license=(BSD)
makedepends=(gcc-fortran)
source=("http://crd-legacy.lbl.gov/~dhbailey/mpdist/$pkgname-$pkgver.tar.gz")
sha256sums=('c58e276f6fcf5f2f442c525f3de42ea00004734572b29c74028bbda0ad81096d')

build() {
  cd $pkgname-$pkgver

  export FC=gfortran
  ./configure --prefix=/usr --enable-shared --with-pic
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
