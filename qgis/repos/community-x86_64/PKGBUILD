# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=qgis
pkgver=1.6.0
pkgrel=6
pkgdesc='A Geographic Information System (GIS) that supports vector, raster & database formats'
url='http://qgis.org/'
license=('GPL')
arch=('i686' 'x86_64')
# update to http://www.qgis.org/wiki/Building_QGIS_from_Source#Overview
depends=('libmysqlclient' 'postgresql-libs' 'sqlite3' 'jasper' 'curl' 'qt' 'python2' 'python2-qt' 'giflib' 'xerces-c' 'cfitsio' 'qwt' 'gdal')
makedepends=('grass' 'gsl' 'postgis' 'netcdf' 'cmake' 'fcgi' 'python2-sip')
optdepends=('postgis: postgis support and SPIT plugin'
            'fcgi: qgis mapserver'
            'python2-sip: python-support'
            'grass: grass plugin'
            'gsl: georeferencer  ')
options=('!makeflags')
source=("http://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        'qgis.desktop'
        'ticket_3378.diff')
md5sums=('9ea8a784d970f5009b0e6271820248f6'
         '8ab66039f2aba519b92f52272ec3c13e'
         '402f3d39a344367fd55bdaa324deb797')

build() {
  # Fix insecure RPATH is weird, but just works ;)
#        echo "os.system(\"sed -i '/^LFLAGS/s|-Wl,-rpath,.\+ ||g' gui/Makefile core/Makefile\")" >> python/configure.py.in

  cd ${pkgname}-${pkgver}
  # fix compilation error https://trac.osgeo.org/qgis/ticket/3378
  patch -Np3 -i ${srcdir}/ticket_3378.diff
  cd ${srcdir}

  rm -rf build
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGRASS_PREFIX=/opt/grass \
    -DQGIS_MANUAL_SUBDIR=share/man

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  # install some freedesktop.org compatibility
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
