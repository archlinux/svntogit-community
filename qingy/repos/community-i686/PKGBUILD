# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=qingy
pkgver=1.0.0
pkgrel=2
pkgdesc="A DirectFB getty replacement"
arch=('i686' 'x86_64')
url="http://qingy.sourceforge.net/"
license=('GPL')
depends=('directfb' 'pam' 'openssl' 'ncurses' 'libxss')
backup=('etc/qingy/settings' 'etc/qingy/welcomes')
options=('!libtool')
install=$pkgname.install
source=("http://downloads.sourceforge.net/sourceforge/qingy/${pkgname}-${pkgver}.tar.bz2"
        'qingy.logrotate'
        'README')
md5sums=('1e0912655e240adfd0ce5ced30bbc685'
         'a9bdfd0ac9f29380eafa97a4a056789d'
         'f54865d71ea34e2f68e53ef60248701b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|/usr/share/xsessions|/etc/X11/sessions|' src/libraries/session.c
  sed -i 's|/usr/X11R6/bin|/usr/X11R6/bin:/sbin:/usr/sbin:/opt/kde/bin:/opt/plt/bin:/usr/share/eclipse:/opt/java/bin:/opt/java/jre/bin:/opt/openoffice/program:/opt/NX/bin:/opt/qt/bin:/opt/plan9/bin:/opt/firebird/bin:/opt/glest/bin:/opt/grass/bin:/opt/maven/bin:/usr/share/netbeans/bin:/opt/qgis/bin:/opt/texlive/bin:/opt/torcs/bin:/opt/wildfire/bin|' src/libraries/session.c

  ./configure --prefix=/usr --sbindir=/sbin --sysconfdir=/etc \
              --disable-optimizations --enable-pam \
              --disable-static-build --enable-gpm-lock --enable-emacs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/var/lib/qingy"
  install -D -m644 "${srcdir}/qingy.logrotate" "${pkgdir}/etc/logrotate.d/qingy"
  install -D -m644 "${srcdir}/README" "${pkgdir}/usr/share/doc/qingy/README"

# fix settings for Arch
  sed -i 's|/var/lib/misc|/var/lib/qingy|' "${pkgdir}/etc/qingy/settings"
  sed -i 's|/usr/X11R6/bin/XFree86|/usr/bin/Xorg|' "${pkgdir}/etc/qingy/settings"
  sed -i 's|/usr/X11R6/bin/xinit|/usr/bin/xinit|' "${pkgdir}/etc/qingy/settings"
  sed -i 's|session    optional|#session    optional|' "${pkgdir}/etc/pam.d/qingy"
}
