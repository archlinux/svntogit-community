# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=qiv
pkgver=2.3.1
pkgrel=2
pkgdesc="Quick Image Viewer (qiv) is a very small and fast GDK/Imlib image viewer"
arch=('i686' 'x86_64')
url="http://spiegl.de/qiv/"
license=('GPL2')
depends=('file' 'gtk2' 'imlib2' 'libexif' 'lcms2' 'desktop-file-utils')
source=(http://spiegl.de/qiv/download/$pkgname-$pkgver.tgz)
md5sums=('93aea7469be64ebd35277a6dac079fc8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s|^PREFIX = .*|PREFIX = /usr|" Makefile
  sed -i "s|\./qiv|echo &|" Makefile  # nuke test
  sed -i "s|/man/|/share&|" Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s|^PREFIX = .*|PREFIX = $pkgdir/usr|" Makefile
  make install

  install -D -m 0644 README "$pkgdir/usr/share/doc/qiv/README"
  install -d "$pkgdir/usr/share/doc/qiv/contrib"
  cp contrib/* "$pkgdir/usr/share/doc/qiv/contrib"
  #install -D -m 0755 contrib/gps2url.py "$pkgdir/usr/share/doc/qiv/contrib/gps2url.py"
  #install -D -m 0755 contrib/qiv-command.example "$pkgdir/usr/share/doc/qiv/contrib/qiv-command.example"
}

