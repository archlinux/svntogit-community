# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Matthias Maennich <arch@maennich.net>
# Contributor: <boenki@gmx.de>

pkgname=qlandkartegt
pkgver=1.8.0
pkgrel=1
pkgdesc="Use your GPS with Linux"
arch=('i686' 'x86_64')
url="http://www.qlandkarte.org/"
license=('GPL')
depends=('gdal' 'gpsd' 'libdmtx' 'libexif' 'libmariadbclient' 'glu' 'postgresql-libs'
         'shared-mime-info' 'qtwebkit')
makedepends=('cmake' 'garmindev' 'mesa')
optdepends=('garmindev: to connect garmin devices')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        $pkgname-mimetypes.xml)
sha256sums=('e747d2e1a78c1884ce00b4606525d2be3b309089a3b5cafbc8f2b7aa94959b14'
            'e8d33948831f8a66edd752c71c653085d6c4d9f1969e70dd8c40b9c7ca37fba0')

prepare() {
  cd "${srcdir}"
  mkdir build
}

build() {
  cd "${srcdir}"/build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package(){
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/$pkgname-mimetypes.xml \
    "${pkgdir}"/usr/share/mime/packages/$pkgname-mimetypes.xml
}
