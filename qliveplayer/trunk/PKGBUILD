# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Megumi_fox <i@megumifox.com>

pkgname=qliveplayer
pkgver=4.1.1
_libcommit=c91879a4aca19d957d79dbdecab68b88f022f905
pkgrel=1
pkgdesc='Cute and useful Live Stream Player with danmaku support.'
arch=('x86_64')
url="https://github.com/THMonster/QLivePlayer"
license=('GPL2')
depends=('ffmpeg' 'mpv' 'qt5-base' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-quickcontrols'
         'qt5-quickcontrols2' 'qt5-svg')
makedepends=('extra-cmake-modules' 'ninja' 'rust')
optdepends=('streamlink: for foreign streaming service support')
source=("https://github.com/THMonster/QLivePlayer/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "https://github.com/THMonster/QLivePlayer-Lib/archive/$_libcommit/$pkgname-lib-$_libcommit.tar.gz")
sha512sums=('040ed3bcfc8b61f5b9516505fe9d4fbbff2d21096c7c1dfdf6d3f4a248626427fc1e7394db2753a30da6bae9cd1f6db68fadfd6eeae94c5a36a7e537af0de5cd'
            'cf819719dc814669b021c931118f2f83f2c1f801aea24e49bcd2cc813a1651351aabffb99dbdab0aef3250a8ea1b31dda4b6d1235d9a50178cf0e4c854e2f62c')
options=(!lto)

prepare() {
    mkdir -p build
    rmdir QLivePlayer-$pkgver/src/QLivePlayer-Lib
    ln -s ../../QLivePlayer-Lib-$_libcommit QLivePlayer-$pkgver/src/QLivePlayer-Lib
}

build() {
    cd build
    cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ../QLivePlayer-$pkgver
    ninja
}

package() {
    cd build
    DESTDIR="$pkgdir" ninja install
}
