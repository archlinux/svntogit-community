# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.16.0
pkgrel=1
pkgdesc='Consumer grade GIS software'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack/wiki'
license=('GPL3')
depends=('alglib' 'gdal' 'proj' 'qt5-tools' 'qt5-webengine' 'quazip' 'routino>=3.1')
makedepends+=('cmake')
source=($pkgname-$pkgver.tar.gz::https://github.com/Maproom/qmapshack/archive/refs/tags/V_1.16.0.tar.gz)
sha256sums=('0beae1063fc17c033e6084ea71bc0cc95de505f6fc04da9cf8b70f8003df4302')

build() {
  cmake -S $pkgname-V_$pkgver -B build -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
