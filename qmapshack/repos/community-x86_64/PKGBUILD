# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.15.1
pkgrel=1
pkgdesc='Plan your next outdoor trip'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack/wiki'
license=('GPL3')
depends=('desktop-file-utils' 'gdal' 'hicolor-icon-theme' 'proj' 'qt5-webengine'
         'quazip' 'routino>=3.1' 'alglib' 'qt5-tools')
makedepends=('cmake' 'patch')
source=($pkgname-$pkgver.tar.gz::https://github.com/Maproom/$pkgname/archive/V_$pkgver.tar.gz
        FindPROJ4.patch
        FindQuaZip5.patch)
sha256sums=('91cc5cbb2fe74af424fd9dda143b126443566ebbb3c739e7dc2e154c89995187'
            '1ffb847b098fedf01e347971da33c58b3fbf1a0f27205d48f6eb52206e228046'
            'be1bd07c47c15fedbadf932b4a0611aa34f61dfd693ec3c18bc5f73e607e5232')

prepare() {
  cd "$pkgname-V_$pkgver"
  patch -Np1 -i "${srcdir}"/FindPROJ4.patch
  patch -Np1 -i "${srcdir}"/FindQuaZip5.patch
}

build() {
  cd "$pkgname-V_$pkgver"

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-V_$pkgver"

  make DESTDIR="${pkgdir}" install
}
