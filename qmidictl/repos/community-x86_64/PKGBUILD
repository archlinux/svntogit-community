# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=0.9.6
pkgrel=3
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=(x86_64)
url="https://qmidictl.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(cmake qt6-tools)
optdepends=(
  'qt6-wayland: for native wayland support'
)
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('ec2337caafebb3d1817bec7b989345a253ee87a0ffbbba3c8ec42929529c6760d35379419d57a7e9b8a3df048e31b98eb41971b1eadc960419f78d7a2fbcfa96')
b2sums=('c0d0dda54f130b7fbd7e4e0ff6adecab1ae9ba5943fccdf28702190205bf236ea15262eabb63cfe7eee204de9ecf947cf92bffa49d6858d91dee019444d35012')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
