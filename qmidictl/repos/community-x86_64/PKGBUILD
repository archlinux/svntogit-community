# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=0.9.3
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=('x86_64')
url="https://qmidictl.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('152cf4b22d2b9c3282543763e6ee61a46404e20a23f2f6d0c111894a2345c5908b91612192e50c6dc866bd15d258ed02e60ec59913fa96f85d16a1fad82c91da')
b2sums=('bbffbb86d6861e0787c268deea85dfc7a98a97be240a1e2d32b48a8ccc92bfb4cc62ebd329a07967f9609e25983df406c99f9f54a729ca540e5f6529f9e1cf28')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
