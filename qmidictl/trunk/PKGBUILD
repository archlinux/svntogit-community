# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=0.9.7
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=(x86_64)
url="https://qmidictl.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(cmake qt6-tools)
optdepends=(
  'qt6-wayland: for native wayland support'
)
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('819f94979781d7caadd868379ccbae4abdc042c70a28df8ad385256b2aa4920a997c5fa4c5d29070a83802c182c92b4eaf469dc90869302f4b6fa28f41c3d884')
b2sums=('3241c6bfcbecfdd8a9addfc4f4a91b061b89f0e464b855353190c563e3e3c6b714a8e446ca8aece55f57360d8e142561e5f643a2de26a05d2eeb0253e7056af0')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
