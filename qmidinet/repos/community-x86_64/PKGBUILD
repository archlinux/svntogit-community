# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=0.9.4
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=('x86_64')
url="https://qmidinet.sourceforge.io"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'jack2' 'qt6-tools')
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('658af6e88bcb2a5dcf78dc7ecc15ba29040c2687fbadfb7c2f40e940d132ae21272208befde43fe227d9d27866235619335cde7d027f9b6595058ef8882421e8')
b2sums=('d07b12d3df04b1a9fbee9491d5657e2b7d7d1c1c9f7e32406d88f8fc78d2ac0c6b5d9d50b913ebe1adf33c1888c67d82c26827c0af094f5cb1523d09fad0ff9d')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libjack.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
