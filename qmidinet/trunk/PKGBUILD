# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidinet
pkgver=0.9.3
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=('x86_64')
url="https://qmidinet.sourceforge.io"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'jack2' 'qt6-tools')
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1ef8eaee6032b3abae0523a2323990283da51ae9e4bc74886bbcd3bb83e05df2912aacb0d45911040151373db0a1237613fbcc05520987b711e82427a4565bf6')
b2sums=('bfe3671afd5e1f58c97f591a287051fc31aa094637090cfd58a14402aacb74883612e4f6e8e1513d18cabd7468ee8b71c06a4a4fb9037b831fbe6e18c79f342b')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libjack.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
