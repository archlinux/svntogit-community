# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <david dot jo dot adler at gmail dot com>
# Contributor: Albert Graef <aggraef at gmail dot com>
# Contributor: Arch Linux Pro Audio <dev@archaudio.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qmidiroute
pkgver=0.4.0
pkgrel=9
pkgdesc="A MIDI router and filter utility"
arch=(x86_64)
url="http://alsamodular.sourceforge.net/"
license=(GPL2)
groups=(pro-audio)
depends=(
  alsa-lib
  gcc-libs
  glibc
  hicolor-icon-theme
  qt5-base
)
makedepends=(qt5-tools)
source=(https://downloads.sourceforge.net/alsamodular/$pkgname-$pkgver.tar.bz2)
sha512sums=('b3b5ded66c0e226848a3181d67f61ea9e4e76c253400dac63592e4f462f4cf06760b3f2d24a33bbc836f4fe29950a4d702adc048ad80c9b6d673ade8c1a88342')
b2sums=('b538f405debb33e16d505075b9fc2b53f0b1d993670bfc5b0fdcd5d76d06ee8c81b8357b3435266f8b0f7d1609a4835450cd664bc73470ab1a3ba9e60e5476bb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-qt5
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
