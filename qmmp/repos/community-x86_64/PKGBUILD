#$Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=qmmp
pkgver=0.9.1
pkgrel=1
pkgdesc="Qt4 based audio-player"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg'  'qt4' 'taglib' 'xdg-utils')
makedepends=('cmake' 'flac' 'jack' 'libmpcdec' 'pulseaudio' 'ffmpeg' 'libcdio-paranoia' 'libcddb' 'libmms' 'libsamplerate'
             'libmodplug' 'libsndfile' 'wavpack' 'projectm' 'mesa' 'faad2' 'libgme' 'libsidplayfp' 'opusfile' 'wildmidi')
optdepends=('flac: native FLAC support'
            'jack: JACK sound output'
            'libmpcdec: Musepack support'
            'pulseaudio: PulseAudio output'
            'libcdio-paranoia: Compact Disc input and control support'
            'libcddb: CDDB server support'
            'libmms: MMS stream protocol support'
            'libmodplug: MOD playing library'
            'libsndfile: sampled sound support'
            'projectm: visual efects'
            'faad2: ADTS AAC support'
            'libgme: support for chiptunes from various platforms'
            'libsidplayfp: C64 music support'
            'opusfile: Opus support'
            'wildmidi: MIDI support'
            'ffmpeg' 'libsamplerate' 'wavpack')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://sourceforge.net/projects/qmmp-dev/files/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('9314f8b019689fa9f3f1bc8df4076a4b32d384ad992d578fd437284a5c86918b')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib \
          -DUSE_HAL:BOOL=FALSE
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  if [[ $CARCH == 'x86_64' ]]; then
    mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  fi
}
