# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Adam Reichold <adam.reichold@t-online.de>

pkgname=qpdfview
pkgver=0.4.18beta1
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler library.'
url='https://launchpad.net/qpdfview'
arch=('x86_64')
license=('GPL2')
depends=('libcups' 'libsynctex' 'poppler-qt5' 'qt5-svg')
makedepends=('qt5-tools' 'libspectre' 'djvulibre')
optdepends=('libspectre: for PostScript support'
            'djvulibre: for DjVu support')
source=("https://launchpad.net/qpdfview/trunk/$pkgver/+download/qpdfview-$pkgver.tar.gz"
        "https://launchpad.net/qpdfview/trunk/$pkgver/+download/qpdfview-$pkgver.tar.gz.asc")
sha256sums=('36d97b88514be66ede66c30f68a8ad26b3e76fe39f27d3789c02775ce92859a2'
            'SKIP')
# Adam Reichold <adam.reichold@t-online.de>
validpgpkeys=('1F521FF0F87E9E1CDE46B8A9F4928C4DD24D4DF8')

prepare() {
    cd $pkgname-$pkgver
    sed -e 's|2.0.0|1.21.0|' -i application.pro
}

build() {
    cd "$pkgname-$pkgver"
    lrelease-qt5 qpdfview.pro
    qmake-qt5 qpdfview.pro
    make
}

package() {
    cd "$pkgname-$pkgver"
    make INSTALL_ROOT="$pkgdir" install
}

