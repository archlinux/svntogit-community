# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=qpid-proton
pkgver=0.21.0
pkgrel=1
pkgdesc='High-performance, lightweight messaging library'
arch=('i686' 'x86_64')
url='https://qpid.apache.org/proton/index.html'
license=('Apache')
depends=('libsasl' 'perl>=5.26' 'perl<5.27' 'python' 'ruby')
makedepends=('cmake' 'doxygen' 'swig')
source=("http://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz")
md5sums=('ff0131cf76335a1ff9b9ddb077731b12')
sha512sums=('81facd2d6dc690a5b5d8ed2278177396dba1f4d0868421b05870c4492953762f563a39a6160ab2aee378596e327f27864fc15bbfddfb1e5efdc4a4066610f84d')

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        ..
    make
}

package() {
    cd ${pkgname}-${pkgver}/build

    make DESTDIR="${pkgdir}" install
}
