# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=qreator
pkgver=16.06.2
pkgrel=1
pkgdesc="Graphical utility for creating QR codes"
arch=(any)
url="https://davidplanella.org/qreator/"
license=(GPL3)
depends=(geoclue libchamplain libnm python-pillow python-cairo python-gobject python-qrencode
         python-requests python-vobject python-xdg)
makedepends=(python-distutils-extra)
source=("https://launchpad.net/${pkgname}/trunk/$pkgver/+download/${pkgname}_${pkgver}-0%7E201%7Eubuntu16.04.1.tar.xz"
        "$pkgname.appdata.xml"
        "fix-wifi.patch"
        "python3.patch")
sha256sums=('4b3f63be3a671460a4ee2e4694077d0154fc5a663413e39a0dde4362b2703094'
            '42cb9b5a81924b6f8c4d484738413fc7178e94419c1cc12e03c059b8a43de41e'
            '1a5c01e010121368add29efe16e49128ebfa737d48e185886d90c8ca2846cbd9'
            '555c3cfab67eb9661017dd25349be305cce308f040174bad59fafa96659258ab')

prepare() {
	cd $pkgname-$pkgver
	patch -Np0 -i ../fix-wifi.patch
	patch -Np1 -i ../python3.patch
}

build() {
	cd $pkgname-$pkgver
	python3 setup.py build
}

package() {
	cd $pkgname-$pkgver
	python3 setup.py install --root "$pkgdir" --optimize=1
	install -Dm644 ../$pkgname.appdata.xml "$pkgdir"/usr/share/metainfo/$pkgname.appdata.xml
}
