# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.9.3
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=('x86_64')
url="https://qsampler.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'linuxsampler' 'qt6-base')
makedepends=('cmake' 'libgig' 'liblscp' 'qt6-tools')
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0abfcb35aafb773d1b0f2c31406ea7b04581a4a47b01074c70d1b4833a3f06e8223cfa57b1e7f0233bc347636f41732db37150e5bfde76ccaad4eb959428a7ba')
b2sums=('1171de42c7f27481a5e2f95c1d868b68594288d3a640fe118df9a7f1588d0fea73e06bad4d3acd08a99ec4ccceaee7d171a047a96e811be09ebb3c48896e064f')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libgig.so' 'liblscp.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
