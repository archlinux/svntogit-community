# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.9.7
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=(x86_64)
url="https://qsampler.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libgig libgig.so liblscp liblscp.so
linuxsampler qt6-base qt6-svg)
makedepends=(cmake qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('3474ef1924ca4090d153ccf2b50de840c8f0cd22c880e2ffb129c9f1f1516238cfe4e3e35b5dfa839ea21d45390814723f530b8618fa0ca4a4cac1c15bcf3083')
b2sums=('743cd6a2ea2ea9a76b8aa840bc67d15dd3be2a89a783c7de896bf618a14c06f51fa0d753337d73e21b9a350cb96936f2288d8893fc441f8e5117ea322fbb2785')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
