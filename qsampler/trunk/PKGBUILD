# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.5.6
pkgrel=2
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=('x86_64')
url="https://qsampler.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'linuxsampler' 'liblscp' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2a3a322fec31d9c66d7d1e9b4a7f74629e4b2bcf3ee2350bca8d450d2f68c41ac80563f8e28bd5314f00f0387503aa1e33c0b94ff518cca27b2ca93d9d72334e')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
