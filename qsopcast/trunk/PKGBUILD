# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qsopcast
pkgver=0.3.8.1
pkgrel=2
pkgdesc="A Qt front-end to p2p TV sopcast"
arch=('i686' 'x86_64')
url="http://code.google.com/p/qsopcast"
license=('GPL2')
depends=('qt4' 'sopcast')
source=("http://qsopcast.googlecode.com/files/${pkgname}-${pkgver}-qt4.tar.bz2"
        'qsopcast.desktop')
sha1sums=('9c89c15544e20c120c97a0b54589c5d2acac398e'
          'ada48f3fdeab903235967142740d057d544494c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-qt4/src"

  sed -i "s:/usr/local:/usr:g" qsopcast.pro main.cpp
  qmake-qt4

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-qt4/src"

  make INSTALL_ROOT=${pkgdir} install
  install -D -m644 "${srcdir}/qsopcast.desktop" \
                   "${pkgdir}/usr/share/applications/qsopcast.desktop"
  install -D -m644 "icons/mozart.xpm" \
                   "${pkgdir}/usr/share/pixmaps/qsopcast.xpm"
  rm -rf "${pkgdir}/usr/bin/sp-sc"
}
