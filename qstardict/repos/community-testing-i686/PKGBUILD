# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

#pkgbase=qstardict
pkgname=('qstardict' 'kdeplasma-applets-qstardict')
pkgver=1.0.1
pkgrel=4
makedepends=('cmake' 'automoc4' 'kdelibs')
arch=('i686' 'x86_64')
url="http://qstardict.ylsoftware.com/index.php"
license=('GPL2')
changelog=$pkgname.changelog
source=(http://qstardict.ylsoftware.com/files/$pkgname-$pkgver.tar.bz2)
sha256sums=('b778ace65a22e5b9724d687ad90c77ec98feb2671774f73cd51725ec7b59e62f')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  sed -i -e 's|glib/gmem.h|glib.h|' -e 's|glib/gstrfuncs.h|glib.h|' plugins/stardict/stardict.cpp

  qmake-qt4 ENABLED_PLUGINS="stardict web swac"
  make


  cd "${srcdir}/$pkgname-$pkgver/kdeplasma"
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_qstardict() {
pkgdesc="Qt4 clone of StarDict with full support of StarDict dictionaries"
provides=('stardict')
depends=('qt4')
  cd "${srcdir}/$pkgname-$pkgver"

  make INSTALL_ROOT="${pkgdir}" install
}

package_kdeplasma-applets-qstardict(){
pkgdesc="Qt4 clone of StarDict with full support of StarDict dictionaries"
depends=('kdelibs' 'qt4' 'qstardict')
  cd "${srcdir}/qstardict-$pkgver/kdeplasma/build"

   make DESTDIR=${pkgdir} install
}

