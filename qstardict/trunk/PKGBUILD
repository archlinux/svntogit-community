# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgname=qstardict
pkgver=1.1
pkgrel=1
pkgdesc="Qt4 clone of StarDict with full support of StarDict dictionaries"
provides=('stardict')
depends=('qt4')
arch=('x86_64')
url="http://qstardict.ylsoftware.com/index.php"
license=('GPL2')
changelog=$pkgname.changelog
source=(https://github.com/a-rodin/qstardict/archive/$pkgname-$pkgver.tar.gz)
sha256sums=('d804abd17891f29fe5f093f44dc6dba026839fc2c21813907a6510bc6831d6e3')

build() {
  cd "${srcdir}"/$pkgname-$pkgname-$pkgver
  sed -i -e 's|glib/gmem.h|glib.h|' -e 's|glib/gstrfuncs.h|glib.h|' plugins/stardict/stardict.cpp

  qmake-qt4 ENABLED_PLUGINS="stardict web swac"
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgname-$pkgver

  make INSTALL_ROOT="${pkgdir}" install
}

