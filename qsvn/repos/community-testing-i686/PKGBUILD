# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jakub Schmidtke <sjakub-at-gmail.com>

pkgname=qsvn
pkgver=0.8.3
pkgrel=4
pkgdesc="Qt4 GUI for Subversion."
arch=('i686' 'x86_64')
url="http://www.anrichter.net/projects/qsvn/"
license=('GPL')
depends=('qt4' 'subversion')
makedepends=('cmake' 'patch')
changelog=$pkgname.changelog
source=(http://www.anrichter.net/projects/qsvn/chrome/site/$pkgname-$pkgver-src.tar.gz
        lib64-fix.patch)
sha256sums=('8396f667dc7f09de2eead056608e90febb089fc26529b7fcebe157797c320f73'
            '6a2787b5852825aaccd50ad7b20983af859378d86b86aaf550bf4e1bfcc96c3e')

build() {
  cd "${srcdir}/$pkgname-$pkgver/src"

  patch -Np2 -i "${srcdir}/lib64-fix.patch"

  cmake -D CMAKE_INSTALL_PREFIX=/usr/ -D CMAKE_BUILD_TYPE="Release" ../src
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver/src"

  make DESTDIR="${pkgdir}" install

  #desktop file
  install -D -m644 "${srcdir}/$pkgname-$pkgver/src/$pkgname.desktop" \
    "${pkgdir}/usr/share/applications/$pkgname.desktop"
}
