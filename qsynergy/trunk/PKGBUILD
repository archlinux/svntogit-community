# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: mutlu_inek <mutlu_inek@yahoo.de>

pkgname=qsynergy
pkgver=0.9.1
pkgrel=2
pkgdesc="Qt gui for Synergy which lets you remotely control other computers."
url="http://www.volker-lanz.de/en/software/qsynergy/"
license=(GPL)
arch=('i686' 'x86_64')
depends=('qt4' 'synergy')
source=("http://sourceforge.net/projects/$pkgname/files/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('17d8989668016185b859332aacf9e7df')

build() {
   cd "$srcdir/$pkgname-$pkgver"
   qmake4
   make
}

package() {
   cd "$srcdir/$pkgname-$pkgver"
   install -Dm755 "$srcdir"/$pkgname-$pkgver/qsynergy "$pkgdir"/usr/bin/qsynergy
   install -Dm644 dist/debian/qsynergy.desktop "$pkgdir"/usr/share/applications/qsynergy.desktop
   install -Dm644 dist/qsynergy.xpm "$pkgdir"/usr/share/pixmaps/qsynergy.xpm
}
