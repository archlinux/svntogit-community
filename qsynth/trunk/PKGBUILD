# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.9.7
pkgrel=2
pkgdesc="Qt GUI for FluidSynth"
arch=(x86_64)
url="https://qsynth.sourceforge.io/"
license=(GPL)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(cmake fluidsynth qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('1df8b96423a44d2a3db47417867319cea5df941e4e17e7b04c5b9c3839fff4c7d77cbc4a1ba86a2c17f5bbe13aefa59fc46a8769ecb1a2590c1f4c6c28ae6081')
b2sums=('7f74aebf0c635943e64be636035f86bb74e94514f97f199046cebed37675503c15225f626aba9bc862d07b13d941bb26bc3d3550f0f2c0dc32a8ed6db6c7f029')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfluidsynth.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README,TRANSLATORS} -t "$pkgdir/usr/share/doc/$pkgname"
}
