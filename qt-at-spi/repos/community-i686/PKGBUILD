# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Kyle <kyle@gmx.ca>

pkgname=qt-at-spi
pkgver=0.3.1
pkgrel=3
pkgdesc="A Qt plugin that bridges the QAccessible APIâ€™s to the AT-SPI 2 protocol, giving blind and visually impaired users access to qt applications."
arch=(i686 x86_64)
url="http://projects.kde.org/qtatspi"
license=('LGPL')
depends=('at-spi2-core' 'qt4>=4.8')
options=('!libtool')
source=($pkgname-$pkgver.tar.gz::https://gitorious.org/$pkgname/$pkgname/archive-tarball/v$pkgver
        qt-accessibility.sh)
md5sums=('16c9dcfe01ec4bc6ed0b05b7ffc411c3'
         'f0c8551ed54f5d4e5daf7ddac9189aaa')

build() {
	cd "$srcdir/$pkgname-$pkgname"
	qmake-qt4
	make
}

check() {
	cd "$srcdir/$pkgname-$pkgname"
	make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgname"
	make INSTALL_ROOT="$pkgdir" install
	install -D -m755 "$srcdir/qt-accessibility.sh" "$pkgdir/etc/profile.d/qt-accessibility.sh"
}
