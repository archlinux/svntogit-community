# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# shellcheck disable=SC2034,SC2154,SC2164

pkgname=('qt5-styleplugins')
pkgver=5.0.0.20170311
_commit=335dbece103e2cbf6c7cf819ab6672c2956b17b3
pkgdesc='Additional style plugins for Qt5'
pkgrel=2
arch=('x86_64')
url="https://code.qt.io/cgit/qt/qtstyleplugins"
license=('LGPL')
depends=('qt5-base' 'gtk2')
source=("$pkgname-$_commit.tar.gz::https://github.com/qt/qtstyleplugins/archive/$_commit.tar.gz")
sha512sums=('50d9d3bd206025a09ef7bb9f36e9ed15f79bc11842fad8daac1a1c797c379b5e67acd3d40e52d63688e87e67a271e95ab116c1f14a054b5055f51ab1d80a99cd')
install='install.sh'

build() {
  cd qtstyleplugins-$_commit
  qmake PREFIX='/usr' \
        LIBDIR='/usr/lib' \
        QMAKE_CFLAGS_RELEASE="$CFLAGS" \
        QMAKE_CXXFLAGS_RELEASE="$CXXFLAGS"
  make
}

package() {
  cd qtstyleplugins-$_commit
  make INSTALL_ROOT="$pkgdir" install
}
