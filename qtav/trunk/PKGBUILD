# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qtav
pkgver=1.8.0.20151027
_commit=e60c890e963e73ddd48015121b07c4dd8f56b219
pkgrel=2
pkgdesc="A cross-platform multimedia playback framework based on Qt and FFmpeg"
arch=('i686' 'x86_64')
url="http://www.qtav.org/"
license=('LGPL')
depends=('qt5-declarative' 'ffmpeg' 'libxv' 'openal' 'portaudio' 'desktop-file-utils')
makedepends=('git')
install=$pkgname.install
source=("git+https://github.com/wang-bin/QtAV.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd QtAV
  git submodule update --init
}

build() {
  cd QtAV

  qmake-qt5 "CONFIG+=no_rpath recheck"
  make
}

package() {
  cd QtAV

  make INSTALL_ROOT="${pkgdir}" install

  mkdir "$pkgdir/usr/bin"
  ln -s ../lib/qt/bin/player "$pkgdir/usr/bin/qtav-player"
  ln -s ../lib/qt/bin/QMLPlayer "$pkgdir/usr/bin/QMLPlayer"

  install -D src/QtAV.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/QtAV.svg
  mv "${pkgdir}"/usr/share/applications/{,qtav-}player.desktop
  sed -i '8,9 s/player/qtav-player/' "${pkgdir}"/usr/share/applications/qtav-player.desktop
}
