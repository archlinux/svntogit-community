# $Id$
# Maintainer: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgname="qtcreator"
pkgver=2.4.0
_pkgver=2.4.0
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://qt.nokia.com/products/developer-tools'
license=('LGPL')
depends=('qt>=4.8.0')
makedepends=('qt-private-headers>=4.8.0' 'git')
options=('docs')
optdepends=('qt-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project suppport'
            'openssh-askpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support')
install=qtcreator.install
source=("http://get.qt.nokia.com/qtcreator/qt-creator-${_pkgver}-src.tar.gz"
        'qtcreator.desktop')
md5sums=('80c1a2be4c685ce147c42424ad977e1a'
         'f1f4680b9838ff0b8f4cf11eb850d662')

build() {
  cd ${srcdir}

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  qmake ${srcdir}/qt-creator-${_pkgver}-src/qtcreator.pro
  make
}

package() {
  cd ${srcdir}/build

  make INSTALL_ROOT="${pkgdir}/usr/" install

  install -Dm644 ${srcdir}/qtcreator.desktop \
    ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${_pkgver}-src/LGPL_EXCEPTION.TXT \
    ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}
