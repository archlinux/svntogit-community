# $Id$
# Maintainer: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgname=qtcreator
pkgver=2.7.0rc
_pkgver=2.7.0-rc
pkgrel=1
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://qt-project.org'
license=('LGPL')
depends=('qt5-quick1' 'qt5-tools')
makedepends=('git')
options=('docs')
optdepends=('qt5-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'cmake: for cmake project support'
            'openssh-askpass: for ssh support'
            'git: for git support'
            'mercurial: for mercurial support'
            'bzr: for bazaar support'
            'valgrind: for analyze support')
install=qtcreator.install
source=("http://releases.qt-project.org/qtcreator/${_pkgver}/qt-creator-${_pkgver}-src.tar.gz"
        'qtcreator.desktop'
        'fix-build.patch')
md5sums=('5c4baef738b00cfdf53739ffd1434234'
         '82888d4be900e7833d768050a135cd37'
         '4087c00d853addf97ca329d304e9a0a5')

build() {
  cd qt-creator-${_pkgver}-src
  patch -p1 -i "${srcdir}"/fix-build.patch
  cd ../

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  qmake ../qt-creator-${_pkgver}-src/qtcreator.pro
  make
  make docs -j1
}

package() {
  cd build

  make INSTALL_ROOT="${pkgdir}/usr/" install
  make INSTALL_ROOT="${pkgdir}/usr/" install_docs

  install -Dm644 ${srcdir}/qtcreator.desktop ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${_pkgver}-src/LGPL_EXCEPTION.TXT ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}
