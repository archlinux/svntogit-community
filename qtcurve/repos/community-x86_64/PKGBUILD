# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-gtk2' 'qtcurve-qt4' 'qtcurve-qt5' 'qtcurve-kde4')
pkgver=1.8.18
pkgrel=4
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('i686' 'x86_64')
url='https://github.com/QtCurve/qtcurve'
license=('LGPL')
groups=('qtcurve')
makedepends=('cmake' 'automoc4' 'gtk2' 'qt4' 'qt5-svg' 'qt5-x11extras' 'kdebase-workspace')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        "https://github.com/QtCurve/qtcurve/commit/323402d8b99502300f65e909c61153cbd5d5d997.diff")
md5sums=('422d1876d944bb278855f320eda19368'
         '632c1dfa26fcc5f5d45fe082c9738f3b')

prepare() {
  cd $pkgbase-$pkgver

  # fix FS#40524 - Qt 5.3 build fix
  patch -p1 -i ../${source[1]##*/}
}

build() {
  cd $pkgbase-$pkgver

  mkdir build{,-kde4}
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT4_ENABLE_KDE=false
  make

  cd ../build-kde4
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_LOCALE_DIR=/usr/share/locale/kde4 \
           -DENABLE_GTK2=false \
           -DENABLE_QT5=false
  make
}

package_qtcurve-utils() {
  depends=('libx11')
  cd $pkgbase-$pkgver/build/lib/utils
  make DESTDIR="$pkgdir" install
}

package_qtcurve-gtk2() {
  depends=('qtcurve-utils' 'gtk2')
  cd $pkgbase-$pkgver/build/gtk2
  make DESTDIR="$pkgdir" install
  cd ../lib/cairo
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt4() {
  depends=('qtcurve-utils' 'qt4')
  cd $pkgbase-$pkgver/build/qt4
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt5() {
  depends=('qtcurve-utils' 'qt5-svg' 'qt5-x11extras')
  cd $pkgbase-$pkgver/build/qt5
  make DESTDIR="$pkgdir" install
}

package_qtcurve-kde4() {
  depends=('qtcurve-utils' 'kdebase-workspace')
  cd $pkgbase-$pkgver/build-kde4/qt4
  make DESTDIR="$pkgdir" install
  cd ../po
  make DESTDIR="$pkgdir" install
}
