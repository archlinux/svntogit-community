# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qtile
pkgver=0.20.0
pkgrel=1
pkgdesc="A full-featured, pure-Python tiling window manager"
arch=(x86_64)
url="http://www.qtile.org"
license=(MIT)
depends=(
  gdk-pixbuf2
  glibc
  libnotify
  librsvg
  pango
  python-cairocffi
  python-cffi
  python-xcffib
  python-gobject
)
makedepends=(
  git
  libpulse
  python-setuptools-scm
)
checkdepends=(
  graphviz
  gtk3
  imagemagick
  lm_sensors
  procps-ng
  python-bowler
  python-dbus-next
  python-gobject
  python-pytest
  python-pywlroots
  python-xdg
  xorg-server-xephyr
  xorg-server-xvfb
  xorg-xrandr
  xorg-xwayland
)
optdepends=(
  'alsa-utils: for volume widget'
  'canto-daemon: for canto widget'
  'cmus: for cmus widget'
  'jupyter_console: for interaction with qtile via Jupyter'
  'khal: for khal_calendar widget'
  'libpulse: for pulse_volume and pulseaudio_ffi widget'
  'lm_sensors: for sensors widget'
  'moc: for moc widget'
  'python-dbus-next: for utils, notifications and several widgets'
  'python-iwlib: for wlan widget'
  'python-keyring: for imapwidget widget'
  'python-mpd2: mpd2widget widget'
  'python-psutil: graph, net and memory widget'
  'python-pywlroots: for experimental wayland support'
  'python-setproctitle: change process name to qtile'
  'python-xdg: launchbar widget'
  'xorg-xwayland: for XWayland support'
)
install="${pkgname}.install"
source=("git+https://github.com/qtile/qtile#tag=v${pkgver}?signed")
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=(
  '3CCAB226289DE0160C61BDB418D18F1BC464DCA3' # Tycho Andersen <tycho@tycho.pizza>
  '35D92E7CC7357A81173EA1C974F9FDD20984FBEC' # Matt Colligan <mcol@posteo.net>
)

build() {
  cd "${pkgname}"
  python setup.py build
  ./scripts/ffibuild
}

check() {
  cd "${pkgname}"
  export LC_TYPE=en_US.UTF-8
  pytest -vv --backend x11 --backend wayland
}

package() {
  cd "${pkgname}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # config
  install -vDm 644 libqtile/resources/default_config.py -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # desktop files
  install -vDm 644 "resources/${pkgname}.desktop" -t "${pkgdir}/usr/share/xsessions/"
  install -vDm 644 "resources/${pkgname}-wayland.desktop" -t "${pkgdir}/usr/share/wayland-sessions/"
  # docs
  install -vDm 644 {CHANGELOG,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
