# Maintainer: David Runge <dave@sleepmap.de>

pkgname=qtile
pkgver=0.11.1
pkgrel=4
pkgdesc="A full-featured, pure-Python tiling window manager."
arch=('any')
url="http://www.qtile.org"
license=('MIT')
depends=('pango' 'python-xcffib' 'python-cairocffi' 'python-dbus'
'python-gobject' 'python-setuptools')
makedepends=('jupyter_console'
             'python-ipykernel'
             'python-iwlib'
             'python-keyring'
             'python-mpd2'
             'python-pytest'
             'python-setuptools'
             'python-xdg')
optdepends=('alsa-utils: volume widget'
            'canto-daemon: canto widget'
            'cmus: cmus widget'
            'khal: khal_calendar widget'
            'lm_sensors: sensors widget'
            'moc: moc widget'
            'python-iwlib: wlan widget'
            'python-keyring: imapwidget widget'
            'python-mpd2: mpdwidget, mpd2widget widget'
            'python-setproctitle: change process name to qtile'
            'python-xdg: launchbar widget')
#checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-server-xephyr')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('9cc2cdf5a01495c680fae62d23348d68cca456ce2c31848bef23825c943a5e8aebcc3ea70879388af06d222ab703dd410db14e6a9bd4180d90f57651b9dbaed5')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix same-name-issue with renameutils:
  # https://bugs.archlinux.org/task/57710
  mv -v bin/qcmd bin/qtile-cmd
  sed -e 's/qcmd \=/qtile-cmd \=/g' -i setup.py
  sed -e 's/qcmd \-h/qtile-cmd \-h/' \
      -e 's/to qcmd/to qtile-cmd/' \
      -e 's,s/qcmd,s/qtile-cmd,' \
      -e 's/(qcmd/(qtile-cmd/g' \
      -i bin/dqcmd
  sed -e 's/qcmd/qtile-cmd/g' -i libqtile/scripts/qcmd.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# tests disabled for now, as they don't run headless
# https://github.com/qtile/qtile/issues/1130
#check() {
#  cd "${pkgname}-${pkgver}"
#  python libqtile/ffi_build.py
#  py.test
#}

package() {
  cd "${pkgname}-${pkgver}"
  # install
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}" \
    --prefix=/usr
  # license
  install -vDm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # config
  install -vDm 644 libqtile/resources/default_config.py \
  "${pkgdir}/usr/share/doc/${pkgname}/default_config.py"
  # desktop file
  install -vDm 644 "resources/${pkgname}.desktop" \
  "${pkgdir}/usr/share/xsessions/${pkgname}.desktop"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGELOG,README.rst}
}
