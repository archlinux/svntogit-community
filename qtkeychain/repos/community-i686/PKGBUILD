# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# Contributor: Teo Mrnjavac <teo@kde.org>

pkgname=qtkeychain
pkgver=0.8.0
pkgrel=2
pkgdesc='Provides support for secure credentials storage'
arch=(i686 x86_64)
url='https://github.com/frankosterfeld/qtkeychain'
license=(BSD)
depends=(qt5-base)
conflicts=(qtkeychain-qt5)
provides=(qtkeychain-qt5)
replaces=(qtkeychain-qt5)
makedepends=(cmake qt5-tools)
optdepends=('kwallet: kwallet backend' 'libgnome-keyring: GNOME keyring backend' 'libsecret: libsecret backend')
source=("$pkgname-$pkgver.zip::https://github.com/frankosterfeld/qtkeychain/archive/v$pkgver.tar.gz")
sha256sums=('b492f603197538bc04b2714105b1ab2b327a9a98d400d53d9a7cb70edd2db12f')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../$pkgname-$pkgver \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=lib \
          -DCMAKE_INSTALL_LIBEXECDIR=lib/${pkgname} \
          -DCMAKE_BUILD_TYPE=Release \
          -DLIBSECRET_SUPPORT=OFF
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
