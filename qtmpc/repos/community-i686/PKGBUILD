# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Shaika-Dzari <shaikadzari at gmail dot com>

pkgname=qtmpc
pkgver=0.6.1
pkgrel=6
pkgdesc="Qt4-based front-end for MPD"
arch=('i686' 'x86_64')
url="http://qtmpc.lowblog.nl/"
license=('GPL')
depends=('kdebase-runtime')
makedepends=('automoc4' 'cmake' 'patch')
source=(http://files.lowblog.nl/$pkgname/QtMPC-$pkgver.tar.bz2
        qtmpc-0.6.1-underlinking.patch)
sha256sums=('82287a26788b36d49db26518b66203f332cf686eefb81ee2d9be095e61123a62'
            '8295218ebeeb14372c36c34510a9eed7210e82d8e474ef3caae1bd58327780d3')

build() {
  cd "${srcdir}"/QtMPC

  patch -Np1 -i "${srcdir}"/qtmpc-0.6.1-underlinking.patch

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/QtMPC

  make DESTDIR="${pkgdir}" install 
}
