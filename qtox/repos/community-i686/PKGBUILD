# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox
_pkgname=qTox
pkgver=1.7.1
pkgrel=4
_gitver='6f155d4db806a664d76457d42a20246ffbd5d1ac'
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines'
arch=('i686' 'x86_64')
url='https://github.com/tux3/qTox'
license=('GPL3')
depends=('libxss'
         'openal'
         'gtk2'
         'ffmpeg'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'toxcore')
makedepends=('qt5-tools' 'git')

#source=("git+https://github.com/qTox/qTox.git#commit=$_gitver")
source=("https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz"
        "https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz.asc"
	"pr4053.patch::https://patch-diff.githubusercontent.com/raw/qTox/qTox/pull/4053.diff")
sha512sums=('89149d712f342ef09596d376c35b8c5573e21ce74f6edf906d6eeadb8dfe1a6b0f2945221bafb7d7509a9c857d928deed86c0ffb1e2071edd6f899e048ea7122'
            'SKIP'
            'b36eb38c50ede1decde5eb17eab447a62dd1d71e52106f4e73c02abf2af67e308deddb9c70d46e41c51ce3aa6463d012cc19b3207e2885c16cae09cd36040bfe')

# GPG key fingerprints taken from https://github.com/qTox/qTox/blob/$_gitver/README.md
validpgpkeys=('DA262CC93C0E1E525AD21C8596775D454B8EBF44'  # sudden6 <sudden6@gmx.at>
              'BA7883E22F9D35945BA3376053137C3033F09008'  # Zetok Zalbavar <zetok@openmailbox.org>
	      '1157616BBD860C539926F8139591A163FF9BE04C'  # - antis81
	      '31039166FA902CA50D05D6085AF9F2E29107C727'  # - Diadlo
	      'C7A2552D0B250F983827742C133203A3AC399151'  # - initramfs
	      '2880C860D95C909D3DA45C687E086DD661263264'  # - tux3
             )
prepare() {
	# apply PR https://github.com/qTox/qTox/issues/4053
	patch -p1 <pr4053.patch
}

build() {
  install -d build
  cd build
  qmake-qt5 ENABLE_SYSTRAY_UNITY_BACKEND=NO GIT_VERSION=$_gitver GIT_DESCRIBE=v$pkgver ..
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install
}
