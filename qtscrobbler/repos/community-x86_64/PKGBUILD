# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Simon Legner <simon dot legner at gmail dot com>

pkgname=qtscrobbler
pkgver=0.11
pkgrel=6
pkgdesc="Submit .scrobbler.log from portable players to Last.fm and Libre.fm"
arch=('i686' 'x86_64')
url="http://qtscrob.sourceforge.net/"
license=('GPL')
depends=('libmtp' 'qt4')
makedepends=('gtk-update-icon-cache' 'gzip')
source=("http://downloads.sourceforge.net/project/qtscrob/qtscrob/${pkgver}/qtscrob-${pkgver}.tar.bz2")
sha256sums=('01c8e48f616ed09504833d27d92fd62f455bd645ea2d1cc2a5f4c287d641daba')

build() {
  cd "$srcdir"/qtscrob-${pkgver}/src

  qmake-qt4
  make
}

package() {
  cd "$srcdir"/qtscrob-${pkgver}/src

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,icons/hicolor,man/man1}}
  install -m 755 cli/scrobbler "${pkgdir}"/usr/bin/qtscrob-cli
  install -m 755 qt/qtscrob "${pkgdir}"/usr/bin/qtscrob

  install -m 644 cli/scrobbler.1 "${pkgdir}"/usr/share/man/man1/qtscrob-cli.1
  install -m 644 qt/qtscrob.1 "${pkgdir}"/usr/share/man/man1/qtscrob.1
  gzip -9 -f "${pkgdir}"/usr/share/man/man1/*

  install -m 644 qt/qtscrob.desktop "${pkgdir}"/usr/share/applications/qtscrob.desktop
  for size in 16 32 48 64 128 256 512 ; do
    install -dm 755 "${pkgdir}"/usr/share/icons/hicolor/${size}x${size}/apps
    install -m 644 qt/resources/icons/${size}x${size}/qtscrob.png "${pkgdir}"/usr/share/icons/hicolor/${size}x${size}/apps/qtscrob.png
  done
}

# vim: ts=2 sw=2 et:
