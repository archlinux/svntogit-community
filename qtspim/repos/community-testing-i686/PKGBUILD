# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=qtspim
pkgver=9.1.17
pkgrel=2
pkgdesc="New user interface for spim, a MIPS simulator."
arch=('i686' 'x86_64')
url="http://spimsimulator.sourceforge.net/"
license=('BSD')
depends=('qt4')
makedepends=('subversion' 'icu')
source=("spimsimulator::svn://svn.code.sf.net/p/spimsimulator/code/#revision=688")
sha256sums=('SKIP')

prepare() {
  cd spimsimulator/QtSpim
  sed -r \
    -e 's/q([a-z]+)generator/q\1generator-qt4/' \
    -e 's/-Wno-write-strings/& -fpermissive/' \
    -i QtSpim.pro
  rm parser_yacc.* scanner_lex.*
}

build() {
  cd spimsimulator/QtSpim

  qmake-qt4

  # Fix Makefile generated by qmake.
  sed -i 's#$(MOVE) help/qtspim.qhc help/qtspim.qhc;##' Makefile

  make -j1
}

package() {
  cd spimsimulator

  install -D QtSpim/QtSpim "$pkgdir/usr/bin/qtspim"

  install -d "$pkgdir/usr/share/qtspim"
  cp -r QtSpim/help "$pkgdir/usr/share/qtspim"

  install -Dm644 Documentation/spim.man "$pkgdir/usr/share/man/man1/qtspim.1"
  install -Dm644 Setup/qtspim_debian_deployment/qtspim.desktop \
    "$pkgdir/usr/share/applications/qtspim.desktop"
  install -Dm644 Setup/qtspim_debian_deployment/copyright \
    "$pkgdir/usr/share/licenses/$pkgname/copyright"
  install -Dm644 Setup/NewIcon48x48.png "$pkgdir/usr/share/qtspim/qtspim.png"
}
