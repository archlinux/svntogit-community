# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=quagga
pkgver=0.99.21
pkgrel=1
pkgdesc='BGP/OSPF/ISIS/RIP/RIPNG routing daemon suite'
arch=('i686' 'x86_64')
url='http://www.quagga.net'
license=('GPL2')
depends=('libcap' 'net-snmp' 'readline' 'ncurses')
options=('!libtool' '!buildflags')
backup=()
install=quagga.install
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"
        'quagga.rc'
        'quagga.conf'
        '0001-isisd-fix-typo-in-topology-generator-BZ-731.patch')
md5sums=('99840adbe57047c90dfba6b6ed9aec7f'
         '71d474228a1e02d698cd24e9fd55ca38'
         '0c4f2dd13c7dac1b73de923d1d5e4e17'
         'c80174f7bdd32cd84550d52451e8f495')

build() {
  cd $pkgname-$pkgver
  patch -p 1 -i "$srcdir/0001-isisd-fix-typo-in-topology-generator-BZ-731.patch"
  ./configure --prefix=/usr --sysconfdir=/etc/quagga --localstatedir=/run/quagga \
    --enable-exampledir=/usr/share/doc/quagga/examples \
    --enable-vtysh \
    --enable-isisd \
    --enable-isis-topology \
    --enable-netlink \
    --enable-snmp \
    --enable-tcp-zebra \
    --enable-irdp \
    --enable-pcreposix \
    --enable-multipath=64 \
    --enable-user=nobody \
    --enable-group=nobody \
    --enable-configfile-mask=0640 \
    --enable-logfile-mask=0640
  make
}

package() {
  # initscripts files
  for d in zebra ripd ripngd bgpd ospfd ospf6d isisd; do
    install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$d"
    install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$d"
    backup+=("etc/conf.d/$d")
  done

  # create /run/quagga directory at startup
  install -D -m 644 /dev/null "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  echo "d /run/$pkgname 0750 $pkgname $pkgname" > "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"

  # soft
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 ft=sh et:
