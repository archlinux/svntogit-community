# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgbase=quassel
pkgname=('quassel-client' 'quassel-core' 'quassel-monolithic')
pkgver=0.11.0
pkgrel=2
pkgdesc="Next-generation distributed IRC client"
arch=('i686' 'x86_64')
url="http://quassel-irc.org/"
license=('GPL')
makedepends=('cmake' 'automoc4' 'qt4' 'icu' 'kdelibs')
source=(http://quassel-irc.org/pub/$pkgbase-$pkgver.tar.bz2
        $pkgbase.service
        $pkgbase.conf)
sha256sums=('99a191b8bc2a410f7020b890ec57e0be49313f539da9f4843675bb108b0f4504'
            '828aaf27696ffe2aa4b80755a09bdf264f644d9539366981697a73bb23340d7b'
            'f3031ea8217e01ba42cea14606169e3e27affa5918968ffd5a03c21ae92fe2b8')

build() {
  cd "${srcdir}"
  [[ ! -d build ]] && mkdir build
  [[ ! -d build-client ]] && mkdir build-client
  [[ ! -d build-mono ]] && mkdir build-mono

  # Core
  cd "${srcdir}"/build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=0 \
    -DCMAKE_BUILD_TYPE="Release" \
    -DWITH_OPENSSL=ON \
    -DWANT_CORE=ON \
    -DWANT_MONO=OFF \
    -DWANT_QTCLIENT=OFF \
    -DWITH_DBUS=OFF \
    -DWITH_OXYGEN=OFF \
    -DWITH_PHONON=OFF \
    -DWITH_WEBKIT=OFF \
    -DCMAKE_BUILD_TYPE="Release" \
    ../quassel-${pkgver}/ \
    -Wno-dev
  make

  # Client
  cd "${srcdir}"/build-client
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DWITH_OPENSSL=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    -DWANT_CORE=OFF \
    -DWANT_QTCLIENT=ON \
    -DWANT_MONO=OFF \
    ../quassel-${pkgver}/ \
    -Wno-dev
  make

  # Monolithic
  cd "${srcdir}"/build-mono
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DWITH_OPENSSL=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    -DWANT_CORE=OFF \
    -DWANT_QTCLIENT=OFF \
    -DWANT_MONO=ON \
    ../quassel-${pkgver}/ \
    -Wno-dev
  make
}

package_quassel-core() {
pkgdesc="Next-generation distributed IRC client - core only"
depends=('qt4' 'icu' 'qca')
backup=(etc/conf.d/quassel)
install=quassel.install
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  # Remove unneeded pixmap icons
  rm -r "${pkgdir}"/usr/share

  install -Dm644 "${srcdir}"/$pkgbase.service \
    "${pkgdir}"/usr/lib/systemd/system/$pkgbase.service
  install -Dm644 "${srcdir}"/$pkgbase.conf \
    "${pkgdir}"/etc/conf.d/$pkgbase
}

package_quassel-client() {
pkgdesc="Next-generation distributed IRC client - client only"
depends=('qt4' 'kdebase-runtime')
conflicts=('quassel-monolithic')
install=quassel-client.install
  cd "${srcdir}"/build-client

  make DESTDIR="${pkgdir}" install
}

package_quassel-monolithic() {
pkgdesc="Next-generation distributed IRC client - monolithic"
depends=('qt4' 'kdebase-runtime')
conflicts=('quassel-client')
install=quassel-monolithic.install
  cd "${srcdir}"/build-mono

  make DESTDIR="${pkgdir}" install
}
