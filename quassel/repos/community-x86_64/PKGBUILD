# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=quassel
pkgver=0.6.1
pkgrel=1
pkgdesc="QT4 irc client with a separated core"
url="http://quassel-irc.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdelibs' 'oxygen-icons' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4')
source=("${url}/pub/${pkgname}-${pkgver}.tar.bz2"
	'rc.quassel'
        'quassel.conf')
md5sums=('569d310d06f124952009704d55a98100'
         '91e414cb313430a2360f1a594b1d6ed3'
         '479229556ba0dd94e3ce28e31e5deea9')
install=quassel.install
backup=(etc/conf.d/quassel.conf)

build() {
  [[ ! -d build ]] && mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DCMAKE_BUILD_TYPE="Release" \
    ../${pkgname}-${pkgver}/ \
    -Wno-dev

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/rc.quassel \
    ${pkgdir}/etc/rc.d/quassel
  install -D -m644 ${srcdir}/quassel.conf \
    ${pkgdir}/etc/conf.d/quassel
}
