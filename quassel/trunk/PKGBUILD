# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgbase=quassel
pkgname=('quassel-core' 'quassel-client' 'quassel-client-small' 'quassel-monolithic')
pkgver=0.12.4
pkgrel=4
pkgdesc="Next-generation distributed IRC client"
arch=('x86_64')
url="https://quassel-irc.org/"
license=('GPL')
makedepends=('qt5-base' 'qt5-tools' 'qt5-webkit' 'qca-qt5' 'qt5-script'
             'knotifyconfig' 'cmake' 'extra-cmake-modules' 'python'
             'hicolor-icon-theme')
source=(https://quassel-irc.org/pub/$pkgbase-$pkgver.tar.bz2
        0001-Fix-handling-systray-notifications.patch
        $pkgbase.service
        $pkgbase.sysusers
        $pkgbase.tmpfiles
        $pkgbase.conf)
sha256sums=('93e4e54cb3743cbe2e5684c2fcba94fd2bc2cd739f7672dee14341b49c29444d'
            '5a465f33c5f233983a12139a1a121665c4ddab9e205997daa453dcffd255156a'
            '5dbe20290f3361b9b7a74a52905137e76b656976febf2d31082a2276f9dcde7f'
            '3c72441a99e2668c6a8a751fa07beeb44f937576c8a1b5f615e4a55f841d93d9'
            '2afd4340c7713f6533e5d175a86b28fd118ecd907776c2b10925d1a4fb31cdca'
            'f3031ea8217e01ba42cea14606169e3e27affa5918968ffd5a03c21ae92fe2b8')

prepare() {
  cd "${srcdir}"
  mkdir build-{core,client,client-small,mono}
  cd quassel-${pkgver}

  # Fix handling systray notifications
  # https://github.com/quassel/quassel/pull/272
  patch -Np1 -i ../0001-Fix-handling-systray-notifications.patch
}

_build() (
  cd build-$1
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DCMAKE_BUILD_TYPE="Release" \
    -DHAVE_SSL=ON \
    -DUSE_QT5=ON \
    "${@:2}" \
    ../quassel-${pkgver}/ \
    -Wno-dev
  make
)

build() {
  _build core \
    -DWANT_QTCLIENT=OFF \
    -DWANT_MONO=OFF \
    -DWITH_OXYGEN=OFF \
    -DWITH_WEBKIT=OFF

  _build client \
    -DWANT_CORE=OFF \
    -DWANT_MONO=OFF \
    -DWITH_KDE=ON

  _build client-small \
    -DWANT_CORE=OFF \
    -DWANT_MONO=OFF \
    -DWITH_WEBKIT=OFF \
    -DCMAKE_DISABLE_FIND_PACKAGE_dbusmenu-qt5=ON \
    -DCMAKE_DISABLE_FIND_PACKAGE_Phonon4Qt5=ON

  _build mono \
    -DWANT_CORE=OFF \
    -DWANT_QTCLIENT=OFF \
    -DWITH_KDE=ON
}

package_quassel-core() {
pkgdesc="Next-generation distributed IRC client - core only"
depends=('icu' 'qca-qt5' 'qt5-script')
optdepends=('postgresql: PostgreSQL database support')
backup=(etc/conf.d/quassel)
  cd "${srcdir}"/build-core

  make DESTDIR="${pkgdir}" install

  # Remove unneeded pixmap icons
  rm -r "${pkgdir}"/usr/share

  install -Dm644 "${srcdir}"/$pkgbase.sysusers \
    "${pkgdir}"/usr/lib/sysusers.d/$pkgbase.conf
  install -Dm644 "${srcdir}"/$pkgbase.tmpfiles \
    "${pkgdir}"/usr/lib/tmpfiles.d/$pkgbase.conf
  install -Dm644 "${srcdir}"/$pkgbase.service \
    "${pkgdir}"/usr/lib/systemd/system/$pkgbase.service
  install -Dm644 "${srcdir}"/$pkgbase.conf \
    "${pkgdir}"/etc/conf.d/$pkgbase
}

package_quassel-client() {
pkgdesc="Next-generation distributed IRC client - client only"
depends=('qt5-base' 'qt5-webkit' 'knotifyconfig' 'hicolor-icon-theme')
conflicts=('quassel-monolithic' 'quassel-client-small')
  cd "${srcdir}"/build-client

  make DESTDIR="${pkgdir}" install
}

package_quassel-client-small() {
pkgdesc="Next-generation distributed IRC client - client only, reduced deps"
depends=('qt5-base' 'hicolor-icon-theme')
conflicts=('quassel-monolithic' 'quassel-client')
replaces=('quassel-remote')
  cd "${srcdir}"/build-client-small

  make DESTDIR="${pkgdir}" install
}

package_quassel-monolithic() {
pkgdesc="Next-generation distributed IRC client - monolithic"
depends=('qt5-base' 'qca-qt5' 'qt5-webkit' 'qt5-script' 'knotifyconfig' 'hicolor-icon-theme')
optdepends=('postgresql: PostgreSQL database support')
conflicts=('quassel-client' 'quassel-client-small')
  cd "${srcdir}"/build-mono

  make DESTDIR="${pkgdir}" install
}
