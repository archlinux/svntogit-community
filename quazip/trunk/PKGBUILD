# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

pkgname=quazip
pkgver=0.7.3
pkgrel=4
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package"
url="http://sourceforge.net/projects/quazip/"
license=('LGPL')
arch=('x86_64')
depends=('qt5-base')
makedepends=('cmake')
provides=('quazip-qt5')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('d4b55bd040e47e97e7bd5d080399ae4aa5fbc4985d6ab5979b468abc852c91b2e3e5d35af90d4b4de3bb01ea9135d0247473d6eab0413392b1c8567dc799a9fe')

prepare() {
  # Fix cmake module install dir
  sed -e 's|${CMAKE_ROOT}/Modules|${CMAKE_INSTALL_PREFIX}/lib/cmake/QuaZip|' -i $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cd "${srcdir}"

  install -d build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT4:BOOL=OFF \
    -DCMAKE_CXX_FLAGS="$CFLAGS -fPIC"
  make
}

package() {
  cd "${srcdir}"

  cd build
  make DESTDIR="${pkgdir}" install
}
