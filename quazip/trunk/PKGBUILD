# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

pkgname=quazip
pkgver=0.7.3
pkgrel=2
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package"
url="http://sourceforge.net/projects/quazip/"
license=('LGPL')
arch=('x86_64')
depends=('qt5-base')
makedepends=('cmake')
provides=('quazip-qt5')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('2ad4f354746e8260d46036cde1496c223ec79765041ea28eb920ced015e269b5')

build() {
  cd "${srcdir}"

  install -d build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT4:BOOL=OFF \
    -DCMAKE_CXX_FLAGS="$CFLAGS -fPIC"
  make
}

package() {
  cd "${srcdir}"

  cd build
  make DESTDIR="${pkgdir}" install
}
