# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=quodlibet
pkgver=4.3.0
pkgrel=3
pkgdesc="Music player and music library manager"
arch=('any')
license=('GPL2')
url="https://github.com/quodlibet/quodlibet"
depends=('gst-plugins-base' 'gtk3' 'python-cairo'  'python-feedparser'
'python-gobject' 'python-mutagen' 'python-senf' 'xine-lib')
makedepends=('gst-plugins-bad' 'gst-plugins-good' 'gst-plugins-ugly'
'python-pycodestyle' 'python-sphinx_rtd_theme')
checkdepends=('python-pytest' 'python-xvfbwrapper')
optdepends=('gst-libav: for WMA support'
            'gst-plugins-bad: for mp3, mp4 and opus support and acoustid plugin'
            'gst-plugins-good: for flac, jack and pulseaudio support and replaygain plugin'
            'gst-plugins-ugly: for mp3 support'
            'gtksourceview3: for undo and redo support in multiline text fields'
            'kakasi: Kana Kanji Simple Inverter plugin'
            'libappindicator-gtk3: for tray icon plugin'
            'libkeybinder3: Multimedia keys support'
            'libmodplug: for MOD support'
            'notification-daemon: for notification plugin'
            'python-dbus: for DBus interface, multimedia key support and several plugins'
            'python-musicbrainzngs: for musicrainz plugin'
            'python-pyinotify: for auto library update plugin'
            'rygel: for uPnP media server'
            'webkit2gtk: for lyrics window plugin')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/release-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('054a31206935ad6bf44420e4d03a4ed7e75523985a944ccf85a0169d2824fd9e44e1c86db75ddef572cf25d73dd5efb588a4cad15bf345cfb6d6d81c3572a10a'
            'SKIP')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC') # Christoph Reiter <reiter.christoph@gmail.com>

prepare() {
  cd "${pkgname}-${pkgver}"
  # Fix zsh completions dir
  sed -e 's|vendor-completions|site-functions|' \
      -i gdist/zsh_completions.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  # not running useless linter checks
  # skipping test_potfile_format because of a gettext 0.21 warning
  pytest -v -k 'not TPEP8 and not TPyFlakes' \
    --deselect tests/test_po.py::test_potfile_format
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {README,NEWS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
