# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=qupzilla
pkgver=2.0.2
pkgrel=1
pkgdesc="Cross-platform QtWebEngine browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qt5-webengine' 'qt5-x11extras' 'hunspell')
makedepends=('qt5-tools' 'kwallet' 'libgnome-keyring')
optdepends=('bash-completion: bash completion support' 'kwallet: kf5 kwallet integration'
              'libgnome-keyring: gnome keyring integration')
provides=('qupzilla-qt5')
conflicts=('qupzilla-qt5')
replaces=('qupzilla-qt5')
source=("$pkgname-$pkgver.tar.gz::https://github.com/QupZilla/qupzilla/archive/v$pkgver.tar.gz")
md5sums=('7036fab0c9ca3a5e5f1cc2b58147100b')

prepare() {
  # configure
  export USE_WEBGL=true \
         QUPZILLA_PREFIX=/usr/ \
         KDE_INTEGRATION=true \
         GNOME_INTEGRATION=true
}

build() {
  cd $pkgname-$pkgver
  qmake
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/_$pkgbase \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

# vim:set ts=2 sw=2 et:
