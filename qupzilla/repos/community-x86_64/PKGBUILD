# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgbase=qupzilla
pkgname=(qupzilla qupzilla-qt5)
pkgver=1.6.3
pkgrel=1
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
makedepends=('qtwebkit' 'qt5-webkit' 'qt5-script' 'hunspell' 'kdelibs' 'libgnome-keyring')
optdepends=('bash-completion: bash completion support')
install="$pkgbase.install"
source=("$pkgbase-$pkgver.tar.gz::https://github.com/QupZilla/qupzilla/archive/v$pkgver.tar.gz")
noextract=("$pkgbase-$pkgver.tar.gz")
md5sums=('37d962c924a58c616104d0cc732bf69f')

prepare() {
  # extract
  mkdir -p qt{4,5}
  bsdtar --strip-components 1 -zxf $pkgbase-$pkgver.tar.gz -C qt4
  bsdtar --strip-components 1 -zxf $pkgbase-$pkgver.tar.gz -C qt5

  # configure
  export USE_WEBGL=true \
         QUPZILLA_PREFIX=/usr/ \
         KDE_INTEGRATION=true \
         GNOME_INTEGRATION=true
}

build() {
  cd qt4
  qmake-qt4
  make

  cd ../qt5
  qmake
  make
}

package_qupzilla() {
  depends=('qtwebkit' 'hunspell')
  optdepends=('kdelibs: kwallet integration'
              'libgnome-keyring: gnome keyring integration')

  cd qt4
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/_$pkgbase \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

package_qupzilla-qt5() {
  pkgdesc+=" (Qt5)"
  depends=('qt5-webkit' 'qt5-script' 'hunspell' 'desktop-file-utils' 'hicolor-icon-theme')
  optdepends=('libgnome-keyring: gnome keyring integration')
  provides=('qupzilla')
  conflicts=('qupzilla')

  cd qt5
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/_$pkgbase \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

# vim:set ts=2 sw=2 et:
