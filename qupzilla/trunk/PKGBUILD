# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgbase=qupzilla
pkgname=(qupzilla qupzilla-qt5)
pkgver=1.4.4
pkgrel=1
pkgdesc="Cross-platform QtWebKit browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
makedepends=('qtwebkit' 'qt5-webkit' 'qt5-script' 'hunspell')
optdepends=('bash-completion: bash completion support')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/QupZilla/$pkgbase/tarball/v$pkgver")
noextract=("$pkgbase-$pkgver.tar.gz")
install="$pkgbase.install"
md5sums=('cfbf5cd23d184275c389c79a39a28631')

prepare() {
  # extract
  mkdir -p qt{4,5}
  bsdtar --strip-components 1 -zxf $pkgbase-$pkgver.tar.gz -C qt4
  bsdtar --strip-components 1 -zxf $pkgbase-$pkgver.tar.gz -C qt5

  # configure
  export USE_WEBGL=true \
         QUPZILLA_PREFIX=/usr/
}

build() {
  cd qt4
  qmake-qt4
  make

  cd ../qt5
  qmake
  make
}

package_qupzilla() {
  depends=('qtwebkit' 'hunspell')

  cd qt4
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/zsh_completion.sh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

package_qupzilla-qt5() {
  pkgdesc+=" (Qt5)"
  depends=('qt5-webkit' 'qt5-script' 'hunspell' 'desktop-file-utils' 'hicolor-icon-theme')
  provides=('qupzilla')
  conflicts=('qupzilla')

  cd qt5
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/zsh_completion.sh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

# vim:set ts=2 sw=2 et:
