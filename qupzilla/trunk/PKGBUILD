# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qupzilla
pkgver=2.2.1
pkgrel=1
pkgdesc="Cross-platform QtWebEngine browser"
arch=(x86_64)
url="https://www.qupzilla.com/"
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg)
makedepends=(qt5-tools kwallet libgnome-keyring)
optdepends=('bash-completion: bash completion support' 'kwallet: kf5 kwallet integration'
            'libgnome-keyring: gnome keyring integration')
source=(https://github.com/QupZilla/qupzilla/releases/download/v$pkgver/QupZilla-$pkgver.tar.xz)
sha256sums=('5605104e2091235607a7406d02ba55d4923e104960c4996e80bcabe61ad7e0cb')

build() {
  cd QupZilla-$pkgver
  export USE_WEBGL=true \
    QUPZILLA_PREFIX=/usr/ \
    KDE_INTEGRATION=true \
    GNOME_INTEGRATION=true
  qmake
  make
}

package() {
  cd QupZilla-$pkgver
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/_$pkgbase \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}
