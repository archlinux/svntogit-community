# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=qupzilla
pkgver=2.1.1
pkgrel=2
pkgdesc="Cross-platform QtWebEngine browser"
arch=(i686 x86_64)
url="http://www.qupzilla.com/"
license=('GPL3')
depends=('qt5-webengine' 'qt5-x11extras' 'qt5-svg' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'kwallet' 'libgnome-keyring')
optdepends=('bash-completion: bash completion support' 'kwallet: kf5 kwallet integration'
              'libgnome-keyring: gnome keyring integration')
provides=('qupzilla-qt5')
conflicts=('qupzilla-qt5')
replaces=('qupzilla-qt5')
source=("$pkgname-$pkgver.tar.gz::https://github.com/QupZilla/qupzilla/archive/v$pkgver.tar.gz" qupzilla-openssl1.1.patch)
md5sums=('ee7f041080df526d69081f8de8e87ed9'
         '005c81b2f9d45911528649b3f5530682')

prepare() {
  # configure
  export USE_WEBGL=true \
         QUPZILLA_PREFIX=/usr/ \
         KDE_INTEGRATION=true \
         GNOME_INTEGRATION=true

 # fix build with OpenSSL 1.1 (Fedora)
  cd $pkgname-$pkgver
  patch -p1 -i ../qupzilla-openssl1.1.patch
}

build() {
  cd $pkgname-$pkgver
  qmake
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/_$pkgbase \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}

# vim:set ts=2 sw=2 et:
