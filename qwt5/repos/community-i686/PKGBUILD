# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Eugen Zagorodniy <https://github.com/ezag>
# Contributor: Ronald van Haren <ronald at archlinux dot org>
# Contributor: Nick Ã˜stergaard <oe.nick at gmail dot com>

pkgname=qwt5
pkgver=5.2.3
pkgrel=4
pkgdesc="Qt Widgets for Technical Applications (version 5.X)"
arch=('i686' 'x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt4')
license=("custom")
source=("http://downloads.sourceforge.net/sourceforge/qwt/qwt-${pkgver}.tar.bz2" \
        "qwtconfig-archlinux.pri")
md5sums=('7d37a11d02bc7d095d0ca6427ec97b8d'
         '5d66906ce26a751c3b5573a36c756ddd')

prepare() {
  # copy our config file to the right place
  install -Dm644 "qwtconfig-archlinux.pri" "qwt-${pkgver}/qwtconfig.pri"
}

build() {
  cd "${srcdir}/qwt-${pkgver}"
  qmake-qt4 qwt.pro
  make
}

package() {
  cd "${srcdir}/qwt-${pkgver}"

  make INSTALL_ROOT="${pkgdir}" QTDIR=/usr install
  # install licence
  install -Dm644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # remove docs
  rm -r "${pkgdir}/usr/share/html"
  rm -r "${pkgdir}/usr/share/man/"
}
