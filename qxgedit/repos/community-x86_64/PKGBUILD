# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=0.9.7
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=(x86_64)
url="https://qxgedit.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(alsa-lib cmake qt6-tools)
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('8ccee111e22116c73fd47eeabafceb324f28683a7aee1e6e5ef136383522283c42ca55da0af50a80f86002d95b589a38c076b3237b92d650d568d93103407a50')
b2sums=('21b3cb1f365fdefb570aeb9a37ae5b21353c0fc675a52fbfeb486833ff0858ceef23fe786f89dc95012f7de8be3411d0a4a86f239da806d5d1c2d6670f0d0ac3')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
