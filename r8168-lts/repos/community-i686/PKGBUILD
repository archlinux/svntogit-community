# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.036.00
pkgrel=5
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' "linux-lts>=3.10" "linux-lts<3.11")
makedepends=("linux-lts-headers>=3.10" "linux-lts-headers<3.11")
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2
        linux-3.10.patch)
install=$pkgname.install

_extramodules=extramodules-3.10-lts

prepare() {
	cd "$_pkgname-$pkgver"
    patch -p0 -i ../linux-3.10.patch
}

build() {
	cd "$_pkgname-$pkgver"
	local _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
	make -C /usr/lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dm644 src/$_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}

sha256sums=('937fd5d7d4cbfc95cf2859d8c3beb15a8536dc4e41752f7a53f2a25f07cdcd33'
            '1fb115b4d1b6b058d23322b89fad52d29748ce75b2774815e72c679a235d0e87')
