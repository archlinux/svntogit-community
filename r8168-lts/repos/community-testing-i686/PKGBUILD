# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.039.00
pkgrel=12
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
#url="http://www.realtek.com.tw"
url="https://code.google.com/p/r8168/"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' "linux-lts>=3.14.42" "linux-lts<3.15")
makedepends=("linux-lts-headers>=3.14.42" "linux-lts-headers<3.15")
install=$pkgname.install
source=(#"ftp://WebUser:wK9xBuD5@95.130.192.218/cn/nic/r8168-$pkgver.tar.bz2"
       http://r8168dl.appspot.com/files/r8168-$pkgver.tar.bz2)
sha256sums=('767d922270274e781d8d42493a0021db1cafcb0388ac62564d0c0c3d82703edd')

_extramodules=extramodules-3.14-lts

build() {
	cd "$_pkgname-$pkgver"
	local _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
	make -C /usr/lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dm644 src/$_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
