# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.040.00
pkgrel=1
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
#url="http://www.realtek.com.tw"
url="https://code.google.com/p/r8168/"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' "linux-lts>=3.14.42" "linux-lts<3.15")
makedepends=("linux-lts-headers>=3.14.42" "linux-lts-headers<3.15")
install=$pkgname.install
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('1f7aad3a066c152837e03e558ba720742e792d281c96c5412cff32d68875415b')

_extramodules=extramodules-3.14-lts

build() {
	cd "$_pkgname-$pkgver"
	local _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
	make -C /usr/lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dm644 src/$_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
