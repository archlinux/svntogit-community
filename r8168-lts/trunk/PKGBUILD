# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.046.00
pkgrel=18
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc' "linux-lts>=4.19.34" "linux-lts<4.20")
makedepends=("linux-lts-headers>=4.19.34" "linux-lts-headers<4.20")
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('b2e66b03b696ba55c9fb7c896db8ac5377233d14120520735d31c33147d0d4c3')

_extramodules=extramodules-4.19-lts

build() {
	cd "$_pkgname-$pkgver"
	KERNEL_VERSION="$(</usr/lib/modules/$_extramodules/version)"
	make -C /usr/lib/modules/$KERNEL_VERSION/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 src/*.ko
	find "$pkgdir" -name '*.ko' -exec xz {} +

	echo "blacklist r8169" | \
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/modprobe.d/$pkgname.conf"
}
