# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
pkgver=8.051.02
pkgrel=20
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
makedepends=('linux-lts-headers')
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/r8168-$pkgver.tar.gz
        https://github.com/mtorromeo/r8168/releases/download/$pkgver/r8168-$pkgver.tar.gz.asc
        0001-Linux-6.1.patch)
sha256sums=('76f1c6f0b273d6a31bdb3e98c39a54f50a65766b99d485f9b4ddeda30dcd11d8'
            'SKIP'
            'd178c927b2619b4f5013ab965168b3f9ddc67e7cf4b32a0edca5906739032393')
validpgpkeys=('0CADAACF70F64C654E131B3111675C743429DDEF')

prepare() {
	cd "r8168-$pkgver"
	patch -Np1 -i ../0001-Linux-6.1.patch
}

build() {
	cd "r8168-$pkgver"
	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/src/linux-lts M="$PWD/src" \
		ENABLE_USE_FIRMWARE_FILE=y \
		CONFIG_R8168_NAPI=y \
		CONFIG_R8168_VLAN=y \
		CONFIG_ASPM=y \
		ENABLE_S5WOL=y \
		ENABLE_EEE=y \
		modules
}

package() {
	depends=('glibc' 'linux-lts')

	local extradir=/usr/lib/modules/$(</usr/src/linux-lts/version)/extramodules
	cd "r8168-$pkgver"
	install -Dt "$pkgdir$extradir" -m644 src/*.ko
	find "$pkgdir" -name '*.ko' -exec strip --strip-debug {} +
	find "$pkgdir" -name '*.ko' -exec xz {} +

	echo "blacklist r8169" | \
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/modprobe.d/$pkgname.conf"
}

# vim:set sw=0 noet:
