# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168
pkgver=8.028.00
pkgrel=5
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux>=3.3' 'linux<3.4')
makedepends=('linux-headers')
source=(http://r8168.googlecode.com/files/$pkgname-$pkgver.tar.bz2 linux-3.3-fix.patch)
install=$pkgname.install

_extramodules=extramodules-3.3-ARCH
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p0 -i "$srcdir/linux-3.3-fix.patch"
	make clean SYSSRC=/usr/src/linux-$_kernver modules
}

package() {
	cd "$srcdir/$pkgname-$pkgver/src"
	install -D -m644 $pkgname.ko "$pkgdir/lib/modules/$_extramodules/$pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('4cc3c1706f00b55ab9bd6f5e06b758c2'
         'ee7e3ef155f0c88ae2dbb137d2e03b25')
sha256sums=('c77873f43693eea5304e17e35ff3d6503f6581e358d28c38927573ea56f4d6a3'
            'f93dd283d4dfbeda43a32fb3a84016b680eca79f0f4e5df00d84e9989edff69a')
