# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.7.15
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rabbitmq/rabbitmq-management/archive/v${pkgver}.tar.gz)
sha256sums=('f42fa8eccdbf8c9f68ff093d3649817f87bb0a2ffb9a5bdf49a9c8a03b86dcaf')
sha512sums=('7d75aba0eefdf25791da3880de2925ad75c21292babbeee36e9ffcb3d5162c11fa4ecdea072654fb445fc8a63c1a25cf5580c07a5f52ebfb82249e8a77e2b62f')

prepare() {
  cd rabbitmq-management-${pkgver}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-${pkgver}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
