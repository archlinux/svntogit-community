# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.8.1
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(https://github.com/rabbitmq/rabbitmq-management/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c05fab95ed176849a4ecd491f3d0e6fb3380706871be23fdd5bbdb2e59bec15f')
sha512sums=('a21a3af8e59a7bb977df0581f10fbf1bca54df617f803b8e3e16ae78a9f215f0c3a80d228808d5fe45539e2a80a9457f9e3b6c097e77cf7e8a599f34000f93fb')

prepare() {
  cd rabbitmq-management-${pkgver}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-${pkgver}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
