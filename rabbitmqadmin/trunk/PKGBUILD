# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.8.3
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(https://github.com/rabbitmq/rabbitmq-management/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2d87387ec45a81147f4de0501f3d434f13afaf5d00b99436243d835f336a1ea6')
sha512sums=('954853cf9734bb52b7767d1534823b0afa5f03e291f6c9dfe69285191f96e99cda44e75403e76df2ba1bd440161ddd0a11a92b440c2a2b4c3697c867f7c9b916')

prepare() {
  cd rabbitmq-management-${pkgver}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-${pkgver}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
