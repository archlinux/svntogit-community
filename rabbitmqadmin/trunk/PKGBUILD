# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=rabbitmqadmin
pkgver=3.8.2
pkgrel=1
pkgdesc='Command-line tool for managing RabbitMQ server (requires "management" plugin)'
url='https://www.rabbitmq.com/management-cli.html'
arch=('any')
license=('MPL')
depends=('python')
source=(https://github.com/rabbitmq/rabbitmq-management/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7a053703463dd2d8fa8e5536ce35b28b03c929580519ad7ec5e41af2c7d0448f')
sha512sums=('c511fd576ed0906994f5520e5b1a3db41244e7ede05753e22263001674b05f5acf65ed39c59ee9f1cf776eabde4dfba99d892ccfa58f7a74fd9581bcb00ab3fc')

prepare() {
  cd rabbitmq-management-${pkgver}
  sed -e "s|%%VSN%%|${pkgver}|" -i bin/rabbitmqadmin
}

package() {
  cd rabbitmq-management-${pkgver}
  install -Dm 755 bin/rabbitmqadmin -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE-MPL-RabbitMQ -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
