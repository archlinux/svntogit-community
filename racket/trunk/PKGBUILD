# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=racket
pkgname=(racket racket-docs)
pkgver=6.5
pkgrel=1
pkgdesc="A full-spectrum language with DrRacket IDE.  Formerly known as PLT Scheme."
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('GPL3' 'LGPL3' 'custom')
depends=('gtk3')
makedepends=('gsfonts' 'sqlite')
options=('!strip' '!emptydirs')
source=("http://download.racket-lang.org/installers/${pkgver}/${pkgname}-${pkgver}-src.tgz")
sha1sums=('d8390e91813de13e59ef56e857fb8a52cc92b2f8')

# possible split packages:
# -docs (180MB of 416MB)
# -minimal (no DrRacket ide)
# (ugh, the -minimal tarball uses the SAME extract path)

prepare() {
  echo "Icon=drracket" >> ${pkgname}-${pkgver}/share/pkgs/drracket/drracket/drracket.desktop
}

build() {
  cd "${pkgbase}-${pkgver}/src"
  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared
  make
}

package_racket() {
  cd "${pkgbase}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING-libscheme.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 ../share/pkgs/drracket/drracket/drracket.desktop "${pkgdir}/usr/share/applications/drracket.desktop"
  install -d "${pkgdir}"/usr/share/icons/hicolor/{16x16,32x32,48x48,256x256}/apps
  ln -s /usr/share/racket/pkgs/icons/plt-16x16.png "${pkgdir}/usr/share/icons/hicolor/16x16/apps/drracket.png"
  ln -s /usr/share/racket/pkgs/icons/plt-32x32.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/drracket.png"
  ln -s /usr/share/racket/pkgs/icons/plt-48x48.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/drracket.png"
  ln -s /usr/share/racket/pkgs/icons/plt-logo-red-diffuse.png "${pkgdir}/usr/share/icons/hicolor/256x256/apps/drracket.png"

  # sloppy
  rm -rf "${pkgdir}/usr/share/doc/"
}

package_racket-docs() {
  arch=('any')
  depends=()
  cd "${pkgbase}-${pkgver}/src"
  # there is an "install-html" target
  # but it doesn't do anything with /usr/share/docs
  make DESTDIR="${pkgdir}" install
  find "$pkgdir" -not -regex '.*share/doc.*' \
    -not -name 'share' -not -name 'usr' -not -name 'racket-docs' -delete

  install -Dm644 COPYING-libscheme.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
