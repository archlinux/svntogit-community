# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=radare2-cutter
_gitcommit=b4535227cf1ebfb94da40c950215512acbcbb9bf
pkgver=1.9.0
epoch=1
pkgrel=5
pkgdesc='Qt and C++ GUI for radare2 reverse engineering framework'
url='https://github.com/radareorg/cutter'
arch=('x86_64')
license=('GPL3')
depends=('radare2' 'capstone' 'qt5-base' 'qt5-svg' 'qt5-webengine' 'icu' 'python' 'jupyter'
         'pyside2' 'python-shiboken2')
makedepends=('git' 'cmake' 'shiboken2')
source=(${pkgname}::"git+https://github.com/radareorg/cutter#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  sed -e 's|PKGCONFIG += python3|PKGCONFIG += python3-embed|' -i $pkgname/src/Cutter.pro # Fix linking to python 3.8
}

build() {
  mkdir -p ${pkgname}/build
  cd ${pkgname}/build
  qmake ../src/Cutter.pro \
    CUTTER_ENABLE_PYTHON=true \
    CUTTER_ENABLE_PYTHON_BINDINGS=true \
    CUTTER_USE_BUNDLED_RADARE2=false
  make
}

package() {
  cd ${pkgname}
  install -Dm 755 build/Cutter -t "${pkgdir}/usr/bin"
  install -Dm 644 src/org.radare.Cutter.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm 644 src/img/cutter.svg -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
