# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=4.3.1
pkgrel=2
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib'
         'lz4' 'xxhash' 'libxxhash.so' 'file' 'libuv')
makedepends=('meson')
optdepends=('r2ghidra-dec: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/radare/radare2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4abcb9c9dff24eab44d64d392e115ae774ab1ad90d04f2c983d96d7d7f9476aa')
sha512sums=('06009dd2c75cfb2fcadd21041861f9569ff4b0b9438d1d5a1cf49ac8690ef041e18feb2445e6772acd0e217110328660e9de47db54d55c64f64764cb06988db8')

build() {
  cd ${pkgname}-${pkgver}
  touch libr/config.mk
  arch-meson build \
    -D use_sys_capstone=true \
    -D use_sys_magic=true \
    -D use_sys_zip=true \
    -D use_sys_zlib=true \
    -D use_sys_lz4=true \
    -D use_sys_xxhash=true \
    -D use_sys_openssl=true \
    -D use_libuv=true \
    -D use_webui=true
  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  cp -r doc/* "${pkgdir}/usr/share/doc/radare2"
  ln -s /usr/bin/radare2 "${pkgdir}/usr/bin/r2"
  ln -s /usr/share/man/man1/radare2.1.gz "${pkgdir}/usr/share/man/man1/r2.1.gz"
}

# vim: ts=2 sw=2 et:
