# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=4.0.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib'
         'lz4' 'xxhash' 'libxxhash.so' 'file' 'libuv')
makedepends=('meson')
options=('!emptydirs')
source=(https://github.com/radare/radare2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7621e38558917dc4b469acf2aa33d7cab6414420a0a1d205c55b9c96423e9cee')
sha512sums=('3877088f61d8fac6a8fa12a35ef895904473e1db7e7428961eb06822ff1218338cd5b9ab16f78773413b31fec7463963b0cc3f801c661ce17cd40c8ddd423d76')
sha256sums=('7621e38558917dc4b469acf2aa33d7cab6414420a0a1d205c55b9c96423e9cee')
sha512sums=('3877088f61d8fac6a8fa12a35ef895904473e1db7e7428961eb06822ff1218338cd5b9ab16f78773413b31fec7463963b0cc3f801c661ce17cd40c8ddd423d76')

build() {
  cd ${pkgname}-${pkgver}
  touch libr/config.mk
  arch-meson build \
    -D use_sys_capstone=true \
    -D use_sys_magic=true \
    -D use_sys_zip=true \
    -D use_sys_zlib=true \
    -D use_sys_lz4=true \
    -D use_sys_xxhash=true \
    -D use_sys_openssl=true \
    -D use_libuv=true \
    -D use_webui=true
  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  cp -r doc/* "${pkgdir}/usr/share/doc/radare2"
  ln -s /usr/bin/radare2 "${pkgdir}/usr/bin/r2"
  ln -s /usr/share/man/man1/radare2.1.gz "${pkgdir}/usr/share/man/man1/r2.1.gz"
}

# vim: ts=2 sw=2 et:
