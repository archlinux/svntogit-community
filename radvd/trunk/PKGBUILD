# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=radvd
pkgver=1.9.1
pkgrel=1
pkgdesc='IPv6 Router Advertisement Daemon'
url='http://www.litech.org/radvd/'
license=('custom')
depends=('glibc' 'libdaemon')
arch=('i686' 'x86_64')
backup=('etc/radvd.conf' 'etc/conf.d/radvd')
source=("http://www.litech.org/radvd/dist/$pkgname-$pkgver.tar.gz"
        "$pkgname.rc"
        "$pkgname.conf"
        "$pkgname.service")
sha1sums=('8db7d69f5ca4ce3643f06866b4dbf634490f479a'
          '08dd01319682f4ef2e847d11aa375e0239c08c7a'
          '63ce41d790a0472e6b3b15928f6a71d1dbb7700d'
          '9cb6b2eaaf445fe8d17dfc50fde15709d4de7d1d')
build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-pidfile=/run/radvd.pid \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  # install default config
  install -D -m 644 radvd.conf.example "$pkgdir/etc/radvd.conf"

  # install license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"

  # install initscripts & systemd files
  install -D -m 755 ../$pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 ../$pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  install -D -m 644 ../$pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 ft=sh et:
