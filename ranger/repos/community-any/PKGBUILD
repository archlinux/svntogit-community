# Maintainer: schuay <jakob.gruber@gmail.com>
# Previous maintainer: Roman Zimbelmann <romanz@lavabit.com>

pkgname=ranger
pkgver=1.7.2
pkgrel=2
pkgdesc='A simple, vim-like file manager'
arch=('any')
url='http://ranger.nongnu.org'
license=('GPL')
depends=('python')
optdepends=(
    'python-chardet: in case of encoding detection problems'
    'sudo: to use the "run as root"-feature'
    'libcaca: for ASCII-art image previews'
    'highlight: for syntax highlighting of code'
    'atool: for previews of archives'
    'w3m: for previews of images and html pages'
    'lynx: for previews of html pages'
    'elinks: for previews of html pages'
    'poppler: for pdf previews'
    'transmission-cli: for viewing bittorrent information'
    'mediainfo: for viewing information about media files'
    'perl-image-exiftool: for viewing information about media files')
conflicts=('ranger-git')
source=("http://ranger.nongnu.org/${pkgname}-${pkgver}.tar.gz"
        "http://ranger.nongnu.org/${pkgname}-${pkgver}.tar.gz.sig")
sha256sums=('94f6e342daee4445f15db5a7440a11138487c49cc25da0c473bbf1b8978f5b79'
            '50810c22a3ac6c88dbc6a30c20feb87d401456cb9128591a16e1f1b7582224e4')
validpgpkeys=('1E9B36EC051FF6F7FFC969A7F08CE1E200FB5CDF') # huterich

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Replace "python" with the python binary you want ranger to use!
  # (python 2.6 and >=3.1 supported, below 2.6 will certainly not work.)
  python setup.py -q install --root="${pkgdir}" --optimize=1
}
