# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Previous maintainer: Roman Zimbelmann <romanz@lavabit.com>

pkgname=ranger
pkgver=1.9.2
pkgrel=1
pkgdesc='Simple, vim-like file manager'
arch=('any')
url='https://ranger.github.io'
license=('GPL')
depends=('python')
optdepends=(
    'atool: for previews of archives'
    'elinks: for previews of html pages'
    'ffmpegthumbnailer: for video previews'
    'highlight: for syntax highlighting of code'
    'libcaca: for ASCII-art image previews'
    'lynx: for previews of html pages'
    'mediainfo: for viewing information about media files'
    'odt2txt: for OpenDocument texts'
    'perl-image-exiftool: for viewing information about media files'
    'poppler: for pdf previews'
    'python-chardet: in case of encoding detection problems'
    'sudo: to use the "run as root"-feature'
    'transmission-cli: for viewing bittorrent information'
    'w3m: for previews of images and html pages')
checkdepends=('python-pytest')
source=(https://ranger.github.io/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('0e1d1b1d3f78c227a6cfa783822e98591ca76a35c643d4814f40f73515d66b8a'
            'SKIP')
validpgpkeys=('1E9B36EC051FF6F7FFC969A7F08CE1E200FB5CDF'
              '66FA95C0F1619BDA520A41F60D63346A5D15D055') # huterich

check() {
  cd ${pkgname}-${pkgver}
  make test_pytest
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
}

# vim: ts=2 sw=2 et:
