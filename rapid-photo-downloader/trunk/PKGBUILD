# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Marc Moody <marc.r.moody at gmail dot com>
# Contributor: Dinh Bowman <dinh at vagueindustries dot com>

pkgname=rapid-photo-downloader
pkgver=0.4.9
pkgrel=1
pkgdesc="Photo and video downloader"
arch=('any')
url="http://www.damonlynch.net/rapid/"
license=('GPL3')
depends=('python2' 'gnome-python' 'python2-notify' 'pygtk' 'python2-rsvg' 'python2-imaging' 'python2-exiv2' 'python2-gconf' 'dbus-python' 'perl-image-exiftool')
optdepends=('kaa-metadata: Enable video downloading'
            'ffmpegthumbnailer: Enable video thumbnails'
            'hachoir-metadata: Enable video downloading')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://launchpad.net/rapid/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
sha256sums=('fde91a89c11a614ba110b1509b5ef4fee2380b13a2114d0e142addad75fd4d3b')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  python2 setup.py build
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  python2 setup.py install --root=${pkgdir}
}
