# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: AqD <aquila.deus@gmail.com>
# Contributor: TheMadsen <arch@themadsens.dk>

pkgname=rapidsvn
pkgver=0.12.0
pkgrel=5
pkgdesc='Cross-platform GUI front-end for the Subversion revision system written in C++ using the wxWidgets framework'
url='http://rapidsvn.tigris.org/'
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool')
depends=('subversion' 'wxgtk')
makedepends=('python2' 'automake' 'libtool')
#source=(http://www.rapidsvn.org/download/release/0.12/rapidsvn-$pkgver-1.tar.gz
source=(http://arch.p5n.pp.ru/~sergej/dl/rapidsvn-$pkgver-1svn20111221.tar.bz2
	$pkgname.desktop)
md5sums=('042173c66e980ac0773579accdcca92f'
         '349e6d9729f88868902611bb1172f134')

build() {
  cd $srcdir/$pkgname-$pkgver-1

  ./autogen.sh

  sed -i 's#src/tests/svncpp/Makefile##' configure
  [ "$NOEXTRACT" == 1 ] || ./configure --prefix=/usr --with-apu-config="`which apu-1-config`" --with-apr-config="`which apr-1-config`"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' tools/*.py
  sed -i 's|python|python2|' src/locale/Makefile

  make -k
  make DESTDIR=$pkgdir install

  install -Dm644 src/res/bitmaps/rapidsvn_32x32.xpm $pkgdir/usr/share/pixmaps/rapidsvn_32x32.xpm
  install -Dm644 ../$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
