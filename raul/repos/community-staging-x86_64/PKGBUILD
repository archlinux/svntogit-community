# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Max a.k.a. Synthead <synthead@gmail.com>
# Contributor: christhemonkey <christhemonkey at gmail dot com>

pkgname=raul
pkgver=0.8.0
pkgrel=5
pkgdesc="C++ Realtime Audio Utility Library"
arch=('i686' 'x86_64')
url="http://drobilla.net/software/raul/"
depends=('glib2')
makedepends=('boost' 'python2')
license=('GPL')
options=('!makeflags')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2"
        'raul-0.8.0-ldconfig.patch')
md5sums=('8fa71a20db81fbed5fb6516dea383ea8'
         'e86cee755a503d25d37fb2bcaaf48a9b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/raul-0.8.0-ldconfig.patch"

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 waf install --destdir="${pkgdir}"
}
