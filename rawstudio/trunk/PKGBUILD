# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: cdhotfire <cdhotfire@gmail.com>

pkgname=rawstudio
pkgver=2.0
pkgrel=6
pkgdesc="An open source raw-image converter written in GTK+"
arch=('i686' 'x86_64')
license=('GPL')
url="http://rawstudio.org/"
depends=('exiv2' 'libgphoto2' 'flickcurl' 'libjpeg' 'fftw' 'gconf' 'lensfun'
	 'raptor1' 'lcms' 'gtk2')
install=${pkgname}.install
options=('!libtool')
source=(http://rawstudio.org/files/release/${pkgname}-${pkgver}.tar.gz
	libpng15.patch)
md5sums=('b2f86b8ca6b83ad954e3104c4cb89e9b'
         '2f8b7acbe10d8096ce613cbea7986fd3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 <$srcdir/libpng15.patch
  export LDFLAGS="$LDFLAGS `pkg-config --libs gthread-2.0`"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  mkdir $pkgdir/usr/lib/rawstudio
  mv $pkgdir/usr/share/rawstudio/plugins $pkgdir/usr/lib/rawstudio
  ln -s ../../lib/rawstudio/plugins $pkgdir/usr/share/rawstudio/plugins
}
