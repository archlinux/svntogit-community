# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

pkgname=rawtherapee
pkgver=3.1.1
pkgrel=1
pkgdesc="RAW photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL')
depends=('bzip2' 'gtkmm' 'libiptcdata' 'lcms2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('mercurial' 'cmake')
install=rawtherapee.install
source=()
md5sums=()

_root="https://rawtherapee.googlecode.com/hg/"
_repo="rawtherapee"

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_repo ] ; then
    cd $_repo
    hg pull
    hg up "$pkgver"
    msg "The local files are updated."
  else
    hg clone -u "$pkgver" $_root $_repo
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir/$_repo-build" ]; then
    rm -rf "$srcdir/$_repo-build"
  fi
  mkdir "$srcdir/$_repo-build"
  cd "$srcdir/$_repo-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
     ../$_repo
  make
}

package() {
  cd "$srcdir/$_repo-build"
  make DESTDIR="$pkgdir/" install
}
