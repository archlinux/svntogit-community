# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

pkgname=rawtherapee
pkgver=5.3
pkgrel=1
epoch=1
pkgdesc="A powerful cross-platform raw image processing program"
arch=('x86_64')
url="http://www.rawtherapee.com/"
license=('GPL3')
depends=('fftw' 'gtk3' 'glibmm' 'gtkmm3' 'lcms2' 'lensfun' 'libcanberra'
         'libiptcdata' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake')
source=("http://rawtherapee.com/shared/source/rawtherapee-$pkgver.tar.xz")
sha1sums=('619b06c0a86f6f88c3f7196bedc71f75ab7a87ea')


build() {
  mkdir -p "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCACHE_NAME_SUFFIX=""
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}


# vim:set ts=2 sw=2 et:
