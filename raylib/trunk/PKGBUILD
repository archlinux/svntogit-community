# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wilhem Barbier <wilhem.b@free.fr>

pkgname=raylib
pkgver=2.6.0
pkgrel=1
pkgdesc='Simple and easy-to-use game programming library'
arch=(x86_64)
url='https://www.raylib.com'
license=(ZLIB)
depends=(glfw)
makedepends=(cmake git libx11 libxcursor libxinerama libxrandr ninja vulkan-headers xorg-server-devel xorg-xinput)
source=("git+https://github.com/raysan5/raylib#commit=6f3c99a295533e41de3049db5e683d15fd5c6e1a") # 2.6.0
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname" \
    -Wno-dev \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -D OpenGL_GL_PREFERENCE=GLVND \
    -D USE_EXTERNAL_GLFW=ON \
    -D BUILD_EXAMPLES=OFF \
    -D BUILD_GAMES=OFF \
    -D SHARED=ON \
    -D STATIC=OFF \
    -D WITH_PIC=ON \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  
  # Also package header files
  for f in build/src/*.h; do
    install -Dm 644 "$f" "$pkgdir/usr/include/$(basename $f)"
  done
  
  # License
  install -Dm 644 "$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/raysan5/raylib/releases
# vim: ts=2 sw=2 et:
