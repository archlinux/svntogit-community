# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wilhem Barbier <wilhem.b@free.fr>

pkgname=raylib
pkgver=4.0.0
pkgrel=2
pkgdesc='Simple and easy-to-use game programming library'
arch=(x86_64)
url='https://www.raylib.com'
license=(ZLIB)
depends=(glfw)
makedepends=(cmake git libx11 libxcursor libxinerama libxrandr ninja vulkan-headers xorg-server-devel xorg-xinput)
source=("git+https://github.com/raysan5/raylib#commit=0851960397f02a477d80eda2239f90fae14dec64") # tag: 4.0.0
b2sums=('SKIP')

build() {
  cmake \
    -B build \
    -D BUILD_EXAMPLES=OFF \
    -D BUILD_SHARED_LIBS=ON \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -D CMAKE_INSTALL_LIBDIR=lib \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D OpenGL_GL_PREFERENCE=GLVND \
    -D USE_EXTERNAL_GLFW=ON \
    -D WITH_PIC=ON \
    -G Ninja \
    -S $pkgname \
    -Wno-dev
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm 644 "$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/raysan5/raylib/releases
