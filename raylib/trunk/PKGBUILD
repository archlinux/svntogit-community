# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wilhem Barbier <wilhem.b@free.fr>

pkgname=raylib
pkgver=2.5.0
pkgrel=1
pkgdesc='Simple and easy-to-use game programming library'
arch=(x86_64)
url='https://www.raylib.com'
license=(ZLIB)
depends=(glfw)
makedepends=(cmake git libx11 libxcursor libxinerama libxrandr ninja vulkan-headers xorg-server-devel xorg-xinput)
source=("git+https://github.com/raysan5/raylib#tag=$pkgver")
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DUSE_EXTERNAL_GLFW=ON \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_GAMES=OFF \
    -DSHARED=ON \
    -DSTATIC=OFF \
    -DWITH_PIC=ON \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  # Also package header files
  for f in build/src/*.h; do
    install -Dm644 "$f" "$pkgdir/usr/include/$(basename $f)"
  done
  # License
  install -Dm644 "$pkgname/LICENSE.md" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  # Fix the pkg-config file
  sed -i 's/: glfw/: glfw3/g' "$pkgdir/usr/lib/pkgconfig/$pkgname.pc"
}

# getver: github.com/raysan5/raylib/releases
# vim: ts=2 sw=2 et:
