# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=rblcheck
pkgver=1.5
pkgrel=2
pkgdesc='Tool to query RBL servers'
arch=('i686' 'x86_64')
url='http://rblcheck.sourceforge.net/'
license=('GPL2')
depends=('glibc' 'bash')
backup=('etc/rblcheckrc')
source=("http://downloads.sourceforge.net/rblcheck/$pkgname-$pkgver.tar.gz"
        '01-change-filename-tags-for-directories.patch'
        '02-fix-configure.patch'
        '03-custom-rbl.patch'
        '04-fix-missing-include.patch')
md5sums=('fb7ee9adc0e09eee9dda195f9b9e7ca4'
         '9e8ed44f4bc6abf235a14065b6aa373c'
         '2db00ffd71e17753e89c0968c76d5871'
         'f9ff2de975584f71f7794550691a0272'
         'ccc95fb87a4192d4a0ddb36cd5df0a36')

prepare() {
  cd $pkgname-$pkgver
  for p in "$srcdir"/*.patch; do
    msg2 "Apply patch ${p##*/}"
    patch -p 1 -i "$p"
  done
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm 644 /dev/null "$pkgdir/etc/rblcheckrc"
}

# vim:set ts=2 sw=2 et:
