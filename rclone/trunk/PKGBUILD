# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rclone
pkgver=1.48.0
pkgrel=1
pkgdesc="Sync files to and from Google Drive, S3, Swift, Cloudfiles, Dropbox and Google Cloud Storage"
arch=('x86_64')
url="https://rclone.org/"
license=('MIT')
depends=('glibc')
makedepends=('python2' 'pandoc' 'go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ncw/rclone/archive/v$pkgver.tar.gz")
sha512sums=('038a72cc386c427646d2a4765664e42679fd647a878d44148a0019c8164ec60f1a2788ae0d228b166c3245fc1df6f0045c681e51e8bf2b06ef74215984dd97b5')

prepare() {
  cd rclone-$pkgver

  sed -i "1s/python$/&2/" bin/make_manual.py bin/make_backend_docs.py

  mkdir -p .gopath/src/github.com/ncw
  ln -sf "$PWD" .gopath/src/github.com/ncw/rclone
  export GOPATH="$PWD/.gopath"

  go get github.com/ncw/rclone
}

build() {
  cd rclone-$pkgver/.gopath/src/github.com/ncw/rclone

  PATH="$GOPATH/bin:$PATH" make TAG=v$pkgver rclone rclone.1 MANUAL.html MANUAL.txt

  ./rclone genautocomplete bash rclone.bash_completion
  ./rclone genautocomplete zsh rclone.zsh_completion
}

check() {
  cd rclone-$pkgver/.gopath/src/github.com/ncw/rclone
  make TAG=v$pkgver test || warning "Tests failed"
}

package() {
  cd rclone-$pkgver/.gopath/src/github.com/ncw/rclone
  make TAG=v$pkgver DESTDIR="$pkgdir" install

  install -Dm644 rclone.bash_completion "$pkgdir"/usr/share/bash-completion/completions/rclone
  install -Dm644 rclone.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_rclone

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  install -Dm644 rclone.1 "$pkgdir"/usr/share/man/man1/rclone.1
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -t "$pkgdir"/usr/share/doc/$pkgname -m644 MANUAL.html MANUAL.txt
}
