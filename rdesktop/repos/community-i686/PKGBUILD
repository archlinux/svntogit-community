# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.8.3
pkgrel=3
pkgdesc="An open source client for Windows Remote Desktop Services"
arch=('i686' 'x86_64')
url="http://www.rdesktop.org/"
license=('GPL3')
depends=('libao' 'libsamplerate' 'xorg-xrandr' 'pcsclite' 'libgssglue')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        rdesktop-send_physical_buttons.diff
        bd6aa6acddf0ba640a49834807872f4cc0d0a773.patch
        c6e8e1074b8ac57de6c80c4e3ed38e105b4d94f1.patch
        )
md5sums=('86e8b368a7c715e74ded92e0d7912dc5'
         'cbfb12729e7f28e497afb883cc42022b'
         '5682991379199228a296b34ba51942a4'
         '940db07a63df082fbbf3cb509a7c42aa')

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"
  patch -Np1 -i ${srcdir}/bd6aa6acddf0ba640a49834807872f4cc0d0a773.patch
  patch -Np1 -i ${srcdir}/c6e8e1074b8ac57de6c80c4e3ed38e105b4d94f1.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
