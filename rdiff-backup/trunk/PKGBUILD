# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>
# Contributor: Erwin Van de Velde <erwin.vandevelde@gmail.com>

pkgname=rdiff-backup
pkgver=1.2.8
pkgrel=7
pkgdesc='A utility for local/remote mirroring and incremental backups.'
arch=('x86_64')
url='http://www.nongnu.org/rdiff-backup/'
license=('GPL')
depends=('python2' 'librsync')
optdepends=('pylibacl: access control list support'
            'pyxattr: extended attribute support')
source=("http://savannah.nongnu.org/download/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        rdiff-backup-1.2.8-librsync-1.0.0.patch)
md5sums=('1a94dc537fcf74d6a3a80bd27808e77b'
         'SKIP'
         '5e54a67845edd6942fcf7359c921e003')
validpgpkeys=('B572DE9BC6537B9D5A4735B114C2601BDCA52872') # Andrew Ferguson

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../rdiff-backup-1.2.8-librsync-1.0.0.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --root="${pkgdir}" -O1

  # Fix for FS#18848
  sed -i 's|python2|python2 -W ignore::DeprecationWarning|' "${pkgdir}/usr/bin/rdiff-backup"
}
