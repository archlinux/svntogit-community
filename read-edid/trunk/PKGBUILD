# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: LaraCraft304 <lara@craft.net.br>
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=read-edid
pkgver=3.0.2
pkgrel=2
pkgdesc="Program that can get information from a PNP monitor"
arch=('x86_64')
url="http://www.polypux.org/projects/read-edid/"
license=('GPL')
depends=('libx86>=0.99')
makedepends=('cmake')
source=("http://www.polypux.org/projects/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('016546e438bf6c98739ff74061df9854')

build() {
    # bug in 3.0.2
    pushd $pkgname-$pkgver
    sed -i 's/COPYING/LICENSE/' CMakeLists.txt
    popd

    mkdir -p $pkgname-build
    cd $pkgname-build
    cmake "$srcdir/$pkgname-$pkgver" -DCMAKE_INSTALL_PREFIX="/usr"
    make
}

package() {
    cd $pkgname-build
    make DESTDIR="$pkgdir/" install
}
