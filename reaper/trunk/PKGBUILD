# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=reaper
pkgver=6.60
pkgrel=1
pkgdesc="A complete digital audio production application for computers"
arch=(x86_64)
url="https://reaper.fm"
license=(custom)
groups=(pro-audio)
depends=(alsa-lib libasound.so fontconfig gcc-libs hicolor-icon-theme libglvnd libx11 libxi)
makedepends=(kservice xdg-utils)
optdepends=(
  'jack: for JACK support'
  'pulseaudio: for PulseAudio support'
)
provides=(lv2-host vst-host vst3-host)
source=(
  https://reaper.fm/files/${pkgver::1}.x/reaper${pkgver//.}_linux_$CARCH.tar.xz
  redistribution.eml
)
sha512sums=('86404edcaba803b99cdfc6bbf141dcff9e5d471a53a0075bd1089b8428a24caf35dfb901d44bd8436a2efd03e61baeb97634afc548dbf41e0f29d5ec91af4253'
            'a2746fa0b847dbd587f24983569b01120f4706306242223c8857360219569d965df8c25c98f31d68140eb66d7458384653b5b042f7433a84957a990d8e587d92')
b2sums=('6ad0d1245a743c028c0d07c5cfb8251cf4f1fd4de1008cb5930f022d4012b262c3809f16f962b25e35c5dd3a8bcf14c4786a1b78b55b9400909705c6a4f8acfa'
        'ddbf47cb04ad9e9fb937b09a0ea0c88d4416ceaabc4e420750ff9303447a50a323060f018bd9f52907a1a5e4ea3ebe685eb336d1b6f4bbbc89a12e2370be8728')

package() {
  cd ${pkgname}_linux_$CARCH
  XDG_DATA_HOME="$srcdir/xdg/" ./install-reaper.sh --install "$pkgdir/usr/lib" --integrate-user-desktop
  # remove unneeded uninstallation script
  rm -v "$pkgdir/usr/lib/${pkgname^^}/uninstall-reaper.sh"

  # strip $pkgdir from XDG desktop Exec line
  sed -e "s|$pkgdir||g" -i "$srcdir/xdg/applications/"*.desktop
  # install only required files for XDG integration
  install -vDm 644 "$srcdir/xdg/applications/"*.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 "$srcdir/xdg/mime/packages/"*.xml -t "$pkgdir/usr/share/mime/packages/"
  install -vDm 644 "$srcdir/xdg/icons/hicolor/256x256/apps/"*.png -t "$pkgdir/usr/share/icons/hicolor/256x256/apps/"

  # add convenience symlink to PATH
  install -vdm 755 "$pkgdir/usr/bin"
  ln -sv /usr/lib/${pkgname^^}/$pkgname "$pkgdir/usr/bin/$pkgname"

  # install EULA and redistribution allowance
  install -vDm 644 "$pkgdir/usr/lib/${pkgname^^}/EULA.txt" -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 "$srcdir/redistribution.eml" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
