# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bartosz Fe≈Ñski <fenio@debian.org>

pkgname=reaver
pkgver=1.6.1
pkgrel=1
pkgdesc="Brute force attack against Wifi Protected Setup"
arch=('i686' 'x86_64')
url="https://github.com/t6x/reaver-wps-fork-t6x"
license=('GPL2')
depends=('libpcap')
source=("$pkgname-$pkgver.tar.gz::https://github.com/t6x/reaver-wps-fork-t6x/archive/v$pkgver.tar.gz")
sha512sums=('08f2d37f4375896d28eddc0deca2cf3487bd6c0a75c3d647a9adf3828f530d16b2d87a33f2ca4f56049044f7def5de73759b57bf7492d5ccc45548cd0fecf188')

build() {
  cd reaver-wps-fork-t6x-$pkgver/src
  ./configure --prefix=/usr --sysconfdir=/etc
  make CONFDIR=/etc/reaver
}

package() {
  cd reaver-wps-fork-t6x-$pkgver

  install -Dm755 src/reaver "$pkgdir"/usr/bin/reaver
  install -Dm755 src/wash "$pkgdir"/usr/bin/wash
  install -dm755 "$pkgdir"/etc/reaver
  install -Dm644 docs/README "$pkgdir"/usr/share/doc/reaver/README
  install -Dm644 docs/reaver.1.gz "$pkgdir"/usr/share/man/man1/reaver.1.gz
}
