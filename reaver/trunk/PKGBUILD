# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartosz Fe≈Ñski <fenio@debian.org>

pkgname=reaver
pkgver=1.6.3
pkgrel=1
pkgdesc='Brute force attack against Wifi Protected Setup'
url='https://github.com/t6x/reaver-wps-fork-t6x'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'libpcap')
source=("$pkgname-$pkgver.tar.gz::https://github.com/t6x/reaver-wps-fork-t6x/archive/v$pkgver.tar.gz")
sha512sums=('4e1ec1e54a96c7e5296ae66adc4d4723c0ea4ab8f98941ef5174ab5b3a2806224403a279226fe72fa7a23025761ba3347b1043a049ffefb933f28dedf816ac21')

prepare() {
  cd reaver-wps-fork-t6x-$pkgver
  sed 's|CFLAGS=|CFLAGS+=|' -i src/Makefile
}

build() {
  cd reaver-wps-fork-t6x-$pkgver/src
  ./configure --prefix=/usr --sysconfdir=/etc
  make CONFDIR=/etc/reaver
}

package() {
  cd reaver-wps-fork-t6x-$pkgver
  install -Dm755 src/{reaver,wash} -t "$pkgdir"/usr/bin
  install -dm755 "$pkgdir"/etc/reaver
  install -Dm644 docs/README -t "$pkgdir"/usr/share/doc/reaver
  install -Dm644 docs/reaver.1.gz -t "$pkgdir"/usr/share/man/man1
}
