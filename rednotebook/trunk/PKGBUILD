# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: SanskritFritz (gmail)
# Contributor: Eugenio M. Vigo <emvigo@gmail.com>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Dmitry Korzhevin <dkorzhevin at gmail dot com>
# Contributor: 4javier <4javier4@gmail.com>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>
# Contributor: cameel <cameel2/gmail/com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=rednotebook
pkgver=2.21
pkgrel=2
pkgdesc="Modern desktop diary and personal journaling tool that lets you format, tag and search your entries"
arch=(any)
url="https://rednotebook.sourceforge.io/"
license=(GPL2)
depends=(gtksourceview3 python-gobject python-pyenchant python-yaml webkit2gtk)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('a19f54ce8b60d7fad08b2d1d3b062e184721c226138b029d6b605880d04c54fa')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0 
  python3 setup.py install --root="$pkgdir" --optimize=1

  for _res in 16 22 32 48 64 128 192 256; do
    install -Dm644 rednotebook/images/rednotebook-icon/rn-$_res.png \
      "$pkgdir/usr/share/icons/hicolor/${_res}x${_res}/apps/$pkgname.png"
  done
}
