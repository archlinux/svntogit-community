# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=redo-sh
pkgver=4.0.4
pkgrel=2
pkgdesc="An implementation of the Redo build system in Bash"
arch=('any')
url="http://news.dieweltistgarnichtso.net/bin/redo-sh.html"
license=('AGPL3')
depends=('bash')
makedepends=('git')
optdepends=('graphviz: dependency graph support via redo-dot')
provides=('redo')
conflicts=('redo-python' 'redo-jdebp' 'redo-c')
_commit='039767509a6b52707f9fc28e8dc16c2da294b92b'
source=("$pkgname::git+http://news.dieweltistgarnichtso.net/bin.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^redo-v//'
}

prepare() {
  cd "$pkgname"

  # description of each expression:
  # 1 - strip executable components in middle of file
  # 2 & 3 - remove shebang & subsequent 'cat' invocation
  # 4 - remove html comments
  # boom, we now have a valid HTML file
  sed -i \
    -e '/^EOF/,/^cat <<EOF/d' \
    -e '/#!\/bin\/sh/d' \
    -e '/cat <<EOF/d' \
    -e '/^<!--/,/^-->/d' \
    "redo-sh.do"
}

package() {
  cd "$pkgname"

  # scripts
  find . \
    -maxdepth 1 \
    -executable \
    -type f \
    -name "redo*" \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # man pages
  find . \
    -maxdepth 1 \
    -type f \
    -name "redo*.1" \
    -exec install -vDm644 -t "$pkgdir/usr/share/man/man1" "{}" +

  # documentation
  install -vDm644 "redo-sh.do" "$pkgdir/usr/share/doc/$pkgname/index.html"
}

