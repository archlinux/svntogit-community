# $Id$
# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=rekonq
pkgver=2.4.0
pkgrel=1
pkgdesc='A WebKit based web browser for KDE'
arch=('i686' 'x86_64')
url='http://rekonq.kde.org/'
license=('GPL')
depends=('kdebase-keditbookmarks' 'qoauth')
makedepends=('cmake' 'automoc4')
optdepends=('kdebase-konqueror: search engines support'
            'qca-ossl: to use the sync handler')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('660f40fe612b6706623977d8cdb94b49aaacdb53aac2689d95f02d782ce2715b')

prepare() {
  mkdir build
}

build(){
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
