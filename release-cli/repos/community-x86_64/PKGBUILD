# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=release-cli
pkgver=0.13.0
pkgrel=1
pkgdesc="Gitlab Release CLI"
arch=(x86_64)
url="https://gitlab.com/gitlab-org/release-cli"
license=(MIT)
depends=(glibc)
makedepends=(go)
source=($pkgname-$pkgver.tar.gz::https://gitlab.com/gitlab-org/$pkgname/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('4133eee8365aafecc4ae5ccd886d6003424ec65ced5fa51cbdacc10ca172305af33ea18463247df8d42f0c37705c1c398ad0d91efdaf61912bce45441909f880')
b2sums=('2b7303fd65b5cd5894d902edcbfc0f9e06c95ef90d1c08f9843dc7021394889dc2ba2d8c020a1901cc412d94216c400f4ac146786b4297e5866f5d3a53f10f49')

prepare() {
  mkdir -vp $pkgname-v$pkgver/build
}

build() {
  cd $pkgname-v$pkgver
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o build/$pkgname ./cmd/$pkgname
}

package() {
  install -vDm 755 $pkgname-v$pkgver/build/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname-v$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-v$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  cd $pkgname-v$pkgver/docs
  find . -type f -iname "*.md" -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
}
