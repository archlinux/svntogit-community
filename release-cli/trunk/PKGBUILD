# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=release-cli
pkgver=0.10.0
pkgrel=1
pkgdesc="Gitlab Release CLI"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/release-cli"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/gitlab-org/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('1b422e98fbc45589941e686e2f31b1a3844cf150568d178a10b1cb80ac114114117f8f81c5dc0a6293dd83a04c9977e79302dfe1283c1a792bf4790b8f3bf895')
b2sums=('917b901afbc62bfe1e6aa3acabab31c9cd80b27c4964e30537ef02572fceed1c3482a824ccd2ac01bb2eabde3254c0169e3f46a908e4c617457b5564565c7df1')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -vp build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o "build/${pkgname}" "./cmd/${pkgname}"
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 755 "build/${pkgname}" -t "${pkgdir}/usr/bin"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cd docs
  find . -type f -iname "*.md" -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
