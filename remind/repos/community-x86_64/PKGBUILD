# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Sebastian Sontberg <ssontberg@h3c.de>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=remind
pkgver=4.0.3
_pkgver=04.00.03
pkgrel=1
pkgdesc='A sophisticated calendar and alarm program.'
arch=('x86_64')
url='https://dianne.skoll.ca/projects/remind/'
license=('GPL')
optdepends=('tk: for tkremind')
source=("https://dianne.skoll.ca/projects/$pkgname/download/$pkgname-$_pkgver.tar.gz"{,.sig})
options=('!emptydirs')
md5sums=('4360fd4e56eb689315168cceda2a254c'
         'SKIP')
validpgpkeys=('738E4D954052902C147D07B2685A5A5E511D30E2') # Dianne Skoll

build() {
  cd "${pkgname}-${_pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
