# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
epoch=1
pkgver=1.2.0rcgit.11
pkgrel=2
pkgdesc="remote desktop client written in GTK+"
arch=(i686 x86_64)
url="http://www.remmina.org/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3'
	 'libgcrypt' 'libxdmcp' 'libsecret' 'webkit2gtk')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
             'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib'
             'libvncserver')
optdepends=('libxkbfile'
            'freerdp'
            'telepathy-glib'
            'gnome-keyring'
            'nxproxy'
            'xorg-server-xephyr: XDMCP support'
            'libvncserver: VNC client/server support')
replaces=('remmina-plugins')
provides=('remmina-plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/Remmina/archive/${pkgver/rc/.rc}.tar.gz")
md5sums=('0074f6259d91962d32f121077a884f3e')

build() {
  cd ${srcdir}/Remmina-${pkgver/rc/.rc}/
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/Remmina-${pkgver/rc/.rc}/
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/build
}
