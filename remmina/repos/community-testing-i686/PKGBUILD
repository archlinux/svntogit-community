# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
pkgver=1.0.0
pkgrel=11
pkgdesc="remote desktop client written in GTK+"
arch=(i686 x86_64)
url="http://remmina.sourceforge.net/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3' 'libgcrypt' 'libxdmcp')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
	     'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib')
optdepends=('libxkbfile'
	    'freerdp'
	    'telepathy-glib'
	    'gnome-keyring'
	    'nxproxy')
replaces=('remmina-plugins')
provides=('remmina-plugins')
install=remmina.install
source=("https://github.com/downloads/FreeRDP/Remmina/Remmina-$pkgver.tar.gz"
	'https://github.com/FreeRDP/Remmina/commit/569d9bb7.patch'
	'https://github.com/FreeRDP/Remmina/commit/f7d1038b.patch'
	'https://github.com/FreeRDP/Remmina/commit/1aebfc02.patch'
	'https://github.com/FreeRDP/Remmina/commit/3ebdd6e7.patch'
	'https://github.com/FreeRDP/Remmina/commit/97c2af8c.patch'
	'https://github.com/FreeRDP/Remmina/commit/84327f81.patch'
	'https://github.com/FreeRDP/Remmina/commit/c1ef3a16.patch'
	'https://github.com/FreeRDP/Remmina/commit/6ee20289.patch'
	'https://github.com/FreeRDP/Remmina/commit/b2277827.patch'
	'https://github.com/FreeRDP/Remmina/commit/cb0b209b.patch')
md5sums=('701c540acaab961bc3adf130a2ddb8b1'
         '658218f0e63f512425893ec204e18f29'
         'dd1aad8524f470c634c4534122c1d101'
         '2cfe2dbb399cbc1b0d89606824b4280d'
         '26d06a319589bdc906422eb902ebfd17'
         'b813f7b1281d808d45da99699e17c4b7'
         '3bb434fd6c6b8ee90d98fcd7be2e307d'
         '083a34a0339f763e52f33b6dbfb0a550'
         '0fdb897dbccf2f986fc49a15f6d22bd7'
         'e891041fb3b14eb6465cc3d783e9b600'
         '59ec2f4ed9382c743db212e86ebfbdfe')

build() {
  cd ${srcdir}/FreeRDP-Remmina-*/

  for i in ${source[@]}; do
    if [ ${i:(-6)} == ".patch" ]; then
      patch -p1 -i "${srcdir}/${i:(-14)}"
    fi
  done

  export LDFLAGS="-lX11 -lgdk_pixbuf-2.0 -lgio-2.0 -lgobject-2.0 -lgmodule-2.0 -lgthread-2.0 -lpthread -lgnutls"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/FreeRDP-Remmina-*/
  make DESTDIR=${pkgdir} install
}
