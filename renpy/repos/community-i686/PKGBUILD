# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Cravix <dr.neemous@gmail.com>
# Contributor: AlexanderR <rvacheva@nxt.ru>
# Contributor: zhn <zhangn1985@gmail.com>

pkgbase=renpy
pkgname=('renpy' 'renpy-demos')
pkgver=6.99.8
pkgrel=2
pkgdesc="The Ren'Py Visual Novel Engine"
arch=('i686' 'x86_64')
url='http://www.renpy.org'
license=('MIT')
depends=('ffmpeg' 'glew' 'python2-pygame-sdl2')
makedepends=('cython2')
source=("http://www.renpy.org/dl/${pkgver}/renpy-${pkgver}-source.tar.bz2"
        'renpy'
        'renpy.desktop'
        'renpy.png'
        'renpy-ffmpeg30.patch')
sha256sums=('0eb0c763bf7e977db06039c69751f1ed5e69c4b738f7f6d975e99e8729eff58e'
            'd12cccb8d5acbe28f6740d7efc58ceb78fb4f303a50724a43c47812d51897c6a'
            'fccde3461617a098a78d938d9db782d403eda410a84ab52825a597498ab95834'
            '611edc07a40ccb8e04e8858847fc1d2a066d29c2ed54e5b357880a0605818dc5'
            'c2d27a3f6b74f874a790ce6c12e9d4b718784478d8a8aa23c879d186f60a25ab')

prepare() {
  cd renpy-${pkgver}-source

  patch -Np1 -i ../renpy-ffmpeg30.patch
  sed 's/python/python2/' -i renpy.py launcher/game/tkaskdir.py
}

build() {
  cd renpy-${pkgver}-source

  export RENPY_CYTHON='cython2'

  python2 module/setup.py build
}

package_renpy() {
  optdepends=('renpy-demos: Tutorial and The Question demos'
              'tk: Set projects directory')
  replaces=('python-renpy' 'python2-renpy')

  cd renpy-${pkgver}-source

  python2 module/setup.py install --root="${pkgdir}" --prefix='/usr' --optimize='1'

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,pixmaps,renpy,doc}}

  cp -dr --no-preserve='ownership' doc launcher renpy renpy.py templates "${pkgdir}"/usr/share/renpy/
  ln -s /usr/share/renpy/doc "${pkgdir}"/usr/share/doc/renpy

  install -m 755 ../renpy "${pkgdir}"/usr/bin/
  install -m 644 ../renpy.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../renpy.png "${pkgdir}"/usr/share/pixmaps/

  install -dm 755 "${pkgdir}"/usr/share/licenses/renpy
  install -m 644 LICENSE.txt "${pkgdir}"/usr/share/licenses/renpy/
}

package_renpy-demos() {
  depends=('renpy')

  cd renpy-${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/share/renpy

  cp -dr --no-preserve='ownership' the_question tutorial "${pkgdir}"/usr/share/renpy/

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s renpy "${pkgdir}"/usr/share/licenses/renpy-demos
}

# vim: ts=2 sw=2 et:
