# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=repo
pkgver=1.13.8
pkgrel=1

pkgdesc="The Multiple Git Repository Tool from the Android Open Source Project"
url="https://android.googlesource.com/tools/repo"
arch=('any')
license=("APACHE")

depends=("git" "python2")

validpgpkeys=('8BB9AD793E8E6153AF0F9A4416530D5E920F5C65')
source=("git+https://android.googlesource.com/tools/repo.git#tag=v$pkgver?signed")

sha256sums=('SKIP')

prepare() {
  cd repo
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' repo
}

package() {
  cd repo
  install -D -m 755 repo "$pkgdir/usr/bin/repo"
  install -D -m 644 docs/manifest-format.md "$pkgdir/usr/share/doc/$pkgname/manifest-format.md"
}
