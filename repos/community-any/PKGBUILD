# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: cth451 <cth451@gmail.com>

pkgname=materia-gtk-theme
pkgver=20180928
pkgrel=1
pkgdesc="A Material Design theme for GNOME/GTK+ based desktop environments"
arch=('any')
url="https://github.com/nana-4/materia-theme"
license=('GPL')
depends=('gtk3' 'gnome-themes-extra')
optdepends=('gtk-engine-murrine: for gtk2 theme')
provides=('materia-theme')
conflicts=('materia-theme')
replaces=('materia-theme')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/nana-4/materia-theme/archive/v${pkgver}.tar.gz")
sha256sums=('aa1cdaa0f822da816802c75664ee2fa1391ffd08a17d74a894c97be2ef53d987')

package() {
    cd ${pkgname/gtk-/}-${pkgver}
    install -d "${pkgdir}"/usr/share/themes
    ./install.sh -d "${pkgdir}"/usr/share/themes
}

