# Maintainer:

pkgname=meson-python
pkgver=0.9.0
pkgrel=1
pkgdesc='Meson PEP 517 Python build backend'
arch=(any)
url='https://github.com/FFY00/meson-python'
license=(MIT)
depends=(meson patchelf python-wheel python-pyproject-metadata)
makedepends=(python-build python-installer ninja)
checkdepends=(python-pytest python-gitpython python-pytest-mock)
source=(https://github.com/FFY00/meson-python/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8b50f6e90028e94d4fee97b4dd9e24659754e6a67e9163b7ccd51f0ef452573b')

prepare() {
  cd $pkgname-$pkgver
  sed -e '/ninja/d' -i pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
