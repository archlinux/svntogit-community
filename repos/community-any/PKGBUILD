# $Id: PKGBUILD 97778 2013-09-29 16:22:13Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgbase=python-html2text
pkgname=('python-html2text' 'python2-html2text')
pkgver=2016.4.2
pkgrel=1
pkgdesc="A HTML to markdown-structured text converter"
arch=('any')
url="https://pypi.python.org/pypi/html2text/"
license=('GPL3')
makedepends=('python-setuptools' 'python2-setuptools')
provides=('html2text')
replaces=('html2text')
source=(https://pypi.python.org/packages/source/h/html2text/html2text-$pkgver.tar.gz)
sha256sums=('32e5e49eb520335bae200a00e3cc981f1ade558930068e1bb2a6e3fb1504f951')

prepare() {
  cp -a html2text-$pkgver{,-py2}
}

build() {
  cd "${srcdir}"/html2text-$pkgver
  python setup.py build
  
  cd "${srcdir}"/html2text-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "${srcdir}"/html2text-$pkgver
  PYTHONPATH=$PYTHONPATH:. python setup.py test --verbose

  cd "${srcdir}"/html2text-$pkgver-py2
  PYTHONPATH=$PYTHONPATH:. python2 setup.py test --verbose
}

package_python-html2text() {
  depends=('python-setuptools')

  cd "${srcdir}"/html2text-$pkgver
  python setup.py install --root="${pkgdir}"
}

package_python2-html2text() {
  depends=('python2-setuptools')

  cd "${srcdir}"/html2text-$pkgver-py2
  python setup.py install --root="${pkgdir}"
  mv "$pkgdir/usr/bin/html2text"{,2}
}
