# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.37.1
_image_pkgver=5.12.0
_podman_pkgver=3.1.2
_skopeo_pkgver=1.2.3
_storage_pkgver=1.30.1
pkgrel=2
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('99e88cf44070aba93ac044c5f27a2d4ed3257360d78c71c43afda2071c21356182cbeaa205231cf121dcfe4687b726e025182ea6413ac6c8718fb3a95a191d5f'
            'c690b1c4fd67754c41cc590279382a8f4228e3160db5ba482455b4c0ef4b6bd40ea92d0ca7662b2cc1504d82afdb33bdce8d60552ba3e216102ed34e1418d8c0'
            'fdc26368f3cce6a11fe9adeae34a2f04396a554bb5f245e07fc98d1ce21dff66faba5549e9e0b94793d67768ff03fea2990e7a2759dd41ed2b72778431f5dd9d'
            '06e3ddc17f45a7b145c44c58d67894d3fff809cd1521753027c703c44425241d4580096fd44d9156eae70336544110e2af95ab6f9d22deff88903c6e77bbe4d8'
            'f9a78c20992eea9812f5589938086960bbcb169807a4004c3b3df3e143e2e4668e95cfdad0c59d7cd1d906be5cf0c619863ec4b306cfd6ff03bb1fd7be49b46b'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('63ecd7ba7a3d17ee5e57d96b773c251020d47f1b79e7590d3f81c811e50cea4786bea15233ae463a44eac0357199db217f0a3f653ed9b20a5cee61394b70d64a'
        'c90e13a400fd7de3d4d57e6e5490d36f47949aa2170f832cd22d34e663ede528fa85cc84c836d48301568657b78a8fafcea2c7ed475da179dc6f0507b8501523'
        '8536f6856e436467541f7714e75d5055545e1514604d6541671894117e752609a9e791355c162ef8fcdbc0cbd992823ecab8a3ab760bab3a469d9768b6b008e4'
        '3db447726d6a545f5225aa37a215da8a7893ea8beae08547bf93148d632a80863173b014949cc69cb033a2e68653738fe86d1c8758b12fc3c2c16ebb5582b203'
        'e3cd50ec72ab850a0e49c7d9081eda9cdcd3fa91c44471605531e5c4bca74502120b536adb93ff83c45e2bb3400306bc1a9a6fa364a256a16f4962e8d0d1e76a'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
