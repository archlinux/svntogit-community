# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-flasgger
pkgver=0.9.4
pkgrel=2
pkgdesc="Extract swagger specs from your flask project"
url="https://github.com/flasgger/flasgger"
license=('PerlArtistic')
arch=('any')
depends=('python-flask' 'python-yaml' 'python-jsonschema' 'python-mistune' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-flex' 'python-marshmallow' 'python-decorator'
              'python-apispec-webframeworks' 'python-flask-restful' 'python-flask-jwt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/flasgger/flasgger/archive/$pkgver.tar.gz")
sha512sums=('7cfd2ebbd2b16a1ae6a9cf24694a1a396786259d28a742e89e9090d3db0d7be0b0a6f567e717c6cf7723275a4129ce37d7dbd90dbc42c9da261dd6cd167eb6f2')

build() {
  cd flasgger-$pkgver
  python setup.py build
}

check() {
  cd flasgger-$pkgver
  PYTHONPATH="$PWD/etc/flasgger_package" python -m pytest
}

package() {
  cd flasgger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
