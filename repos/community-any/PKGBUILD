# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mypy
pkgver=0.6.2
pkgrel=1
pkgdesc='Mypy static type checker plugin for Pytest'
arch=('any')
license=('MIT')
url='https://github.com/dbader/pytest-mypy'
depends=('mypy' 'python-filelock' 'python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dbader/pytest-mypy/archive/v$pkgver.tar.gz")
sha512sums=('e7921ce2ea31b9cafa4364faea6c58d2b3284d3809bcfb80d2a9130d66192ffbf1449d23368098f862ee92321c61cc7ec98e14fb7fe5e0b46f28dd812ee8f836')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-mypy-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-mypy-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-mypy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
