# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-pysaml2
pkgver=6.5.1
pkgrel=1

pkgdesc='Python implementation of SAML Version 2'
url='https://pypi.python.org/pypi/pysaml2'
arch=('any')
license=('Apache')

depends=('python'
         'python-dateutil'
         'python-defusedxml'
         'python-future'
         'python-importlib_resources'
         'python-pycryptodomex'
         'python-pyopenssl'
         'python-pytz'
         'python-requests'
         'python-six'
         'python-zope-interface'
         'python-xmlschema'
         'xmlsec')
makedepends=('python-setuptools')

source=("https://pypi.org/packages/source/p/pysaml2/pysaml2-$pkgver.tar.gz")

md5sums=('50865443d5d8d922ea11cdfc262a5a19')

build() {
	cd "$srcdir"/pysaml2-$pkgver
	python setup.py build
}

check() {
	cd "$srcdir"/pysaml2-$pkgver
	python setup.py test
}

package() {
	cd pysaml2-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
