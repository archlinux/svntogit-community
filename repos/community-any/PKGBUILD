# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-ddt
pkgname=('python-ddt' 'python2-ddt')
pkgver=1.2.2
pkgrel=1
pkgdesc="Data-Driven/Decorated Tests"
arch=('any')
license=('MIT')
url="https://github.com/txels/ddt"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-yaml' 'python2-yaml')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/txels/ddt/archive/$pkgver.tar.gz")
sha512sums=('9b45c4b422cab9e6858235b413b622f077bf4e5254214165c50afdea767dda7d9f39e7be7b0d583b87849a078271f4fba34ac5fe0f35bd196eb7f396bd308fe7')

prepare() {
  cp -a ddt-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/ddt-$pkgver
  python setup.py build

  cd "$srcdir"/ddt-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/ddt-$pkgver
  nosetests3

  cd "$srcdir"/ddt-$pkgver-py2
  nosetests2
}

package_python-ddt() {
  depends=('python')

  cd ddt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

package_python2-ddt() {
  depends=('python2')

  cd ddt-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
