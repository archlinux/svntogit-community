# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=python-django-picklefield
pkgname=('python-django-picklefield' 'python2-django-picklefield')
pkgver=0.3.2
pkgrel=1
pkgdesc='A pickled object field for Django'
arch=('any')
url='https://github.com/gintas/django-picklefield'
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=("django-picklefield-${pkgver}.tar.gz::https://github.com/gintas/django-picklefield/archive/v${pkgver}.tar.gz")
sha256sums=('b10beb3d604c00845c429b3ce2485fab26b95de78e4af06e0fb76e02941afcb3')

prepare() {
  cp -r {,python-}django-picklefield-${pkgver}
  mv {,python2-}django-picklefield-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-django-picklefield-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-django-picklefield() {
  depends+=('python-django')

  cd python-django-picklefield-${pkgver}

  python setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-django-picklefield
}

package_python2-django-picklefield() {
  depends=('python2-django')

  cd python-django-picklefield-${pkgver}

  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python2-django-picklefield
}

# vim: ts=2 sw=2 et:

