# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>

pkgbase=python-queuelib
pkgname=('python2-queuelib' 'python-queuelib')
pkgver=1.4.2
pkgrel=2
pkgdesc="Collection of persistent (disk-based) queues."
arch=('any')
license=('BSD')
url="http://scrapy.org"
makedepends=('git' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/scrapy/queuelib.git#tag=v${pkgver}")
md5sums=('SKIP')

prepare() {
  cp -a queuelib{,-py2}
}

check() {
  cd "$srcdir/queuelib"
  py.test

  cd "$srcdir/queuelib-py2"
  py.test2
}

package_python-queuelib() {
  depends=('python')

  cd "${srcdir}/queuelib"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "README.rst" "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}

package_python2-queuelib() {
  depends=('python2')

  cd "${srcdir}/queuelib-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "README.rst" "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}
