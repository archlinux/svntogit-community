# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailman-hyperkitty
pkgname=mailman3-hyperkitty
pkgver=1.2.0
pkgrel=1
pkgdesc="Mailman plugin to archive emails with HyperKitty"
arch=('any')
url="https://gitlab.com/mailman/mailman-hyperkitty"
license=('GPL3')
depends=('mailman3' 'python-requests' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-nose2')
replaces=('python-mailman-hyperkitty-plugin')
backup=('etc/mailman-hyperkitty.cfg')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc}
        "${pkgname}.tmpfiles")
sha512sums=('1704cbde75f004ba814508b926aae9ed2c7ec87934849ff00db4559e3b27f3f378f28fa60da44778e5018dff147d72ae4378dd7b2771263e8caa37cbb5c4be4f'
            'SKIP'
            'bc97105a6208d951cbb884a18fea9f1aeb94cb1dab5966ef707c4af526d6bcf869c6c75321fa834f66ca20d315f73a071bb62aba9353c320d7ba84ba935cce6b')
b2sums=('5c0774ccff45eaa71648cd6e9969fb688498bc3d8f8b41efef1e6c5d83b6c36fca8ae729d248c656a47aa789c260e4f0c486e54401da8b0ac806568a723a959d'
        'SKIP'
        '973b3ef36a3707fdefb36a94a7b5d4a2556f1dfd0d1c70fc648b90226203d33e488d8b79526b837a99ab0acbd4562894b8b62fe84021485180e03f9a874e66bf')
# enquired upstream about establishing a chain of trust for signed release artifacts:
# https://gitlab.com/mailman/mailman-hyperkitty/-/issues/22
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C')  # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # remove runtime requirement on setuptools:
  # https://gitlab.com/mailman/mailman-hyperkitty/-/issues/26
  sed -e "/'setuptools'/d" -i setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  nose2 -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 640 "${_name}.cfg" -t "${pkgdir}/etc/"
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
