# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
pkgver=0.44.4
pkgrel=1
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-python-lint'
license=('custom:MIT No Attribution')
depends=(python python-yaml python-six python-aws-sam-translator
         python-jsonpatch python-jsonschema python-networkx
         python-junit-xml)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-mock python-pydot)
optdepends=(
  'python-pydot: for building graphs from templates'
)
source=("https://github.com/aws-cloudformation/cfn-python-lint/archive/v$pkgver/cfn-python-lint-$pkgver.tar.gz")
sha256sums=('ba57e1cf3475e4342232b728304a2707c3722d589137bcb3d27cc2aa11d23ee6')

build() {
  cd cfn-python-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-python-lint-$pkgver

  # Tests in test/integration need the cfn-lint binary
  python setup.py install_scripts --install-dir="$srcdir"/tmp_install

  export PYTHONPATH="$PWD/src"
  export PATH="$PATH:$srcdir/tmp_install"
  # test_update_docs requires cfn-python-lint as a git repo
  pytest test -k 'not test_update_docs'
}

package() {
  cd cfn-python-lint-$pkgver
  # use PYTHONHASHSEED=0 to work around https://bugs.python.org/issue34033
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
