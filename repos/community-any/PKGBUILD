# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-i18n
pkgname=(python-oslo-i18n python2-oslo-i18n)
pkgver=3.15.0
pkgrel=1
pkgdesc="Oslo i18n library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.i18n/$pkgver"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel')
checkdepends=('python-mock' 'python2-mock' 'python-oslotest' 'python2-oslotest'
              'python-oslo-config' 'python2-oslo-config')
options=('!emptydirs')
source=("git+https://git.openstack.org/openstack/oslo.i18n#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a oslo.i18n{,-py2}
}

build() {
  cd "$srcdir"/oslo.i18n
  python setup.py build

  cd "$srcdir"/oslo.i18n-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.i18n
  python setup.py testr

  cd "$srcdir"/oslo.i18n-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-i18n() {
  depends=('python-six' 'python-pbr' 'python-babel')

  cd "$srcdir"/oslo.i18n
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-i18n() {
  depends=('python2-six' 'python2-pbr' 'python2-babel')

  cd "$srcdir"/oslo.i18n-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
