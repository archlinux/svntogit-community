# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-py
pkgver=1.11.0
_commit=447bac514febbb5433963582103d48bb27b3db17
pkgrel=2
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="https://py.readthedocs.io/"
depends=('python-iniconfig' 'python-apipkg')
makedepends=('git' 'python-setuptools' 'python-setuptools-scm')
checkdepends=('python-pytest-runner')
source=("git+https://github.com/pytest-dev/py.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cd py
  # pytest 4
  git show f4ed62ad39f289d09b3efdfed7305f935ce60bfc -- testing | git apply -
  rm -r py/_vendored_packages
}

build() {
  cd py
  python setup.py build
}

check() {
  # Known failure: https://github.com/pytest-dev/py/issues/104
  cd py
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd py
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
