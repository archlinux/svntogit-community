# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-net-openssh
pkgver=0.74
pkgrel=4
pkgdesc='Perl SSH client package implemented on top of OpenSSH'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Net-OpenSSH
source=("http://search.cpan.org/CPAN/authors/id/S/SA/SALVA/Net-OpenSSH-$pkgver.tar.gz")
md5sums=(3a8287d59746ddc245fbe38435b63a43)
sha512sums=(27b35ed512fe6da160e71264aaf7e1696f8182c5e731a71b95f84736e2dd04b9d3324d3a26549d4426ab6ca470810e5e16c82fbb6a88526015122dee3411a73a)
_ddir="Net-OpenSSH-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
