# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=4.0.3
pkgrel=3
pkgdesc="Aids in the development and testing of Ansible roles"
arch=(any)
url="https://github.com/ansible-community/molecule"
license=(MIT)
depends=(python-ansible-compat python-click
python-click-help-colors python-cookiecutter python-enrich python-jinja
python-jsonschema python-packaging python-pluggy python-pyyaml python-rich)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm)
checkdepends=(ansible python-ansi2html python-filelock python-pexpect
python-pytest python-pytest-mock python-pytest-testinfra python-pytest-xdist
yamllint)
optdepends=(
  'ansible: for the ansible verifier'
  'molecule-docker: for the docker driver'
  'molecule-podman: for the podman driver'
  'molecule-vagrant: for the vagrant driver'
  'python-pywinrm: for Windows support'
  'python-pytest-testinfra: for the testinfra verifier'
)
source=(
  https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz
  $pkgname-4.0.3-dependencies.patch
)
sha512sums=('8121b1b27e9449222c5e9a4ccf396e219165abf4899c5709404caeace61a8785b51cc20dfd60940bd51796acfaa8e3fcb3fa19d23116d5d2a48ab590c5a7bd8d'
            'db9c7bede6654f5634ebf1b361354d82b350e4578276b9b521d00e4602fd01cd81766f515599e347a3d70ec125a3caf0b1de3bde617c9b4ae10c2d51690fe8bf')
b2sums=('3c082aeb7196f04c6fa13bd904bb8bc7818d69df8bff7beafd394ce98d3b9e5bd7c60f713703e4dc4098d945f9b0d31e1cf122b12383d3743b133702f9ba5d6f'
        '907c788dec9e6bc58dd1b1731bdeb03d59297fd991ec24b700124e7994a915e18c0c114a022a1b2e86cd412f763b87e1f9c68a8fabc4a43d11347c358e3ca5a2')

prepare() {
  # remove pip and setuptools-scm-git-archive from makedepends: https://github.com/ansible-community/molecule/pull/3734
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-4.0.3-dependencies.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="$PWD/test_dir"

  # install into test location
  mkdir -vp "$_test_dir"
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$_test_dir/usr/bin:$PATH"
  pytest -v "$_test_dir/$_site_packages/molecule/test/unit/" -c /dev/null
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
