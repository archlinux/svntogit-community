# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=2.1.0
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('any')
license=('BSD')
url="https://scrapy.org"
depends=('python-twisted' 'python-cryptography' 'python-cssselect' 'python-lxml' 'libxml2'
         'python-parsel' 'python-pydispatcher' 'python-pyopenssl' 'python-queuelib'
         'python-service-identity' 'python-setuptools' 'python-w3lib' 'python-zope-interface'
         'python-protego')
checkdepends=('python-pytest-runner' 'python-pytest-twisted' 'python-botocore' 'bpython'
              'python-brotlipy' 'python-jmespath' 'ipython' 'mitmproxy' 'python-pillow'
              'python-sybil' 'python-testfixtures')
optdepends=('ipython: for enhanced support of the interactive scraping shell')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/scrapy/archive/$pkgver.tar.gz")
sha512sums=('72bf6ebcabd6022236cfb8ab4479d577e38417a8c4bcfec80539bd25711cb39ceb44702f801fa0f029777411840506e07da1982ff19776e458ab697756e3f31c')

build() {
  cd scrapy-$pkgver
  python setup.py build
}

check() {
  cd scrapy-$pkgver
  python setup.py pytest --addopts tests
}

package() {
  cd scrapy-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 README.rst "$pkgdir"/usr/share/doc/$pkgname/README.rst
  install -D -m644 docs/intro/install.rst "$pkgdir"/usr/share/doc/$pkgname/INSTALL.rst
}
