# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgbase='python-cheroot'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cheroot' 'python2-cheroot')
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=6.5.5
pkgrel=1
arch=('any')
url="https://github.com/cherrypy/cheroot"
license=('MIT')
makedepends=('python-six' 'python2-six' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python2-backports.functools_lru_cache'
  'python2-backports.unittest_mock' 'tree' 'python-tox' 'python2-tox')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        "LICENSE"
        "cheroot-fix-setup-python3.patch"
        "disable-broken-tests.patch")
sha512sums=('85ac5072615ba1b906c23b167e2582ad3cad12c5e4b70d81cef971a61a1caf287f479a665e8fabd66b2bae9c8bb1c9947e5e3fb4ba6e064678782bb3eb351d34'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944'
            'f303770c9f8565755230c2e957c444f2c69aea711abd8efc3d916688a31fe187d996e250eeba43e692e87d558831d4708fc712e2c653519d7ea42efba7d3e832'
            'ee08317c027a36461a89642e47edd5f9706796dbd6c273bbda3a37da5b1578701f861b09f23957636de5e72f38c312ad07caf0216408ec5d543feea7130839ba')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p1 -N -i "${srcdir}/cheroot-fix-setup-python3.patch"

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p1 -N -i "${srcdir}/disable-broken-tests.patch"

  # git-archive support is not needed since we use PyPI tarballs
  sed -i '/setuptools_scm_git_archive/d' setup.cfg

  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  tox

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  tox2
}

package_python-cheroot() {
  depends=('python-six')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cheroot() {
  depends=('python2-six' 'python2-backports.functools_lru_cache'
           'python2-backports.unittest_mock')

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/cheroot" "${pkgdir}/usr/bin/cheroot2"
}

# vim:set ts=2 sw=2 et:
