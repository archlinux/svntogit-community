# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=2.0.1
pkgrel=1
pkgdesc="A new approach to API documentation in Sphinx."
arch=(any)
url="https://github.com/readthedocs/sphinx-autoapi"
license=(MIT)
depends=(python-astroid python-jinja python-sphinx python-unidecode
python-pyyaml)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
# TODO: package python-go and python-dotnet
# optdepends=(python-go python-dotnet)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('cdf47968c20852f4feb0ccefd09e414bb820af8af8f82fab15a24b09a3d1baba')
b2sums=('d584618f11addb94816660643392b6b4884b8540d046a1914b7362d87cf308e27bfc9f6e78b0444e94771fdee516678c2f4592aca09cb4590c19efd05f6a69fa')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local deselected=(
    --deselect tests/python/test_pyintegration.py::TestPy3Module::test_annotations
    --deselect tests/python/test_pyintegration.py::TestAnnotationCommentsModule::test_integration
    --deselect tests/python/test_pyintegration.py::TestPositionalOnlyArgumentsModule::test_integration
  )

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -v "${deselected[@]}"
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/sphinx_autoapi-$pkgver.dist-info/LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
