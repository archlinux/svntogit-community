# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.35.4
_image_pkgver=5.10.5
_podman_pkgver=3.0.1
_skopeo_pkgver=1.2.2
_storage_pkgver=1.28.1
pkgrel=1
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('9e9ffdd3ca941e6bd34385739f5adeb9812cb639aa97d277373e9aa0ecb6228fa310304bd76d34f7b704360df0621453d049b6f499b8c3c597e6bba967010fe7'
            'c852dd5285f1dc5a8b6aa4730a256385e729b3d0f801786b2f71976e921b223677c3274ef1378fe997eebea6e02b2afaa4271449ac45396cd4758155556b59b2'
            '4addc986ad94b512d90b675a5a1988eeef365fff02c3449fd310582245a91d4ee96ff9fd3c56ce333a972dca627aa28fbd468a4a363c5c8963524bb67bc26754'
            '046f20049b7792c84b0dda4f332ac28f34d53821654cfa4ebe87e06f08bc4fa4a75a2b10b119c168df52ca0a0fb9ced4d7d4eb11b75a82d4f45bdae7be0e9ecb'
            '48fe9bd7c651a9072034bd1ae8f1cd65c3d434fb2128f150cdd0090dff1a8068bcb4608ab47ed4109ac94dc902718c7678767da3d13fb2a05d6ff67ec7dc17f6'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('8662cfe273c223247a0222e63efd7021a0e6533b92efbfea34faacd458f73c3ca6aa4f8a9cea68a850234187c4875657e322c0ea9a2737700aa633cd61992f72'
        'f71ead8296f4489724d09e0a084d8ed69c194ff433e1d2b516aaad7fd59ca4128ec8442d4731894f7dfe4e2d486769326dfe41d5c4d567a5823ca2758ebd2384'
        '93a227bf1395145ebaec8e18aa8ad8101704e8e0f7344c8850ee9e1532b191aef96b1cbbba2e21bd716591d68969cfd16ee811621a383cb2d31e4105a44834bf'
        '3dd3a9f7c061ec408b93b51531f063a2ea9cd489cf746d96b9e7dbcb5c3a2723955cc018c80b7de68098e88644ab0d075a5416adce912665bde203c4830f09e1'
        '6a80030c9a680ac58ee1fedb3cc87e6d51d880dcab182f371cd26f661b8c907ac04d0a047a8a166f50cdc861c799d3a5a922894b864b16c90659f8a70e048ec0'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
