# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-run
pkgver=9.2.1
pkgrel=1
pkgdesc="Install packages and run Python with them"
url="https://github.com/jaraco/pip-run"
license=('MIT')
arch=('any')
depends=('python-autocommand' 'python-packaging' 'python-path' 'python-pip' 'python-more-itertools'
         'python-jaraco.context' 'python-jaraco.text')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'jupyter-nbformat' 'python-pytest-enabler' 'python-jaraco.path')
source=("https://github.com/jaraco/pip-run/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f320da7641817df5c9418094fb49792e3368907ddac551ef22d97ab45b5183cb84f40338da106d5a47d238ca68ca376cbfc6d6c3528b9fca1db02bf910bd3d28')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pip-run-$pkgver
  python -m build -wn
}

check() {
  cd pip-run-$pkgver
  python -m pytest
}

package() {
  cd pip-run-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
