# Maintainer: Florian Pritz <bluewind@xinu.at>
pkgname=grafana-zabbix
pkgver=4.2.5
pkgrel=1
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('any')
url="https://github.com/alexanderzobnin/grafana-zabbix"
license=('APACHE')
depends=('grafana')
makedepends=(yarn python2 libfaketime go git nodejs-lts-fermium) # node-sass depends on node-gyp 3.8.0 which is still python2 https://github.com/sass/node-sass/issues/2877
source=("$pkgname-$pkgver.tar.gz::https://github.com/alexanderzobnin/grafana-zabbix/archive/v$pkgver.tar.gz")
sha256sums=('804ed6e8579db7d170179797489ac23dfe8e1cffb430aaa3bba95a8715ae1ca5')

build() {
	cd "$pkgname-$pkgver"
	export CXXFLAGS+=' -std=c++14 '
	make install
	make build
	make dist
	# Rerun this part since otherwise the linux_amd64 file ist missing from the
	# dist dir
	make dist-backend
}

check() {
	cd "$pkgname-$pkgver"
	# Force UTC timezone so that tests pass, even after a DST change
	PATH="$PATH:/build/go/bin" TZ=UTC make test lint
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -r dist/* "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
