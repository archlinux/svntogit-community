# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=pytest-expect
pkgbase='python-pytest-expect'
pkgname=('python-pytest-expect' 'python2-pytest-expect')
pkgver=1.1.0
pkgrel=2
pkgdesc="py.test plugin to store test expectations and mark tests based on them"
arch=('any')
url="https://github.com/gsnedders/${_pkgname}"
license=('MIT')
makedepends=('python-pytest' 'python2-pytest' 'python-u-msgpack' 'python2-u-msgpack')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('8392093123dfc9807c28fee251e1d710aaed0d46ee77c7531528fb0a265eb798')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py build
  python2 setup.py build
}

package_python-pytest-expect() {
  depends=('python-pytest' 'python-u-msgpack')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest-expect() {
  depends+=('python2-pytest' 'python2-u-msgpack')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
