# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flexmock
pkgname=('python-flexmock' 'python2-flexmock')
pkgver=0.9.7
pkgrel=1
pkgdesc="Mock/Stub/Spy library for Python"
arch=('any')
license=('BSD')
url="http://has207.github.com/flexmock"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/has207/flexmock.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a flexmock{,-py2}
}

build() {
  cd "$srcdir/flexmock"
  python setup.py build

  cd "$srcdir/flexmock-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/flexmock"
  nosetests3

  cd "$srcdir/flexmock-py2"
  nosetests2
}

package_python-flexmock() {
  depends=('python')

  cd flexmock
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-flexmock() {
  depends=('python2')

  cd flexmock-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
