# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.0.4
pkgrel=3
pkgdesc="Aids in the development and testing of Ansible roles"
arch=('any')
url="https://github.com/ansible-community/molecule"
license=('MIT')
depends=('ansible' 'python-cerberus' 'python-click' 'python-click-completion'
'python-click-help-colors' 'python-colorama' 'python-cookiecutter'
'python-gilt' 'python-jinja' 'python-paramiko' 'python-pexpect' 'python-pluggy'
'python-selinux' 'python-sh' 'python-six' 'python-tabulate'
'python-tree-format' 'python-yaml' 'yamllint')
makedepends=('python-setuptools-scm' 'python-simplejson')
# TODO: potentially add 'python-shade'
checkdepends=('docker' 'python-ansi2html' 'python-docker' 'python-mock'
'python-pytest' 'python-pytest-cov' 'python-pytest-helpers-namespace'
'python-pytest-html' 'python-pytest-mock' 'python-pytest-sugar'
'python-pytest-verbose-parametrize' 'python-pytest-xdist' 'python-testinfra')
optdepends=('docker: for the docker driver'
            'molecule-vagrant: for the vagrant driver'
            'podman: for the podman driver'
            'python-pywinrm: for Windows support'
            'python-testinfra: for the testinfra verifier')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "64b8b7608513bd4aceb6e58f3bdbd775913064da.patch")
sha512sums=('84420677d62aea7e4a3570b97884d85c48a0fe1d94ccdecc400e962a37d772c37bb64b569ac9c2f6e1b034f85758554ff1e16a2a9d5a42120d27d54d8691bdc0'
            '6b3bcfde754ad54439e1adab1457b95afcf7b7850047df9405b98bd1a592f00e06568e6e2ea792efe3e275e31bc5d94b74497b1de2b55f42ddeab6e017b52a1e')

prepare() {
  cd "$pkgname-$pkgver"
  # python-pre-commit is absolutely not a runtime dependency
  sed -e '/pre-commit/d' -i setup.cfg
  # increase python-sh requirement
  patch -p1 -i "$srcdir/64b8b7608513bd4aceb6e58f3bdbd775913064da.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  # installing into test location, so pytest can pick up the executable
  mkdir -vp test_dir
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="test_dir"
  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  export PYTHONPATH="${PWD}/test_dir/usr/lib/python${python_version}/site-packages/:${PYTHONPATH}"
  export PATH="${PWD}/test_dir/usr/bin:${PATH}"
  # do not run tests that require interaction with the docker daemon
  pytest -v \
         --ignore='molecule/test/functional/docker/' \
         --ignore='molecule/test/functional/test_command.py' \
         -k 'not test_execute \
             and not test_execute_bakes \
             and not test_execute_bakes_with_ansible_args \
             and not test_executes_cathces_and_exits_return_code_with_stdout'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGELOG,CONTRIBUTING,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
