# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMySQL
pkgname=python-pymysql
pkgver=0.9.3
pkgrel=2
pkgdesc="Pure Python MySQL Client"
arch=('any')
url="https://github.com/PyMySQL/PyMySQL/"
license=('MIT')
depends=('mariadb' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
optdepends=('python-cryptography: for cryptography')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('2056bc4aca7a600ca50214b399edc6f3068b288c426aecb115db148f645b0ec7e9a1a7134cf67f39d4820ea59a925dc1ce3ed0e383fb5e16e0651e87449824f9')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

# the tests are integration tests and can only be run with a running and
# configured mariadb server
# check() {
#   cd "$pkgname-$pkgver"
#   export PYTHONPATH="build:${PYTHONPATH}"
#   pytest -v pymysql/tests
# }

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,README.rst,example.py} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
