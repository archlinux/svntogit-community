# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=sqlalchemy-utils
pkgname=python-sqlalchemy-utils
pkgver=0.37.1
pkgrel=1
pkgdesc='Various utility functions, new data types and helpers for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-utils'
depends=('python' 'python-sqlalchemy' 'python-anyjson' 'python-babel'
         'python-arrow' 'python-intervals' 'python-phonenumbers' 'python-passlib'
         'python-colour' 'python-dateutil' 'python-furl' 'python-cryptography'
         'python-pendulum')
checkdepends=('python-pytest' 'python-mock' 'python-flexmock' 'python-jinja')
makedepends=('python')
license=('BSD')
arch=('any')
source=(https://github.com/kvesteri/sqlalchemy-utils/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a97fccb85e8de7007716a9a7218a517539b911290c0ac0a5556fcd346266f947bd5d353be2b24b513986ee557d1b3aca2bef85563b5d30b1684cc5e2474d4d08')
b2sums=('d79fd21f73af3b41e33b4f1c87cac92f36c8249b785dbb76972a0ef19931778ee2d9a24dfb0885508bc396007b6a4387313b1b8f384aa323d2c52a7ba10788f2')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

check() {
  cd ${_name}-${pkgver}
  # Tests require a postgres and MySQL db
  pytest --disable-pytest-warnings tests/test_models.py
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
