# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=7.0.4
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("https://github.com/Simplistix/testfixtures/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('30f51a7ff7511ac92998228ddd8778798759668659d544b43738c602a9ac66a50dcab002e694a42fdbb8fadb40b9a1e85dd6d3fc7e560625d87e248561889701')

build() {
  cd  testfixtures-$pkgver
  python setup.py build
}

check() {
  cd  testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 docs/license.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
