# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Serge Victor <arch@random.re>

_pyname=flask-wtf
pkgbase=python-flask-wtf
pkgname=('python-flask-wtf' 'python2-flask-wtf')
pkgver=0.13.1
pkgrel=1
pkgdesc='Simple integration of Flask and WTForms'
url='https://flask-wtf.readthedocs.io/'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python-sphinx' 'python-flask' 'python-wtforms' 'python-werkzeug'
             'python2-setuptools' 'python2-sphinx' 'python2-flask' 'python2-wtforms' 'python2-werkzeug')
checkdepends=('python-flask-babel' 'python-nose'
              'python2-flask-babel' 'python2-nose')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/lepture/flask-wtf/archive/v${pkgver}.tar.gz)
sha512sums=('131eb94e6af676b1aad9047be90318565972d5bfb7c3afa701bb34788cfbd3730f27b137a204abb3d0e54dd974d3b441a304be2fee99ca7a0a559d0f7b1eceff')

prepare() {
  sed -r 's|dist = get_distribution|#|' -i ${_pyname}-${pkgver}/docs/conf.py
  sed -r "s|(version =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
  sed -r "s|(release =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    make -C docs man text SPHINXBUILD=sphinx-build
  )
  (cd ${_pyname}-${pkgver}-py2
    make -C docs man text SPHINXBUILD=sphinx-build2
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    nosetests
  )
  (cd ${_pyname}-${pkgver}-py2
    nosetests2
  )
}

package_python-flask-wtf() {
  depends=('python-flask' 'python-wtforms' 'python-werkzeug')
  cd ${_pyname}-${pkgver}
  find docs
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/flask-wtf.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

package_python2-flask-wtf() {
  depends=('python2-flask' 'python2-wtforms' 'python2-werkzeug')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/flask-wtf.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
