# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=cxxopts
pkgver=3.1.0
pkgrel=1
pkgdesc="Lightweight C++ command line option parser"
arch=(any)
url="https://github.com/jarro2783/cxxopts"
license=(MIT)
makedepends=(cmake)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('bfb593f6393160ae3eeff1fe7bc77394606c3af6ae3b785f9740d178514a8fd286556440aa8a2932633f65b6336695fa286d503f3ac544d0f73affd49051e85d')
b2sums=('0e8d17c2f75fbc3326a67c1787eb80177a72e07e79862288498b4bb97a395269a4b14749db4ea14292b77d6b883f8dc9a33a4001c699e53e51b63c7d14e9ece0')

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -Wno-dev
    -B build
    -S $pkgname-$pkgver
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
