# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-dist-checkconflicts
pkgver=0.10
pkgrel=1
pkgdesc="Declare version conflicts for your dist"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-list-moreutils' 'perl-exporter')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-fatal' 'perl-test-simple' 'perl-module-runtime')
url="https://metacpan.org/release/Dist-CheckConflicts"
source=(http://search.cpan.org/CPAN/authors/id/D/DO/DOY/Dist-CheckConflicts-$pkgver.tar.gz)
md5sums=('421fd89a8f888d2a3411754da1c384a8')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps            \
      PERL_MM_OPT="INSTALLDIRS=vendor"       \
      PERL_MB_OPT="--installdirs vendor"     \
      MODULEBUILDRC=/dev/null
    cd Dist-CheckConflicts-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Dist-CheckConflicts-$pkgver

    make test
  )
}

package() {
  cd Dist-CheckConflicts-$pkgver

  make DESTDIR="$pkgdir"/ install
}
