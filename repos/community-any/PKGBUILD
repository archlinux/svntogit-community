# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-moo'
pkgver=2.003006
pkgrel=3
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.1' 'perl-devel-globaldestruction>=0.11' 'perl-import-into>=1.002'
         'perl-module-runtime>=0.014' 'perl-role-tiny>=2' 'perl-sub-quote')
makedepends=('perl-test-fatal>=0.003')
url='https://metacpan.org/release/Moo'
source=("https://cpan.metacpan.org/authors/id/H/HA/HAARG/Moo-${pkgver}.tar.gz")
sha256sums=('bcb2092ab18a45005b5e2e84465ebf3a4999d8e82a43a09f5a94d859ae7f2472')

build() {
  cd "$srcdir"/Moo-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
      PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
      MODULEBUILDRC=/dev/null
  perl Makefile.PL
  make
}

check() {
  cd "$srcdir"/Moo-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
#  make test
}

package() {
  cd "$srcdir"/Moo-$pkgver
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
