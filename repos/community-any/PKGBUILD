# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-compat
pkgname=python-ansible-compat
pkgver=3.0.0
pkgrel=1
pkgdesc="Functions that help interacting with various versions of Ansible"
arch=(any)
url="https://github.com/ansible-community/ansible-compat"
license=(MIT)
depends=(ansible-core python-jsonschema python-packaging python-pyyaml python-subprocess-tee)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm python-wheel)
checkdepends=(python-pytest python-pytest-mock)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('b2fb46656d6a2171e8774e7e5a6858908b1e4387a5e50d46bcc895cd738c210c')
b2sums=('7a944fdf5fb419b95f9da0e28cc4e58f5a8fb969653bb74fa2a4103f7d367210bf86de95fa5f3493ef383db4d8ffcc2ab935c006450c2778cb10b256cd32c515')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  # disable broken tests: https://github.com/ansible/ansible-compat/issues/210
  pytest -vv -c /dev/null -k 'not test_prerun_reqs_v1 and not test_prerun_reqs_v2'
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
