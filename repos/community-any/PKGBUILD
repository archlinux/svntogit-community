# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-enabler
pkgver=1.2.1
pkgrel=2
pkgdesc='Enable installed pytest plugins'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-enabler'
depends=('python-jaraco.context' 'python-jaraco.functools' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-checkdocs'
              'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy'
              'python-pip')
source=("https://github.com/jaraco/pytest-enabler/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "https://files.pythonhosted.org/packages/source/t/types-toml/types-toml-0.10.1.tar.gz")
sha512sums=('15762f966203d574e9e6b7c3fbeac2de9815a4ce264e603518d4edfbc82bc1faef2ad50d4508063d6a878033de03be457c59545f0712ca7cc71d183eb86f15a6'
            '001757f956dd3af0688a6544b90ba415b26ab3128e0fbb238f4dfaf0bab6b5795d5598bcc9422d854ea235acbc9e78b245c17f9ebc9ad9251ae3c0722b382812')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd pytest-enabler-$pkgver
  # no idea what this is or why mypy wants it
  ln -s ../types-toml-0.10.1/toml-stubs .
}

build() {
  cd pytest-enabler-$pkgver
  python setup.py build
}

check() {
  cd pytest-enabler-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-enabler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
