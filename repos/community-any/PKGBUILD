# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-bottle
pkgname=("python-bottle" "python2-bottle")
pkgver=0.12.1
pkgrel=1
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="http://bottlepy.org"
license=('MIT')
options=(!emptydirs)
makedepends=('python3' 'python2')
source=("https://github.com/defnull/bottle/archive/${pkgver}.tar.gz")

build() {
  cp -r "bottle-${pkgver}" "python2-bottle-${pkgver}"
}

package_python-bottle() {
  depends=('python3')

  cd bottle-${pkgver}
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-bottle/LICENSE"
}

package_python2-bottle() {
  depends=('python2')

  cd python2-bottle-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-bottle/LICENSE"

  # deal with conflict with python-bottle's /usr/bin/bottle.py
  mv "$pkgdir/usr/bin/bottle.py"{,2}
}
  
sha512sums=('90b61ac75f422210303153a110a75671b86e571b856f04701babd5f42104cec57d170a273d94b3fc104a0b054db0346ff1acd54d9cc88085a27696ba681ad29d')
