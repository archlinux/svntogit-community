# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: David Moore <davidm@sjsoft.com>

pkgbase=python-xlwt
pkgname=('python2-xlwt' 'python-xlwt')
pkgver=1.3.0
pkgrel=2
pkgdesc="Library to create spreadsheet files compatible with MS Excel 97/2000/XP/2003 XLS files"
arch=('any')
url="http://www.python-excel.org/"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/python-excel/xlwt/archive/${pkgver}.tar.gz")
md5sums=('fb4226ac8f1eb306ed6b74eac075d28f')

build() {
  cd "$srcdir"
  cp -a xlwt-${pkgver} xlwt-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/xlwt-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/xlwt-${pkgver}
  python setup.py build
}

package_python2-xlwt() {
  depends=('python2')

  cd "$srcdir"/xlwt-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 docs/licenses.rst "$pkgdir/usr/share/licenses/$pkgname/licences.rst"
}

package_python-xlwt() {
  depends=('python')

  cd "$srcdir"/xlwt-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 docs/licenses.rst "$pkgdir/usr/share/licenses/$pkgname/licences.rst"
}

