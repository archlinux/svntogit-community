# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-sub-exporter
pkgver=0.989
pkgrel=1
pkgdesc="A sophisticated exporter for custom-built routines"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-data-optlist' 'perl-params-util' 'perl-sub-install')
url="https://metacpan.org/release/Sub-Exporter"
source=(https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Sub-Exporter-$pkgver.tar.gz)
sha512sums=('7c491349bc9704604a268b45eed17db30c49f43f3c67f088d132d7f35c20eb1ddec46eca88b1e966fb48cf9580cb7007cc240f6d3ac9ba21cffbc7437b7dccbb')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL="--skipdeps"            \
      PERL_MM_OPT="INSTALLDIRS=vendor"         \
      PERL_MB_OPT="--installdirs vendor"       \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/Sub-Exporter-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""

    cd "$srcdir"/Sub-Exporter-$pkgver

    make test
  )
}

package() {
  cd "$srcdir"/Sub-Exporter-$pkgver

  make DESTDIR="$pkgdir"/ install
}
