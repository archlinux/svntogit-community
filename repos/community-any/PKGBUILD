# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric Berquist <eric dot berquist at gmail dot com>

pkgname=ruby-mixlib-cli
_pkgname="${pkgname#ruby-}"
pkgver=2.1.10
pkgrel=1
pkgdesc='A simple mixin for CLI interfaces, including option parsing'
arch=('any')
url='https://github.com/chef/mixlib-cli'
license=('Apache')
depends=('ruby')
makedepends=('git' 'ruby-rake' 'ruby-rdoc' 'ruby-bundler')
options=('!emptydirs')
_commit='6569ee9a3477bdb3cdf1232082026ae48ac61ca6'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  rake build
}

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/pkg/$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
