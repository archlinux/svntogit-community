# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-server
pkgver=1.21.0
pkgrel=1
pkgdesc='Provides the backend for Jupyter web applications like Jupyter notebook, JupyterLab, and Voila'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(python-jupyter_client jupyter-nbformat jupyter-nbconvert python-terminado python-prometheus_client python-traitlets python-tornado
         python-jinja python-send2trash python-anyio python-jupyter_packaging python-websocket-client python-requests-unixsocket python-argon2_cffi)
makedepends=(python-build python-installer python-pre-commit)
checkdepends=(python-pytest-tornasync python-pytest-console-scripts python-pytest-timeout)
optdepends=('jupyter-server-mathjax: to use local MathJax')
source=(https://pypi.io/packages/source/j/${pkgname/-/_}/${pkgname/-/_}-$pkgver.tar.gz)
sha256sums=('d0adca19913a3763359be7f0b8c2ea8bfde356f4b8edd8e3149d7d0fbfaa248b')

build() {
  cd ${pkgname/-/_}-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname/-/_}-$pkgver
  sed -e '/pytest_tornasync/d' -i jupyter_server/pytest_plugin.py
# tests fail on the build server, run them locally
# pytest -v
}

package() {
  cd ${pkgname/-/_}-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 COPYING.md -t "$pkgdir"/usr/share/licenses/$pkgname
}
