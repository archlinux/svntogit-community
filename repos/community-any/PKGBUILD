# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jeffrey Zhang <zhang.lei.fly@gmail.com>

_name=esbonio
pkgname=python-esbonio
pkgver=0.16.0
pkgrel=1
pkgdesc='Language server for working with Sphinx projects'
arch=('any')
url=https://github.com/swyddfa/esbonio
license=('MIT')
depends=('python-appdirs' 'python-sphinx' 'python-pygls' 'python-pyspellchecker'
         'python-setuptools' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-pytest-lsp' 'python-pytest-timeout')
source=("$url/archive/esbonio-language-server-v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('ec44c9305dfe9b473430176c28f207a7828407d0dadfc7e7ba650a60250c5fad847a83baf584de3a64cdf689e05c01d25dd3d6ce810ac65609eabcb8c252706d')

build() {
  cd esbonio-esbonio-language-server-v$pkgver/lib/esbonio
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd esbonio-esbonio-language-server-v$pkgver/lib/esbonio
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
  cd esbonio-esbonio-language-server-v$pkgver/lib/esbonio
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
