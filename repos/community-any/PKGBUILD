# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=stdlibs
pkgname=python-$_pkgname
pkgver=2022.6.8
pkgrel=1
pkgdesc="Simple list of top-level packages in Python's stdlib"
arch=('any')
url='https://github.com/omnilib/stdlibs'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-flit-core' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e5f48a623092522cdcca379f797a065e48ff362f2b42bfe2ad2bbf555d84fcc4e9c7e2e5f858563713e043cf69b85bcbd3a2825a97805a5396b0f52dfaedc365')

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
