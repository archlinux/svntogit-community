# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-cjkwrap
pkgname=(python-cjkwrap python2-cjkwrap)
pkgver=2.2
pkgrel=2
pkgdesc="A library for wrapping and filling CJK text."
url="https://github.com/fgallaire/cjkwrap"
license=('LGPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/C/CJKwrap/CJKwrap-$pkgver.tar.gz")
sha512sums=('59e0139daadad4d0397f0684a0c1abd5803d133e90ff7ced0adb703be02b20e430bb96a6777227640e55aca6d1aaea796bfc79b5d5b3379953e38323003cbde0')

prepare() {
  cp -a CJKwrap-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/CJKwrap-$pkgver
  python setup.py build

  cd "$srcdir"/CJKwrap-$pkgver-py2
  python2 setup.py build
}

package_python-cjkwrap() {
  depends=('python')

  cd CJKwrap-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cjkwrap() {
  depends=('python2')

  cd CJKwrap-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
