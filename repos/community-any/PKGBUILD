# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-passwordsafe
pkgver=3.30.2
pkgrel=2
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
arch=(any)
url="https://gitlab.gnome.org/World/PasswordSafe"
license=(GPL3)
depends=(libhandy python-pykeepass)
makedepends=(git gobject-introspection meson)
_commit=a480c5b1b7746e45f26ec682f571d10195ea3eb7  # tags/3.30.2
source=("git+https://gitlab.gnome.org/World/PasswordSafe.git/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd PasswordSafe
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson PasswordSafe build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
