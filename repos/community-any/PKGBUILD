# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=starlette
pkgname=python-$_name
pkgver=0.23.1
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=(any)
url="https://github.com/encode/starlette"
license=(BSD)
depends=(python python-anyio python-typing-extensions)
makedepends=(python-build python-installer python-hatchling python-wheel)
checkdepends=(
  python-aiosqlite
  python-databases
  python-pytest
  python-trio

  # optdepends
  python-itsdangerous
  python-jinja
  python-python-multipart
  python-pyyaml
  python-httpx

  # not specified,but required
  python-sqlalchemy
)
optdepends=(
  'python-itsdangerous: for session middleware support'
  'python-jinja: for jinja templates'
  'python-python-multipart: for form parsing'
  'python-pyyaml: for schema generator'
  'python-httpx: for test client'
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('9af4ab2b2e3af4e7acd2e93a9a4b4f4c8c3b56ba971f7a8faf65308d76a4f30b8ab0ab263535480af614a5082420cafb3d939c7477472710cce9241bd947139b')
b2sums=('b1eaf59421d413179ad294b240babb9b1beabbf10d8364187eb406a3b87a1412b3959b90f44ac1bbada7858c3ffc90560208bc433ca5aaf835dda1766282fe65')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv -o 'markers=filterwarnings' -p no:warnings
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
