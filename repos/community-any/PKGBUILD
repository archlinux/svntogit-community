# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("yapf" "python2-yapf")
pkgver=0.20.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/yapf/archive/v$pkgver.tar.gz")
sha512sums=('878246ab440e539fc330f99dfb350b35a062859decf888e4d81fe721e81fd399e9c9a1db52c0fb884eb0f753d5f87d8b7ae376e2ac23b194af47156854516460')

prepare() {
  cp -a yapf-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/yapf-$pkgver
  python setup.py build

  cd "$srcdir"/yapf-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/yapf-$pkgver
  nosetests3

  cd "$srcdir"/yapf-$pkgver-py2
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd yapf-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/yapf{,2}
}

# vim:set ts=2 sw=2 et:
