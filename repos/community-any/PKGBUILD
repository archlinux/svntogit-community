# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: googol <googol@posteo.de>
# Contributor: Oliver Mangold <o.mangold at gmail dot com>

pkgbase=python-alembic
pkgname=('python-alembic' 'python2-alembic')
pkgver=1.3.2
pkgrel=1
pkgdesc='Lightweight database migration tool for usage with SQLAlchemy'
url='https://bitbucket.org/zzzeek/alembic'
arch=('any')
license=('MIT')
makedepends=('python-setuptools' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil'
             'python2-setuptools' 'python2-mako' 'python2-sqlalchemy' 'python2-editor' 'python2-dateutil')
checkdepends=('python-pytest' 'python-mock' 'python2-pytest' 'python2-mock')
source=(https://pypi.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz{,.asc})
sha512sums=('bbd5f013ef532b3e5d4bcb49990c3416f2c3bec33631fc15d99ee2ddb0d06ca5bf83e7fc3355e93145df4811c2d00fedb034753aea7af5f72ed9c9fd13b98f17'
            'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1') # Michael Bayer <classic@zzzcomputing.com>

prepare() {
  cp -a alembic-${pkgver}{,-py2}
}

build() {
  msg2 'Building python...'
  (cd alembic-${pkgver}
    python setup.py build
  )
  msg2 'Building python2...'
  (cd alembic-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 'Checking python...'
  (cd alembic-${pkgver}
    py.test
  )
  msg2 'Checking python2...'
  (cd alembic-${pkgver}-py2
    py.test2
  )
}

package_python-alembic() {
  depends=('python' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
  cd alembic-${pkgver}
  python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-alembic() {
  depends=('python2' 'python2-mako' 'python2-sqlalchemy' 'python2-editor' 'python2-dateutil')
  cd alembic-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv "${pkgdir}/usr/bin/alembic"{,2}
}

# vim: ts=2 sw=2 et:
