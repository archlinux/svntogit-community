# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxde-common
pkgver=0.5.6
pkgrel=1
pkgdesc='Common files of the LXDE Desktop'
arch=('any')
url='http://lxde.org/'
license=('GPL2')
groups=('lxde')
backup=(etc/xdg/lxsession/LXDE/desktop.conf
        etc/xdg/lxsession/LXDE/autostart)
depends=('gtk-engines' 'lxde-icon-theme' 'lxsession')
source=(http://downloads.sourceforge.net/lxde/${pkgname}-${pkgver}.tar.xz)
md5sums=('259b4a2db50117e632e34912251cfcb9')

build() {
    cd $srcdir/$pkgname-$pkgver
    ./configure --sysconfdir=/etc --prefix=/usr
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install

    # Install lxde-logout.desktop file.
    # https://sourceforge.net/p/lxde/bugs/399/
    install -Dm644 lxde-logout.desktop \
      "$pkgdir/usr/share/applications/lxde-logout.desktop"

    # Fix lxpanel profile location.
    # https://sourceforge.net/p/lxde/patches/482/
    mv "$pkgdir"/etc/xdg/lxpanel/{profile/,}LXDE
    rm -r "$pkgdir/etc/xdg/lxpanel/profile"
}
