# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslo-serialization
pkgname=(python-oslo-serialization python2-oslo-serialization)
pkgver=3.0.0
pkgrel=2
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-msgpack' 'python2-msgpack' 'python-oslo-utils'
             'python2-oslo-utils' 'python-pytz' 'python2-pytz')
checkdepends=('python-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz")
sha512sums=('246831aa95db535becf130e759376773640fb94a5b78243ab6393e47bb4acc4bdb21c2352ce45be801e34f69c0dca3cf847b3f194c13edd219d46a3a69063e0f')

export PBR_VERSION=$pkgver

prepare() {
  cp -a oslo.serialization-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/oslo.serialization-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  python2 setup.py build
}

check() {
  cd oslo.serialization-$pkgver
  stestr run
}

package_python-oslo-serialization() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
           'python-pytz')

  cd "$srcdir"/oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-serialization() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-msgpack' 'python2-oslo-utils'
           'python2-pytz')

  cd "$srcdir"/oslo.serialization-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
