# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=autopep8
pkgver=1.7.0
pkgrel=1
epoch=1
pkgdesc="A tool that automatically formats Python code to conform to the PEP 8 style guide"
arch=('any')
url="https://github.com/hhatto/autopep8"
license=('MIT')
depends=('python-pycodestyle' 'python-toml')
makedepends=('python-setuptools')
source=("https://github.com/hhatto/autopep8/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('41bddd5c0cc93fd47dd01ba6b89f085fa7b3723abed9ac829e9f4d67a0587a7ee4457b23f16f223aaab18f33440166f402a567ca80d13535e85a6a3d4b968c0d')

build() {
  cd autopep8-$pkgver
  python setup.py build
}

check() {
  cd autopep8-$pkgver
  python setup.py test
}

package() {
  cd autopep8-$pkgver
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
