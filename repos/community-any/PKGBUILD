# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.28
pkgrel=1
pkgdesc='Tool for monitoring webpages for updates'
arch=('any')
url=https://thp.io/2008/urlwatch/
license=('BSD')
depends=('python-appdirs' 'python-cssselect' 'python-keyring' 'python-lxml'
         'python-minidb' 'python-requests' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-docutils' 'python-pycodestyle' 'python-pytesseract'
              'python-pytest')
optdepends=('lynx: for one of the html2text filter methods'
            'python-beautifulsoup4: for the beautify filter and one of the html2text filter methods'
            'python-cssbeautifier: for the beautify filter'
            'python-html2text: for one of the html2text filter methods'
            'python-jq: for the jq filter'
            'python-jsbeautifier: for the beautify filter'
            'python-pdftotext: for the pdf2text filter'
            'python-pytesseract: for the ocr filter')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('911df3abbd8923e46ec167a9657a812436caf93f7f9917cb7c95ebd73d28cce5')
b2sums=('73fcff8c153db472c944d0c9406e0e98bf64299d5532486a08cb024339544f17d716a22d6f0807ba55c79af9dc7fef41fa8dc5a7fe79c831f3e9958e3ea8958e')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$pkgname-$pkgver.dist-info/COPYING \
    "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
