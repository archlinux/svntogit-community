# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyter_events
pkgname=python-jupyter-events
pkgver=0.5.0
pkgrel=1
pkgdesc='Configurable event system for Jupyter applications and extensions'
arch=(any)
url='https://jupyter.org/'
license=(BSD)
depends=(python-jsonschema python-json-logger python-traitlets python-yaml python-click python-rich)
makedepends=(python-build python-installer python-hatchling)
checkdepends=(python-pytest-console-scripts)
source=(https://github.com/jupyter/jupyter_events/releases/download/v$pkgver/$_pipname-$pkgver.tar.gz)
sha256sums=('e27ffdd6138699d47d42cb65ae6d79334ff7c0d923694381c991ce56a140f2cd')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pipname-$pkgver
  python -m venv --system-site-packages test-env
  export PATH="$PWD/test-env/bin:$PATH"
  test-env/bin/python -m installer dist/*.whl
# https://github.com/jupyter/jupyter_events/issues/50
  test-env/bin/python -m pytest -k 'not test_emit_badschema_format and not test_bad_validations'
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  rm -r "$pkgdir"/build
}
