# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxde-common
pkgver=0.99.0
pkgrel=3
pkgdesc='Common files of the LXDE Desktop'
arch=('any')
url='http://lxde.org/'
license=('GPL2')
groups=('lxde')
backup=(etc/xdg/lxsession/LXDE/desktop.conf
        etc/xdg/lxsession/LXDE/autostart)
depends=('lxsession')
makedepends=('intltool')
optdepends=('gtk-engines: default GTK+ theme'
            'lxde-icon-theme: default icon theme')
source=(http://downloads.sourceforge.net/lxde/${pkgname}-${pkgver}.tar.xz
        dbus-update-environment.patch)
md5sums=('5a417f5a0e5b510a0c018b66b0dc4081'
         '9f5cdfb8aef6286b6de79a50d1fddd7f')

prepare() {
    cd $srcdir/$pkgname-$pkgver
    patch -Np1 -i ../dbus-update-environment.patch
}

build() {
    cd $srcdir/$pkgname-$pkgver
    ./configure --sysconfdir=/etc --prefix=/usr
    make
}

package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir install
}
