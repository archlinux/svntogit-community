# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx_basic_ng
pkgname=python-sphinx-basic-ng
pkgver=0.0.1a12
pkgrel=1
pkgdesc='Modernised skeleton for Sphinx themes'
arch=('any')
url=https://github.com/pradyunsg/sphinx-basic-ng
license=('MIT')
depends=('python-sphinx')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('cffffb14914ddd26c94b1330df1d72dab5a42e220aaeb5953076a40b9c50e801')
b2sums=('edacea2bf70d3781e52e38a707b1ac08d60a284b977b2b4323238947c433ae53a9cc5b48623fe3527b85e6077bb84828538a63bc45c0b2693dcb917651365597')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
