# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>
# Contributor: Pierre-Yves Ritschard <pyr@spootnik.org>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=clojure
pkgver=1.10.1
# https://github.com/clojure/brew-install/CHANGELOG.md
_toolver=697
pkgrel=9
pkgdesc='Lisp dialect for the JVM'
url='https://clojure.org/'
arch=(any)
license=(EPL)
depends=(java-environment)
optdepends=('leiningen: automate clojure projects'
            'rlwrap: friendly shell with readline support')
install=clojure.install
source=("https://download.clojure.org/install/clojure-tools-$pkgver.$_toolver.tar.gz"
        clojure.sh)
sha256sums=('700dd4a4fad95eda3392fd9f6026bbb4b0a3f31d7bf6c01324f225623f365974'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')

prepare() {
  sed -i -e 's,PREFIX,/usr/share/clojure,g' clojure-tools/clojure
}

package() {
  cd clojure-tools
  install -Dm644 deps.edn "$pkgdir/usr/share/clojure/deps.edn"
  install -Dm644 example-deps.edn "$pkgdir/usr/share/clojure/example-deps.edn"
  install -Dm644 clojure-tools-$pkgver.$_toolver.jar \
    "$pkgdir/usr/share/clojure/libexec/clojure-tools-$pkgver.$_toolver.jar"
  install -Dm755 clojure "$pkgdir/usr/bin/clojure"
  install -Dm755 clj "$pkgdir/usr/bin/clj"
  install -Dm644 clojure.1 "$pkgdir/usr/share/man/man1/clojure.1"
  install -Dm644 clj.1 "$pkgdir/usr/share/man/man1/clj.1"
  install -Dm755 "$srcdir/clojure.sh" "$pkgdir/etc/profile.d/clojure.sh"
}

# getver: clojure.org/downloads
# vim: ts=2 sw=2 et:
