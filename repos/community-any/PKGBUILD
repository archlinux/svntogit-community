# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mox3
pkgver=1.0.0
pkgrel=3
arch=('any')
pkgdesc='Mock object framework for Python'
url='https://docs.openstack.org/developer/mox3'
license=('Apache')
depends=('python-pbr' 'python-fixtures')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/mox3/archive/$pkgver.tar.gz")
sha512sums=('18e4fe6514993cfae07b0b9d0a7759285803cd09035865bf8355ef3d5eb24a07294cfcdb20fdd222c6774a6fa09a8838186ce7bbfe19e0430a060041c0ca0320')

export PBR_VERSION=$pkgver

prepare() {
  # Python 3.6 fixes
  sed -i 's/, flags=4//' mox3-$pkgver/mox3/tests/test_mox.py
}

build() {
  cd mox3-$pkgver
  python setup.py build
}

check() {
  cd mox3-$pkgver
  stestr run
}

package() {
  cd mox3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
