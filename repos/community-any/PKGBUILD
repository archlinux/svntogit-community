# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgbase=python-paramiko
pkgname=(python-paramiko python2-paramiko)
pkgver=2.0.0
pkgrel=1
pkgdesc="Python module that implements the SSH2 protocol"
url="https://github.com/paramiko/paramiko/"
license=('LGPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cryptography' 'python2-cryptography' 'git')
source=("git+https://github.com/paramiko/paramiko.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a paramiko{,-py2}

  find paramiko-py2 -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
        -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

check() {
  cd "$srcdir/paramiko"
  LANG=en_US.UTF-8 python test.py

  cd "$srcdir/paramiko-py2"
  LANG=en_US.UTF-8 python2 test.py
}

package_python-paramiko() {
  depends=('python-setuptools' 'python-cryptography')
  cd "$srcdir/paramiko"

  python setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py
}

package_python2-paramiko() {
  depends=('python2-setuptools' 'python2-cryptography')
  cd "$srcdir/paramiko-py2"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py
}

