# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>
# Contributor: Anton Eliasson <devel@antoneliasson.se>

pkgbase=python-httplib2
pkgname=(python-httplib2 python2-httplib2)
pkgver=0.13.0
pkgrel=1
pkgdesc='Comprehensive HTTP client library, supporting many features'
url='https://github.com/httplib2/httplib2'
license=('MIT')
arch=('any')
makedepends=('ca-certificates' 'python-setuptools' 'python2-setuptools')
checkdepends=('flake8' 'python2-flake8' 'python-mock' 'python2-mock' 'python-pytest-cov'
              'python2-pytest-cov' 'python-pytest-forked' 'python2-pytest-forked'
              'python-pytest-timeout' 'python2-pytest-timeout' 'python-pytest-xdist'
              'python2-pytest-xdist' 'python-pytest-randomly' 'python2-pytest-randomly'
              'python-future' 'python2-future')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/httplib2/httplib2/archive/v$pkgver.tar.gz"
        'cert.patch')
sha512sums=('e6bfb205581e469ff6edd651d810f2c8f08109be684a40433d97ca246cb0f6b0fff664c8b947477ffa2715b202c932188af6d20ae5528140be429919ad99e9bb'
            '503719fb07b9ff1f06f252b73804787759139098dba990a7dcd9e9b3602f7271d2251e703993636b249b3597c9a240c721c536a4fe31526ec4ba9441f6c8da56')

prepare() {
  cd httplib2-$pkgver
  patch -p0 -i "$srcdir"/cert.patch
  sed -i 's/==/>=/' requirements-test.txt

  cp -a "$srcdir"/httplib2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/httplib2-$pkgver
  python setup.py build

  cd "$srcdir"/httplib2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/httplib2-$pkgver
  python setup.py test

  cd "$srcdir"/httplib2-$pkgver-py2
  python2 setup.py test || warning "Tests failed"
}

package_python-httplib2() {
  depends=('python' 'ca-certificates')

  cd httplib2-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-httplib2() {
  depends=('python2' 'ca-certificates')

  cd httplib2-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
