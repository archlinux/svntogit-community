# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mongomock
pkgver=3.22.0
pkgrel=1
pkgdesc="Fake pymongo stub for testing simple MongoDB-dependent code"
url="https://github.com/mongomock/mongomock"
license=('BSD')
arch=('any')
depends=('python-sentinels' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongomock/mongomock/archive/$pkgver.tar.gz")
sha512sums=('c8b970de2d18adeb59cce22bd6213fa0b8f5142e6a9e3ada242ad27083d74284e15da3b2836c273e014a9c21e4816d844ccd420a004fa247f46fd59f2ccc7a8b')

export PBR_VERSION=$pkgver

build() {
  cd mongomock-$pkgver
  python setup.py build
}

check() {
  cd mongomock-$pkgver
  python setup.py nosetests
}

package_python-mongomock() {
  cd mongomock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
