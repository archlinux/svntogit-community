# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Marcello "mererghost" Rocha <https://github.com/mereghost>
# Refactored by Bla≈æ "Speed" Hrastnik <https://github.com/archSeer>

pkgname=elasticsearch
pkgver=6.2.3
pkgrel=1
pkgdesc="Distributed RESTful search engine built on top of Lucene"
arch=('any')
url="https://www.elastic.co/products/elasticsearch"
license=('APACHE')
depends=('java-runtime-headless=8' 'systemd')
source=(
  "https://artifacts.elastic.co/downloads/$pkgname/$pkgname-$pkgver.tar.gz"
  elasticsearch-env
  elasticsearch.service
  elasticsearch@.service
  elasticsearch-sysctl.conf
  elasticsearch-user.conf
  elasticsearch-tmpfile.conf
  elasticsearch.default
)
sha256sums=('01dd8dec5f0acf04336721e404bf4d075675a3acae9f2a9fdcdbb5ca11baca76'
            'cffa3aeabd4ec781d90e6432555959140cccba1d311066bb1b8e8bf166f9defd'
            'ac484a5e44c04f1d1687bf223720468f3a9b9dd11db248072c9ba0f4a6a9e8aa'
            'dae13e9d5311841eaa6582b333ffb0bd782eabc205a55bb35f891f2e404704ab'
            'b3feb1e9c7e7ce6b33cea6c727728ed700332aae942ca475c3bcc1d56b9f113c'
            '815f6a39db6f54bb40750c382ffbdc298d2c4c187ee8ea7e2f855923e2ff354b'
            '3173e3efa429507e6329f518699a072dfd442d9b5da7c62452a55f82334dd2b5'
            'bb74e5fb8bc28f2125e015395ab05bea117b72bfc6dadbca827694b362ee0bf8')

backup=('etc/elasticsearch/elasticsearch.yml'
        'etc/elasticsearch/log4j2.properties'
        'etc/elasticsearch/jvm.options'
        'etc/default/elasticsearch')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  sed -r 's;source .*/elasticsearch-env;source /usr/share/elasticsearch/elasticsearch-env;' \
      -i bin/elasticsearch{,-keystore,-plugin,-translog}
}

package() {
  cd "$pkgdir"
  install -dm750 etc/elasticsearch/scripts
  install -dm755 usr/share/elasticsearch
  install -dm755 var/lib/elasticsearch
  install -dm755 var/log/elasticsearch

  install -Dm644 "$srcdir"/elasticsearch-env usr/share/elasticsearch/elasticsearch-env
  install -Dm644 "$srcdir"/elasticsearch.service usr/lib/systemd/system/elasticsearch.service
  install -Dm644 "$srcdir"/elasticsearch@.service usr/lib/systemd/system/elasticsearch@.service
  install -Dm644 "$srcdir"/elasticsearch-user.conf usr/lib/sysusers.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch-tmpfile.conf usr/lib/tmpfiles.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch-sysctl.conf usr/lib/sysctl.d/elasticsearch.conf
  install -Dm644 "$srcdir"/elasticsearch.default etc/default/elasticsearch

  cd "$srcdir"/$pkgname-$pkgver
  cp -R lib modules plugins "$pkgdir"/usr/share/elasticsearch/

	cd config
	for conf in *; do
		install -Dm644 "$conf" "$pkgdir/etc/elasticsearch/$conf"
	done
	cd ..

  for script in elasticsearch{,-keystore,-plugin,-translog}; do
    install -Dm755 bin/$script "$pkgdir"/usr/bin/$script
  done

  ln -s ../../bin "$pkgdir"/usr/share/elasticsearch/bin
  ln -s ../../../var/log/elasticsearch "$pkgdir"/usr/share/elasticsearch/logs
  ln -s ../../../var/lib/elasticsearch "$pkgdir"/usr/share/elasticsearch/data
}
