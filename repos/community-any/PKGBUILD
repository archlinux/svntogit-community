# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: brent s. <bts[at]square-r00t[dot]net>

_pkgname=python-gitpython
_pypiname=GitPython
pkgbase=python-gitpython
pkgdesc="A python library used to interact with Git repositories"
pkgname=('python-gitpython' 'python2-gitpython')
pkgver=2.1.10
pkgrel=1
url="https://github.com/gitpython-developers/gitpython"
license=('BSD')
arch=("any")
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("${_pkgname}-${pkgver}.tar.gz::https://pypi.org/packages/source/G/${_pypiname}/${_pypiname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}.tar.gz.asc::https://pypi.org/packages/source/G/${_pypiname}/${_pypiname}-${pkgver}.tar.gz.asc")
sha512sums=('626933ccf8b29ea17846c6fa62211c78a022c9e02906d9107a858af0f12b02bd4c4cf9a24038c59393d5c66ad5258440a0893f4dc712264d43d8a9d8fc60a454'
            'SKIP')
validpgpkeys=('2CF6E0B51AAF73F09B1C21174D1DA68C88710E60') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
    cp -a "${srcdir}/${_pypiname}-${pkgver}"{,-py2}
}

build() {
    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py build
}

package_python-gitpython() {
    depends=('python' 'python-gitdb>=2.0.0')

    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-gitpython() {
    depends=('python2' 'python2-gitdb>=2.0.0')

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
