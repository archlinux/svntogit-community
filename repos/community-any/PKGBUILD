# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("yapf" "python2-yapf")
pkgver=0.20.1
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/yapf/archive/v$pkgver.tar.gz")
sha512sums=('87ab21283b1d2bb319db5c3ed810bf84a5aa9491f04178f186ab84beb6bf4f1052d52d1f7b30d7c1b5ea41c077976fc3ff43683a44b8b3e9170cbb21047e5177')

prepare() {
  cp -a yapf-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/yapf-$pkgver
  python setup.py build

  cd "$srcdir"/yapf-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/yapf-$pkgver
  nosetests3

  cd "$srcdir"/yapf-$pkgver-py2
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd yapf-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/yapf{,2}
}

# vim:set ts=2 sw=2 et:
