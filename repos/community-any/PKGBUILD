# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.7.1
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python-fluidity' 'python-lexicon' 'python-six' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz"
        invoke-devendor.patch)
sha512sums=('10671faf7eccb44491860a3272324fe79ea32b05d83d68f2c929bf1cec348f64a6d10234eeffd9ecaef402631df3d9c07f2d63daa0ee9e7e3ea7756af268ff09'
            'c5e414990f7a36455ff28288f90cccd62e570775523ea307f4ed2b7cdc7b9b0c1ee2d2eb046a048843fd2f016aed0a3cf270355297a1d73119f83edf8bc37be9')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
  rm -r invoke-$pkgver/invoke/vendor

  patch -d invoke-$pkgver -p1 < invoke-devendor.patch
}

build() {
  cd invoke-$pkgver
  python setup.py build
}

check() {
  cd invoke-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest tests
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
