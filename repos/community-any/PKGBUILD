# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Andreas Zwinkau
# Contributor: rabyte <rabyte__gmail>
# Contributor: Stefan Husmann < stefan-husmann@t-online.de>

pkgname=ttf-liberation
pkgver=2.00.5
pkgrel=1
pkgdesc='Red Hats Liberation fonts'
arch=('any')
license=('custom:OFL')
url=https://github.com/liberationfonts/liberation-fonts
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
makedepends=('fontforge' 'python-fonttools')
provides=('ttf-font')
source=("$url/archive/$pkgver/liberation-fonts-$pkgver.tar.gz"
        '30-0-liberation-mono.conf'
        '30-0-liberation-sans.conf'
        '30-0-liberation-serif.conf')
sha512sums=('4c7848bf6907d200f2d827141d30187e397361e667cea76cd3f5d790c95ebb4f2a76439904f57525bf649990f7003da3eda91e2c661db48c5f0f74c7ddd2007d'
            'bb6ae42be2fc5981cae30482702e9543f393db5ce084d2bfc4cfcdb07e61ad78a4d0757d689028d9e89123bfb82acd3129f0e6596e9a3d42546f4c2ce001248e'
            '8814f2fa087b6f36ad40418d0de6cee00b6f6654439a3a84b0ea09921d22c8e4ae1ae8c4897ddd40808bb9d76f2d0288b8ede05f94fbead80e4974ab9ba6e70a'
            'c243a16b2f7db18119b7e2161e3c4f6cbdd2965645a6a766b086196fa24d35b4426fc9ac789cea815d5061ef7aa5c1dd909840957bf06d0a065fb35bafa7be68')

build() {
  cd liberation-fonts-$pkgver
  make
}

package() {
  cd liberation-fonts-$pkgver

  install -Dm644 -t "$pkgdir/usr/share/fonts/TTF" \
    liberation-fonts-ttf-$pkgver/*.ttf

  # install fontconfig files
  install -Dm644 ../30-0-liberation-mono.conf \
    "$pkgdir/etc/fonts/conf.avail/30-$pkgname-mono.conf"
  install -Dm644 ../30-0-liberation-sans.conf \
    "$pkgdir/etc/fonts/conf.avail/30-$pkgname-sans.conf"
  install -Dm644 ../30-0-liberation-serif.conf \
    "$pkgdir/etc/fonts/conf.avail/30-$pkgname-serif.conf"

  # install license
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
