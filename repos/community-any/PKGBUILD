# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.23.0
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-urllib3' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpserver' 'python-tomli'
              'python-tomli-w')
source=("https://github.com/getsentry/responses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6a4d8f07eee0846c9507513002ff4cf157144c3d35790c888aa163409be01fc14331c8b4d1eae400d8d2fefa130a396f8739fbc12813b1d16b9e41af0fad535b')

prepare() {
  cd responses-$pkgver
  sed -i '/types-PyYAML/d' setup.py
}

build() {
  cd responses-$pkgver
  python setup.py build
}

check() {
  cd responses-$pkgver
  pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
