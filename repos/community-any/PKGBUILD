# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.240
_portal_pkgver=0.9.1
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://github.com/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-python' 'gtk3' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'python')
optdepends=('easytag: Modify tags'
            'flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support'
            'youtube-dl: YouTube playback')
_commit='8f3fec20c99498a48f33df00eb64d5af20f81a38'
source=("https://github.com/gnumdk/lollypop/releases/download/${pkgver}/lollypop-${pkgver}.tar.xz"
        "https://github.com/gnumdk/lollypop-portal/releases/download/${_portal_pkgver}/lollypop-portal-${_portal_pkgver}.tar.xz")
sha256sums=('78cbd57c41f5d97f8c9286f7e01c0c3d0fde17a3dc01a4a5bae1bf665e653e58'
            'd7c5ae781eb3a7d24b1303b6f0c618d386e7ee0c977f87220f889c12e2713e6f')

build() {
  cd lollypop-portal-${_portal_pkgver}

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lollypop'
  make

  cd ../lollypop-${pkgver}

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lollypop' \
    --disable-schemas-compile
  make
}

package() {
  cd lollypop-portal-${_portal_pkgver}

  make DESTDIR="${pkgdir}" install

  cd ../lollypop-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
