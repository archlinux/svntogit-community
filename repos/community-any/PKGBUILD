# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-spec-validator
pkgver=0.4.0
pkgrel=2
pkgdesc="A Python library that validates OpenAPI Specs against the OpenAPI 2.0 (aka Swagger) and OpenAPI 3.0.0 specification"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache')
arch=('any')
depends=('python-jsonschema' 'python-openapi-schema-validator' 'python-six' 'python-yaml' 'python-setuptools' 'python-requests')
makedepends=('python-build' 'python-installer' 'python-poetry')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-flake8' 'python-pytest-cov'
              'python-tox')
source=("$pkgname-$pkgver.tar.gz::https://github.com/p1c2u/openapi-spec-validator/archive/$pkgver.tar.gz")
sha512sums=('d2eaf22c75e72eb5061a916eb37b13179a5ba65eb8a00ec42e81c6b4168239bc8613388f2d77ce35113d260385b175280d8863a3deedf18ea5aa93a79f058419')

build() {
  cd openapi-spec-validator-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd openapi-spec-validator-$pkgver
  PYTHONPATH="$PWD" pytest
}

package() {
  cd openapi-spec-validator-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
