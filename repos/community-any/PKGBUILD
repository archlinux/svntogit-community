# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=4.0.1
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
         'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz")
sha512sums=('cae1b6341499d58fbdbae7a5120b49382a6c9230eeacfc6b408d78e5f72f1ff6983d16eea2c390a1af397fc8319c2f17be8f35306aa298d021f169fb751688cb')

export PBR_VERSION=$pkgver

build() {
  cd oslo.serialization-$pkgver
  python setup.py build
}

check() {
  cd oslo.serialization-$pkgver
  stestr run
}

package() {
  cd oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
