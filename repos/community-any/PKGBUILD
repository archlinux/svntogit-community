# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thayne <astrothayne at gmail dot com>
# Contributor: Yiqiao Wang <yiqiao.wang96 at gmail dot com>
# Contributor: wenLiangcan <boxeed at gmail dot com>

pkgname=ammonite
pkgver=2.4.0
_scalaversion=2.13.2
pkgrel=1
pkgdesc="A cleanroom re-implementation of the Scala REPL from first principles. "
arch=('any')
url="https://github.com/lihaoyi/Ammonite"
license=('MIT')
depends=('bash' 'java-environment')
makedepends=('mill')
source=("https://github.com/lihaoyi/Ammonite/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('568719f7e8c367a64c1634c96ab58dcaa887a168991d17585343c582a757214db2823bb24eb43aa66055cd071baa35440c5a16a6618d66eec769818a1152ab2c')

prepare() {
  cd Ammonite-$pkgver
  sed -i "s/'git/'true/;s/\"git\"/\"true\"/;s/\$latestTaggedVersion-\$commitsSinceTaggedVersion-\$gitHash/$pkgver/;s/^    \\.toInt$//;s/(tagName, false)/(\"$pkgver\", false)/" build.sc
}

build() {
  cd Ammonite-$pkgver
  TRAVIS_TAG=$pkgver mill -i amm[$_scalaversion].assembly
}

package() {
  cd Ammonite-$pkgver
  install -dm755 "$pkgdir"/usr/bin
  (echo "#!/bin/sh"; cat out/amm/$_scalaversion/assembly/dest/out.jar) > "$pkgdir"/usr/bin/amm
  chmod +x "$pkgdir"/usr/bin/amm
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
