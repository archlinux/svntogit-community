# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

_pyname=json-stream
pkgname=python-${_pyname}
pkgver=2.3.0
pkgrel=1
pkgdesc='Simple streaming JSON parser'
arch=(any)
url="https://github.com/daggaz/json-stream"
license=('MIT')
depends=('python')
makedepends=(python-build python-installer python-setuptools python-wheel)
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
b2sums=('d18e3356bbf900e71250a55377ad76bbfd552ea6eea90d2b1d89e84733b0c507cbc507747bed123682ee9a3fc3f167b2fe27be3925c6809c60a02bc236a0db87')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
