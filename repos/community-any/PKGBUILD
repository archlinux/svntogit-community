# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-configuration
pkgver=4.4.1
pkgrel=1
pkgdesc="Zope Configuration Markup Language (ZCML)"
arch=('any')
url="https://github.com/zopefoundation/zope.configuration"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema' 'python-zope-i18nmessageid')
checkdepends=('python-manuel' 'python-zope-testing' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.configuration/archive/$pkgver.tar.gz")
sha512sums=('11bdfa72f06c5416a73c4ca9a986ae93aa351ad02c3f6b6bd2eb9f36fd155aacf224fedf4c888ceecf9b3fb5a2acb0c11a013fdbb21bb4a0bfabfa15fed20249')

build() {
  cd "$srcdir"/zope.configuration-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.configuration-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || echo "Tests failed"
}

package() {
  cd "$srcdir"/zope.configuration-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
