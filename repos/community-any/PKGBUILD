# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=1.10.0
pkgrel=2
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-six' 'python-cachetools')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography' 'python-freezegun')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver.tar.gz")
sha512sums=('5167e127481bf193ba93e0d4165a3f16898b9ad341832046a5acab01dc5d750aa4e553cc9f86853104878dd5c90ad502610f451d77aab8f643e71bf9e790d278')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         google-auth-library-python-$pkgver/google/auth/transport/urllib3.py
}

build() {
  cd "$srcdir"/google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd "$srcdir"/google-auth-library-python-$pkgver
  python setup.py pytest --addopts "tests -k 'not test__make_default_http_without_certifi'"
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
