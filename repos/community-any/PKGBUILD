# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-objgraph
pkgname=(python-objgraph python2-objgraph)
pkgver=3.4.1
pkgrel=3
pkgdesc="Draws Python object reference graphs with graphviz"
url="https://github.com/mgedmin/objgraph"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-mock' 'python2-mock' 'graphviz')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mgedmin/objgraph/archive/$pkgver.tar.gz")
sha512sums=('9139db6ca96801330ebc14ec485aae360de012b76060a9ea25d9ba1e7113bcdd6f161bab2313e9c8ab3cd1448dc466d028a394d6729d965884f91384a4fc1338')

prepare() {
  # Only for ipython support
  sed -i "/'graphviz'/d" objgraph-$pkgver/setup.py

  cp -a objgraph-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/objgraph-$pkgver
  python setup.py build

  cd "$srcdir"/objgraph-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/objgraph-$pkgver
  python setup.py test

  cd "$srcdir"/objgraph-$pkgver-py2
  python2 setup.py test
}

package_python-objgraph() {
  depends=('python')

  cd objgraph-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-objgraph() {
  depends=('python2')

  cd objgraph-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
