# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: googol <googol@posteo.de>
# Contributor: Oliver Mangold <o.mangold at gmail dot com>

pkgname=python-alembic
pkgver=1.7.3
pkgrel=1
pkgdesc='Lightweight database migration tool for usage with SQLAlchemy'
url='https://github.com/sqlalchemy/alembic'
arch=('any')
license=('MIT')
depends=('python' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
makedepends=('python-setuptools' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
checkdepends=('python-pytest' 'python-mock')
source=(https://pypi.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz{,.asc})
sha512sums=('cf6d6dd0913d1552ed82ceefc6254af3ef9aea46c08a2d0879a941e7807e5ed41195f5d0b98d108f77a2db5a3c502115af1ce4c9c76618f8c720255dbe58bcad'
            'SKIP')
b2sums=('209ccb03b5745eda2dd54a9a6376ab62dfd5902b497a86cde460c258c87917a534bbe5a028be58444fcbf3acb9c7d84ce5f26798a636089b5d869a238bc2f23b'
        'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1') # Michael Bayer <classic@zzzcomputing.com>

prepare() {
  cd alembic-${pkgver}
  # do not treat warnings as errors
  sed 's|-p no:warnings||g' -i setup.cfg
}

build() {
  cd alembic-${pkgver}
  python setup.py build
}

check() {
  cd alembic-${pkgver}
  py.test
}

package() {
  cd alembic-${pkgver}
  python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
