# Maintainer:

pkgname=perl-file-pushd
pkgver=1.016
pkgrel=5
pkgdesc="Change directory temporarily for a limited scope"
arch=('any')
url="https://metacpan.org/release/File-pushd"
license=('Apache')
depends=('perl')
makedepends=('perl-extutils-makemaker')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/D/DA/DAGOLDEN/File-pushd-${pkgver}.tar.gz")
sha256sums=('d73a7f09442983b098260df3df7a832a5f660773a313ca273fa8b56665f97cdc')

build() {
  cd File-pushd-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-pushd-${pkgver}
  make test
}

package() {
  cd File-pushd-${pkgver}
  make DESTDIR="$pkgdir" install
}
