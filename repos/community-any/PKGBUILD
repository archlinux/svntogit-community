# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible
pkgver=1.8.4
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='http://www.ansible.com'
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-paramiko' 'python2-jinja')
makedepends=('asciidoc' 'fakeroot')
backup=('etc/ansible/ansible.cfg')
source=(http://releases.ansible.com/ansible/$pkgname-$pkgver.tar.gz
        0001-Fix-os_family-and-distribution-on-archlinux.patch)
md5sums=('e0bbf484e9fa49cd119f50ad65905978'
         '2d00eecedea58697186087b7c446f112')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-Fix-os_family-and-distribution-on-archlinux.patch
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 COPYING "$pkgdir"/usr/share/doc/ansible/COPYING

  install -d "$pkgdir"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "$pkgdir"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "$pkgdir"/etc/ansible/ansible.cfg

  install -d "$pkgdir"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "$pkgdir"/usr/share/man/man1
}
