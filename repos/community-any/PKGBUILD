# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgbase=python-redis
pkgname=('python-redis' 'python2-redis')
_basename=redis
pkgver=2.10.3
pkgrel=2
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="http://github.com/andymccurdy/redis-py"
license=('MIT')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/r/${_basename}/${_basename}-${pkgver}.tar.gz")
md5sums=('7619221ad0cbd124a5687458ea3f5289')

prepare() {
  cp -a ${_basename}-${pkgver}{,-py2}
}

build() {
  cd ${_basename}-${pkgver}
  python setup.py build

  cd ../${_basename}-${pkgver}-py2
  python2 setup.py build
}

package_python-redis() {
  depends=('python')

  cd ${_basename}-${pkgver}
  python setup.py install --root=${pkgdir}/ --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-redis() {
  depends=('python2')

  cd ${_basename}-${pkgver}-py2
  python2 setup.py install --root=${pkgdir}/ --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
