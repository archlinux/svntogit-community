# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonlines
pkgver=2.0.0
pkgrel=1
pkgdesc="Library with helpers for the jsonlines file format"
url="https://github.com/wbolster/jsonlines"
license=('BSD')
arch=('any')
depends=('python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/wbolster/jsonlines/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('24a86ca3ff100eb16ba573130ce8f0d933dbf4686c73f1251178195265ef8d11f0786cda13635bf5828a36c80903a77fed37147f3ffb81353769112971bf5952')

build() {
  cd jsonlines-$pkgver
  python setup.py build
}

check() {
  cd jsonlines-$pkgver
  python -m pytest
}

package() {
  cd jsonlines-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
