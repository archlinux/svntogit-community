# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-copy' 'python2-zope-copy')
pkgver=4.0.3
pkgrel=2
pkgdesc="Pluggable object copying mechanism"
arch=('any')
url="https://github.com/zopefoundation/zope.copy"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-zope-testing' 'python2-zope-testing'
              'python-zope-location' 'python2-zope-location')
source=("git+https://github.com/zopefoundation/zope.copy.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.copy{,-py2}
}

build() {
  cd "$srcdir/zope.copy"
  python setup.py build

  cd "$srcdir/zope.copy-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.copy"
  nosetests3

  cd "$srcdir/zope.copy-py2"
  nosetests2
}

package_python-zope-copy() {
  depends=('python-setuptools' 'python-zope-interface')

  cd "$srcdir/zope.copy"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-copy() {
  depends=('python2-setuptools' 'python2-zope-interface')

  cd "$srcdir/zope.copy-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
