# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jerome Leclanche <jerome@leclan.ch>

pkgbase=python-webtest
pkgname=(python-webtest python2-webtest)
pkgver=2.0.35
pkgrel=1
pkgdesc="Helper to test WSGI applications"
arch=('any')
url='http://webtest.pythonpaste.org/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-webob' 'python2-webob' 'python-six'
             'python2-six' 'python-beautifulsoup4' 'python2-beautifulsoup4' 'python-waitress'
             'python2-waitress')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock'
              'python-pyquery' 'python2-pyquery'  'python-pastedeploy'
              'python2-pastedeploy' 'python-wsgiproxy2' 'python2-wsgiproxy2'
              'python-pylons-sphinx-themes' 'python2-pylons-sphinx-themes')
source=("https://pypi.io/packages/source/W/WebTest/WebTest-$pkgver.tar.gz")
sha512sums=('ecad031702a9d2031d4e2230dc8213aaa6b1a3e9e6add296ee796aa2ef77d81c992dc24e481f721bc34711d1e33f3932d56cec4be3d62ee82a71fb405942c339')

prepare() {
  cp -a WebTest-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/WebTest-$pkgver
  python setup.py build

  cd "$srcdir"/WebTest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/WebTest-$pkgver
  PYTHONPATH=".:$PYTHONPATH" nosetests3

  cd "$srcdir"/WebTest-$pkgver-py2
  PYTHONPATH=".:$PYTHONPATH" nosetests2
}

package_python-webtest() {
  depends=('python-webob' 'python-six' 'python-beautifulsoup4' 'python-waitress')
  optdepends=('python-pyquery: for response.pyquery'
              'python-lxml: for response.lxml')

  cd WebTest-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 license.rst "$pkgdir"/usr/share/licenses/$pkgname/license.rst
}

package_python2-webtest() {
  depends=('python2-webob' 'python2-six' 'python2-beautifulsoup4' 'python2-waitress')
  optdepends=('python2-pyquery: for response.pyquery'
              'python2-lxml: for response.lxml')

  cd WebTest-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 license.rst "$pkgdir"/usr/share/licenses/$pkgname/license.rst
}
