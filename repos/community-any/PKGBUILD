# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-poe
pkgver=1.370
pkgrel=1
pkgdesc="portable multitasking and networking framework for Perl "
arch=(any)
url="https://search.cpan.org/dist/POE"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-io-tty>=1.08' 'perl-io-pipely')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/POE-$pkgver.tar.gz)
sha256sums=('57de2b635b15fa3a31a9e55dd51122149e5414e1158ee82235062634ee18a693')

build() {
  cd  "$srcdir"/POE-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/POE-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
