# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=gufw
pkgver=20.04.1
pkgrel=3
pkgdesc='Uncomplicated way to manage your Linux firewall'
url='https://gufw.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'polkit' 'python' 'python-gobject' 'ufw' 'webkit2gtk' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('intltool' 'python-distutils-extra')
source=(https://github.com/costales/gufw/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('fbc5a8ce863c3178354230ca4224b6c32e2aee691d3db1ba397f0b0c8a7acdd3d0a2f739c62ec5f99957e422387fd3ccee4ddce9959160c3c50c81a769b49981')
b2sums=('af273dc6430ae2d77eb857f3b00428ca12ace441e355642fd44adfaf0f6b369baa2fbd5d25ec57a8317911166330777d2ef94ea7a59eff8ddb6e51d6c322ccfd')

prepare() {
  cd ${pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  sed -E "s|python3\.[0-9]|python${PYTHONVERSION}|g" -i bin/gufw-pkexec
  sed -E 's/(loginctl\|grep \$\(whoami\))/\1|sort -n|tail -n 1/g' -i bin/gufw
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  # required so desktop, icon and translation files get installed
  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:
