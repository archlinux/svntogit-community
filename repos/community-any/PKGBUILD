# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requirementslib
pkgver=1.5.1
pkgrel=1
pkgdesc="A tool for converting between pip-style and pipfile requirements"
url="https://github.com/sarugaku/requirementslib"
license=('MIT')
arch=('any')
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-distlib' 'python-first'
         'python-orderedmultidict' 'python-packaging' 'python-pep517' 'python-pip-shims'
         'python-plette' 'python-cerberus' 'python-requests' 'python-setuptools' 'python-tomlkit'
         'python-vistir')
makedepends=('python-invoke' 'python-wheel' 'python-parver' 'git')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-timeout'
              'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/requirementslib/archive/$pkgver.tar.gz"
        "git+https://github.com/kennethreitz/requests.git#commit=64bde6582d9b49e9345d9b8df16aaa26dc372d13"
        "git+https://github.com/sarugaku/shellingham.git#commit=9abe7464dab5cc362fe08361619d3fb15f2e16ab")
sha512sums=('4f1b05a633989ea43517c78f143439a992e67d5e300cb89b8a65bc2cdc5b2e57de4192ca2bc9aea65efe5d441fde0e7347fb8ad7891e6220550c3da4737a93dc'
            'SKIP'
            'SKIP')

prepare() {
  cd requirementslib-$pkgver
  sed -i 's/pkg_resources.extern.//' src/requirementslib/models/*.py

  cd tests/artifacts/git
  rmdir requests shellingham
  ln -s "$srcdir"/requests
  ln -s "$srcdir"/shellingham
}

build() {
  cd requirementslib-$pkgver
  python setup.py build
}

check() {
  cd requirementslib-$pkgver
  python setup.py pytest
}

package() {
  cd requirementslib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
