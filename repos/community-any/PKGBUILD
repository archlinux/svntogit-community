# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=java-batik
pkgver=1.16
pkgrel=1
pkgdesc='SVG library for Java'
arch=('any')
url='https://xmlgraphics.apache.org/batik/'
license=('APACHE')
depends=('java-runtime' 'java-commons-io' 'java-commons-logging' 'java-xmlgraphics-commons')
makedepends=('java-environment' 'apache-ant' 'unzip')
source=("https://www.apache.org/dist/xmlgraphics/batik/source/batik-src-$pkgver.tar.gz"{,.asc})
sha512sums=('024ab7a204d00b6c855e26c62a326dfcde5fb486a7635890c6fcb37d421c7195ae04b3d292daabc0fcb6564f55bfa93d6fef15cf4c2dad77e569ca17d1eeb250'
            'SKIP')
validpgpkeys=('9D5F9D9B3E21B585E8F4551082036989D56B5F17'  # Luis Bernardo
              '5C9A30FF22B2C02F30261C305B93F1DF7CDB6DEA') # Simon Steiner

build() {
  cd batik-$pkgver

  ant all-jar
}

package() {
  install -d "$pkgdir"/usr/share/java/batik
  install -m 644 "$srcdir"/batik-$pkgver/batik-$pkgver/lib/batik-all-$pkgver.jar "$pkgdir"/usr/share/java/batik/batik-all-$pkgver.jar

  ln -s batik-all-$pkgver.jar "$pkgdir"/usr/share/java/batik/batik-all.jar

  cp --no-preserve=ownership "$srcdir/batik-$pkgver/lib"/*.jar "$pkgdir"/usr/share/java/batik/
}

