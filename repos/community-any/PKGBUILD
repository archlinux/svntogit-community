# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vistir
pkgver=0.7.2
pkgrel=1
pkgdesc="Miscellaneous utilities for dealing with filesystems, paths, projects, subprocesses, and more"
url="https://github.com/sarugaku/vistir"
license=('BSD')
arch=('any')
depends=('python-colorama')
makedepends=('python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-hypothesis-fspaths' 'python-pytest-timeout' 'python-requests'
              'python-yaspin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/vistir/archive/v$pkgver.tar.gz")
sha512sums=('0a6b07fc924b9d2f2d40db570c0f5889c009c63c8e864d98e8b4fa7bc7567d33d8ca4cae8ce1ed346972b21aee28d878e9d5d3c235cbb0b439469e44fb884831')

build() {
  cd vistir-$pkgver
  python setup.py build
}

check() {
  cd vistir-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd vistir-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
