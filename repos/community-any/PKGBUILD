# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=graphviz
pkgname=python-graphviz
pkgver=0.15
pkgrel=1
pkgdesc="Simple Python interface for Graphviz"
arch=('any')
url="https://github.com/xflr6/${_pkgname}"
license=('MIT')
depends=('graphviz' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.zip")
sha256sums=('2b85f105024e229ec330fe5067abbe9aa0d7708921a585ecc2bf56000bf5e027')
b2sums=('81d4a121ca2654e6c08570ede7a1f5ef580b6a281358bf32f4178898fc12cfae21b10ce8f4f6fba4644e3275162cc09dbf5ae287a114fb8503be98d6a2fbf728')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run python-coverage in unittests :/
    sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
    # don't add pointless dependency on python-mock needed on python 3.5
    sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
