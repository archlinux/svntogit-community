# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>

pkgname=gbrainy
pkgver=2.2.4
pkgrel=1
epoch=1
pkgdesc="A brain teaser game and trainer to have fun and to keep your brain trained"
arch=('any')
url="https://wiki.gnome.org/Apps/gbrainy"
license=('GPL2')
depends=('gtk-sharp-2' 'mono-addins' 'librsvg' 'hicolor-icon-theme'
         'desktop-file-utils')
makedepends=('intltool' 'gettext' 'gnome-doc-utils')
install=gbrainy.install
source=(http://gent.softcatala.org/jmas/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('e8c940f0de2696335c2354139c856c6e2b9ae2ea57f90f7b96a4bd186a4fa449')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export MONO_SHARED_DIR="$srcdir"
  ./configure \
    --prefix=/usr \
    --enable-addins
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
