# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-argh
pkgver=0.27.1
_commit=6cd34fdcd5369a23b8a1c75764e5b43de5885fef
pkgrel=1
pkgdesc="An unobtrusive argparse wrapper with natural syntax"
arch=(any)
url="https://pypi.python.org/pypi/argh"
license=('LGPL3')
depends=('python')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest' 'python-iocapture')
source=("git+https://github.com/neithere/argh.git#commit=$_commit")
sha256sums=('SKIP')

build() {
  cd argh
  python setup.py build
}

check() {
  cd argh
  # https://github.com/neithere/argh/issues/148#issuecomment-981508602
  pytest --deselect test/test_integration.py::test_invalid_choice \
         --deselect test/test_integration.py::test_explicit_cmd_name
}

package() {
  cd argh
  python setup.py install --root="$pkgdir" --optimize=1
}
