# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cachetools
pkgver=5.2.1
pkgrel=1
pkgdesc="Extensible memoizing collections and decorators"
url="https://github.com/tkem/cachetools"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tkem/cachetools/archive/v$pkgver.tar.gz")
sha512sums=('231ee0a4f9951f176eae227bb22dd62106f61354fbc255ba9544192df0c77f616a3cd855efd1d6c9643b9e582546734b42aac2d6983298773eeddbe3d2c35f5a')

build() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py pytest
}

package() {
  cd cachetools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
