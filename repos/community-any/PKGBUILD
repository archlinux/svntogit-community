# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-waitress
pkgname=('python-waitress' 'python2-waitress')
pkgver=1.3.1
pkgrel=3
pkgdesc="Waitress WSGI server"
arch=('any')
license=('ZPL')
url="https://github.com/Pylons/waitress"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/waitress/archive/v$pkgver.tar.gz")
sha512sums=('6a97cd1c1171610528744978c8faf6b326b9624bd142aaa5dc049ed2afe172ef9aeb15831845520ea16ed477ff091ae9299881fe4c761183ce79fa63a23c504d')

prepare() {
  cp -a waitress-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/waitress-$pkgver
  python setup.py build

  cd "$srcdir"/waitress-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/waitress-$pkgver
  nosetests3

  cd "$srcdir"/waitress-$pkgver-py2
  nosetests2
}

package_python-waitress() {
  depends=('python-setuptools')

  cd waitress-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-waitress() {
  depends=('python2-setuptools')

  cd waitress-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/waitress-serve{,2}
}
