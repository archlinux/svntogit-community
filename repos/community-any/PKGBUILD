# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Evgeniy Filimonov <evgfilim1@gmail.com>

pkgname=python-aiogram
pkgver=2.18
pkgrel=1
pkgdesc="A pretty simple and fully asynchronous library for Telegram Bot API written with asyncio and aiohttp"
arch=('any')
url="https://github.com/aiogram/aiogram"
license=('MIT')
depends=('python-aiohttp' 'python-babel')
makedepends=('python-setuptools')
checkdepends=('python-aiohttp-socks' 'python-aioredis' 'python-aresponses' 'python-pytest'
              'python-pytest-lazy-fixture' 'pifpaf' 'redis')
optdepends=('python-uvloop: fast, drop-in replacement of the built-in asyncio event loop'
            'python-ujson: ultra fast JSON encoder and decoder written in pure C'
            'python-rapidjson: extremely fast C++ JSON parser and serialization library'
            'python-emoji: emojize and demojize support'
            'python-aioredis: Redis storage support'
            'python-aiohttp-socks: SOCKS4(a) and SOCKS5 proxy support'
#            'python-rethinkdb: RethinkDB storage support'  # No such package yet
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b48c59ee652cddb6f32deb583f36ce5accc56f34ae4f63ba8e1bf568702cf4237184b7f99c3d55c69576df17640d916ac508b3164a85bb39b08d1b84e56275cb')

prepare() {
  cd aiogram-$pkgver
  sed -e '/import certifi/d' \
      -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
      -i aiogram/bot/base.py
}

build() {
  cd aiogram-$pkgver
  python setup.py build
}

check() {
  cd aiogram-$pkgver
  pifpaf run redis -- bash -c 'python -m pytest --redis $PIFPAF_REDIS_URL'
}

package() {
  cd aiogram-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -Dm644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
