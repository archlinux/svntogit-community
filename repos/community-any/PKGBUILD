# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: XZS <d dot f dot fischer at web dot de>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=waf
pkgver=2.0.21
pkgrel=3
pkgdesc='General-purpose build system modelled after Scons'
url="http://waf.io/"
arch=('any')
license=('BSD')
depends=('python')
makedepends=('unzip')
provides=('python-waf')
source=("https://waf.io/${pkgname}-${pkgver}.tar.bz2"{,.asc}
        'building-waf.md')
sha512sums=('b76c32d5992757af1f483dc59dc439e414d06d0325eaf1c8ac20bc20ee62bee25d8a97099d80e6d8e3a2ca9c9aeadf0543c2ba5005479e9617c1fbcfd60ba6b8'
            'SKIP'
            'fce4e44c3eff4b87165ad4c6856c028f90437af26ed0d2e7cdef825987a60ebe16afda3238813bc2aa4f4f39df2a705a9df870f93c85e9d755d5d1636f07af70')
b2sums=('ba7d85c50d4ba1ad566620f7da2779ac1a4e0a5fcc5bcc639c64a59750bf9df40318070f771098d21335cc8d494b4043ecbdae1baa67c4bce3ce791ce2e538d5'
        'SKIP'
        'a42039f8419417a312ccdb65a01b22c5e1133ebaa3e3a905387565bf2e3991ce3efb74f3b2fb0ec5e8408b665044946337c6ecfd4af1083ae2811aad9dc7d76e')
validpgpkeys=('0B3972B2D9E32EAB423D2E0F22BE0C62FFBFA548') # Thomas Nagy <tnagy@waf.io>

prepare() {
  local _py_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  cd "$pkgname-$pkgver"
  # make waf find its waflib
  sed -e "/test(i/ s|dirname|'python${_py_ver}/site-packages/waf'|" -i waf-light
  # license needs to be extracted:
  # https://gitlab.com/ita1024/waf/issues/2251
  head -n 30 waf | tail -n 28 | sed -e 's/# //g; s/"""//g' > LICENSE
}

build() {
  cd "$pkgname-$pkgver"
  ./waf-light \
    configure --prefix=/usr \
    build --make-waf

  # removing compiled in vendored library from executable
  sed -i '/^#==>$/,/^#<==$/d' waf
}

package() {
  local _py_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  cd "$pkgname-$pkgver"
  install -vDm 755 waf -t "$pkgdir/usr/bin"
  install -vDm 644 wscript -t "$pkgdir/usr/share/$pkgname"

  # installing, byte-compiling waflib and stripping pkgdir from it
  install -vDm 644 waflib/*.py \
    -t "${pkgdir}/usr/lib/python${_py_ver}/site-packages/waf/waflib"
  install -vDm 644 waflib/Tools/*.py \
    -t "${pkgdir}/usr/lib/python${_py_ver}/site-packages/waf/waflib/Tools"
  install -vDm 644 waflib/extras/*.py \
    -t "${pkgdir}/usr/lib/python${_py_ver}/site-packages/waf/waflib/extras"
  python -m compileall -d "/usr/lib/python${_py_ver}" \
    "${pkgdir}/usr/lib/python${_py_ver}"
  python -O -m compileall -d "/usr/lib/python${_py_ver}" \
    "${pkgdir}/usr/lib/python${_py_ver}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {README,../building-waf}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
