# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
pkgver=1.19.1
pkgrel=2
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/awslabs/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-cov python-yaml python-mock python-parameterized)
source=("https://github.com/awslabs/serverless-application-model/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('3f0e1e5efdb72ab944ef99677f693f84222e87cd3048125ab3a2bc36c72b016a')

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver
  pytest -v tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  rm -rv "$pkgdir"/usr/lib/python*/site-packages/tests
}
