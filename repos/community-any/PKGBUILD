# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-utils
pkgver=0.003
pkgrel=1
pkgdesc="Utility functions for PPI"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-Utils"
depends=('perl-b-keywords' 'perl-ppi')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DB/DBOOK/PPIx-Utils-$pkgver.tar.gz")
sha512sums=('7f56bc2bf2afbf1f6d3c0e5bc0ed21752be809714e32b256feb1ea39125c7671c6ef949e2c122a95a41d3a603f5d5253133886f3700890387ad6cf0732ab87c6')

build() {
  cd PPIx-Utils-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-Utils-$pkgver
  make test
}

package() {
  cd PPIx-Utils-$pkgver
  make DESTDIR="$pkgdir" install
}
