# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=2.91.2
_commit=b046682325717a2ba24b1027a71b948d19be168b
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-sound-theme' 'deepin-artwork-themes')
makedepends=('git')
conflicts=('dde-daemon<=2.92.1')
replaces=('deepin-default-gsettings')
groups=('deepin')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/deepin-desktop-schemas.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin_default_background.jpg'#" overrides/com.deepin.wrap.gnome.desktop.override
}

build() {
  cd "${pkgname}"
  make
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
}
