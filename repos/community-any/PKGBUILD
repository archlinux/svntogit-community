# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>

pkgbase=python-pyserial
pkgname=('python-pyserial' 'python2-pyserial')
pkgver=3.4
pkgrel=2
pkgdesc="Multiplatform Serial Port Module for Python"
arch=('any')
url="https://github.com/pyserial/pyserial"
license=('custom:PYTHON')
makedepends=('python2' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyserial/pyserial/archive/v$pkgver.tar.gz")
sha256sums=('ca25c777d06ad0b4b129910642aaa75f724157c80e2898a8e3219adc5599a2a1')

build() {
  cp -a "$srcdir"/pyserial-${pkgver} "$srcdir"/py2serial-${pkgver}
}

package_python-pyserial() {
  depends=('python')

  cd "$srcdir"/pyserial-${pkgver}
  python setup.py install --root="$pkgdir"
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyserial() {
  depends=('python2')

  cd "$srcdir"/py2serial-${pkgver}
  python2 setup.py install --root="$pkgdir"
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv "$pkgdir"/usr/bin/miniterm.py "$pkgdir"/usr/bin/miniterm2.py
}
