# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=simde
pkgver=0.7.4
pkgrel=2
pkgdesc="Implementations of SIMD instruction sets"
arch=(any)
url="https://github.com/simd-everywhere/simde"
license=(MIT)
makedepends=(meson)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  $pkgname-0.7.4-fix_define.patch::https://github.com/simd-everywhere/simde/commit/f9cf4675d101eef2dd70dabbb4d6806b405eac29.patch
)
sha512sums=('9add192021014f503699dedff8644ad8079a6381302fe56b91950a3b498b58ba7d069a4779007738edfad1ec57dbce02d462bd833a517240e8ff992e3867868a'
            '245a92fffdf6e16f47a77cfabd8388d1c253a894285d50875b5587f73a70119e307ae4a2f3329865e5d918295c39320bf39dc683fc4c9ea1214684bb3a6c46fd')
b2sums=('553e986a345a7ab832995b4079adbc51287d3c22186a278255d2e5cc4a131c9c93c06fb21754601dd974d16b3fe3e455078c35f761e22f584c1b1af8c70b996c'
        '808659a90ac2b5262e6515ea481579b396c22ba1428de91c34339236f1fd53e28fe45b899d0bd240f61be320a402b87e2559a407967ab35450a8fdf217e91828')

prepare() {
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.7.4-fix_define.patch
}

build() {
  arch-meson build $pkgname-$pkgver
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
