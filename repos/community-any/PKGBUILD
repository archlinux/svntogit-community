# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-contracts
pkgver=0.17
_commit=987ec3593e2f530ce0edb6d60650ff29ea635250
pkgrel=2
pkgdesc='Contracts for Ruby'
arch=(any)
url='https://github.com/egonSchiele/contracts.ruby'
license=(BSD)
depends=(ruby)
makedepends=(git)
checkdepends=(ruby-rspec)
options=(!emptydirs)
source=(git+https://github.com/egonSchiele/contracts.ruby.git#commit=$_commit)
sha256sums=('SKIP')

build() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby
  gem build contracts.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    contracts-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd contracts.ruby
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd contracts.ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
