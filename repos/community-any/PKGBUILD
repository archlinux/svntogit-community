# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>
# Contributor: Asa Marco <marcoasa90[at]gmail[.]com>

pkgname=kupfer
pkgver=319
pkgrel=2
pkgdesc="Launcher application written in python. Similar to Gnome-Do / Launchy"
arch=('any')
url="https://kupferlauncher.github.io/"
license=('GPL3')
depends=('libkeybinder3' 'libwnck3' 'python-cairo' 'python-dbus' 'python-gobject' 'python-xdg')
makedepends=('gnome-doc-utils' 'intltool' 'python-docutils')
source=(https://github.com/kupferlauncher/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz{,.sig})
validpgpkeys=('2578ABA1F9023159DFF211B7368712C4D7A44445') # Ulrik Sverdrup
sha256sums=('22357233984a6588a9f9743002363af38ea63b5c2684af487693a13c42973d31'
            'SKIP')

build() {
  cd $pkgname-v$pkgver
  ./waf configure --prefix=/usr \
                  --no-update-mime \
                  --no-update-icon-cache
  ./waf
}

package() {
  cd $pkgname-v$pkgver
  ./waf install --destdir="$pkgdir"
}
