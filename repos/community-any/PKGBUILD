# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: gmes78 <gmes.078 at gmail dot com>

pkgname=python-qasync
pkgver=0.23.0
pkgrel=2
pkgdesc="Python library for using asyncio in Qt-based applications"
arch=(any)
url="https://github.com/CabbageDevelopment/qasync"
license=('BSD')
depends=('python')
optdepends=('pyside2' 'pyside6' 'python-pyqt5' 'python-pyqt6')
makedepends=('python-setuptools')
checkdepends=('python-pytest-raises' 'pyside2' 'pyside6' 'python-pyqt5' 'python-pyqt6'
              'xorg-server-xvfb')
source=("https://github.com/CabbageDevelopment/qasync/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5d0f4a1363461d88f9f3b6583cf87cc87fef08f18adcf26682cf40f2fd4202d42887e0b15e52c505c9565e59284f3809f222df0c96c5d78f5261556645715213')

build() {
  cd qasync-$pkgver
  python setup.py build
}

check() {
  cd qasync-$pkgver
  QT_API=pyside2 xvfb-run python -m pytest
  QT_API=pyside6 xvfb-run python -m pytest
  QT_API=pyqt5 xvfb-run python -m pytest
  QT_API=pyqt6 xvfb-run python -m pytest
}

package() {
  cd qasync-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
