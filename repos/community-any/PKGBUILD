# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgbase=python-arrow
pkgname=('python-arrow' 'python2-arrow')
pkgver=0.10.0
pkgrel=1
pkgdesc='Better dates and times for Python'
arch=('any')
url='http://crsmithdev.com/arrow/'
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-arrow-${pkgver}.tar.gz::https://github.com/crsmithdev/arrow/archive/${pkgver}.tar.gz")
sha256sums=('4ed4f6c86e9d3e75a1f77af13d6f5a08f6891ab5c815b20f1f7b7c88ffcad118')

prepare() {
  cp -r {,python-}arrow-${pkgver}
  mv {,python2-}arrow-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-arrow-${pkgver}
    ${py} setup.py build
    popd
  done
}
package_python-arrow() {
  depends=('python' 'python-dateutil' 'python-nose')

  cd python-arrow-${pkgver}

  python setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

package_python2-arrow() {
  depends=('python2' 'python2-dateutil' 'python2-nose')

  cd python2-arrow-${pkgver}

  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
