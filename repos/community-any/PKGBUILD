# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: fenuks

pkgname=python-language-server
pkgver=0.36.0
pkgrel=3
pkgdesc="An implementation of the Language Server Protocol for Python"
arch=("any")
url="https://github.com/palantir/python-language-server"
license=("MIT")
depends=("python" "python-setuptools" "python-jsonrpc-server" "python-future" "python-jedi" "python-pluggy" "python-ujson")
optdepends=("python-mccabe: for complexity checking"
            "python-rope: for completions and renaming"
            "python-pyflakes: for linter to detect various errors"
            "flake8: for code linting"
            "python-pycodestyle: for style checking"
            "python-pylint: for code linting"
            "autopep8: for code formatting"
            "yapf: for code formatting (preferred over autopep8)"
            "python-pydocstyle: for docstring style checking")
checkdepends=("python-pytest" "python-pytest-runner" "python-pytest-cov"  "python-pyflakes" "flake8"
              "python-mock" "python-jedi" "python-future" "yapf" "python-mccabe" "python-pycodestyle"
              "autopep8" "python-rope" "python-pydocstyle" "python-pylint" "python-jsonrpc-server"
              "python-numpy" "python-pandas" "python-matplotlib" "python-flaky")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "fix-test.patch")
sha256sums=('a0fab3c37e3f61e4ee0a721418bf753b66b188a163b3ec04df7efac35599411b'
            '75bee7f9e288bf72e116c8a8f10d32758a800409a91266c7cd15888c6c254bdf')


prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 < "$srcdir/fix-test.patch"
}

build() {
    cd "${pkgname}-${pkgver}"
    export PYTHONHASHSEED=0
    python setup.py build
}

check() {
    cd "${pkgname}-${pkgver}"
    py.test -k 'not test_pyqt_completion'
}

package() {
    cd "${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
