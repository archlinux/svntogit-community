# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: onny <onny@project-insanity.org>

_pypiname=passlib
pkgbase=python-$_pypiname
pkgname=(python-$_pypiname python2-$_pypiname)
pkgver=1.6.5
pkgrel=2
pkgdesc="A password hashing library for Python"
arch=('any')
url=("https://code.google.com/p/passlib/")
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-bcrypt' 'python2-bcrypt'
              'python-django' 'python2-django' 'python2-m2crypto')
source=("https://pypi.python.org/packages/source/p/passlib/passlib-$pkgver.tar.gz")
sha512sums=('b5323834a0a7fc7e799882c512a2fcaddb0cbf4dbfb3ec578c9a9ea7a7f08349335cd3124fe2b379eef265ecdf9d823b7562951881710cf4dafff128a65c82d4')

prepare() {
  cp -a ${_pypiname}-${pkgver}{,-py2}
}

check() {
  export PASSLIB_TEST_MODE=full

  cd "$srcdir/$_pypiname-$pkgver"
  nosetests3

  cd "$srcdir/$_pypiname-$pkgver-py2"
  nosetests2
}

package_python-passlib() {
  depends=("python")
  optdepends=("python-bcrypt: accelerate Bcrypt hashes")

  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-passlib() {
  depends=("python2")
  optdepends=("python2-m2crypto: accelerate PBKDF2-based hashes"
              "python2-bcrypt: accelerate Bcrypt hashes")

  cd ${_pypiname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
