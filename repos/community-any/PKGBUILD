# $Id$
# Maintainer:
# Contributor: Jonathan Kotta <jpkotta AT gmail DOT com>

pkgname=('python-sqlparse' 'python2-sqlparse')
_libname=sqlparse
pkgver=0.1.19
pkgrel=1
pkgdesc='Non-validating SQL parser for Python'
arch=('any')
url=https://github.com/andialbrecht/sqlparse
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=(http://pypi.python.org/packages/source/s/$_libname/$_libname-$pkgver.tar.gz)
md5sums=('f200d8dea67b58bc15575bfa76e4e9dc')

prepare() {
  cp -a $_libname-$pkgver ${_libname}2-$pkgver
}

package_python-sqlparse() {
  depends=('python')

  cd ${_libname}-$pkgver/
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sqlparse() {
  depends=('python2')

  cd ${_libname}2-$pkgver/
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/sqlformat{,2}
}
