# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-traces
pkgver=0.8.0
pkgrel=1
pkgdesc='Application instrumentation and tracing'
arch=(any)
url='https://github.com/socketry/traces'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-bake-modernize ruby-bake-test ruby-bake-test-external ruby-covered ruby-sus
              ruby-console)
options=(!emptydirs)
source=(https://github.com/socketry/traces/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d1123d125c159e20921d11d364fd0ac62b488f0a9f7ea79e0ca75958002d95a8')

prepare() {
  cd traces-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i traces.gemspec
  sed -i '/bake-gem/d;/bake-github-pages/d;/utopia-project/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd traces-$pkgver
  gem build traces.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    traces-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd traces-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd traces-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 license.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
