# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-b-keywords
pkgver=1.20
pkgrel=1
pkgdesc="Lists of reserved barewords and symbol names"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/B-Keywords"
depends=('perl')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/R/RU/RURBAN/B-Keywords-$pkgver.tar.gz")
sha512sums=('481eda4d1388901412e982a6c799fab0965f363c4549233e24793aee96ffad68da6045a79fb78c1e946ca48b6ec67d1d49a687801a19254e9c3db12b5e29cb0f')

build() {
  cd B-Keywords-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd B-Keywords-$pkgver
  make test
}

package() {
  cd B-Keywords-$pkgver
  make DESTDIR="$pkgdir" install
}
