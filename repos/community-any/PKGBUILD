# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=wurlitzer
pkgname=python-${_pkg}
pkgver=2.1.1
pkgrel=1
pkgdesc="Capture C-level stdout/stderr in Python"
arch=(any)
url="https://github.com/minrk/wurlitzer"
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-mock)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('5a3ea5a13a8aac2d808864087fec87a0518bf7d9776173ab06a6bb4ade9f4d27')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  py.test test.py
}

package() {
  cd ${_pkg}-${pkgver}
  export PYTHONHASHSEED=0
  python setup.py install --root "${pkgdir}" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
