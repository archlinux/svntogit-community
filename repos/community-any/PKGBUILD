# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Musikolo <musikolo@hotmail.com>
# Contributor: Ruben Fonseca <fonseka@gmail.com>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>

pkgname=groovy
pkgver=2.5.2
pkgrel=1
pkgdesc='Programming language based on Java, inspired by Python, Ruby and Smalltalk'
arch=('any')
url='http://groovy-lang.org/'
license=('Apache')
depends=('bash' 'java-environment')
optdepends=('groovy-docs: html and pdf documentation for Groovy')
makedepends=('gendesk')
# .asc file is available for download, but there was no key available
source=("https://dl.bintray.com/groovy/maven/apache-$pkgname-binary-$pkgver.zip"
        "$pkgname.png::https://raw.githubusercontent.com/apache/incubator-groovy/master/subprojects/groovy-console/src/main/resources/groovy/ui/ConsoleIcon.png")
sha256sums=('495f96f1be35ef838abb5f1c10fc6f642460cacd94c7095eb3a9f1fbf62b282e'
            '24e539235b5762b1220f39d7953a52e9b4a458c2be4f66a60219755363ba6dba')

prepare() {
	# Generate desktop shortcut
  	gendesk -f -n \
    		--pkgname "$pkgname" \
    		--pkgdesc 'Groovy programming language' \
    		--exec 'groovyConsole' \
    		--name 'Groovy Console'

  	# Patch the scripts
  	for f in "$pkgname-$pkgver/bin/"*; do
    		sed 's:bin/env\ sh:bin/env\ sh\nGROOVY_HOME=/usr/share/groovy\nexport _JAVA_OPTIONS="-Dawt.useSystemAAFontSettings=gasp $_JAVA_OPTIONS":' -i "$f"
  	done
}

package() {
  	cd "$pkgname-$pkgver"

  	# Create the directories and package the files
  	install -d "$pkgdir/usr/share/groovy" "$pkgdir/usr/bin"
  	cp -r lib conf "$pkgdir/usr/share/groovy"
  	cp bin/* "$pkgdir/usr/bin"
  	rm "$pkgdir"/usr/bin/*completion
  	install -Dm644 bin/*completion -t "$pkgdir"/usr/share/bash-completion/completions

  	# Remove all DOS/Windows batch files
  	find $pkgdir -name '*.bat' -exec rm {} \;

  	# Package the license file
  	install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

  	# Package the desktop shortcut for Groovy Console
  	install -Dm644 "$srcdir/$pkgname.desktop" -t "$pkgdir"/usr/share/applications
  	install -Dm644 "$srcdir/$pkgname.png" -t "$pkgdir"/usr/share/pixmaps
}
