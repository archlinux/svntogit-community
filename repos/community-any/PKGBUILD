# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=4.3.3
pkgrel=1
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/willthames/ansible-lint"
license=('MIT')
depends=('python' 'ansible' 'python-ruamel-yaml' 'python-pyaml' 'python-rich')
makedepends=('git')
source=(https://github.com/willthames/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('49b0c6bfa89dede50fed269a4aae575756e73b7e563aa5f12f102f62ab96b14d')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop setuptools from using the scm version which doesn't exist in this context
  sed -i "/^\[metadata\]/a version = ${pkgver}" setup.cfg
  sed -i 's/use_scm_version/#use_scm_version/' setup.cfg
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
