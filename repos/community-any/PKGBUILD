# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=matrix-synapse
pkgver=0.99.2
pkgrel=3

pkgdesc="Matrix reference homeserver"
url="https://github.com/matrix-org/synapse"
arch=('any')
license=('Apache')

depends=('python-jsonschema' 'python-twisted' 'python-service-identity'
         'python-pyopenssl' 'python-yaml' 'python-pyasn1' 'python-pynacl'
         'python-daemonize' 'python-bcrypt' 'python-frozendict'
         'python-pillow' 'python-ujson'
         'python-pysaml2' 'python-setuptools'
         'python-systemd' 'python-unpaddedbase64' 'python-canonicaljson'
         'python-signedjson' 'python-pymacaroons'
         'python-service-identity' 'python-msgpack'
         'python-phonenumbers' 'python-prometheus_client'
         'python-attrs' 'python-netaddr' 'python-sortedcontainers'
         'python-treq' 'python-psutil'
         'systemd')
checkdepends=('python-lxml' 'python-jinja' 'python-mock' 'python-parameterized')
optdepends=('python-psycopg2: PostgreSQL support'
            'python-lxml: URL previewing'
            'python-jinja: e-mail notifications'
            'python-bleach: e-mail notifications'
            'python-psutil: metrics')

source=("synapse-$pkgver.tar.gz::https://github.com/matrix-org/synapse/archive/v$pkgver.tar.gz"
        'synapse.service'
        'sysusers-synapse.conf'
        '0001-Bump-python_dependencies.patch')

md5sums=('920a2773497b1487b29d20558a651ed2'
         '276a99050f40601089255ea168bb7620'
         'ecd9f66fb57fe1a2e1e2df07a460a35b'
         'ad2900abb04351778e577175cedf6edc')

backup=('etc/synapse/log_config.yaml')
install=synapse.install

prepare() {
	cd synapse-$pkgver
	patch -p1 <"$srcdir"/0001-Bump-python_dependencies.patch
}

build() {
	cd synapse-$pkgver
	python setup.py build
}

check() {
	cd synapse-$pkgver
	PYTHONPATH=. trial3 tests
}

package() {
	install -Dm644 synapse.service "$pkgdir"/usr/lib/systemd/system/synapse.service

	cd synapse-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build

	install -dm755 -o 198 -g 198 "$pkgdir"/etc/synapse
	install -Dm644 contrib/systemd/log_config.yaml "$pkgdir"/etc/synapse/log_config.yaml
	install -Dm644 "$srcdir"/sysusers-synapse.conf "$pkgdir"/usr/lib/sysusers.d/synapse.conf
}
