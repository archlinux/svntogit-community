# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=1.3.2
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('any')
license=('BSD')
url="http://scrapy.org"
depends=('python-twisted' 'libxml2' 'python-w3lib' 'python-lxml' 'python-six' 'python-queuelib'
         'python-setuptools' 'python-cssselect' 'python-pyopenssl' 'python-parsel'
         'python-pydispatcher' 'python-service-identity')
makedepends=('git')
checkdepends=('python-pytest-runner' 'python-pillow' 'python-testfixtures' 'python-botocore'
              'python-jmespath' 'mitmproxy')
optdepends=('ipython: for enhanced support of the interactive scraping shell')
source=("git+https://github.com/scrapy/scrapy.git#tag=${pkgver}")
md5sums=('SKIP')

check() {
  cd scrapy
  python setup.py pytest --addopts tests
}

package() {
  cd scrapy
  python setup.py install -O1 --root="$pkgdir"

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 README.rst "$pkgdir"/usr/share/doc/$pkgname/README.rst
  install -D -m644 docs/intro/install.rst "$pkgdir"/usr/share/doc/$pkgname/INSTALL.rst
}
