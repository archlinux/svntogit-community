# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-config
pkgver=9.1.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
depends=('python-debtcollector' 'python-netaddr' 'python-stevedore'
         'python-oslo-i18n' 'python-rfc3986' 'python-yaml' 'python-requests')
makedepends=('python-pbr' 'python-setuptools')
checkdepends=('python-oslotest' 'python-sphinx' 'python-requests-mock' 'python-oslo-log')
options=('!emptydirs')
source=("https://github.com/openstack/oslo.config/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0af00e88c0a0aa747de5a70847789fdcf3847309883a229d63e30a7c78ec0059bb455781ef1ca49bb794078d0048c4a832463f22c5ee95d4d10127d157d33e3a')

export PBR_VERSION=$pkgver

build() {
  cd oslo.config-$pkgver
  python setup.py build
}

check() {
  cd oslo.config-$pkgver
  stestr run
}

package() {
  cd oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
