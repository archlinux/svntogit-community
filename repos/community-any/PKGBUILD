# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-test-deep
pkgver=1.130
pkgrel=2
pkgdesc="Extremely flexible deep comparison "
arch=('any')
url="https://search.cpan.org/dist/Test-Deep"
license=('GPL' 'PerlArtistic')
depends=('perl-test-nowarnings' 'perl-test-simple')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Test-Deep-$pkgver.tar.gz)
sha256sums=('4064f494f5f62587d0ae501ca439105821ee5846c687dc6503233f55300a7c56')

build() {
  cd  "$srcdir"/Test-Deep-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Test-Deep-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
