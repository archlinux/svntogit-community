# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Daniel M. Capella <polycitizen@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgbase=python-parsedatetime
pkgname=('python-parsedatetime' 'python2-parsedatetime')
pkgver=2.4
pkgrel=2
pkgdesc="Parse human-readable date/time strings."
arch=('any')
license=('Apache')
url='http://github.com/bear/parsedatetime'
makedepends=('python-future' 'python2-future' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bear/parsedatetime/archive/v$pkgver.tar.gz")
sha512sums=('0fd8bfa3411476a20d8874b44036bc162735fb7865d39ca98b0a608daec39daac47100314279dbf66229ec333821492744f7505315d955f58216fdaa75cbf046')

prepare() {
  cp -a parsedatetime-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/parsedatetime-$pkgver
  python setup.py build

  cd "$srcdir"/parsedatetime-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parsedatetime-$pkgver
  python setup.py pytest

  cd "$srcdir"/parsedatetime-$pkgver-py2
  python2 setup.py pytest
}

package_python-parsedatetime() {
  depends=('python-future')

  cd parsedatetime-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-parsedatetime() {
  depends=('python2-future')

  cd parsedatetime-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
