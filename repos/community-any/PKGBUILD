# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=clap
pkgver=1.1.4
pkgrel=1
pkgdesc="CLever Audio Plugin API"
arch=(any)
url="https://github.com/free-audio/clap"
license=(MIT)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('24810424a56a69fc13caa780ced9897f116906a5be679f45f1965bcce2a40bffb005228641cfd3acbdd2243e5b83e91d83ead796ffc3d8e0f8ac8cc08bd0143c')
b2sums=('d30ea7f6dbd6286f7295f6f2c5f9a4286f7278c7e1fa7590995fa9377363124817b78d29eee89249e13f1b4f6132fde2784b91fb8980f185f7af9511ff30a4c0')

build() {
  local cmake_options=(
    -B build
    -S $pkgname-$pkgver
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
