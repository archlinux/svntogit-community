# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname=reuse
pkgver=1.1.1
pkgrel=1
pkgdesc='Helper tool for providing and confirming copyright and licensing information'
arch=('any')
url='https://reuse.software/'
license=('GPL3' 'Apache' 'custom:CC0-1.0')
depends=(
  'python'
  'python-debian'
  'python-license-expression'
  'python-boolean.py'
  'python-jinja'
  'python-binaryornot'
  'python-setuptools' 
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-poetry-core'
)
checkdepends=('python-pytest')
optdepends=(
  'git: git repository support'
  'mercurial: mercurial repository support'
)
_commit='2d6896af04ff3ca020a0637b9699f30019cfd08b'
source=(
  "$pkgname::git+https://github.com/fsfe/reuse-tool#commit=$_commit"
  'ftbfs-build.py.patch'
)
b2sums=('SKIP'
        '52794da4f1c871591a0be306388aff4973ca0a90ad43162b8762c8c8c283122943ed8e383e1424307dff8e8112267f02ea6f5c66b28d45f6afb6ec583bd27a0d')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  # https://github.com/fsfe/reuse-tool/issues/640
  # why does this look horrible?
  # let me count the ways:

  # 1. this creates the locale files, but not the rest of the actual package!
  python -m build --wheel --no-isolation

  # 2. this removes the problem
  patch -p1 -i "$srcdir/ftbfs-build.py.patch"

  # 3. this builds the rest of the actual package
  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname"

  pytest -v
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.{rst,md}

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSES/*

  sed -n '/## License/,$ p' README.md > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
