# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: JakobDev<jakobdev at gmx dot de>

pkgname=python-sphinx-reredirects
_pkgname=${pkgname#python-}
pkgver=0.1.1
pkgrel=2
pkgdesc='Handles redirects for moved pages in Sphinx documentation projects'
arch=(any)
url="https://gitlab.com/documatt/$_pkgname"
license=("BSD")
depends=(python
         python-sphinx)
makedepends=(python-{build,installer}
             python-setuptools
             python-wheel)
checkdepends=(python-pytest)
_archive="$_pkgname-v$pkgver"
source=("$url/-/archive/v$pkgver/$_archive.tar.bz2")
sha256sums=('5f494fad2fbb3c37b787815a4c6f8c9b1dbab7a1d91c55025033c16d4d226a06')

build() {
    cd "$_archive"
    python -m build -wn
}

check() {
	cd "$_archive"
	pytest
}

package() {
    cd "$_archive"
    python -m installer -d "$pkgdir" dist/*.whl
}
