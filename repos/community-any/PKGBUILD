# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-planout
pkgver=0.6.0
_commit=6ba0aba6addb08e03ed6e2d21f9b224b3d56d503
pkgrel=9
pkgdesc="A framework for online field experimentation"
arch=('any')
url='https://facebook.github.io/planout'
license=('custom:BSD')
depends=('python-six')
checkdepends=('python-pytest')
source=("$pkgname-$_commit.tar.gz::https://github.com/facebook/planout/archive/$_commit.tar.gz"
        python310.patch)
sha512sums=('a066aafcc61b0bad3a7ac439d6bc1bb1081405cf5dc99db49e31594dd45b1b3ff743216a1a9a12e04b35d176cdb8e296026f2b56c8cd28afe6edf7b7ea3275b9'
            'd277e072def87b2ca07e11541fd42819d0794a904c2c9adb12dddf252b52f93c77d14c58cbe6943bc7bce924a2d62a4dddbfb57c77a2c7400440da06c7007fac')

prepare() {
  cd planout-$_commit
  patch -Np1 -i ../python310.patch
  # https://github.com/facebook/planout/pull/104
  sed -e '42s/\t/        /' \
      -e '43s/range(8)/list(range(8))/' \
      -i python/planout/test/test_assignment.py
}

build() {
  cd planout-$_commit/python
  python setup.py build
}

check() {
  cd planout-$_commit/python/planout/test
  py.test
}

package() {
  cd planout-$_commit/python
  python setup.py install -O1 --root "$pkgdir"
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
