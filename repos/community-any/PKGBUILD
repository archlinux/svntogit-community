# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-concurrency
pkgver=4.2.0
pkgrel=1
pkgdesc="OpenStack library for all concurrency-related code"
arch=('any')
url="https://pypi.python.org/pypi/oslo.concurrency/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-oslo-config' 'python-oslo-i18n' 'python-oslo-utils'
         'python-fasteners')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest' 'python-oslo-config' 'python-eventlet')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.concurrency/archive/$pkgver.tar.gz")
sha512sums=('3b7395e6bd711e7a7b5978aed7fe5efb6beee7eeecb7a3412b19aeab5c1e6d9de545e84091eecf0fd9910b3ab9a8edef1a9ef8b18312ee9111b4b14774b80667')

export PBR_VERSION=$pkgver

build() {
  cd oslo.concurrency-$pkgver
  python setup.py build
}

check() {
  cd oslo.concurrency-$pkgver
  stestr run || : # "1 failure about raising rlimit"
}

package() {
  cd oslo.concurrency-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
