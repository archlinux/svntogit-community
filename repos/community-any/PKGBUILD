# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-flask-talisman
_pyname=${pkgname/python-/}
pkgver=1.0.0
pkgrel=1
pkgdesc='HTTP security headers for Flask'
url='https://github.com/wntrblm/flask-talisman'
arch=('any')
license=('APACHE')
depends=('python-flask' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(https://github.com/wntrblm/flask-talisman/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('015db92a07b4b4c751fb9bf3fc4f82be7fa5b07f10e3f1d8e5d5f1fc00c968bd')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  pytest flask_talisman
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
