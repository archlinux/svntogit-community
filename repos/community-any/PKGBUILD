# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgname=python-xcffib
pkgver=1.0.0
pkgrel=1
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-cffi' 'libxcb')
makedepends=('python-setuptools' 'haskell-xcffib')
checkdepends=('python-pytest' 'xorg-server-xvfb' 'xorg-xeyes')
source=("https://github.com/tych0/xcffib/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ab6ff69b9e2f4f8adc87ddb090dc6c374db781098800c07ce00bfc843fb0a617c6c8ebcc6cf5bd2efc0e9a581d79110f65656814a7bc116fc4b12f8594b26fa8')

build() {
  cd xcffib-$pkgver
  make xcffib GEN=xcffibgen CABAL=true
  python setup.py build
}

check() {
  cd xcffib-$pkgver
  xvfb-run pytest
}

package() {
  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
