# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Evgeniy Filimonov <evgfilim1@gmail.com>

pkgname=python-aiogram
pkgver=2.22
pkgrel=1
pkgdesc="A pretty simple and fully asynchronous library for Telegram Bot API written with asyncio and aiohttp"
arch=('any')
url="https://github.com/aiogram/aiogram"
license=('MIT')
depends=('python-aiohttp' 'python-babel' 'python-certifi')
makedepends=('python-setuptools')
checkdepends=('python-aiohttp-socks' 'python-aioredis' 'python-aresponses' 'python-pytest'
              'python-pytest-lazy-fixture' 'pifpaf' 'redis')
optdepends=('python-uvloop: fast, drop-in replacement of the built-in asyncio event loop'
            'python-ujson: ultra fast JSON encoder and decoder written in pure C'
            'python-rapidjson: extremely fast C++ JSON parser and serialization library'
            'python-emoji: emojize and demojize support'
            'python-aioredis: Redis storage support'
            'python-aiohttp-socks: SOCKS4(a) and SOCKS5 proxy support'
#            'python-rethinkdb: RethinkDB storage support'  # No such package yet
)
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8daa1e9d59abc95ccfbdbcea6c305fcaae4f7dcfcff53dd9303f994402abddbceeb7e99aee856cbd2dccd74501621c7d46380d3ed078235133fb57766c356c17')

build() {
  cd aiogram-$pkgver
  python setup.py build
}

check() {
  cd aiogram-$pkgver
  pifpaf run redis -- bash -c 'python -m pytest --redis $PIFPAF_REDIS_URL'
}

package() {
  cd aiogram-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  install -Dm644 README.md -t "$pkgdir"/usr/share/doc/$pkgname/
}
