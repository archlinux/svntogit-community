# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thayne <astrothayne at gmail dot com>
# Contributor: Yiqiao Wang <yiqiao.wang96 at gmail dot com>
# Contributor: wenLiangcan <boxeed at gmail dot com>

pkgname=ammonite
pkgver=2.5.6
_scalaversion=2.13.2
pkgrel=3
pkgdesc="A cleanroom re-implementation of the Scala REPL from first principles. "
arch=('any')
url="https://github.com/lihaoyi/Ammonite"
license=('MIT')
depends=('bash' 'java-environment')
makedepends=('mill')
source=("https://github.com/lihaoyi/Ammonite/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        java-target.patch)
b2sums=('28eb868dfd1346007f0cf11b35f78827c315ee8d89be8986dc6992b9195f5880bff70ca075d8c0cf9047ab01eb9c0fd2911c1ce59e170c6d016baea6a05cdaa8'
        '741c8a369df28d1d96a7fee831a1b5aadacddaccba25b8c72d020802c054791a583671276d8e933f81962253377cf60a4842d93bfd78061c7f58f9a9be7192c9')

prepare() {
  cd Ammonite-$pkgver
  patch -p1 -i ../java-target.patch
  sed -i "s/'git/'true/;s/\"git\"/\"true\"/;s/\$latestTaggedVersion-\$commitsSinceTaggedVersion-\$gitHash/$pkgver/;s/^    \\.toInt$//;s/(tagName, false)/(\"$pkgver\", false)/" build.sc
}

build() {
  cd Ammonite-$pkgver
  TRAVIS_TAG=$pkgver mill -i amm[$_scalaversion].assembly
}

package() {
  cd Ammonite-$pkgver
  install -dm755 "$pkgdir"/usr/bin
  (echo "#!/bin/sh"; cat out/amm/$_scalaversion/assembly.dest/out.jar) > "$pkgdir"/usr/bin/amm
  chmod +x "$pkgdir"/usr/bin/amm
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
