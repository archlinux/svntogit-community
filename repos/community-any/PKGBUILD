# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Rafael G. Martins <rafael@rafaelmartins.com>
# Contributor: Iván Pulido <ijpulidos@riseup.net>

pkgbase=python-mpmath
pkgname=(python2-mpmath python-mpmath)
pkgver=1.1.0
pkgrel=1
pkgdesc='Python library for arbitrary-precision floating-point arithmetic.'
arch=(any)
url='http://mpmath.org/'
license=(BSD)
makedepends=(python python2)
source=($pkgbase-$pkgver.tar.gz::"https://github.com/fredrik-johansson/mpmath/archive/$pkgver.tar.gz")
sha256sums=('16c01d589bcb1be5cab3a07de2855b578e5fc4a7882fb61a15f2aaf118fdd06e')

prepare(){
  cp -r mpmath-$pkgver mpmath2-$pkgver
}

build() {
  cd mpmath2-$pkgver  
  python2 setup.py build

  cd ../mpmath-$pkgver
  python setup.py build
}

package_python2-mpmath() {
  depends=(python2-gmpy2)
  cd mpmath2-$pkgver

  find -name '*.py' | xargs sed -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
                                -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i

  python2 setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}


package_python-mpmath() {
  depends=(python-gmpy2)
  cd mpmath-$pkgver  

  python setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
