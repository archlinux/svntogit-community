# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname='python-joblib'
pkgver=0.14.0
pkgrel=2
pkgdesc="Set of tools to provide lightweight pipelining in Python"
arch=('any')
url="https://joblib.readthedocs.io/"
license=('BSD')
depends=('python')
optdepends=('python-numpy: for array manipulation'
            'python-lz4: for compressed serialization')
makedepends=('python-setuptools')
source=("https://github.com/joblib/joblib/archive/${pkgver}.tar.gz")
sha256sums=('9e66f3e31669f772fcbad95043fd05e4530e78288d60363b785d4c288a5dc707')

build() {
  cd "$srcdir"/joblib-${pkgver}
  python setup.py build
}

package_python-joblib() {
  cd "$srcdir"/joblib-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
