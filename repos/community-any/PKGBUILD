# $Id$
# Maintainer : Felix Yan <felixonmars@gmail.com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Arnau Sanchez <tokland {at} gmail {dot} com>

_name=pyquery
pkgbase=python-pyquery
pkgname=('python-pyquery' 'python2-pyquery')
pkgver=1.2.9
pkgrel=2
pkgdesc="A jquery-like library for python."
arch=('any')
url="https://github.com/gawel/pyquery/"
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-lxml' 'python2-lxml' 'python-cssselect' 'python2-cssselect')
checkdepends=('python-requests' 'python2-requests' 'python-webob' 'python2-webob' 'python-nose' 'python2-nose'
              'python-coverage' 'python2-coverage' 'python-webtest' 'python2-webtest' 'python2-beautifulsoup4')
              # In AUR: 'python-waitress' 'python2-waitress') 
source=("http://pypi.python.org/packages/source/p/$_name/$_name-$pkgver.zip"
        "https://bitbucket.org/olauzanne/pyquery/raw/b70710332032/LICENSE.txt")
sha512sums=('f5152b471c896b39032add6a7f0e6ce63f426b4903bc7cf9eef12723c73453b40c310dd1497a7c4189fc6faaf804690fd91fb6d2f97fb6c4d6314682de4b8d05'
            '4932f86cbe9ed4c2e658acf9c228fdb0b2430fd622d559555196c7db0f43ea435483c7ae9916c325b40130334f4706d30e5adea6bc1d097b3283a7fc64926685')

prepare() {
  cp -a "$_name-$pkgver"{,-python2}
}

build() {
  cd $_name-$pkgver
  python setup.py build

  cd ../$_name-$pkgver-python2
  python2 setup.py build
}

check() {
  cd $_name-$pkgver
  nosetests3 || warning "Tests failed"

  cd ../$_name-$pkgver-python2
  nosetests2 || warning "Tests failed"
}

package_python-pyquery() {
  depends=('python-lxml' 'python-cssselect')
  cd $_name-$pkgver
  python3 setup.py install -O1 --root="$pkgdir/"

  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/python-pyquery/LICENSE"
}

package_python2-pyquery() {
  depends=('python2-lxml' 'python2-cssselect')
  cd $_name-$pkgver-python2
  python2 setup.py install -O1 --root="$pkgdir/"

  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/python2-pyquery/LICENSE"
}
