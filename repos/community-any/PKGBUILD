# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# ThomasV PGP key: gpg --recv-key 6694D8DE7BE8EE5631BED9502BD5824B7F9470E6
# http://bitcoin-otc.com/viewgpg.php?nick=ThomasV

pkgname=electrum
pkgver=2.2
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('python2-ecdsa' 'python2-pbkdf2' 'python2-requests' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-qrcode'
         'python2-protobuf' 'python2-tlslite' 'python2-dnspython' 'python2-pyqt4' 'python2-sip' 'python2-slowaes')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools' 'desktop-file-utils')
optdepends=('python2-btchip: BTChip hardware wallet support'
            'python2-matplotlib: plot transaction history in graphical mode'
            'zbar: QR code reading support')
url="https://electrum.org"
license=('GPL3')
source=("https://download.electrum.org/${pkgname^}-${pkgver}.tar.gz"{,.asc}
	https://github.com/spesmilo/electrum/commit/3ae2fb2e90b0296066509d1934d1c24b6333260f.patch)
md5sums=('b2fa1c14168aedd1106ae8ee21b1a9e1'
         'SKIP'
         '55821e2591267b8efca22368eb063977')
sha256sums=('66035c4e5c2ccdf033e593a2358d801c3c1f97d6c4d6872e30245ae1af2cb5f6'
            'SKIP'
            'ca706164c16f0e1558506b5f581bde620c2b25d7dc48ca6a31bceabecf863cbe')
validpgpkeys=('6694D8DE7BE8EE5631BED9502BD5824B7F9470E6')

provides=('electrum')
install=electrum.install

prepare() {
  cd ${pkgname^}-${pkgver}

  find . -type f -exec sed -i 's#/usr/bin/python#/usr/bin/python2#g' {} +
  find . -type f -exec sed -i 's#/usr/bin/env python#/usr/bin/env python2#g' {} +

  # Disable useless (on linux) pyusb dependency
  patch -uNp1 -i ../3ae2fb2e90b0296066509d1934d1c24b6333260f.patch
}

build() {
  cd ${pkgname^}-${pkgver}

  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${pkgname^}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
