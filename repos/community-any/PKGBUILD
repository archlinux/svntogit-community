# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-utils
pkgver=4.1.0
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz'
         'python-netaddr' 'python-netifaces' 'python-debtcollector')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-ddt' 'python-eventlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.utils/archive/$pkgver.tar.gz"
        oslo-utils-py38.patch
        oslo-utils-oslotest4.patch::https://github.com/openstack/oslo.utils/commit/a830a6b8a18172ec22b343ffd9734355260e48e5.patch)
sha512sums=('6fa8fdf690fc42d6183912f822d80faed04e2877cc32ad730c75fd7e2fcc0b11476d5fb9cd5fac8411aa2afb78c558eaa57c721e1b940bfecae9759b4f89a2d9'
            'edc9861dfc02b82d0312774f2484bb5853a9e5b2e7de9ee19efbd7e1220be56e52d99abb15f71dba4d764832835371200e8d7cac2d40c0546d5cfc8daf68554a'
            'adb6ec0951bdb6043c3369b2886506dc1ed6216062a537bbb456324467b7a6da069b61dcf6146944f454ff56115cd125c3bd6aa7ecd13f4aaab1991d7c5b8c8d')

export PBR_VERSION=$pkgver

prepare() {
  patch -d oslo.utils-$pkgver -p1 -i ../oslo-utils-py38.patch
  patch -d oslo.utils-$pkgver -p1 -i ../oslo-utils-oslotest4.patch
}

build() {
  cd oslo.utils-$pkgver
  python setup.py build
}

check() {
  cd oslo.utils-$pkgver
  stestr run
}

package() {
  cd oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
