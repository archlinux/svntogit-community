# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-requests-mock
pkgname=('python-requests-mock' 'python2-requests-mock')
pkgver=1.0.0
pkgrel=1
pkgdesc="A mock of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/openstack/requests-mock"
license=('Apache')
makedepends=('python-pbr' 'python2-pbr' 'python-requests' 'python2-requests'
             'python-six' 'python2-six' 'git')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-mock' 'python2-mock')
source=("git+https://github.com/openstack/requests-mock.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a requests-mock{,-py2}
}

build() {
  cd "$srcdir"/requests-mock
  python setup.py build

  cd "$srcdir"/requests-mock-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/requests-mock
  python setup.py testr

  cd "$srcdir"/requests-mock-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-requests-mock() {
  depends=('python-requests' 'python-six')

  cd requests-mock
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-requests-mock() {
  depends=('python2-requests' 'python2-six')

  cd requests-mock-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
