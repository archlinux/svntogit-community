# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=6.10.0
pkgrel=1
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/ansible/ansible-lint"
license=('GPL3')
depends=(python ansible-core yamllint 
  python-{ansible-compat,black,enrich,filelock,jsonschema,pyaml,packaging,rich,ruamel-yaml,wcmatch})
makedepends=(python-{build,installer,setuptools,wheel})
optdepends=('ansible: check official ansible collections')
source=(https://github.com/ansible/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        disable_version_check.patch)
b2sums=('1277398c93e26110babe4f0ef7ef94c39aacf3cd0fc14c609e12d3db88516da723756b42bb2164224bea58df457062748fb195116dc910e1c3fa57d664077272'
        '09a8a69cd2953e6f0b813976fae31388db87647e80ef7942a960adf1cf71e6a843ed86a2456e718042c790a8481db3bb2f7092c52c9553950cc05f46fe7ee35f')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop ansible-lint from calling home every 24h to check for a new version
  patch -Np1 < "${srcdir}"/disable_version_check.patch
  # use pkg version
  sed -i "s/dynamic = \[\"version\"\]/version = \"${pkgver}\"/" pyproject.toml
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
}
