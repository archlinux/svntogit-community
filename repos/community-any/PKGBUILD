# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: DasIch <dasdasich@googlemail.com>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgbase=python-repoze.lru
pkgname=(python-repoze.lru python2-repoze.lru)
pkgver=0.6
pkgrel=4
pkgdesc="A tiny LRU cache implementation and decorator"
arch=("any")
url="https://pypi.python.org/pypi/repoze.lru"
license=("custom:BSD")
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.python.org/packages/source/r/repoze.lru/repoze.lru-${pkgver}.tar.gz")
md5sums=("2c3b64b17a8e18b405f55d46173e14dd")

prepare() {
  cp -a repoze.lru-${pkgver}{,-py2}
}

build() {
  cd "${srcdir}/repoze.lru-${pkgver}/"
  python setup.py build

  cd "${srcdir}/repoze.lru-${pkgver}-py2/"
  python2 setup.py build
}

check() {
  cd "${srcdir}/repoze.lru-${pkgver}/"
  python setup.py test

  cd "${srcdir}/repoze.lru-${pkgver}-py2/"
  python2 setup.py test
}

package_python-repoze.lru() {
  depends=("python")

  cd "${srcdir}/repoze.lru-${pkgver}/"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-repoze.lru() {
  depends=("python2")

  cd "${srcdir}/repoze.lru-${pkgver}-py2/"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
