# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Omar Pakker <archlinux@opakker.nl>

pkgname=python-bidict
_name=${pkgname#python-}
pkgver=0.22.0
pkgrel=1
pkgdesc='Bidirectional mapping library for Python'
url='https://bidict.readthedocs.org/'
arch=('any')
license=('MPL2')
depends=('python')
makedepends=('python-setuptools' 'python-pip' 'python-wheel' 'python-build' 'python-installer')
checkdepends=('python-pytest' 'python-sphinx' 'python-sphinx-autodoc-typehints' 'python-pytest-benchmark'
	          'python-hypothesis' 'python-py-cpuinfo' 'python-sortedcollections')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz)
sha256sums=('5c826b3e15e97cc6e615de295756847c282a79b79c5430d3bfc909b1ac9f5bd8')
b2sums=('511bea446922c959c7ab2c42fadfe63dc486f9b8f4d060779048195ed02344c36819be917716be6773e2173aa9dbd9e5452e747ee325db6ee92057644789653e')

build() {
  cd "bidict-${pkgver}"
  python -m build --wheel --no-isolation
}

check() {
  cd "bidict-${pkgver}"
  PYTHONPATH="build/lib" pytest
}

package() {
  cd "bidict-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
