# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgbase=python-pudb
pkgname=(python-pudb python2-pudb)
pkgver=2017.1.1
pkgrel=1
pkgdesc="A full-screen, console-based Python debugger"
url="http://pypi.python.org/pypi/pudb"
arch=('any')
license=('MIT')
makedepends=('python-urwid' 'python-pygments' 'python2-urwid' 'python2-pygments' 'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/inducer/pudb.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a pudb{,-py2}

  sed -i 's,^#! /usr/bin/env python$,#! /usr/bin/env python2,' pudb-py2/pudb/debugger.py
}

build() {
  cd "$srcdir"/pudb
  python setup.py build

  cd "$srcdir"/pudb-py2
  python2 setup.py build
}

check() {
  # TODO: make it work...

  cd "$srcdir"/pudb
  # python -m pytest --tb=native -rxs test/

  cd "$srcdir"/pudb-py2
  # python2 -m pytest --tb=native -rxs test/
}

package_python-pudb() {
  depends=('python-urwid' 'python-pygments')

  cd pudb
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pudb() {
  depends=('python2-urwid' 'python2-pygments')

  cd pudb-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: set ft=sh ts=2 sw=2 et:
