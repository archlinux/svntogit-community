# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=docker-compose
pkgver=1.24.1
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('any')
url="https://www.docker.com/"
license=("Apache")
depends=('python-cached-property' 'python-docopt' 'python-yaml' 'python-requests'
         'python-texttable' 'python-websocket-client' 'python-docker' 'python-dockerpty'
         'python-six' 'python-jsonschema' 'python-setuptools' 'docker' 'python-paramiko')
checkdepends=('python-pytest-runner' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/compose/archive/$pkgver.tar.gz")
sha512sums=('9b8632ef263b8dfa678e8b99b46e00441f779716a524280ce25f62fcd2605f400723b7790c466338374c34653946dc2c5d7f09593ddd892fee3409c2a2fd1a5f')

prepare() {
  cd compose-$pkgver

  # Remove upper bound on requires
  sed -i 's/==/>=/g' requirements.txt
  sed -i "s/, < .*',$/',/" setup.py
}

build() {
  cd compose-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it
  # TODO: need a running docker daemon to test

  cd compose-$pkgver
  # python setup.py install --root="$PWD/tmp_install" --optimize=1
  # PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" python setup.py pytest
}

package() {
  cd compose-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 contrib/completion/bash/docker-compose      "$pkgdir"/usr/share/bash-completion/completions/docker-compose
  install -Dm644 contrib/completion/fish/docker-compose.fish "$pkgdir"/usr/share/fish/completions/docker-compose.fish
  install -Dm644 contrib/completion/zsh/_docker-compose      "$pkgdir"/usr/share/zsh/site-functions/_docker-compose
}
