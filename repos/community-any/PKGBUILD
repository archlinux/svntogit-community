# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: onny <onny@project-insanity.org>

_pypiname=passlib
pkgbase=python-$_pypiname
pkgname=(python-$_pypiname python2-$_pypiname)
pkgver=1.6.1
pkgrel=3
pkgdesc="A password hashing library for Python"
arch=('any')
url=("https://code.google.com/p/passlib/")
license=('custom:BSD')
makedepends=('python' 'python2')
source=("https://${_pypiname}.googlecode.com/files/$_pypiname-${pkgver}.tar.gz")
sha512sums=('93c047dc0a06f19a20e5da97db43c5c76e06d69baeac15cc932ef0306ae22dac5c1a3925889a140fd181ab4e00ac513ec829951e15babf5aad89bd2d08dc9907')

prepare() {
  cp -r ${_pypiname}-${pkgver} python2-${_pypiname}-${pkgver}
}

package_python-passlib() {
  depends=("python")

  cd ${_pypiname}-${pkgver}
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-passlib() {
  depends=("python2")
  optdepends=("python2-m2crypto: accelerate PBKDF2-based hashes")

  cd python2-${_pypiname}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
