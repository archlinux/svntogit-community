# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=('python-jsbeautifier' 'python-cssbeautifier')
pkgbase=python-jsbeautifier
pkgver=1.14.7
pkgrel=3
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-editorconfig' 'python-six')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://github.com/beautify-web/js-beautify/archive/v$pkgver/js-beautify-v$pkgver.tar.gz")
b2sums=('42353a2e7a89006e13c2a505f1cb0d3e379f778c3d874ac3c4824aa287030e2a18129ae26de7d8ad0d54e6506e95587b515d80b7307ca9cda0dc8592c9a41975')

prepare() {
  cd js-beautify-$pkgver
  cp -a python python-css
}

build() {
  cd js-beautify-$pkgver/python
  mv setup-js.py setup.py
  python -m build --wheel --skip-dependency-check --no-isolation
  cd ../python-css
  mv setup-css.py setup.py
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd js-beautify-$pkgver
  python python/js-beautify-test.py
}

package_python-jsbeautifier() {
  pkgdesc='JavaScript unobfuscator and beautifier'
  cd js-beautify-$pkgver/python
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[0]}" ../LICENSE
}

package_python-cssbeautifier() {
  pkgdesc='CSS unobfuscator and beautifier'
  depends+=('python-jsbeautifier')
  cd js-beautify-$pkgver/python-css
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[1]}" ../LICENSE
}
