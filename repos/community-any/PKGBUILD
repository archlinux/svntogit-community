# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plette
pkgver=0.4.1
pkgrel=1
pkgdesc="Structured Pipfile and Pipfile.lock models"
url="https://github.com/sarugaku/plette"
license=('ISC')
arch=('any')
depends=('python-tomlkit')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-cerberus')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/plette/archive/v$pkgver.tar.gz")
sha512sums=('c84f701838ab003805aeac388c452aaafd95b6ccef898bb59db893d9246540c206513035976a40d7d2f3b84ccd1c4291714fb47090ad23ee22ed9c5b4873f978')

build() {
  cd plette-$pkgver
  python -m build -nw
}

check() {
  cd plette-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd plette-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
