# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Jose Valecillos <valecillosjg@gmail.com>
# Contributor: Kaos <gianlucaatlas@gmail.com>
# Contributor: Christopher Rogers <slaxemulator@gmail.com>

pkgname=wordpress
pkgver=5.2.2
pkgrel=1
pkgdesc='Blog tool and publishing platform'
arch=(any)
url='https://wordpress.org/'
license=(GPL)
depends=(php)
optdepends=('nginx: Web server for serving WordPress'
            'apache: Web server for serving WordPress'
            'mariadb: Database server')
options=(emptydirs)
install=$pkgname.install
source=("https://wordpress.org/$pkgname-$pkgver.tar.gz"
        password_reset_exploit.patch)
sha256sums=('783fc929a64d37e4ad4e8eea0a5f7ca2d9469f430a48a6aa1555bde0f6a23122'
            '84c716953fb1aad583bfe62c2aba88173b760c60a90f6752228d4ba966e647b9')

prepare() {
  cd $pkgname
  patch -p1 -i "$srcdir/password_reset_exploit.patch"
}

package() {
  install -d "$pkgdir/usr/share/webapps"
  cp -r $pkgname "$pkgdir/usr/share/webapps"
}

# getver: core.trac.wordpress.org/timeline?milestone=on
# vim: ts=2 sw=2 et:
