# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Felip Manyer i Ballester <archlinux at res hyphen telae dot cat>
# Contributor: Alex Ferrando <alferpal@gmail.com>
# Contributor: Zhang Li <richselian at gmail.com>

pkgname=vim-syntastic
_pkgname=syntastic
pkgver=3.9.0
pkgrel=1
pkgdesc='Automatic syntax checking for Vim'
arch=('any')
url='http://www.vim.org/scripts/script.php?script_id=2736'
license=('custom:WTFPL')
depends=('vim')
groups=('vim-plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrooloose/${_pkgname}/archive/${pkgver}.tar.gz")
md5sums=('9632d807d54de03968fad8ccbe831764')

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    install -d "${pkgdir}/usr/share/vim/vimfiles/"{plugin,doc,autoload/syntastic/,syntax_checkers}
    find . -type d -exec chmod 755 '{}' \;
    cp -dr plugin/* "$pkgdir/usr/share/vim/vimfiles/plugin/"
    install -Dm0644 doc/syntastic.txt "${pkgdir}/usr/share/vim/vimfiles/doc/syntastic.txt"
    cp autoload/syntastic/* "$pkgdir/usr/share/vim/vimfiles/autoload/syntastic/"
    cp -dr syntax_checkers/* "$pkgdir/usr/share/vim/vimfiles/syntax_checkers/"

    # remove rustc checker (fixes FS#41517)
    rm -rf "${pkgdir}/usr/share/vim/vimfiles/syntax_checkers/rust/"

    # install license file
    install -Dm0644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
