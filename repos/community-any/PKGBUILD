# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Jose Valecillos <valecillosjg@gmail.com>
# Contributor: Kaos <gianlucaatlas@gmail.com>
# Contributor: Christopher Rogers <slaxemulator@gmail.com>

pkgname=wordpress
pkgver=5.0.1
pkgrel=1
pkgdesc='Blog tool and publishing platform'
arch=('any')
url='https://wordpress.org/'
license=('GPL')
depends=('php')
optdepends=('nginx: Web server for serving WordPress'
            'apache: Web server for serving WordPress'
            'mariadb: Database server')
options=('emptydirs')
install="$pkgname.install"
source=("https://wordpress.org/$pkgname-$pkgver.tar.gz"
        'password_reset_exploit.patch')
sha256sums=('3a64a5c61a946dc19a0354c34f02e835042e4538d65b9c448e075f4ad5275024'
            'e73886f5cd0dd79a1ddc64113b181df0d32cefc975a09796994e885d75361726')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/password_reset_exploit.patch"
}

package() {
  install -d "$pkgdir/usr/share/webapps"
  cp -r "$pkgname" "$pkgdir/usr/share/webapps"
}

# getver: wordpress.org/download/
# vim: ts=2 sw=2 et:
