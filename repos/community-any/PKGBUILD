# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>

pkgname=diff-so-fancy
pkgver=0.11.4
pkgrel=1
pkgdesc="Good-looking diffs with diff-highlight and more"
arch=('any')
url="https://github.com/so-fancy/diff-so-fancy"
license=('MIT')
depends=('git')
checkdepends=('bash-bats')
source=("git+https://github.com/so-fancy/diff-so-fancy.git#tag=v$pkgver"
        "git+https://github.com/ztombol/bats-support.git"
        "git+https://github.com/ztombol/bats-assert.git"
        system-diff_highlight.patch)
sha384sums=('SKIP'
            'SKIP'
            'SKIP'
            '9ec860b54a91f4852d9426e7e471de918132c29e846772cc3d65b33ff5be78b95875ba994f96882e0ce9f5c6dccac998')

prepare() {
  cd "$srcdir"/$pkgname
  git config submodule."test/test_helper/bats-support".url "$srcdir"/bats-support
  git config submodule."test/test_helper/bats-assert".url "$srcdir"/bats-assert
  git submodule update --init

  sed -i 's/libexec/lib/g' diff-so-fancy readme.md
  mv libexec lib

  patch -p1 -i ../system-diff_highlight.patch
  rm -r third_party
  rm test/diff-highlight.bats
}

check() {
  cd "$srcdir"/$pkgname
  bats test/*.bats
}

package() {
  cd "$srcdir"/$pkgname

  sed -i 's|$(get_script_dir)/lib/diff-so-fancy.pl|/usr/share/diff-so-fancy/diff-so-fancy.pl|' diff-so-fancy
  
  install -Dm755 'diff-so-fancy' "$pkgdir"/usr/bin/diff-so-fancy
  install -Dm755 'lib/diff-so-fancy.pl' "$pkgdir"/usr/share/diff-so-fancy/diff-so-fancy.pl

  install -Dm644 readme.md "$pkgdir"/usr/share/doc/diff-so-fancy/readme.md
}
