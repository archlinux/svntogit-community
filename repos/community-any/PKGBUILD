# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslotest
pkgver=4.4.0
pkgrel=1
pkgdesc="Oslo test framework"
arch=('any')
url="https://github.com/openstack/oslotest"
license=('Apache')
depends=('python-fixtures' 'python-subunit' 'python-stestr' 'python-testscenarios'
         'python-testtools' 'python-mock' 'python-mox3' 'python-os-client-config'
         'python-debtcollector')
makedepends=('python-pbr')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslotest/archive/$pkgver.tar.gz")
sha512sums=('b334f8ae3185a49d33e36fd733b01d1b22d799379125308679efa13a500ee42c53bc830b5cd62922b5e721539972aa5909724fedf760a2831626c4797b40cb4e')

export PBR_VERSION=$pkgver

build() {
  cd oslotest-$pkgver
  python setup.py build
}

check() {
  cd oslotest-$pkgver
  stestr run
}

package() {
  cd "$srcdir"/oslotest-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
