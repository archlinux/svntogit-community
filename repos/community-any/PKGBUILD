# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgbase=python-html2text
pkgname=('python-html2text' 'python2-html2text')
pkgver=2019.8.11
pkgrel=1
pkgdesc="A HTML to markdown-structured text converter"
arch=('any')
url="https://pypi.python.org/pypi/html2text/"
license=('GPL3')
makedepends=('python-setuptools' 'python2-setuptools' 'python-coverage' 'python2-coverage' 'python-tox' 'python2-tox')
provides=('html2text')
replaces=('html2text')
source=($pkgname-$pkgver.tar.gz::https://github.com/Alir3z4/html2text/archive/$pkgver.tar.gz)
sha256sums=('aeffe2b403ddf242f43aba6b91b77791a489b32e6e8fa108f7963d11cb85c862')

prepare() {
  cp -a html2text-$pkgver{,-py2}
}

build() {
  cd "${srcdir}"/html2text-$pkgver
  python setup.py build
  
  cd "${srcdir}"/html2text-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "${srcdir}"/html2text-$pkgver
#   tox3

  cd "${srcdir}"/html2text-$pkgver-py2
#   tox2
}

package_python-html2text() {
  depends=('python-setuptools')

  cd "${srcdir}"/html2text-$pkgver
  python setup.py install --root="${pkgdir}"
}

package_python2-html2text() {
  depends=('python2-setuptools')

  cd "${srcdir}"/html2text-$pkgver-py2
  python2 setup.py install --root="${pkgdir}"
  mv "$pkgdir/usr/bin/html2text"{,2}
}
