# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hypothesis
pkgname=('python-hypothesis' 'python2-hypothesis')
pkgver=3.34.0
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-attrs' 'python2-attrs'
             'python-coverage' 'python2-coverage' 'python2-enum34')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'flake8' 'python2-flake8' 'python-pytz'
              'python2-pytz' 'python-numpy' 'python2-numpy' 'python-faker' 'python2-faker'
              'python-flaky' 'python2-flaky' 'python-pytest-benchmark' 'python2-pytest-benchmark'
              'python-django' 'python2-django' 'python-pytest-xdist' 'python2-pytest-xdist'
              'python-mock' 'python2-mock' 'python-pandas' 'python2-pandas')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis-python/archive/$pkgver.tar.gz")
sha512sums=('45e49bb6bfa60b1bc78b09021c23c7180a2a7ff1f5bab317a9538a09af14e61b07b13d8fbef1ac0bc09acd7deae7b2a5c35b9869426dfa9494422033107a9f31')

prepare() {
  # Make tests faster
  sed -i 's/ -n 2$/ -n 16/' hypothesis-python-$pkgver/tox.ini

  cp -a hypothesis-python-$pkgver{,-py2}

  rm -r hypothesis-python-$pkgver/tests/py2
  rm -r hypothesis-python-$pkgver-py2/tests/py3

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/hypothesis-python-$pkgver
  python setup.py build

  cd "$srcdir"/hypothesis-python-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hypothesis-python-$pkgver
  mv tests/django ../
  python setup.py pytest
  mv ../django tests/
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m tests.django.manage test tests.django

  cd "$srcdir"/hypothesis-python-$pkgver-py2
  mv tests/django ../
  python2 setup.py pytest
  mv ../django tests/
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m tests.django.manage test tests.django
}

package_python-hypothesis() {
  depends=('python-attrs' 'python-coverage')
  optdepends=('python-pytz: for datetime and django module'
              'python-faker: for fakefactory and django module'
              'python-django: for django module'
              'python-numpy: for numpy module'
              'python-pytest: for pytest module')

  cd hypothesis-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-hypothesis() {
  depends=('python2-attrs' 'python2-coverage' 'python2-enum34')
  optdepends=('python2-pytz: for datetime and django module'
              'python2-faker: for fakefactory and django module'
              'python2-django: for django module'
              'python2-numpy: for numpy module'
              'python2-pytest: for pytest module')

  cd hypothesis-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
