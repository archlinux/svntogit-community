# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname='python-joblib'
pkgver=0.14.1
pkgrel=1
pkgdesc="Set of tools to provide lightweight pipelining in Python"
arch=('any')
url="https://joblib.readthedocs.io/"
license=('BSD')
depends=('python')
optdepends=('python-numpy: for array manipulation'
            'python-lz4: for compressed serialization')
makedepends=('python-setuptools')
source=("https://github.com/joblib/joblib/archive/${pkgver}.tar.gz")
sha256sums=('9cf07185bd8c778b2515f76b19a04af4979d417356e330fd90edebf8cfd29793')

build() {
  cd "$srcdir"/joblib-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/joblib-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
