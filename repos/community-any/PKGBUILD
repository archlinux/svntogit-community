# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-eventlet
_pyname=eventlet
pkgver=0.25.2
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='https://eventlet.net'
arch=('any')
license=('MIT')
depends=('python' 'python-greenlet' 'python-monotonic' 'python-dnspython')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq')
optdepends=(
  'python-psycopg2: non-blocking PostgreSQL support'
  'python-pyopenssl: non-blocking SSL support'
  'python-httplib2: non-blocking HTTP support'
  'python-pyzmq: non-blocking ZeroMQ support'
  'python-dnspython: non-blocking DNS support'
)
options=('!makeflags')
source=(https://github.com/eventlet/eventlet/archive/v${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('f59ccfef34411b38e497d99f960a7c696a80becb942fbfccc14a41ba1c70c73a17dbabf36f5dc03b4f250625992605002dd2753dd39e76b10d971a30fbf6bd31')
b2sums=('52b931597c9528e3d2061eef062a08b011320e39565119e2e0e8b01500844065b9f818ebbc35da1ef7ab75766644bdf46b06f952b421e33a68aa55f6dd30e856')

prepare() {
  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i ${_pyname}-${pkgver}/tests/*_test.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  make -C doc text
}

check() {
  cd ${_pyname}-${pkgver}
  nosetests -sv tests
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
