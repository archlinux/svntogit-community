# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=4.1.4
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='http://whitenoise.evans.io'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-django' 'python-requests' 'python-brotli')
source=("$pkgname-$pkgver.tar.gz::https://github.com/evansd/whitenoise/archive/v$pkgver.tar.gz")
sha512sums=('45d55976dbba26bfdde15537886d80593535b141636334c6e0ce403c9f6d474c177cd90ede339701eafcf4ac23dbb2cd4d36541563f477bdfaccce0bb51b10a5')

build() {
  cd whitenoise-$pkgver
  python setup.py build
}

check() {
  export DJANGO_SETTINGS_MODULE=tests.django_settings
  export PYTHONWARNINGS=all

  cd whitenoise-$pkgver
  python -m unittest discover
}

package() {
  cd whitenoise-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
