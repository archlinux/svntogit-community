# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-pydocstyle
pkgver=5.0.2
pkgrel=1
pkgdesc="Docstring style checker"
arch=("any")
url="http://www.pydocstyle.org/"
license=("MIT")
depends=("python-snowballstemmer")
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-mock" "python-pip" "python-snowballstemmer")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pydocstyle/archive/${pkgver}.tar.gz")
sha256sums=('51543d61812f1b3a7195c122de4db2102b4cce07fe48cdac0eb59edf5b2cd8e0')

build() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py pytest --addopts "-k 'not test_integration'"
}

package() {
    cd "pydocstyle-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
