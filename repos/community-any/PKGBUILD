# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=firefox-extension-mailvelope
pkgver=4.6.0
pkgrel=1
pkgdesc="Browser extension for OpenPGP encryption with Webmail"
url="https://www.mailvelope.com"
license=('GPL2')
arch=('any')
groups=('firefox-addons')
depends=('firefox' 'gpgme')
source=("mailvelope-${pkgver}.xpi::https://github.com/mailvelope/mailvelope/releases/download/v${pkgver}/mailvelope.firefox.xpi"
				"mailvelope-${pkgver}.xpi.sig::https://github.com/mailvelope/mailvelope/releases/download/v${pkgver}/mailvelope.firefox.xpi.sig"
		    'gpgmejson.json')
sha256sums=('f332e8a407f342f6144c7689e8f192fc94171ae2b896cec8bd0654f147ad4287'
            'SKIP'
            '1f8d0cdef00aad2291772931da99968167b8c40ff48ce244084f381cea6a333f')
validpgpkeys=('16F45CCC184812B4AC930EF360DD1F16B3390CD1')
noextract=("mailvelope-${pkgver}.xpi")

package() {
	install -Dm644 "mailvelope-${pkgver}.xpi" \
    "${pkgdir}/usr/lib/firefox/browser/extensions/jid1-AQqSMBYb0a8ADg@jetpack.xpi"
	install -Dm644 "gpgmejson.json" \
    "${pkgdir}/usr/lib/mozilla/native-messaging-hosts/gpgmejson.json"
}
