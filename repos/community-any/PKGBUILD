# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Simon Kohlmeyer <simon.kohlmeyer@gmail.com>

_gemname=minitest
pkgname=ruby-${_gemname}
_gitcommit=2362e328883ff133140fd8bea5e9ec9a714a7999
pkgver=5.11.3
pkgrel=2
pkgdesc='Complete suite of testing facilities supporting TDD, BDD, mocking, and benchmarking'
url='https://github.com/seattlerb/minitest'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('git' 'rubygems' 'ruby-rake' 'ruby-rdoc' 'ruby-hoe')
options=('!emptydirs')
source=("git+https://github.com/seattlerb/minitest#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${_gemname}
  grep 'VERSION = "' lib/minitest.rb|sed -E 's|.*"(.+)".*|\1|'
}

build() {
  cd ${_gemname}
  rake gem
}

check() {
  cd ${_gemname}
  rake test
}

package() {
  cd ${_gemname}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc History.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
