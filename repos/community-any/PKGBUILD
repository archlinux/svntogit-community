# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parsel
pkgver=1.6.0
pkgrel=1
pkgdesc="Parsel is a library to extract data from HTML and XML using XPath and CSS selectors"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/parsel"
depends=('python-cssselect' 'python-lxml' 'python-six' 'python-w3lib')
makedepends=('python-pytest' 'python-sybil')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/parsel/archive/v$pkgver.tar.gz")
sha512sums=('d3161522e1102cceeb1f29ab020b33019d4ac7f096cfab8fbe40f54073e49afc24f02aa54206d86f56c8f92e3db2d0d2f8a3dbdfa6417492cf299201f2ae6604')

build() {
  cd parsel-$pkgver
  python setup.py build
}

check() {
  cd parsel-$pkgver
  python -m pytest
}

package() {
  cd parsel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
