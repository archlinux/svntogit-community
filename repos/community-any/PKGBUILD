# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.41.0
_image_pkgver=5.13.2
_podman_pkgver=3.2.2
_skopeo_pkgver=1.3.1
_storage_pkgver=1.32.6
pkgrel=2
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('9d096fd6fafc2d02c814eb9724ab3f4f4cc73b6930fc725eff1bd7e72278d1f4687527b31d771f72a6923e5d9875ed10ff4e7b38ca822d8b3304fd12eaef962f'
            '8d7413ca676099378d51831b2d0bdf8d412352a582333bfed6a701b041d59d05ed3a596271ba57635daa1779cc1cbee670c8f00d83d6caf8c333fa9175a7c4fa'
            '00f20459feb72126cf8b87918dfeabe95844b7b15ef891f4c5baa850ad21b5694ec75e477c3ab3a9da08427a307fdc4421533037e72bda26392c0d42d3635e39'
            '65b8ba735bd409b25b56a5538de4e09edfd11d684752a6e9d28037d0f2b80aef80796c2e9588fcf192d24bd885cc7b2d65b0ee7f7c34d18b2eacc25816da5d36'
            'cc518c2960a1f83ddc120fe97a7ecc48fe2800565e5c09792df8b84023ce55270dd3d7f6e2f7eb3da257b476b89ba0e4c15883b242935dbe6c6518fd47b9f2ec'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('0ce91a52f0edf08cb5c2c86d7deaa29ed1b95814ded2545c1b02698fef26977fecca10293114b07a99f3827ae8f0d0d63437e317b5e7a8fc6095bb88abbb8f3b'
        '9b11624aa1f9be5845cdb114156025f563d62145a87f3be854f9e9834c86d8dfb2f39e27c4aff822495e198b79f08bd43a1b8ebcf4491b459b02af2bc17a380b'
        '3e8683ef32a6058e2184cb3789c9f80353ba34818870aebb1d76b84c0e43cd111b31c0237e4dd71fe61c8e831e01db42f8ef817a23f4f5f92605d9ef9592bea2'
        'eda2767a1b1b899b99fc70ef361541c388cab77d4521cc2792baf78ca2474e73ff2d7d64d26f713410a40600866fd6c20855d4acdbdb0a31b11d7efaa8101b83'
        '06870e1276e3838e591872ac7e5563938eaf7cd3b8f2cf997d51c1fc45f81f7ba013bbae4fe012befb7dc4dada92c599180a44a05481cd9ffb46f735bc2623e1'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
