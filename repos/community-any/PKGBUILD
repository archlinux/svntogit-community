# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cerberus
pkgver=1.3.3
pkgrel=1
pkgdesc="Lightweight, extensible schema and data validation tool for Python dictionaries"
url="https://github.com/pyeve/cerberus"
license=('ISC')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-benchmark')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyeve/cerberus/archive/$pkgver.tar.gz")
sha512sums=('2f2696751dd1f5191fc1ef328c1b1d604d94c87d3c1eb3210ecbede499d2f13a1d197a1c42a5eb4a11c7257214437857f4023a55415d83793a2f3b1a8aa4a8bd')

build() {
  cd cerberus-$pkgver
  python setup.py build
}

check() {
  cd cerberus-$pkgver
  python setup.py pytest --addopts cerberus
}

package() {
  cd cerberus-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
