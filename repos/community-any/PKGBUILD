# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgbase='python-qtawesome'
_pkgbase='qtawesome'
pkgname=('python-qtawesome' 'python2-qtawesome')
pkgver=0.4.4
pkgrel=3
pkgdesc='Iconic fonts in PyQt and PySide applications'
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
arch=('any')
url="https://github.com/spyder-ide/${_pkgbase}/"
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${_pkgbase}/archive/v${pkgver}.tar.gz")
sha256sums=('f6cdc1546906c00019adf748d259ec68055113b65108e90ce7bd8a869eb3daf4')

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py build

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build
}

package_python2-qtawesome() {
  depends=('python2-six' 'python2-qtpy')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-qtawesome() {
  depends=('python-six' 'python-qtpy')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
