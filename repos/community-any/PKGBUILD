# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-cmake3
pkgver=0.04
pkgrel=3
pkgdesc="Find or download or build cmake 3 or better"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-cmake3"
depends=('cmake' 'perl-alien-build')
makedepends=('perl-html-parser' 'perl-sort-versions' 'perl-test2-suite' 'perl-uri')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-cmake3-$pkgver.tar.gz")
sha512sums=('12e8104dad59183a59b8a5cf75560292bc04aea23e2c33a2cd318e8779df4641ee8a75a5a3d785e33e59c3105e45bb64e3f1b749656e9f661897ecd42ab4b75c')

build() {
  cd Alien-cmake3-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-cmake3-$pkgver
  make test
}

package() {
  cd Alien-cmake3-$pkgver
  make DESTDIR="$pkgdir" install
}
