# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=hypercorn
pkgver=0.7.0
pkgrel=1
pkgdesc='An ASGI Server based on Hyper libraries and inspired by Gunicorn'
url=https://gitlab.com/pgjones/hypercorn
arch=(any)
license=(MIT)
depends=(
  python-h11
  python-h2
  python-toml
  python-typing_extensions
  python-wsproto
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-hypothesis
  python-pytest
  python-pytest-asyncio
  python-pytest-sugar
  python-pytest-trio
  python-tox
  python-trio
)
optdepends=(
  'python-trio: trio support'
)
source=(git+https://gitlab.com/pgjones/hypercorn.git#tag=${pkgver})
sha256sums=(SKIP)

build() {
  cd hypercorn

  python setup.py build
}

check() {
  cd hypercorn

  tox -e py37
}

package() {
  cd hypercorn

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/hypercorn/
}

# vim: ts=2 sw=2 et:
