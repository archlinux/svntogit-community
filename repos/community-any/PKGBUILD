# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-tweepy
_pypiname=tweepy
pkgver=4.14.0
pkgrel=1
pkgdesc='Python library for accessing the entire Twitter API'
url='https://www.tweepy.org/'
arch=('any')
license=('MIT')
depends=('python' 'python-oauthlib' 'python-python-socks' 'python-requests' 'python-requests-oauthlib')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=(
  'python-aiohttp'
  # 'python-async-lru'  # TODO: needs package
  'python-vcrpy'
)
optdepends=(
  'python-aiohttp: for async support'
  # 'python-async-lru: for async support'  # TODO: needs package
)
source=(https://github.com/tweepy/${_pypiname}/archive/v${pkgver}/${_pypiname}-${pkgver}.tar.gz)
sha256sums=('ad30f817b746b4519f40af2f6577624cf7775d4e9379c559cc7d1099702eb921')
sha512sums=('b4368c379b01cda05bac71cf188c31ad305ee6b1a66c76c6feb9622aa43b7875759763fce95be3ee12798f4948121501af788dc3161fada0d2f4a23bebfb0a1e')

build() {
  cd ${_pypiname}-${pkgver}
  python -m build --wheel --no-isolation
}

# check() {
#   cd ${_pypiname}-${pkgver}
#   python -m unittest discover -v
# }

package() {
  cd ${_pypiname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
