# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Rich Li <rich at dranek com>
# Contributor: Thomas Haider <t.haider@deprecate.de>
# Contributor: Filip S. Adamsen <fsa [at] fsadev [dot] com>
# Contributor: Samed Beyribey <ras0ir@eventualis.org>

pkgname=python-authres
pkgver=1.2.0
pkgrel=9
pkgdesc='Python Authentication-Results headers generation and parsing'
arch=('any')
url=https://launchpad.net/authentication-results-python
license=('Apache')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/a/authres/authres-$pkgver.tar.gz")
sha256sums=('93d1b995ad7ce21e62db649f361048125dd6022563a0ae8a23909465f1fd25b7')

build() {
  cd authres-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd authres-$pkgver
  python -m authres -v
}

package() {
  cd authres-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
