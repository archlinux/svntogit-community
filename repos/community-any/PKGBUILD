# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer:  Christian Hesse <mail@eworm.de>
# Contributor: Jon Eyolfson <jon@eyolfson.com>

pkgname=gitolite
pkgver=3.6.10
pkgrel=1
pkgdesc='An access control layer on top of git'
url='https://github.com/sitaramc/gitolite'
arch=('any')
license=('GPL2')
depends=('git' 'perl' 'openssh')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz"
        'gitolite-sysusers.conf')
sha256sums=('0ae3bea637b25cff13826e5ecd181c7b74a6eff377cf4c2243d85c2b0a290d3f'
            '5e4f3aaa406c25fcd88fffaee74ea4878cecf1dd8c777456e4416caf35292dbf')

prepare() {
  cd $pkgname-$pkgver/src
  echo "$pkgver" > VERSION
}

package() {
  # create directories
  install -d "$pkgdir"/usr/{bin,lib/gitolite}

  # install files and create symlink
  cp -a $pkgname-$pkgver/src/* "$pkgdir"/usr/lib/gitolite
  ln -s /usr/lib/gitolite/gitolite "$pkgdir"/usr/bin/

  # sysusers file
  install -D -m 0644 "${srcdir}"/gitolite-sysusers.conf "${pkgdir}"/usr/lib/sysusers.d/gitolite.conf
}
