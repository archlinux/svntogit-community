# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

_gemname=paint
pkgname=ruby-${_gemname}
pkgver=2.2.1
pkgrel=2
pkgdesc='Ruby paint manages terminal colors and effects'
url='https://github.com/janlelis/paint'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rspec' 'ruby-rainbow' 'ruby-term-ansicolor')
makedepends=('ruby-rake')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('8190c220310b724defa0f482b264afe32bcfd00044380dbb3d8354050857f121c1e084bd737f7b9b89a6950ea4be2c87b7f7282854641df65918bfb6bead5450')
b2sums=('f0d3e39244fd103e7f248161369189e2e5dafc7eb0f0813998e0da4a80714ab355ca1ebb9357695e97b57db49a90198a2efc0a4043a4e7bfd18fedf58515d113')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i paint.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  rake gems
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 MIT-LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
