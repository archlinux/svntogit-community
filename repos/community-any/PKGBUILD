# Maintainer: Ivy Foster <escondida@iff.ink>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

# Gentium Plus is upstream's replacement for ttf-gentium, now called Gentium Basic.
pkgname=gentium-plus-font
pkgver=5.000
pkgrel=3

pkgdesc='Font supporting a wide range of Latin- and Cyrillic-based alphabets'
url='https://scripts.sil.org/Gentium'
license=('custom:OFL')

replaces=(ttf-gentium)

arch=(any)
source=(
	"https://software.sil.org/downloads/r/gentium/GentiumPlus-$pkgver.zip"
	"https://software.sil.org/downloads/r/gentium/GentiumPlusCompact-$pkgver.zip"
)
# Checksum provided by packager, not upstream.
sha256sums=(
	335911f17bd2de4e43742e1d0367cfeff19a90abf7ed604f100a42705042e154
	1d8a4ff03dce90f6002b008a5e37f890c409bc22e4e26561b67f3f3c40991b5c
)
package() {
	mkdir -p "$pkgdir/usr/share/fonts/gentium/" \
		"$pkgdir/usr/share/licenses/gentium-plus-font" \
		"$pkgdir/usr/share/doc/gentium-plus-font"

	cd "GentiumPlus-$pkgver"
	install -m644 *.ttf "$pkgdir/usr/share/fonts/gentium"
	install -m644 OFL.txt OFL-FAQ.txt \
		"$pkgdir/usr/share/licenses/gentium-plus-font"
	install -m644 GENTIUM-FAQ.txt README.txt documentation/* \
		"$pkgdir/usr/share/doc/gentium-plus-font"

	cd "../GentiumPlusCompact-$pkgver"
	install -m644 *.ttf "$pkgdir/usr/share/fonts/gentium"
	# Most of the documentation is identical to that in the main
	# Gentium Plus distribution. These files aren't.
	install -m644 README.txt \
		"$pkgdir/usr/share/doc/gentium-plus-font/README.compact.txt"
	install -m644 feat_set_tuned.xml \
		"$pkgdir/usr/share/doc/gentium-plus-font/compact_variant_feat_set_tuned.xml"
}
