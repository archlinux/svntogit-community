# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: PepeSmith
# Contributor: Aron Asor <aronasorman at gmail.com>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor : Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=ipython 
pkgname=(ipython ipython2 ipython-notebook ipython2-notebook)
pkgver=3.2.0
pkgrel=1
pkgdesc="An enhanced Interactive Python shell."
arch=('any')
url="http://ipython.org"
license=('BSD')
depends=('python' 'sqlite' 'python-setuptools')
makedepends=('python-setuptools' 'python2-setuptools' 'sqlite')
optdepends=("python-nose: for IPython's test suite"
            "python-pyqt4: for ipython qtconsole"
            "python-sip: for ipython qtconsole"
            "python-pygments: for ipython qtconsole"
            "python-pyzmq: for ipython qtconsole")
#source=("http://archive.ipython.org/release/$pkgver/ipython-$pkgver.tar.gz")
#source=("https://github.com/ipython/ipython/archive/rel-$pkgver.tar.gz")
source=("https://pypi.python.org/packages/source/i/ipython/$pkgbase-$pkgver.tar.gz")
md5sums=('41aa9b34f39484861e77c46ffb29b699')

# confirm that an update does not break sage?

build() {
  cd "$srcdir"
  cp -r ipython-$pkgver ipython2-$pkgver
}

package_ipython() {
  provides=('ipython3')
  replaces=('ipython3')
  cd "$srcdir/ipython-$pkgver"

  # see https://github.com/ipython/ipython/issues/2057
  #export LC_ALL=en_US.UTF-8
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  find "$pkgdir/" -type d -empty -delete

  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython/LICENSE"

  cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython.desktop"
  install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython-qtconsole.desktop"
}

package_ipython2() {
  pkgdesc="An enhanced Interactive Python2 shell."
  depends=('python2' 'sqlite' 'python2-setuptools')
  optdepends=("wxpython: needed for ipython2 --gui=wx"
              "python2-nose: for IPython's test suite"
              "python2-pyqt4: for ipython qtconsole"
              "python2-sip: for ipython qtconsole"
              "python2-pygments: for ipython qtconsole"
              "python2-pyzmq: for ipython qtconsole")

  cd "$srcdir/ipython2-$pkgver"

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  rm -rf "$pkgdir/usr/share/doc"
  find "$pkgdir" -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
  find "$pkgdir/" -name '*.pyc' -delete

  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython2/LICENSE"

  # hack to get around ipython collision
  cd "$pkgdir/usr/share/man/man1/"
  for i in *; do
    mv $i ${i/%.1/2.1}
  done
  find "$pkgdir/usr/bin/" -type f -regex '.*[^2]$' -delete

  cd "$srcdir/ipython-$pkgver/examples/IPython Kernel/"
  sed -i 's/ython/ython2/g' *.desktop
  install -Dm644 ipython.desktop "$pkgdir/usr/share/applications/ipython2.desktop"
  install -Dm644 ipython-qtconsole.desktop "$pkgdir/usr/share/applications/ipython2-qtconsole.desktop"
}

# Nothing in these packages except dependencies because five optdeps is too many

package_ipython-notebook() {
  pkgdesc="Web-based environment where you can combine code, text, math, plots and media into a single document"
  depends=('python-pyzmq' 'python-tornado' 'python-terminado'
           'python-jinja' 'python-jsonschema' 'python-mistune'
           'python-pygments')
  optdepends=('haskell-pandoc: ipython notebook conversion'
              'texlive-bin: notebook pdf export')
  cd "$srcdir/ipython-$pkgver"
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython-notebook/LICENSE"
}

package_ipython2-notebook() {
  pkgdesc="Web-based environment where you can combine code, text, math, plots and media into a single document"
  depends=('python2-pyzmq' 'python2-tornado' 'python2-terminado'
           'python2-jinja' 'python2-jsonschema' 'python2-mistune'
           'python2-pygments')
  optdepends=('haskell-pandoc: ipython notebook conversion'
              'texlive-bin: notebook pdf export')
  cd "$srcdir/ipython2-$pkgver"
  install -Dm644 docs/source/about/license_and_copyright.rst "$pkgdir/usr/share/licenses/ipython2-notebook/LICENSE"
}

