# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rb-inotify
pkgname=ruby-rb-inotify
pkgver=0.9.10
pkgrel=2
pkgdesc='Thorough inotify wrapper for Ruby using FFI'
url='https://github.com/guard/rb-inotify'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-ffi')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/guard/rb-inotify/archive/v${pkgver}.tar.gz)
sha256sums=('b7de45be4b976515848bd67282445dfb10437c2bd8ef9abd2e5ac1b312c2ce32')
sha512sums=('9c935f89dc088b0f72834bf86c42378d8acc401a5e485e0f7823523be4110e4b23aa19986aed18262bb3233e8e6971b4f532a29ea7a039d797fe93dd52968eab')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files|find -type f|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
