# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=precis_i18n
pkgname=python-precis_i18n
pkgver=1.0.5
pkgrel=1
pkgdesc='PRECIS framework (RFC 8264, RFC 8265, RFC 8266)'
url='https://github.com/byllyfish/precis_i18n'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0cc6281d206d5d682ba905a8f383b710a3eb39410a57477fd0d7bb24adc05d4ab2cab8475ac6617c473480a5f10a6e13396455237612e294ca4fb4de5b20af5b')
b2sums=('6f1b07e461adedf8345b943145d75447111240f0c9f45a1b7f866f4247c7aac8b3a914ea4a712fc712eeebdccd363513c2ac2b1541139ddc29e83308bf5e0412')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGELOG.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
