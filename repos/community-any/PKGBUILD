# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-run
pkgver=9.1.0
pkgrel=1
pkgdesc="Install packages and run Python with them"
url="https://github.com/jaraco/pip-run"
license=('MIT')
arch=('any')
depends=('python-autocommand' 'python-packaging' 'python-path' 'python-pip' 'python-more-itertools')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'jupyter-nbformat' 'python-pytest-enabler')
source=("https://github.com/jaraco/pip-run/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('71e742f113132b95bb06ac98de881beae701ba04f8eeaaa6ed848a731b652a2ff7e3dad9b697e66b25138eaf0dd35cb06f61c2d92c185d565a7c96392685f65d')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pip-run-$pkgver
  python -m build -wn
}

check() {
  cd pip-run-$pkgver
  python -m pytest
}

package() {
  cd pip-run-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
