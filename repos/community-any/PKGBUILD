# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>

pkgname=clojure
pkgver=1.6.0
pkgrel=1
pkgdesc='LISP dialect for the JVM'
url='http://www.clojure.org/'
arch=('any')
license=('CPL')
depends=('java-environment' 'sh')
makedepends=('java-environment' 'classpath' 'git' 'maven')
optdepends=('rlwrap: friendlier shell with readline support')
install='clojure.install'
source=("$pkgname::git://github.com/clojure/clojure.git#tag=$pkgname-$pkgver"
        'clj.sh'
        "$pkgname.sh")
sha256sums=('SKIP'
            '0d0f9e7db77f4b5f507ee18e140c29138b7bff5c4ca9d0b963dfd657c6e3c157'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')

build() {
  cd "$pkgname"

  mvn package -Dmaven.test.skip=true -Dmaven.repo.local=m2
}

check() {
  cd "$pkgname"

  #mvn test -Dmaven.repo.local=m2
}

package() {
  install -Dm755 clj.sh "$pkgdir/usr/bin/clj"
  install -Dm755 clj.sh "$pkgdir/usr/bin/clojure"
  install -Dm755 "$pkgname.sh" "$pkgdir/etc/profile.d/$pkgname.sh"
  install -Dm644 "$pkgname/target/$pkgname-$pkgver.jar" \
    "$pkgdir/usr/share/$pkgname/$pkgname.jar"
}

# vim:set ts=2 sw=2 et:
