# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-cssselect2
pkgver=0.7.0
pkgrel=2
pkgdesc="CSS selectors for Python ElementTree"
url="https://pypi.org/project/cssselect2/"
license=('BSD')
arch=('any')
depends=('python-tinycss2' 'python-webencodings')
makedepends=('python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-pytest-cov' 'python-pytest-isort' 'python-pytest-runner')
source=("https://pypi.io/packages/source/c/cssselect2/cssselect2-$pkgver.tar.gz")
sha512sums=('e3c975fe159d3bec53002bda31c72d9cb346f40529272d8d5bef6aa13142ce60f1e8aa20b039d93ff5ff3d2cd34119b8b2d406f889964eded69e770e4f04e949')

build() {
  cd cssselect2-$pkgver
  python -m build -nw
}

check() {
  cd cssselect2-$pkgver
  pytest
}

package() {
  cd cssselect2-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
