# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=SolidPython
pkgname=python-solidpython
pkgver=1.0.1
pkgrel=2
pkgdesc='Python frontend for solid modelling that compiles to OpenSCAD'
arch=('any')
url='https://github.com/SolidCode/SolidPython'
license=('LGPL')
depends=('python-euclid3' 'python-pypng' 'python-prettytable' 'python-regex')
makedepends=('python-setuptools' 'python-dephell')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('4b2c9193be5389b12759f94fd9aa448dfa96b860fd182e6b17bb9106c83bdff1627a858eeaa46fcfd30b41c418f3986adc9d06cd5bbf2ecf18e31a44abce0816')

prepare() {
  cd $_pkgname-$pkgver

  sed -i -e '/tox/d' -e 's|regex = "^2019.4"|regex = ">=2019.4"|' pyproject.toml

  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=$(pwd) solid/test/run_all_tests.sh
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

