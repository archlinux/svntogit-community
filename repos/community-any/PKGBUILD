# $Id$
# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>
# Contributor: M0Rf30

pkgname=drbl
pkgver=2.8.25
pkgrel=1
pkgdesc="Diskless Remote Boot in Linux: manage the deployment of the GNU/Linux operating system across many clients"
arch=('any')
url="http://drbl.sourceforge.net"
license=('GPL')
depends=('perl' 'bc')
makedepends=('vim')
backup=('etc/drbl/drbl.conf')
options=('!emptydirs')
source=(http://free.nchc.org.tw/$pkgname-core/src/stable/$pkgname-$pkgver.tar.bz2
        usrbin.patch)
md5sums=('2bdca7d7da68f28d2d13ac2124fd4092'
         'f01eac478f1ae62f505de645de96c68a')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  patch -Np0 -i "$srcdir"/usrbin.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  make  
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make DESTDIR="$pkgdir"/ install

  chmod +r "$pkgdir"/usr/share/drbl/pkg/syslinux/makeboot.sh
}
