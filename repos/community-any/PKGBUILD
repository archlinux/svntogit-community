# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stick <stick@stma.is>
# Contributor: MountainX

_name=atkinson-hyperlegible
pkgbase=$_name-font
pkgname=(otf-$_name ttf-$_name)
pkgver=1.006
_pkgver=1cb311624b2ddf88e9e37873999d165a8cd28b46
pkgrel=1
pkgdesc='A typeface focusing on leterform distinction for legibility for low vision readers'
arch=(any)
url=https://www.brailleinstitute.org/freefont
_url="https://github.com/googlefonts/$_name"
license=(OFL)
_archive="$_name-$_pkgver"
source=("$_url/archive/$_pkgver/$_archive.tar.gz")
sha256sums=('d761a7dd4f020d883a90cebda3258618159230523a19a16ed8b94ab0b0d17dae')

package_otf-atkinson-hyperlegible() {
	cd "$_archive"
	provides=($pkgbase)
	replaces=(${pkgbase}s)
	install -Dm0644 -t "$pkgdir/usr/share/fonts/OTF/" fonts/otf/*.otf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" OFL.txt
}

package_ttf-atkinson-hyperlegible() {
	cd "$_archive"
	provides=($pkgbase)
	install -Dm0644 -t "$pkgdir/usr/share/fonts/TTF/" fonts/ttf/*.ttf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" OFL.txt
}
