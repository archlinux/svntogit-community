# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=pytest-socket
pkgname=python-pytest-socket
pkgver=0.5.1
pkgrel=2
pkgdesc='Pytest Plugin to disable socket calls during tests'
arch=('any')
license=('MIT')
url='https://github.com/miketheman/pytest-socket'
depends=('python-pytest')
makedepends=('python-build' 'python-installer' 'python-poetry-core' 'python-wheel')
checkdepends=('python-asynctest' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-httpbin'
              'python-httpx' 'python-requests' 'python-starlette')
source=("https://github.com/miketheman/pytest-socket/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f43a99419056086aaee52206bc2816452919da617e54535285ce9e6c59b8bc39edc3326a5c629cf25f1677bd2152d300a6191464671298888c7683d3c2181eff')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
