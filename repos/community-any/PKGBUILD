# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=ydcv
pkgname=('ydcv' 'ydcv-python2')
pkgver=0.3.3
pkgrel=1
arch=("any")
url="https://github.com/felixonmars/ydcv"
license=("GPL")
makedepends=('python2' 'python3')
source=("https://github.com/felixonmars/$pkgbase/archive/$pkgver.tar.gz")

package_ydcv() {
  pkgdesc="YouDao Console Version - Simple wrapper for Youdao online translate (Chinese <-> English) service API, as an alternative to the StarDict Console Version(sdcv)"
  depends=("python3")
  install -Dm755 "${srcdir}/$pkgbase-$pkgver/$pkgbase.py" "${pkgdir}/usr/bin/$pkgbase"
}

package_ydcv-python2() {
  pkgdesc="YouDao Console Version - Simple wrapper for Youdao online translate (Chinese <-> English) service API, as an alternative to the StarDict Console Version(sdcv) - Python2 version"
  depends=("python2")
  provides=('ydcv')
  conflicts=('ydcv')

  cp "${srcdir}/$pkgbase-$pkgver/$pkgbase.py" "${srcdir}/$pkgbase-$pkgver/$pkgbase-python2.py"
  sed -i -e "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" "${srcdir}/$pkgbase-$pkgver/$pkgbase-python2.py"

  install -Dm755 "${srcdir}/$pkgbase-$pkgver/$pkgbase-python2.py" "${pkgdir}/usr/bin/$pkgbase"
}

# vim:set ts=2 sw=2 et:
sha512sums=('9e355f3e925153bb2eb7534e34064b02854720c2c94583ded08d8dc93f5835049c0767e10a665bd5b03a641018852f9c9455cee9de586e8c49732f25c5f9f5f7')
