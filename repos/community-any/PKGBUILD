# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-treq
pkgver=18.6.0
pkgrel=3
pkgdesc="A requests-like API built on top of twisted.web's Agent"
arch=('any')
license=('MIT')
url='https://github.com/twisted/treq'
depends=('python-incremental' 'python-requests' 'python-pyopenssl' 'python-service-identity'
         'python-idna' 'python-attrs' 'python-twisted')
makedepends=('python-setuptools' 'httpbin')
checkdepends=('python-mock' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/twisted/treq/archive/release-$pkgver.tar.gz"
        py3.7.diff)
sha512sums=('1d0d793725647c6b451853f166436040e49095fca43f6c74053f3ba18ec8f8ae0095ed78c7d141cc95dfba9674339ac44262943225bec7be0f0cb05253758688'
            '6806e8186c8742ac3c40e71d4e14b25b96d198995f03a8730755b3594b4a9e854fd799ceec4d6799571e14e02bca2d3797658b2f2eba38b76a8660156e44c84e')

prepare() {
  patch -d treq-release-$pkgver -p1 < py3.7.diff
}

build() {
  cd treq-release-$pkgver
  python setup.py build
}

check() {
  cd treq-release-$pkgver
  python setup.py pytest
}

package() {
  cd treq-release-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
