# Maintainer: Maxim Baz <archlinux at maximbaz dot com>

pkgname=rebuild-detector
pkgver=4.4.2
pkgrel=1
pkgdesc='Detects which packages need to be rebuilt'
arch=('any')
url="https://github.com/maximbaz/${pkgname}"
license=('ISC')
depends=('binutils' 'parallel' 'pacutils' 'pacman-contrib')
makedepends=('marked-man')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/maximbaz/${pkgname}/archive/refs/tags/${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::https://github.com/maximbaz/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig")
b2sums=('2cc13d6f20d027cfccd272853fb532441e981ea82185a107b10a625352a6dee645d8dafb35a151ab17292a455b98a0a8ba6c6f5bf9a70775dc41f1ea2b38f3ed'
        'SKIP')
validpgpkeys=('56C3E775E72B0C8B1C0C1BD0B5DB77409B11B601')

build() {
    cd "${pkgname}-${pkgver}"
    make docs
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
