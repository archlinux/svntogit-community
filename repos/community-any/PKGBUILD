# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rake-compiler
pkgname=ruby-${_gemname}
pkgver=1.1.1
pkgrel=2
pkgdesc='Rake-based Ruby Extension (C, Java) task generator'
url='https://github.com/rake-compiler/rake-compiler'
arch=('any')
license=('MIT')
depends=('ruby' 'rubygems' 'ruby-rake')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/rake-compiler/rake-compiler/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha512sums=('e9cf9752e48eae912657a34d2f86a2b41f7d57620c6e43b1fa6fb8e5715336b2fdf99bf20a4851889e8b9b7b2e834421c13e108c538abdc3cec968952345361d')
b2sums=('c6862f24ec57c25fdeb7faeeda9eb96cb64ac972c852a81b77ec9b35d21018a5647089cd38d80b03a79e2c07d3f9af42bbaf9998457ad3beed27c8285c23689f')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md History.txt -t "${pkgdir}/usr/share/doc/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
