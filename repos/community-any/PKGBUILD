# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-toposort
pkgname=('python-toposort' 'python2-toposort')
pkgver=1.5
pkgrel=2
pkgdesc='Implements a topological sort algorithm'
arch=('any')
license=('Apache')
url='https://bitbucket.org/ericvsmith/toposort'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://bitbucket.org/ericvsmith/toposort/get/$pkgver.tar.bz2")
sha512sums=('f79ee4a04b157db164e484be054bed6d310023722cd13469454022eb96ccd3ff5bf6dc570384d3ca790c819b67afa4a96c716da2b99c458d8ddfe2e56e5222a0')

prepare() {
  mv ericvsmith-toposort-* toposort-$pkgver
  cp -a toposort-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/toposort-$pkgver
  python setup.py build

  cd "$srcdir"/toposort-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/toposort-$pkgver
  python setup.py test

  cd "$srcdir"/toposort-$pkgver-py2
  python2 setup.py test
}

package_python-toposort() {
  depends=('python')

  cd toposort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-toposort() {
  depends=('python2')

  cd toposort-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
