# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-validators
pkgver=0.18.2
_commit=f7221ba5f847aad46714bae7f562a83a2df3327e
pkgrel=3
pkgdesc="Python Data Validation for Humans"
url="https://github.com/kvesteri/validators"
license=('MIT')
arch=('any')
depends=('python-decorator' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/kvesteri/validators/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('24a6a119aae5ac509b7d52a85ca11051760ca4ebead98ffc274cb253da7176327347cdcebf1e93a61968c464074901731e84a609557c7c80ab3fdd12c1229907')

build() {
  cd validators-$_commit
  python setup.py build
}

check() {
  cd validators-$_commit
  python -m pytest
}

package() {
  cd validators-$_commit
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
