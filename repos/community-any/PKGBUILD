# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-mongomock
pkgver=3.19.0
pkgrel=2
pkgdesc="Fake pymongo stub for testing simple MongoDB-dependent code"
url="https://github.com/mongomock/mongomock"
license=('BSD')
arch=('any')
depends=('python-sentinels' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mongomock/mongomock/archive/$pkgver.tar.gz")
sha512sums=('1f89786d2c2db69a6f9552d3779da5e2cd68ce8dc0b526b46c1de27937b6fa080816b64828f85ad3f0337237fb882bc0644a76eb9bf1d6b6beb5f897e566cb01')

export PBR_VERSION=$pkgver

build() {
  cd "$srcdir"/mongomock-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/mongomock-$pkgver
  python setup.py nosetests
}

package_python-mongomock() {
  cd mongomock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
