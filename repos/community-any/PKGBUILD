# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-home-assistant-bluetooth
pkgdesc='Basic bluetooth models used by Home Assistant'
pkgver=1.9.1
pkgrel=1
arch=(any)
url=https://github.com/home-assistant-libs/home-assistant-bluetooth
license=(APACHE)
depends=(
  python
  python-bleak
)
makedepends=(
  cython
  git
  python-build
  python-installer
  python-poetry-core
  python-setuptools
  python-wheel
)
_tag=cc7efb2ef5a49f9bb6ea86496bcea4fafb4af3eb
source=(git+https://github.com/home-assistant-libs/home-assistant-bluetooth.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd home-assistant-bluetooth
  git describe --tags | sed 's/^v//'
}

build() {
  cd home-assistant-bluetooth
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" home-assistant-bluetooth/dist/*.whl
}

# vim: ts=2 sw=2 et:
