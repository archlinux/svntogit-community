# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: FJ <joostef@gmail.com>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgname=gaupol
pkgver=0.26
pkgrel=1
pkgdesc="An editor for text-based subtitles"
arch=('any')
url="http://home.gna.org/gaupol"
license=('GPL')
depends=('python-gobject' 'gtk3' 'gst-plugins-base' 'gst-plugins-good'
         'iso-codes' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('python-pyenchant: spell-checking'
            'gtkspell3: inline spell-checking'
            'python-chardet: character encoding auto-detection'
            'mplayer: subtitle preview'
            'vlc: subtitle preview'
            'gst-plugins-bad: extra media codecs'
            'gst-plugins-ugly: extra media codecs'
            'gst-libav: extra media codecs')
install=gaupol.install
source=(http://download.gna.org/$pkgname/0.26/$pkgname-$pkgver.tar.xz)
sha256sums=('fd9ca00166c643b6c955c591b5e71f1891d2d4f9224ad477999da6dcd5363d0a')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python3 setup.py --without-iso-codes install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
