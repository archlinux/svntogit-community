# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_pypiname=jieba
pkgname=(python-jieba python2-jieba)
pkgver=0.37
pkgrel=1
pkgdesc="Chinese Words Segementation Utilities"
arch=('any')
url='https://github.com/fxsjy/jieba'
license=('MIT')
makedepends=('python' 'python2')
source=("http://pypi.python.org/packages/source/j/jieba/jieba-$pkgver.zip"
        LICENSE)
sha512sums=('61342a14215f65f6abb6c7cd0872e9ac16a6778415704c422b272c1f1f384ae9913e007cddb15df64f4696219034b4cc328cf28d304c3239cc4181359e5cf84f'
            '4ff70d3b714cd57b9c53e156fc4c0dd14f22001e20fce2edbdba079f352271f85e5e8c1e519ff2f91ff1aa258adb8ecc3ffc3bd8cb34ffdc6a44ced9a583feaf')

prepare() {
  cp -a jieba-$pkgver{,-py2}
}

package_python-jieba() {
  depends=('python')

  cd "$srcdir/jieba-$pkgver"
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-jieba() {
  depends=('python2')

  cd "$srcdir/jieba-$pkgver-py2"
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
