# Maintainer: David Runge <dave@sleepmap.de>
_name=micawber
pkgname=python-micawber
pkgver=0.5.1
pkgrel=1
pkgdesc="A small library for extracting rich content from urls."
arch=('any')
url="https://github.com/coleifer/micawber"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-django')
optdepends=('python-django: django support'
            'python-flask: flask support'
            'python-redis: redis caching support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('309546cc90e7ec4d4a5839f79bff43684ceed4cc7a682ac40d7f09841c89b64631d9667893edd70fd16f0cdb718d05cc5567482bf368da9164615248e5d4e379')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
