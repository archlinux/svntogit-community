# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-httpretty
pkgname=("python-httpretty" "python2-httpretty")
pkgver=0.9.6
pkgrel=3
pkgdesc="HTTP client mock for Python"
arch=('any')
url="https://github.com/gabrielfalcao/httpretty"
license=('MIT')
makedepends=('python-urllib3' 'python2-urllib3' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-coverage' 'python2-coverage' 'python-mock'
              'python2-mock' 'python-sure' 'python2-sure' 'python-tornado' 'python2-tornado'
              'python-requests' 'python2-requests' 'python-httplib2' 'python2-httplib2'
              'python-nose-randomly' 'python2-nose-randomly' 'python-rednose' 'python2-rednose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gabrielfalcao/HTTPretty/archive/$pkgver.tar.gz")
sha512sums=('6d4274961d4ff2475c4035e0174694a851640dcd383727de5508f342827f77143b09a7a2c644978d1ca5932945fec75a85187ca6979dbfd2e109e3540467abf0')

prepare() {
  cp -a HTTPretty-$pkgver{,-py2}
}

check() {
  cd HTTPretty-$pkgver
  nosetests3 -s tests || warning "Tests failed"

  cd ../HTTPretty-$pkgver-py2
  nosetests2 -s tests
}

package_python-httpretty() {
  depends=('python-urllib3')

  cd HTTPretty-$pkgver
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python-httpretty/COPYING"
}

package_python2-httpretty() {
  depends=('python2-urllib3')

  cd HTTPretty-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python2-httpretty/COPYING"
}
