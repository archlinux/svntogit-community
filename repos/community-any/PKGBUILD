# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=1.4.36
pkgrel=1
pkgdesc='Music player for GNOME'
arch=(any)
url=https://gitlab.gnome.org/World/lollypop
license=(GPL)
depends=(
  gst-plugins-base-libs
  gst-python
  gtk3
  libhandy
  libsoup
  python-beautifulsoup4
  python-cairo
  python-gobject
  python-pillow
  totem-plparser
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  intltool
  itstool
  meson
)
optdepends=(
  'easytag: Modify tags'
  'gst-libav: FFmpeg plugin for GStreamer'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
  'kid3-qt: Store covers in tags'
  'libsecret: Last.FM support'
  'youtube-dl: Youtube support'
)
_tag=e3daa763ef0baa1d8679af8de6175b3d77b74e04
source=(git+https://gitlab.gnome.org/World/lollypop.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd lollypop
  git describe --tags
}

build() {
  arch-meson lollypop build \
    --libexecdir=lib/lollypop
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
