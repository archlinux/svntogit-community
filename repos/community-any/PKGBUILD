# Maintainer:  Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgname='python-cherrypy'
_pkgname="${pkgname#python-}"
pkgver=18.2.0
pkgrel=1
pkgdesc='A pythonic, object-oriented web development framework'
arch=('any')
url='https://cherrypy.org'
license=('BSD')
depends=('python' 'python-six' 'python-cheroot' 'python-portend' 'python-jaraco'
  'python-more-itertools' 'python-zc.lockfile')
makedepends=('python-setuptools' 'python-setuptools-scm' 'python-six'
  'python-cheroot' 'python-portend' 'python-more-itertools' 'python-zc.lockfile')
checkdepends=('python-tox')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v$pkgver.tar.gz"
  'disable-sessiontest-file_concurrency.patch'
  'fix-nullbyte-test.patch'
)
sha512sums=('563aff736187e1b9db5775da501f2a205ce54b99ba479cc6d215c1430cea4b4211c76f9c361aeedb9f16b4e5b351d23dea423060515aa773c6437e485cc4807a'
            'e8abb7e3f8a064a0da529fcaf393dff305541bc273b539f3129b119d506be3be8cd67894dd4223e067b4dd8b55df8adc5d4feddb8d87188c9e528670c39e252f'
            '99c77ca8108ba18fadd9106d8b1c9509730f85832d8da12bfaefa374444b400aab1e4c998f43c7e5501a2c5a6f29091c5975883f04f5b81188fc0ca6a8f214e7')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  tox
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
