# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: dcraven <dcraven@gmail.com>

pkgname=bless
pkgver=0.6.0
pkgrel=5
pkgdesc="High-quality, full-featured hex editor"
arch=('any')
url="https://web.archive.org/web/20170503150524/http://home.gna.org/bless/"
license=('GPL')
depends=('gtk-sharp-2')
makedepends=('rarian')
source=(http://http.debian.net/debian/pool/main/b/$pkgname/${pkgname}_$pkgver.orig.tar.gz
        bless_0.6.0-5+fix-savingfiles.patch)
sha256sums=('34dc984c35df19184e5b61a4b11fe79901c417693f57d874dc70ef0c58896de1'
            '3068969d1125cf65cd27c523dcdbb153e14832c1dfb61f313fc9d68607c279c0')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/gmcs/mcs/' configure{,.ac} builder/ModuleBuilder.cs

  # Fix saving preferences
  patch -Np1 -i ../bless_0.6.0-5+fix-savingfiles.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" MKDIR_P='mkdir -p' install
}
