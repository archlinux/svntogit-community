# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=mail-gpg
pkgname=ruby-mail-gpg
pkgver=0.4.0
pkgrel=1
pkgdesc='GPG/MIME extension for the Ruby Mail Library'
url='https://github.com/jkraemer/mail-gpg'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-gpgme' 'ruby-mail' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jkraemer/mail-gpg/archive/v${pkgver}.tar.gz
        ${pkgname}-${pkgver}.tar.gz.asc::https://github.com/jkraemer/mail-gpg/releases/download/v${pkgver}/mail-gpg-${pkgver}.tar.gz.asc)
sha512sums=('38be3e7c1842c5640665b431851b39ed40831db332d4e316213516308eb2e4a1f79756e40e4dd7b83358a10e426b69cfdae898e5df61b79ee05de6a80f86447d'
            'SKIP')
validpgpkeys=('C3880ACE6187419B177C472E08625A14CE3CDDAA') # Jens Kraemer <jk@jkraemer.net>

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files|find -type f|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md History.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
