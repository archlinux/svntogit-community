# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-pallets-sphinx-themes
_pyname=${pkgname/python-/}
pkgver=2.1.0
pkgrel=1
pkgdesc='Sphinx themes for Pallets and related projects'
url='https://github.com/pallets/pallets-sphinx-themes'
arch=('any')
license=('BSD')
depends=('python' 'python-sphinx' 'python-importlib-metadata')
makedepends=('python-setuptools')
source=(https://github.com/pallets/pallets-sphinx-themes/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('6525646a75cd4033cd495fb65036d27c600b8152596f6d0e0df4bab24cf97ea21f61b4c6d56a722e5444d7bc4421584fc9ec4213e3b6733ef09e81a45f085774')
b2sums=('456e3e42b5b7850bf4cb02212ed0ec0629e49a7e491ed77f34038f37220a236b52c1b7c8ae1da0c9a80910262a6759d1e82f7f1c8ff730f8a6d224573ae25d9a')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
