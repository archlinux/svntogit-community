# Maintainer: Sarkasper <echo a2FzcGVyLm1lbnRlbkBnbXguY29tCg== | base64 -d>
# Contributor: Jelle van der Waa <jelle@vdwaa nl>

pkgname=hunspell-nl
pkgver=2.20.19
pkgrel=2
pkgdesc="Dutch hunspell dictionaries"
arch=('any')
url="https://www.opentaal.org/"
license=('BSD' 'custom:Creative Commons, Attribution 3.0 Unported')
depends=('hunspell')
optdepends=('hunspell:  the spell checking libraries and apps')
makedepends=('unzip' 'qt6-webengine')
# Upstream source http://opentaal.org/bestanden/license_result/20-woordenlijst-v-210g-voor-openofficeorg-3?bid=20&agree=1
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/OpenTaal/opentaal-hunspell/archive/${pkgver}.tar.gz)
sha512sums=('19e93421da7096259c18f69dadcf49a2d9f16890edd8474ee226a39ae344803bfe4d3b952669fb1d42dcc28aed36582468f7a92c3289db18e2284be3f4889f40')

package() {
  cd opentaal-hunspell-${pkgver}
  install -dm755 "${pkgdir}"/usr/share/hunspell
  install -m644 nl.dic "${pkgdir}"/usr/share/hunspell/nl_NL.dic
  install -m644 nl.aff "${pkgdir}"/usr/share/hunspell/nl_NL.aff

  pushd "${pkgdir}"/usr/share/hunspell/
  nl_NL_aliases="nl_AW nl_BE"
  for lang in ${nl_NL_aliases}; do
      ln -s nl_NL.aff ${lang}.aff
      ln -s nl_NL.dic ${lang}.dic
  done
  popd

  # the symlinks
  install -dm755 "${pkgdir}"/usr/share/myspell/dicts
  pushd "${pkgdir}"/usr/share/myspell/dicts
    for file in "${pkgdir}"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename ${file}) .
    done
  popd

  # docs
  install -dm755 "${pkgdir}"/usr/share/doc/${pkgname}
  install -m644 README.md "${pkgdir}"/usr/share/doc/${pkgname}

  # licences
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt

  # Install webengine dictionaries   
  install -d "$pkgdir"/usr/share/qt{,6}/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    /usr/lib/qt6/qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  ln -rs "$pkgdir"/usr/share/qt6/qtwebengine_dictionaries/${_filename/\.dic/\.bdic} "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  done
}
