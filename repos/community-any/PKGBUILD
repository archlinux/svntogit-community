# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>

_pkgname=Markups
pkgbase=python-markups
pkgname=('python-markups' 'python2-markups')
pkgver=3.0.0
pkgrel=4
pkgdesc='Wrapper around various text markups'
arch=('any')
url='https://launchpad.net/python-markups'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-markdown-math' 'python2-markdown-math'
             'python-docutils' 'python2-docutils' 'python-pygments' 'python2-pygments')
source=($pkgname-$pkgver.tar.gz::http://github.com/retext-project/pymarkups/archive/$pkgver.tar.gz)
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('1ea19458dfca6a4562044e701aa8698089a0c659fc535689ed260f89a04f8d39'
            'SKIP')
validpgpkeys=('F24299FF1BBC9018B906A4CB6026936D2F1C8AE0') # Dmitry Shachnev <mitya57@debian.org>

build() {
  cd "${srcdir}"/${_pkgname}-${pkgver}

  python setup.py build
  python2 setup.py build
}

check() {
  cd "${srcdir}"/${_pkgname}-${pkgver}

  python setup.py test
  python2 setup.py test
}

package_python-markups() {
  depends=('python-markdown-math')
  optdepends=('python-docutils: for reStructuredText language support'
              'python-pygments: for highlighting output style')

  cd "${srcdir}"/${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-markups() {
  depends=('python2-markdown-math')
  optdepends=('python2-docutils: for reStructuredText language support'
              'python2-pygments: for highlighting output style')

  cd "${srcdir}"/${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
