# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=2.1.0
pkgrel=2
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL')
depends=('python-pyqt5' 'python-pyserial' 'python-requests' 'python-babel')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neutree/COMTool/archive/v$pkgver.tar.gz")
sha512sums=('6d9f65edd5fcccb9e1efa87a46a441b0d6ffb934b176e61a4df1750e3c84f846c2101507ff182606356de48e8967ea507f258174f7ca26836951d4e724290e27')

prepare() {
  cd COMTool-$pkgver
  sed -i "s/'pyqt5>=5',//" setup.py
}

build() {
  cd COMTool-$pkgver
  python setup.py build
}

package() {
  cd COMTool-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"

  rm "$pkgdir"/usr/{LICENSE,README.MD}
}

# vim:set ts=2 sw=2 et:
