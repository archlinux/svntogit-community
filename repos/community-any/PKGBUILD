# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin-data
pkgver=2.0.3.20140820
epoch=1
_lm_sc_ver=20140820
_dict_ver=20131214
pkgrel=1
arch=('any')
pkgdesc="Statistical language model data from open-gram project for sunpinyin"
license=('APACHE')
provides=("sunpinyin-data-le=$pkgver")
url="http://sunpinyin.googlecode.com"
makedepends=('sunpinyin' 'git')
source=('git+https://code.google.com/p/open-gram')
md5sums=('SKIP')
# Google gives a 403 to the 3gm file
#source=("https://open-gram.googlecode.com/git/lm_sc.3gm.arpa-${_lm_sc_ver}.tar.bz2"
#        "https://open-gram.googlecode.com/git/dict.utf8-${_dict_ver}.tar.bz2")
#sha512sums=('3159dd3700382cfa617ba025ce542660580f3057e0dd6bbb92947902495db08e4454c1d09d01a55c90653444513d125dd034e944b060a9d8e009c3633b106d97'
#            'adaf52a27c6afdeb950912845f3fcb7a6799f997d50a2892ceecf76ed13e3644407338d734832c2ce4b09bce11fec4731774f1eb2dd4c30bea5490555915bd5b')

prepare() {
  bsdtar -xf open-gram/lm_sc.3gm.arpa-${_lm_sc_ver}.tar.bz2
  bsdtar -xf open-gram/dict.utf8-${_dict_ver}.tar.bz2
  mkdir raw && cd raw
  ln -s ../lm_sc.3gm.arpa
  ln -s ../dict.utf8

  ln -sf "/usr/share/doc/sunpinyin/SLM-inst.mk" ./Makefile
}

build() {
  cd raw
  make
}

package() {
  cd raw
  make DESTDIR="$pkgdir" install
}

