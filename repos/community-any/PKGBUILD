# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=2.8.4
pkgrel=2
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py' 'git'
             'python-pluggy' 'python2-pluggy')
checkdepends=('lsof' 'python-nose' 'python2-nose'
              'python-mock' 'python2-mock' 'python-yaml' 'python2-yaml' 'python2-enum34'
              'python-pexpect' 'python2-pexpect' 'python-pytest-xdist' 'python2-pytest-xdist'
              'python-zope-interface' 'python2-zope-interface' 'python-twisted' 'python2-twisted')
source=("git+https://github.com/pytest-dev/pytest.git#tag=$pkgver"
        fix-deprecated_call.patch)
sha512sums=('SKIP'
            '37754ab7eabf7e389ef30b4253152d2ba2fca4bbcfe11a14bf1f13da0a17e92f88808e76bbc7b66d2cddbc00fb1ecde814dc19277a36a92b7d540d84ae0391f2')

prepare() {
  # Remove bundled pluggy - disabled for now as it will break tests
  # rm -r pytest/_pytest/vendored_packages
  # sed -i "s/'_pytest.vendored_packages'//" pytest/setup.py

  # https://github.com/pytest-dev/pytest/issues/1238
  (cd pytest; patch -p1 -i ../fix-deprecated_call.patch)

  cp -a pytest{,-py2}
}

build() {
  cd "$srcdir/pytest"
  python setup.py build

  cd "$srcdir/pytest-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pytest"
  python setup.py test

  cd "$srcdir/pytest-py2"
  python2 setup.py test || warning "Tests failed"
  # https://github.com/pytest-dev/pytest/issues/927
}

package_python-pytest() {
  depends=('python-py' 'python-setuptools') # 'python-pluggy'

  cd pytest
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest() {
  depends=('python2-py' 'python2-setuptools') # 'python2-pluggy'

  cd pytest-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/py.test" "${pkgdir}/usr/bin/py.test2"
}

