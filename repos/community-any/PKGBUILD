# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Akaash Suresh <https://github.com/Curry>
# Contributor: Batuhan Baserdem <lastname dot firstname at gmail>

pkgname=qmk
pkgver=0.0.36
pkgrel=1
pkgdesc="CLI tool for customizing supported mechanical keyboards."
arch=('any')
url="https://github.com/qmk/qmk_cli"
license=('MIT')
depends=('python-appdirs' 'python-argcomplete' 'python-colorama'
         'git' 'avr-gcc' 'arm-none-eabi-gcc' 'dfu-programmer' 
         'dfu-util' 'avrdude')
makedepends=('python-setuptools')
optdepends=('arm-none-eabi-binutils'
            'arm-none-eabi-newlib'
            'avr-binutils'
            'avr-libc'
            'gcc'
            'libffi'
            'libusb-compat'
            'clang: chibios-upgrader'
            'zip: some distribution script'
            'wget: some update scripts'
            'diffutils: some update scripts')
source=("https://files.pythonhosted.org/packages/source/q/qmk/qmk-${pkgver}.tar.gz"
        '50-atmel-dfu.rules'
        '54-input-club-keyboard.rules'
        '55-caterina.rules'
        '56-dfu-util.rules'
        '57-bootloadhid.rules')
sha256sums=('8d8f0bb7dd2c740d8af99ab4650e306a87c75e5a5785cf3f03a0272d215d1671'
            'e755c3809ca9bf7952aa009df7cd453ffed9587a7bd73bbaca86d504b9bdfc9b'
            '93a26f044baf2dd9d0ba78a2c18eef3bb3a2512605467228f9af309532f8a61b'
            'fb9d05568466da0250331f3eb1602f17218c5ae7f40cf6abaee455ca74c270a6'
            'fc8b52f38c6fb1f793477755198f0e55d8935e707e602211c8f5f23603f6a8ff'
            '44bc829563b073327396337abd0565396167871ed06e83fe4b18763ab63cfc3f')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/lib/udev/rules.d"
  install -Dm644 ../*.rules "${pkgdir}/usr/lib/udev/rules.d"
}
