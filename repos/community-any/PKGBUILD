# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pyftpdlib
pkgver=1.5.6
pkgrel=3
pkgdesc='Very fast asynchronous FTP server library'
arch=('any')
url='https://github.com/giampaolo/pyftpdlib/'
license=('MIT')
depends=('python')
optdepends=('python-pyopenssl: FTPS support'
            'python-psutil: to keep track of FTP server memory usage')
makedepends=('python-setuptools' 'python-pysendfile')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pyopenssl' 'python-psutil')
source=("https://pypi.io/packages/source/p/pyftpdlib/pyftpdlib-$pkgver.tar.gz")
sha512sums=('a3c3c1cac221a8a3c090ee208c03134bb1b8de58099a23ca6ee422f06fb97a0ed561c96e62c62b21fa112dd0b3458651b9fc0c55d72dc0bed79d0583b319a50d')

build() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py build
}

check() {
  # https://github.com/giampaolo/pyftpdlib/issues/500

  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd pyftpdlib-$pkgver
  python setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
