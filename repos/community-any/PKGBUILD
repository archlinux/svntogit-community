# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Hilton Medeiros <medeiros.hilton at gmail.com>

_name=crimson
pkgbase=$_name-font
pkgname=(otf-$_name ttf-$_name)
pkgver=0.800
_pkgver=f8255058575d26ddf35ab0b2d5ab7832c40f7a1a
pkgrel=1
pkgdesc='A font family for book production in the tradition of beautiful oldstyle typefaces'
arch=(any)
url="https://github.com/skosch/${_name^}/"
license=(OFL)
_archive="${_name^}-$_pkgver"
source=("$url/archive/$_pkgver/$_archive.zip")
sha256sums=('751e1922bc3fc89bd545e7a6c28a6b8c0f28e266dce83f67fa6bd546672b6cad')

package_otf-crimson() {
	cd "$_archive"
	provides=($pkgbase $pkgname-text)
	replaces=($pkgname-text)
	install -Dm0644 -t "$pkgdir/usr/share/fonts/OTF/" "Desktop Fonts/OTF/"*.otf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}

package_ttf-crimson() {
	cd "$_archive"
	provides=($pkgbase $pkgname-text)
	install -Dm0644 -t "$pkgdir/usr/share/fonts/TTF/" "Desktop Fonts/TTF/"*.ttf
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
