# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Matthew Bowra-Dean <matthew@ijw.co.nz>
pkgname=openra
pkgver=20131223
pkgrel=2
pkgdesc="An open-source implementation of the Red Alert engine using .NET/mono and OpenGL"
arch=('any')
url="http://open-ra.org"
license=('GPL3')
install=openra.install
depends=('mono' 'openal' 'libgl' 'freetype2' 'sdl' 'hicolor-icon-theme' 'ttf-dejavu' 'gtk-update-icon-cache' 'desktop-file-utils')
makedepends=('git' 'unzip')
optdepends=('nvidia-cg-toolkit: for using the alternative Cg renderer')
options=(!strip)
source=("openra-$pkgver::https://github.com/OpenRA/OpenRA/tarball/release-$pkgver")
md5sums=('799e94c92fccb26830000b6483dd3166')

build() {
  cd OpenRA-OpenRA-*

  make
}

package() {
  cd OpenRA-OpenRA-*

  mkdir -p "$pkgdir/usr/share/applications"
  sed "s/{VERSION}/$pkgver/g" packaging/linux/openra.desktop > "$pkgdir/usr/share/applications/openra.desktop"

  sed "/Version/s/{DEV_VERSION}/release-${pkgver}/" -i mods/{ra,d2k,cnc}/mod.yaml  

  mkdir -p "$pkgdir/usr/share/icons/"
  cp -r packaging/linux/hicolor "$pkgdir/usr/share/icons/"

  make prefix=/usr DESTDIR="$pkgdir" install

  find "$pkgdir" -iname \*.mdb -delete
}
