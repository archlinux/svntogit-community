# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgname=python-keyrings-alt
_pkgname=keyrings.alt
epoch=1
pkgver=4.0.1
pkgrel=3
pkgdesc="Alternate keyring implementations"
arch=('any')
url='https://github.com/jaraco/keyrings.alt'
license=('MIT')
depends=('python-keyring')
makedepends=('python-setuptools_scm')
checkdepends=('libgnome-keyring' 'python-pycryptodome' 'python-gobject' 'python-keyring'
              'python-pytest-runner' 'python-pytest-flake8' 'python-pytest-black' 'python-pytest-cov')
optdepends=('libgnome-keyring: GNOME Integration'
            'python-gobject: GNOME Integration'
            'python-fs'
            'python-crypto: Encrypted file storage')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ab608decd191695f245b5be180bd3a29cade2580927a86b978dd7d8043b239e88c4ad4f372c0c8c8e8cb6f6a72a8fe705d7fdeda8a7d3d94500563c3cc1328f6')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
