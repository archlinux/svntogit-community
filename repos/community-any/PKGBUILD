# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.16.14
pkgrel=1
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL')
makedepends=('docbook2x' 'docbook-sgml' 'docbook-utils')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaio/ibus-table/archive/$pkgver.tar.gz")
sha512sums=('2e7ad39d40fd7d8081008848e1710b92e175e43e5fb5f21725f478d7bacccb07133231a389c2adf23509e0ce25941ad9641862c01638d266ec8d010de3c5b39c')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
