# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Julien MISCHKOWITZ <wain@archlinux.fr>
# Contributor: Panagiotis Papadopoulos pano_90 (AT) gmx (DOT) net

pkgname=languagetool
pkgver=4.5
pkgrel=1
pkgdesc="An open source language checker"
arch=('any')
url="https://www.languagetool.org" 
license=("LGPL")
depends=('java-runtime-headless>=8')
makedepends=('unzip')
optdepends=('java-runtime: needed for the GUI version'
            'libxtst: needed for the GUI version'
            'gtk2: needed for the GUI version')
source=("https://www.languagetool.org/download/LanguageTool-${pkgver}.zip" "$pkgname.sh")
noextract=($pkgname-$pkgver.zip)
sha256sums=('61e239148309580d02f1729c3b5a7da61eaa2cf36898a5be6daaac95263f1af9'
            '009c0a65a978ad11ac097edd4be64688a2c80281b201495eff9ab667c5b0f0fe')

package() {
  cd "$srcdir"
  install -d "$pkgdir"/usr/{bin,share/java/$pkgname}
  unzip -q LanguageTool-$pkgver.zip -d "$pkgdir"/usr/share
  cd "$pkgdir"/usr/share/
  mv LanguageTool-$pkgver $pkgname
  cd $pkgname
  mv *.jar "$pkgdir"/usr/share/java/$pkgname
  mv libs/*.jar "$pkgdir"/usr/share/java/$pkgname
  rm -rf libs
  install -m755 "$srcdir"/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
}
