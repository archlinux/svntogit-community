# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
pkgver=2.3.0.20160214
_commit=197d817a39101836a38ad22bffad60a1b3937959
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('any')
url="https://gitcafe.com/Deepin/deepin-movie"
license=('GPL3')
depends=('python2-pyqt5' 'qtav' 'mediainfo' 'deepin-menu' 'python2-dbus'
         'python2-peewee' 'python2-pillow' 'python2-magic' 'python2-bottle'
         'deepin-qml-widgets' 'python2-xpybutil' 'python2-deepin-utils'
         'python2-requests' 'dleyna-renderer' 'deepin-dbus-factory')
makedepends=('deepin-gettext-tools' 'git')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-movie.git#commit=$_commit"
        pyqt5.6.patch)
sha256sums=('SKIP'
            '9e0c61503f46ebba7dd24917e84a1248e9f339a82bcca3091a02be7ed5ceb8aa')

prepare() {
  cd deepin-movie

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

  patch -p1 -i ../pyqt5.6.patch
}

build() {
  cd deepin-movie
  python2 configure.py
  deepin-generate-mo locale/locale_config.ini
}

package() {
  cd deepin-movie
  make DESTDIR="${pkgdir}" PREFIX="/usr" install

  chmod 0755 "$pkgdir"/usr/share/deepin-movie/main.py "$pkgdir"/usr/bin/host_service.py
}
