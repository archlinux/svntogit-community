# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Chris Warrick <aur@chriswarrick.com>

_pkg=cloudpickle
pkgname=python-${_pkg}
pkgver=2.2.0
pkgrel=3
pkgdesc="Extended pickling support for Python objects"
arch=(any)
url="https://github.com/cloudpipe/cloudpickle"
license=(BSD)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(
    python-pytest
    python-psutil
    python-tornado
    python-numpy
)
options=(!emptydirs)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('3f4219469c55453cfe4737e564b67c2a149109dabf7f242478948b895f61106f')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  cd tests/cloudpickle_testpkg
  python setup.py build
  cd ../../
  # file_handles: https://github.com/cloudpipe/cloudpickle/issues/252
  # builtin_classmethod: https://github.com/cloudpipe/cloudpickle/pull/486
  # pytest_module: https://github.com/cloudpipe/cloudpickle/issues/487
  PYTHONPATH="${PWD}"/tests/cloudpickle_testpkg/build/lib/ \
  pytest -vl --color=yes -m 'not avoid_ci' \
    --deselect tests/cloudpickle_test.py::CloudPickleTest::test_builtin_classmethod \
    --deselect tests/cloudpickle_test.py::CloudPickleTest::test_dynamic_pytest_module \
    --deselect tests/cloudpickle_test.py::Protocol2CloudPickleTest::test_builtin_classmethod \
    --deselect tests/cloudpickle_test.py::Protocol2CloudPickleTest::test_dynamic_pytest_module \
    --deselect tests/cloudpickle_file_test.py::CloudPickleFileTests::test_pickling_special_file_handles \
    --deselect tests/cloudpickle_test.py::CloudPickleTest::test_pickling_file_handles \
    --deselect tests/cloudpickle_test.py::Protocol2CloudPickleTest::test_pickling_file_handles

}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
