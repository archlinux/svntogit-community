# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi
pkgver=0.13.4
pkgrel=1
pkgdesc="Editor for audio/video projects using the GStreamer framework"
arch=('any')
url="http://www.pitivi.org/"
license=('LGPL')
depends=('gstreamer0.10>=0.10.24' 'pygoocanvas' 'gstreamer0.10-good' 'pygtk>=2.14.0'
	 'gstreamer0.10-python>=0.10.16' 'gnonlin>=0.10.13' 'setuptools'
	 'zope-interface' 'dbus-python' 'gnome-icon-theme')
makedepends=('pkgconfig' 'intltool')
install=$pkgname.install
options=(force)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.13/$pkgname-$pkgver.tar.gz)
md5sums=('222c1509457bb9a7025b5df387f81f9f')

build() {
   cd "$srcdir/$pkgname-$pkgver"

   # python2 fix
   for file in $(find . -name '*.py' -print) bin/pitivi.in; do
      sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
      sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
   done

   ./configure --prefix=/usr
   make
   make DESTDIR="$pkgdir/" install
}
