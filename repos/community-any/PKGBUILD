# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.9.3
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-libtmux')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6e256d177add0fa820a3971646b2dfb9aa2390888511c046e2191250c68c310b214a69780211e953458a311b72dad8370f34256bc76169a4fa60cdc591067e9d')
b2sums=('9a465d188094eddcc8822145a3ccc65a7c6d3506ff8e90d01e7eb5d29bbbdab27d15529553be9eb902d3500cb6dd0e2db00f7e8139ea9102fe140d3600313205')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CHANGES,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.{json,yaml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}

# vim:set ts=2 sw=2 et:}
