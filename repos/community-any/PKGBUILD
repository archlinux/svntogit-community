# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-apache
pkgver=2.2.0
pkgrel=1
pkgdesc='Apache plugin for Certbot'
arch=('any')
license=('Apache')
url='https://pypi.python.org/pypi/certbot-apache'
replaces=('letsencrypt-apache')
depends=(
  'certbot'
  'python-acme'
  'python-augeas'
  'python-setuptools'
)
makedepends=(
  'python-build'
  'python-wheel'
  'python-installer'
)
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2'
              '86379B4F0AF371B50CD9E5FF3402831161D1D280'
              '20F201346BF8F3F455A73F9A780CC99432A28621'
              'F2871B4152AE13C49519111F447BF683AA3B26C3')
sha512sums=('3fe3d794519f5f81e08ae7208da6be57b78fe6d00d27422759c2b01173527e220166a22410f0d29dcde8571e63096a9f9d16e8afb6d60f30a50ad12d8a92a7b3'
            'SKIP')
b2sums=('f23d89f1bd40bae1de3e1d2b43cb983ca4d84363b7f7f4680bf26c75714dbde823ef51255368a60259570e8424b0c7711c1f261fdcf7c8835cdc1e8a9e9dc76c'
        'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  # nuke setuptools from orbit ^W install_requires
  sed \
    -e '/setuptools>=/d' \
    -i setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"

  python -m pytest
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
