# $Id$
# Contributor: Pierre Buard <pierre.buard@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=('python-pyenchant' 'python2-pyenchant')
pkgver=1.6.8
pkgrel=2
arch=('any')
url="https://pypi.python.org/pypi/pyenchant"
license=('LGPL')
makedepends=('python2-setuptools' 'python-setuptools' 'enchant')
source=('https://pypi.python.org/packages/73/73/49f95fe636ab3deed0ef1e3b9087902413bcdf74ec00298c3059e660cfbb/pyenchant-1.6.8.tar.gz')

build() {
  cp -r pyenchant-${pkgver} pyenchant2-${pkgver}

  cd ${srcdir}/pyenchant-${pkgver}

  python3 setup.py build

  cd ${srcdir}/pyenchant2-${pkgver}

  python2 setup.py build
}

package_python-pyenchant() {
  pkgdesc="PyEnchant is a spellchecking library for Python3 based on the Enchant library"
  depends=('python' 'enchant')

  cd pyenchant-${pkgver}
  
  python3 setup.py install --root=${pkgdir} --optimize=1
}

package_python2-pyenchant() {
  pkgdesc="PyEnchant is a spellchecking library for Python2 based on the Enchant library"
  depends=('python2' 'enchant')

  cd pyenchant2-${pkgver}
  
  python2 setup.py install --root=${pkgdir} --optimize=1
}
md5sums=('b59c06be2d6c7f79f521b569ee0e441b')
