# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=typescript-svelte-plugin
pkgver=0.3.23
pkgrel=1
pkgdesc='TypeScript plugin for Svelte intellisense'
url=https://github.com/sveltejs/language-tools/tree/master/packages/typescript-plugin
license=('MIT')
arch=('any')
depends=('nodejs')
makedepends=('yarn')
source=("https://github.com/sveltejs/language-tools/archive/typescript-plugin-$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('567863338eb473950480f38cdec7ec5bc4901be35bf9c769ceae0a841ce5ef76b6099f55e23d933d169fc78f57a5ac41f8300575c18d7c1314bd8f3888586591')

build() {
  cd language-tools-typescript-plugin-$pkgver
  yarn --frozen-lockfile
  yarn workspace svelte2tsx build
  cd packages/typescript-plugin
  yarn build
}

package() {
  install -d "$pkgdir"/usr/lib/node_modules/$pkgname

  cd language-tools-typescript-plugin-$pkgver
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  # Emulate `npm prune --production`
  mv package.json{,.bak}
  cd packages/typescript-plugin
  yarn --frozen-lockfile --production
  mv ../../package.json{.bak,}

  cp -r dist node_modules package.json \
    "$pkgdir"/usr/lib/node_modules/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
}
