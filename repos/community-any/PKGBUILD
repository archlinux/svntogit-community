# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sébastien Luttringer

pkgname=ldoc
pkgver=1.4.3
pkgrel=2
pkgdesc='LuaDoc-compatible documentation generation system'
arch=('any')
url='http://stevedonovan.github.com/ldoc/'
license=('custom')
depends=('lua' 'lua-penlight')
source=($pkgname-$pkgver.tar.gz::https://github.com/stevedonovan/ldoc/archive/$pkgver.tar.gz)
md5sums=('eda6879140dce9435f5a264ef970a205')

package() {
  cd LDoc-$pkgver

  # install library
  install -dm755 "$pkgdir/usr/share/lua/5.3"
  cp -a ldoc "$pkgdir/usr/share/lua/5.3"

  # install binaries
  install -Dm755 ldoc.lua "$pkgdir/usr/bin/ldoc.lua"
  ln -s ldoc.lua "$pkgdir/usr/bin/ldoc"

  # copy docs
  install -Dm644 readme.md "$pkgdir/usr/share/doc/$pkgname/readme.md"
  install -Dm644 doc/doc.md "$pkgdir/usr/share/doc/$pkgname/doc.md"
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
