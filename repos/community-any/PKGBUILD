# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-widgetsnbextension
pkgver=4.0.4
pkgrel=1
epoch=1
pkgdesc="Interactive HTML widgets for Jupyter notebooks"
arch=('any')
url="https://pypi.python.org/pypi/widgetsnbextension"
license=('BSD')
depends=('jupyter-notebook' 'python-ipywidgets')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://pypi.io/packages/source/w/widgetsnbextension/widgetsnbextension-$pkgver.tar.gz")
sha256sums=('44c69f18237af0f610557d6c1c7ef76853f5856a0e604c0a517f2320566bb775')

build() {
  cd widgetsnbextension-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd widgetsnbextension-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  mv "$pkgdir/usr/etc" "$pkgdir/etc"
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
