# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-execnet
pkgname=('python-execnet' 'python2-execnet')
pkgver=1.4.1
pkgrel=4
pkgdesc="Rapid multi-Python deployment"
arch=('any')
license=('MIT')
url="http://codespeak.net/execnet"
makedepends=('python-setuptools' 'python2-setuptools' 'python-apipkg' 'python2-apipkg')
checkdepends=('python-pytest-timeout' 'python2-pytest-timeout' 'procps-ng' 'openssh' 'pypy')
source=("http://pypi.python.org/packages/source/e/execnet/execnet-$pkgver.tar.gz")
sha512sums=('e8c0c32e873f8585ff4d4da3b9cbd40046479370f5653f67a98c9029d4535a619731d1b825332fe4af4dc5ebc9ab11ee879ef45df07f08943b1e1685f2255790')

prepare() {
  cp -a execnet-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/execnet-${pkgver}"
  python setup.py build

  cd "$srcdir/execnet-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/execnet-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test testing

  cd "$srcdir/execnet-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 testing
}

package_python-execnet() {
  depends=('python-apipkg')

  cd execnet-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-execnet() {
  depends=('python2-apipkg')

  cd execnet-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

