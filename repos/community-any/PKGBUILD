# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org

_pkgname=sqlalchemy-continuum
pkgname=python-sqlalchemy-continuum
pkgver=1.3.13
pkgrel=2
pkgdesc='Versioning and auditing extension for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-continuum'
arch=('any')
license=('BSD')
depends=('python' 'python-sqlalchemy' 'python-sqlalchemy-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('sqlite' 'python-pytest' 'python-flexmock' 'python-psycopg2' 'python-mysql-connector'
              'python-six' 'python-anyjson' 'python-flask' 'python-flask-login' 'python-flask-sqlalchemy'
              'python-sqlalchemy-i18n')
optdepends=('python-flask: flask plugin'
            'python-flask-login: flask plugin'
            'python-flask-sqlalchemy: flask plugin'
            'python-sqlalchemy-i18n: internationalization')
options=('!makeflags')
source=(https://github.com/kvesteri/sqlalchemy-continuum/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        sqlalchemy-continuum-1.3.13-revert-use-get_id-instead-of-id-attr.patch::https://patch-diff.githubusercontent.com/raw/kvesteri/sqlalchemy-continuum/pull/325.patch)
sha512sums=('423e105ae7259c5a3c7e1c54a6110028abfc8ce838308ce01466edcea162d91afc707a1ecd08acc3c8ffb39b5881a41619baad6b5e6d262896cbf6256335d69d'
            '26d42403da0445dccd535e8939c2eb39708a62df9155a4814ff9635f9faf206ceb74724a1c1a82e39e6f7be7149e5fd7b2685d42404801e5802ad77c9cfe0c56')
b2sums=('36a6e6d65fe35d8a7682321715d533ae5e01e97f1a04e9adfb1f5dae686228976cbbdeeae11857d3d38945ad7aee89d87848102c7f3f8770134791af90fd0b22'
        '36f2c5e68740702690b18d23d54974dff22a704aa528012bea17a01d8b8766ad5a1fcb7c4cb2cf0a796a216161ec6cb8c8d0a4d50fe11cdae04ec34be19d9c43')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 < ../sqlalchemy-continuum-1.3.13-revert-use-get_id-instead-of-id-attr.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  make -C docs text man
}

check() {
  cd ${_pkgname}-${pkgver}/tests
  DB=sqlite pytest -k 'not test_flask' || true
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/sqlalchemy-continuum.1 -t "${pkgdir}/usr/share/man/man1"
  ln -sf /usr/share/man/man1/sqlalchemy-continuum.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
}

# vim: ts=2 sw=2 et:
