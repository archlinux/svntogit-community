# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org.com>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=0.3.12
pkgrel=1
pkgdesc='A dynamic firewall daemon with a D-BUS interface'
arch=('any')
url='http://fedorahosted.org/firewalld'
license=('GPL2')
depends=('ebtables' 'gtk3' 'iptables' 'libnotify' 'networkmanager'
         'python-slip')
makedepends=('intltool' 'docbook-xsl')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
install='firewalld.install'
source=("http://fedorahosted.org/released/firewalld/firewalld-${pkgver}.tar.bz2"
        "firewalld-paths.patch"
        "firewalld-syntax.patch")
sha256sums=('7909e29ef276f4c87e52a0c26d3184ad7dd1cf6bff04a4ab875662bc519bf48c'
            '41591c5358c5ad8d43b29d72f42c331fcfc25e3d38c0ba5b3c818fc757ceef2f'
            '42f0e4dbf574ac7b87df77117816dbc4a2ac17f709a22990eeeb973950cc6322')

prepare() {
  cd firewalld-${pkgver}

  patch -Np1 -i ../firewalld-paths.patch
  patch -Np1 -i ../firewalld-syntax.patch
}

build() {
  cd firewalld-${pkgver}

  ./configure --prefix='/usr'\
              --localstatedir='/var' \
              --sbindir='/usr/bin' \
              --sysconfdir='/etc' \
              --disable-schemas-compile
  make
}

package() {
  cd firewalld-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
