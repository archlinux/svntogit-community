# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-watchdog
pkgver=2.1.7
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events"
arch=('any')
url="https://github.com/gorakhargosh/watchdog"
license=('Apache')
depends=('python')
optdepends=('python-yaml: for watchmedo')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-timeout')
source=($pkgname-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('f69265aac6e8a840d230c15cfe88c803f043cb540785446113617c0fd1a3c5dce8a885a376bbadc75372a718800948dd4b00c618e18e033d510b8e2bc2bbb36b')

prepare() {
  cd watchdog-$pkgver
  sed -i '/--cov/d' setup.cfg
}

build() {
  cd watchdog-$pkgver
  python setup.py build
}

check() {
  cd watchdog-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:ts=2:sw=2:et:
