# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Eric Berquist <eric dot berquist at gmail dot com>
# Contributor: Joel Goguen <contact+aur@jgoguen.ca>

pkgname=ruby-mixlib-config
_pkgname="${pkgname#ruby-}"
pkgver=3.0.29
pkgrel=1
pkgdesc='A class based configuration library'
arch=('any')
url='https://github.com/chef/mixlib-config'
license=('Apache')
depends=('ruby-tomlrb')
makedepends=('git' 'ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-rspec')
options=('!emptydirs')
_commit='39e44bcf2634bd6d7e42b436d3ffdb556f700ffc'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  rake build
}

package() {
  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$pkgname/pkg/$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"

  # license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  ln -sf "$_gemdir/gems/$_pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
