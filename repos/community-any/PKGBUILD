# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-ipykernel
pkgver=6.18.1
pkgrel=1
pkgdesc="The ipython kernel for Jupyter"
arch=('any')
url="https://pypi.org/project/ipykernel/"
license=('BSD')
depends=('python-comm' 'python-tornado' 'ipython' 'python-jupyter_client' 'python-debugpy' 'python-psutil')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest-timeout' 'python-pytest-asyncio' 'python-flaky' 'python-ipyparallel' 'python-matplotlib')
source=("ipykernel-$pkgver.tgz::https://github.com/ipython/ipykernel/archive/v$pkgver.tar.gz")
sha256sums=('bae333a31ae035b94fde6c6b5e797450b3152da3dd2bc67e3e63182bbd9b704c')

# dep cycle with python-jupyter_client

build() {
  cd ipykernel-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ipykernel-$pkgver
  pytest -v
}

package() {
  cd ipykernel-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
