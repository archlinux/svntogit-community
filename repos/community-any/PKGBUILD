# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=rime-prelude
pkgname=(rime-prelude librime-data)
pkgver=0.0.0.20210208
_commit=bc9ec4cad9fcaf3e300be9bbf2313104e75b5f25
pkgrel=1
pkgdesc="Essential files for building up your Rime configuration"
arch=('any')
url="https://github.com/rime/rime-prelude"
license=('LGPL')
source=("https://github.com/rime/rime-prelude/archive/$_commit/rime-prelude-$_commit.tar.gz")
sha512sums=('171f12a63da7025e6f165fe818657914940d3282b4372d3ca982b3ecc850c2ad19c54225fbaea0d2f148d7603e29ed80eba1fd2fb0d1cb1ef405d198a42eb424')

package_rime-prelude() {
  pkgdesc="Essential files for building up your Rime configuration"

  cd $pkgname-$_commit
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
}

package_librime-data() {
  pkgdesc="Meta package of preset librime data"
  depends=('rime-prelude' 'rime-essay' 'rime-luna-pinyin' 'rime-bopomofo' 'rime-cangjie' 'rime-stroke' 'rime-terra-pinyin')
  replaces=('brise')
  conflicts=('brise')
}
