# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgname=python-redis
pkgver=3.5.1
pkgrel=2
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="https://github.com/andymccurdy/redis-py"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'pifpaf' 'redis')
source=("https://pypi.io/packages/source/r/redis/redis-$pkgver.tar.gz")
sha512sums=('713089fe9687558b651ac9aa0161f3ebfccb71ee12cd6aed784b07bf8f7b52cab1d1d436c4ea7c91b9d688e48da96251feed8e3add140ae714a6ff9e1d23bc85')

build() {
  cd "$srcdir"/redis-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/redis-$pkgver
  pifpaf run redis py.test || warning "https://github.com/andymccurdy/redis-py/issues/1073"
}

package() {
  cd redis-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
