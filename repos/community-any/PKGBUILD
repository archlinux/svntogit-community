# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-utils
pkgver=4.12.1
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz' 'python-netaddr'
         'python-netifaces' 'python-debtcollector' 'python-pyparsing' 'python-packaging')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-ddt' 'python-eventlet')
source=("https://github.com/openstack/oslo.utils/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9d9a6699f4a418570bf4db18c4c51cda1a14203b0670b6d7134f23dc3dd70d9688572e46ef02dc760dfee3a39f5979ea3f38a2b709b3d52c9814fffc4de36924')

export PBR_VERSION=$pkgver

build() {
  cd oslo.utils-$pkgver
  python setup.py build
}

check() {
  cd oslo.utils-$pkgver
  stestr run
}

package() {
  cd oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
