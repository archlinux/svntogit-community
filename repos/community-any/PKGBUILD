# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-trustme
_pyname=${pkgname/python-/}
pkgver=0.5.3
pkgrel=1
pkgdesc='Library for fake certificate authority (CA) to generate fake TLS certs'
url='https://trustme.readthedocs.io'
arch=('any')
license=('MIT')
depends=('python' 'python-cryptography')
makedepends=('python' 'python-setuptools' 'python-cryptography')
checkdepends=('python-pytest' 'python-pyopenssl' 'python-service_identity')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/python-trio/trustme/archive/v${pkgver}.tar.gz)
sha512sums=('dd890964ef2d2387ec0a67afa1ead8b30872d6e95c396225310df375fab342ed32874dbaf62649ec5479ea736f8ab0ad1c88d06e675d6f65411cdd37d84045a3')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=. py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
