# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testtools
pkgname=('python-testtools' 'python2-testtools')
pkgver=2.3.0
pkgrel=6
pkgdesc="Extensions to the Python standard library unit testing framework"
arch=('any')
license=('MIT')
url="https://github.com/testing-cabal/testtools"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyrsistent' 'python2-pyrsistent'
             'git' 'python-pbr' 'python2-pbr' 'python-extras' 'python2-extras'
             'python-fixtures' 'python2-fixtures' 'python-mimeparse' 'python2-mimeparse'
             'python2-unittest2' 'python2-traceback2')
checkdepends=('python-testscenarios')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/testing-cabal/testtools/archive/$pkgver.tar.gz"
        'python-3.7.patch')
sha512sums=('7c3b9e180c5f22d613763446525fcb3f7f260d79bc875669134c3077af0e0d3e101863429e0469ffcb26fd0755b2c915454abc949be864639a2faa9cf5024c62'
            '12430f9b813fea33b4e7530d446f5eebfe65785f4132b8732f771cbe72ed23bfdf3841db0a85ffab58f518bccbb5cf2711b62021b9ee294a9efc28b5df59d411')

prepare() {
  cp -a testtools-$pkgver{,-py2}

  patch -Np1 -d testtools-$pkgver <python-3.7.patch

  # These are for python 2.x and old python 3.x only
  sed -i 's|unittest2|unittest|' testtools-$pkgver/testtools/tests/test_run.py \
                                 testtools-$pkgver/testtools/tests/test_testsuite.py \
                                 testtools-$pkgver/testtools/tests/twistedsupport/test_matchers.py \
                                 testtools-$pkgver/testtools/tests/twistedsupport/test_deferred.py \
                                 testtools-$pkgver/testtools/tests/twistedsupport/test_runtest.py
  sed -i '/unittest2>=1.0.0/d' testtools-$pkgver/requirements.txt
  sed -i 's|import linecache2 as linecache|import linecache|' testtools-$pkgver/testtools/tests/test_compat.py
  sed -i "s|traceback = try_import('traceback2')|import traceback|" testtools-$pkgver/testtools/content.py
  sed -i '/traceback2/d' testtools-$pkgver/requirements.txt

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/testtools-$pkgver
  python setup.py build

  cd "$srcdir"/testtools-$pkgver-py2
  python2 setup.py build
}

check() {
  cd testtools-$pkgver
  python -m testtools.run testtools.tests.test_suite
}

package_python-testtools() {
  depends=('python-pbr' 'python-extras' 'python-fixtures' 'python-pyrsistent' 'python-mimeparse')

  cd testtools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-testtools() {
  depends=('python2-pbr' 'python2-extras' 'python2-fixtures' 'python2-pyrsistent'
           'python2-mimeparse' 'python2-unittest2' 'python2-traceback2')

  cd testtools-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
