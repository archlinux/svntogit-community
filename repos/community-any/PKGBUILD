# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

# TODO: python-fastcluster is optional dependency (move from AUR)

pkgname=python-seaborn
pkgver=0.12.1
pkgrel=1
pkgdesc="Statistical data visualization"
arch=('any')
url="https://seaborn.pydata.org/"
license=('BSD')
depends=('python-pandas' 'python-matplotlib' 'python-scipy')
makedepends=('python-build' 'python-installer' 'python-flit-core')
optdepends=('python-statsmodels: for some advanced statistical plots')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mwaskom/seaborn/archive/v${pkgver}.tar.gz")
sha256sums=('80ffde0de7539fee159266add2ca9ab922fc9bacc3a4ed3d017077387c102d02')

build() {
  cd seaborn-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd seaborn-${pkgver}

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
