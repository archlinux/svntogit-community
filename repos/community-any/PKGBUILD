# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-transaction
pkgname=('python-transaction' 'python2-transaction')
pkgver=2.1.2
pkgrel=1
pkgdesc='Transaction management for Python'
arch=('any')
license=('ZPL')
url='https://github.com/zopefoundation/transaction'
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/transaction/archive/$pkgver.tar.gz")
sha512sums=('60900d9a0072b800ae832e80de2730883822d673473962553631025efde2e655436e4cc34ba45db1fdb4a6917cab98e0c13cb1a99505a79be9b8ce9d769e0c74')

prepare() {
  cp -a transaction-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/transaction-$pkgver
  python setup.py build

  cd "$srcdir"/transaction-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/transaction-$pkgver
  python setup.py nosetests

  cd "$srcdir"/transaction-$pkgver-py2
  python2 setup.py nosetests
}

package_python-transaction() {
  depends=('python-zope-interface')

  cd transaction-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-transaction() {
  depends=('python2-zope-interface')

  cd transaction-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
