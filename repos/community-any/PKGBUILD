# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Federico Cuello <fedux@fedux.com.ar>

pkgname=redmine
pkgver=4.1.1
pkgrel=1
pkgdesc="A flexible project management web application written using Ruby on Rails framework."
arch=('any')
url="https://www.redmine.org"
license=('GPL2')
depends=('ruby2.6' 'libmagick6' 'libxml2' 'libxslt')
makedepends=('mariadb-libs' 'postgresql-libs')
optdepends=('apache: A supported application server'
            'nginx: A supported application server'
            'mysql: MySQL database support'
            'postgresql: PostgreSQL database support'
            'sqlite: SQLite database support')
source=("$url/releases/$pkgname-$pkgver.tar.gz"
	"Gemfile.local"
	"redmine.service")
sha512sums=('fe691318fe8464de197a49ac172ecfcd65047ca62273226f63549620475557cd27cf8e193355a665abef64fc0c9e59556b4732cdcaa8061746918b06a7afcf81'
            'f9b19ae2c3ef548ccde3e6fcc0c37906e38496a145adb2b4161843db7ed64a2fd7c1b35a18d8bb23dc36e896a1505388b65df7bb768517a5cafbd43e0abc3a3f'
            '0b897980a8920538674fa7585e1dbaef48a152226215d598c41b3fcf4ac6566b5866d6e3132c30c0adbdc0d8e0080e2541cb4a119aedc6bef24fd6c129abd4eb')

build() {
    cd "$srcdir"/redmine-$pkgver

    export PATH=/opt/ruby2.6/bin:$PATH
    export PKG_CONFIG_PATH="/usr/lib/imagemagick6/pkgconfig"

    cp "${srcdir}"/Gemfile.local Gemfile.local
    bundle-2.6 lock
    bundle-2.6 install --no-cache --deployment --without development test
}

package() {
    install -dm 755 "$pkgdir"/usr/share/webapps/
    cp -a --no-preserve=ownership "$srcdir"/redmine-$pkgver "$pkgdir"/usr/share/webapps/redmine

    install -Dm0644 "${srcdir}"/redmine.service "${pkgdir}"/usr/lib/systemd/system/redmine.service

    cd "$pkgdir"/usr/share/webapps/redmine
    rm -rf files log
    install -dm0755 -o http -g http "$pkgdir"/var/lib/redmine/files
    install -dm0755 -o http -g http "$pkgdir"/var/log/redmine
    mv tmp "$pkgdir"/var/lib/redmine/tmp
    chown -R http:http "$pkgdir"/var/lib/redmine/tmp
    ln -s /var/log/redmine log
    ln -s /var/lib/redmine/files files
    ln -s /var/lib/redmine/tmp tmp
}
