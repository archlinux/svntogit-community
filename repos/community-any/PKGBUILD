# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-utils
pkgver=4.4.0
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz'
         'python-netaddr' 'python-netifaces' 'python-debtcollector')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-ddt' 'python-eventlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.utils/archive/$pkgver.tar.gz"
        oslo-utils-py38.patch)
sha512sums=('d3b680333dcb3d3b3d3cc9bbeca49315ab2dbee01c4f4696a0a85684949458a05fe23b319e444e07d7014d60e06d19afaf42aa4319120a5d49db6fd99a2d51e4'
            'edc9861dfc02b82d0312774f2484bb5853a9e5b2e7de9ee19efbd7e1220be56e52d99abb15f71dba4d764832835371200e8d7cac2d40c0546d5cfc8daf68554a')

export PBR_VERSION=$pkgver

prepare() {
  patch -d oslo.utils-$pkgver -p1 -i ../oslo-utils-py38.patch
}

build() {
  cd oslo.utils-$pkgver
  python setup.py build
}

check() {
  cd oslo.utils-$pkgver
  stestr run
}

package() {
  cd oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
