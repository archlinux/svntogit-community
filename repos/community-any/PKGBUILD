# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-doc
pkgver=9.1
pkgrel=1
pkgdesc="HTML documentation for SageMath"
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
depends=(gap-doc python-pplpy)
makedepends=(sagemath python-pyzmq python-docutils python-jupyter_client python-pkgconfig cython python-ipywidgets thebe)
source=(sagemath-$pkgver.tar.gz::"https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
        docbuild_main.patch
        sagemath-ecl-sigfpe.patch
        sagemath-matplotlib-3.2.patch
        sagemath-doc-sphinx-3.patch)
sha256sums=('07f3da5ca3e48f92c5a55b690408a3c87642689c28675a1f25b55a0f48ce5023'
            'ea5e17bb7a7cb36a22e5e3872fcc2585852bc971c4b139b0b2cd69a36c1b009b'
            'e44bbde87f3312548faad75b7383ef21fade55be251ab5804de41cd3842ca8a0'
            '979777363001cca87f73128dd4db5f372fc9e4f1079805a20af01df47797468f'
            '5576d2702a12b6c65e40bf1bdefe9a7f595e6c57fc6ff003903ce0f21643f64b')
options=(!strip)

prepare() {
  cd sage-$pkgver

# fix relative imports
  patch -p0 -i ../docbuild_main.patch
# use sage_setup imports from sagemath source, but use system sage ones (which include compiled modules)
  mkdir -p local-python
  ln -sr src/sage_setup local-python
# Fix SIGFPE crashes with ecl 16.1.3 https://trac.sagemath.org/ticket/22191
  patch -p1 -i ../sagemath-ecl-sigfpe.patch
# Fix warnings with matplotlib 3.2
  patch -p1 -i ../sagemath-matplotlib-3.2.patch
# Fix build with sphinx 3.0
  patch -p1 -i ../sagemath-doc-sphinx-3.patch

  SAGE_ROOT="$PWD" PATH="$PWD/build/bin:$PATH" src/doc/bootstrap
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr" \
         SAGE_ROOT="/usr" \
         SAGE_SRC="$PWD" \
         SAGE_DOC_SRC="$PWD"/doc \
         SAGE_DOC="$PWD"/doc \
         MATHJAX_DIR="/usr/share/mathjax2" \
         PYTHONPATH="$srcdir"/sage-$pkgver/local-python \
         SAGE_NUM_THREADS=10
  python sage_setup/docbuild --no-pdf-links --mathjax all html
}

package() {
  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
    rm -r $_dir
    ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}
