# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=2.2.12
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
depends=('python2-ass' 'python2-pyqt5' 'qtav' 'mediainfo' 'deepin-menu' 'python2-dbus'
         'python2-peewee' 'python2-magic' 'deepin-qml-widgets' 'python2-xpybutil' 'python2-pysrt'
         'python2-opengl' 'deepin-manual')
makedepends=('deepin-gettext-tools' 'git')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-movie.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd deepin-movie

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-movie
  python2 configure.py
  deepin-generate-mo locale/locale_config.ini
}

package() {
  cd deepin-movie
  make DESTDIR="${pkgdir}" PREFIX="/usr" install

  chmod 0755 "$pkgdir"/usr/share/deepin-movie/main.py
}
