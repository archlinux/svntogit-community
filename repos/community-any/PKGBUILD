# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Fabius
# Contributor: Adria Arrufat <swiftscythe @t gmail d@t com>
# Contributor: Gordin <9ordin @t gmail d@t com>

pkgname=screenkey
pkgver=1.4
pkgrel=1
pkgdesc="A screencast tool to display your keys inspired by Screenflick"
arch=('any')
url="https://www.thregr.org/~wavexx/software/screenkey/"
license=('GPL3')
depends=('python-gobject' 'gtk3' 'python-cairo' 'libx11')
makedepends=('python-setuptools' 'python-distutils-extra')
optdepends=('slop' 'ttf-font-awesome' 'libappindicator-gtk3')
source=("https://www.thregr.org/~wavexx/software/screenkey/releases/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('41958F4116FDD1B43DEE754A2BB7D6F2153410EC')
sha256sums=('d7347e31fab4a5b18da431e3fc26f39700e99a505eb0c0168295adc12c8dd4f7'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build --optimize=1 --root="$pkgdir/"
}
