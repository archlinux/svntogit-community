# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-schema' 'python2-zope-schema')
pkgver=4.4.2
pkgrel=2
pkgdesc="zope.interface extension for defining data schemas"
arch=('any')
url="https://github.com/zopefoundation/zope.schema"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-event' 'python2-zope-event'
             'git')
checkdepends=('python-nose' 'python2-nose' 'python-zope-testing' 'python2-zope-testing')
source=("git+https://github.com/zopefoundation/zope.schema.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.schema{,-py2}
}

build() {
  cd "$srcdir/zope.schema"
  python setup.py build

  cd "$srcdir/zope.schema-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.schema"
  python setup.py test

  cd "$srcdir/zope.schema-py2"
  python2 setup.py test
}

package_python-zope-schema() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')

  cd "$srcdir/zope.schema"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-schema() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-event')

  cd "$srcdir/zope.schema"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
