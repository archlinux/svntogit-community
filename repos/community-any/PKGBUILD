# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible-core
pkgver=2.14.4
pkgrel=3
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://pypi.org/project/ansible-core'
license=('GPL3')
depends=(
  'python'
  'python-cryptography'
  'python-jinja'
  'python-packaging'
  'python-pyyaml'
  'python-resolvelib'
)
# not directly required, but either convenient or indirectly required
depends+=('python-paramiko' 'python-typing-extensions')
makedepends=(
  'python-build'
  'python-docutils'
  'python-installer'
  'python-setuptools'
  'python-straight.plugin'
  'python-wheel'
)
checkdepends=(
  'git'
  'python-bcrypt'
  'python-botocore'
  'python-passlib'
  'python-pexpect'
  'python-py'  # missing as dependency of python-pytest-forked
  'python-pytest'
  'python-pytest-mock'
  'python-pytest-xdist'
  'python-pytest-forked'
  'python-pywinrm'
  'python-voluptuous'
)
optdepends=(
  'python-dnspython: for dig lookup'
  'python-jmespath: json_query support'
  'python-netaddr: for the ipaddr filter'
  'python-passlib: crypt values for vars_prompt'
  'python-pip: for module to manage Python libarary dependencies'
  'python-pywinrm: connect to Windows machines'
  'python-setuptools: for module to manage Python libarary dependencies'
  'python-systemd: for journald support'
  'sshpass: for ssh connections with password'
)
provides=('python-ansible' 'ansible-base')
replaces=('ansible-base')
backup=('etc/ansible/ansible.cfg')
source=(
  "https://pypi.python.org/packages/source/a/ansible-core/ansible-core-${pkgver}.tar.gz"
  "$pkgname-2.14.4-resolvelib_1.1.patch"
)
sha512sums=('86164dfded15232174e4f11140a71b91ef5b0d93b40a2df9588930b4b955f20feb419258c2bbf9d6735d298ec626c88c5e9b1c933a51e3273a28b85fac7a3762'
            'f69bfd47c7079873c78ca795fb83826ffebfc6b5955759484c08edfd651baf0c9ef136b6df1a9bedceebd80fb26ee0f8b95b07f1fb38ee50506c65b17e2cef5a')
b2sums=('ae096debaf676dbd46cb047f0f87af5d09b4265368500964bbd0cf3ac8e58969bee489d66486fb570a697df5b22065fae206f1df361eb911a7fe025628655371'
        '87ab3cac6acdefbf265526803ace205ade1caed8a97e244a8e96d5329b1779661defb5fa0d4ebf2ebee7e2bc547a7996aa7adc82126d07d3b226ad2fd133370f')

prepare() {
  # backport (minus test requirements!) of patch to add resolvelib < 1.1.0 support
  # https://github.com/ansible/ansible/commit/b148fd8dd74c8599f809f71117a86577ccfb0638
  # https://github.com/ansible/ansible/commit/016b7f71b10539c90ddbb3246f19f9cbf0e65428
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.14.4-resolvelib_1.1.patch
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation

  make docs
}

# tests don't find the ansible-core internals: https://github.com/ansible/ansible/issues/80472
# check() {
#   local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

#   cd $pkgname-$pkgver
#   # install to test location
#   # python -m installer --destdir=test_dir dist/*.whl
#   # export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
#   # export PATH="$PWD/test_dir/usr/bin:$PATH"
#   # pytest -vv test/units/

#   # upstream test wrapper
#   # bin/ansible-test units

#   # run only locally
#   # export PYTHONPATH="lib:$PYTHONPATH"
#   # export PATH="bin:$PATH"
#   # pytest -vv --ignore test/integration/ --ignore test/ansible_test/
# }

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 COPYING -t "${pkgdir}"/usr/share/doc/${pkgname}/
  install -Dm644 examples/ansible.cfg -t "${pkgdir}"/etc/ansible/
  install -Dm644 examples/{ansible.cfg,hosts} -t "${pkgdir}"/usr/share/ansible/doc/examples/
  install -Dm644 docs/man/man1/*.1 -t "${pkgdir}"/usr/share/man/man1/
}
