# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cachetools
pkgver=4.2.4
pkgrel=1
pkgdesc="Extensible memoizing collections and decorators"
url="https://github.com/tkem/cachetools"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tkem/cachetools/archive/v$pkgver.tar.gz")
sha512sums=('3850dd04e68b839b53c01bf411c5226374b07c0fc9f6965824c1111f86d417e7c9a3e42fc1957d82863eb4c0afb7a9b9048a049ae252d7e1727c9d81ebd5e061')

build() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py pytest
}

package() {
  cd cachetools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
