# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=3.4.0.1
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://nicolargo.github.io/glances/'
license=('LGPL')
makedepends=('python-setuptools')
depends=('python' 'python-psutil' 'python-future' 'python-defusedxml' 'python-ujson')
optdepends=('hddtemp: HDD temperature monitoring support'
            'python-bottle: web server support'
            'python-docker: for the Docker monitoring support'
            'python-matplotlib: for graphical/chart support'
            'python-netifaces: for the IP plugin'
            'python-zeroconf: for the autodiscover mode'
            'python-pystache: templating engine'
            'python-prometheus_client: for the Prometheus export module')
source=("${pkgname}-${pkgver}-${pkgrel}.tar.gz::https://github.com/nicolargo/glances/archive/v${pkgver}.tar.gz"
        'glances.service')
sha512sums=('e1fabd8a503dfc734b758866c7b3bda2aa40237e956e5b4b52c74134a5404e1ba2112ecf00ebe99e9a99628869442c7efbe235d2e5c95d9a951b667389bae680'
            '49f0d185a37a5c5837e5beb463770c943ede40b2f1b8405e338129e897e97d9fc58373a8586fabc506266e6343cfea3c91b9787ac6832cc97a1ab63d6ad058d4')
b2sums=('fd4b826055f00ecb3af325e6b3ed4a23ea21871d4e3c7d4030cc8e35de253ef32c773e0a30ff903e3b315861a104d3e5409b0b477e43a89b9953094900efd5c8'
        'ecc44f8c06b1e8624cec92e41422a65d11e024b9fc23bae09b4e52fbedeb172a5034e5b612bbff7ba93d45189fb25eda0d54bc47b22b7f3f0acba984391e4017')

package() {
  cd "glances-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 "${srcdir}"/glances.service \
    "${pkgdir}"/usr/lib/systemd/system/glances.service
}

# vim:set ts=2 sw=2 et:
