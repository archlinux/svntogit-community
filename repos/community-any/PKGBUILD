# $Id: PKGBUILD 106746 2014-03-07 08:11:41Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-iso8601
pkgname=(python-iso8601 python2-iso8601)
_libname=iso8601
pkgver=0.1.12
pkgrel=2
pkgdesc="Simple module to parse ISO 8601 dates"
arch=(any)
url="https://pyiso8601.readthedocs.io/en/latest/"
license=('MIT')
source=(https://files.pythonhosted.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz)
makedepends=(python-setuptools python2-setuptools)
sha256sums=('49c4b20e1f38aa5cf109ddcd39647ac419f928512c869dc01d5c7098eddede82')

build() {
	cd "$srcdir/$_libname-$pkgver"

	rm -rf ../buildpy3; mkdir ../buildpy3
	python setup.py build -b ../buildpy3

	rm -rf ../buildpy2; mkdir ../buildpy2
	python2 setup.py build -b ../buildpy2
}

package_python-iso8601() {
	depends=(python)

	cd "$srcdir/$_libname-$pkgver"
	rm -rf build; ln -s ../buildpy3 build
	python setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-iso8601() {
	depends=(python2)

	cd "$srcdir/$_libname-$pkgver"
	rm -rf build; ln -s ../buildpy2 build
	python2 setup.py install --skip-build -O1 --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
