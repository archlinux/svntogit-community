# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-haystack
pkgname=python-django-haystack
pkgver=3.1.1
pkgrel=1
pkgdesc="Modular search for Django"
arch=('any')
url="https://github.com/django-haystack/django-haystack"
license=('BSD')
depends=('python-django' 'python-six')
makedepends=('git' 'python-setuptools-scm')
checkdepends=('gdal' 'python-coverage' 'python-dateutil' 'python-elasticsearch'
'python-geopy' 'python-mock' 'python-nose' 'python-pysolr' 'python-requests'
'python-pytest' 'python-whoosh')
optdepends=('python-elasticsearch: interface with an elasticsearch instance as search backend'
            'python-pysolr: interface with a solr instance as search backend'
            'python-whoosh: use whoosh as search backend'
            'python-xapian-haystack: use python-xapian as search backend')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('509c2c922b6c6c5a2961b2d10b640cf82e8bc2536203c865c2c7a6fdebd2c7173616ce22d620bfd5ef14b61e11a9c88f64cc941195ff2adc2d62604d2c13717c')
b2sums=('2e30f428de9e3c8eea4160821e5e409d53226523e6849c1f4412e895176c136f3332d93644e0c0397c0c67bef2779dd394082ae2bd89626b19ebb1e95174a290')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # remove useless version pinning
  sed -e 's/==/>=/g' -i setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CONTRIBUTING.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
