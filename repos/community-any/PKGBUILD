# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mox3
pkgname=('python-mox3' 'python2-mox3')
pkgver=0.28.0
pkgrel=3
arch=('any')
pkgdesc='Mock object framework for Python'
url='https://docs.openstack.org/developer/mox3'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-fixtures' 'python2-fixtures')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/mox3/archive/$pkgver.tar.gz")
sha512sums=('abded0c36c8b1609d68d79c6c24616de534506178ff6ab1c93a23d67448973ee2515ced5fb0b6c73ff3f5cb72553dd75497356694eb6377c276fc2e1fc2c1cd3')

prepare() {
  # Python 3.6 fixes
  sed -i 's/, flags=4//' mox3-$pkgver/mox3/tests/test_mox.py

  cp -a mox3-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/mox3-$pkgver
  python setup.py build

  cd "$srcdir"/mox3-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mox3-$pkgver
  stestr run

  cd "$srcdir"/mox3-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-mox3() {
  depends=('python-pbr' 'python-fixtures')

  cd mox3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-mox3() {
  depends=('python2-pbr' 'python2-fixtures')

  cd mox3-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
