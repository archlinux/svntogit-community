# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-scripttest
pkgname=(python-scripttest python2-scripttest)
pkgver=1.3.0
pkgrel=2
pkgdesc="Utilities to help with testing command line scripts"
url="https://github.com/pypa/scripttest"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pypa/scripttest/archive/$pkgver.tar.gz")
sha512sums=('038bfb1726bff85b6e3675be51db829d3519b47665266f6ae1d3c5c6f4f06e7b16742262ca1ba240544e9bd39ae96c1be0bd5e8583736f72bb04241a1bb8acf6')

prepare() {
  cp -a scripttest-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/scripttest-$pkgver
  python setup.py build

  cd "$srcdir"/scripttest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/scripttest-$pkgver
  python setup.py pytest

  cd "$srcdir"/scripttest-$pkgver-py2
  python2 setup.py pytest
}

package_python-scripttest() {
  depends=('python')

  cd scripttest-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-scripttest() {
  depends=('python2')

  cd scripttest-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
