# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-h11
pkgver=0.12.0
pkgrel=3
pkgdesc="A pure-Python, bring-your-own-I/O implementation of HTTP/1.1"
url="https://github.com/njsmith/h11"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/h/h11/h11-$pkgver.tar.gz")
sha512sums=('ba2a2c185af270185ba5e1fd80ec9308e35afcd60a125a77cbeae2669f462983d063339371312f154a7c80c154ac97796ab36782b36549235435dcfd81b93421')

build() {
  cd "$srcdir"/h11-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/h11-$pkgver
  python setup.py pytest
}

package_python-h11() {
  cd h11-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
