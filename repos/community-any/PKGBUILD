# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Diego Principe <cdprincipe at gmail dot com>
# Contributor: paterbrown <paterbrown at silberhelme dot de>
# Contributor: Josip <josipponjavic at gmail dot com>

pkgname=elementary-icon-theme
pkgver=3.1
pkgrel=4
pkgdesc="elementary icon theme"
arch=('any')
url="https://launchpad.net/elementaryicons"
license=('GPL3')
depends=('gtk-update-icon-cache' 'hicolor-icon-theme')
options=('!emptydirs')
install="${pkgname}.install"
source=("http://launchpad.net/elementaryicons/${pkgver%?}x/${pkgver}/+download/elementaryicons${pkgver}.tar.gz")
sha256sums=('9a3f2c8e5067c64992f2efcddd13b5665e30143729e26ab89da73ab3211220de')

prepare() {
  cd elementaryicons${pkgver}

  rm AUTHORS CONTRIBUTORS COPYING
  sed '/Name/a Inherits=gnome,hicolor' -i index.theme
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/icons
  cp -dr --no-preserve=ownership elementaryicons${pkgver} "${pkgdir}"/usr/share/icons/elementary
}

# vim: ts=2 sw=2 et:
