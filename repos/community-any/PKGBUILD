# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-dogpile.cache
pkgname=(python-dogpile.cache python2-dogpile.cache)
pkgver=0.7.0
pkgrel=1
pkgdesc="A caching front-end based on the Dogpile lock"
arch=('any')
url="http://pypi.python.org/pypi/dogpile.cache"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator')
checkdepends=('memcached' 'redis' 'python-mako' 'python2-mako' 'python-redis' 'python2-redis'
              'python2-memcached' 'python-pytest-runner' 'python2-pytest-runner' 'python-mock'
              'python2-mock' 'python-pytest-cov' 'python2-pytest-cov' 'python-pylibmc'
              'python2-pylibmc' 'pifpaf' 'python2-pifpaf')
source=("https://pypi.io/packages/source/d/dogpile.cache/dogpile.cache-$pkgver.tar.gz")
sha512sums=('5e2995b966796d39f1b3bf5820bd6a9d285b7e24f2c5e139053712964c4486139f98ebdc18f148cab549ff5dd463e7095a0b3dc9b15c392f5d092deb20ad378d')

prepare() {
  cp -a "dogpile.cache-$pkgver"{,-py2}
}

build() {
  cd "$srcdir"/dogpile.cache-$pkgver
  python setup.py build

  cd "$srcdir"/dogpile.cache-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/dogpile.cache-$pkgver
  python -m pytest -k 'not test_memcached_backend and not test_redis_backend'
  pifpaf --env-prefix DOGPILE run memcached --port 11234 -- python -m pytest tests/cache/test_memcached_backend.py
  pifpaf --env-prefix DOGPILE run redis --port 11234 -- python -m pytest tests/cache/test_redis_backend.py

  cd "$srcdir"/dogpile.cache-$pkgver-py2
  python2 -m pytest -k 'not test_memcached_backend and not test_redis_backend'
  pifpaf2 --env-prefix DOGPILE run memcached --port 11234 -- python2 -m pytest tests/cache/test_memcached_backend.py
  pifpaf2 --env-prefix DOGPILE run redis --port 11234 -- python2 -m pytest tests/cache/test_redis_backend.py
}

package_python-dogpile.cache() {
  depends=('python-decorator')

  cd dogpile.cache-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-dogpile.cache() {
  depends=('python2-decorator')

  cd dogpile.cache-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
