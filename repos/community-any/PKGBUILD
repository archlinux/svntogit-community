# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-mock
pkgname=('python-pytest-mock' 'python2-pytest-mock')
pkgver=0.8.1
pkgrel=1
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
makedepends=('python-pytest' 'python2-pytest' 'python2-mock')
#checkdepends=('python-coverage' 'python2-coverage')
source=("http://pypi.python.org/packages/source/p/pytest-mock/pytest-mock-$pkgver.zip")
sha512sums=('30b20e315124a1a0a2aadd548d8b6b4a4762b425e5b4807e7ac4661cdaa3e5b62876fe62a68a3e3de693d2910f8c0c24be3e82e5b24215e5692c80fedfd1f8df')

prepare() {
  cp -a pytest-mock-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/pytest-mock-${pkgver}"
  python setup.py build

  cd "$srcdir/pytest-mock-${pkgver}-py2"
  python2 setup.py build
}

check() {
  # Tests not included

  cd "$srcdir/pytest-mock-${pkgver}"
  # coverage3 run --source=pytest_mock.py runtests.py

  cd "$srcdir/pytest-mock-${pkgver}-py2"
  # coverage2 run --source=pytest_mock.py runtests.py
}

package_python-pytest-mock() {
  depends=('python-pytest')

  cd pytest-mock-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pytest-mock() {
  depends=('python2-pytest' 'python2-mock')

  cd pytest-mock-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

