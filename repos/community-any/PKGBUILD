# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=4.0.4
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=(any)
url="https://github.com/ansible-community/molecule"
license=(MIT)
depends=(python-ansible-compat python-click
python-click-help-colors python-cookiecutter python-enrich python-jinja
python-jsonschema python-packaging python-pluggy python-pyyaml python-rich)
makedepends=(python-build python-installer python-setuptools python-setuptools-scm)
checkdepends=(ansible python-ansi2html python-filelock python-pexpect
python-pytest python-pytest-mock python-pytest-testinfra python-pytest-xdist
yamllint)
optdepends=(
  'ansible: for the ansible verifier'
  'molecule-docker: for the docker driver'
  'molecule-podman: for the podman driver'
  'molecule-vagrant: for the vagrant driver'
  'python-pywinrm: for Windows support'
  'python-pytest-testinfra: for the testinfra verifier'
)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('d7c1ceefbf74cbc27a397e7a40d855b23325bbde31a4cd1920961413a7db950e9fc530fec180de877eb37984bac8603033aa5a582e68e7f4c4ac2a10721c6630')
b2sums=('14c493fa8aaa15466ebca19af9e5325ddb824c151d07d800136dcb714a430ea2dc42b38c9ec6e805e29ec3226376a532793d51c5d0544d6d5105510e5365ca8c')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="$PWD/test_dir"

  # install into test location
  mkdir -vp "$_test_dir"
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  export PATH="$_test_dir/usr/bin:$PATH"
  pytest -v "$_test_dir/$_site_packages/molecule/test/unit/" -c /dev/null
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
