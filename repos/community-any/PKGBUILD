# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgname=python-icalendar
pkgver=4.0.7
pkgrel=1
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=('any')
url="https://github.com/collective/icalendar"
license=('BSD')
depends=('python-dateutil' 'python-pytz' 'python-setuptools')
checkdepends=('python-hypothesis' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('97f7184ddc42ade2a447af5112f5bd18ea9a89ef25ea3983cc2213fefe2c59255102b7babcd49b3e04cbe435d75d2712dd57eebc1bf1c20c54aae7081ba24379')
b2sums=('b5d980335b1a9d8135ad9ec037e140f3ae49c3e8db6736b9e469748e3bd51c43f39fc5f9dc443de46d4d4c5856ec74bb548a8d1b47b7e52278fdc2e269be0cbe')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  PYTHONPATH="${PWD}:${PYTHONPATH}"
  pytest -v "src/${_name}/tests"
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE.rst \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
