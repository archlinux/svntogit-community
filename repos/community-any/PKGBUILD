# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-h11
pkgver=0.11.0
pkgrel=1
pkgdesc="A pure-Python, bring-your-own-I/O implementation of HTTP/1.1"
url="https://github.com/njsmith/h11"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/h/h11/h11-$pkgver.tar.gz")
sha512sums=('13dba59afd949d715fd693e5224e57dea5c923eb91b0981a5b5987bbe6e6e00e32552e1602353a48458615a8dd21f2a561961747ae15291eb3462fe42cef3f82')

build() {
  cd "$srcdir"/h11-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/h11-$pkgver
  python setup.py pytest
}

package_python-h11() {
  cd h11-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
