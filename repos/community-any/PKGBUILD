# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgname=emacs-php-mode
pkgver=1.19.1
pkgrel=4
license=("GPL")
pkgdesc="PHP mode for emacs"
url="https://github.com/ejmr/php-mode"
arch=('any')
depends=('emacs')
options=(docs zipman)
install=emacs-php-mode.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/ejmr/php-mode/archive/v$pkgver.tar.gz")
sha256sums=('23a891e989eb0466d1dddcb6cb62404d3f1c7a329d7ab6709c8fc16e1bc277c9')

build() {
  cd "$srcdir"/php-mode-$pkgver
  make -j1
}

package() {
  cd "$srcdir"/php-mode-$pkgver
  for i in *.el{,c}; do
    install -Dm0644 $i "$pkgdir"/usr/share/emacs/site-lisp/php-mode/$i
  done
  install -dm0755 "$pkgdir"/usr/share/emacs/site-lisp/php-mode
  cp -a skeleton "$pkgdir"/usr/share/emacs/site-lisp/php-mode/skeleton
}
