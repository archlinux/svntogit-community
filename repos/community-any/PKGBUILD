# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-execnet
pkgname=('python-execnet' 'python2-execnet')
pkgver=1.7.1
pkgrel=5
pkgdesc="Rapid multi-Python deployment"
arch=('any')
license=('MIT')
url="https://codespeak.net/execnet"
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-apipkg' 'python2-apipkg')
checkdepends=('python-pytest-timeout' 'python2-pytest-timeout' 'procps-ng' 'openssh' 'pypy')
source=("https://pypi.io/packages/source/e/execnet/execnet-$pkgver.tar.gz")
sha512sums=('375748c03f8d955bd8fca90d5c6308073c31a978fd7a604257512e0a710f899dec5db8a91d5bb5f16de9b0656e1742da9ab513827a1a065a5125bafcf1e63a70')

prepare() {
  cp -a execnet-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/execnet-${pkgver}"
  python setup.py build

  cd "$srcdir/execnet-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/execnet-${pkgver}"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test testing

  cd "$srcdir/execnet-${pkgver}-py2"
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 testing
}

package_python-execnet() {
  depends=('python-apipkg')

  cd execnet-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-execnet() {
  depends=('python2-apipkg')

  cd execnet-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
