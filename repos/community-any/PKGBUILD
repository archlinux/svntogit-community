# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pyname=tld
pkgname=python-${_pyname}
pkgver=0.13
pkgrel=4
pkgdesc="Extracts the top level domain (TLD) from the URL given"
arch=(any)
url="https://github.com/barseghyanartur/tld"
license=('MPL' 'GPL' 'LGPL')
depends=('python')
makedepends=('python-'{build,installer,wheel}
             'python-setuptools-scm')
source=("${url}/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz")
b2sums=('9f84307ce704500096dd2abff75ead534dbd17b59ea0d3dae84394b791aeee873638e6e224ddd3b5c598293a46ead964ae36022b0c3c7411de7c64096d0c69cc')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build -wn
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer -d "${pkgdir}" dist/*.whl
}

# vim: ts=2 sw=2 et:
