# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-commentjson
pkgver=0.8.3
pkgrel=1
pkgdesc="Add Python and JavaScript style comments in your JSON files"
url="https://github.com/vaidik/commentjson"
license=('MIT')
arch=('any')
depends=('python-lark-parser')
makedepends=('python-setuptools')
checkdepends=('python-tests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/vaidik/commentjson/archive/v$pkgver.tar.gz")
sha512sums=('407f6a04d1200f92a64dfa0a93cefc5bb9429b215d2623c1d9c2e1f73f192844215fc2133db8d6fd927be90c86834a3cb9c472af1b8d71e855eecda7dfd58427')

prepare() {
  cd commentjson-$pkgver
  sed -i 's/,<0/,<1/' setup.py
}

build() {
  cd commentjson-$pkgver
  python setup.py build
}

check() {
  cd commentjson-$pkgver
  python setup.py test
}

package() {
  cd commentjson-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.rst -t "$pkgdir"/usr/share/licenses/$pkgname/
}
