# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-spec-validator
pkgver=0.3.1
pkgrel=1
pkgdesc="A Python library that validates OpenAPI Specs against the OpenAPI 2.0 (aka Swagger) and OpenAPI 3.0.0 specification"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache')
arch=('any')
depends=('python-jsonschema' 'python-openapi-schema-validator' 'python-six' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-flake8' 'python-pytest-cov'
              'python-tox')
source=("$pkgname-$pkgver.tar.gz::https://github.com/p1c2u/openapi-spec-validator/archive/$pkgver.tar.gz")
sha512sums=('c7004f82971c668498881070d7a8b77e0de1596b5487ad3e44a5b8d54e931512352739089efb5d496782635c6c314d86e33d801d6decd9918b167e45f8211ff6')

build() {
  cd openapi-spec-validator-$pkgver
  python setup.py build
}

check() {
  cd openapi-spec-validator-$pkgver
  python setup.py pytest
}

package() {
  cd openapi-spec-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
