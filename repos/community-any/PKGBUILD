# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=daemons
pkgname=ruby-${_gemname}
pkgver=1.3.1
pkgrel=2
pkgdesc='Easy way to wrap existing ruby scripts to be run as a daemon'
url='https://github.com/thuehlinger/daemons'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/thuehlinger/daemons/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ceeeefbf44727a1ee43b651bbc3f8e5ae83c73388d017d240d95624586b91a3b32602464f4a5ff53a854d6cc6fa1dc47e433a665f0ad7f08e440c0a798743d80')
b2sums=('4ddb9cb9a5a6de5ebeac8ac69c2712500f9dd18c9d194efce3152735533a2aef7a4cf0e034e4c1781e5d89c1be18e7e7c119455bafb0bf4d8cec279b1e8f6308')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
  sed 's|README.md LICENSE Releases ||' -i ${_gemname}.gemspec
  sed -r 's|`git ls-files (.+)`|`find \1 -type f`|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem -- --use-system-libraries
  install -Dm 644 README.md Releases -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
