# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-debtcollector
pkgname=('python-debtcollector' 'python2-debtcollector')
pkgver=2.0.0
pkgrel=2
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='https://docs.openstack.org/developer/debtcollector'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-wrapt' 'python2-wrapt' 'python2-funcsigs')
checkdepends=('python-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/debtcollector/archive/$pkgver.tar.gz")
sha512sums=('cdacf720cd783e1d795089451f9d2cf91ba79ef6085b18760d13f0b98fdbecb0c21960a771f3a03a867b5a301f0cc66f09407dfb8ae5713132d5b3fd027bc439')

prepare() {
  cp -a debtcollector-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py build

  cd "$srcdir"/debtcollector-$pkgver-py2
  python2 setup.py build
}

check() {
  cd debtcollector-$pkgver
  stestr run
}

package_python-debtcollector() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')

  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-debtcollector() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-wrapt' 'python2-funcsigs')

  cd debtcollector-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
