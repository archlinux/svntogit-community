# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=bugzilla
pkgver=5.0.2
pkgrel=1
pkgdesc="bug tracker"
arch=('any')
url="http://www.bugzilla.org"
license=("GPL")
depends=(perl perl-timedate perl-datetime
	 perl-chart perl-email-abstract perl-email-address perl-email-date-format
	 perl-email-messageid perl-email-mime perl-email-mime-attachment-stripper
	 perl-email-mime-contenttype perl-email-mime-encodings
	 perl-email-mime-modifier perl-email-mime-creator perl-email-reply perl-email-send perl-email-simple
	 perl-email-simple-creator perl-gd perl-gdgraph perl-gdtextutil perl-patchreader
	 perl-return-value perl-template-gd perl-ldap perl-soap-lite perl-authen-sasl
	 perl-xml-twig perl-html-scrubber perl-authen-radius perl-template-toolkit
	 perl-data-dumper perl-math-random-isaac perl-email-sender)
optdepends=('perl-dbd-pg'
	    'perl-dbd-mysql'
	    'perl-dbd-oracle'
	    'python2'
	    'ruby'
	    'perl-class-trigger'
	    'perl-data-objectdriver'
	    'perl-html-formattext-withlinks'
	    'perl-test-taint'
	    'perl-theschwartz'
	    'perl-xmlrpc-lite')
options=(!strip)
backup=('srv/http/bugzilla/.htaccess')
source=(http://ftp.mozilla.org/pub/mozilla.org/webtools/bugzilla-$pkgver.tar.gz)
md5sums=('84e6a070d425846d55a1f926e883957e')

package() {
  cd $srcdir
  install -d -m0755 $pkgdir/srv/http
  find $pkgname-$pkgver -type d -name CVS -exec rm -rf {} \; || true
  find $pkgname-$pkgver -type f -name .cvsignore -exec rm -f {} \; || true
  cp -a $pkgname-$pkgver $pkgdir/srv/http/bugzilla
  rm -rf $pkgdir/srv/http/bugzilla/.bzr
  rm -rf $pkgdir/srv/http/bugzilla/.bzrrev
  rm -rf $pkgdir/srv/http/bugzilla/.bzrignore
  rm -rf $pkgdir/srv/http/bugzilla/.git
  rm -rf $pkgdir/srv/http/bugzilla/.gitignore
}
