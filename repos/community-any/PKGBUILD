# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='parallel'
pkgname="ruby-${_gemname}"
pkgver=1.22.1
pkgrel=1
pkgdesc='Run any kind of code in parallel processes'
arch=('any')
url="https://github.com/grosser/${_gemname}"
license=('MIT')
depends=('ruby')
#checkdepends=('ruby-bump' 'ruby-rspec')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f106ac605a5998d0e5a43b7863f8400e49270c06e55b6342a84fed788e1e854a26a5e29ec31da9d96a4d403227d002691e27786c25e8d7542f3877bf097e55ad')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"

  # we build based on a tar archive, not a git repo
  sed --in-place --regexp-extended 's|git ls-files lib MIT-LICENSE.txt|find lib MIT-LICENSE.txt -type f|' "${_gemname}.gemspec"

  rm --verbose Gemfile.lock
}

build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

# checks are not possible for now as we don't have rails packaged yet
# Bundler::GemNotFound: Could not find gem 'activerecord (~> 6.0)' in locally installed gems.
# and https://aur.archlinux.org/packages/ruby-bump is not in the official repos yet
#check() {
#  cd "${_gemname}-${pkgver}"
#
#  rake --tasks
#}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "${pkgdir}/${_gemdir}/cache/" \
    "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "${pkgdir}/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "${pkgdir}/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete

  install --verbose -D --mode=0644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install --verbose -D --mode=0644 *.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
