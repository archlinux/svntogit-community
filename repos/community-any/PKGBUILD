# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dbus-deviation
pkgname=python-dbus-deviation
pkgver=0.6.1
pkgrel=6
pkgdesc="Parse D-Bus introspection XML and process it in various ways"
arch=('any')
url="https://github.com/pwithnall/${_pkgname}"
license=('LGPL')
depends=('python-lxml')
makedepends=('python-setuptools-git' 'python-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('e06b88efe223885d2725df51cf7c9b7b463d1c6f04ea49d4690874318d0eb7a3')
b2sums=('2042cb78bfd5ae259842001f39d8d10456c6b7aaac9bdde309895547c2e9649fac14e0a4a3a6432e3c67be562c443b2b44229e5c61e2802a10d8c66e94a544d4')

build() {
    cd ${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd ${_pkgname}-${pkgver}

    python -m unittest discover -vs dbusapi/tests
}

package() {
    cd ${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
