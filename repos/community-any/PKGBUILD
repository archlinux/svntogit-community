# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

pkgbase=python-requests-toolbelt
pkgname=('python-requests-toolbelt' 'python2-requests-toolbelt')
_pkgname=requests-toolbelt
pkgver=0.5.0
pkgrel=1
pkgdesc="A toolbelt of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/sigmavirus24/requests-toolbelt"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-betamax' 'python2-betamax'
              'python-mock' 'python2-mock')
source=("git+https://github.com/sigmavirus24/requests-toolbelt.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a ${_pkgname}{,-py2}
}

build() {
  cd "$srcdir/${_pkgname}"
  python setup.py build

  cd "$srcdir/${_pkgname}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/${_pkgname}"
  py.test

  cd "$srcdir/${_pkgname}-py2"
  py.test2
}

package_python-requests-toolbelt() {
  depends=('python-requests')

  cd "$srcdir/${_pkgname}"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-requests-toolbelt() {
  depends=('python2-requests')

  cd "$srcdir/${_pkgname}-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}
