# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cldr-emoji-annotation
pkgver=36.12.120191002.0
_tag=${pkgver%.*}_${pkgver##*.}
pkgrel=1
pkgdesc="Emoji annotation files in CLDR"
arch=('any')
url="https://github.com/fujiwarat/cldr-emoji-annotation"
license=('custom')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fujiwarat/cldr-emoji-annotation/archive/$_tag.tar.gz")
sha512sums=('d186413587ff62280d6f40f1ae7fae50c0e821a68407288a440b21bcb3a0ccf87617a40e02e606c30d895b8da0be3d7f073092090219457bf1f6d5ac4e54a0a8')

build() {
  cd cldr-emoji-annotation-$_tag
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd cldr-emoji-annotation-$_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 unicode-license.txt "$pkgdir"/usr/share/licenses/$pkgname/unicode-license.txt
}
