# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin-data
pkgver=2.0.3.20131211
pkgrel=1
arch=('any')
pkgdesc="Statistical language model data from open-gram project for sunpinyin"
license=('APACHE')
provides=("sunpinyin-data-le=$pkgver")
url="http://sunpinyin.googlecode.com"
makedepends=("sunpinyin")
source=('http://open-gram.googlecode.com/files/lm_sc.t3g.arpa-20121025.tar.bz2'
        'http://open-gram.googlecode.com/files/dict.utf8-20131211.tar.bz2')

prepare() {
  mkdir raw && cd raw
  ln -s ../lm_sc.t3g.arpa
  ln -s ../dict.utf8

  ln -sf "/usr/share/doc/sunpinyin/SLM-inst.mk" ./Makefile
}

build() {
  cd raw
  make
}

package() {
  cd raw
  make DESTDIR="$pkgdir" install
}

sha512sums=('3159dd3700382cfa617ba025ce542660580f3057e0dd6bbb92947902495db08e4454c1d09d01a55c90653444513d125dd034e944b060a9d8e009c3633b106d97'
            'c8fb734cfe04ca4b6b5746bc405f025b55b09773d043a0b72f9bd24e89566e0e2aa7f1167efedb0799e07767b36c08a552a25e357e7e678337796087559e4a0c')
