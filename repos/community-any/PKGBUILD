# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pifpaf
pkgver=2.6.0
pkgrel=1
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-six'
         'python-fixtures' 'python-psutil' 'python-xattr')
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
makedepends=('python-setuptools')
checkdepends=('python-testrepository' 'python-requests' 'python-testtools' 'python-oslotest'
              'memcached' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb' 'httpbin' 'rabbitmq')
# 'elasticsearch': test fails
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz")
sha512sums=('096e86190c636587b0e456de8ed5a5a26872a471287f6d55d37a0c5847e4e2e27a7d103d9c2d72bf59a28577e8681d2077c4491832acad2d6692f64dfc198dd4')

export PBR_VERSION=$pkgver

build() {
  cd pifpaf-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr
}

package() {
  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
