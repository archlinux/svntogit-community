# $Id: PKGBUILD 175443 2016-05-16 05:54:00Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-ddt
pkgname=('python-ddt' 'python2-ddt')
pkgver=1.1.0
pkgrel=1
pkgdesc="Data-Driven/Decorated Tests"
arch=('any')
license=('MIT')
url="https://github.com/txels/ddt"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-yaml' 'python2-yaml')
source=("git+https://github.com/txels/ddt.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a ddt{,-py2}
}

build() {
  cd "$srcdir"/ddt
  python setup.py build

  cd "$srcdir"/ddt-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/ddt
  nosetests3

  cd "$srcdir"/ddt-py2
  nosetests2
}

package_python-ddt() {
  depends=('python')

  cd ddt
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

package_python2-ddt() {
  depends=('python2')

  cd ddt-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
