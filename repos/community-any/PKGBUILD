# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-bottle
pkgver=0.12.25
pkgrel=3
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="https://bottlepy.org"
license=('MIT')
options=(!emptydirs)
depends=('python')
makedepends=('python-setuptools' 'git')
checkdepends=('python-mako' 'python-jinja' 'python-cherrypy' 'python-twisted'
              'python-tornado' 'python-paste' 'python-gevent' 'python-eventlet'
              'gunicorn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/defnull/bottle/archive/$pkgver.tar.gz")
sha512sums=('b4b163d0225cc2be0f009427ac903dfbddc6f4bda89d3d7da2b201ccd2edbee97c2c08e7b210ffaa95077fe8ab6c1ff79ac5ffb212e8fdfed6268bfcfd6749bd')

build() {
  cd bottle-$pkgver
  python setup.py build
}

check() {
  cd bottle-$pkgver
  python -m unittest discover
}

package() {
  cd bottle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim: ts=2 sw=2 et:
