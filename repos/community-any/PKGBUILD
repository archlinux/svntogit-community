# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=aurpublish
pkgver=r24.ga630272
pkgrel=1
pkgdesc="PKGBUILD management/upload framework for the Arch User Repository"
arch=('any')
url="https://github.com/eli-schwartz/${pkgname}"
license=('GPL')
depends=('git' 'openssh')
_commit=a6302723da4780119db4eb345a9c550c6d5838db
source=("git+${url}.git?signed#commit=${_commit}")
sha512sums=('SKIP')
validpgpkeys=('BD27B07A5EF45C2ADAF70E0484818A6819AF4A9B') # Eli Schwartz <eschwartz@archlinux.org>

pkgver() {
    cd "${srcdir}"/${pkgname}

    printf "r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "${srcdir}"/${pkgname}

    make PREFIX=/usr
}

package() {
    cd "${srcdir}"/${pkgname}

    make PREFIX=/usr DESTDIR="${pkgdir}" install
}
