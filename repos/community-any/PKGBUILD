# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

# TODO: move optional python-requests-cache

pkgbase=python-pandas-datareader
pkgname=('python2-pandas-datareader' 'python-pandas-datareader')
pkgver=0.7.0
pkgrel=1
pkgdesc="Data readers extracted from the pandas codebase"
arch=('any')
url="https://github.com/pydata/pandas-datareader"
license=('BSD')
makedepends=('python-setuptools' 'python-pandas' 'python-lxml' 'python-requests' 'python-wrapt'
             'python2-setuptools' 'python2-pandas' 'python2-lxml' 'python2-requests' 'python2-wrapt')
source=("https://github.com/pydata/pandas-datareader/archive/v${pkgver}.tar.gz")
md5sums=('b8199981d1a5148d754da3970e0f09de')

prepare() {
  cd "$srcdir"

  cp -a pandas-datareader-${pkgver} pandas-datareader-py2-${pkgver}
  cd pandas-datareader-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/pandas-datareader-py2-${pkgver}
  python2 setup.py build_ext --inplace
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/pandas-datareader-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package_python2-pandas-datareader() {
  depends=('python2-pandas' 'python2-lxml' 'python2-requests' 'python2-wrapt')

  cd "$srcdir"/pandas-datareader-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-pandas-datareader() {
  depends=('python-pandas' 'python-lxml' 'python-requests' 'python-wrapt')

  cd "$srcdir"/pandas-datareader-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

