# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgbase=python-pygeoip
pkgname=(python-pygeoip python2-pygeoip)
_pypiname=pygeoip
pkgver=0.3.2
pkgrel=4
pkgdesc="Pure Python GeoIP API"
arch=('any')
url="https://pypi.python.org/pypi/$_pypiname"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
optdepends=('geoip-database: provides a database for geoip lookups')
source=("https://pypi.python.org/packages/source/p/${_pypiname}/${_pypiname}-${pkgver}.tar.gz")
md5sums=('861664f8be3bed44820356539f2ea5b6')

prepare() {
  cp -a $_pypiname-$pkgver{,-py2}
}

build() {
  cd "$srcdir/$_pypiname-$pkgver"
  python setup.py build

  cd "$srcdir/$_pypiname-$pkgver-py2"
  python2 setup.py build
}

check() {
  # Missing data files for running tests

  cd "$srcdir/$_pypiname-$pkgver"
  #nosetests3

  cd "$srcdir/$_pypiname-$pkgver-py2"
  #nosetests2
}

package_python-pygeoip() {
  depends=('python')

  cd "$srcdir/$_pypiname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pygeoip() {
  depends=('python2')

  cd "$srcdir/$_pypiname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
