# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-protocol-http
pkgver=0.24.0
pkgrel=1
pkgdesc='Provides abstractions to handle HTTP protocols'
arch=(any)
url='https://github.com/socketry/protocol-http'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-bake-modernize ruby-bake-test ruby-bake-test-external ruby-covered ruby-rspec
              ruby-sus)
options=(!emptydirs)
source=(https://github.com/socketry/protocol-http/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('361a721c0cb5da04f5ac4456933e93e2e5f1f308427f986484474c9b14974f5e')

prepare() {
  cd protocol-http-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i protocol-http.gemspec
  sed -i '/bake-gem/d;/utopia-project/d' gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http-$pkgver
  gem build protocol-http.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    protocol-http-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd protocol-http-$pkgver
  GEM_HOME="tmp_install/$_gemdir" bake test
}

package() {
  cd protocol-http-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
