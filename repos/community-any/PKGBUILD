# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-webcolors
pkgname=(python-webcolors python2-webcolors)
pkgver=1.9.1
pkgrel=1
pkgdesc="A library for working with color names and color value formats defined by the HTML and CSS specifications for use in documents on the Web"
url="https://github.com/ubernostrum/webcolors"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/w/webcolors/webcolors-$pkgver.tar.gz")
sha512sums=('333f7ff7a6d0eca1eaf73f70be660eeeaa14c85af41ebc93e1a27a3564bf5fc3d51c1f73136befbb88065f56f4d3b5d25935610308302254302f290c389e2628')

prepare() {
  cp -a webcolors-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/webcolors-$pkgver
  python setup.py build

  cd "$srcdir"/webcolors-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/webcolors-$pkgver
  python setup.py pytest

  cd "$srcdir"/webcolors-$pkgver-py2
  python2 setup.py pytest
}

package_python-webcolors() {
  depends=('python')

  cd webcolors-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-webcolors() {
  depends=('python2')

  cd webcolors-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
