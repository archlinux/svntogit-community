# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Michele Sch√§uble <mschaeuble@swissonline.ch>

pkgname=josm
pkgver=14945
pkgrel=1
pkgdesc="An editor for OpenStreetMap written in Java"
arch=('any')
url="http://josm.openstreetmap.de/"
license=('GPL')
depends=('java-runtime>=8' 'libxtst' 'ttf-font')
changelog=$pkgname.changelog
backup=('etc/conf.d/josm')
source=(https://josm.openstreetmap.de/$pkgname-tested.jar
        $pkgname-$pkgver.desktop::https://josm.openstreetmap.de/export/${pkgver}/josm/trunk/linux/tested/usr/share/applications/org.openstreetmap.josm.desktop
        $pkgname.conf.d)
noextract=($pkgname-tested.jar)
sha256sums=('eb6244b5566515f63509b8a8a83fa51403c73d4a65a445e99c4ebe13816dae4d'
            'dab386f139d773e34e14c2755525a9a60ef4db549f2562388273bd2f1257e2fc'
            'c86a73251eed42fcb129ae9b88a0ee3cf03d8d00a9385519ad536d5af0907663')

prepare() {
  cd "${srcdir}"
  bsdtar -xf $pkgname-tested.jar images/logo.svg
}

package() {
  cd "${srcdir}"

  install -Dm644 $pkgname-tested.jar "${pkgdir}"/usr/share/java/$pkgname/$pkgname.jar

#.desktop and icon file
  install -Dm644 "${srcdir}"/$pkgname-$pkgver.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop
  install -Dm644 images/logo.svg "${pkgdir}"/usr/share/pixmaps/$pkgname.svg

#executable file
  install -d "${pkgdir}"/usr/bin
  cat <<"EOF" >"${pkgdir}"/usr/bin/$pkgname 
#!/bin/sh
# source application-specific settings
while true; do
    JOSM_ARGS=
    [ -f /etc/conf.d/josm ] && . /etc/conf.d/josm
    CLASSPATH="/usr/share/java/josm/josm.jar"
    java ${JOSM_ARGS} -cp "${CLASSPATH}" -Djosm.restart=true org.openstreetmap.josm.gui.MainApplication "$@"
    [ $? -eq 9 ] || break
done
EOF
  chmod 755 "${pkgdir}"/usr/bin/$pkgname
  install -Dm644 "${srcdir}"/$pkgname.conf.d "${pkgdir}"/etc/conf.d/$pkgname
}
