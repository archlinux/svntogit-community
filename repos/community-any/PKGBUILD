# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer:  Christian Hesse <mail@eworm.de>
# Contributor: Jon Eyolfson <jon@eyolfson.com>

pkgname=gitolite
pkgver=3.6.2
pkgrel=2
pkgdesc='An access control layer on top of git'
url='https://github.com/sitaramc/gitolite'
arch=('any')
license=('GPL2')
depends=('git' 'perl')
install=gitolite.install
source=($pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz
        gitolite-3.6.2-openssh-6.8-compat.patch)
sha256sums=('012c372b53d765558131ae0df23a590d41b042c8b88e38e952992caf52fc4fbf'
            '4943769dcf45a3f68fe7ffd7450239eae83c2bd723051b208a5d1fa4a5a93a75')

prepare() {
  cd $pkgname-$pkgver/src
  echo "$pkgver" > VERSION

  patch -p2 -i "$srcdir"/gitolite-3.6.2-openssh-6.8-compat.patch
}

package() {
  install -d "$pkgdir"/usr/{bin,lib/gitolite}
  cp -a $pkgname-$pkgver/src/* "$pkgdir"/usr/lib/gitolite
  ln -s /usr/lib/gitolite/gitolite "$pkgdir"/usr/bin/
}
