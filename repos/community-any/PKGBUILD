# Maintainer: Maxim Baz <archlinux at maximbaz dot com>

pkgname=rebuild-detector
pkgver=4.4.1
pkgrel=3
pkgdesc='Detects which packages need to be rebuilt'
arch=('any')
url="https://github.com/maximbaz/${pkgname}"
license=('ISC')
depends=('binutils' 'parallel' 'pacutils' 'pacman-contrib')
makedepends=('marked-man')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/maximbaz/${pkgname}/archive/refs/tags/${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::https://github.com/maximbaz/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig")
sha256sums=('1b728629755dd2d45fd63aba1ae7c6e98e5ba0f5e8e36e0f7bbf9cfe2b20156b'
            'SKIP')
validpgpkeys=('56C3E775E72B0C8B1C0C1BD0B5DB77409B11B601')

build() {
    cd "${pkgname}-${pkgver}"
    make docs
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
