# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-yaspin
pkgver=0.14.3
pkgrel=2
pkgdesc="Yet Another Terminal Spinner"
url="https://github.com/pavdmyt/yaspin"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pavdmyt/yaspin/archive/v$pkgver.tar.gz")
sha512sums=('f6fc5e8a39c6d1606f68c9f2521aaa9ba1023b7b7b598d5125fc8b0f0bb2b228c81898014270a985ddb0add8e5f6edc9453a15bddb8c4b29f9b75577a6f94b4b')

build() {
  cd yaspin-$pkgver
  python setup.py build
}

check() {
  cd yaspin-$pkgver
  python setup.py pytest
}

package() {
  cd yaspin-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
