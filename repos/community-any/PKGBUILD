# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
pkgver=1.3.13
pkgrel=2
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
depends=(python-aws-xray-sdk python-boto python-boto3
         python-cryptography python-dateutil python-docker python-jinja
         python-jsondiff python-jose python-mock python-pyaml
         python-pytz python-requests python-responses python-xmltodict
         python-werkzeug python-cfn-lint python-setuptools python-sshpubkeys)
checkdepends=(python-flask python-freezegun python-nose python-sure tk)
optdepends=(
  'python-flask: for moto_server'
)
source=("https://files.pythonhosted.org/packages/source/m/moto/moto-$pkgver.tar.gz"
        moto-fix-setup.patch::https://github.com/yan12125/moto/commit/92bf8eff12b49def8e9689c57d26184c548546d0.patch
        botocore-1.12.198.patch::https://github.com/spulec/moto/commit/e4a4e6183560489e98b95e815b439c7a1cf3566c.patch)
sha256sums=('95d48d8ebaad47fb5bb4233854cf1cf8523ec5307d50eb1e4017ce10f1960b66'
            '913485b0605c1b9cbb445b1862eb91ee6130430d5b735836f8e1b093565924d8'
            '01d94989ff1016ec6668221ddd07e1423542a8dad7b091f92cdc31602097bfae')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../moto-fix-setup.patch
  patch -Np1 -i ../botocore-1.12.198.patch

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#,?<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # boto 2.x is broken with proxies https://github.com/boto/boto/issues/2869
  # XXX: is there a way to skip boto 2.x tests only?
  if [ -n "$http_proxy" -o -n "$https_proxy" ] ; then
    echo "Warning: Skipping tests as some tests do not work with proxies."
    return
  fi

  TZ=UTC nosetests -sv ./tests/ --exclude='test_iot.*' --exclude='test_lambda.*'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
