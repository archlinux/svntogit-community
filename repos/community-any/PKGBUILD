# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flexmock
pkgver=0.10.5
pkgrel=1
pkgdesc="Mock/Stub/Spy library for Python"
arch=('any')
license=('BSD')
url="https://has207.github.com/flexmock"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bkabrda/flexmock/archive/$pkgver.tar.gz")
sha512sums=('d56adeb04d15103021649c7834513a8c934e4efb1270fad81a62a923a95d77d6c3f8e88c9bcdb05614fae953585249cf6cbb3792fb79965a67da6f3be8a1df08')

export LC_CTYPE=en_US.UTF-8

build() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py pytest
}

package() {
  cd flexmock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
