# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>

pkgname=pydf
pkgver=9
pkgrel=1
pkgdesc="A colorized df clone"
arch=("any")
url="http://kassiopeia.juls.savba.sk/~garabik/software/pydf/"
license=('custom')
depends=('python')
backup=('etc/pydfrc')
source=(http://kassiopeia.juls.savba.sk/~garabik/software/pydf/pydf_$pkgver.tar.gz)
md5sums=('71778fd7b6668ac157ea06a8867f2d20')

package() {
  cd "$srcdir/pydf-$pkgver"
  install -Dm755 pydf "$pkgdir/usr/bin/pydf"
  install -Dm644 pydfrc "$pkgdir/etc/pydfrc"
  install -Dm644 pydf.1 "$pkgdir/usr/share/man/man1/pydf.1"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/pydf/COPYING"
}
