# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=zaproxy
pkgver=2.12.0
pkgrel=1
pkgdesc='Integrated penetration testing tool for finding vulnerabilities in web applications'
url='https://www.owasp.org/index.php/ZAP'
arch=('any')
license=('Apache')
depends=('bash' 'java-runtime' 'ttf-font')
_java=11
makedepends=('gradle' "java-environment=${_java}")
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/zaproxy/zaproxy/archive/v${pkgver}.tar.gz")
sha512sums=('9e525540029e3467564d81c93826890727c7a84b20fe1274d52c9369abce05ee812e4a2ff6712e70b32957481025c5b3b7e3a736942404cff6c301c71c33cbdc')
b2sums=('f2a2c3e5aa89608e8db1f7b1418991dd1446dacb754500efd975dd30c37c63cce290b37ba0767783f2146bf499ff6a24cf9fd5cb28c144c1a77ded48c18ef1a7')

prepare() {
  cd ${pkgname}-${pkgver}
  cat > ${pkgname} <<EOF
#!/bin/sh
cd /usr/share/zaproxy
./zap.sh
cd -
EOF
}

build() {
  cd ${pkgname}-${pkgver}
  export PATH="/usr/lib/jvm/java-${_java}-openjdk/bin:$PATH"
  export JAVA_HOME="/usr/lib/jvm/java-${_java}-openjdk"
  /usr/bin/gradle :zap:distLinux
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr/share/${pkgname}"
  tar zxf zap/build/distributions/ZAP_${pkgver}_Linux.tar.gz
  cp -pR ZAP_${pkgver}/. "${pkgdir}/usr/share/${pkgname}"
  install -Dm 755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
