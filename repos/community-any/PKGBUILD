# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-mock
pkgname=('python-pytest-mock' 'python2-pytest-mock')
pkgver=1.13.0
pkgrel=1
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
makedepends=('python-pytest' 'python2-pytest' 'python2-mock' 'python-setuptools-scm'
             'python2-setuptools-scm')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-mock/archive/v$pkgver.tar.gz")
sha512sums=('9ce45bf8d595b3616102577ff9781e824bb28a8e9f93c84af8910105e1ec342f2210cea1d740f2453663ed35e5d6bf220c850c171bd9922ef511546e4b61552c')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cp -a pytest-mock-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-mock-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-mock-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-mock-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH:$PWD/tests" py.test

  cd "$srcdir"/pytest-mock-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD/tests" py.test2
}

package_python-pytest-mock() {
  depends=('python-pytest')

  cd pytest-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pytest-mock() {
  depends=('python2-pytest' 'python2-mock')

  cd pytest-mock-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
