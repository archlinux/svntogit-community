# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=3.0.0
pkgrel=3
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
         'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz")
sha512sums=('246831aa95db535becf130e759376773640fb94a5b78243ab6393e47bb4acc4bdb21c2352ce45be801e34f69c0dca3cf847b3f194c13edd219d46a3a69063e0f')

export PBR_VERSION=$pkgver

build() {
  cd oslo.serialization-$pkgver
  python setup.py build
}

check() {
  cd oslo.serialization-$pkgver
  stestr run
}

package() {
  cd oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
