# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-structlog
pkgver=19.1.0
pkgrel=2
pkgdesc="Structured Logging for Python"
url="http://www.structlog.org"
license=('Apache')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-freezegun' 'python-pretend' 'python-simplejson'
              'python-twisted' 'python-rapidjson' 'python-greenlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hynek/structlog/archive/$pkgver.tar.gz")
sha512sums=('7d2705afab3ab2415e4ce636061572e1dce57b612d45e34b5f5b55947d180b8b69f97c084ce9e6e79fde270daab8506618fe7da881f2db1fd3a781b240b8a6c5')

build() {
  cd structlog-$pkgver
  python setup.py build
}

check() {
  cd structlog-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd structlog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
