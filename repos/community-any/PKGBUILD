# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=daemons
pkgname=ruby-${_gemname}
pkgver=1.2.6
pkgrel=1
pkgdesc='Easy way to wrap existing ruby scripts to be run as a daemon'
url='https://github.com/thuehlinger/daemons'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/thuehlinger/daemons/archive/v${pkgver}.tar.gz)
sha256sums=('63e9745d1f7ea78f3de4f56609e8e210ba7db3e78b557615fce461815e5c7a8b')
sha512sums=('8276685a684093252bf136c552a613a5208f4ab34f178be9425c62ea59fe1eebe671a7dd6c57d30744187b9ae58d7a7f1ebf58468e988565a30ca4417f2a90ea')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|README.md LICENSE Releases ||' -i ${_gemname}.gemspec
  sed -r 's|`git ls-files (.+)`|`find \1 -type f`|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md Releases -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a examples "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
