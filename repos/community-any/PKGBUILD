# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-testrunner
pkgver=5.4.0
pkgrel=1
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface'
         'python-zope-exceptions' 'python-six')
makedepends=('python-setuptools' 'python-zope-interface'
             'python-zope-exceptions' 'python-six')
checkdepends=('python-zope-testing' 'python-zope-testrunner'
              'python-virtualenv')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.testrunner/archive/$pkgver.tar.gz")
sha512sums=('26020291523ac6da6441abe0562a5b21f365a0de7e6fa0c22df1c4f79484a228815cce8553174cc4460f854425f10f94cc0003c02d012911d9c120a74b89c111')

prepare() {
  cp -a zope.testrunner-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.testrunner-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.testrunner-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python setup.py test
  )
}

package() {
  cd zope.testrunner-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
