# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-marshmallow
pkgver=3.7.1
pkgrel=1
pkgdesc="A lightweight library for converting complex datatypes to and from native Python datatypes."
url="https://github.com/marshmallow-code/marshmallow"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-simplejson' 'python-pytz' 'python-dateutil')
source=("https://pypi.io/packages/source/m/marshmallow/marshmallow-$pkgver.tar.gz")
sha512sums=('5012d25b9c18854428e1154d46c6923b1b8b43b4e5b89fb5efe213affdf08b10f8899f4c3ed27ed86da5baff2ee9f78417ec5578091eac092218d578bf79ceda')

build() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py pytest
}

package() {
  cd marshmallow-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
