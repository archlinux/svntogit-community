# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.3
pkgrel=5
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="http://phpldapadmin.sourceforge.net/"
license=('GPL')
depends=('libldap' 'php' 'php-ldap')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=(http://downloads.sourceforge.net/project/phpldapadmin/phpldapadmin-php5/$pkgver/phpldapadmin-$pkgver.tgz
        phpldapadmin-1.2.3-php5_5.patch )
md5sums=('59a49054039ceb852c2da377d8742360'
         '966de02d066d9cb90dadf12a213726d3')

build() {
  cd $srcdir/phpldapadmin-${pkgver}/
  find -name '*.png' -exec optipng -quiet -force -fix {} \;
  # patch from http://pkgs.fedoraproject.org/cgit/phpldapadmin.git/tree/
  patch -p1 -i $srcdir/phpldapadmin-1.2.3-php5_5.patch
}

package() {
  _instdir=$pkgdir/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} $pkgdir/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra $srcdir/phpldapadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example $pkgdir/etc/webapps/phpldapadmin/config.php

  cat >$pkgdir/etc/webapps/phpldapadmin/apache.example.conf <<EOF
	Alias /phpldapadmin "/usr/share/webapps/phpldapadmin"
	<Directory "/usr/share/webapps/phpldapadmin">
		AllowOverride All
		Options FollowSymlinks
		Order allow,deny
		Allow from all
		php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/"
	</Directory>
EOF
}
