# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requests-mock
pkgver=1.9.0
pkgrel=1
pkgdesc="A mock of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/jamielennox/requests-mock"
license=('Apache')
depends=('python-requests' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-mock' 'python-purl' 'python-pytest' 'python-testrepository')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jamielennox/requests-mock/archive/$pkgver.tar.gz")
sha512sums=('b51e2d5568069e219fe446502e6a33ceae6fff80788db213429b44c2c46328ea2d465abad8b329cfd48196c8d06e70d504c9d1a744fcc61087677d3c8040c004')

export PBR_VERSION=$pkgver

build() {
  cd requests-mock-$pkgver
  python setup.py build
}

check() {
  cd requests-mock-$pkgver
  python -m pytest tests/pytest
  python setup.py testr
}

package() {
  cd requests-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
