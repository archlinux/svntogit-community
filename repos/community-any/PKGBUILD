# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cloudflare
pkgver=2.8.15
pkgrel=4
pkgdesc='Python wrapper for the Cloudflare v4 API'
arch=('any')
license=('MIT')
url='https://github.com/cloudflare/python-cloudflare'
depends=('python-requests' 'python-future' 'python-yaml' 'python-jsonlines' 'python-beautifulsoup4')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/cloudflare/python-cloudflare/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6553ce6b4f2c31b4cfc603b821853f486ec682b8bea0c3a3ada8ad7ac0d64a7ab74a4c95ddee09ea977eb9c94169dc41def2cdd335e8d47be40ff5dae55301f3')

build() {
  cd python-cloudflare-$pkgver
  python setup.py build
}

check() {
  cd python-cloudflare-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest tests/test1.py
}

package() {
  cd python-cloudflare-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -dm755 "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/
  mv "$pkgdir"/usr/share/man/man1/cli4.{man,1}

  rm -r "$pkgdir"/usr/lib/python3.10/site-packages/examples
}

# vim:set ts=2 sw=2 et:
