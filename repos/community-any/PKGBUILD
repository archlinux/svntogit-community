# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pyftpdlib
pkgname=('python-pyftpdlib' 'python2-pyftpdlib')
pkgver=1.5.3
pkgrel=1
pkgdesc='Very fast asynchronous FTP server library'
arch=('any')
url='https://github.com/giampaolo/pyftpdlib/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-pyopenssl' 'python2-pyopenssl' 'python-psutil' 'python2-psutil')
source=("https://pypi.io/packages/source/p/pyftpdlib/pyftpdlib-$pkgver.tar.gz")
sha512sums=('f0e56f170fa77e0866cbbead52ad52276ee45b6c3244557bc59cd2bf599934ae4414a684661d4d47f76098b3da19e23c1a037aaa07a6a81f8d1be14b90191517')

prepare() {
  cp -a pyftpdlib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py build

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-pyftpdlib() {
  depends=('python')
  optdepends=('python-pyopenssl: FTPS support'
              'python-psutil: to keep track of FTP server memory usage')

  cd pyftpdlib-$pkgver
  python setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyftpdlib() {
  depends=('python2')
  optdepends=('python2-pyopenssl: FTPS support'
              'python2-psutil: to keep track of FTP server memory usage')

  cd pyftpdlib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/ftpbench{,2}
}
