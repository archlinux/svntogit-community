# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=1.7.9
pkgrel=1
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL')
depends=('python-pyqt5' 'python-pyserial' 'python-beautifulsoup4' 'python-setuptools')
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neutree/COMTool/archive/v$pkgver.tar.gz")
sha512sums=('062dee106daaac54ea81320eced79716ec276e1023c73021f1d555a12332dfd9127373ce20ab101d0f9db74c643a5c16f856eb1e0c264f1541db3e864f5ec3cb')

prepare() {
  cd COMTool-$pkgver
  sed -i "s/'pyqt5>=5',//" setup.py
  sed -e 's|^strDataAssetsDirName = .*|strDataAssetsDirName = "share/COMToolData/assets"|' \
      -e 's|^strDataDirName = .*|strDataDirName = "share/COMToolData"|' \
      -i COMTool/parameters.py
}

build() {
  cd COMTool-$pkgver
  python setup.py build
}

check() {
  cd COMTool-$pkgver
  # Disabled: Tests hang
  # xvfb-run python -m unittest discover
}

package() {
  cd COMTool-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
