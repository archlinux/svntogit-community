# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-manual
pkgver=1.0.6
pkgrel=1
pkgdesc='Deepin User Manual'
arch=('any')
url="https://github.com/linuxdeepin/deepin-manual"
license=('GPL3')
depends=('python-jieba' 'python-pyqt5' 'python-gobject' 'deepin-qml-widgets' 'python-dae')
makedepends=('npm' 'sassc' 'git')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/deepin-manual.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd deepin-manual

  sed -e 's_ln -sf /usr/bin/nodejs ./symdir/node__' \
      -e 's/sass /sassc /' \
      -e 's/--unix-newlines//' \
      -i Makefile
}

build() {
  cd deepin-manual
  make
}

package() {
  cd deepin-manual
  make DESTDIR="$pkgdir" install

  cp -r manual "$pkgdir"/usr/share/dman/dman
  rm -r "$pkgdir"/usr/share/dman/dman-daemon/
  rm "$pkgdir"/etc/xdg/autostart/dman-daemon.desktop
  rmdir "$pkgdir"/etc{/xdg/autostart,/xdg,}
}
