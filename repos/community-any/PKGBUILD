# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wand
pkgver=0.5.5
pkgrel=1
pkgdesc="Ctypes-based simple MagickWand API binding for Python"
url="https://github.com/emcconville/wand"
license=('MIT')
arch=('any')
depends=('imagemagick' 'libxml2' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-memory-profiler' 'python-psutil'
              'ghostscript' 'librsvg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/emcconville/wand/archive/$pkgver.tar.gz")
sha512sums=('b9f51e506cd6256a9b9ec8d52c1ef5cf1428e500f5c28740c07b4188fce2f5164ca7ae23341e40ec51ee93c11afd6655a7f01d361b911984810b242e4b66324d')

build() {
  cd wand-$pkgver
  python setup.py build
}

check() {
  cd wand-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd wand-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm "$pkgdir"/usr/README.rst
}
