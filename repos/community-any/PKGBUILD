# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: JÃ¶rg Thalheim <joerg@higgsboson.tk>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>

pkgbase=python-mpd2
pkgname=('python2-mpd2' 'python-mpd2')
pkgver=1.0.0
pkgrel=2
pkgdesc='Python library which provides a client interface for the Music Player Daemon'
url='https://github.com/Mic92/python-mpd2'
license=('GPL3')
makedepends=('python2-setuptools' 'python-setuptools')
arch=('any')
source=("python-mpd2-${pkgver}.tar.gz::https://github.com/Mic92/python-mpd2/archive/v${pkgver}.tar.gz")
sha256sums=('877fa1685a56815107cb0b1eb004e7fe261b620fafd4d38cdaa2b6f3edd35928')

prepare() {
  cp -r python{,2}-mpd2-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-mpd2-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-mpd2() {
  depends=('python')

  cd python-mpd2-${pkgver}

  python setup.py install --root="$pkgdir/" --optimize='1' --skip-build
}

package_python2-mpd2() {
  depends=('python2')

  cd python2-mpd2-${pkgver}

  python2 setup.py install --root="$pkgdir/" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
