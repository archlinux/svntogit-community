# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christos Nouskas nous <nous at archlinux.us>

pkgname=smokeping
pkgver=2.7.2
pkgrel=1
pkgdesc="A tool to keep track of your network latency"
arch=('any')
url="https://oss.oetiker.ch/smokeping/"
license=('GPL2')
depends=('perl-config-grammar' 'perl-digest-hmac' 'perl-libwww' 'rrdtool' 'perl-fcgi' 'perl-cgi-fast'
         'perl-io-tty' 'perl-net-dns' 'perl-net-snmp' 'perl-net-telnet' 'perl-ldap' 'perl-net-openssh')
source=(https://oss.oetiker.ch/smokeping/pub/smokeping-${pkgver}.tar.gz
        smokeping.sysusers
        smokeping.tmpfiles
        service)
sha256sums=('5277fb88eed3c79c8ed22ca830e099727b0e612e887b64bbfeaa98d8d75d493b'
            'ab3b399f58118fc31fac5f627814bbb3b1d7752dd3b4fed2d6b7ee58f24fbd2e'
            'de0042cd54c4bd3329604939a6c3f4fd2034be466aed4421a7467493c024e1a1'
            '4691650aed6c7341bf3a898f63c8ad03f2453d4ce9874e83f24ced5ee21317d9')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc/smokeping --with-htdocs-dir=/srv/http/smokeping
  touch thirdparty/touch thirdparty/CPAN/touch
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}

  # We package CPAN modules separately
  rm -rf thirdparty/lib/perl5

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/smokeping.service"
  install -Dm644 "$srcdir/smokeping.sysusers" "$pkgdir/usr/lib/sysusers.d/smokeping.conf"
  install -Dm644 "$srcdir/smokeping.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/smokeping.conf"
  chown -R 181:181 "$pkgdir"/{etc/smokeping,srv/http/smokeping}
  chmod 700 "$pkgdir/etc/smokeping"
}
