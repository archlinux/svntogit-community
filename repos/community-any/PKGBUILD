# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Aleksandar Trifunović <akstrfn at gmail dot com>
# Contributor: Matt Spaulding <matt at mattops dot io>
# Contributor: pyjano <pyjano at protonmail dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Adrià Arrufat <swiftscythe@gmail.com>

pkgname=ttf-ibm-plex
pkgver=5.2.1
pkgrel=2
pkgdesc="IBM Plex Mono, Sans, and Serif"
arch=('any')
url='https://github.com/IBM/plex'
license=('custom:OFL')
source=("${url}/releases/download/v${pkgver}/TrueType.zip")
provides=('ttf-font')
sha256sums=('6ee1073ce3568de2b537a44c556f436020eea42b05864be0c4ed2e9c81ba099c')

conflicts=('ibm-plex-fonts')

package() {
    cd TrueType
    find -name *.ttf ! -path "*/unhinted/*" \
         -exec install -Dm644 {} -t "${pkgdir}/usr/share/fonts/TTF" \;

    # All the other licenses are the same as the one here...
    install -Dm644 IBM-Plex-Mono/license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
