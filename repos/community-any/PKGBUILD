# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=1.29.0
pkgrel=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-setuptools'
         'python-cachetools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses' 'python-pyopenssl' 'python-pyu2f')
source=("https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('eb005ac505cd1b735f0051a02de0129a52082432976cb5e11d181277a8177e98e5bd6e11646a59e6795331b16b1034e7c157043b8e1ddb8520917a1d0faefcab')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         -e 's|import certifi|pass|' \
         google-auth-library-python-$pkgver/google/auth/transport/{urllib3,requests}.py
}

build() {
  cd google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd google-auth-library-python-$pkgver
  python -m pytest tests --deselect tests/transport/test_urllib3.py::test__make_default_http_without_certifi
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
