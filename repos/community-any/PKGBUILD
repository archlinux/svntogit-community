# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=perl-anyevent-i3
_cpanname=AnyEvent-I3
pkgver=0.15
pkgrel=1
pkgdesc="communicate with the i3 window manager"
arch=('any')
url="http://search.cpan.org/~mstplbg/${_cpanname}/"
license=('GPL')
depends=('perl>=5.10.0' 'perl-anyevent')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSTPLBG/${_cpanname}-${pkgver}.tar.gz")
md5sums=('a282c832cee131e3c53dc5891a99a149')

build() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
