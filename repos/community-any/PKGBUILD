# Maintainer:  Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgname='python-cherrypy'
_pkgname="${pkgname#python-}"
pkgver=18.6.0
pkgrel=4
pkgdesc='A pythonic, object-oriented web development framework'
arch=('any')
url='https://cherrypy.org'
license=('BSD')
depends=('python' 'python-six' 'python-cheroot' 'python-portend' 'python-jaraco'
  'python-more-itertools' 'python-zc.lockfile' 'python-tempora' 'python-pytz')
makedepends=('python-setuptools' 'python-setuptools-scm' 'python-six' 'python-jaraco'
  'python-cheroot' 'python-portend' 'python-more-itertools' 'python-zc.lockfile'
  'python-pytz' 'python-tempora')
checkdepends=('python-pytest' 'python-pytest-sugar' 'python-pytest-services'
              'python-objgraph' 'python-path' 'python-requests' 'python-zc.lockfile'
              'python-requests-toolbelt' 'python-coverage' 'python-webtest')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v$pkgver.tar.gz"
  'disable-sessiontest-file_concurrency.patch'
  'disable-broken-testCombinedTools.patch' # https://github.com/cherrypy/cherrypy/issues/1849
  'disable-broken-ServerStateTests.test_2_KeyboardInterrupt.patch' # https://github.com/cherrypy/cherrypy/issues/1873
)
sha512sums=('d39e9529e4872437c6d94609561820f4946f00c8ded5aa857facbd2c48e0bafb5a4b70368ad72899dad12593a4f8ea86003c1da62c763d6de0181ef24573c465'
            'e8abb7e3f8a064a0da529fcaf393dff305541bc273b539f3129b119d506be3be8cd67894dd4223e067b4dd8b55df8adc5d4feddb8d87188c9e528670c39e252f'
            '8160f7a0e43042c233af22712641724bbaff63d603b31eb3f058c810d2b8f1ca0f5a4666464e4492457bb665ebe0eb5da55bc7e8158e36968f5ccb08ad641733'
            '60ba651c113ecf32e865a40b9361dbe176200274f2b3f40f6d17cbea3d4a868dc2eaf6088906230417c423969ce9692c10ae1f2e0f045571e5c443b53d4e07ca')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      echo "Applying patch ${filename##*/}"
      patch -p1 -N -i "$srcdir/${filename##*/}"
    fi
  done
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  export WEBTEST_INTERACTIVE=0
  rm -f ./{,cherrypy/}{pytest.ini,tox.ini}
  py.test -vvv -s -ra --showlocals --noconftest cherrypy/test/
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
