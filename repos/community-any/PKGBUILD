# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.4.0
pkgrel=1
pkgdesc='A powerful IMAP/Maildir synchronization tool'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2')
makedepends=('docutils')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/spaetz/$pkgname/tarball/v$pkgver)
sha256sums=('9c24745d9cc64b6ce19a0c985dd553e6acdaaa16c76807615a1382b7cbe678f2')

package() {
  cd ${srcdir}/spaetz-${pkgname}-*

  python2 setup.py build install --root=${pkgdir}

#man page
  sed -i 's/\^/=/g' docs/MANUAL.rst
  rst2man docs/MANUAL.rst $pkgname.1
  install -D -m644 $pkgname.1 ${pkgdir}/usr/share/man/man1/$pkgname.1

#config file examples
  install -D -m644 $pkgname.conf.minimal ${pkgdir}/usr/share/$pkgname/$pkgname.conf.minimal
  install -D -m644 $pkgname.conf ${pkgdir}/usr/share/$pkgname/$pkgname.conf
}
