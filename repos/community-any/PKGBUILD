# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=15.0.1
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("https://github.com/jaraco/path/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ba2909aa6d8e4f25c14831a28eff1e5e54427dd3904983b9c1d688a59975e95666d35b269589fbb06c3b95ca7f9b0ad868ddce5037da7427decfae3118628052')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path-$pkgver
  python setup.py build
}

check() {
  cd path-$pkgver
  python -m pytest
}

package() {
  cd path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
