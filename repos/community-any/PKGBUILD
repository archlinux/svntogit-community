# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible
pkgver=1.9.1
pkgrel=2
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='http://www.ansible.com'
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-paramiko' 'python2-jinja')
makedepends=('asciidoc' 'fakeroot')
backup=('etc/ansible/ansible.cfg')
source=(http://releases.ansible.com/ansible/$pkgname-$pkgver.tar.gz
        ansible-arch.patch::https://github.com/ansible/ansible/commit/bb81f025b00a8b22f5002315b4e48543b07d14b1.patch)
md5sums=('3c0f0ecc8b5d60831b10045dba64bbbb'
         '244db3a313781bf39bdeef9fc8898d23')

build() {
  cd $pkgname-$pkgver
  patch -Np1 < $srcdir/ansible-arch.patch
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm644 COPYING "$pkgdir"/usr/share/doc/ansible/COPYING

  install -d "$pkgdir"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "$pkgdir"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "$pkgdir"/etc/ansible/ansible.cfg

  install -d "$pkgdir"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "$pkgdir"/usr/share/man/man1
}
