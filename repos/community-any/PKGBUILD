# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-configargparse
pkgname=('python-configargparse' 'python2-configargparse')
pkgver=0.11.0
pkgrel=2
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/zorro3/ConfigArgParse'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/bw2/ConfigArgParse.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a ConfigArgParse{,-py2}
}

check() {
  cd "$srcdir/ConfigArgParse"
  # https://github.com/bw2/ConfigArgParse/issues/31
  python setup.py test || warning "Tests failed"

  cd "$srcdir/ConfigArgParse-py2"
  python2 setup.py test
}

package_python-configargparse() {
  depends=('python')
  cd ConfigArgParse

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-configargparse() {
  depends=('python2')
  cd ConfigArgParse

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
