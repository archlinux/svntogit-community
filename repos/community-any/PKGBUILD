# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
pkgname=jython
pkgver=2.7.1
pkgrel=1
pkgdesc="An implementation of the Python language written in Java"
arch=('any')
url="http://www.jython.org/"
license=('PSF' 'APACHE' 'custom')
depends=('java-environment' 'bash' 'python')
backup=('opt/jython/registry')
options=('!emptydirs')
install='jython.install'
source=("http://search.maven.org/remotecontent?filepath=org/python/$pkgname-installer/$pkgver/$pkgname-installer-$pkgver.jar"
        'README.ArchLinux'
        'jython.sh')

noextract=(${pkgname}_installer-${pkgver}.jar)

sha512sums=('11cf3d1227af445fc0944c62a47d747c32cc2478dd03ac5ee8a533aac67ae7ec52cde207a4bb845a6d0284001271e6d82b3722c809c2141384b747ae5a11a2aa'
            '0ab0237d456f859b2cbb0739264bb978ad98b2c52153e89fc131aa60edf7837f41617c0e53cbde05688dfc0d6fb08f1d02ebbd41da99b07af72e34a11a83f0b9'
            'c1aaf79f1cfba165fd4a03be016ac8afcc5024972a9626060f57ced0bf4dad93ccdd9ac303bdc8a5ed6b9bcb3307d152dfff2682308c740b6d2db44c2cbaf1b4')

package() {
    cd "$srcdir"
    java -jar jython-installer-${pkgver}.jar -s -t standard -d "$pkgdir"/opt/"$pkgname"

    install -m644 -D $srcdir/jython.sh $pkgdir/etc/profile.d/jython.sh
    install -m644 -D "$srcdir/README.ArchLinux" "$pkgdir/usr/share/doc/$pkgname/README.ArchLinux"

    install -m644 -D "$pkgdir/opt/$pkgname/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
