# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.19.0
pkgrel=1
pkgdesc="Python api for tmux"
arch=(any)
url="https://libtmux.git-pull.com/"
license=(MIT)
depends=(python python-typing-extensions tmux)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-pytest procps-ng)
optdepends=('python-pytest: for pytest plugin')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('f39fd3adeb0ac8f12fd3ce4d8cbca100995dc22a20c5d6f7571ca6bb2faeff1f30a478b59fd3cb4e4728ddd90a3903d38f7b0426d075faa199d4c3d93bea1fea')
b2sums=('538c600db31ac1a8c7bb412784a6834715d726d1ea262394cd8e199261e1c3588bf64255170cbfc08431ade0c5730b36ab22b2edc317dbae74b3359e749986de')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages"
  pytest -vv tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
