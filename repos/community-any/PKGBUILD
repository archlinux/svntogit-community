# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mock
pkgver=3.6.1
pkgrel=3
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
depends=('python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-asyncio')
source=("https://github.com/pytest-dev/pytest-mock/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0a227799af85e4703bef32cf7922573a69ec84fbe35b502b5b96a660a8baa42b9fb1ff8d93115461d23af0c1b18c45cdc67e4695a31713c6553ffc73053448ad')

build() {
  cd pytest-mock-$pkgver
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
  python setup.py build
}

check() {
  cd pytest-mock-$pkgver
  python setup.py egg_info
  export PYTHONPATH="src:${PYTHONPATH}"
  # disable all tests that require the plugin to be installed
  pytest -v --assert=plain \
    -k "not test_used_with_session_scope \
        and not test_used_with_package_scope \
        and not test_used_with_module_scope \
        and not test_used_with_class_scope \
        and not test_monkeypatch_ini \
        and not test_monkeypatch_native \
        and not test_standalone_mock \
        and not test_plain_stopall"
}

package() {
  cd pytest-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
