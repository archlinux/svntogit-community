# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# ThomasV PGP key: gpg --recv-key 6694D8DE7BE8EE5631BED9502BD5824B7F9470E6
# http://bitcoin-otc.com/viewgpg.php?nick=ThomasV

pkgname=electrum
pkgver=1.9.8
pkgrel=2
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('python2' 'python2-ecdsa' 'python2-pbkdf2' 'python2-pyqt4' 'python2-sip' 'python2-slowaes' 'qt4' 'sip')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools')
optdepends=('desktop-file-utils: update desktop database'
            'python2-zbar: QR code reading support')
url="https://electrum.org"
license=('GPL3')
source=("https://download.electrum.org/${pkgname^}-${pkgver}.tar.gz"{,.asc}
        setup.py.patch)
md5sums=('e3918fec0254267f08e41a1fb8691382'
         '4fdd91fe50c4718067b5f22a866ceb53'
         '562f61b12e8bc80de23fb56b8ce56fe7')
sha256sums=('8fc144a32013e4a747fea27fff981762a6b9e14cde9ffb405c4c721975d846ff'
            '181b88ceecd8701844d79110d31fe71b16d943c47612989f3c51e19270f2a87b'
            '5f8d846dedce20601f3131e687bd2dc54ca8df0137cbdaa1b06eab11eb6a52da')
provides=('electrum')
install=electrum.install

prepare() {
  cd ${srcdir}/${pkgname^}-${pkgver}

  msg 'Patching setup.py...'
  patch -p1 < ${srcdir}/setup.py.patch

  msg 'Fixing Python version...'
  find . -type f -exec sed -i 's#/usr/bin/python#/usr/bin/python2#g' {} +
  find . -type f -exec sed -i 's#/usr/bin/env python#/usr/bin/env python2#g' {} +
}

build() {
  cd ${srcdir}/${pkgname^}-${pkgver}

  msg 'Building...'
  python2 mki18n.py
  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${srcdir}/${pkgname^}-${pkgver}

  msg 'Installing...'
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
