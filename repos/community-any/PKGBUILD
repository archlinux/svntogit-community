# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego Principe <cdprincipe@gmail.com>
# Contributor: paterbrown <paterbrown@silberhelme.de>
# Contributor: Josip <josipponjavic@gmail.com>

pkgname=elementary-icon-theme
pkgver=4.3.1
pkgrel=1
pkgdesc='Named, vector icons for elementary OS'
arch=('any')
url='https://github.com/elementary/icons'
license=('GPL3')
groups=('pantheon')
depends=('hicolor-icon-theme')
makedepends=('cmake')
source=("elementary-icon-theme-${pkgver}.tar.gz::https://github.com/elementary/icons/archive/${pkgver}.tar.gz")
sha256sums=('2fcb35760aa3285a17ecd5ef127fe5384a20cb6e86c2b4cd9ed7942063a70a10')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../icons-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
