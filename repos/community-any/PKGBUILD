# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yq
pkgver=3.1.1
pkgrel=1
pkgdesc="Command-line YAML, XML, TOML processor - jq wrapper for YAML/XML/TOML documents"
arch=('any')
url="https://github.com/kislyuk/yq"
license=('Apache')
depends=('jq'
         'python-argcomplete'
         'python-toml'
         'python-xmltodict'
         'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
conflicts=('go-yq')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('853f342b1562ddee979038ec8ecee5405cdd9b6a7cb81ed6cdb8018c9e802633')
b2sums=('0f8ed3d23e1c1b65fae0164f34b19ab774308ee8ec9b5c21547c5eddf930c49750cdbee858b6b48bcd92d4146715f7e4c45dd976141dc5837c788e4ae68a51bc')

build() {
  cd $pkgname-$pkgver
  python -m build -wn
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
