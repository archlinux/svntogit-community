# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyterlab_widgets
pkgname=${_pipname/_/-}
pkgver=3.0.2
pkgrel=1
pkgdesc='JupyterLab extension to use ipywidgets'
arch=(any)
url='https://jupyter.org/'
license=(custom)
depends=(jupyterlab python-ipywidgets)
makedepends=(python-build python-installer python-setuptools python-jupyter_packaging)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('47ab54cd165aa0cb3bcef1232d77471580cd2c36bbe2153fc5ba31e26ad87320')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
