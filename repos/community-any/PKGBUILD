# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-strict-rfc3339
pkgname=(python-strict-rfc3339 python2-strict-rfc3339)
pkgver=0.7
pkgrel=2
pkgdesc="Strict, simple, lightweight RFC3339 functions"
url="http://www.danielrichman.co.uk/libraries/strict-rfc3339.html"
license=('GPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/s/strict-rfc3339/strict-rfc3339-$pkgver.tar.gz")
sha512sums=('94a2992a9ea41b75527348f0f8dae1d118d57b0764122f874baa85f5e3b523beb219ed83c2d7224725e504cd3737a6bde485957135856dcde470f32675e44e77')

prepare() {
  cp -a strict-rfc3339-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/strict-rfc3339-$pkgver
  python setup.py build

  cd "$srcdir"/strict-rfc3339-$pkgver-py2
  python2 setup.py build
}

package_python-strict-rfc3339() {
  depends=('python')

  cd strict-rfc3339-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-strict-rfc3339() {
  depends=('python2')

  cd strict-rfc3339-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
