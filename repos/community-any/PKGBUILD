# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=KiKit
pkgname=python-kikit
pkgver=0.7
pkgrel=2
pkgdesc='Automation tools for KiCAD'
arch=('any')
url='https://github.com/yaqwsx/KiKit'
license=('MIT')
depends=('python' 'kicad' 'python-numpy' 'python-shapely' 'python-click' 'python-markdown2'
         'python-pybars3' 'python-solidpython')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('38c243f2c8e65a8d961f292558c0eaab9724c8cd0a8af5f5cf569d91b3e9d41d1f94406f0855cc21325676b95a7a967db9d5deac2492ca9d119a166d4fa08f9d')

prepare() {
  sed -i '/versioneer/d' $_pkgname-$pkgver/setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

# vim:set ts=2 sw=2 et:
