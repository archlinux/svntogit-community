# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jrnl
pkgver=3.1
pkgrel=1
pkgdesc="Collect your thoughts and notes without leaving the command line"
arch=('any')
url="https://jrnl.sh/"
license=('GPL3')
depends=('python-ansiwrap' 'python-colorama' 'python-cryptography' 'python-keyring'
         'python-parsedatetime' 'python-dateutil' 'python-pyxdg' 'python-rich' 'python-ruamel-yaml'
         'python-tzlocal')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-bdd' 'python-toml')
source=("https://github.com/jrnl-org/jrnl/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3701680e0add29e81b895896b75168799e0815a081f87d67475bff9de4bb6dc99a6e445ebc60f4dc271a607da7054fa74dc0f2b6d1f94c070fcde720d0a1bcd8')

prepare() {
  cd jrnl-$pkgver
  # https://github.com/pytest-dev/pytest-bdd/issues/501
  sed -i '/-n=auto/d' pyproject.toml
}

build() {
  cd jrnl-$pkgver
  python -m build -nw
}

check() {
  cd jrnl-$pkgver
  python -m pytest
}

package() {
  cd jrnl-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
