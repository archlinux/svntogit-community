# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-swiftclient
pkgver=4.3.0
_commit=236c2775c6b8f1c79444899726ca4c9b87fbb47e
pkgrel=2
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://docs.openstack.org/python-swiftclient"
license=('Apache')
depends=('python-six' 'python-requests')
optdepends=('python-keystoneclient: for Auth versions 2.0 and 3 support')
makedepends=('git' 'python-setuptools' 'python-pbr')
checkdepends=('python-stestr' 'python-keystoneauth1' 'python-keystoneclient')
source=("git+https://github.com/openstack/python-swiftclient.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd python-swiftclient
  python setup.py build
}

check() {
  cd python-swiftclient
  stestr run
}

package() {
  cd python-swiftclient
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
