# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Martchus <martchus@gmx.net>
# Contributor: Nuno Araujo <nuno.araujo@russo79.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jason Giangrande <jgiangrande@clarku.edu>

_pkgname=pyusb
pkgbase=python-pyusb
pkgname=('python-pyusb' 'python2-pyusb')
pkgdesc="USB access on Python"
pkgver=1.0.2
pkgrel=2
arch=('any')
url="https://github.com/pyusb/pyusb"
license=('custom')
makedepends=('python-distribute' 'python2-distribute')
#source=("${pkgname}-${pkgver}.tar.gz::https://github.com/walac/${_pkgname}/archive/${pkgver}.tar.gz")
source=("${_pkgname}-${pkgver}.tar.gz::https://pypi.org/packages/source/p/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "${_pkgname}-${pkgver}.tar.gz.asc::https://pypi.org/packages/source/p/${_pkgname}/${_pkgname}-${pkgver}.tar.gz.asc")
sha512sums=('218d118c8572ed548d2317725a9717c0a442eb67c05148ccd90505a407bd8821a1519f511abc14f96b8e09d914f5a35f57cfabd57c47cb9b3fcd8b61947375a2'
            'SKIP')
validpgpkeys=('B04841AE800C1BF01FE1BC3D084C5584542E1574') # Wander Lairson Costa <wcosta@mozilla.com>

prepare() {
    # Create a copy for the python2 package
    cp -r "${_pkgname}-${pkgver}" "python2-${_pkgname}-${pkgver}"
}

package_python-pyusb() {
    pkgdesc="USB access on Python 3"
    depends=('python' 'libusb')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pyusb() {
    pkgdesc="USB access on Python 2"
    depends=('python2' 'libusb')
    replaces=('python2-pyusb-beta')

    cd "${srcdir}/python2-${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
