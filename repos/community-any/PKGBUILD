# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=3.4.0
pkgrel=2
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-filelock')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-pytest-pylint' 'python-responses'
              'python-isort')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('6cd6b079c35af62e4f8da67c687630471d1a57ebfab7d982abe411b03fb6673dd238a1f676de5624d13bd2eb42b91ae0a97708e33579217916d88bca4db218db')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  python -m pytest --ignore build
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set sw=2 et:
