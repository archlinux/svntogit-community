# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>

pkgname=catfish
pkgver=1.2.2
pkgrel=2
pkgdesc='Versatile file searching tool'
arch=('any')
url='https://launchpad.net/catfish-search'
license=('GPL')
install="$pkgname.install"
depends=('gtk3' 'python2-gobject' 'python2-xdg' 'xdg-utils' 'python2-pexpect')
makedepends=('intltool')
optdepends=('zeitgeist: integration with zeitgeist'
            'findutils: filename search'
            'mlocate: filename search'
            'tracker: fulltext search'
            'strigi: fulltext search'
            'pinot: fulltext search')
source=("https://launchpad.net/catfish-search/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.bz2"
        'fixes.patch')
sha256sums=('0e92844e761207b3d3086c45b26fccf83f0ebced741e1af33d8590779f25eedf'
            'cc2f9bf1e964e1f41af19d1f0081189568229765df365fc1103c30aab5448b5f')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i '1s|python|python2|' {bin,catfish,catfish_lib}/*.py build.py
  sed -i 's|ln -sf $(PREFIX)/share|ln -sf ..|' Makefile.in.in
  patch -p1 -i "$srcdir/fixes.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --python=python2
  make
}

package () {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
  install -d "$pkgdir"/usr/share/pixmaps
  ln -s "/usr/share/icons/hicolor/scalable/apps/$pkgname.svg" \
    "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}

# vim:set ts=2 sw=2 et:
