# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cmd2
pkgver=1.3.2
pkgrel=1
pkgdesc="A tool for building interactive command line apps"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
depends=('python-attrs' 'python-colorama' 'python-pyperclip' 'python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-pytest-mock' 'vi')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('04adfec7e56c967c250cb59b17d556e822dcdb8a35fb44f03a3860b2bf623703a1ff9b853486fc4512c808d06eb77523e254af01728e296c8b29819c827a5117')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd cmd2-$pkgver
  python setup.py build
}

check() {
  cd cmd2-$pkgver
  python setup.py pytest
}

package() {
  cd cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
