# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslo-serialization
pkgname=(python-oslo-serialization python2-oslo-serialization)
pkgver=2.6.0
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=('any')
url="http://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel' 'python-msgpack' 'python2-msgpack'
             'python-oslo-utils' 'python2-oslo-utils' 'python-pytz' 'python2-pytz')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("git+https://git.openstack.org/openstack/oslo.serialization#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a oslo.serialization{,-py2}
}

build() {
  cd "$srcdir"/oslo.serialization
  python setup.py build

  cd "$srcdir"/oslo.serialization-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.serialization
  python setup.py testr

  cd "$srcdir"/oslo.serialization-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-serialization() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
           'python-pytz')

  cd "$srcdir"/oslo.serialization
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-serialization() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-msgpack' 'python2-oslo-utils'
           'python2-pytz')

  cd "$srcdir"/oslo.serialization-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
