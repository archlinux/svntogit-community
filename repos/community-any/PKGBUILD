# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pzl <alsoelp _at_ gmail>

pkgname=perl-devel-checklib
pkgver=1.13
pkgrel=2
pkgdesc="Module to check if other perl modules are installed"
arch=('any')
url='http://search.cpan.org/dist/Devel-CheckLib/'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-io-captureoutput')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MA/MATTN/Devel-CheckLib-${pkgver}.tar.gz")
sha512sums=('2952b0ba75cc0099182aeef533749dd044518ae3df368956caa5d7d860f94658b8eadc1b1f8fabf2ba2dc3e0a3e05498f077e4cb6469609e57272b55a026cc46')

build() {
  cd Devel-CheckLib-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Devel-CheckLib-$pkgver
  make test
}

package() {
  cd Devel-CheckLib-$pkgver
  make install DESTDIR="$pkgdir"
}
