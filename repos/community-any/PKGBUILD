# Maintainer:  Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgname='python2-cherrypy'
_pkgname="${pkgname#python2-}"
pkgver=17.4.1
pkgrel=1
pkgdesc='A pythonic, object-oriented web development framework'
arch=('any')
url='https://cherrypy.org'
license=('BSD')
depends=('python2' 'python2-six' 'python2-cheroot' 'python2-more-itertools'
  'python2-portend' 'python2-jaraco' 'python2-contextlib2' 'python2-zc.lockfile')
makedepends=('python2-setuptools' 'python2-setuptools-scm' 'python2-six'
  'python2-cheroot' 'python2-portend' 'python2-more-itertools'
  'python2-contextlib2' 'python2-zc.lockfile')
checkdepends=('python2-tox')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('4844cbdd73b05d5bf7625e28360d7f210a8c96674605598023931ad9189fbd04e9799e2f30389b9bfdfc8f8459a9b70827884c488aad2a1b062831265aa5a121')

prepare() {
  # fix python interpreter for python2 stuff
  find "${srcdir}/${_pkgname}-${pkgver}" -type f -name '*.py' \
    -exec sed -i 's|^#!/usr/bin/python$|#!/usr/bin/env python2|1' {} +
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  tox2
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python2 ./setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}/usr/bin/cherryd" "${pkgdir}/usr/bin/cherryd2"

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
