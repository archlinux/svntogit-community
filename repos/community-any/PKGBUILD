# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.10.2
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=(any)
url="https://github.com/python/importlib_resources"
license=(Apache)
depends=(python-zipp)
makedepends=(python-build python-installer python-setuptools-scm python-toml python-wheel)
checkdepends=(python-pytest python-tests)
provides=(python-importlib-resources)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('c4906719ac0e16a9e2d0c65bb0ff2e9344c3ddedb59d36f7455fa417bdd590967afd53006a3d1237ed9a9cc24d7601619b222de2fcabc66225cf47e43ad8757b')
b2sums=('41395d35a2413db141cee7b83e1ab7f7a492deea644c58aab1abdc6e4f6ca39b329bc9e902089b1b16a90e3654313e7a7b1e36e7487d2e9c6e2785b029df128f')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  # remove tests
  rm -frv "$pkgdir/$_site_packages/$_name/tests/"
}
