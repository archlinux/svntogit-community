# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=5.0.0
pkgrel=1
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-debtcollector')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest')
source=("https://github.com/openstack/oslo.context/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2f144f8956dd6341f9dc534de42a2a93d858443c8e6824538439c7911b1aa784ab52eaf013bce9757111d688ee92d9dad589b2829669a0331c1718855a74aa1f')

export PBR_VERSION=$pkgver

build() {
  cd oslo.context-$pkgver
  python setup.py build
}

check() {
  cd oslo.context-$pkgver
  stestr run
}

package() {
  cd oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
