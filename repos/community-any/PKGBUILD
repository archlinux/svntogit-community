# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-waitress
pkgname=('python-waitress' 'python2-waitress')
pkgver=1.4.4
pkgrel=1
pkgdesc="Waitress WSGI server"
arch=('any')
license=('ZPL')
url="https://github.com/Pylons/waitress"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/waitress/archive/v$pkgver.tar.gz")
sha512sums=('774fd5c436238a360d116b8683e5be6a4d9e09fcbe3853dea7e803e2c5f309f1d955b4eb9b5fe8c9ec9b8e5363a817e5bc57ca20ba5e5046744338b555dc439a')

prepare() {
  cp -a waitress-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/waitress-$pkgver
  python setup.py build

  cd "$srcdir"/waitress-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/waitress-$pkgver
  python setup.py pytest

  cd "$srcdir"/waitress-$pkgver-py2
  python2 setup.py pytest
}

package_python-waitress() {
  depends=('python-setuptools')

  cd waitress-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-waitress() {
  depends=('python2-setuptools')

  cd waitress-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/waitress-serve{,2}
}
