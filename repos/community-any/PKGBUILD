# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgname=python-pint
pkgver=0.18
pkgrel=1
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="https://pint.readthedocs.org"
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-subtests' 'python-numpy' 'python-uncertainties')
source=("https://pypi.io/packages/source/P/Pint/Pint-$pkgver.tar.gz")
sha512sums=('46d1bb2e7e26316dd4ba077f95a57ba43d87cf1622ad6864029f2a7f7ab345925cc3cc1da674d5c21b41ec1c4c19bedff427740b7df22e7d7711b8c19f2fdbd3')

prepare() {
  cd Pint-$pkgver
  sed -i 's/pkg_resources.extern.//' pint/quantity.py
}

check() {
  cd Pint-$pkgver
  pytest
}

package() {
  cd Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
