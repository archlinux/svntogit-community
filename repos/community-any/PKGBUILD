# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-debtcollector
pkgver=2.1.0
pkgrel=1
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='https://docs.openstack.org/developer/debtcollector'
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/debtcollector/archive/$pkgver.tar.gz")
sha512sums=('5cb434a1c50f6703348525e4daae9aec3d0306b4a358b58fc5aa2a01648b07d602f1d305cd1e299d94220d054e6fbe9286adeb93d1b258b801f8335187798b61')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py build
}

check() {
  cd debtcollector-$pkgver
  stestr run
}

package() {
  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
