# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mongomock
pkgname=(python-mongomock python2-mongomock)
pkgver=3.17.0
pkgrel=1
pkgdesc="Fake pymongo stub for testing simple MongoDB-dependent code"
url="https://github.com/mongomock/mongomock"
license=('BSD')
arch=('any')
makedepends=('python-pbr' 'python2-pbr' 'python-sentinels' 'python2-sentinels' 'python-six'
             'python2-six')
checkdepends=('python-nose' 'python2-nose' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mongomock/mongomock/archive/$pkgver.tar.gz")
sha512sums=('8e8da76df649928b5d8f21899613a5c850668aaf675a58ed7fbc5d8769841a26f7ff33e128f31df092078c346a21460b0c254a88b8781409516ed512a1ddb575')

export PBR_VERSION=$pkgver

prepare() {
  cp -a mongomock-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/mongomock-$pkgver
  python setup.py build

  cd "$srcdir"/mongomock-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/mongomock-$pkgver
  python setup.py nosetests

  cd "$srcdir"/mongomock-$pkgver-py2
  python2 setup.py nosetests
}

package_python-mongomock() {
  depends=('python-sentinels' 'python-six')

  cd mongomock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-mongomock() {
  depends=('python2-sentinels' 'python2-six')

  cd mongomock-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
