# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-bottle
pkgver=0.12.24
pkgrel=1
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="https://bottlepy.org"
license=('MIT')
options=(!emptydirs)
depends=('python')
makedepends=('python-setuptools' 'git')
checkdepends=('python-mako' 'python-jinja' 'python-cherrypy' 'python-twisted'
              'python-tornado' 'python-paste' 'python-gevent' 'python-eventlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/defnull/bottle/archive/$pkgver.tar.gz")
sha512sums=('4f0b5629338e2318ea6835e621c67099bdb5c41980e1e29581697d5cdbed04f6234ade8a2fc811cd9dbc929c3095af99c663386ca5d192fa661e10aeea96f03d')

check() {
  cd "$srcdir"/bottle-$pkgver
  # https://github.com/bottlepy/bottle/issues/791
  python test/testall.py || echo "Tests failed"
}

build() {
  cd "$srcdir"/bottle-$pkgver
  python setup.py build
}

package() {
  cd bottle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
