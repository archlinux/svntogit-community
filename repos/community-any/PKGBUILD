# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elementpath
pkgver=2.0.4
pkgrel=3
pkgdesc="XPath 1.0/2.0 parsers and selectors for ElementTree and lxml"
url="https://github.com/sissaschool/elementpath"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-pytest' 'python-xmlschema')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/elementpath/archive/v$pkgver.tar.gz")
sha512sums=('2e2613388e3a49853bd9eb0947bdc989760582f3446fd11ba76567abe3b598349c6a41bfc4c30ec0a4a41d2429d335e562c557ee4bb15ee71f83b80f3992b349')

build() {
  cd elementpath-$pkgver
  python setup.py build
}

check() {
  cd elementpath-$pkgver
  python -m pytest
}

package() {
  cd elementpath-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
