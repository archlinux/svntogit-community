# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbconvert
_name=nbconvert
pkgver=7.2.2
pkgrel=1
pkgdesc="Jupyter Notebook Conversion"
arch=('any')
url="https://pypi.python.org/pypi/nbconvert"
license=('BSD')
depends=(
  'jupyter-nbformat'
  'jupyter-nbclient'
  'jupyterlab_pygments'
  'python-beautifulsoup4'
  'python-bleach'
  'python-defusedxml'
  'python-entrypoints'
  'python-jinja'
  'python-pandocfilters'
  'python-mistune>=2'
  'python-pygments'
  'python-traitlets'
  'python-tinycss2'
  'python-lxml'
)
makedepends=('python-build' 'python-installer' 'python-hatchling' 'python-pyppeteer')
checkdepends=('python-pytest' 'python-ipywidgets')
optdepends=('pandoc: non-html conversion output'
            'python-pyppeteer: for webPDF conversion support')
source=("https://github.com/jupyter/nbconvert/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('2ee3514d31eed16e04e8f18cfa6e74d37f011b803c23fbb7fef5e87dd202ef54')

build() {
  cd nbconvert-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbconvert-$pkgver
  export PYTHONPATH="$PWD" # Make sure system nbconvert is not used
  # disable tests that download chromium and (probably) require a graphical session
  pytest -v -k 'TestWebPDFExporter and not (test_export or test_webpdf_without_pyppeteer)' -W ignore::DeprecationWarning
  pytest -v -k 'TestNbConvertApp and not test_webpdf_with_chromium' -W ignore::DeprecationWarning
  # run all remaining tests
  pytest -v -k 'not TestWebPDFExporter and not TestNbConvertApp' -W ignore::DeprecationWarning
}

package() {
  cd nbconvert-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
