# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=1.2.2
pkgrel=1
pkgdesc="An XML Schema validator and decoder"
url="https://github.com/sissaschool/xmlschema"
license=('MIT')
arch=('any')
depends=('python-elementpath')
makedepends=('python-setuptools')
checkdepends=('python-lxml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/xmlschema/archive/v$pkgver.tar.gz")
sha512sums=('8c728981fd951ac2101fccb8b0567bb5ca16000bb4eb8e38aa5e81bfdb3de67e94dd25d8ee0ac5215a7424bbae408d6fdbd06a136c9afd4e8f73bd6f0b464cab')

prepare() {
  sed -i 's/~=/>=/' xmlschema-$pkgver/setup.py
}

build() {
  cd xmlschema-$pkgver
  python setup.py build
}

check() {
  cd xmlschema-$pkgver
  python -c "import xmlschema.codepoints; xmlschema.codepoints.save_unicode_categories()"
  cp xmlschema/unicode_categories.json build/lib/xmlschema/
  PYTHONPATH="$PWD/build/lib" python -m unittest
}

package() {
  cd xmlschema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
