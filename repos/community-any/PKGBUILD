# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-sniffio
pkgver=1.1.0
pkgrel=3
pkgdesc='Sniff out which async library your code is running under'
arch=(any)
url=https://github.com/python-trio/sniffio
license=(MIT)
depends=(python)
makedepends=(
  git
  python-curio
  python-pytest
  python-setuptools
)
source=(git+https://github.com/python-trio/sniffio.git#tag=v${pkgver})
sha256sums=(SKIP)

build() {
  cd sniffio

  python setup.py build
}

check() {
  cd sniffio

  python setup.py test
}

package() {
  cd sniffio

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-sniffio/
}

# vim: ts=2 sw=2 et:
