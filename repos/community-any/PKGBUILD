# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: liara <liara@archlinux.email>

pkgname=python-shodan
pkgver=1.29.1
pkgrel=1
pkgdesc='The official Python library and CLI for Shodan'
url='https://shodan.readthedocs.io/'
license=('MIT')
arch=('any')
depends=(
  'python'
  'python-click'
  'python-click-plugins'
  'python-colorama'
  'python-xlsxwriter'
  'python-requests'
  'python-setuptools'
)
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-wheel'
)
optdepends=('python-ujson: faster JSON decoding')
_commit='9ccc16ada4761d19ee2079a9334cac3ddbc62415'
source=("$pkgname::git+https://github.com/achillean/shodan-python.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  # https://github.com/achillean/shodan-python/issues/185
  # git describe --tags | sed 's/^v//'

  grep 'version=' setup.py | sed -e "s/^    version='\(.*\)',/\1/"
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl

  # symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "$site_packages/${pkgname#python-}-$pkgver.dist-info/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
