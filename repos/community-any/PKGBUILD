# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=15.10.0
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('flattr-icon-theme')
makedepends=('git' 'inkscape')
source=("git+https://github.com/linuxdeepin/deepin-icon-theme.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"
  sed 's\flattr\Flattr\' -i deepin/index.theme
  sed 's\Faenza\Adwaita\' -i deepin/index.theme

  mkdir -p build
}

build() {
  cd "${pkgname}"
  python2 tools/convert.py deepin build
}

package() {
  install -d "${pkgdir}"/usr/share/icons
  cp -r "${pkgname}"/build/deepin "${pkgdir}"/usr/share/icons/Deepin
}
