# $Id: PKGBUILD 97778 2013-09-29 16:22:13Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgbase=python-html2text
pkgname=('python-html2text' 'python2-html2text')
pkgver=2016.5.29
pkgrel=1
pkgdesc="A HTML to markdown-structured text converter"
arch=('any')
url="https://pypi.python.org/pypi/html2text/"
license=('GPL3')
makedepends=('python-setuptools' 'python2-setuptools')
provides=('html2text')
replaces=('html2text')
source=(https://github.com/Alir3z4/html2text/archive/$pkgver.tar.gz)
sha256sums=('7f59fb29083a7819063b227c24a8dd5e25aeef74b68bf2318a68cc1e3e653add')

prepare() {
  cp -a html2text-$pkgver{,-py2}
}

build() {
  cd "${srcdir}"/html2text-$pkgver
  python setup.py build
  
  cd "${srcdir}"/html2text-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "${srcdir}"/html2text-$pkgver
  PYTHONPATH=$PYTHONPATH:. python setup.py test --verbose

  cd "${srcdir}"/html2text-$pkgver-py2
  PYTHONPATH=$PYTHONPATH:. python2 setup.py test --verbose
}

package_python-html2text() {
  depends=('python-setuptools')

  cd "${srcdir}"/html2text-$pkgver
  python setup.py install --root="${pkgdir}"
}

package_python2-html2text() {
  depends=('python2-setuptools')

  cd "${srcdir}"/html2text-$pkgver-py2
  python2 setup.py install --root="${pkgdir}"
  mv "$pkgdir/usr/bin/html2text"{,2}
}
