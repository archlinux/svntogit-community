# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Blair Bonnett <blair dot bonnett at gmail dot com>

_name=sphinx-copybutton
pkgname=python-$_name
pkgver=0.5.0
pkgrel=1
pkgdesc="Sphinx extension to add a \"copy\" button to code blocks"
arch=('any')
url="https://sphinx-copybutton.readthedocs.io/"
license=('MIT')
depends=('python-sphinx')
makedepends=('python-setuptools')
source=(https://files.pythonhosted.org/packages/source/s/$_name/$_name-$pkgver.tar.gz)
sha256sums=('a0c059daadd03c27ba750da534a92a63e7a36a7736dcf684f26ee346199787f6')

build() {
  cd $_name-$pkgver
  python setup.py build
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim:set ts=2 sw=2 et:
