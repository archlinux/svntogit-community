# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-numpydoc
pkgver=1.5.0
pkgrel=1
pkgdesc="Numpy's Sphinx extensions"
url="https://github.com/numpy/numpydoc"
license=('Apache')
arch=('any')
depends=('python-sphinx' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-matplotlib' 'python-numpy' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/numpy/numpydoc/archive/v$pkgver.tar.gz")
sha512sums=('e87535c74767946a71b3094b6591fcfb4d7e42acd505c28b3a5c00ea19c29bb21806c5cb655b5d3aca670d105654956d9b59c06d8be68cd51a1760b219c1c815')

build() {
  cd numpydoc-$pkgver
  python setup.py build
}

check() {
  cd numpydoc-$pkgver
  python -m pytest
}

package() {
  cd numpydoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
