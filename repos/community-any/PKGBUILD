# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Corrado Primier <cp663137@stud.polimi.it>

pkgname=drupal
pkgver=7.34
pkgrel=2
pkgdesc="A PHP-based content management platform"
arch=('any')
url="http://www.drupal.org/"
license=('GPL')
depends=('php')
install='drupal.install'
options=(!strip)
source=(http://www.drupal.org/files/projects/$pkgname-$pkgver.tar.gz
	https://www.drupal.org/files/issues/1599774-htaccess-apache-2.4-53-D7-backport.patch)
md5sums=('bb4d212e1eb1d7375e41613fbefa04f2'
         'a892343516f50041173d36d1d74d4b04')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/1599774-htaccess-apache-2.4-53-D7-backport.patch
}

package() {
  _instdir=$pkgdir/usr/share/webapps/drupal
  mkdir -p ${_instdir}
  cd ${_instdir}
  cp -r $srcdir/$pkgname-$pkgver/{*,.htaccess} ${_instdir}

  # move .htaccess
  mkdir -p $pkgdir/etc/webapps/drupal
  sed -i '1,1iRequire all denied' ${_instdir}/.htaccess
  mv ${_instdir}/.htaccess $pkgdir/etc/webapps/drupal/.htaccess
  ln -s /etc/webapps/drupal/.htaccess ${_instdir}/.htaccess

  # move sites/
  mkdir -p $pkgdir/var/lib/drupal/
  mv ${_instdir}/sites $pkgdir/var/lib/drupal/sites
  ln -s /var/lib/drupal/sites ${_instdir}/sites

  # apache config example
  cat >$pkgdir/etc/webapps/drupal/apache.example.conf <<EOF
	Alias /drupal "/usr/share/webapps/drupal"
	<Directory "/usr/share/webapps/drupal">
		AllowOverride All
		Options FollowSymlinks
		Require all granted
		php_admin_value open_basedir "/srv/:/tmp/:/usr/share/webapps/:/etc/webapps:/usr/share/pear/:/var/lib/drupal"
	</Directory>
EOF
}
