# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-eventlet
_pyname=eventlet
pkgver=0.29.1
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='https://eventlet.net'
arch=('any')
license=('MIT')
depends=('python' 'python-greenlet' 'python-monotonic' 'python-dnspython')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq')
optdepends=(
  'python-psycopg2: non-blocking PostgreSQL support'
  'python-pyopenssl: non-blocking SSL support'
  'python-httplib2: non-blocking HTTP support'
  'python-pyzmq: non-blocking ZeroMQ support'
  'python-dnspython: non-blocking DNS support'
)
options=('!makeflags')
source=(https://github.com/eventlet/eventlet/archive/v${pkgver}/${_pyname}-${pkgver}.tar.gz
        ${pkgname}-py39.patch::https://github.com/eventlet/eventlet/commit/087ba743c7af.patch)
sha512sums=('fc3563baa0df210fb62b53d821c1013feea4e680feb34958ba6cd37ed1b8967beebfe696754bd07c60345152ed7a291f0a439d2f12bf47d974f7543d3b60c775'
            'a40750ef2ed0eb8e895328bef6474e46a242e7fda6445f4b1a4e3ae309ea06bbcef72fa3610147284499d6b784a0ab838463c5596eda14c7fb6e2d78cff31c5e')
b2sums=('b1ac914276f429050c9e233618360107bd5504a87368355aa3b2effcefe2195ac4c7bfa27fb3d40fc1dbdbe3d85044f9aa5c3dc18bae777965851205f47567f7'
        '4a933aa4346b9460b496c3534c622216fd965bdd83303bbc77cc7c333a752c3a204380bea57ac1e7f3e347c828089c35c981899447195b201bb55f9dcf9d0546')

prepare() {
  cd ${_pyname}-${pkgver}
  patch -Np1 -i ../${pkgname}-py39.patch
  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i tests/*_test.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  make -C doc text
}

check() {
  cd ${_pyname}-${pkgver}
  nosetests -sv tests
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
