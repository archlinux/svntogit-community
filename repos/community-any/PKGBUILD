# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: shadyabhi <abhijeet.1989@gmail.com>

pkgbase=python-google-api-python-client
pkgname=('python2-google-api-python-client' 'python-google-api-python-client')
pkgver=1.7.11
pkgrel=3
pkgdesc="Google API Client Library for Python"
arch=('any')
url="https://github.com/google/google-api-python-client"
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools'
             'python2-google-auth-httplib2' 'python2-uritemplate'
	     'python-google-auth-httplib2' 'python-uritemplate')
source=("https://github.com/google/google-api-python-client/archive/v${pkgver}.tar.gz")
sha256sums=('7a95af326f36ae9b57093513f2770cc49bf59ad92c218c70fe005f2da4987fc9')

prepare() {
  cd "$srcdir"
  cp -a google-api-python-client-${pkgver} google-api-python-client-py2-${pkgver}
  cd google-api-python-client-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  cd "$srcdir/google-api-python-client-$pkgver"

  # "Building Python2"
  cd "$srcdir"/google-api-python-client-py2-${pkgver}
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/google-api-python-client-${pkgver}
  python setup.py build
}
 
package_python2-google-api-python-client() {
  depends=('python2-google-auth-httplib2' 'python2-uritemplate')
  optdepends=('python2-oauth2client: optional backend')
  cd "$srcdir/google-api-python-client-py2-$pkgver"
 
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  # Workaround for FS#47243
  _site_packages=`python2 -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod -R a+r "$pkgdir$_site_packages"
}

package_python-google-api-python-client() {
  depends=('python-google-auth-httplib2' 'python-uritemplate')
  optdepends=('python-oauth2client: optional backend')
  cd "$srcdir/google-api-python-client-$pkgver"
 
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  # Workaround for FS#47243
  _site_packages=`python -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod -R a+r "$pkgdir$_site_packages"
}

