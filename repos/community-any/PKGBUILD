# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=beets
pkgver=1.3.11
pkgrel=2
pkgdesc='Flexible music library manager and tagger'
arch=('any')
url='http://beets.radbox.org/'
license=('MIT')
depends=('mutagen' 'python2-enum34' 'python2-munkres' 'python2-musicbrainzngs'
         'python2-setuptools' 'python2-unidecode' 'python2-yaml')
optdepends=('bash-completion: Bash completion'
            'gst-plugins-bad: ReplayGain plugin'
            'gst-plugins-good: ReplayGain plugin'
            'gst-plugins-ugly: ReplayGain plugin'
            'gst-libav: ReplayGain plugin'
            'gst-python: ReplayGain plugin'
            'gstreamer0.10-bad-plugins: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-good-plugins: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-ugly-plugins: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-ffmpeg: Chromaprint/Acoustid/BPD plugins'
            'gstreamer0.10-python: Chromaprint/Acoustid/BPD plugins'
            'python2-flask: Web plugin'
            'python2-gobject: ReplayGain plugin'
            'python2-pyacoustid: Chromaprint/Acoustid plugin'
            'python2-pylast: LastGenre plugin'
            'python2-requests: FetchArt plugin')
source=("https://github.com/sampsyo/beets/releases/download/v${pkgver}/beets-${pkgver}.tar.gz"
        'https://raw.githubusercontent.com/sampsyo/beets/master/extra/_beet')
sha256sums=('4a926b4c82409382b993b14cac7ecdbd18dea84845030ad41e26198542871408'
            '51c23b07ae5b4d67bd3d9f1c6ec3187a04b18d805e0b198951c6b63c67d34715')

build() {
  cd beets-${pkgver}

  python2 setup.py build
}

package() {
  cd beets-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'

  install -dm 755 "${pkgdir}"/{etc/bash_completion.d,usr/share/zsh/site-functions}
  echo 'eval "$(beet completion)"' > "${pkgdir}"/etc/bash_completion.d/beet
  install -m 644 ../_beet "${pkgdir}"/usr/share/zsh/site-functions/

  install -dm 755 "${pkgdir}"/usr/share/man/man{1,5}
  install -m 644 man/beet.1 "${pkgdir}"/usr/share/man/man1/
  install -m 644 man/beetsconfig.5 "${pkgdir}"/usr/share/man/man5/

  install -dm 755 "${pkgdir}"/usr/share/licenses/beets
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/beets/
}

# vim: ts=2 sw=2 et:
