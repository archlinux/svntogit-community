# Maintainer: Felix Yan <felixonmars@archlinux.org>

_name=hypothesis-auto
pkgname=python-hypothesis-auto
pkgver=1.1.4
_commit=4ed588ab631d6c44c8959334a3425a8c0d207eff
pkgrel=7
pkgdesc="Extends Hypothesis to add fully automatic testing of type annotated functions"
url="https://github.com/timothycrosley/hypothesis-auto"
license=('MIT')
arch=('any')
depends=('python-pydantic' 'python-hypothesis')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core' 'python-wheel')
checkdepends=('python-pytest')
optdepends=('python-pytest: for pytest plugin')
# NOTE: there are no tags: https://github.com/timothycrosley/hypothesis-auto/issues/14
source=(
  "git+$url#commit=$_commit"
  $pkgname-1.1.4-poetry_core.patch
  $pkgname-1.1.4-version_constraints.patch
)
sha512sums=('SKIP'
            'a9d0d6316c884a82368b15ea11d5a16ada570c710507c5a581c48640670ee5667ac538796e4849f53cac22083799d412d7bf63f9cebff7c6dfcb00a57d8064ad'
            'c93e54063d3758f136d085844c4b983c5ba9457eb8affa2b91acfa89ad8e0a11e0aa06e6250ed56fe90cb4851d621f988d31ca04619ce05e9fb3b6fede4c5487')

prepare() {
  # fix broken build-system: https://github.com/timothycrosley/hypothesis-auto/pull/19
  patch -Np1 -d $_name -i ../$pkgname-1.1.4-poetry_core.patch
  # fix broken version constraints: https://github.com/timothycrosley/hypothesis-auto/pull/20
  patch -Np1 -d $_name -i ../$pkgname-1.1.4-version_constraints.patch
}

build() {
  cd $_name
  python -m build --wheel --no-isolation
}

check() {
  cd $_name
  python -m pytest -v
}

package() {
  cd $_name
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
