# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-warlock
pkgver=1.3.3
pkgrel=1
pkgdesc="Python object model built on JSON schema and JSON patch."
arch=('any')
license=('Apache')
url="https://github.com/bcwaldon/warlock"
depends=('python-jsonschema' 'python-jsonpatch' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bcwaldon/warlock/archive/$pkgver.tar.gz")
sha512sums=('8a9135a17ccc0d0939ad98eee16100ec68b911d672b3af549f7629edcd6e08129f9e4ab707082593274fc896bc405ab6d6ba9e4bf6fc40b8522ec99327c983ec')

build() {
  cd warlock-$pkgver
  python setup.py build
}

check() {
  cd warlock-$pkgver
  python setup.py pytest
}

package() {
  cd warlock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
