# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-runner
pkgname=('python-pytest-runner' 'python2-pytest-runner')
pkgver=2.6.2
pkgrel=2
pkgdesc="Invoke py.test as distutils command with dependency resolution"
arch=('any')
license=('MIT')
url="https://bitbucket.org/pytest-dev/pytest-runner"
makedepends=('python-setuptools_scm' 'python2-setuptools_scm' 'mercurial')
checkdepends=('python-pytest' 'python2-pytest')
source=("hg+https://felixonmars@bitbucket.org/pytest-dev/pytest-runner#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pytest-runner{,-py2}
}

build() {
  cd "$srcdir/pytest-runner"
  python setup.py build

  cd "$srcdir/pytest-runner-py2"
  python2 setup.py build
}

check() {(
  cd "$srcdir/pytest-runner"
  py.test

  cd "$srcdir/pytest-runner-py2"
  py.test2
  
  ) || warning "Expected to fail because of https://bitbucket.org/pytest-dev/pytest-runner/issues/6"
}

package_python-pytest-runner() {
  depends=('python-pytest')

  cd pytest-runner
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-pytest-runner() {
  depends=('python2-pytest')

  cd pytest-runner-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

