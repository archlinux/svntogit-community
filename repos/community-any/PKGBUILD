# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: David McInnis <dave@dave3.xyz>
# Contributor:  jyantis <yantis@yantis.net>

pkgname=python-internetarchive
pkgver=2.3.0
pkgrel=1
pkgdesc='Wrapper for the various Internet Archive APIs (IA-S3, Metadata API, etc)'
arch=('any')
url='https://github.com/jjjake/ia-wrapper'
license=('AGPL3')
depends=('python'
         'python-six'
         'python-yaml'
         'python-requests'
         'python-jsonpatch'
         'python-docopt'
         'python-jsonpointer'
         'python-args'
         'python-tqdm'
         'python-schema'
        )
makedepends=('python-setuptools')
optdepends=('python-ujson: faster json parsing'
            'python-gevent: concurrent downloads'
            'cython: speedups')
checkdepends=(python-responses
              python-pytest
             )
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jjjake/internetarchive/archive/v${pkgver}.tar.gz")
sha256sums=('8858599fdf4897124357df1f1a30337185113137590aec75720e85940dd7a7bb')

build() {
  cd internetarchive-${pkgver}
  python setup.py build
}

check() {
  cd internetarchive-${pkgver}

  python setup.py test
}

package() {
  cd internetarchive-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
}

# vim:set ts=2 sw=2 et:
