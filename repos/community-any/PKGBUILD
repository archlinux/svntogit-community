# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-i18n
pkgname=(python-oslo-i18n python2-oslo-i18n)
pkgver=3.25.1
pkgrel=2
pkgdesc="Oslo i18n library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.i18n/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel')
checkdepends=('python-mock' 'python-oslotest' 'python-oslo-config')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.i18n/archive/$pkgver.tar.gz")
sha512sums=('77d1cf755dd6fb26bb38ba648309114f38472ed97a43d777b5a4bb4c665b6336ef579ff71ed8a01aecb24b1cbaf187d671be4f01a0b8480dcc4e3687745689b0')

export PBR_VERSION=$pkgver

prepare() {
  cp -a oslo.i18n-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/oslo.i18n-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  python2 setup.py build
}

check() {
  cd oslo.i18n-$pkgver
  stestr run
}

package_python-oslo-i18n() {
  depends=('python-six' 'python-pbr' 'python-babel')

  cd "$srcdir"/oslo.i18n-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-i18n() {
  depends=('python2-six' 'python2-pbr' 'python2-babel')

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
