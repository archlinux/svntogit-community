# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyhamcrest
pkgver=2.0.1
pkgrel=1
pkgdesc="Hamcrest framework for matcher objects"
arch=('any')
license=('BSD')
url="https://github.com/hamcrest/PyHamcrest"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/hamcrest/PyHamcrest/archive/V$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('29e85e83b1d7ed691f4fd9e21172149cd6bea702d5838208023641e68585b42a48106122574d6238b40cbb40601273e6202e6cf3a91b71f03d80f3dc1c72f94e')

build() {
  cd PyHamcrest-$pkgver
  python setup.py build
}

check() {
  cd PyHamcrest-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd PyHamcrest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
