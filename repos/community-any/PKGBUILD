# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ocrfeeder
pkgver=0.8
pkgrel=1
pkgdesc="GTK+ document layout analysis and optical character recognition application"
arch=('any')
url="https://wiki.gnome.org/Apps/OCRFeeder"
license=('GPL')
depends=('python2-pyenchant' 'python2-gobject' 'python2-lxml' 'python2-pillow'
         'python2-reportlab' 'gtk3' 'goocanvas' 'gtkspell3' 'ghostscript' 'unpaper' 'sane'
         'iso-codes' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils' 'python2-gobject2')
optdepends=('tesseract: character recognition'
            'ocrad: character recognition'
            'gocr: character recognition'
            'cuneiform: character recognition')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('bb2e415e1e7ce9c7d819423b6b6a311f9d476eca54e7b183070ba6cd124265a1')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/ocrfeeder*.in src/ocrfeeder/odf/*.py

  # Fix build (X display is not available in chroot)
  sed -i 's/PYTHON_GI_MODULES=.*/PYTHON_GI_MODULES=/' configure{.ac,}
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-maintainer-mode \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../ocrfeeder/icons/ocrfeeder.svg "$pkgdir/usr/share/pixmaps/ocrfeeder.svg"
}
