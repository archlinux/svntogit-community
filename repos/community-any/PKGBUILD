# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown-netease
pkgver=0.9.4
pkgrel=2
pkgdesc="feeluown netease plugin"
arch=('any')
url="https://github.com/feeluown/feeluown-netease"
license=('GPL3')
depends=('feeluown' 'python-beautifulsoup4' 'python-pycryptodome' 'python-marshmallow'
         'python-requests' 'python-mutagen')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
groups=('feeluown-full')
source=("https://github.com/feeluown/feeluown-netease/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('dff7366a3e173e22897dfc254792584e37c3864fc9d725d5bc55844f0ce0e36471a64ca40957354191a175e10f2ab198f6c25f935a8f189b0f2b9a987983b3fc')

build() {
  cd feeluown-netease-$pkgver
  python setup.py build
}

check() {
  cd feeluown-netease-$pkgver
  pytest
}

package() {
  cd feeluown-netease-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
