# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=8.0.1
pkgrel=3
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
depends=('python')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-typeguard')
source=("https://github.com/jd/tenacity/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('923b40b5edfd9d9ae83f6e3b9030ff4d28b17204b09fb55c6b1049201b4cc6a57a2bd58c6a5ace5a514928fa61da4e0b78d5166f70d0a69e127ec21e1e3e90d0')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tenacity-$pkgver
  python setup.py build
}

check() {
  cd tenacity-$pkgver
  python setup.py pytest
}

package() {
  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
