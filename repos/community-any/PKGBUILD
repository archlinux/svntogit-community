# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-location' 'python2-zope-location')
pkgver=4.0.3
pkgrel=2
pkgdesc="Special objects that have a structural location"
arch=('any')
url="https://github.com/zopefoundation/zope.location"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-schema' 'python2-zope-schema'
             'python-zope-proxy' 'python2-zope-proxy' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-zope-copy' 'python2-zope-copy'
             'python-zope-configuration' 'python2-zope-configuration')
source=("git+https://github.com/zopefoundation/zope.location.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.location{,-py2}
}

build() {
  cd "$srcdir/zope.location"
  python setup.py build

  cd "$srcdir/zope.location-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.location"
  nosetests3

  cd "$srcdir/zope.location-py2"
  nosetests2
}

package_python-zope-location() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema'
           'python-zope-proxy')

  cd "$srcdir/zope.location"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-location() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-schema'
           'python2-zope-proxy')

  cd "$srcdir/zope.location-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
