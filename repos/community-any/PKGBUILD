# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tuxbubling <tuxbubling@jabber.fr>

pkgname=cacti
pkgver=0.8.8_h
pkgrel=1
pkgdesc="complete network graphing solution designed to harness the power of RRDTool's data storage and graphing functionality"
arch=(any)
url="http://www.cacti.net"
license=('GPL')
depends=('rrdtool' 'mariadb' 'php' 'php-gd' 'net-snmp' 'xorg-fonts-100dpi')
backup=('etc/webapps/cacti/.htaccess'
	'etc/webapps/cacti/config.php')
install=cacti.install
source=("http://www.cacti.net/downloads/$pkgname-${pkgver/_/}.tar.gz"
	"cacti-php7.patch"
	"cacti-php7_2.patch")
md5sums=('e5a2eb9e1b187a92284f46e7d4cd6505'
         'f8bc449356b00a5a697522e1e0ca4c54'
         '7c20c6dfee134849ecfa60b5e99b9b9e')

prepare() {
  cd $srcdir/$pkgname-${pkgver/_/}
  patch -p1 -i $srcdir/cacti-php7.patch
  patch -p1 -i $srcdir/cacti-php7_2.patch
}

package() {
  mkdir -p $pkgdir/usr/share/webapps/cacti $pkgdir/etc/webapps/cacti
  mv $srcdir/$pkgname-${pkgver/_/}/* $pkgdir/usr/share/webapps/cacti
  echo "Require all denied" >> $pkgdir/etc/webapps/cacti/.htaccess
  mv $pkgdir/usr/share/webapps/cacti/include/config.php $pkgdir/etc/webapps/cacti
  ln -s /etc/webapps/cacti/.htaccess $pkgdir/usr/share/webapps/cacti/.htaccess
  ln -s /etc/webapps/cacti/config.php $pkgdir/usr/share/webapps/cacti/include/config.php

  install -dm0755 $pkgdir/var/log/ $pkgdir/var/lib/cacti/

  mv $pkgdir/usr/share/webapps/cacti/rra $pkgdir/var/lib/cacti/rra
  ln -s /var/lib/cacti/rra $pkgdir/usr/share/webapps/cacti/rra

  mv $pkgdir/usr/share/webapps/cacti/log $pkgdir/var/log/cacti
  ln -s /var/log/cacti $pkgdir/usr/share/webapps/cacti/log
}
