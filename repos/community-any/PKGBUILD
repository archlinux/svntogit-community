# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requirementslib
pkgver=1.5.4
pkgrel=1
pkgdesc="A tool for converting between pip-style and pipfile requirements"
url="https://github.com/sarugaku/requirementslib"
license=('MIT')
arch=('any')
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-distlib'
         'python-orderedmultidict' 'python-packaging' 'python-pep517' 'python-pip-shims'
         'python-plette' 'python-cerberus' 'python-dateutil' 'python-requests' 'python-setuptools'
         'python-tomlkit' 'python-vistir')
makedepends=('python-invoke' 'python-wheel' 'python-parver' 'git')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-sugar' 'python-pytest-timeout'
              'python-pytest-xdist' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/requirementslib/archive/$pkgver.tar.gz"
        "git+https://github.com/kennethreitz/requests.git#commit=64bde6582d9b49e9345d9b8df16aaa26dc372d13"
        "git+https://github.com/sarugaku/shellingham.git#commit=9abe7464dab5cc362fe08361619d3fb15f2e16ab")
sha512sums=('9da415f4111dc6afccbd74b63d7a4cabf81483ce89414104cc8bf0f512dd3e12b5ac16d786bc061fe3190d77246eb6dca58ee6ab4cc085600014324d07c780fe'
            'SKIP'
            'SKIP')

prepare() {
  cd requirementslib-$pkgver
  sed -i 's/pkg_resources.extern.//' src/requirementslib/models/*.py

  # https://github.com/sarugaku/requirementslib/issues/181
  sed -e 's/list(MARKER_OP)/MARKER_OP.exprs/' -e 's/list(VARIABLE)/VARIABLE.exprs/' -i tests/unit/strategies.py

  # https://github.com/sarugaku/requirementslib/issues/183
  sed -i 's/not isinstance(source\["verify_ssl"\], bool)/isinstance(source["verify_ssl"], str)/' -i src/requirementslib/models/pipfile.py

  cd tests/artifacts/git
  rmdir requests shellingham
  ln -s "$srcdir"/requests
  ln -s "$srcdir"/shellingham
}

build() {
  cd requirementslib-$pkgver
  python setup.py build
}

check() {
  cd requirementslib-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd requirementslib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
