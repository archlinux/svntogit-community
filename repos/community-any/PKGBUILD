# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=MyST-Parser
pkgname=python-myst-parser
pkgver=0.12.10
pkgrel=3
pkgdesc='An extended commonmark compliant parser, with bridges to docutils/sphinx'
arch=('any')
url='https://github.com/executablebooks/MyST-Parser'
license=('MIT')
depends=('python-markdown-it-py' 'python-yaml' 'python-docutils' 'python-sphinx')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-regressions' 'python-beautifulsoup4')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('6f68568741f6359ba0add564318d355a0af62cd1aedcce155da77402756dda83b98bf6300eed33589908e2a233fbcf5095ca5e04e89da80d3f2ead41d15ab0b2')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
