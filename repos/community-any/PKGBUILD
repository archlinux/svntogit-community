# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgname=('python-zope-component' 'python2-zope-component')
pkgver=4.3.0
pkgrel=2
pkgdesc="Represents the core of the Zope Component Architecture"
arch=('any')
url="https://github.com/zopefoundation/zope.component"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-event' 'python2-zope-event'
             'git')
checkdepends=('python-nose' 'python2-nose' 'python-persistent' 'python2-persistent'
              'python-zope-hookable' 'python2-zope-hookable' 'python-zope-location'
              'python2-zope-location' 'python-zope-proxy' 'python2-zope-proxy'
              'python-zope-security' 'python2-zope-security' 'python-zope-configuration'
              'python2-zope-configuration' 'python-zope-i18nmessageid'
              'python2-zope-i18nmessageid')
source=("git+https://github.com/zopefoundation/zope.component.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.component{,-py2}
}

build() {
  cd "$srcdir/zope.component"
  python setup.py build

  cd "$srcdir/zope.component-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.component"
  nosetests3

  cd "$srcdir/zope.component-py2"
  nosetests2
}

package_python-zope-component() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')

  cd "$srcdir/zope.component"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-component() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-event')

  cd "$srcdir/zope.component-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
