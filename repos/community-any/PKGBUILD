# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-exceptions' 'python2-zope-exceptions')
pkgver=4.0.8
pkgrel=2
pkgdesc="Generic exceptions and implementations for Zope"
arch=('any')
url="https://github.com/zopefoundation/zope.exceptions"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/zopefoundation/zope.exceptions.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.exceptions{,-py2}
}

build() {
  cd "$srcdir/zope.exceptions"
  python setup.py build

  cd "$srcdir/zope.exceptions-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.exceptions"
  nosetests3

  cd "$srcdir/zope.exceptions-py2"
  nosetests2
}

package_python-zope-exceptions() {
  depends=('python-setuptools' 'python-zope-interface')

  cd "$srcdir/zope.exceptions"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-exceptions() {
  depends=('python2-setuptools' 'python2-zope-interface')

  cd "$srcdir/zope.exceptions-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
