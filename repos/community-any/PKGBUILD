# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Sven Klomp <mail@klomp.eu>

pkgname=python-gnupg
pkgdesc="A wrapper for the Gnu Privacy Guard (GPG or GnuPG)"
pkgver=0.4.5
pkgrel=2
url="https://pythonhosted.org/python-gnupg/"
license=('BSD')
arch=('any')
depends=('gnupg' 'python')
makedepends=('python-setuptools')
source=("https://pypi.io/packages/source/p/python-gnupg/${pkgname}-${pkgver}.tar.gz"
        "https://pypi.io/packages/source/p/python-gnupg/${pkgname}-${pkgver}.tar.gz.asc")
sha512sums=('441b49e1f87a7e8e3abf2d6e90f677dc33f1b66a36483787f972378127f31308bacc5e200ae254dd75942191d2fabd61ca5a2f200cf5400be70957942f889ca4'
            'SKIP')
validpgpkeys=('CA749061914EAC138E66EADB9147B477339A9B86') # Vinay Sajip (CODE SIGNING KEY) <vinay_sajip@yahoo.co.uk>

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export NO_EXTERNAL_TESTS='true'
  python test_gnupg.py
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
