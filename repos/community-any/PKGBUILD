# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastien LEDUC <sebastien@sleduc.fr>
# Contributor: Andrey Mikhaylenko <neithere at gmail dot com>

pkgbase=python-pytest-cov
pkgname=('python-pytest-cov' 'python2-pytest-cov')
pkgver=2.10.0
pkgrel=1
pkgdesc='py.test plugin for coverage reporting with support for both centralised and distributed testing, including subprocesses and multiprocessing'
arch=('any')
license=('MIT')
url='https://github.com/schlamar/pytest-cov'
makedepends=('python-coverage' 'python2-coverage' 'python-setuptools' 'python2-setuptools'
             'python-pytest' 'python2-pytest')
checkdepends=('python-virtualenv' 'python2-virtualenv' 'python-process-tests'
              'python2-process-tests' 'python-pytest-cache' 'python2-pytest-cache'
              'python-pytest-xdist' 'python2-pytest-xdist' 'python-fields' 'python2-fields')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-cov/archive/v$pkgver.tar.gz")
sha512sums=('4f6e62d41796b53ed07c990bffe08e9f8eeb77b874efc3025754a737685bd494e21ed5985b6ce36293e6d9eecf19c44f5e01bad12d8fff884696dc6fa2ac4f14')

prepare() {
  cp -a pytest-cov-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-cov-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-cov-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-cov-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH:$PWD/tests" py.test || warning "Tests failed"

  cd "$srcdir"/pytest-cov-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD/tests" py.test2 || warning "Tests failed"
}

package_python-pytest-cov() {
  depends=('python-pytest' 'python-coverage')

  cd pytest-cov-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pytest-cov() {
  depends=('python2-pytest' 'python2-coverage')

  cd pytest-cov-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
# vim:set ts=2 sw=2 et:
