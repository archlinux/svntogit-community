# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-xdist
pkgver=3.0.0
pkgrel=1
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-xdist'
depends=('python-execnet' 'python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-filelock' 'python-psutil')
source=("https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1bc4fc700d78fc1097c7eb5dc8f0e1745de2610ab1cca3b74b78b06ecacc430aec86f5afa609e6d578f4b0fe3d6119e08c624c80093699b82706c5caea7be74f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-xdist-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-xdist-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-xdist-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/s
}

# vim:set ts=2 sw=2 et:
