# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jrnl
pkgver=3.2
pkgrel=1
pkgdesc="Collect your thoughts and notes without leaving the command line"
arch=('any')
url="https://jrnl.sh/"
license=('GPL3')
depends=('python-ansiwrap' 'python-colorama' 'python-cryptography' 'python-keyring'
         'python-parsedatetime' 'python-dateutil' 'python-pyxdg' 'python-rich' 'python-ruamel-yaml'
         'python-tzlocal')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-pytest-bdd<6' 'python-toml')
source=("https://github.com/jrnl-org/jrnl/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fb5cdf155091fe155eee314700d6ee2ad765a9943950aaed5b9d772c688bf0f34199ac352689f4ff21dd4804a8154a54d8ca232a4dfc8cd4a9ad7fd76867d1f4')

prepare() {
  cd jrnl-$pkgver
  # https://github.com/pytest-dev/pytest-bdd/issues/501
  sed -i '/-n=auto/d' pyproject.toml
}

build() {
  cd jrnl-$pkgver
  python -m build -nw
}

check() {
  cd jrnl-$pkgver
  python -m pytest
}

package() {
  cd jrnl-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
