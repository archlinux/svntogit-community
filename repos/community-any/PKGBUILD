# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Army

pkgname=vim-latexsuite
_pkgname=vim-latex
pkgver=20141116
_pkgver="1.8.23-${pkgver}.812-gitd0f31c9"
pkgrel=2
pkgdesc="Tools to view, edit, and compile LaTeX documents within Vim."
arch=('any')
url="http://vim-latex.sourceforge.net"
license=('custom:vim')
depends=('vim')
optdepends=('python2' 'texlive-bin')
groups=('vim-plugins')
source=("https://sourceforge.net/projects/${_pkgname}/files/snapshots/${_pkgname}-${_pkgver}.tar.gz")
md5sums=('c7662fd5ccad9912655ca585626ed515')
install=$pkgname.install
 
build() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"
	find . -type f -name *.py -exec sed -i 's:/usr/bin/python:&2:g' {} +
}

package() {
	cd "${srcdir}/${_pkgname}-${_pkgver}"
	make DESTDIR="${pkgdir}" PREFIX='/usr' VIMDIR='/usr/share/vim/vimfiles' install
}
