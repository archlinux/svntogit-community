# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nox
pkgname=python-$_pkgname
pkgver=2021.6.12
pkgrel=1
pkgdesc='Flexible test automation for Python'
arch=('any')
url='https://github.com/theacodes/nox'
license=('Apache')
depends=('python' 'python-argcomplete' 'python-colorlog' 'python-py' 'python-virtualenv' 'python-packaging')
optdepends=('python-jinja: tox_to_nox'
            'python-tox: tox_to_nox')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-jinja' 'python-tox')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('25344c4c6a955449b96de90b1019d0feedaeb697b9b555334c08f7fbd7148dab5c086145ff87b0aff4dd8459e1fe7face3455ee798d4989a8bf8fb4489a481e7')

prepare() {
  # missing python-contexter
  rm $_pkgname-$pkgver/tests/test_main.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
