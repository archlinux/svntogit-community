# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python-requests-file
pkgname=('python2-requests-file' 'python-requests-file')
pkgver=1.4.3
pkgrel=2
pkgdesc="File transport adapter for Requests"
arch=('any')
url="https://github.com/dashea/requests-file"
license=('Apache')
makedepends=('python-setuptools' 'python-requests' 'python-six'
             'python2-setuptools' 'python2-requests' 'python2-six')
source=("https://github.com/dashea/requests-file/archive/${pkgver}.tar.gz")
md5sums=('54ec29f29540b43b9b2c8a392dc96abc')

prepare() {
  cd "$srcdir"
  cp -a requests-file-${pkgver} requests-file-py2-${pkgver}
  cd requests-file-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/requests-file-py2-${pkgver}
  python2 setup.py build_ext --inplace
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/requests-file-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package_python2-requests-file() {
  depends=('python2-six' 'python2-requests')

  cd "$srcdir"/requests-file-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python-requests-file() {
  depends=('python-six' 'python-requests')

  cd "$srcdir"/requests-file-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}

