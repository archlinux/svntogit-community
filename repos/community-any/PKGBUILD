# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=8.5.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='https://eslint.org'
license=('MIT')
depends=('acorn' 'nodejs')
makedepends=('npm' 'rsync')
options=('!emptydirs')
source=("https://github.com/eslint/eslint/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('3def75e6b77c669a1aa81ded6bbfd28309e961377d19259692d1bdbb35e10e27fcfedd312742ea4fbbd8337a10b24e0844482200cf6ce9e5fef145ccb16b54b0')

prepare() {
  cd eslint-$pkgver
  # Dependencies still pin older ESLint
  npm install --force
}

check() {
  cd eslint-$pkgver
  node Makefile mocha
}

package() {
  cd eslint-$pkgver

  npm prune --production

  install -d "$pkgdir"/usr/bin
  ln -s ../lib/node_modules/$pkgname/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  install -Dt "$pkgdir"/usr/lib/node_modules/$pkgname/bin bin/$pkgname.js
  rsync -r --exclude .cache conf lib messages node_modules package.json \
    "$pkgdir"/usr/lib/node_modules/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  # Experimental dedup
  rm -r "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/acorn
}
