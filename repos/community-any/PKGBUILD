# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-regexp
pkgver=0.073
pkgrel=1
pkgdesc="Parse regular expressions"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-Regexp"
depends=('perl-list-moreutils' 'perl-ppi' 'perl-task-weaken')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-Regexp-$pkgver.tar.gz")
sha512sums=('5f0e1f3c7896dbb0597193b8c939290a30b38fccded0e3a9b3c6253affc5bc1d40f4ebf4389dcb6360772c45bdf787d9beb9222a1d25f56b9f4f04b062be8e5b')

build() {
  cd PPIx-Regexp-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-Regexp-$pkgver
  make test
}

package() {
  cd PPIx-Regexp-$pkgver
  make DESTDIR="$pkgdir" install
}
