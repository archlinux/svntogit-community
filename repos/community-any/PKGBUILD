# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

pkgbase=python-seaborn
pkgname=('python2-seaborn' 'python-seaborn')
pkgver=0.8.1
pkgrel=2
pkgdesc="Statistical data visualization"
arch=('any')
url="http://stanford.edu/~mwaskom/software/seaborn/"
license=('BSD')
makedepends=('python2-setuptools' 'python2-pandas' 'python2-matplotlib' 'python2-scipy'
             'python-setuptools' 'python-pandas' 'python-matplotlib' 'python-scipy')
source=("https://github.com/mwaskom/seaborn/archive/v${pkgver}.tar.gz")
md5sums=('14d49fcf304132684a9f240e14ab5dad')

prepare() {
  cd "$srcdir"
  cp -a seaborn-${pkgver} seaborn-py2-${pkgver}
}

build() {
  msg "Building Python2"
  cd "$srcdir"/seaborn-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/seaborn-${pkgver}
  python setup.py build
}

package_python2-seaborn() {
  depends=('python2-pandas' 'python2-matplotlib' 'python2-scipy')
  optdepends=('python2-statsmodels: for some advanced statistical plots')

  cd "$srcdir"/seaborn-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-seaborn() {
  depends=('python-pandas' 'python-matplotlib' 'python-scipy')
  optdepends=('python-statsmodels: for some advanced statistical plots')

  cd "$srcdir"/seaborn-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
