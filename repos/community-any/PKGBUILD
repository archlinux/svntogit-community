# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Daniel M. Capella <polycitizen@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgbase=python-parsedatetime
pkgname=('python-parsedatetime' 'python2-parsedatetime')
pkgver=2.1
pkgrel=2
pkgdesc="Parse human-readable date/time strings."
arch=('any')
license=('Apache')
url='http://github.com/bear/parsedatetime'
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-pyicu' 'python2-pyicu'
              'python-coverage' 'python2-coverage')
source=("git+https://github.com/bear/parsedatetime.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a parsedatetime{,-py2}
}

build() {
  cd "$srcdir"/parsedatetime
  python setup.py build

  cd "$srcdir"/parsedatetime-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parsedatetime
  nosetests3

  cd "$srcdir"/parsedatetime-py2
  nosetests2
}

package_python-parsedatetime() {
  depends=('python')

  cd parsedatetime
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-parsedatetime() {
  depends=('python2')

  cd parsedatetime-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
