# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: Sorin Ionescu <sorin.ionescu@gmail.com>

pkgname=ufw
pkgver=0.34
pkgrel=1
pkgdesc="Uncomplicated and easy to use CLI tool for managing a netfilter firewall"
arch=('any')
url="https://launchpad.net/ufw"
license=('GPL3')
depends=('iptables' 'python2')
backup=('etc/ufw/before.rules'
        'etc/ufw/before6.rules'
        'etc/ufw/after.rules'
        'etc/ufw/after6.rules'
        'etc/ufw/ufw.conf'
        'etc/ufw/user.rules'
        'etc/ufw/sysctl.conf'
        'etc/default/ufw'
        'usr/lib/ufw/user.rules'
        'usr/lib/ufw/user6.rules')
source=(http://launchpad.net/$pkgname/$pkgver/$pkgver/+download/$pkgname-$pkgver.tar.gz
        service)
sha256sums=('49b512ec49e39f6d17ea679a052c54aebbe6f0640ef476577f07bb5a14cfb908'
            'fdface6f4690e4748fbbd5661c02e1967a8750fdb23581d35516174017a3fe39')

package() {
  cd "$pkgname-$pkgver"
  
  # FS#28769 - move from /lib to /usr/lib
  sed -i -e 's|/lib|/usr/lib|' \
         -e 's|sbin|bin|g' setup.py

  python2 setup.py install --root="$pkgdir"

  # FS#35458 - correct iptables location
  sed -i -e 's|sbin|bin|g' "$pkgdir/usr/lib/python2.7/site-packages/ufw/"*

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/ufw.service"
  chmod 644 "$pkgdir/etc/ufw/"*.rules "$pkgdir/usr/lib/ufw/"*.rules
}
