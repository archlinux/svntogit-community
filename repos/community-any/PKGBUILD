# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
# https://github.com/aio-libs/aiobotocore/releases
pkgver=2.4.1
pkgrel=1
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-aioitertools)
makedepends=(python-setuptools)
checkdepends=(python-moto python-pytest python-pytest-asyncio
              # moto optdepends
              python-docker python-openapi-spec-validator python-yaml python-flask python-flask-cors)
source=("https://github.com/aio-libs/aiobotocore/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('662a9e301923e8a48442380239677030fe51198d2a4c48ecc88fec046c3e54cf')

prepare() {
  cd $_pkgname-$pkgver

  # Disable dependency pinning
  # Upstream tracking issue: https://github.com/aio-libs/aiobotocore/issues/670
  sed --in-place=.orig -r "s#'(botocore.*),<.*',#'\1',#" setup.py
  diff -u setup.py{.orig,} || true

  # moto 3.1 rename
  sed -i "s#'dynamodb2'#'dynamodb'#" tests/mock_server.py

  # For pytest-asyncio >= 0.19
  # https://github.com/aio-libs/aiobotocore/issues/965
  echo asyncio_mode = auto >> pytest.ini
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  export PYTHONPATH="$PWD"
  # Needed since 2.3.0 https://github.com/aio-libs/aiobotocore/issues/948
  export AWS_SECRET_ACCESS_KEY=test
  export AWS_ACCESS_KEY_ID=test
  # test_lambda uses moto.awslambda, which requires a running Docker service
  # See: https://github.com/spulec/moto/issues/3276
  # test_version checks lower and upper bounds for dependencies in setup.py,
  # and they are patched away in prepare()
  # test_load_sso_credentials_with_cache_expired: failed with newer botocore
  pytest -m moto tests \
      --ignore=tests/test_patches.py \
      --ignore=tests/test_lambda.py \
      --ignore=tests/test_version.py \
      -k 'not test_load_sso_credentials_with_cache_expired'
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
