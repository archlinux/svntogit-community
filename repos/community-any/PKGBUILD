# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-debtcollector
pkgver=2.0.0
pkgrel=3
arch=('any')
pkgdesc='A collection of Python deprecation patterns and strategies that help you collect your technical debt in a non-destructive manner.'
url='https://docs.openstack.org/developer/debtcollector'
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/debtcollector/archive/$pkgver.tar.gz")
sha512sums=('cdacf720cd783e1d795089451f9d2cf91ba79ef6085b18760d13f0b98fdbecb0c21960a771f3a03a867b5a301f0cc66f09407dfb8ae5713132d5b3fd027bc439')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/debtcollector-$pkgver
  python setup.py build
}

check() {
  cd debtcollector-$pkgver
  stestr run
}

package() {
  cd debtcollector-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
