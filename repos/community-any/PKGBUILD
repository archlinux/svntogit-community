# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-rfc3987
pkgname=(python-rfc3987 python2-rfc3987)
pkgver=1.3.7
pkgrel=1
pkgdesc="Parsing and validation of URIs (RFC 3986) and IRIs (RFC 3987)"
url="https://pypi.python.org/pypi/rfc3987"
license=('GPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/r/rfc3987/rfc3987-$pkgver.tar.gz")
sha512sums=('c5fa8eb36358f7f42d12adc305031bfff7bd77e83316ad4458ed07acc6094c7d9109b95848b9296dcbc6677ce34aa59b1be8fd57dfcdc6be47eb92aeda51c518')

prepare() {
  cp -a rfc3987-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/rfc3987-$pkgver
  python setup.py build

  cd "$srcdir"/rfc3987-$pkgver-py2
  python2 setup.py build
}

package_python-rfc3987() {
  depends=('python')

  cd rfc3987-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-rfc3987() {
  depends=('python2')

  cd rfc3987-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
