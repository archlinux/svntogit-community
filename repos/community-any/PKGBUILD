# Maintainer: David Runge <dave@sleepmap.de>
_name=phpserialize
pkgname=python-phpserialize
pkgver=1.3
pkgrel=3
pkgdesc="A port of the serialize and unserialize functions of php to python"
arch=('any')
url="https://github.com/mitsuhiko/phpserialize"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# tests not included in pypi source:
# https://github.com/mitsuhiko/phpserialize/issues/21
# LICENSE not in any source tarball:
# https://github.com/mitsuhiko/phpserialize/issues/22
#source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mitsuhiko/${_name}/archive/${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/mitsuhiko/${_name}/master/LICENSE")
sha512sums=('d96e3aa87d2f03fef6c7cee234e90b373989a9c8e3a07a8e2147a2e7487d3448ea0ef646263e517d6b307bf66f9ba1a4e8def04f919767532cede802cc7db144'
            '95be97fbe3b8d03ba27aee2d9f5e29ff2923425970103f8b15f13e36beb744e8bc1f9361e89b05d2e9677368b5ab95c5ad84722e3523b3b8574e2d94b69f2d3e')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # disabling broken test: https://github.com/mitsuhiko/phpserialize/issues/17
  sed -e 's/test_tuple_roundtrips/disabled_test_tuple_roundtrips/' \
      -e 's/test_dumps_dict/disabled_test_dumps_dict/' \
      -i tests.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}
