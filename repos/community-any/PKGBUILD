# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-run
pkgver=9.2.0
pkgrel=1
pkgdesc="Install packages and run Python with them"
url="https://github.com/jaraco/pip-run"
license=('MIT')
arch=('any')
depends=('python-autocommand' 'python-packaging' 'python-path' 'python-pip' 'python-more-itertools'
         'python-jaraco.context' 'python-jaraco.text')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest' 'jupyter-nbformat' 'python-pytest-enabler' 'python-jaraco.path')
source=("https://github.com/jaraco/pip-run/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('73fa2e253b3ebce6b7079b5669db7f5b18694cf1724438e431b1d10dbcb3d67581db32748706e5a71feceed3a6e4cf3e63afe6cc27b92e41f1935bd2e2ed3e99')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pip-run-$pkgver
  python -m build -wn
}

check() {
  cd pip-run-$pkgver
  python -m pytest
}

package() {
  cd pip-run-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
