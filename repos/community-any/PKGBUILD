# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Anatol Pomozov

pkgbase=python-autobahn
pkgname=(python-autobahn python2-autobahn)
pkgver=18.12.1
pkgrel=1
pkgdesc='Real-time framework for Web, Mobile & Internet of Things'
arch=(any)
url='http://autobahn.ws/python/'
license=(MIT)
makedepends=(python-setuptools python2-setuptools python-twisted python-six python-txaio python2-twisted python2-six python2-txaio)
checkdepends=(python2-unittest2)
source=(https://pypi.io/packages/source/a/autobahn/autobahn-$pkgver.tar.gz)
sha256sums=('bec9e04c97536a6c0044965db4a478672a01c4adee0aa84d9c4a0945b2adc879')

build() {
  cd "$srcdir/autobahn-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$srcdir/autobahn-$pkgver"
  #It requires python-unittest2 in [community]
  #python setup.py test
  #python2 setup.py test
}

package_python-autobahn() {
  depends=(python python-twisted python-six python-txaio)

  cd "$srcdir/autobahn-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-autobahn() {
  depends=(python2 python2-twisted python2-six python2-txaio)

  cd "$srcdir/autobahn-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
