# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-apipkg
pkgname=('python-apipkg' 'python2-apipkg')
pkgver=1.4
pkgrel=4
pkgdesc="Namespace control and lazy-import mechanism"
arch=('any')
license=('MIT')
url="https://bitbucket.org/hpk42/apipkg"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://files.pythonhosted.org/packages/source/a/apipkg/apipkg-$pkgver.tar.gz")
sha512sums=('118f806706944513f50d3cdef596b32f8ce585cfaf94d2531a6d260f90a0e2147177161aaa6f5d6823150e6182d0f73be3dc01259b8a2f8d710733a22ea54789')

prepare() {
  cp -a apipkg-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/apipkg-${pkgver}"
  python setup.py build

  cd "$srcdir/apipkg-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/apipkg-${pkgver}"
  py.test

  cd "$srcdir/apipkg-${pkgver}-py2"
  py.test2
}

package_python-apipkg() {
  depends=('python')

  cd apipkg-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-apipkg() {
  depends=('python2')

  cd apipkg-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
