# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-soupsieve
pkgname=(python-soupsieve python2-soupsieve)
pkgver=1.9.4
pkgrel=1
pkgdesc="A CSS4 selector implementation for Beautiful Soup"
url="https://github.com/facelessuser/soupsieve"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python2-backports.functools_lru_cache')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-beautifulsoup4'
              'python2-beautifulsoup4' 'python-html5lib' 'python2-html5lib' 'python-lxml'
              'python2-lxml')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/facelessuser/soupsieve/archive/$pkgver.tar.gz")
sha512sums=('7b2b7f0383991bc643748fe6506fad22be7d23b541777222c7e2ccc386ab63d1d26f85b4b2a4db5d0cc37a7e9f0710db3037958eb70acef5d35346f4b57bc2d1')

build() {
  cd soupsieve-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd soupsieve-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-soupsieve() {
  depends=('python')

  cd soupsieve-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

package_python2-soupsieve() {
  depends=('python2-backports.functools_lru_cache')

  cd soupsieve-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
