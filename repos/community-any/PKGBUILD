# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=4.6.1
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-py' 'python2-py' 'python-pluggy'
             'python2-pluggy' 'python-attrs' 'python2-attrs' 'python-more-itertools'
             'python2-more-itertools' 'python-atomicwrites' 'python2-atomicwrites'
             'python2-funcsigs' 'python2-pathlib2' 'python-wcwidth' 'python2-wcwidth')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-tox'
              'python2-tox' 'python-yaml' 'python2-yaml' 'python-pytest-xdist'
              'python2-pytest-xdist' 'python-twisted' 'python2-twisted' 'python-requests'
              'python2-requests' 'python-hypothesis' 'python2-hypothesis')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('102748a0af2e3acdea1fc094828019502411c955068c28dddaeb613c84c97ba24698727a9e3a71c4eac804029c4118ab8d941859e91a6295849bc7b077fc4d6f')

prepare() {
  sed -i 's/==0/>=0/' pytest-$pkgver/setup.py
  cp -a pytest-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-$pkgver-py2
  python2 setup.py build
}

check() {
  # Skip test_collect_pyargs_with_testpaths because it mangles PYTHONPATH

  cd "$srcdir"/pytest-$pkgver
  PYTHONPATH="$PWD"/build/lib python src/pytest.py -k 'not test_collect_pyargs_with_testpaths'

  cd "$srcdir"/pytest-$pkgver-py2
  PYTHONPATH="$PWD"/build/lib python2 src/pytest.py -k 'not test_collect_pyargs_with_testpaths'
}

package_python-pytest() {
  depends=('python-py' 'python-setuptools' 'python-pluggy' 'python-attrs' 'python-more-itertools'
           'python-atomicwrites' 'python-wcwidth')

  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest() {
  depends=('python2-py' 'python2-setuptools' 'python2-pluggy' 'python2-attrs' 'python2-wcwidth'
           'python2-more-itertools' 'python2-atomicwrites' 'python2-funcsigs' 'python2-pathlib2')

  cd pytest-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/py.test{,2}
  mv "$pkgdir"/usr/bin/pytest{,2}
}
