# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# AUR Maintainer: Marcus Scheunemann

pkgname=python-markdown-math
pkgver=0.6
pkgrel=5
pkgdesc="Math extension for Python-Markdown"
arch=('any')
url="https://github.com/mitya57/python-markdown-math"
license=('BSD')
depends=('python-markdown')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${pkgname:0:1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "fix-markdown-3.x.patch::https://github.com/mitya57/python-markdown-math/commit/7c2a82327dc6aabca25cd42bf9cce4f0c237fd80.patch")
sha256sums=('c68d8cb9695cb7b435484403dc18941d1bad0ff148e4166d9417046a0d5d3022'
            'SKIP'
            '06ba6b40175a410534d14fe5482d773c1d49d684c0ac74c4795f187c10890c5f')
validpgpkeys=('F24299FF1BBC9018B906A4CB6026936D2F1C8AE0') # Dmitry Shachnev <mitya57@debian.org>

prepare() {
    cd "python-markdown-math-${pkgver}"
    patch -Np1 -i ../fix-markdown-3.x.patch
}

build() {
    cd "python-markdown-math-${pkgver}"

    python setup.py build
}

check() {
    cd "python-markdown-math-${pkgver}"

    python setup.py test
}

package() {
    cd "python-markdown-math-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
