# Maintainer: David Runge <dave@sleepmap.de>
_name=Pyphen
pkgname=python-pyphen
pkgver=0.9.5
pkgrel=1
pkgdesc="Pure Python module to hyphenate text"
arch=('any')
url="http://pyphen.org/"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('15b9a29517df2b873b5b8d4739d8fa48a09ab199ab9246c7a4fd25c46253a74a18a88fd9484978476b3b1c8d88fed7729382b9dbe4f425724fe54bcd6227de96')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}
