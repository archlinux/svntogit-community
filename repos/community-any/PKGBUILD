# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-testrunner
pkgver=5.6
pkgrel=1
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions' 'python-six')
checkdepends=('python-zope-testing' 'python-zope-testrunner')
source=("https://github.com/zopefoundation/zope.testrunner/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bce65e5cd64f7ddbe2b1aad359d084e1b7175ce7186b08ab9b24e3cc745c0b118ce69e4f09e86bf27a420823192b60f9d1a21ac02dd66abfbdd5c10986302b61')

build() {
  cd zope.testrunner-$pkgver
  python setup.py build
}

check() {
  cd zope.testrunner-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python setup.py test
  )
}

package() {
  cd zope.testrunner-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
