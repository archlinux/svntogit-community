# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Akaash Suresh <https://github.com/Curry>
# Contributor: Batuhan Baserdem <lastname dot firstname at gmail>

pkgname=qmk
pkgver=0.0.40
_qmk_pkgver=0.12.15
pkgrel=1
pkgdesc="CLI tool for customizing supported mechanical keyboards."
arch=('any')
url="https://github.com/qmk/qmk_cli"
license=('MIT')
depends=('python-appdirs' 'python-argcomplete' 'python-colorama'
         'python-milc' 'git' 'avr-gcc' 'arm-none-eabi-gcc' 'avr-libc'
         'arm-none-eabi-binutils' 'arm-none-eabi-newlib' 'avr-binutils'
         'dfu-programmer' 'dfu-util' 'avrdude' 'python-hjson' 'python-pygments')
makedepends=('python-setuptools')
optdepends=('gcc'
            'libffi'
            'libusb-compat'
            'clang: chibios-upgrader'
            'zip: some distribution script'
            'wget: some update scripts'
            'diffutils: some update scripts')
source=("https://files.pythonhosted.org/packages/source/q/qmk/qmk-${pkgver}.tar.gz"
        "50-qmk.rules_${pkgver}::https://raw.githubusercontent.com/qmk/qmk_firmware/${_qmk_pkgver}/util/udev/50-qmk.rules")
sha256sums=('b4c051d2cb922dda1561069d7d98e9300071d37a27d96610cc5cc703eccb5d06'
            'd6f0bfa30fb1f68b5428911a9165123059523ed781038f2b50cb83b828cc2436')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/lib/udev/rules.d"
  install -Dm644 ../"50-qmk.rules_${pkgver}" "${pkgdir}/usr/lib/udev/rules.d/50-qmk.rules"
}
