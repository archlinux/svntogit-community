# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin-data
pkgver=2.0.3.20131214
_lm_sc_ver=20121025
_dict_ver=20131214
pkgrel=1
arch=('any')
pkgdesc="Statistical language model data from open-gram project for sunpinyin"
license=('APACHE')
provides=("sunpinyin-data-le=$pkgver")
url="http://sunpinyin.googlecode.com"
makedepends=("sunpinyin")
source=("http://open-gram.googlecode.com/files/lm_sc.t3g.arpa-${_lm_sc_ver}.tar.bz2"
        "http://open-gram.googlecode.com/files/dict.utf8-${_dict_ver}.tar.bz2")

prepare() {
  mkdir raw && cd raw
  ln -s ../lm_sc.t3g.arpa
  ln -s ../dict.utf8

  ln -sf "/usr/share/doc/sunpinyin/SLM-inst.mk" ./Makefile
}

build() {
  cd raw
  make
}

package() {
  cd raw
  make DESTDIR="$pkgdir" install
}

sha512sums=('3159dd3700382cfa617ba025ce542660580f3057e0dd6bbb92947902495db08e4454c1d09d01a55c90653444513d125dd034e944b060a9d8e009c3633b106d97'
            'adaf52a27c6afdeb950912845f3fcb7a6799f997d50a2892ceecf76ed13e3644407338d734832c2ce4b09bce11fec4731774f1eb2dd4c30bea5490555915bd5b')
