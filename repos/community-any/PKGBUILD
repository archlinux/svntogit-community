# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible
pkgver=1.4.5
pkgrel=1
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='http://www.ansible.com'
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-paramiko>=1.7.7' 'python2-jinja' 'python2-httplib2')
makedepends=('asciidoc' 'fakeroot')
backup=('etc/ansible/ansible.cfg')
source=(http://releases.ansible.com/ansible/$pkgname-$pkgver.tar.gz)
sha256sums=('faa6c1f1b801de3639c8375a4f5ab36c75d8cdb5c69bde2a21532430d6c8b4a9')

build() {
  cd $pkgname-$pkgver

  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/usr/share/ansible"
  cp -dpr --no-preserve=ownership ./library/* "$pkgdir/usr/share/ansible/"
  cp -dpr --no-preserve=ownership ./examples "$pkgdir/usr/share/ansible"

  python2 setup.py install -O1 --root="$pkgdir"

  install -D examples/ansible.cfg "$pkgdir/etc/ansible/ansible.cfg"

  install -D README.md "$pkgdir/usr/share/doc/ansible/README.md"
  install -D COPYING "$pkgdir/usr/share/doc/ansible/COPYING"

  mkdir -p "$pkgdir/usr/share/man/man1"
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "$pkgdir/usr/share/man/man1"
  mkdir -p "$pkgdir/usr/share/man/man3"
  cp -dpr --no-preserve=ownership docs/man/man3/*.3 "$pkgdir/usr/share/man/man3"
}

# vim:set ts=2 sw=2 et:
