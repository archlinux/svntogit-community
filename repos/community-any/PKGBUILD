# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hypothesis
pkgname=('python-hypothesis' 'python2-hypothesis')
pkgver=4.5.1
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-attrs' 'python2-attrs'
             'python-coverage' 'python2-coverage' 'python2-enum34')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'flake8' 'python2-flake8' 'python-pytz'
              'python2-pytz' 'python-numpy' 'python2-numpy' 'python-faker' 'python2-faker'
              'python-flaky' 'python2-flaky' 'python-django' 'python2-django' 'python-mock'
              'python2-mock' 'python-pandas' 'python2-pandas' 'python-dpcontracts'
              'python2-dpcontracts' 'python-pytest-xdist' 'python2-pytest-xdist'
              'python-lark-parser' 'python2-lark-parser')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis/archive/hypothesis-python-$pkgver.tar.gz")
sha512sums=('15db83dccda726e2c7b1b70f81cf417603e12c0e297224e1acdc6a32d1a88661b9e90ade5604bd917786bfc58bace3986ca718ddf48c129b6223ba19e6c56a0d')

prepare() {
  mv hypothesis-hypothesis-python-$pkgver hypothesis-$pkgver

  cp -a hypothesis-$pkgver{,-py2}

  rm -r hypothesis-$pkgver/hypothesis-python/tests/py2
  rm -r hypothesis-$pkgver-py2/hypothesis-python/tests/py3

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/hypothesis-$pkgver/hypothesis-python
  python setup.py build

  cd "$srcdir"/hypothesis-$pkgver-py2/hypothesis-python
  python2 setup.py build
}

check() {
  cd "$srcdir"/hypothesis-$pkgver/hypothesis-python
  mv tests/django ../
  python setup.py pytest
  mv ../django tests/
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m tests.django.manage test tests.django

  cd "$srcdir"/hypothesis-$pkgver-py2/hypothesis-python
  mv tests/django ../
  # https://github.com/python/typing/issues/612
  python2 setup.py pytest || warning "Tests failed"
  mv ../django tests/
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m tests.django.manage test tests.django
}

package_python-hypothesis() {
  depends=('python-attrs' 'python-coverage')
  optdepends=('python-pytz: for datetime and django module'
              'python-faker: for fakefactory and django module'
              'python-django: for django module'
              'python-numpy: for numpy module'
              'python-pytest: for pytest module'
              'python-lark-parser: for lark module')

  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-hypothesis() {
  depends=('python2-attrs' 'python2-coverage' 'python2-enum34')
  optdepends=('python2-pytz: for datetime and django module'
              'python2-faker: for fakefactory and django module'
              'python2-django: for django module'
              'python2-numpy: for numpy module'
              'python2-pytest: for pytest module'
              'python2-lark-parser: for lark module')

  cd hypothesis-$pkgver-py2/hypothesis-python
  python2 setup.py install --root="$pkgdir" --optimize=1
}
