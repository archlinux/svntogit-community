# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-text-template
pkgver=1.58
pkgrel=1
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='https://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings' 'perl-test-more-utf8')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/MS/MSCHOUT/Text-Template-$pkgver.tar.gz")
sha512sums=('23bf03529c7607806c16573b5c0d08ba0b87cf7eb859538d1944bc13952741a88c15588e8de173053a51640c5bdad99c5cc246a67cc05be7eff32853887fbb11')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
