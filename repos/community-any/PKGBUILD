# Maintainer: David Runge <dvzrv@archlinux.org>

_name=binaryornot
pkgname=python-binaryornot
pkgver=0.4.4
pkgrel=8
pkgdesc="Ultra-lightweight pure Python package to check if a file is binary or text"
arch=(any)
url="https://github.com/audreyr/binaryornot"
license=(BSD)
depends=(python-chardet)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-hypothesis)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz
  $pkgname-0.4.4-hypothesis_tests.patch::https://github.com/audreyr/binaryornot/pull/52.patch
)
sha512sums=('379e71b41824b9389ea02c64223e26c57694d07b749c197e3a6b9f10558c8d502e9b93a13ae6c3a4bca49064ffc650ce822073dc312fb06e50e06b8a3f04f419'
            '8a0f1066a580f08778434ed7d30c2ebf2764dbfd746b561ffce2fb8dd8d77cafaf4a58b03504cf5b1e4d37e0a6ffe3038dcaa5611cdfd7d42ada86edd1e47f3f')
b2sums=('4a70e22eba51a266987308e8d1b4571ace5760f945920194a1a61f4336f33dcb655c4f0b1f760aa3502834d29a1a64ac62657e5d73eac8f1d7d02edf71db8af9'
        'f07730709f11bf0732b60b81603c40bea1fa6da2d8df545b088072e0f710a7e123e438b03d2390122b6000c14343e932b38d4b6f8956ea87b72e29deae1a4715')

prepare() {
  # fix tests using python-hypothesis: https://github.com/audreyr/binaryornot/issues/46
  patch -Np1 -d $_name-$pkgver -i ../$pkgname-0.4.4-hypothesis_tests.patch
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m unittest discover -vs tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,CONTRIBUTING,HISTORY,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
