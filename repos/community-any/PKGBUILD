# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=frescobaldi
pkgver=3.2
pkgrel=1
pkgdesc="A LilyPond sheet music text editor"
arch=(any)
url="https://www.frescobaldi.org/"
license=(GPL)
depends=(
  python python-pyqt5 python-pyqt5-webengine python-ly poppler
  python-poppler-qt5 python-qpageview qt5-base qt5-svg qt5-webkit
)
makedepends=(python-{build,installer}
             python-setuptools
             python-wheel)
optdepends=(
  'lilypond: music engraving'
  'python-pygame: MIDI input and playback'
  'python-pycups: printing to a CUPS server'
  'hyphen-lang: hyphenation patterns for desired languages'
)
source=(https://github.com/frescobaldi/frescobaldi/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=(4d707b5b35392f84d904d1cc7ecc36992f8e85f6868fdc5a4983c78a0e850acf)

prepare() {
  cd ${pkgname}-${pkgver}
  # Provided by hyphen-*
  rm -f frescobaldi_app/hyphdicts/hyph_*.dic
  rm -f frescobaldi_app/hyphdicts/README*
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build -wn
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer -d "${pkgdir}" dist/*.whl
}
