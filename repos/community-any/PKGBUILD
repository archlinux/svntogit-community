# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=MyST-Parser
pkgname=python-myst-parser
pkgver=0.15.1
pkgrel=1
pkgdesc='An extended commonmark compliant parser, with bridges to docutils/sphinx'
arch=('any')
url='https://github.com/executablebooks/MyST-Parser'
license=('MIT')
depends=('python-markdown-it-py' 'python-mdit_py_plugins' 'python-yaml' 'python-docutils' 'python-sphinx' 'python-jinja')
optdepends=('python-linkify-it-py: linkify support')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-regressions' 'python-beautifulsoup4' 'python-linkify-it-py')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('79755cc0e901655db650113615bf2e620f8455598ef81b98400273bda56f2e3239f9bca2e602796039af8ca0efdeb1704bb104633b68944d82d5433357d5d389')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest -k 'not test_sphinx_builds and not test_fixtures'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
