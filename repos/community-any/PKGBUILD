# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonne Ha√ü <me@jhass.eu>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

pkgname=ruby-bundler
_gemname=${pkgname#ruby-}
pkgver=2.2.16
pkgrel=3
pkgdesc="Manages an application's dependencies through its entire life, across many machines, systematically and repeatably."
arch=('any')
url='http://bundler.io'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('3a147677cc026df2f10bdd5d73c6c4c483637c4cd507a521ce604fe385146254c653b29618c774545b1c038f08b7101315110ce7f891d83bff4c79412c6e3c64')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  HOME="/tmp" GEM_HOME="$_gemdir" GEM_PATH="$_gemdir" gem install --no-user-install --ignore-dependencies \
     -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  # disable installing manpages to workaround FS#70398
  #install -d "$pkgdir/usr/share/man/"{man1,man5}
  #mv "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/lib/bundler/man"/*.1 "$pkgdir/usr/share/man/man1/"
  #mv "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/lib/bundler/man"/*.5 "$pkgdir/usr/share/man/man5/"
}
