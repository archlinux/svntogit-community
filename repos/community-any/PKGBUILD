# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-iniconfig
pkgver=2.0.0
pkgrel=1
pkgdesc="brain-dead simple config-ini parsing"
url="https://github.com/RonnyPfannschmidt/iniconfig"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-hatchling' 'python-hatch-vcs')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/RonnyPfannschmidt/iniconfig/archive/v$pkgver.tar.gz")
sha512sums=('e474aaa904070ed79dbbd2b4b572574ebb591b97a4ca0f9c4e4aef437af01035d2f73c48bb670afcec2cd8b7ea093b92d6b0c0eca7ba42033418a2b0e0ac80ad')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd iniconfig-$pkgver
  python -m build -nw
}

check() {
  cd iniconfig-$pkgver
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd iniconfig-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
