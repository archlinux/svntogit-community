# Maintainer:

_pkgname=pcbnewTransition
pkgname=python-pcbnewtransition
pkgver=0.3.4
pkgrel=2
pkgdesc='Library that allows you to support both, KiCAD 5 and KiCAD 6 in your plugins'
arch=(any)
url='https://github.com/yaqwsx/pcbnewTransition'
license=(MIT)
depends=(python kicad)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://pypi.io/packages/source/p/$_pkgname/$_pkgname-$pkgver.tar.gz)
sha512sums=('cca02f15d59e051f2a04f561a1ecc9a3dcf2f755695feb38850eaf3c0fbb2a92a43049d4af51cb9fc7850059404c8da209121c82902e0775f23af7aeb4c178bc')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation --skip-dependency-check
}

package() {
  cd $_pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
