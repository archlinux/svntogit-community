# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-toposort
pkgver=1.9
pkgrel=1
pkgdesc='Implements a topological sort algorithm'
arch=('any')
license=('Apache')
url='https://gitlab.com/ericvsmith/toposort'
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://gitlab.com/ericvsmith/toposort/-/archive/$pkgver/toposort-$pkgver.tar.bz2")
sha512sums=('167532d84e4c6c45a942b24704fc47db5ecee43925e88948ba538c1a15002cd5bdc9f6b887c5ca248d68549e41b46ea4ccf570ee3093df0c45916b92860116f7')

build() {
  cd toposort-$pkgver
  python -m build -nw
}

check() {
  cd toposort-$pkgver
  make test
}

package() {
  cd toposort-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
