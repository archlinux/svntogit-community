# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=graphviz
pkgname=python-graphviz
pkgver=0.19.1
pkgrel=1
pkgdesc='Simple Python interface for Graphviz'
arch=(any)
url="https://github.com/xflr6/$_pkgname"
license=(MIT)
depends=(graphviz
         python)
makedepends=(python-{build,installer}
             python-setuptools
             python-wheel)
checkdepends=(python-mock
              python-pytest
              python-pytest-mock)
#source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.zip")
_archive="$_pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('e1feb29cc94aa812ce5ad49d82176ec4e52f7addc9804be48856a1154d164b38')
b2sums=('0422c710724ca67994131b98fc63e8f15944a8bd7064949ee0ab4e6196eadb02ed6c6d61f1699cb2e5ab875c8cf06a68078a9c34618c59d616b7c9bb96e3aa4f')

prepare() {
	cd "$_archive"
	# do not run python-coverage in unittests :/
	sed -i 's/--cov --cov-report=term --cov-report=html//' setup.cfg
	# don't add pointless dependency on python-mock needed on python 3.5
	sed -i '/mock_use_standalone_module/d' setup.cfg
}

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive/tests"
	python -m pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.txt
}
