# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=python-beautifulsoup4
pkgname=('python-beautifulsoup4' 'python2-beautifulsoup4')
pkgver=4.8.0
pkgrel=3
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping"
arch=('any')
url="https://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=('python-setuptools' 'python2-setuptools' 'python-soupsieve' 'python2-soupsieve')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://www.crummy.com/software/BeautifulSoup/bs4/download/${pkgver%.*}/${pkgbase#*-}-$pkgver.tar.gz")
sha512sums=('00e157f8031dbb0e30e49158ca30f9e9207647dd3355edf23c2906b9d0c8b93250669edf5b20372e62546501a0cd2d7e87b793faf30bc122b5544f9358c1a4db')

prepare() {
  cp -a beautifulsoup4-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/beautifulsoup4-$pkgver
  python setup.py build

  cd "$srcdir"/beautifulsoup4-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/beautifulsoup4-$pkgver/build
  py.test

  cd "$srcdir"/beautifulsoup4-$pkgver-py2/build
  py.test2
}

package_python-beautifulsoup4() {
  depends=('python-soupsieve')
  optdepends=('python-chardet: to autodetect character encodings'
              'python-lxml: alternative HTML parser'
              'python-html5lib: alternative HTML parser')

  cd beautifulsoup4-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-beautifulsoup4() {
  depends=('python2-soupsieve')
  optdepends=('python2-chardet: to autodetect character encodings'
              'python2-lxml: alternative HTML parser'
              'python2-html5lib: alternative HTML parser')

  cd beautifulsoup4-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
