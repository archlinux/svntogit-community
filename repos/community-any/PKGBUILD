pkgname=perl-sub-quote
pkgver=2.006006
pkgrel=3
pkgdesc="efficient generation of subroutines via string eval"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-fatal>=0.010')
url='https://search.cpan.org/dist/Sub-Quote'
source=("https://search.cpan.org/CPAN/authors/id/H/HA/HAARG/Sub-Quote-$pkgver.tar.gz")
sha256sums=('6e4e2af42388fa6d2609e0e82417de7cc6be47223f576592c656c73c7524d89d')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$srcdir/Sub-Quote-$pkgver"
    perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir/Sub-Quote-$pkgver"
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir/Sub-Quote-$pkgver"
  make install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
