# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pypandoc
pkgver=1.6.3
pkgrel=1
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
depends=('python-setuptools' 'pandoc')
makedepends=('python-setuptools' 'pandoc')
checkdepends=('texlive-core' 'texlive-latexextra')
source=("https://github.com/bebraw/pypandoc/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b9e7d769aa75b96b7f04cce0cc93045f38434df88433c2bd392653fa744a28c96392777dfc0c0c96cfbc6a187be8ec64b9ac3491f8b5d346d949b4dd0000e4a5')

prepare() {
  # We don't really need pip and wheel at runtime
  sed -i "s/install_requires = .*/install_requires = ['setuptools'],/" pypandoc-$pkgver/setup.py
}

build() {
  cd pypandoc-$pkgver
  python setup.py build
}

check() {
  cd pypandoc-$pkgver
  # Ignore test failures now as pandoc-citeproc is deprecated and gone
  python setup.py test || echo "Tests failed"
}

package() {
  cd pypandoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
