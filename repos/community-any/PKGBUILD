# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=arch-wiki-lite
pkgver=20160308
_codever=20160430
pkgrel=2
pkgdesc="The wiki without html.  1/9 as big, easily searched and viewable on console."
arch=('any')
url="http://kmkeen.com/arch-wiki-lite/"
_watch="https://www.archlinux.org/packages/community/any/arch-wiki-docs/"
license=('GPL' 'FDL')
depends=('bash' 'gzip')
makedepends=('python' 'arch-wiki-docs')
optdepends=('dialog: for interactive menu'
            'arch-wiki-docs: for wiki-search-html'
            'xdg-utils: for wiki-search-html')
source=("http://kmkeen.com/$pkgname/$pkgname-$_codever.tar.gz")
md5sums=('827909e927c20303395d74c65e34f04e')

build() {
  cd "$srcdir/$pkgname"
  LC_ALL=en_US.UTF-8 python3 wiki_lite.py
}

package() {
  cd "$srcdir/$pkgname"
  install -d "$pkgdir/usr/share/doc/arch-wiki/text/"
  install -m 0644 wiki/* "$pkgdir/usr/share/doc/arch-wiki/text/"
  install -Dm 0755 wiki-search      "$pkgdir/usr/bin/wiki-search"
  install -Dm 0755 wiki-search-html "$pkgdir/usr/bin/wiki-search-html"
  install -Dm 0644 wiki-search.dialog.rc "$pkgdir/etc/dialog.d/wiki-search.dialog.rc"
}

