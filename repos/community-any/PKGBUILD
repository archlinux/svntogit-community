# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-pytest-jupyter
pkgver=0.0.3
pkgrel=1
pkgdesc='A pytest plugin for testing Jupyter core libraries and extensions'
arch=(any)
license=(BSD)
url='https://github.com/jupyter-server/pytest-jupyter'
depends=(python-pytest)
makedepends=(python-build python-installer python-setuptools python-jupyter_packaging)
source=(https://github.com/jupyter-server/pytest-jupyter/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('67ebebb726cd178c729a10ecac1b457f75feb1e66a1bf3d0b50660daa5830ec0')

prepare() {
# Remove jupyter_packaging version constraint
  sed -e 's|~=0.7.5||' -i pytest-jupyter-$pkgver/pyproject.toml
}

build() {
  cd pytest-jupyter-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd pytest-jupyter-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
