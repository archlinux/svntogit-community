# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgbase=python-unidecode
pkgname=('python-unidecode' 'python2-unidecode')
pkgver=0.04.16
pkgrel=1
pkgdesc='ASCII transliterations of Unicode text'
arch=('any')
url='http://pypi.python.org/pypi/Unidecode/'
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools')
options=('!emptydirs')
source=("http://pypi.python.org/packages/source/U/Unidecode/Unidecode-${pkgver}.tar.gz")
sha256sums=('4cd218737d1a807bbaba9a6534fc3c80d129cff76cf7d7fdbd71e744d836657b')

prepare() {
  for d in python{,2}-unidecode; do
    cp -r Unidecode-${pkgver} $d
  done
}

build() {
  for d in python{,2}-unidecode; do
    pushd $d
    ${d%-*} setup.py build
    popd
  done
}

package_python-unidecode() {
  cd python-unidecode

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-unidecode() {
  cd python2-unidecode

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
