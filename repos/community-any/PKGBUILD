# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pifpaf
pkgname=('pifpaf' 'python2-pifpaf')
pkgver=2.0.2
pkgrel=1
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-click'
             'python2-click' 'python-six' 'python2-six' 'python-daiquiri' 'python2-daiquiri'
             'python-fixtures' 'python2-fixtures' 'python-psutil' 'python2-psutil'
             'python-jinja' 'python2-jinja' 'python-xattr' 'python2-xattr')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-requests' 'python2-requests'
              'python-testtools' 'python2-testtools' 'python-oslotest' 'python2-oslotest'
              'memcached' 'mongodb' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb')
# 'elasticsearch': test fails
# 'rabbitmq': test hangs
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz")
sha512sums=('38a9910ce42539cf33361b3ca200e8cee575e5c52f48bfa0aeee2c066a38cea70bb12148261145c694e0921c72970a75ba2f4b28d77db3a397bca79dcfea5b5d')

prepare() {
  cp -a pifpaf-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pifpaf-$pkgver
  python setup.py build

  cd "$srcdir"/pifpaf-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr

  cd "$srcdir"/pifpaf-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHON=python2 python2 setup.py testr
}

package_pifpaf() {
  depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-six'
           'python-fixtures' 'python-psutil' 'python-xattr')
  provides=("python-pifpaf=$pkgver")
  conflicts=('python-pifpaf')
  replaces=('python-pifpaf')

  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pifpaf() {
  depends=('python2-pbr' 'python2-click' 'python2-six' 'python2-fixtures' 'python2-daiquiri'
           'python2-psutil' 'python2-jinja' 'python2-xattr')

  cd pifpaf-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/pifpaf{,2}
}

# vim:set ts=2 sw=2 et:
