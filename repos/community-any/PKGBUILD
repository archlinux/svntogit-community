# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

pkgname=babel-cli
pkgver=6.26.0
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
provides=('nodejs-babel-cli')
conflicts=('nodejs-babel-cli' 'openbabel')
replaces=('nodejs-babel-cli')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz"
        LICENSE)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('c1abbe043b507ee481190f4fcf314bdd112f47d4b19dde0b2b0b5c1c088f8e1ba003b2bff34be91d769f8fe3b96c0a893ae49e7e33b1e59267352476275430e9'
            'fc5471459770967e2632efb110b8370caa3424b7d87c1180f49605f7e6b1593746888b6c99c713c0c3046c5833c01c1a15bf3d549a343eaa077885bcc9f3d4e3')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
