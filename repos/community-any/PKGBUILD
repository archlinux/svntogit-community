# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=zim
pkgver=0.73.1
pkgrel=1
pkgdesc="A WYSIWYG text editor that aims at bringing the concept of a wiki to the desktop."
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://zim-wiki.org/"
depends=('python' 'python-gobject' 'ttf-font' 'gtk3' 'python-xdg')
checkdepends=('xorg-server-xvfb')
optdepends=('bzr: Version Control plugin'
            'git: Version Control plugin'
            'mercurial: Version Control plugin'
            'gnuplot: Insert Gnuplot plugin'
            'ditaa: Insert Ditaa plugin'
            'graphviz: Insert Diagram plugin'
            'xdot: Link Map plugin'
            'gtkspell3: Spell Checker plugin'
            'r: Insert GNU R Plot plugin'
            'scrot: Insert Screenshot plugin'
            'lilypond: Insert Score plugin'
            'gtksourceview3: Source View plugin'
            'texlive-bin: Insert Equation plugin')
source=("https://www.zim-wiki.org/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('7fbc5242c9e96d13d9d43d341db9dc94')


check() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	if [[ "$pkgver" != "0.73.1" ]]; then
		# Test are broken starting with 0.71.0, but no fix is in sight yet.
		# Skip the tests since I don't have the time to fix them myself.
		# When the next version comes out, either update the if condition or be
		# happy that they are fixed.
		# https://github.com/zim-desktop-wiki/zim-desktop-wiki/issues/814
		xvfb-run -w0 ./test.py
	fi
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	python setup.py install --root="${pkgdir}" --optimize=1

	install -dm755 "$pkgdir/usr/share/icons/"
	cp -r xdg/hicolor/ "$pkgdir/usr/share/icons/"
}
