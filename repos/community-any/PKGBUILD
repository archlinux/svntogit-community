# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

_pyname=json-stream
pkgname=python-${_pyname}
pkgver=2.2.0
pkgrel=2
pkgdesc='Simple streaming JSON parser'
arch=(any)
url="https://github.com/daggaz/json-stream"
license=('MIT')
depends=('python')
makedepends=(python-build python-installer python-setuptools python-wheel)
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
b2sums=('ce3687b99a6993bbd282005ade31da644058559be70b4dae77f3989f256898059ad6edaf49b8e8c6b22981fd1f31fa70253815111b8cbb03d197da3638a181bb')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
