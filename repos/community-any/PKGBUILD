# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov

pkgname=python-txaio
pkgver=18.8.1
pkgrel=3
pkgdesc='Compatibility API between asyncio/Twisted/Trollius'
arch=('any')
url="https://github.com/crossbario/txaio"
license=('MIT')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-twisted')
source=("https://pypi.io/packages/source/t/txaio/txaio-$pkgver.tar.gz"
        'make-pytest-happy.patch')
sha512sums=('5aa0024b32211534b0c673da13b092ba08e15195b3b016bc21104618605d5c0b49096fa2795e13d9d5c4247defa1d72f903cbcc8d00a21359825224faab64b64'
            'bc78ad2ba52d8cd9eb8a2743be93b88b3e162ede50e848e16a7db60f9c0a47115cd8fa92f4151c1d8377852524c07483a18d3d38e349347538a7bde1c56afe27')

prepare() {
  # This tests whether pip can install the sdist, and is completely broken
  # except in their boutique setup. They won't fix it.
  # https://github.com/crossbario/txaio/issues/77#issuecomment-246276723
  rm txaio-$pkgver/test/test_packaging.py

  # https://github.com/crossbario/txaio/issues/140
  patch -Np1 -d txaio-$pkgver <make-pytest-happy.patch

  # Fix tests on Python 3.7 (https://github.com/crossbario/txaio/issues/134)
  sed -i 's/asyncio\.test_utils/test.test_asyncio.utils/' txaio-$pkgver/test/*.py
}

build() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/txaio-$pkgver
  python -m pytest
}

package() {
  cd txaio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
