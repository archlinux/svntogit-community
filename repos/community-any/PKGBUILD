# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Emil Lundberg <emil@emlun.se> <emil@yubico.com>

pkgname=python-fido2
_tag='d363aefc2da174a361a772576467551135742b5a' # git rev-parse "${pkgver}"
pkgver=1.0.0
pkgrel=2
pkgdesc='Library for FIDO 2.0, including communication with a device over USBA'
url='https://github.com/Yubico/python-fido2'
license=('custom:BSD-2-clause')
arch=('any')
depends=('python')
makedepends=('git' 'python-build' 'python-installer' 'python-poetry-core')
_tarball_basename="fido2-${pkgver}"
validpgpkeys=('20EE325B86A81BCBD3E56798F04367096FBA95E8') # Dain Nilsson <dain@yubico.com>
source=("git+https://github.com/Yubico/${pkgname}.git#tag=${_tag}?signed")
sha256sums=('SKIP')

# Tests don't currently work in release tarballs
# check() {
#   cd "${srcdir}/${pkgname}"
#   python setup.py test
# }

build() {
  cd "${srcdir}/${pkgname}"

  python -m build --wheel --no-isolation
}

package() {
  cd "${srcdir}/${pkgname}"

  python -m installer --destdir="$pkgdir" dist/*.whl

  install -D -m0644 'COPYING' "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
