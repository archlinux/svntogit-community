# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Carl George < arch at cgtx dot us >

pkgbase=python-smmap
pkgname=("python-smmap" "python2-smmap")
pkgver=0.9.0
pkgrel=4
pkgdesc="A pure git implementation of a sliding window memory map manager"
arch=("any")
url="https://github.com/gitpython-developers/smmap"
license=("BSD")
makedepends=("python-setuptools" "python2-setuptools")
checkdepends=("python-nose" "python2-nose")
source=("https://pypi.python.org/packages/source/s/smmap/smmap-${pkgver}.tar.gz"
        "https://raw.githubusercontent.com/gitpython-developers/smmap/v${pkgver}/LICENSE")
sha256sums=('0e2b62b497bd5f0afebc002eda4d90df9d209c30ef257e8673c90a6b5c119d62'
            '88e9d93f708d110b328a834302dd1d5c6afbda530e7721d15d80b3511d86f235')

prepare() {
    cp -a "${srcdir}/smmap-${pkgver}"{,-py2}
}

build() {
    cd "${srcdir}/smmap-${pkgver}"
    python setup.py build

    cd "${srcdir}/smmap-${pkgver}-py2"
    python2 setup.py build
}

check() {
    cd "${srcdir}/smmap-${pkgver}"
    nosetests3
    
    cd "${srcdir}/smmap-${pkgver}-py2"
    nosetests2
}

package_python-smmap() {
    depends=("python")

    cd "${srcdir}/smmap-${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-smmap() {
    depends=("python2")

    cd "${srcdir}/smmap-${pkgver}-py2"
    python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
