# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=python-voluptuous-serialize
pkgver=2.5.0
pkgrel=3
pkgdesc='Convert voluptuous schemas to JSON'
arch=(any)
url=https://github.com/balloob/voluptuous-serialize
license=(APACHE)
depends=(python-voluptuous)
makedepends=(
  git
  python-setuptools
)
checkdepends=(python-pytest)
_tag=bea77030a4a8b782eb19d0391fb15842ff6cbd40
source=(git+https://github.com/balloob/voluptuous-serialize.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd voluptuous-serialize
  git describe --tags
}

build() {
  cd voluptuous-serialize
  python setup.py build
}

check() {
  cd voluptuous-serialize
  PYTHONPATH=$PWD/build/lib pytest
}

package() {
  cd voluptuous-serialize
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
