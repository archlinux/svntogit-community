# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-testing
pkgname=('python-zope-testing' 'python2-zope-testing')
pkgver=4.7
pkgrel=5
pkgdesc="Zope testing helpers"
arch=('any')
url="https://github.com/zopefoundation/zope.testing"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-exceptions' 'python2-zope-exceptions')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.testing/archive/$pkgver.tar.gz")
sha512sums=('8d285e64a5ec0f7df8e12e8887246f76b14a6bf8c5e1febd83afbd2baff28bc7ce98bbc4fffae69678270219fab84849b05a892cbd94674b3925623d338b0176')

prepare() {
  cp -a zope.testing-$pkgver{,-py2}
}

build() {
  cd "$srcdir/zope.testing-$pkgver"
  python setup.py build

  cd "$srcdir/zope.testing-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.testing-$pkgver"
  python setup.py test

  cd "$srcdir/zope.testing-$pkgver-py2"
  python2 setup.py test
}

package_python-zope-testing() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions')

  cd "$srcdir/zope.testing-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-testing() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-exceptions')

  cd "$srcdir/zope.testing-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
