# Maintainer: Caleb Maclennan <caleb@alerque.com>

_pkgname=multivolumefile
pkgname=python-$_pkgname
pkgver=0.2.3
pkgrel=6
pkgdesc='Multi volume file wrapper library'
arch=(any)
url='https://github.com/miurahr/multivolume'
license=(LGPL)
depends=(python)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
checkdepends=(python-pytest
              python-hypothesis)
_archive="$_pkgname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pkgname::1}/$_pkgname/$_archive.tar.gz")
sha256sums=('a0648d0aafbc96e59198d5c17e9acad7eb531abea51035d08ce8060dcad709d6')

build() {
  cd "$_archive"
  python -m build -wn
}

check() {
  cd "$_archive"
  python -m pytest -rsx
}

package() {
  cd "$_archive"
  python -m installer -d "$pkgdir" dist/*.whl
}
