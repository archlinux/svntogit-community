# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: alphazo <alphazo@gmail.com>

pkgname=sshuttle
pkgver=0.73.20151209
_commit=2eeea9536afe87cbe21f9417768a6977c2bb11d1
pkgrel=1
pkgdesc='Transparent proxy server that forwards all TCP packets over ssh'
arch=('any')
url="https://github.com/sshuttle/sshuttle"
license=('GPL2')
depends=('python-setuptools' 'iptables' 'openssh' 'net-tools')
makedepends=('git' 'pandoc')
checkdepends=('python-pytest-runner' 'python-mock')
backup=('etc/sshuttle/tunnel.conf' 'etc/sshuttle/prefixes.conf')
source=("git+https://github.com/sshuttle/$pkgname.git#commit=$_commit"
        'sshuttle.service' 'prefixes.conf' 'tunnel.conf')
md5sums=('SKIP'
         'f2cd1660dcdb4e24b45b71e589da73f3'
         'e780f69851445f739ea76774bb3a3b17'
         '4137f5e6d5dd92b7f7f34a1bfdbda45c')

build() {
  cd $pkgname
  python setup.py build

  pandoc -s -r markdown -w man -o sshuttle.8 sshuttle/sshuttle.md
}

check() {
  cd $pkgname
  python setup.py ptr
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 sshuttle.8 "$pkgdir/usr/share/man/man8/sshuttle.8"

  install -d "$pkgdir/etc/sshuttle"
  install -m644 "$srcdir"/{tunnel.conf,prefixes.conf} "$pkgdir/etc/sshuttle"
  install -Dm644 "$srcdir/sshuttle.service" "$pkgdir/usr/lib/systemd/system/sshuttle.service"
}
