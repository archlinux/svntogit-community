# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sybil
pkgname=('python-sybil' 'python2-sybil')
pkgver=1.0.6
pkgrel=1
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cjw296/sybil/archive/$pkgver.tar.gz")
sha512sums=('6389f179afa6af9ffd23877cc7ea1b71b6431d3955262ec6707d65bcd45774e848a36c9cdbba323cca3bf016e22c7e31672059ee86f6556345311099f42a78da')

prepare() {
  cp -a sybil-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py build

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py pytest

  cd "$srcdir"/sybil-$pkgver-py2
  python2 setup.py pytest
}

package_python-sybil() {
  depends=('python')

  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sybil() {
  depends=('python2')

  cd sybil-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
