# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-kaitaistruct
pkgname=('python-kaitaistruct' 'python2-kaitaistruct')
pkgver=0.7
pkgrel=1
pkgdesc="Kaitai Struct declarative parser generator for binary data: runtime library for Python"
arch=('any')
license=('MIT')
url="http://kaitai.io"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kaitai-io/kaitai_struct_python_runtime/archive/$pkgver.tar.gz")
sha512sums=('b7b5b1455107a4129615db9fdcf396c7c4655ababbf6c8ccd82a63a9fd93cd549a04f183b235467d7dc989b522f3a1939bb248808c5fb88e8c723ce9b2068047')

prepare() {
  cp -a kaitai_struct_python_runtime-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/kaitai_struct_python_runtime-$pkgver
  python setup.py build

  cd "$srcdir"/kaitai_struct_python_runtime-$pkgver-py2
  python2 setup.py build
}

package_python-kaitaistruct() {
  depends=('python')

  cd kaitai_struct_python_runtime-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-kaitaistruct() {
  depends=('python2')

  cd kaitai_struct_python_runtime-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
