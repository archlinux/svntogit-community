# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hacking
pkgver=4.1.0
pkgrel=1
arch=('any')
pkgdesc='OpenStack Hacking Guideline Enforcement'
url='https://docs.openstack.org/developer/hacking'
license=('Apache')
depends=('flake8')
makedepends=('python-pbr' 'python-setuptools')
checkdepends=('python-ddt' 'python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/hacking/archive/$pkgver.tar.gz")
sha512sums=('25d6a76bdf0b6243cb9b28e64aaa8c9e80598cb44d794ec1714fa518a6e6d19a291a1c711a71dd0bdcb79d5b3135b791bfb5fed94ab97af095fcd88209d62ba4')

export PBR_VERSION=$pkgver

prepare() {
  cd hacking-$pkgver
  sed -i 's/<.*//' requirements.txt
}

build() {
  cd "$srcdir"/hacking-$pkgver
  python setup.py build
}

check() {
  cd hacking-$pkgver
  stestr run
}

package_python-hacking() {
  cd hacking-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
