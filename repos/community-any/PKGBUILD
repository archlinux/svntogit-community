# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

_gemname=rspec-core
pkgname=ruby-${_gemname}
pkgver=3.8.0
pkgrel=1
pkgdesc='RSpec runner and formatters'
url='https://github.com/rspec/rspec-core'
arch=('any')
license=('MIT')
depends=('ruby-rspec-support')
options=('!emptydirs')
source=(https://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
noextract=(${_gemname}-${pkgver}.gem)
sha512sums=('c3163b4bc8463c9b6ac86921fb315d000b944f7b825f489ebc264473aa3ee8a61eb7bec92b7b528da40a051a83ef14d341790087d21746b1da142981f5d16ab0')

package() {
  local _gemdir="$(gem environment gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
