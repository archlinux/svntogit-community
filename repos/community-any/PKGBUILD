# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-subtests
pkgver=0.9.0
_commit=bc84f33580ebf0594267ab902f8c7d832c58a9be
pkgrel=2
pkgdesc='unittest subTest() support and subtests fixture'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-subtests'
depends=('python-pytest')
makedepends=('git' 'python-setuptools-scm')
source=("git+https://github.com/pytest-dev/pytest-subtests.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd pytest-subtests
  python setup.py build
}

check() {
  cd pytest-subtests
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-subtests
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
