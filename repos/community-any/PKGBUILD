# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gramps
pkgver=3.3.0
pkgrel=3
pkgdesc="A genealogy program, which helps you track your family tree"
arch=('any')
url="http://gramps-project.org/"
license=('GPL')
depends=('pygtk' 'librsvg' 'desktop-file-utils' 'shared-mime-info'
	 'gnome-icon-theme' 'xdg-utils' 'python2-bsddb')
makedepends=('intltool')
optdepends=('python2-gnomevfs: mime functions support'
            'python2-gtkspell: spellchecking support'
            'pywebkitgtk: html rendering support'
            'graphviz: graph exporting support'
            'ttf-freefont: more font support')
#            'python-osmgpsmap: openstreetmap support'
#            'pyexiv2: exif support'
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/gramps/Stable/$pkgver/gramps-$pkgver-1.tar.gz)
md5sums=('c28d387a5c50eac6813d91ac684028d3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s|register('preferences.use-bsddb3', False)|register('preferences.use-bsddb3', True)|" src/config.py
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-mime-install \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
