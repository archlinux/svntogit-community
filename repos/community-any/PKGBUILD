# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=virt-firmware
pkgver=23.4
pkgrel=2
pkgdesc="Collection of tools for edk2 firmware images"
arch=(any)
url="https://gitlab.com/kraxel/virt-firmware"
license=(GPL2)
depends=(
  python
  python-crc32c  # undeclared runtime dependency: https://gitlab.com/kraxel/virt-firmware/-/issues/4
  python-cryptography
  python-pefile
  python-setuptools
)
makedepends=(
  python-build
  python-installer
  python-wheel
)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('a4724cdcf03175aa30e15f7ad515eefb28ad30ae21a3a70a01782ff0a281b824ea0d820d86313f7568d375dfd3ecc4e79acfd35ea00761d2df4e2aad56b92203')
b2sums=('a0fa60d09c33fec796af0aeb37d4bc3a24be09f16b61ab90473a5544c51ad3514ec7715b678b9c06b98d0a850c2c8e259ef424c278ad8c102b0cf63e4f34c3ed')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $pkgname-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"

  pytest -vv tests/tests.py
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 man/virt-*.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
