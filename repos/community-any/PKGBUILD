# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz@mail.ru>

pkgname=ruby-mime-types
pkgver=2.2
pkgrel=1
pkgdesc='MIME Content-Type database'
arch=('any')
url='http://mime-types.rubyforge.org'
license=('GPL2' 'MIT' 'PerlArtistic')
depends=('ruby')
options=('!emptydirs')
source=("http://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('fb20b5fb6b25c2b8abbf74b21b82c6e14ae1e3ed5146ec23d305333c9c360bc6')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} ${pkgname#*-}-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s ${_gemdir}/gems/${pkgname#*-}-${pkgver}/Licence.rdoc "${pkgdir}"/usr/share/licenses/${pkgname}/
  ln -s ${_gemdir}/gems/${pkgname#*-}-${pkgver}/docs/COPYING.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
