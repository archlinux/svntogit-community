# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elasticsearch
pkgver=7.7.0
pkgrel=1
arch=('any')
pkgdesc='Python client for Elasticsearch'
url='https://github.com/elastic/elasticsearch-py'
license=('Apache')
depends=('python-urllib3')
makedepends=('python-setuptools' 'git')
checkdepends=('python-nose' 'python-requests' 'python-coverage' 'python-mock' 'python-pyaml'
              'python-nosexcover' 'python-numpy' 'python-pandas')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elastic/elasticsearch-py/archive/$pkgver.tar.gz"
        git+https://github.com/elastic/elasticsearch.git)
sha512sums=('6da382c5056f61dde2bf509c902a4ca33548ee937d037dc977e8c8864ebc09385f630b5c99ab8b24b69390773a241786495d1c01a5684ea3892d834e7a1550b1'
            'SKIP')

prepare() {
  sed -i 's/, "certifi"//' elasticsearch-py-$pkgver/setup.py
  sed -i 's|CA_CERTS = None|CA_CERTS = "/etc/ssl/certs/ca-certificates.crt"|' elasticsearch-py-$pkgver/elasticsearch/connection/http_urllib3.py
}

build() {
  cd elasticsearch-py-$pkgver
  python setup.py build
}

check() {
  cd elasticsearch-py-$pkgver
  TEST_ES_REPO="$srcdir"/elasticsearch python setup.py test
}

package() {
  cd elasticsearch-py-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
