# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=py7zr
pkgname=python-py7zr
pkgver=0.16.2
pkgrel=1
pkgdesc="Pure python 7-zip library"
arch=('any')
url="https://github.com/miurahr/${_pkgname}"
license=('LGPL')
depends=('python-pycryptodomex' 'python-texttable' 'python-multivolumefile' 'python-pyppmd' 'python-pyzstd' 'python-brotlicffi')
optdepends=('python-zstandard: zstd compression')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-benchmark' 'python-pytest-timeout' 'pyannotate' 'p7zip' 'python-py-cpuinfo')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('e741f0271ebb12551e4a4b57bf01fca226e7136ff301d15c083cd262f8c4a0d6')
b2sums=('75b74735155b93bbaf835c44a607b8c7b321b8663b873007f7197685d4acdc34328430358eb0f8a9902a48d0c27aeb07a0f6edaef1ddd30d48ed2b4316d4e007')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest -rsx -m 'not benchmark'
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
