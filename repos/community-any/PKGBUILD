# Maintainer: Florian Pritz <flo@xinu.at>

pkgname=perl-rename
pkgver=1.14
pkgrel=1
pkgdesc="Renames multiple files using Perl regular expressions."
arch=(any)
url="https://search.cpan.org/~pederst/rename/"
license=('unknown')
depends=(perl)
options=(!emptydirs)
source=(https://search.cpan.org/CPAN/authors/id/P/PE/PEDERST/rename-$pkgver.tar.gz)
sha256sums=('4d19e5cb8fb09fe35e6df69ae07132cf621b0b2a82f54149091bce630642adbd')

build() {
    cd "$srcdir/rename-$pkgver"

    perl Makefile.PL PREFIX=/usr INSTALLDIRS=vendor
    make
}

package() {
    cd "$srcdir/rename-$pkgver"

    make DESTDIR="$pkgdir/" install
    install -d "$pkgdir/usr/bin/"
    ln -s vendor_perl/rename "$pkgdir/usr/bin/perl-rename"
    ln -s /usr/share/man/man1/rename.1p.gz "$pkgdir/usr/share/man/man1/perl-rename.1p.gz"
}
