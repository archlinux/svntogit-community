# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=uvicorn
pkgver=0.12.2
pkgrel=1
pkgdesc='The lightning-fast ASGI server'
arch=('any')
url='https://github.com/encode/uvicorn'
license=('BSD')
depends=('python' 'python-click' 'python-h11' 'python-websockets' 'python-httptools' 'python-uvloop')
optdepends=('python-watchgod: watchgod reload')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('cc94f9482f4417c6cc79283c0ae1b9d658df9131f3d70d52ae4f1c03fdd7aef28ed902a722827529cb97e302f90bfe208d966546f6484bc01762e8f691121c49')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
