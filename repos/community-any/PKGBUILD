# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgbase='python-cheroot'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cheroot')
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=8.5.2
pkgrel=4
arch=('any')
url="https://github.com/cherrypy/cheroot"
license=('MIT')
depends=('python-six' 'python-jaraco')
makedepends=('python-six' 'python-setuptools-scm'
             'tree' 'python-tox' 'python-jaraco' 'git')
checkdepends=('python-pytest' 'python-pytest-xdist'
              'python-requests' 'python-pyopenssl' 'python-trustme')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        "LICENSE"
        "disable-broken-tests.patch")
sha512sums=('245c4157f5e11741d94375099878ef5b31945412a2b02961502e762b9770fd968f4f977fe2d74744aa6596c668079d7f07b6f129fa11438783a95d3a5ee088fa'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944'
            '488766eeb0ce4e1c95e1848a4d89aeb190317c634b2894dcc696cb4af64bae748554154ea76cc34cf0b666d507c42e631d0e8759a6ed7669fe49bf7f8ef5b9a9')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p1 -N -i "${srcdir}/disable-broken-tests.patch"
  # don't use python-coverage
  sed -i '/^    --cov/d' pytest.ini

  # git-archive support is not needed since we use PyPI tarballs
  sed -i '/setuptools_scm_git_archive/d' setup.cfg

  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  # tox doesn't really provide any meaningful results for downstream packaging
  # TODO: properly run the tests with distro packages

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  # tox doesn't really provide any meaningful results for downstream packaging
  # TODO: properly run the tests with distro packages
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
