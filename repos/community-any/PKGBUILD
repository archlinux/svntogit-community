# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

pkgname='vcspull'
pkgver=1.20.1
pkgrel=1
pkgdesc='Synchronize projects via yaml/json manifest. Built using `libvcs`'
arch=('any')
url='https://vcspull.git-pull.com/'
license=('MIT')
depends=('python' 'python-colorama' 'python-libvcs' 'python-shtab' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-poetry-core' 'python-typing_extensions')
checkdepends=('git' 'python-pytest' 'python-pytest-rerunfailures')
optdepends=(
  'bash-completion: for command line completion in bash'
)
source=("https://github.com/vcs-python/vcspull/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a9831866dc967b77853e9dad48a53bf3a6b3c484e3a5d8595aca36cb4a2f98dfef77be9311a8a84180ed04191fdfa8848c83d219e13d59cae9f11ec6da26b82a')
b2sums=('d9fba4dabdc0cbcc832b01309dcb083df90fdf06d479116eadddf621506751a9ef6481496e78bac3fd4431d244b8568e07c0e48d691ebb689343b9f819cb7456')

build() {
  cd  "${pkgname}-${pkgver}"

  local _site_packages="$(python -c "import site; print(site.getsitepackages()[0])")"

  python -m build --wheel --skip-dependency-check --no-isolation
  python -m installer --destdir=tmp_install dist/*.whl

  export PYTHONPATH="tmp_install${_site_packages}:${PYTHONPATH}"
}

check() {
  cd  "${pkgname}-${pkgver}"

  pytest -vv tests
}

package() {
  cd  "${pkgname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose --directory --mode=0755 \
      "${pkgdir}/usr/share/bash-completion/completions" \
      "${pkgdir}/usr/share/zsh/site-functions" \
      "${pkgdir}/etc/profile.d"

  shtab --shell=bash --error-unimportable vcspull.cli.create_parser > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  shtab --shell=zsh --error-unimportable vcspull.cli.create_parser > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  shtab --shell=tcsh --error-unimportable vcspull.cli.create_parser > "${pkgdir}/etc/profile.d/${pkgname}.completion.csh"

  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.md CHANGES MIGRATION --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
  install --verbose -D --mode=0644 examples/* --target-directory "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: tabstop=2 shiftwidth=2 expandtab:
