# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yamllint
pkgver=1.30.0
pkgrel=1
pkgdesc='Linter for YAML files'
arch=('any')
url=https://github.com/adrienverge/yamllint
license=('GPL3')
depends=('python-pathspec' 'python-yaml')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-nose')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('4f58f323aedda16189a489d183ecc25c66d7a9cc0fe88f61b650fef167b13190')
b2sums=('670115e61b4c744067d658474ef645a6f10351b898a544800c657bb2adf949167448d6cb6feeabe1dc3096124b862a0ff93d62cbb3428bef3dd5b42c33e9c750')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/setuptools/d' setup.cfg
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  python -m unittest discover tests
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
