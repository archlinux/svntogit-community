# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-quotelike
pkgver=0.010
pkgrel=1
pkgdesc="Parse Perl string literals and string-literal-like things"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-QuoteLike"
depends=('perl-ppi')
makedepends=('perl-ppix-regexp')
optdepends=('perl-ppix-regexp: to find variables inside nested regular expressions')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-QuoteLike-$pkgver.tar.gz")
sha512sums=('91a09cc9218e6cfa3deb668b18142e1743459c64463c24a57b8b3ff2dd9b18efb667731ed19cc8173e2c09b931e7e01c281434956a7dc63ddb99f3ff630cc7a7')

build() {
  cd PPIx-QuoteLike-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-QuoteLike-$pkgver
  make test
}

package() {
  cd PPIx-QuoteLike-$pkgver
  make DESTDIR="$pkgdir" install
}
