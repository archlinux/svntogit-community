# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: M0Rf30

pkgname=clonezilla
pkgver=3.27.16
pkgrel=1
pkgdesc="ncurses partition and disk imaging/cloning program"
arch=('any')
url="http://clonezilla.org"
license=('GPL2')
depends=('drbl' 'partclone' 'ntfs-3g' 'partimage' 'pigz' 'sshfs' 'parted'
         'gptfdisk' 'dosfstools' 'gzip' 'bzip2' 'pbzip2' 'lbzip2' 'lrzip'
         'xz' 'pixz' 'lzop' 'ecryptfs-utils')
backup=('etc/drbl/drbl-ocs.conf')
source=(http://free.nchc.org.tw/drbl-core/src/stable/$pkgname-$pkgver.tar.xz
        usrbin.patch)
md5sums=('4beae63adc5d75632b2b2a07f0f095bc'
         'a976c9209f7bbccd352a9d0a5c8b41ae')

prepare() {
  cd $pkgname-$pkgver

  patch -Np0 -i "$srcdir"/usrbin.patch
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install  
}
