# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org.
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-config
pkgname=(python-oslo-config python2-oslo-config)
_pypiname=oslo.config
pkgver=3.7.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
options=('!emptydirs')
source=("git+https://git.openstack.org/openstack/oslo.config#tag=$pkgver")
md5sums=('SKIP')

prepare(){
  cp -a $_pypiname{,-py2}
  find $_pypiname-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
  sed -i '/argparse/d' "$_pypiname"{,-py2}/requirements.txt
}

build(){
  cd "$srcdir/$_pypiname"
  python setup.py build

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py build
}

package_python-oslo-config() {
  depends=('python')

  cd "$srcdir/$_pypiname"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-config() {
  depends=('python2')

  cd "$srcdir/$_pypiname-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv $pkgdir/usr/bin/oslo-config-generator $pkgdir/usr/bin/oslo-config-generator2
}

# vim:set ts=2 sw=2 et:
