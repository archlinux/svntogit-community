# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-milc
pkgver=1.6.5
pkgrel=1
pkgdesc="Opinionated Batteries-Included Python 3 CLI Framework."
arch=("any")
url="https://milc.clueboard.co/#/"
license=("MIT")
depends=("python-appdirs" "python-argcomplete" "python-colorama" "python-halo")
makedepends=("python-setuptools")
checkdepends=("python-semver" "python-nose2")
source=("$pkgname-$pkgver.tar.gz::https://github.com/clueboard/milc/archive/${pkgver}.tar.gz")
sha256sums=('c2ec7ca643f7bc59ed22b510cdd2ff9fb28c1778a9b98021c4386bcb6854ea08')

build() {
    cd "${pkgname#python-}-${pkgver}"
    python setup.py build
}

check() {
    cd "${pkgname#python-}-${pkgver}"
    rm tests/test_script*.py
    nose2
}

package() {
    cd "${pkgname#python-}-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=4 sw=4 et:
