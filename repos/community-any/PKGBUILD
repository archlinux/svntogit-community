# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

_pkg=GnuPG-Interface
pkgname=perl-${_pkg,,}
pkgver=1.00
pkgrel=2
pkgdesc="Object-oriented methods for interacting with GnuPG"
arch=('any')
url="https://metacpan.org/release/${_pkg}"
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('gnupg' 'perl-math-bigint' 'perl-moo' 'perl-moox-handlesvia' 'perl-moox-late')
makedepends=('perl-module-install')
source=("https://cpan.metacpan.org/authors/id/J/JE/JESSE/${_pkg}-${pkgver}.tar.gz")
sha512sums=('82436543cc3fff19c10fce2be70db5c056e1e186c283127e4e197aa213dc70b5d6d5b61ccd66b577c411a822aef8f2c02057c996e561d5b192fcc2d96a677f07')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    # 19 tests failing, likely because we are in a chroot
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
