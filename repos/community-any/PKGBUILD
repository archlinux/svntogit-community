# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=python-wiktionaryparser
pkgver=0.0.97.20210613
_commit=71f396ef6e915b3d1461a41e8a7aec35063d30bf
pkgrel=4
pkgdesc="A tool to parse word data from wiktionary.com into a JSON object"
url="https://github.com/Suyash458/WiktionaryParser"
license=('MIT')
arch=('any')
depends=('python-beautifulsoup4' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-deepdiff' 'python-parameterized' 'python-pytest')
source=("https://github.com/Suyash458/WiktionaryParser/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('533219d357c10e893cfc5504bd96dfbc34c1597afe98c4d20f7d49c1b66a8975636200f7c161ac116464df3a02ca9524aa63763c5dd804b516bf89274d4e94bb')

prepare() {
  cd WiktionaryParser-$_commit
  sed -i 's/import mock/from unittest import mock/' tests/test_core.py
}

build() {
  cd WiktionaryParser-$_commit
  python setup.py build
}

check() {
  cd WiktionaryParser-$_commit
  pytest
}

package() {
  cd WiktionaryParser-$_commit
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/

  rm -r "$pkgdir"/usr/{readme,requirements,testOutput}

  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%.*}/site-packages/tests"
}
