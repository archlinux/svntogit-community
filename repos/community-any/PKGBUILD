# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Platon Pronko <platon7pronko@gmail.com>

pkgname=python-lsp-server
pkgver=1.6.0
pkgrel=2
pkgdesc="Fork of the python-language-server project, maintained by the Spyder IDE team and the community"
arch=(any)
url="https://github.com/python-lsp/python-lsp-server"
license=(MIT)
depends=(python-jedi python-lsp-jsonrpc python-pluggy python-ujson python-setuptools python-websockets python-docstring-to-markdown)
makedepends=(python-build python-installer python-setuptools-scm python-wheel)
optdepends=(
    'python-mccabe: complexity checking'
    'python-pycodestyle: linter - style checking'
    'python-pydocstyle: linter - docstring style checking'
    'python-pyflakes: linter to detect various errors'
    'python-pylint: code linting'
    'python-rope: for completions and renaming'
    'autopep8: code formatting'
    'flake8: error checking'
    'yapf: code formatting (preferred)'
    'python-whatthepatch: required for yapf support'
)
checkdepends=(
    python-pytest
    python-flaky
    python-mccabe
    python-pycodestyle
    python-pydocstyle
    python-pyflakes
    python-pylint
    python-rope
    autopep8
    flake8
    yapf
    python-whatthepatch
    python-matplotlib
    python-numpy
    python-pandas
    python-pyqt5
)
source=(https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('d75cdff9027c4212e5b9e861e9a0219219c8e2c69508d9f24949951dabd0dc1b')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|autopep8>=1.6.0,<1.7.0|autopep8>=1.6.0|' -i pyproject.toml
  sed 's|flake8>=5.0.0,<5.1.0|flake8>=5.0.0|' -i pyproject.toml
  sed 's|mccabe>=0.7.0,<0.8.0|mccabe>=0.7.0|' -i pyproject.toml
  sed 's|pycodestyle>=2.9.0,<2.10.0|pycodestyle>=2.9.0|' -i pyproject.toml
  sed 's|pyflakes>=2.5.0,<2.6.0|pyflakes>=2.5.0|' -i pyproject.toml
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build -wn
}

check() {
  cd ${pkgname}-${pkgver}
  # Disable coverage
  sed -i '/--cov/d' pyproject.toml
  # Numpy completions broken with numpy 1.23
  pytest -vv --color=yes -k 'not test_numpy_completions'
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer -d="${pkgdir}" dist/*.whl
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
