# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-cucumber-cucumber-expressions
pkgver=16.0.0
pkgrel=1
pkgdesc="Cucumber Expressions - a simpler alternative to Regular Expressions"
arch=(any)
url='https://github.com/cucumber/cucumber-expressions/tree/main/ruby'
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rspec)
options=(!emptydirs)
source=(https://github.com/cucumber/cucumber-expressions/archive/v$pkgver/cucumber-cucumber-expressions-$pkgver.tar.gz)
sha256sums=('13efac2b59d9b1605de52c9cb50a26bf0713233b8454e7394843c96378bd168e')

prepare() {
  cd cucumber-expressions-$pkgver/ruby
  sed -r -e 's|git ls-files -- spec/\*|echo|' -i cucumber-cucumber-expressions.gemspec
  sed 's|git ls-files|find -type f|' -i cucumber-cucumber-expressions.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-expressions-$pkgver/ruby
  gem build cucumber-cucumber-expressions.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-cucumber-expressions-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd cucumber-expressions-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd cucumber-expressions-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
