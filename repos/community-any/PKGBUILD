# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>
# Contributor: Brecht Machiels <brecht at mos6581 dot org>

pkgname=pyprof2calltree
pkgver=1.4.5
pkgrel=8
pkgdesc="Help visualize profiling data from cProfile with kcachegrind"
url="https://pypi.python.org/pypi/pyprof2calltree/"
arch=('any')
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("https://pypi.io/packages/source/p/pyprof2calltree/pyprof2calltree-$pkgver.tar.gz")
sha512sums=('9ad50a5c00881cbd8c361726b16fcb407c899c8dfe201769eddf10c3541531491f49cd5a08658b70d1e85e4c00d9a89801d600ab4f7d9906d357a0de82784f9e')

prepare() {
  sed -n '/# Copyright/,/# THE SOFTWARE\./p' pyprof2calltree-$pkgver/pyprof2calltree.py > LICENSE
}

build() {
  cd "$srcdir"/pyprof2calltree-$pkgver
  python setup.py build
}

package() {
  cd pyprof2calltree-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
