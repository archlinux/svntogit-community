# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Byron Clark <byron@theclarkfamily.name>
# Contributor: Guan 'kuno' Qing <neokuno AT gmail DOT com>

pkgname=python-couchdb
pkgver=1.2
_commit=44ec56ced41d507fce4237d01ea467f7505f58e1
pkgrel=2
pkgdesc="Python library for working with CouchDB"
url="https://github.com/djc/couchdb-python/"
license=('BSD')
arch=('any')
depends=('python-setuptools')
checkdepends=('pifpaf' 'couchdb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/djc/couchdb-python/archive/$pkgver.tar.gz")
sha512sums=('eaa672a820b6dfd8ffcc5b8aeba43e95f90249746127c2d30907b5cea847a3e4226906dab187040a75bd4c0952ff6eefd1ec8593607eac208fc44ecf27ea77d5')

build() {
  cd couchdb-python-$pkgver
  python setup.py build
}

check() {
  # New couchdb doesn't provide /usr/bin/couchdb
  export PATH="$PATH:/usr/lib/couchdb/bin"

  cd couchdb-python-$pkgver
  pifpaf run couchdb python setup.py test || warning "python 3 tests failed"
}

package() {
  cd couchdb-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -D -m644 "COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
