# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-whelk
pkgname=('python-whelk' 'python2-whelk')
pkgver=2.7
pkgrel=2
pkgdesc="Easy access to shell commands from python"
arch=('any')
license=('custom:zlib')
url="https://pypi.python.org/pypi/whelk"
makedepends=('python' 'python2' 'git')
source=("git+https://github.com/seveas/whelk.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a whelk{,-py2}
}

build() {
  cd "$srcdir"/whelk
  python setup.py build

  cd "$srcdir"/whelk-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/whelk
  python -munittest discover

  cd "$srcdir"/whelk-py2
  python2 -munittest discover
}

package_python-whelk() {
  depends=('python')

  cd whelk
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-whelk() {
  depends=('python2')

  cd whelk-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
