# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: leif_thande <leif.thande@gmail.com>
# Contributor: tranquility <trankas@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=atool
pkgver=0.39.0
pkgrel=8
pkgdesc='A script for managing file archives of various types'
arch=(any)
url="https://www.nongnu.org/$pkgname"
license=(GPL3)
depends=(file
         perl)
optdepends=('bzip2: support bzip2 compression'
            'cpio: support cpio archives'
            'gzip: support gzip compression'
            'lha: support lha, lharc, and similar archives'
            'xz: support lzma compressed archives'
            'lzop: support lzop compressed archives'
            'p7zip: support 7z cpmressed archives'
            'tar: support tar archives'
            'unace: support ace archives'
            'unrar: support rar archives'
            'zip: support creating zip archives'
            'unzip: support extracting zip archives')
_archive="$pkgname-$pkgver"
source=("https://savannah.nongnu.org/download/$pkgname/$_archive.tar.gz"{,.sig})
validpgpkeys=('4E6E6521EF300E30457D7AFB8AAAC30E7088EFCB') # Oskar Liljeblad <oskar@osk.mine.nu>
sha256sums=('aaf60095884abb872e25f8e919a8a63d0dabaeca46faeba87d12812d6efc703b'
            'SKIP')

build() {
  cd "$_archive"
  ./configure --prefix=/usr
}

package() {
  cd "$_archive"
  make DESTDIR="$pkgdir" install
}
