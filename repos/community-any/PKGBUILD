# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=sparsehash
pkgver=2.0.2
pkgrel=2
pkgdesc="Library that contains several hash-map implementations, including implementations that optimize for space or speed."
arch=("any")
url="http://code.google.com/p/sparsehash"
license=('BSD')
source=("http://sparsehash.googlecode.com/files/$pkgname-$pkgver.tar.gz")

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir/" install
	install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

sha256sums=('2ed639a7155607c097c2029af5f4287296595080b2e5dd2e2ebd9bbb7450b87c')
