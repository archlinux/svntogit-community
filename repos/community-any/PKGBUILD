# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslotest
pkgver=4.0.0
pkgrel=1
pkgdesc="Oslo test framework"
arch=('any')
url="https://github.com/openstack/oslotest"
license=('Apache')
depends=('python-fixtures' 'python-subunit' 'python-stestr' 'python-testscenarios'
         'python-testtools' 'python-mock' 'python-mox3' 'python-os-client-config'
         'python-debtcollector')
makedepends=('python-pbr')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslotest/archive/$pkgver.tar.gz")
sha512sums=('3c565a6a1a6ef9efa103f861852b978d93fdbd4babc03bd4556e4e4cafcf2bf5e310aa0edfd1182e16598ebe8be81147f7f8710f84cb4c43da8f56b389b25a33')

export PBR_VERSION=$pkgver

build() {
  cd oslotest-$pkgver
  python setup.py build
}

check() {
  cd oslotest-$pkgver
  stestr run
}

package() {
  cd "$srcdir"/oslotest-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
