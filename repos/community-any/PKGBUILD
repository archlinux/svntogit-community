# Maintainer: David Runge <dvzrv@archlinux.org>

_name=aiosmtpd
pkgname=python-aiosmtpd
pkgver=1.4.3
pkgrel=1
pkgdesc="An asyncio based SMTP server"
arch=(any)
url="https://github.com/aio-libs/aiosmtpd"
license=(Apache)
depends=(python-atpublic python-attrs python-typing-extensions)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-pytest-mock)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('f1aab75c80c1434a0944eb391fe2aaff82fe318f178aa1cd75bf6ca8589967aaccd8fcdd4f1619c0b9a89760f15d6207eef58a76adb2867493272f7d962d42a5')
b2sums=('e101efcab06869cf20e671df419155120fc5f35163096df27412038ec8e3d340accc4834db3067ef91c670ce4de1ba70283de9d804fb88f96971d2d325b45a7a')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv --ignore=aiosmtpd/qa/
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove unneeded stuff: https://github.com/aio-libs/aiosmtpd/issues/356
  rm -frv "$pkgdir/$_site_packages/$_name/"{docs,qa,tests}
}
