# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=torbrowser-launcher
pkgver=0.3.2
pkgrel=4
pkgdesc="Securely and easily download, verify, install, and launch Tor Browser in Linux"
url="https://github.com/micahflee/torbrowser-launcher"
depends=('python' 'python-pyqt5' 'python-requests' 'python-pysocks' 'python-gpgme' 'gnupg' 'tor')
makedepends=('python-setuptools' 'python-distro')
optdepends=('apparmor: support for apparmor profiles')
arch=('any')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/micahflee/torbrowser-launcher/archive/v${pkgver}.tar.gz"
        # fix for python >= 3.8
        "https://github.com/micahflee/torbrowser-launcher/pull/444/commits/66781d299d9dfd812c7aca9a04de7ea37f4d57e2.patch"
        # replace expired signing key: https://github.com/micahflee/torbrowser-launcher/pull/482
        "tor-browser-developers.pub::https://sources.archlinux.org/other/community/torbrowser-launcher/tor-browser-developers-2020-12-19.asc")
sha512sums=('26c5517723c730591ea5c4b81e30728e9db7323afcafc99d17a178d4300bad3df8731ffb063cc53fe189a3b5e2872395c913b9eea6595acac7f31ff4a1c5f105'
            'af8110da76a2d5763a158c436e712e1929b7524de129b4b3d09d7175df3ba45731a3924480415902cb5b27d1885bbc64c09fb4416367e2aa461e7c0ba3443b3e'
            '5ca6c3f2ecc7e48f3b79f7c2ad329b086f2e9b7b3835620123fe48cb6323dac76ad7b1ece673e87de952102c183f024deec19fcfb0325b1a89d9ea2a875ebc68')
b2sums=('b3e35f546ef84aec02a3b75068b08a6d60f9f41f6194d1bf25862c48780f77d72e4443e594add04c5a0d44cf01bcfc8c04d6d7d67c767c2cc75cc8b4a9c42f9c'
        '198da65696d6586f8fe279c8161f18e335ee8677c3a1026fda47933046313955648567d24fdfb01cbf533bab31a6526da0adb8861782079e151705011d9afd94'
        '2e0d6a1afbaed0b6860415a7f75f89392caa2c5bcf276b3f66f7f30ca669dd5cb7e78e9632a401c838b39e401a65df40e4c39bf9790cb0ca39dcc21460b2441d')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/66781d299d9dfd812c7aca9a04de7ea37f4d57e2.patch"
  cp "${srcdir}/tor-browser-developers.pub" share/torbrowser-launcher/tor-browser-developers.asc
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
