# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-qtconsole
pkgname=(python-qtconsole python2-qtconsole)
pkgver=4.3.1
pkgrel=2
pkgdesc="Qt-based console for Jupyter with support for rich media output"
arch=('any')
url="https://pypi.python.org/pypi/qtconsole"
license=('BSD')
depends=('python-ipykernel' 'python-jupyter_core' 'python-jupyter_client'
         'python-traitlets' 'python-pygments' 'python-pyzmq'
         'qt5-svg' 'python-pyqt5' 'python-sip')
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tgz::https://github.com/jupyter/qtconsole/archive/$pkgver.tar.gz")
md5sums=('45f15be486ea24522ac97f39a0cbcedc')

prepare() {
  cd "$srcdir"
  cp -r qtconsole-$pkgver python2-qtconsole-$pkgver
}

build() {
  cd "$srcdir"
}

package_python-qtconsole() {
  cd "$srcdir/qtconsole-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd examples
  # FS#47046 fix .desktop icon
  # FS#57556 fix .desktop icon again
  sed -i 's|^Icon=.*$|Icon=/usr/lib/python3.6/site-packages/qtconsole/resources/icon/JupyterConsole|' \
    jupyter-qtconsole.desktop
  install -Dm644 jupyter-qtconsole.desktop \
    "$pkgdir/usr/share/applications/jupyter-qtconsole.desktop"

}

package_python2-qtconsole() {
  depends=('python2-ipykernel' 'python2-jupyter_core' 'python2-jupyter_client'
           'python2-traitlets' 'python2-pygments' 'python2-pyzmq'
           'qt5-svg' 'python2-pyqt5' 'python2-sip')

  cd "$srcdir/python2-qtconsole-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/bin"
  mv jupyter-qtconsole jupyter-qtconsole-py2
}

