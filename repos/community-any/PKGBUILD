# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fernando Jim√©nez Solano <fjim@sdfeu.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgbase=python-pyacoustid
pkgname=('python-pyacoustid' 'python2-pyacoustid')
pkgver=1.1.5
pkgrel=1
pkgdesc='Bindings for Chromaprint acoustic fingerprinting and the Acoustid API'
arch=('any')
url='https://github.com/beetbox/pyacoustid'
license=('MIT')
depends=('chromaprint')
makedepends=('python-setuptools' 'python2-setuptools')
source=("pyacoustid-${pkgver}.tar.gz::https://github.com/beetbox/pyacoustid/archive/v${pkgver}.tar.gz"
        'https://raw.github.com/beetbox/beets/master/LICENSE')
sha256sums=('614e350d62af87c7cbc62c0bcaed9fc8330879772d050d245b7bd86556fb1b3f'
            '69b9fa8fe9700714fc1f002aa6bfef097e5422372979195ab48956cd48e20aaf')

prepare() {
  cp -r {,python-}pyacoustid-${pkgver}
  mv {,python2-}pyacoustid-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-pyacoustid-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-pyacoustid() {
  cd python-pyacoustid-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1' --skip-build
  install -Dm 644 ../LICENSE -t "${pkgdir}"/usr/share/licenses/python-pyacoustid/
}

package_python2-pyacoustid() {
  cd python2-pyacoustid-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1' --skip-build
  install -Dm 644 ../LICENSE -t "${pkgdir}"/usr/share/licenses/python2-pyacoustid/
}

# vim: ts=2 sw=2 et:
