# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=python-matplotlib
pkgname=('python2-matplotlib' 'python-matplotlib')
pkgver=1.2.1
pkgrel=1
pkgdesc="A python plotting library, making publication quality plots"
arch=('i686' 'x86_64')
url='http://matplotlib.org'
license=('custom')
makedepends=('python2-pytz' 'python2-numpy' 'python2-pyqt' 'python-pytz' 'python-numpy' 'pyqt'
             'tk' 'python-cairo' 'python2-cairo' 'python-dateutil' 'python2-dateutil'
             'python-gobject' 'python2-gobject' 'python-pyparsing' 'python2-pyparsing'
             'pygtk' 'python-six' 'ghostscript' 'texlive-bin')
optdepends=('pygtk: for use with the GTK or GTKAgg backend'
            'tk: used by the TkAgg backend'
            'ghostscript: usetex dependencies' 
            'texlive-bin: usetex dependencies')
source=("http://sourceforge.net/projects/matplotlib/files/matplotlib/matplotlib-${pkgver}/matplotlib-${pkgver}.tar.gz")
sha1sums=('82fc44d0047a713c1b0b1b4ea2503e6a41c57f98')

build() {
   cd "${srcdir}"/matplotlib-${pkgver}

   # use system python-six
   rm lib/six.py

   # remove internal copies of pyparsing
   rm -r lib/matplotlib/pyparsing_py{2,3}.py
   sed -i -e 's/matplotlib.pyparsing_py[23]/pyparsing/g' lib/matplotlib/{mathtext,fontconfig_pattern}.py

   cd ..
   cp -a matplotlib-${pkgver} matplotlib-${pkgver}-py3

   # Build python2
   cd matplotlib-${pkgver}
   for file in $(find . -name '*.py' -print); do
      sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python2|" \
             -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python2|" ${file}
   done

   python2 setup.py build

   # Build python3
   cd ../matplotlib-${pkgver}-py3
   for file in $(find . -name '*.py' -print); do
      sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python3|" \
             -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python3|" ${file}
   done

   python3 setup.py build
}

package_python2-matplotlib() {
   depends=('python2-pytz' 'python2-numpy' 'python2-cairo' 'python2-pyqt' 'python2-dateutil' 'python2-pyparsing')

   cd "${srcdir}"/matplotlib-${pkgver}
   python2 setup.py install -O1 --skip-build --root "${pkgdir}" --prefix=/usr

   install -dm755 "${pkgdir}"/usr/share/licenses/python2-matplotlib
   install -m 644 doc/users/license.rst "${pkgdir}"/usr/share/licenses/python2-matplotlib
}

package_python-matplotlib() {
   depends=('python-pytz' 'python-numpy' 'python-cairo' 'pyqt' 'python-dateutil' 'python-pyparsing')
   cd "${srcdir}"/matplotlib-${pkgver}-py3
   python3 setup.py install -O1 --skip-build --root "${pkgdir}" --prefix=/usr

   install -dm755 "${pkgdir}"/usr/share/licenses/python-matplotlib
   install -m 644 doc/users/license.rst "${pkgdir}"/usr/share/licenses/python-matplotlib
}
