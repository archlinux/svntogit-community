# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=cabextract
pkgver=1.4
pkgrel=3
pkgdesc="A program to extract Microsoft cabinet (.CAB) files"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.cabextract.org.uk/"
depends=('glibc')
DLAGENTS=('http::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -A "Mozilla/4.0" -o %o %u')
source=("http://www.cabextract.org.uk/${pkgname}-${pkgver}.tar.gz")
md5sums=('79f41f568cf1a3ac105e0687e8bfb7c0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
