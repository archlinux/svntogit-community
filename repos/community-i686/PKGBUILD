# $Id: PKGBUILD 83714 2013-02-04 16:20:13Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Orivej Desh <smpuj@bk.ru>

pkgname=libunibreak
pkgver=1.1
pkgrel=1
pkgdesc="An implementation of the line breaking algorithm as described in Unicode 5.2.0 Standard Annex 14, Revision 24"
arch=("i686" "x86_64")
url="http://vimgadgets.sourceforge.net/libunibreak/"
license=("custom:zlib/libpng")
depends=("glibc")
makedepends=()
replaces=('liblinebreak')
options=("!libtool")
source=("$pkgname-$pkgver.tar.gz::https://github.com/adah1972/libunibreak/archive/libunibreak_${pkgver//./_}.tar.gz")
md5sums=('80c933a03493bd920553c9380aa7f13d')

build() {
  cd "$srcdir/libunibreak-libunibreak_${pkgver//./_}"
  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/libunibreak-libunibreak_${pkgver//./_}"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
