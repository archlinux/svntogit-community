# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=3.0.11
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'deepin-tool-kit' 'gtk2' 'deepin-account-faces' 'deepin-api'
         'deepin-daemon' 'deepin-dbus-factory' 'startdde')
makedepends=('deepin-dock' 'qt5-tools' 'git')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/dde-control-center.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd dde-control-center
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr WITH_MODULE_GRUB=NO WITH_MODULE_REMOTE_ASSIST=NO
  make
}

package() {
  cd dde-control-center
  make INSTALL_ROOT="${pkgdir}" install
}
