# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>
# Contributor: Ricardo Catalinas Jiménez <jimenezrick@gmail.com>

pkgbase=erlang
pkgname=('erlang' 'erlang-unixodbc')
pkgver=20.0
pkgrel=1
arch=('x86_64' 'i686')
url='http://www.erlang.org/'
license=('Apache')
makedepends=('perl' 'mesa' 'lksctp-tools' 'unixodbc' 'wxgtk' 'glu' 'git'
             'libxslt' 'fop' 'java-environment')
options=('staticlibs')
source=("git+https://github.com/erlang/otp.git#tag=OTP-20.0"
        "http://www.erlang.org/download/otp_doc_man_20.0.tar.gz"
        'epmd.service' 'epmd.socket' 'epmd.conf')
sha256sums=('SKIP'
            'b7f1542a94a170f8791f5d80a85706f9e8838924ea65d4301032d0c0cfb845cc'
            'b121ec9053fb37abca5f910a81c526f93ec30fe13b574a12209223b346886a9e'
            '998a759e4cea4527f9d9b241bf9f32527d7378d63ea40afa38443c6c3ceaea34'
            '78ce5e67b21758c767d727e56b20502f75dc4385ff9b6c6db312d8e8506f2df2')

prepare() {
  cd otp

  ./otp_build autoconf
}

build() {
  cd otp

  ./configure --prefix=/usr --enable-smp-support --with-odbc --enable-builtin-zlib
  make
}

package_erlang() {
  pkgdesc='General-purpose concurrent functional programming language developed by Ericsson'
  depends=('ncurses' 'glu' 'wxgtk' 'openssl')
  optdepends=('erlang-unixodbc: database support'
              'java-environment: for Java support'
              'lksctp-tools: for SCTP support')
  provides=('erlang-nox')
  conflicts=('erlang-nox')

  make -C otp DESTDIR="$pkgdir" install

  # Documentation
  install -d "$pkgdir/usr/share/doc/erlang"
  install -m644 "$srcdir/otp/README.md" \
    "$srcdir"/COPYRIGHT \
    "$pkgdir/usr/share/doc/erlang"

  # Compressed man pages
  for page in "$srcdir/man/man?/*"; do gzip $page; done
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang/"

  # License
  install -Dm644 "$srcdir/otp/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"

  # Move over files that will be packaged as erlang-unixodbc
  mkdir "$srcdir/unixodbc"
  mv "$pkgdir/usr/lib/erlang/lib/odbc"* "$srcdir/unixodbc/"
  mv "$pkgdir/usr/lib/erlang/man/man3/odbc.3.gz" "$srcdir"

  # epmd service, socket and conf
  cd "$srcdir"
  install -Dm644 epmd.service "$pkgdir/usr/lib/systemd/system/epmd.service"
  install -Dm644 epmd.socket "$pkgdir/usr/lib/systemd/system/epmd.socket"
  install -Dm644 epmd.conf "$pkgdir/etc/conf.d/epmd"
}

package_erlang-unixodbc() {
  pkgdesc='Unixodbc support for Erlang'
  depends=('unixodbc' 'erlang-nox')

  # Get the files that should be packaged as erlang-unixodbc
  mkdir -p "$pkgdir/usr/lib/erlang/"{lib,man/man3}
  mv "$srcdir/unixodbc/"* "$pkgdir/usr/lib/erlang/lib/"
  mv "$srcdir/odbc.3.gz" "$pkgdir/usr/lib/erlang/man/man3/"

  # License
  install -Dm644 "$srcdir/otp/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENCE.txt"
}

# getver: raw.githubusercontent.com/erlang/otp/maint/OTP_VERSION
# vim: ts=2 sw=2 et:
