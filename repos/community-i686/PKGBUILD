# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=pcmanfm-qt
pkgver=0.10.0
pkgrel=4
pkgdesc="The LXQt file manager, Qt port of PCManFM"
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=("liblxqt" "libfm" "lxmenu-data")
makedepends=("cmake" "qt5-tools")
optdepends=(
	"gvfs: for trash support, mounting with udisks and remote filesystems"
	"udisks: alternative for mounting volumes"
	"xarchiver: archive management"
)
source=(
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"254c1da0d3fce2ab0a03b21861191b0875ce43382c74d3abbfa8f3dc88a080a4"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build

	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
