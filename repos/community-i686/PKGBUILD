# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

pkgname=musescore
pkgver=2.0.1
pkgrel=1
pkgdesc='Create, play and print beautiful sheet music'
arch=('i686' 'x86_64')
url='http://musescore.org/'
license=('GPL')
depends=('libpulse' 'portaudio' 'qt5-quick1' 'qt5-svg' 'qt5-tools')
makedepends=('cmake' 'doxygen' 'lame' 'qt5-script' 'texlive-core')
optdepends=('lame: MP3 export')
install='musescore.install'
source=("musescore-${pkgver}.tar.gz::https://github.com/musescore/MuseScore/archive/v${pkgver}.tar.gz")
sha256sums=('3c16cbf70eca6da87aad7e247a9926305276db0c58e21f5913b8bd27cb989d58')

build() {
  cd MuseScore-${pkgver}

  make PREFIX='/usr' release
}

package() {
  cd MuseScore-${pkgver}/build.release

  make DESTDIR="${pkgdir}" install
  install -m 644 ../assets/musescore-icon-round-512.png "${pkgdir}"/usr/share/pixmaps/mscore.png
}

# vim: ts=2 sw=2 et:
