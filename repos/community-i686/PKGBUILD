# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-desktop
pkgver=1.6.2
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=(GPL LGPL)
depends=('dconf' 'gtk2' 'hicolor-icon-theme' 'libunique' 'python2'
         'startup-notification')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
optdepends=('lightdm-gtk2-greeter: A lightweight display manager')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('413ef92f1aedf0ffbbf232ce6907211f0f4722a2')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --disable-static \
        --disable-scrollkeeper \
        --disable-schemas-compile \
        --enable-gtk-doc \
        --with-omf-dir=/usr/share/omf/${pkgname}
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
