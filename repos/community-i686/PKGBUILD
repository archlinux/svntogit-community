# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=csfml
pkgver=2.4
pkgrel=1
pkgdesc='C bindings for sfml'
arch=('i686' 'x86_64')
url='http://www.sfml-dev.org/'
license=('zlib')
depends=("sfml")
makedepends=('cmake' 'doxygen')
source=("${pkgname}-${pkgver}::https://github.com/LaurentGomila/CSFML/archive/${pkgver}.tar.gz")
sha512sums=('6a194c2640002065219c6c562e5c3f41a3174d91d268c75609d6cd8f18e7f10fc33aa057b7d1aefcd63747448e551874b306eff816f579a965965d5195da988f')

build() {
  cd "$srcdir"/CSFML-${pkgver}

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCSFML_BUILD_DOC=true
  make
  make doc
}

package() {
  cd "$srcdir"/CSFML-${pkgver}/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

