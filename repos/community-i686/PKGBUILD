# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=9306
pkgrel=1
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/puzzles/"
license=('MIT')
makedepends=('pkgconfig')
depends=('gtk2')
source=("http://www.chiark.greenend.org.uk/~sgtatham/${pkgname}/${pkgname}-r${pkgver}.tar.gz")
md5sums=('30eac18bcfe1e202003709a8281a81bc')

build() {
	cd ${srcdir}/${pkgname}-r${pkgver}

	sed -i 's|\$(gamesdir)|\$(bindir)|' Makefile
	make prefix=/usr
}

package() {
	cd ${srcdir}/${pkgname}-r${pkgver}

	install -d ${pkgdir}/usr/{bin,share/licenses/${pkgname}}
	install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/

	make prefix=/usr DESTDIR=${pkgdir} install

	#fix FS#14600
	mv ${pkgdir}/usr/bin/net ${pkgdir}/usr/bin/puzzles-net
	mv ${pkgdir}/usr/bin/cube ${pkgdir}/usr/bin/puzzles-cube
	mv ${pkgdir}/usr/bin/blackbox ${pkgdir}/usr/bin/puzzles-blackbox
}
