# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=2.2.4
pkgrel=2
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'libibus' 'libcanberra-pulse' 'librsvg' 'nss'
         'pulseaudio-alsa' 'upower' 'libnotify' 'libgnomekbd' 'libwacom')
makedepends=('intltool' 'docbook-xsl' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs')
install=cinnamon-settings-daemon.install
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
        "power-manager-upower-0.99-support.patch"
        "xrandr-manager-upower-0.99-support.patch")
sha256sums=('5ee1f3d3af20fdadd5a6b948be667a0968c94f99f7b5a1992f03be0e876ff414'
            'f62cd7f2cc6aa4843686ab2b11e290ab83838fcf5d1c63596c618ee08c478e70'
            'c7918224605b84c484915ab91307112d4978c32b78d9bd027dc788865e762ebf')

prepare() {
  cd $pkgname-$pkgver

  # Compatibility fixes for upower-0.99 (from Debian)
  patch -Np1 -i ../power-manager-upower-0.99-support.patch
  patch -Np1 -i ../xrandr-manager-upower-0.99-support.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
