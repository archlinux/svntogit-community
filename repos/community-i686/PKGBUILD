# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>

pkgname=libmateweather
pkgver=1.6.2
pkgrel=5
pkgdesc="Provides access to weather information from the net"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('dconf' 'gtk2' 'libsoup' 'pygtk' 'python2-gobject2')
makedepends=('mate-common' 'perl-xml-parser')
options=('!emptydirs')
source=("http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz")
sha1sums=('56f30e35345864954a37228ad8103350f4bc3651')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --enable-python \
        --enable-locations-compression
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
