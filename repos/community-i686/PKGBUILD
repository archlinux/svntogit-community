# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=0.5.1
pkgrel=3
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('i686' 'x86_64')
url="http://$pkgname.org/"
license=('GPL')
depends=('boost' 'sdl' 'jack' 'imagemagick' 'ffmpeg' 'glew' 'libxml++' 'alsa-lib' 'librsvg' \
	'libjpeg' 'libpng' 'cairo')
makedepends=('cmake>=2.6' 'pkgconfig' 'help2man')
optdepends=('performous-freesongs: free songs for performous')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/Performous-$pkgver-Source.tar.bz2 \
        'png14-support.patch')
md5sums=('3c86c4810111e1c45e7b8ab4aa321d7d'
         'f8320212e38f30fb651dcc8874434406')

build() {
  cd ${srcdir}/Performous-${pkgver}-Source
  patch -p1 < ../png14-support.patch

  mkdir -p build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd ${srcdir}/Performous-${pkgver}-Source/build

  make DESTDIR="$pkgdir" install
}
