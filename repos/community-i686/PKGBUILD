# $Id$
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gdal
pkgver=1.6.1
pkgrel=7
pkgdesc="A translator library for raster geospatial data formats"
arch=('i686' 'x86_64')
url="http://www.gdal.org"
license=('custom')
depends=('sqlite3' 'libgeotiff' 'libpng' 'mysql' 'libtiff' 'geos' 'python' 'postgresql>=8.4.1' 'netcdf' 'libjpeg>=7')
makedepends=('cfitsio')
options=('!libtool' '!makeflags')
source=(http://download.osgeo.org/gdal/$pkgname-$pkgver.tar.gz
	gdal-1.5.1-python-install.patch) 

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  cd "$srcdir/gdal-$pkgver"
  patch -Np0 -i "$srcdir/gdal-1.5.1-python-install.patch" || return 1

  ./configure --prefix=/usr --with-netcdf --with-libtiff \
    --with-sqlite --with-geotiff --with-mysql --with-python \
    --without-libtool || return 1
  
  # workaround for bug #13646
  sed -i 's/PY_HAVE_SETUPTOOLS=1/PY_HAVE_SETUPTOOLS=/g' ./GDALmake.opt || return 1
  sed -i 's/EXE_DEP_LIBS/KILL_EXE_DEP_LIBS/' apps/GNUmakefile || return 1
  mkdir -p "$pkgdir/usr/lib/python2.6/site-packages/"

  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  # install license
  install -D -m644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1
}
md5sums=('4059285a5cef41ff975ed0ec02a97009'
         'e8e1cc229aa0fdd0b864629b71a064b1')
