# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.2.3
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64' 'i686')
url='http://liballeg.org/'
license=('custom')
makedepends=('cmake' 'mesa-libgl' 'glu' 'ninja' 'libtheora' 'git' 'opusfile')
depends=('jack' 'libxpm' 'libxxf86dga' 'libgl' 'physfs' 'gtk2' 'libpulse' 'libtheora' 'opusfile')
source=("git+https://github.com/liballeg/${pkgname}${pkgver%%.*}#tag=$pkgver.0")
md5sums=('SKIP')

build() {
  mkdir -p build
  cd build

  cmake "../${pkgname}${pkgver%%.*}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS=OFF \
    -DWANT_PHYSFS=ON \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  install -Dm644 "${pkgname}${pkgver%%.*}/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: liballeg.org/a5docs/trunk
# vim: ts=2 sw=2 et:
