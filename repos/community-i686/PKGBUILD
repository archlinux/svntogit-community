# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Andy Li <andy@onthewings.net>
# Contributor: Kyle Brady  <kyle at spark-gap dot com>
# Contributor: Patrick Hof <courts@offensivethinking.org>
# Contributor: Michael Fellinger <mf@rubyists.com>

pkgname=haxe
pkgver=3.3.0rc1
pkgrel=1
pkgdesc='Cross-platform toolkit and programming language'
arch=('x86_64' 'i686')
url='http://haxe.org/'
license=('GPL' 'MIT' 'LGPL') # http://haxe.org/foundation/open-source.html
groups=('devel')
depends=('neko>=2.0.0')
optdepends=('python' 'php' 'java-environment' 'mono')
makedepends=('git' 'ocaml>=4.00.0' 'zlib' 'neko>=2.0.0' 'camlp4')
provides=('haxe' 'haxelib')
options=('!strip' '!makeflags')
source=("$pkgname-${pkgver/rc?/}::git+https://github.com/HaxeFoundation/haxe.git#tag=${pkgver/rc/-rc}"
  'git+https://github.com/HaxeFoundation/haxelib.git'
  'git+https://github.com/HaxeFoundation/ocamllibs.git'
  'haxe.sh'
)
md5sums=('SKIP' 'SKIP' 'SKIP' '977476c8fc1dfc0acbb05bd22f9136b0')

build() {
  cd "$pkgname-${pkgver/rc?/}"

  git submodule init
  git config submodule.extra/haxelib_src.url "$srcdir/haxelib"
  git config submodule.libs.url "$srcdir/ocamllibs"
  git submodule update

  make
  make tools
}

check() {
  cd "$pkgname-${pkgver/rc?/}"

  echo "haxe version:"
  ./haxe -version
  echo "haxelib version:"
  ./haxelib version
}

package() {
  install -d "$pkgdir"/usr/bin/ "$pkgdir"/usr/share/haxe/
  install -m 755 "$srcdir/$pkgname-${pkgver/rc?/}"/{haxe,haxelib} "$pkgdir"/usr/bin/
  cp -rf "$srcdir/$pkgname-${pkgver/rc?/}"/std "$pkgdir"/usr/share/$pkgname/
  install -Dm 644 "$srcdir/$pkgname-${pkgver/rc?/}"/extra/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  install -Dm 644 "$srcdir/haxe.sh" "$pkgdir/etc/profile.d/haxe.sh"
}

# vim:set ts=2 sw=2 et:
