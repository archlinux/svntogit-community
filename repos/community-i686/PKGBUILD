# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname=minetest
pkgver=0.4.9
pkgrel=4
pkgdesc='An Infiniminer/Minecraft inspired game'
arch=('i686' 'x86_64')
url='http://minetest.net/'
license=('GPL')
depends=('sqlite' 'freetype2' 'leveldb' 'openal' 'libvorbis' 'curl' 'irrlicht' 'hicolor-icon-theme')
makedepends=('cmake')
install=minetest.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}/tarball/${pkgver}
	${pkgname}_game-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}_game/tarball/${pkgver}
        optional-opengles.patch)
md5sums=('2a2629befa86ef72924bcc0e489730c7'
         '097666443a71988cf2d41ebd9be31ec9'
         '116de5a70e13dbd0e2419fc7fb59ee3b')

prepare() {
  cd "$srcdir"/minetest-minetest-*

  # Fix rendering with NVIDIA driver (FS#37918)
  patch -Np1 -i "$srcdir"/optional-opengles.patch
}

build() {
  cd "$srcdir"/minetest-minetest-*

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GETTEXT=on \
    -DENABLE_FREETYPE=1 \
    -DENABLE_LEVELDB=on 
  make
}

package() {
  cd "$srcdir"/minetest-minetest-*

  make DESTDIR="$pkgdir" install

  mv -v "$srcdir"/minetest-minetest_game-* "$pkgdir"/usr/share/minetest/games/minetest
  
  # small cleanup
  rm "$pkgdir"/usr/share/minetest/games/minetest/.gitignore
}
