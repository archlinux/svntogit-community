# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=3.2.8.1
pkgrel=8
pkgdesc="Open Source IRC Server"
arch=('i686' 'x86_64')
url="http://unrealircd.com"
license=('GPL2')
depends=(openssl curl)
conflicts=(ircd)
provides=(ircd)
backup=(etc/unrealircd/unrealircd.conf)
source=(http://www.unrealircd.com/downloads/Unreal$pkgver.tar.gz
	unrealircd.rc
	arch-fixes.patch)
md5sums=('7b741e94e867c0a7370553fd01506c66'
         '0d728240132a97a2cef61be9558750b1'
         '77807313c4578f2c30286b9f9e3fb21c')

build() {
  mkdir -p $pkgdir/etc/unrealircd
  mkdir -p $pkgdir/usr/bin
  cd $srcdir/Unreal3.2

  patch -p1 <$srcdir/arch-fixes.patch

  ./configure --with-showlistmodes \
      --enable-hub --enable-prefixaq --with-listen=5 \
      --with-dpath=/etc/unrealircd \
      --with-spath=/usr/bin \
      --with-nick-history=2000 --with-sendq=3000000 \
      --with-bufferpool=18 --with-hostname=archlinux \
      --with-permissions=0600 --with-fd-setsize=1024 \
      --enable-dynamic-linking \
      --enable-ziplinks \
      --enable-ssl \
      --enable-libcurl=/usr

  make
  make IRCDDIR=$pkgdir/etc/unrealircd BINDIR=$pkgdir/usr/bin install

  mv $srcdir/Unreal3.2/src/ircd $pkgdir/usr/bin/unrealircd
  mv $srcdir/Unreal3.2/doc/example.conf $pkgdir/etc/unrealircd/unrealircd.conf
  mkdir -p $pkgdir/usr/lib/unrealircd
  mv $pkgdir/etc/unrealircd/modules $pkgdir/usr/lib/unrealircd/
  ln -s /usr/lib/unrealircd/modules $pkgdir/etc/unrealircd/modules
  install -D -m0755 $srcdir/unrealircd.rc $pkgdir/etc/rc.d/unrealircd

  # log
  mkdir -p $pkgdir/var/log/unrealircd/
  touch $pkgdir/var/log/unrealircd/ircd.log
  ln -s /var/log/unrealircd/ircd.log $pkgdir/etc/unrealircd/ircd.log

  find $pkgdir/usr -type f -exec chmod ugo+r {} \;
  find $pkgdir/usr -type d -exec chmod ugo+rx {} \;
}
