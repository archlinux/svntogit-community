# $Id$ 
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=gpicview
pkgver=0.2.4
pkgrel=1
pkgdesc='Lightweight image viewer'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2' 'desktop-file-utils' 'xdg-utils')
install=$pkgname.install
makedepends=('pkgconfig' 'intltool')
optdepends=('librsvg: For SVG support')
source=(http://downloads.sourceforge.net/sourceforge/lxde/$pkgname-$pkgver.tar.gz)
md5sums=('b209e36531f89c48e3067b389699d4c7')

prepare() {
	cd $pkgname-$pkgver

	# Apply only one main category (FS#34684)
	sed -i '/^Categories=/ s/Utility;//' gpicview.desktop.in
}

build() {
	cd $pkgname-$pkgver
	./configure --sysconfdir=/etc --prefix=/usr
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
