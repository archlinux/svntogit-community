# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Sven Kauber, <celeon@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=encfs
pkgver=1.8
pkgrel=1
pkgdesc='Encrypted filesystem in user-space'
arch=('i686' 'x86_64')
url='https://vgough.github.io/encfs/'
license=('LGPL')
depends=('rlog' 'openssl' 'fuse' 'boost-libs')
makedepends=('boost' 'autoconf')
source=(https://github.com/vgough/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('464453f182cff2a245e830707464a758f63c34368351ba75ba41f4804b7cc931')

build(){
  cd "${srcdir}"/${pkgname}-${pkgver}

  autoreconf -if
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
