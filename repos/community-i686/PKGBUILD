# Maintainer: Alex Szczuczko <alex at szc dot ca>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Imanol Celaya <ilcra1989@gmail.com>

pkgname=bmake
pkgver=20170201
pkgrel=1
pkgdesc="Portable version of the NetBSD 'make' build tool"
arch=('i686' 'x86_64')
url="http://www.crufty.net/help/sjg/bmake.html"
license=('BSD')
source=("http://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz")
sha256sums=('fa0b70af31101b78e7c2557f16455bd6d383f53edb03ebec66b9c715530d2373')

prepare() {
    cd "$srcdir/$pkgname"
    # Add missing bsd.*.mk symlinks
    _links_orig="dep doc init lib links man nls obj own prog subdir"
    _links_new="dep doc inc init lib links man nls obj own prog subdir"
    sed -i -e "s/$_links_orig/$_links_new/" mk/install-mk
}

build() {
    cd "$srcdir"
    mkdir -p bmake-build
    cd bmake-build
    sh ../bmake/boot-strap --prefix=/usr
}

package() {
    cd "$srcdir/bmake-build"
    # Fix directory permissions on install
    install -dm0755 "$pkgdir/usr/bin"
    install -dm0755 "$pkgdir/usr/share/man/cat1"
    install -dm0755 "$pkgdir/usr/share/licenses/$pkgname"
    sh ../bmake/boot-strap --prefix=/usr --install-destdir="$pkgdir" op=install
    head -n70 ../bmake/main.c >"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
