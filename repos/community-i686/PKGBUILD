# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=bcg729
pkgver=1.0.1
pkgrel=3
pkgdesc="g729 codec"
arch=('i686' 'x86_64')
url="http://www.linphone.org/eng/documentation/dev/bcg729.html"
license=('GPL')
depends=('mediastreamer')
source=(http://download-mirror.savannah.gnu.org/releases/linphone/plugins/sources/$pkgname-$pkgver.tar.gz
	"$pkgname-old-api.patch::https://bugs.archlinux.org/task/47938?getfile=13790")
md5sums=('c4dc6c09c71850f0b392772f519f1d19'
         'a7139d23ecc928e94715b11c90b460d2')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i $srcdir/$pkgname-old-api.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-strict
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
