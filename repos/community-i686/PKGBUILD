# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-primitive
pkgver=0.6
pkgrel=4
pkgdesc="Wrappers for primitive operations"
url="http://hackage.haskell.org/package/primitive"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=()
depends=("ghc")
options=('strip' 'staticlibs')
install="${pkgname}.install"
source=("http://hackage.haskell.org/packages/archive/primitive/${pkgver}/primitive-${pkgver}.tar.gz")
sha256sums=('34a5f39213c68369e7edc2a3ea175d3f4edbf89e9f0777784710eff6f2d69722')

build() {
  cd ${srcdir}/primitive-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
  depends=("ghc=${_ghcver}")

  cd ${srcdir}/primitive-${pkgver}
  install -Dm744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
  install -dm755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/primitive
  runhaskell Setup copy --destdir=${pkgdir}
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
