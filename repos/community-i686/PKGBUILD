# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=dante
pkgver=1.4.0
pkgrel=4
pkgdesc="SOCKS v4 and v5 compatible proxy server and client"
url="http://www.inet.no/dante"
arch=(i686 x86_64)
license=('custom')
depends=('pam' 'krb5')
backup=('etc/socks.conf'
	'etc/sockd.conf')
install=sockd.install
source=(ftp://ftp.inet.no/pub/socks/${pkgname}-${pkgver}.tar.gz
	sockd.tmpfiles.d
	sockd.service)
md5sums=('c117a3ce25afb936f13b76f1d2524879'
         '8588bb1cf6e9cde0f542a96edc0a0f17'
         'f5520e3350de5d4d1baa05cf33678c06')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc \
	--sbindir=/usr/bin \
	--disable-libwrap
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -dm0755 ${pkgdir}/etc
  cp example/{socks,sockd}.conf ${pkgdir}/etc
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm0644 $srcdir/sockd.service $pkgdir/usr/lib/systemd/system/sockd.service
  install -Dm0644 $srcdir/sockd.tmpfiles.d $pkgdir/usr/lib/tmpfiles.d/sockd.conf
}
