# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.3.0.20100621
pkgrel=2
pkgdesc='The IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on.'
arch=('i686' 'x86_64')
url="http://code.google.com/p/ibus/"
license=('LGPL')
depends=('python-pyenchant' 'ibus' 'aspell-en')
provides=('ibus-table=1.3.0')
conflicts=('ibus-table-git')
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('0fd5812197400f7b586480fca1a0c082')

build() {
  cd ${pkgname}-${pkgver}

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
