# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.46
pkgrel=1
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('glibc')
optdepends=('lzo: for libddr_lzo')
arch=('x86_64' 'i686')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.gz")
sha512sums=('efd1bf5d80d4bafa6e6971caa84205dc5df4456a29a9fa3bb56aa984d68675ae3418e8ba3a355906f491f0f2e750c38fd695e37dbd193e554cd41afec3841e28')

prepare() {
  cd $pkgname

  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile
}

build() {
  cd $pkgname
  ./autogen.sh
  ./configure --enable-shared 
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}
