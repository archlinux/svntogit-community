# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

pkgname=freerdp
pkgver=1.0.2
pkgrel=4
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse')
makedepends=('krb5' 'cmake' 'damageproto' 'xmlto' 'docbook-xsl')
conflicts=('freerdp-git')
changelog=${pkgname}.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/FreeRDP/archive/$pkgver.tar.gz
        ffmpeg2.0.patch
        patch_numblock.patch)
md5sums=('c260051a94caff590def5caa4fcf59d6'
         '1260eecf01ea5212e2db8ac9c5e7fa6d'
         'ac581d445ded7cdbd59082a48d9c28ac')

prepare() {
  cd $srcdir/FreeRDP-$pkgver
  patch -Np1 -i ../ffmpeg2.0.patch
  patch -Np1 -i ../patch_numblock.patch
}

build() {
  cd $srcdir/FreeRDP-$pkgver
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSEAUDIO=ON \
	.
  make
}

package() {
  cd $srcdir/FreeRDP-$pkgver
  make DESTDIR="${pkgdir}" install
}
