# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=snes9x
pkgname=('snes9x' 'snes9x-gtk')
pkgver=1.53
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.snes9x.com"
license=('custom')
makedepends=('intltool' 'nasm' 'mesa' 'libpulse'  'libpng' 'sdl' 'gtk2' 'libxv' \
             'desktop-file-utils' 'hicolor-icon-theme')
source=(http://snes9x-gtk.googlecode.com/files/${pkgbase}-${pkgver}-src.tar.bz2
        snes9x-gtk2.24.diff.gz)
sha1sums=('57be6480369f66b9aaaeb722789ce114f58ed1e2'
          '680dbe0a70f1821bc7931539e72f5a1a95e3938c')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}-src"
  patch -p1 -i "${srcdir}/snes9x-gtk2.24.diff"
  cd unix
  ./configure --prefix=/usr --with-sdd1-decomp --enable-netplay
  make

  cd "${srcdir}/${pkgbase}-${pkgver}-src/gtk"
  ./configure --prefix=/usr --with-sdd1-decomp --with-netplay --with-opengl
  make
}

package_snes9x() {
  pkgdesc="A portable Emulator for the Super Nintento Entertainment System"
  depends=('gcc-libs' 'libpng' 'libxext' 'libsm')

  cd "${srcdir}/${pkgbase}-${pkgver}-src/unix"
  install -D -m755 snes9x "${pkgdir}/usr/bin/snes9x"
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}-src/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/snes9x/LICENSE"
}

package_snes9x-gtk() {
  pkgdesc="A portable Emulator for the Super Nintento Entertainment System - GTK version"
  license=('custom' 'LGPL')
  depends=('sdl' 'libgl' 'libpulse' 'gtk2' 'libxv' 'desktop-file-utils' 'hicolor-icon-theme')
  install=snes9x-gtk.install

  cd "${srcdir}/${pkgbase}-${pkgver}-src/gtk"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}-src/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/snes9x-gtk/LICENSE"
}
