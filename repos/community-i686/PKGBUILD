# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.1.103
pkgrel=1
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('dkms' 'jsoncpp' 'luajit')
makedepends=('cmake')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('65f272b20ad1858534b6b4a6f5e07012a3d0ae46d2bb9db36bf7821cc8230949')
install=$pkgname.install

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDIR_ETC=/etc \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_LUAJIT=OFF \
        -DUSE_BUNDLED_JSONCPP=OFF \
        -DUSE_BUNDLED_ZLIB=OFF \
        -DUSE_BUNDLED_NCURSES=OFF \
        -DBUILD_DRIVER=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  cd build
  make install DESTDIR="$pkgdir"
}
