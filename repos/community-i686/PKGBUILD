# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=workrave
pkgver=1.10.20
pkgrel=1
pkgdesc="a tool to help RSI"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://workrave.org"
depends=('libxtst' 'gtkmm3' 'gdome2' 'gstreamer' 'libsm' 'libice' 'libxss')
makedepends=('intltool' 'gobject-introspection' 'python2-cheetah' 'autoconf-archive')
optdepends=('gst-plugins-good: to play sound events')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rcaelers/workrave/archive/v${pkgver//./_}.tar.gz"
	"gschema.patch::https://bugs.archlinux.org/task/56115?getfile=15841")
sha256sums=('a89c6e82d5bbbaae5e171100b87c4efd8316ab8a18d82b83714035e1116983ec'
            'e9fdad85184bf6e6f1122f6ef2aa3a97a575320b4d5c553265f440a97fe8059a')

prepare() {
  cd "$srcdir"/workrave-${pkgver//./_}
#  patch -p0 < "$srcdir"/gschema.patch
}

build() {
  cd "$srcdir"/workrave-${pkgver//./_}
  export PYTHON=python2
  ./autogen.sh
  ./configure --prefix=/usr --enable-dbus --sysconfdir=/etc --libexecdir=/usr/lib/workrave \
	--disable-pulse
  make
}

package() {
  cd "$srcdir"/workrave-${pkgver//./_}
  make DESTDIR="$pkgdir" install
}
