# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org> 
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Sï¿½ren Holm <sgh@sgh.dk> 

pkgname=vym
pkgver=2.2.4
pkgrel=2
pkgdesc="A mindmapping tool"
arch=('i686' 'x86_64')
url="http://www.insilmaril.de/vym/"
license=('GPL')
depends=('qt4' 'unzip' 'zip')
options=('!emptydirs')
changelog=$pkgname.changelog
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2 \
        $pkgname.desktop)
sha256sums=('fa6d088327feae80c4c6760454fba64fe253a7de6e7e46f45a4c7f7db9b998f4'
            'e299c69c213e7aac3f5b5d0ab088132b4ec7cb63a391f272e75ed64f049d541b')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  qmake PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

# install files
  make INSTALL_ROOT="${pkgdir}" install

# correct doc location
  install -d "${pkgdir}"/usr/share/doc/$pkgname
  install -m644 doc/* "${pkgdir}"/usr/share/doc/$pkgname
  rm -rf "${pkgdir}"/usr/share/doc/packages

# .desktop and icon file
  install -D -m644 "${srcdir}"/$pkgname.desktop \
    "${pkgdir}"/usr/share/applications/$pkgname.desktop
  install -D -m644 "${pkgdir}"/usr/share/vym/icons/$pkgname.png \
    "${pkgdir}"/usr/share/pixmaps/$pkgname.png
}
