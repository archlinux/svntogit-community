# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname='packagekit'
pkgver=1.0.4
pkgrel=3
pkgdesc='A system designed to make installation and updates of packages easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('dbus-glib' 'pacman' 'polkit' 'shared-mime-info' 'sqlite')
makedepends=('dbus-glib' 'gobject-introspection' 'gtk-doc' 'intltool'
	'networkmanager' 'pacman>=4.2.0' 'polkit'
	'shared-mime-info' 'sqlite' 'bash-completion')
optdepends=('networkmanager: detect connection status'
	'bash-completion: command completion in bash')
install='packagekit.install'
backup=('var/lib/PackageKit/transactions.db'
	'etc/PackageKit/alpm.d/pacman.conf'
	'etc/PackageKit/alpm.d/repos.list')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes
source=("http://www.freedesktop.org/software/PackageKit/releases/PackageKit-${pkgver}.tar.xz"{,.asc}
	'packagekit-0001-fix-bad-event-usage-when-removing-package.patch::https://github.com/hughsie/PackageKit/commit/d4e5654eff038e828e142d4e0fefc64e1e2491e7.patch'
	'packagekit-0002-honor-simulate-flag-while-removing-packages.patch::https://github.com/hughsie/PackageKit/commit/b024647bd4e6ced41e811b88aea1cea16cb28dfb.patch')
sha256sums=('1b949a7dc92e20f7d7deb634e5b399148dbb749e813cea955f1546a99e169a71'
	'SKIP'
	'bb2ea68145af2e542c74b5e76ec729c46f65345861b624cb37695830b899ecbf'
	'4c2ca5ee5b60257ecf67c3822561510587b0afe263abf4fba98879d4d15156ea')

prepare() {
	cd "${srcdir}/PackageKit-${pkgver}"

	# https://github.com/hughsie/PackageKit/pull/43/commits
	# alpm: fix bad event usage when removing package
	patch -Np1 < "${srcdir}/packagekit-0001-fix-bad-event-usage-when-removing-package.patch"
	# alpm: honor simulate flag while removing packages
	patch -Np1 < "${srcdir}/packagekit-0002-honor-simulate-flag-while-removing-packages.patch"

	# packagekit does not know about updates made by pacman while daemon is running
	# https://github.com/hughsie/PackageKit/issues/15
	# For now we just exit the daemon... It will launch with a clean status if required.
	sed -i '/ExecStart=/s/$/ --timed-exit/' data/packagekit.service.in
}

build() {
	cd "${srcdir}/PackageKit-${pkgver}"

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-dummy \
		--enable-alpm
	make
}

package() {
	cd "${srcdir}/PackageKit-${pkgver}"

	# install directory with polkit's owner and permission
	install -d -m0700 -o 102 "${pkgdir}"/usr/share/polkit-1/rules.d

	make DESTDIR="${pkgdir}" install
}

