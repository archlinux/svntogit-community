# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=idris
pkgver=0.99
pkgrel=30
pkgdesc="Functional Programming Language with Dependent Types"
url="http://www.idris-lang.org/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib' 'gcc')
makedepends=("ghc=8.0.1" "git" "haskell-aeson" "haskell-annotated-wl-pprint" "haskell-ansi-terminal"
             "haskell-ansi-wl-pprint" "haskell-async" "haskell-base64-bytestring"
             "haskell-blaze-html" "haskell-blaze-markup" "haskell-cheapskate" "haskell-fingertree"
             "haskell-fsnotify" "haskell-ieee754" "haskell-mtl" "haskell-libffi" "haskell-network"
             "haskell-optparse-applicative" "haskell-parsers" "haskell-regex-tdfa"
             "haskell-safe" "haskell-split" "haskell-terminal-size" "haskell-text"
             "haskell-transformers-compat" "haskell-trifecta" "haskell-uniplate"
             "haskell-unordered-containers" "haskell-utf8-string" "haskell-vector"
             "haskell-vector-binary-instances" "haskell-zip-archive")
source=("git+https://github.com/idris-lang/Idris-dev.git#tag=v$pkgver"
        datadir.patch)
sha256sums=('SKIP'
            'e7cf290770a7ee08cc4089eccf23a7501300d70e85df03d84652b3045d568371')

prepare() {
    sed -i 's/, safe == 0.3.9/, safe >= 0.3.9/' Idris-dev/${pkgname}.cabal

    cd Idris-dev
    # https://github.com/idris-lang/Idris-dev/issues/3544
    patch -p1 -i ../datadir.patch
}

build() {
    cd "${srcdir}/Idris-dev"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fFFI -fGMP -frelease -f-freestanding -f-CI -f-execonly
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
}

package() {
    cd "${srcdir}/Idris-dev"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
}
