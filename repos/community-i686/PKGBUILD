# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>
 
pkgname=pacmanlogviewer
pkgver=1.3.1
pkgrel=5
pkgdesc="Inspect pacman log files"
arch=('i686' 'x86_64')
url='https://opendesktop.org/content/show.php?content=150484'
license=('GPL')
depends=('qt4')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz"::"http://qt-apps.org/CONTENT/content-files/150484-${pkgname}-${pkgver}.tar.gz")
sha256sums=('c75ee4974983f090b0d0dd84cc6358bab4085ec20e3dfcb84513153562f344b4')
 
build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
} 
