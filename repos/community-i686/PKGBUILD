# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=bladerf
pkgver=0.16.0
_CLIver=0.11.0
_FPGAver=0.0.5
_firmver=1.7.0
pkgrel=1
pkgdesc="Driver, userspace, fpga & firmware for the bladeRF SDR."
url="https://github.com/Nuand/bladeRF"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('libusb')
makedepends=('cmake' 'help2man')
#backup=("etc/bladerf.cfg")

source=(
    "https://github.com/Nuand/bladeRF/archive/libbladeRF_v$pkgver.tar.gz"
    "http://nuand.com/fpga/v$_FPGAver/hostedx40.rbf"
    "http://nuand.com/fpga/v$_FPGAver/hostedx115.rbf"
    "http://www.nuand.com/fx3/bladeRF_fw_v$_firmver.img")

md5sums=('3a3dd03382a0f649a442851ef094f864'
         'a474050c832cbe528fb8635b36c36a34'
         'd3ca5dc09115daf0bab731744b081165'
         '73ace21e693049e475cceb88bed900d7')

# bladeRF-cli can use libtecla (aur)?

build() {
  cd "$srcdir/bladeRF-libbladeRF_v$pkgver"
  mkdir -p build
  cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DINSTALL_UDEV_RULES=ON \
    -DBUILD_DOCUMENTATION=ON \
    -DCMAKE_INSTALL_LIBDIR:PATH=lib \
    -Dusb_LIBRARY:FILEPATH=/usr/lib/libusb-1.0.so \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
  # ENABLE_FX3_BUILD needs cypress toolchain
  # really really wants to put files in /usr/lib64
}

package() {
  cd "$srcdir/bladeRF-libbladeRF_v$pkgver/build"
  make DESTDIR="${pkgdir}" install
  cd "$srcdir"
  install -Dm644 "bladeRF_fw_v$_firmver.img" "$pkgdir/usr/share/bladerf/firmware/bladeRF_fw_v$_firmver.img"
  install -Dm644 hostedx40.rbf "$pkgdir/usr/share/bladerf/fpga/hostedx40.rbf"
  install -Dm644 hostedx115.rbf "$pkgdir/usr/share/bladerf/fpga/hostedx115.rbf"
}
