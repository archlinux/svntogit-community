# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Anatol Pomozov <anatol.pomozov gmail>
# Contributor: RunningDroid <runningdroid AT zoho.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=gflags
pkgver=2.2.0
pkgrel=2
pkgdesc='C++ Library for commandline flag processing'
arch=(i686 x86_64)
url='https://github.com/schuhschuh/gflags'
license=(BSD)
depends=(gcc-libs)
makedepends=(cmake)
options=(staticlibs)
source=($pkgname-$pkgver.tar.gz::https://github.com/schuhschuh/gflags/archive/v$pkgver.tar.gz)
sha256sums=('466c36c6508a451734e4f4d76825cf9cd9b8716d2b70ef36479ae40f08271f88')

build() {
  cd gflags-$pkgver
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DREGISTER_INSTALL_PREFIX=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_STATIC_LIBS=ON \
    -DBUILD_TESTING=ON
  make
}

check() {
  cd gflags-$pkgver
  make test
}

package() {
  cd gflags-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.txt "$pkgdir"/usr/share/licenses/$pkgname/COPYING.txt
}
