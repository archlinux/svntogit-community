# $Id$
# Maintainer: Kyle Keen <keenerd at gmail dot com>
pkgbase=python-pyzmq
pkgname=(python2-pyzmq python-pyzmq)
pkgver=15.2.0
pkgrel=1
pkgdesc="Python bindings for zeromq, written in Cython"
arch=(i686 x86_64)
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
depends=(zeromq python)
makedepends=(python python2 zeromq)
checkdepends=(python-nose python2-nose python-tornado python2-tornado)
#source=(https://github.com/zeromq/pyzmq/downloads/pyzmq-$pkgver.tar.gz)
source=(https://pypi.python.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz)
md5sums=('9722046c27475441d47ac17a98c665bb')

prepare() {
  cd "$srcdir"
  cp -a pyzmq-${pkgver} py2zmq-${pkgver}
}

build() {
  cd "$srcdir/pyzmq-$pkgver"
  # py3 errors added in 2.2.0.1 
  sed -i 's|except socket.error, e:|except socket.error as e:|' zmq/eventloop/ioloop.py
  sed -i 's|except gevent.Timeout, t:|except gevent.Timeout as t:|' zmq/green/core.py
  cd "$srcdir/py2zmq-$pkgver"
  sed -i 's|^#!/usr/bin/env python$|&2|' $(find ./ -name '*.py')
}

package_python2-pyzmq() {
  pkgdesc="Python2 bindings for zeromq, written in Cython"
  depends=(zeromq python2)
  cd "$srcdir/py2zmq-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
}

package_python-pyzmq() {
  pkgdesc="Python3 bindings for zeromq, written in Cython"
  depends=(zeromq python)
  cd "$srcdir/pyzmq-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=0
}

check() {
  # 15.2.0 - 32 bit platform fails test_large_send with OverflowError
  # https://github.com/zeromq/pyzmq/issues/799
  if [[ $CARCH != "x86_64" ]]; then
    sed -i 's/buf = .*$/return/' "$srcdir/pyzmq-$pkgver/zmq/tests/test_socket.py"
    sed -i 's/buf = .*$/return/' "$srcdir/py2zmq-$pkgver/zmq/tests/test_socket.py"
  fi

  cd "$srcdir/pyzmq-$pkgver"
  python3 setup.py build_ext --inplace
  python3 setup.py test
  cd "$srcdir/py2zmq-$pkgver"
  python2 setup.py build_ext --inplace
  python2 setup.py test
}

