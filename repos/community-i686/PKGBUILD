# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libiodbc
pkgver=3.52.9
pkgrel=2
pkgdesc='Independent Open DataBase Connectivity for Linux'
arch=('i686' 'x86_64')
url='http://www.iodbc.org/dataspace/iodbc/wiki/iODBC/'
license=('LGPL')
makedepends=('chrpath')
source=("http://downloads.sourceforge.net/iodbc/${pkgname}-${pkgver}.tar.gz")
sha256sums=('33207d9f477aa5fa76962ff644baa38e59255b96812f28574794659fb61a7ab2')

build() {
	cd ${pkgname}-${pkgver}

	./configure --prefix=/usr \
		--disable-static \
		--includedir=/usr/include/libiodbc \
		--disable-gui \
		--disable-libodbc
	make
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	chrpath -d ${pkgdir}/usr/bin/iodbctest{,w}
	rm -rf ${pkgdir}/usr/share/libiodbc/
}
