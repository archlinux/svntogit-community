# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox
_pkgname=qTox
pkgver=1.4.0
pkgrel=4
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines'
arch=('i686' 'x86_64')
url='https://github.com/tux3/qTox'
license=('GPL3')
depends=('libxss'
         'desktop-file-utils'
         'openal'
         'gtk2'
         'ffmpeg'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'toxcore')
makedepends=('qt5-tools')
source=("https://github.com/tux3/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4c93e90ec20e8c9289f5be628f6543d356c99f59d72e735814ba2640b257fbb78f18a9dbd42070a5cfb7fded492906f07cc678d1ae737ac795da689b3e6abe2d')

build() {
  cd $_pkgname-$pkgver
  install -d build
  cd build
  qmake-qt5 ENABLE_SYSTRAY_UNITY_BACKEND=NO ..
  make
}

package() {
  # executable
  cd $_pkgname-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
