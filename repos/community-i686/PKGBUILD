# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=tilda
pkgver=1.1.10
pkgrel=1
pkgdesc="A Gtk based drop down terminal for Linux and Unix"
arch=('i686' 'x86_64')
url="https://github.com/lanoxx/tilda"
license=('GPL')
depends=('vte3' 'confuse')
source=(https://github.com/lanoxx/$pkgname/archive/$pkgname-$pkgver.tar.gz)
sha256sums=('292f98d344c88998a0f7d67372020f1c3c7192de94fabf14a0d4eae18726b267')

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
