# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haskell-hslogger
pkgver=1.2.6
pkgrel=2
pkgdesc="Versatile logging framework"
url="http://hackage.haskell.org/package/hslogger"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-mtl' 'haskell-network')
options=('staticlibs')
install=${pkgname}.install
source=("http://hackage.haskell.org/packages/archive/hslogger/${pkgver}/hslogger-${pkgver}.tar.gz"
        '0001-relax-network-upper-bound-to-2.6.patch')
md5sums=('873f0641d59621c0d9803120915b8094'
         '14b3339395dcb5ba666987bc6aac998e')

build() {
  cd ${srcdir}/hslogger-${pkgver}

  # upstream bug: https://github.com/jgoerzen/hslogger/pull/22
#  patch -Np1 -i ${srcdir}/0001-relax-network-upper-bound-to-2.6.patch

  runhaskell Setup configure -p --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O \
	--enable-split-objs --enable-shared --libsubdir=\$compiler/site-local/\$pkgid \
	--enable-library-profiling
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${srcdir}/hslogger-${pkgver}
  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/hslogger
  runhaskell Setup copy --destdir=${pkgdir}
}
