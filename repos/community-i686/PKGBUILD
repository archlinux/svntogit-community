# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=stunnel
pkgver=5.09
pkgrel=1
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL"
arch=('i686' 'x86_64')
url="http://www.stunnel.org"
license=('GPL')
depends=('openssl')
install=$pkgname.install
source=("https://www.stunnel.org/downloads/$pkgname-$pkgver.tar.gz"{,.asc}
        'Makefile.patch'
        'stunnel.tmpfiles.conf')
md5sums=('deab04ec8b78ec3102b36113b28b4cce'
         'SKIP'
         '579f198ecb90a42fcbfd8a4e48006264'
         '2e065a39998e57727ee79887bbd1751e')
validpgpkeys=('F6B1BFC55F32243FB2C8919AFCD53E9D74C732D1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # don't create a certificate...
  patch -p0 <"$srcdir/Makefile.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-ipv6 \
    --disable-libwrap

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 tools/stunnel.cnf "$pkgdir/etc/stunnel/stunnel-openssl.cnf-sample"
  install -Dm644 tools/stunnel.service "$pkgdir/usr/lib/systemd/system/stunnel.service"
  install -Dm644 "$srcdir/stunnel.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/stunnel.conf"

  sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
      -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
      -e "s:no\(body\|group\):stunnel:g" \
      -i "$pkgdir/etc/stunnel/stunnel.conf-sample"
}
