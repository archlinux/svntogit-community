# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=iasl
pkgver=20130328
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('i686' 'x86_64')
url="http://acpica.org"
license=('custom')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
#install=iasl.install
source=(http://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz
	LICENSE)
md5sums=('99fa19c67c8fa95f90cb7ec0f29d6242'
         '8615526144f69ea4e870d9bc37df9b29')

build() {
  cd "${srcdir}/acpica-unix-${pkgver}"/generate/unix

  sed -i -e 's/_CYGWIN/_LINUX/g' -e 's/-Werror//g' Makefile.config

  if [[ $CARCH == i686 ]]; then
    make BITS=32
  else
    make BITS=64
  fi
}

package() {
  cd "${srcdir}/acpica-unix-${pkgver}"/generate/unix

  make DESTDIR=${pkgdir} install

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
