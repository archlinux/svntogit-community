# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas at gmail dot com>
# Contributor: Laszlo Papp <djszapi2 at gmail dot com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Vladimir Cerny <blackvladimir@gmail.com>
# Contributor: 4javier <4javieregATgmailDOTcom>

pkgname=avr-gdb
_pkgname=gdb
pkgver=7.4
pkgrel=1
pkgdesc="The GNU Debugger for AVR"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('python')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/gdb/${_pkgname}-${pkgver}.tar.bz2)
md5sums=('95a9a8305fed4d30a30a6dc28ff9d060')

build() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  unset LDFLAGS
  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="$CFLAGS"
    
  ./configure --prefix=/usr \
    --target=avr \
    --disable-nls \
    --enable-languages=c,c++ \
    --infodir=/usr/share/info \
    --libdir=/usr/lib \
    --program-prefix=avr- \
    --libexecdir=/usr/lib \
    --mandir=/usr/share/man \
    --with-system-readline
  make
}

package() {
  cd ${srcdir}/${_pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -rf ${pkgdir}/usr/share/info
  rm -rf ${pkgdir}/usr/share/gdb
  rm -rf ${pkgdir}/usr/include/gdb
}
