# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vis
pkgver=0.4
pkgrel=1
_pkgname_test=vis-test
_commit_test='59be906c4fd4091016fa533c61f09d338e10da23'
pkgdesc='modern, legacy free, simple yet efficient vim-like editor'
arch=('i686' 'x86_64')
url='http://www.brain-dump.org/projects/vis/'
depends=('ncurses' 'libtermkey' 'lua' 'lua-lpeg' 'tre' 'file')
checkdepends=('vim')
license=('custom:ISC')
backup=('etc/vis/visrc.lua')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/martanne/${pkgname}/archive/v${pkgver}.tar.gz"
        "${_pkgname_test}-${_commit_test}.tar.gz::https://github.com/martanne/${_pkgname_test}/archive/${_commit_test}.tar.gz")
sha256sums=('f11ba41cfb86dd39475960abfd12469de4da0ccfdb941f1d7680d89d987694c5'
            '9574473e00b6db1acab22fc3927ba18a9ba40b1ba5fff497c3e096e6f6faff58')

prepare() {
	cd "${pkgname}-${pkgver}/"

	rmdir test/
	mv "../${_pkgname_test}-${_commit_test}" test
}

build() {
	cd "${pkgname}-${pkgver}/"

	./configure \
		--prefix=/usr

	make
}

check() {
	cd "${pkgname}-${pkgver}/"

	make -C test/
}

package() {
	cd "${pkgname}-${pkgver}/"

	make DESTDIR="${pkgdir}" install

	install -D -m0644 lua/visrc.lua "${pkgdir}"/etc/vis/visrc.lua

	install -D -m0644 'LICENSE' "${pkgdir}/usr/share/licenses/vis/LICENSE"
}

