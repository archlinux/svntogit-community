# $Id: PKGBUILD 149613 2012-02-08 20:20:01Z pierre $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=krusader
pkgver=2.5.0
pkgrel=1
pkgdesc="Advanced twin panel (commander style) file manager for KDE"
arch=('i686' 'x86_64')
url="https://krusader.org/"
license=('GPL')
depends=('karchive' 'kbookmarks' 'kcodecs' 'kcompletion' 'kcoreaddons' 'kconfig'
         'kdoctools' 'ki18n' 'kiconthemes' 'kitemviews' 'kio' 'knotifications'
         'kparts' 'solid' 'ktextwidgets' 'kwallet' 'kwidgetsaddons' 'kwindowsystem'
         'kxmlgui' 'kguiaddons' 'hicolor-icon-theme')
makedepends=('cmake' 'extra-cmake-modules' 'python')
source=("http://download.kde.org/stable/krusader/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('5074c7c8dcd7fa8c8509f472bc2e0815')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
