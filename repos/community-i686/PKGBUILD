# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.135
pkgrel=1
pkgdesc="A free cross-platform lobby client for the Spring RTS project."
arch=('i686' 'x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('wxgtk' 'curl' 'libtorrent-rasterbar' 'boost-libs')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer') 
makedepends=('boost' 'asio' 'cmake')
source=(http://www.springlobby.info/tarballs/${pkgname}-${pkgver}.tar.bz2 
        springlobby.desktop
        springlobby.png)
md5sums=('6d02bc0d91a112591460ac86e6a9baaa'
         '45f8b59d033931d02e734fe3bd7777dd'
         '1f388187539aeb0358b51995e26ed890')

build() {
  cd $srcdir/${pkgname}-$pkgver

  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  cp $srcdir/${pkgname}-$pkgver/tools/test-susynclib.awk $srcdir/${pkgname}-$pkgver/tools/test-susynclib.awk.bak
  sed -e 's|/usr/bin/awk|/bin/awk|g' $srcdir/${pkgname}-$pkgver/tools/test-susynclib.awk.bak>$srcdir/${pkgname}-$pkgver/tools/test-susynclib.awk

  sed -i 's/boost_system-mt/boost_system/g' $srcdir/$pkgname-$pkgver/CMakeLists.txt
  #sed -i '/curl\/types.h/d' $srcdir/$pkgname-$pkgver/src/utils/downloader.cpp
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPTION_SOUND=false
  make 
}

package() {
  cd $srcdir/${pkgname}-$pkgver

  make DESTDIR=$pkgdir install
  
  install -m755 -d $pkgdir/usr/share/applications
  install -m644 $srcdir/springlobby.desktop $pkgdir/usr/share/applications

  install -m755 -d -p $pkgdir/usr/share/pixmaps
  install -m644 $srcdir/springlobby.png $pkgdir/usr/share/pixmaps
}

# vim: sw=2:ts=2 et:
