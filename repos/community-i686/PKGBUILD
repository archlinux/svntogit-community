# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=lxc
pkgver=0.9.0
epoch=1
pkgrel=5
pkgdesc="Linux Containers"
arch=('i686' 'x86_64')
url="http://linuxcontainers.org"
depends=('bash' 'perl' 'libseccomp')
makedepends=('docbook2x')
optdepends=('arch-install-scripts: for archlinux template')
license=('LGPL')
install=lxc.install
source=("http://linuxcontainers.org/downloads/$pkgname-${pkgver}.tar.gz"
	"lxc.tmpfiles.d"
	"lxc@.service")
md5sums=('8552a4479090616f4bc04d8473765fc9'
         'df94c9fb8a753011c86ee664e9f521ff'
         '8003139e60ea66c05460660f31780706')

prepare() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  sed -i \
    -e "s/for name in docbook2x-man db2x_docbook2man; do/for name in docbook2x-man db2x_docbook2man docbook2man; do/" \
    -e "s|AM_INIT_AUTOMAKE.*|AM_INIT_AUTOMAKE([-Wall -Werror -Wno-portability subdir-objects])|" \
    configure.ac
}

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --disable-apparmor \
    --enable-seccomp
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  make DESTDIR="$pkgdir" install
  install -d -m755 "$pkgdir/var/lib/lxc"
  install -D -m644 ${srcdir}/lxc@.service ${pkgdir}/usr/lib/systemd/system/lxc@.service
  install -D -m644 ${srcdir}/lxc.tmpfiles.d ${pkgdir}/usr/lib/tmpfiles.d/lxc.conf

  cd doc
  find . -type f -name '*.1' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man1/{}" \;
  find . -type f -name '*.5' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man5/{}" \;
  find . -type f -name '*.7' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man7/{}" \;
}
