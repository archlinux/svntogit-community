# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: KokaKiwi <admin@kokaelkiwi.eu>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgname=nanomsg
pkgver=0.8
_tag=$pkgver-beta
pkgrel=1
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
url='http://nanomsg.org/'
license=(MIT)
arch=(i686 x86_64)
depends=(glibc)
source=(nanomsg-$pkgver.tar.gz::https://github.com/nanomsg/nanomsg/archive/$_tag.tar.gz
        fix_tests.patch::https://github.com/nanomsg/nanomsg/commit/1a5a90b298e7bf4c5f8be73b57d46a577af8aa5f.patch)
sha1sums=('8a42a6ff71731fdeb240979e796a92bde58f17e9'
          '646788e94800f85441d905d5027925f13effde82')


prepare() {
  cd nanomsg-$_tag
  patch -p1 < ../fix_tests.patch
}

build() {
  cd nanomsg-$_tag
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd nanomsg-$_tag
  make check -j1
}

package() {
  cd nanomsg-$_tag
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
