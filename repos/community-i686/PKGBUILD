# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=0.0.6
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('dtkwidget' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('76f636470254a5f4f592798683fae9dba350c9429721f1ec973ad16f985c9c178c9471e1a9394fd5d7b03aabeaa71c7f83e71451b62c50adcfc3794a1ad8e2f9')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' deepin-clone.pro deepin-clone-ionice deepin-clone-pkexec com.deepin.pkexec.deepin-clone.policy{,.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
