# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: fanningert <thomas@fanninger.at>

pkgname=glide
pkgver=0.13.0
pkgrel=1
pkgdesc='Dependency management and vendoring for Go projects'
arch=('x86_64' 'i686')
_gourl="github.com/Masterminds"
url='https://github.com/Masterminds/glide'
license=('MIT')
makedepends=('git' 'go')
depends=('glibc')
provides=('glide')
options=('!strip' '!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://$_gourl/glide/archive/v${pkgver}.tar.gz")
sha256sums=('ebbfa28e547db921add3557317c810f87d4ece983213d8e9899783b3e3b43ae7')

prepare() {
  mkdir -p build/go; cd build/go
  for f in "/usr/lib/go/"*; do ln -s "$f"; done
  rm pkg; mkdir pkg; cd pkg
  for f in "/usr/lib/go/pkg/"*; do ln -s "$f"; done
  export GOROOT="$srcdir/build/go"
  export GOPATH="$srcdir/build"
  mkdir -p "$GOPATH/src/$_gourl"
  mv "$srcdir/$pkgname-$pkgver" "$GOPATH/src/$_gourl/$pkgname"
}

build() {
  cd "$GOPATH/src/${_gourl}/${pkgname}"

  go fix && go build -o glide -ldflags "-X main.version=$pkgver" glide.go
}

package() {
  install -Dm755 "build/src/$_gourl/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "build/src/$_gourl/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: https://github.com/Masterminds/glide
# vim:set ts=2 sw=2 et:
