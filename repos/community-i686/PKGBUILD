# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Ivan Bobrov <ibobrik at gmail dot com>
# Contributor: Black_Mage <vleon1 at gmail dot com>

pkgname=astromenace
pkgver=1.2.0
pkgrel=6
pkgdesc="Hardcore 3D space shooter with spaceship upgrade possibilities. English version"
arch=('i686' 'x86_64')
url="http://www.viewizard.com/astromenace/index_linux.php"
license=('GPL3')
depends=('sdl' 'freealut' 'libjpeg' 'libvorbis' 'glu')
makedepends=('cmake' 'mesa')
source=(http://downloads.sourceforge.net/openastromenace/openamenace-src-$pkgver.tar.bz2
        http://downloads.sourceforge.net/openastromenace/oamenace-data-$pkgver.tar.bz2
        http://downloads.sourceforge.net/openastromenace/oamenace-lang-en-$pkgver.tar.bz2
        astromenace.desktop
        astromenace.png
        astromenace.patch
        astromenace.sh)
md5sums=('3c8e7013aa64fcde7ba8a7750df51ab8'
         'a8ad3495f8eac419ebcf93473008484d'
         'f137da529da1345a0243f22fb8bf4913'
         '1edc9367564a6c639a2b8199adf26a08'
         '1643d876d4e21cc0c6ceddc8e7c8ceed'
         '8d23310a2bb9c96dd150d30ab39799a1'
         '2d4504929a620709cd058657bba335b3')

build() {
    cd $srcdir/OpenAstroMenaceSVN

    # Patch build and install the engine
    patch  -Np1 -i $srcdir/$pkgname.patch
    cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr
    sed -i 's/`sdl-config --libs`/-lSDL -lpthread/' CMakeFiles/AstroMenace.dir/link.txt
    make
}

package() {
    cd $srcdir/OpenAstroMenaceSVN

    # Manually install the engine and data
    install -Dm 755 AstroMenace $pkgdir/usr/bin/astromenace_bin
    install -Dm 644 $srcdir/gamedata.vfs $pkgdir/usr/share/astromenace/gamedata.vfs
    install -Dm 644 $srcdir/gamelang_en.vfs $pkgdir/usr/share/astromenace/gamelang_en.vfs
    install -d $pkgdir/usr/share/astromenace/DATA/SCRIPT
    install -m 644  $srcdir/DATA/SCRIPT/* $pkgdir/usr/share/astromenace/DATA/SCRIPT

    # Install .desktop, icon and bin
    install -Dm 644 $srcdir/astromenace.desktop $pkgdir/usr/share/applications/astromenace.desktop
    install -Dm 644 $srcdir/astromenace.png $pkgdir/usr/share/pixmaps/astromenace.png
    install -Dm 755 $srcdir/astromenace.sh $pkgdir/usr/bin/astromenace
}
