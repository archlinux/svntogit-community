# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Contributor: 3ED <krzysztof1987@gmail.com>

pkgname=caja-actions
pkgver=1.8.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="An extension for Caja which allows arbitrary programs to be launched via context menu."
url="https://github.com/NiceandGently/caja-actions"
license=('GPL')
depends=('caja' 'libgtop')
makedepends=('mate-common' 'yelp-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/raveit65/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('c9d09ef73979adbabaa7b4e618164b935cb6c7219e83803d7a61aa3fa498f5ad')
install=${pkgname}.install

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2 \
        --disable-static
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
