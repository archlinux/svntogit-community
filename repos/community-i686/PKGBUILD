# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=ndisc6
pkgver=1.0.2
pkgrel=7
pkgdesc='Collection of IPv6 networking utilities'
arch=('i686' 'x86_64')
url='http://www.remlab.net/ndisc6/'
license=('GPL')
depends=('glibc' 'perl')
makedepends=('glibc' 'gcc')
options=(!emptydirs)
install=$pkgname.install
source=("http://www.remlab.net/files/ndisc6/$pkgname-$pkgver.tar.bz2"
	      'rdnssd.service')
md5sums=('50cb4c19606cf6ff2b7388e71832f579'
         '88a290cbcf51ac5e10ed5f995a8ba4b9')

prepare() {
  # patch invalid path
  sed -ri 's,PATH=/sbin:/bin,PATH=/sbin:/bin:/usr/sbin:/usr/bin,' \
    $pkgname-$pkgver/rdnssd/merge-hook.in
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/rdnssd.service" "$pkgdir/usr/lib/systemd/system/rdnssd.service"
  install -Dm644 /dev/null "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  echo 'd /run/rdnssd 0755 nobody root' > "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
