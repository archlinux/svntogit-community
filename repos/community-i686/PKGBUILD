# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.10.1
pkgrel=1
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('dkms' 'jsoncpp' 'luajit' 'curl')
makedepends=('cmake' 'pandoc')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('fa98d6ec98666e5e052ebebc30d6b40d2b0ca79ce22e236bab39a2cda725297f')
install=$pkgname.install

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  sed 's|${DIR_ETC}/bash_completion.d|share/bash-completion/completions/|' \
      -i scripts/CMakeLists.txt
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DUSE_BUNDLED_JSONCPP=OFF \
        -DUSE_BUNDLED_B64=ON \
        -DBUILD_DRIVER=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  cd build
  make install DESTDIR="$pkgdir"
}
