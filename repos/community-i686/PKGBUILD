# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-i18nmessageid' 'python2-zope-i18nmessageid')
pkgver=4.0.3
pkgrel=2
pkgdesc="Message Identifiers for internationalization"
arch=('i686' 'x86_64')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/zopefoundation/zope.i18nmessageid.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.i18nmessageid{,-py2}
}

build() {
  cd "$srcdir/zope.i18nmessageid"
  python setup.py build

  cd "$srcdir/zope.i18nmessageid-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/zope.i18nmessageid"
  nosetests3

  cd "$srcdir/zope.i18nmessageid-py2"
  nosetests2
}

package_python-zope-i18nmessageid() {
  depends=('python-setuptools')

  cd "$srcdir/zope.i18nmessageid"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-zope-i18nmessageid() {
  depends=('python2-setuptools')

  cd "$srcdir/zope.i18nmessageid-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
