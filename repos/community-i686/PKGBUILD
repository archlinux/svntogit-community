# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=14.0
pkgrel=2
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('libxml2' 'python')
changelog=$pkgname.changelog
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('41d243617f3523dcba86676ff1193fa361562aecdfa100ebc01bf57bc26d38e5')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck --with-tlslib=OpenSSL
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/$pkgname
  install -m644 -t "${pkgdir}"/usr/share/$pkgname AUTHORS ChangeLog README
}
