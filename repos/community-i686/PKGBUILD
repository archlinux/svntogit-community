# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libparserutils
pkgver=0.2.1
pkgrel=1
pkgdesc='Library for building efficient parsers'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libparserutils'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('30589e6b3adf0c14b5a78de1fba3b9b84cbd74e95e22d45827fa3e09b563f735')

build() {
	cd "$pkgname-$pkgver"

	sed 's:_BSD_SOURCE:_DEFAULT_SOURCE:' -i Makefile
	make LIBDIR=lib PREFIX=/usr COMPONENT_TYPE="lib-shared"
}

package() {
	cd "$pkgname-$pkgver"

	make install LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared" 
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
