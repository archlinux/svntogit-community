# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.13
pkgrel=1
pkgdesc='Minimalistic Mumble server'
arch=('i686' 'x86_64')
url='http://code.google.com/p/umurmur/'
license=('custom')
depends=('openssl' 'libconfig' 'protobuf-c')
install=$pkgname.install
backup=('etc/umurmur/umurmur.conf')
source=(http://umurmur.googlecode.com/files/umurmur-$pkgver.tar.gz
        umurmur.service)
md5sums=('04d0ea7a90b7858c16df26317b318bf4'
         'd9d556e4ffa77e193fb40ce508804720')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-ssl=openssl
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 umurmur.conf.example "$pkgdir"/etc/umurmur/umurmur.conf
  install -Dm644 ../umurmur.service "$pkgdir"/usr/lib/systemd/system/umurmur.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
