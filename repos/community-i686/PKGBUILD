# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Tim Hütz <tim@huetz.biz>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgbase=python-mpi4py
pkgname=(python-mpi4py python2-mpi4py)
pkgver=1.3.1
pkgrel=3
pkgdesc='Python bindings for the Message Passing Interface (MPI) standard'
arch=(x86_64 i686)
url='https://planet.scipy.org/'
license=(BSD)
makedepends=(python-setuptools python2-setuptools openmpi)
source=(https://pypi.python.org/packages/source/m/mpi4py/mpi4py-$pkgver.tar.gz)
sha1sums=('083a4a9b6793dfdbd852082d8b95da08bcf57290')

prepare() {
  cp -a mpi4py-$pkgver{,-py2}
}

build() {
  cd "$srcdir/mpi4py-$pkgver"
  python setup.py build

  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py test

  cd "$srcdir/mpi4py-$pkgver"
  # Tests fail on Python3
  # https://bitbucket.org/mpi4py/mpi4py/issues/2/fails-test-with-py33
  # python setup.py test
}

package_python-mpi4py() {
  depends=(python openmpi)

  cd "$srcdir/mpi4py-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-mpi4py() {
  depends=(python2 openmpi)

  cd "$srcdir/mpi4py-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
