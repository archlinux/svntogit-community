# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=sleuthkit
pkgver=4.4.2
pkgrel=1
pkgdesc='File system and media management forensic analysis tools'
arch=('i686' 'x86_64')
url="http://www.sleuthkit.org/sleuthkit"
license=('GPL2' 'CPL' 'custom:"IBM Public Licence"')
depends=('perl' 'libewf')
changelog=$pkgname.changelog
source=(https://github.com/$pkgname/$pkgname/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('0917A7EE58A9308B13D3963338AD602EC7454C8B') #Brian Carrier <carrier@sleuthkit.org>
sha256sums=('135964463f4b0a58fcd95fdf731881fcd6f2f227eeb8ffac004880c8e4d8dd53'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

#licenses
  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm0644 licenses/* "${pkgdir}"/usr/share/licenses/${pkgname}
}
