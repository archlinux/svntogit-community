# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=cataclysm-dda
pkgver=0.C
pkgrel=1
pkgdesc="A post-apocalyptic roguelike."
#url="http://cataclysmrl.blogspot.com/"
#url="http://www.cataclysm.glyphgryph.com/"
url="http://en.cataclysmdda.com/"
arch=('i686' 'x86_64')
license=("CCPL:by-sa")
depends=('ncurses' 'lua')
makedepends=('sdl2_image' 'sdl2_ttf' 'sdl2_mixer' 'freetype2')
optdepends=('sdl2_image: for tiles'
            'sdl2_ttf: for tiles'
            'freetype2: for tiles'
            'sdl2_mixer: for sound')
install=cataclysm-dda.install
source=("https://github.com/CleverRaven/Cataclysm-DDA/archive/$pkgver.tar.gz")
md5sums=('805132ab7651ba93e5247ced7fe1fc97')

# official docs say to use lua51
# but makefile supports lua52?
# what the heck, let's try lua53

# tiles + executable are about half of the package
# consider splitting?

build() {
  cd "Cataclysm-DDA-$pkgver"

  sed -i 's/ncursesw5-config/ncursesw6-config/' Makefile
  #sed -i 's|"\(l.*h\)"|"lua5.1/\1"|' src/catalua.{h,cpp}

  make PREFIX=/usr RELEASE=1 ZLEVELS=1 USE_HOME_DIR=1 LUA=1
  make PREFIX=/usr RELEASE=1 ZLEVELS=1 USE_HOME_DIR=1 LUA=1 TILES=1 SOUND=1
  #LUA_BINARY="/usr/bin/lua5.1"
}

package() {
  cd "Cataclysm-DDA-$pkgver"

  # no DESTDIR
  make PREFIX="$pkgdir/usr" \
  RELEASE=1 ZLEVELS=1 USE_HOME_DIR=1 LUA=1 TILES=0 SOUND=0 \
  install
  # doesn't install executable?
  install -Dm755 cataclysm "$pkgdir/usr/bin/cataclysm"

  make PREFIX="$pkgdir/usr" \
  RELEASE=1 ZLEVELS=1 USE_HOME_DIR=1 LUA=1 TILES=1 SOUND=1 \
  install
}
