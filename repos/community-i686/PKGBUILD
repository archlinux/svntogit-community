# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.9
pkgrel=1
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('i686' 'x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse' 'libtorrent-rasterbar' 'curl')
makedepends=('git' 'boost')
optdepends=('python2: for btplay')
source=("git://github.com/johang/btfs.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$srcdir/$pkgname"
  sed -i 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' scripts/btplay
}

build() {
  cd "$srcdir/$pkgname"
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}
