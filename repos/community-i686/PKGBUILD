# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
epoch=1
pkgver=2.0.2
pkgrel=1
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('i686' 'x86_64')
url="http://www.poedit.net/"
license=('MIT')
# poedit-2.0: https://github.com/vslavik/poedit/issues/366
depends=('wxgtk' 'desktop-file-utils' 'lucene++' 'gtkspell')
makedepends=('boost' 'libsm')
source=(https://github.com/vslavik/poedit/releases/download/v$pkgver-oss/poedit-${pkgver}.tar.gz
        icu59.patch)
sha256sums=('b88bc222d4215344d0eb87dda19aafcc0b2465718c4b556015330beb96f7b402'
            '4d2fcbc1ce5a8d8bab878687337c1f038ed1a83b37652e0a41d7f5c2b26b27bc')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i ../icu59.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --libexecdir=/usr/lib/poedit
    make CPPFLAGS+=' -DUCHAR_TYPE=uint16_t'
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
