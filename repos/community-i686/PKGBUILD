# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=13.0
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('libpar2' 'libsigc++' 'libxml2' 'python')
changelog=${pkgname}.changelog
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('666f5244f31f10333a18450e7a6c1ac729ac68594042236711ba99dd32e78f8e')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck --with-tlslib=OpenSSL
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/${pkgname}
  install -m644 -t "${pkgdir}"/usr/share/${pkgname} AUTHORS ChangeLog README
}
