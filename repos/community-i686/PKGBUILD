# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libgeotiff
pkgver=1.4.1
pkgrel=1
pkgdesc="A TIFF based interchange format for georeferenced raster imagery"
arch=('i686' 'x86_64')
url="http://www.remotesensing.org/geotiff"
license=('custom')
depends=('libtiff' 'proj' 'libjpeg>=7')
options=('!makeflags')
changelog=$pkgname.changelog
source=(http://download.osgeo.org/geotiff/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('acfc76ee19b3d41bb9c7e8b780ca55d413893a96c09f3b27bdb9b2573b41fd23')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/libgeotiff/LICENSE
}
