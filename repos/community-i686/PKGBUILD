# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: J0k3r <moebius282 e4a t gmail D0_T com>
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor: JerichoKru   <jerichokru(AT)hotmail.com>

pkgname=pragha
pkgver=1.3.0
pkgrel=1
pkgdesc="A lightweight GTK+ music manager - fork of Consonance Music Manager."
arch=('i686' 'x86_64')
url="http://pragha.wikispaces.com/"
license=('GPL3')
depends=('libnotify' 'libcdio-paranoia' 'gst-plugins-base'
         'taglib' 'hicolor-icon-theme'  'gtk3'
         'dbus-glib' 'sqlite' 'desktop-file-utils'
         'libclastfm' 'libpeas')
optdepends=('notification-daemon: OSD notification'
            'gst-plugins-good: A collection of gstreamer codecs'
            'gst-plugins-bad: A collection of gstreamer codecs'
            'gst-plugins-ugly: A collection of gstreamer codecs')
makedepends=('intltool')
replaces=('dissonance')
install=pragha.install
source=("https://github.com/matiasdelellis/pragha/releases/download/v$pkgver/pragha-$pkgver.tar.bz2")
sha1sums=('8fb6c074292a65915cc21c3b0b0ca35d65afff58')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  CPPFLAGS="-DHAVE_PARANOIA_NEW_INCLUDES" LIBS+="-ldbus-glib-1" \
    ./configure --prefix=/usr \
    --with-gstreamer=1.0
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  install -m 644 "data/$pkgname.desktop" "$pkgdir/usr/share/applications"
  install -d "$pkgdir/usr/share/pixmaps"
  install -m 644 "data/$pkgname.png" "$pkgdir/usr/share/pixmaps/"
  install -m 644 "data/$pkgname.1" "$pkgdir/usr/share/man/man1/"

}

# vim:ts=2:sw=2:expandtab

