# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.31
pkgrel=1
pkgdesc='Plugins for Geany'
arch=('x86_64' 'i686')
url='https://plugins.geany.org/'
license=('GPL')
depends=("geany>=$pkgver" 'gtkspell' 'ctpl' 'gpgme' 'vte' 'desktop-file-utils'
         'lua51' 'libgit2' 'python2' 'pygtk')
makedepends=('intltool' 'vala' 'gdb' 'cppcheck' 'libgit2')
optdepends=('hspell: hebrew spell checker')
source=("https://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('76bd9e803db5a626b86669f08330cf95b8cc35057a1cdf65759bc00aef120e25')

build() {
  cd "$pkgname-$pkgver"

  # Looking forward to Python 3 being supported instead
  export PYTHON=/usr/bin/python2

  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
