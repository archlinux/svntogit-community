# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgbase=quassel
pkgname=('quassel-client' 'quassel-core')
pkgver=0.9.1
pkgrel=3
pkgdesc="QT4 irc client with a separated core"
arch=('i686' 'x86_64')
url="http://quassel-irc.org/"
license=('GPL')
makedepends=('cmake' 'automoc4' 'qt4' 'icu' 'kdelibs')
source=(http://quassel-irc.org/pub/$pkgbase-$pkgver.tar.bz2
        $pkgbase.service
        $pkgbase.conf)
sha256sums=('39e97bd67160d6575a57d4f71c4b1be5c13461ca238baef462ab43164419864e'
            '88b5b693ad7eea7fb2d084da29c6abc26ea5fa990668f9807d462778243f1d6a'
            'f3031ea8217e01ba42cea14606169e3e27affa5918968ffd5a03c21ae92fe2b8')

build() {
  cd $srcdir
  [[ ! -d build ]] && mkdir build
  mkdir build-client

  # Core
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=0 \
    -DCMAKE_BUILD_TYPE="Release" \
    -DWITH_OPENSSL=ON \
    -DWANT_CORE=ON \
    -DWANT_MONO=OFF \
    -DWANT_QTCLIENT=OFF \
    -DWITH_DBUS=OFF \
    -DWITH_OXYGEN=OFF \
    -DWITH_PHONON=OFF \
    -DWITH_WEBKIT=OFF \
    -DCMAKE_BUILD_TYPE="Release" \
    ../quassel-${pkgver}/ \
    -Wno-dev

  make

  # Client
  cd $srcdir/build-client
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DWITH_OPENSSL=ON \
    -DCMAKE_BUILD_TYPE="Release" \
    -DWANT_CORE=OFF \
    -DWANT_QTCLIENT=ON \
    -DWANT_MONO=ON \
    ../quassel-${pkgver}/ \
    -Wno-dev

  make
  
}

package_quassel-core() {
pkgdesc="Qt4 IRC client with a separated core - core only"
depends=('qt4' 'icu')
backup=(etc/conf.d/quassel)
install=quassel.install
  cd $srcdir/build

  make DESTDIR="${pkgdir}" install
  
  # Remove unneeded pixmap icons
  rm -r $pkgdir/usr/share


  install -Dm644 "${srcdir}"/$pkgbase.service \
    "${pkgdir}"/usr/lib/systemd/system/$pkgbase.service
  install -Dm644 "${srcdir}"/$pkgbase.conf \
    "${pkgdir}"/etc/conf.d/$pkgbase
}

package_quassel-client() {
pkgdesc="Qt4 IRC client with a separated core - client only"
depends=('qt4' 'kdelibs')
install=quassel-client.install
  cd $srcdir/build-client

  make DESTDIR=${pkgdir} install
}
