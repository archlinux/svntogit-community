# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.3.1.1
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-terminal'
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'libnotify' 'vte290')
makedepends=('cmake' 'vala')
install='pantheon-terminal.install'
source=("https://launchpad.net/pantheon-terminal/0.3.x/${pkgver}/+download/pantheon-terminal-${pkgver}.tgz")
sha256sums=('928f705376a4da5ed6f9319333d9af3e87a24c7feab21107715191e00c6abc96')

build() {
  cd pantheon-terminal-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd pantheon-terminal-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
