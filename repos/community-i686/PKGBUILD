# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=2.8.4
pkgrel=3
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'libcanberra-pulse' 'librsvg' 'nss'
         'pulseaudio-alsa' 'upower' 'libnotify' 'libgnomekbd' 'libwacom'
         'libgudev' 'dbus-glib')
makedepends=('intltool' 'docbook-xsl' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs')
install=cinnamon-settings-daemon.install
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
        "0001-power-refresh-device-if-any-properties-changed.patch")
sha256sums=('2f7e8764a842d9b4f21c1e6d5321e4101720f1600462b5452d14a061b608c584'
            'a6e43f32db01f452af8eaa98ff40063313388ba4b1045eff18804ccd02dae099')

prepare() {
  cd $pkgname-$pkgver

  # power: refresh device if any properties changed
  # https://github.com/linuxmint/cinnamon-settings-daemon/pull/113
  patch -Np1 -i ../0001-power-refresh-device-if-any-properties-changed.patch

  # Support new libsystemd (which replaces libsystemd-login)
  sed -i 's/libsystemd-login/libsystemd/' configure.ac
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
