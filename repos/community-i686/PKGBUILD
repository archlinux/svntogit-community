# $Id$
# Maintainer  : Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer  : Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-cli
pkgver=1.4.5
pkgrel=2
pkgdesc='The OpenVAS Command-Line Interface'
arch=('i686' 'x86_64')
url='http://www.openvas.org/'
license=('GPL')
depends=('openvas-libraries')
makedepends=('cmake' 'xmltoman' 'doxygen')
groups=(openvas)
# These URLs need to be manually updated on every pkgver upgrade.
# http://wald.intevation.org/frs/?group_id=29
source=("http://wald.intevation.org/frs/download.php/2397/${pkgname}-${pkgver}.tar.gz"
        "http://wald.intevation.org/frs/download.php/2398/${pkgname}-${pkgver}.tar.gz.sig")
sha512sums=('35ec391d1e25bfe1a49507b9e2ec69243411a6e839232334bb0f2eed26750a159942b7d14528fab9e57e1c7d7bf40fa087686444168ced4e4281ad78f2aeae85'
            'SKIP')
validpgpkeys=(
              'C3B468D2288C68B9D526452248479FF648DB4530' # OpenVAS Transfer Integrity
)

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
}
