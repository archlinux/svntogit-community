# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=blobby2
pkgver=1.0
arch=('x86_64' 'i686')
pkgrel=2
pkgdesc="A beach ball game with blobs of goo"
url="http://sourceforge.net/projects/blobby/"
license=('GPL')
depends=('sdl2' 'physfs' 'libgl')
makedepends=('cmake' 'boost' 'mesa' 'zip')
source=(http://downloads.sourceforge.net/blobby/$pkgname-linux-$pkgver.tar.gz
        ostream-include.patch
        $pkgname.desktop
        $pkgname.png)
md5sums=('a6c89ae64693490385fa05c0a01b0a3b'
         '926f1e5760769548562c2a569fe18196'
         'ab05bed794ee78db693fd3036393275a'
         'c1bc427b41a0a3facd771ac83c7fb412')

prepare() {
  cd $srcdir/blobby-$pkgver
  patch -Np2 -i ../ostream-include.patch # r1542 upstream
  sed -i "s|data|/usr/share/blobby|g" src/main.cpp
  sed -i "s|file\(filename\)|file(\"/usr/share/blobby\" + filename|g" src/main.cpp
}

build() {
  cd $srcdir/blobby-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/blobby-$pkgver

  make DESTDIR=$pkgdir install

  install -Dm644 data/Icon.bmp ${pkgdir}/usr/share/blobby/Icon.bmp

  # install .desktop file and icon
  install -dm755 $pkgdir/usr/share/{applications,pixmaps}
  install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications
  install -m644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps
}
