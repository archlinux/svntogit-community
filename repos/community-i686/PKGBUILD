# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>

pkgname=supertuxkart
pkgver=0.9.3rc1
_pkgver=0.9.3-rc1
pkgrel=2
pkgdesc='Kart racing game featuring Tux and his friends'
arch=('i686' 'x86_64')
url='http://supertuxkart.sourceforge.net/'
license=('GPL2')
depends=('openal' 'libvorbis' 'fribidi' 'curl' 'bluez-libs' 'libxrandr' 'glu')
makedepends=('cmake' 'subversion' 'mesa' 'imagemagick' 'setconf' 'mesa-libgl')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}-src.tar.xz")
sha512sums=('b68310142ce47fb8f58a61ed7e03c4ed2e38a0c46f768d00c18e5727d075f052445e912cacb92084d5057d8fb453acc735fcd44b5b9ccf87ea8f6427bb8a6e66')

build() {
  cd ${srcdir}/supertuxkart-${_pkgver}

  _fn="data/${pkgname}.desktop"
  setconf "$_fn" Exec "$pkgname"
  setconf "$_fn" TryExec "$pkgname"
  setconf "$_fn" Icon "$pkgname"

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DIRRLICHT_DIR="$srcdir/irrlicht" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_RECORDER=0 \
    -DCMAKE_CXX_FLAGS="-lpthread -lm -ldl $CXXFLAGS -std=gnu++98"

  make
}

package() {
  cd ${srcdir}/supertuxkart-${_pkgver}

  cd build
  make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
