# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=atomicparsley
pkgver=0.9.0
pkgrel=6
pkgdesc="A command line program for reading, parsing and setting iTunes-style metadata in MPEG4 files"
arch=('i686' 'x86_64')
url="http://atomicparsley.sourceforge.net/"
license=('GPL')
depends=('gcc-libs')
source=(http://downloads.sourceforge.net/$pkgname/AtomicParsley-source-$pkgver.zip
        atomicparsley.diff)
sha1sums=('6a73aed6ba569e693fe94a313e7e9e9ce204d038'
          'c17939f2348da4de2787f2f87dfa6679a02b8f5f')

prepare() {
  cd "${srcdir}/AtomicParsley-source-${pkgver}"
  patch -p0 -i "${srcdir}/atomicparsley.diff"
}

build() {
  cd "${srcdir}/AtomicParsley-source-${pkgver}"
  ./build
}

package() {  
  cd "${srcdir}/AtomicParsley-source-${pkgver}"
  install -D -m755 AtomicParsley "${pkgdir}/usr/bin/AtomicParsley"
}
