# $Id$
# Maintainer: Lukas Fleischer <archlinux@cryptocrack.de>
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>
# Contributor: Ricardo Catalinas Jiménez <jimenezrick@gmail.com>

pkgbase=erlang
pkgname=('erlang' 'erlang-unixodbc')
pkgver=R15B03_1
pkgrel=3
arch=('x86_64' 'i686')
url='http://www.erlang.org/'
license=('custom')
makedepends=('perl' 'mesa' 'lksctp-tools' 'unixodbc')
source=("http://www.erlang.org/download/otp_src_${pkgver/_/-}.tar.gz"
        "http://www.erlang.org/download/otp_doc_man_${pkgver/_/-}.tar.gz")
sha256sums=('4bccac86dd76aec050252e44276a0283a0df9218e6470cf042a9b9f9dfc9476c'
            '07980d8014c7cf8194b7078c137353f5083992add4663ced3dcba2ff91f228d8')

build() {
  cd "$srcdir/otp_src_${pkgver/_1/}"

  ./configure --prefix=/usr --enable-smp-support --with-odbc
  make
}

package_erlang() {
  pkgdesc='General-purpose concurrent functional programming language developed by Ericsson'
  depends=('ncurses' 'glu' 'wxgtk' 'openssl')
  optdepends=('erlang-unixodbc: database support'
              'java-environment: for Java support'
              'lksctp-tools: for SCTP support')

  cd "$srcdir/otp_src_${pkgver/_1/}"

  make DESTDIR="$pkgdir" install

  # Documentation
  install -d "$pkgdir/usr/share/doc/erlang"
  install -m0644 "$srcdir/otp_src_${pkgver/_1/}/README.md" \
    "$srcdir"/{README,COPYRIGHT} \
    "$pkgdir/usr/share/doc/erlang"

  # Compressed man pages
  for page in "$srcdir/man/man?/*"; do gzip $page; done
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang/"

  # License
  install -Dm0644 "$srcdir/otp_src_${pkgver/_1/}/EPLICENCE" \
    "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"

  # Move over files that will be packaged as erlang-unixodbc
  mkdir "$srcdir/unixodbc"
  mv "$pkgdir/usr/lib/erlang/lib/odbc"* "$srcdir/unixodbc/"
  mv "$pkgdir/usr/lib/erlang/man/man3/odbc.3.gz" "$srcdir"
}

package_erlang-unixodbc() {
  pkgdesc='Unixodbc support for Erlang'
  depends=('unixodbc' 'erlang')

  # Get the files that should be packaged as erlang-unixodbc
  mkdir -p "$pkgdir/usr/lib/erlang/"{lib,man/man3}
  mv "$srcdir/unixodbc/"* "$pkgdir/usr/lib/erlang/lib/"
  mv "$srcdir/odbc.3.gz" "$pkgdir/usr/lib/erlang/man/man3/"

  # License
  install -Dm0644 "$srcdir/otp_src_${pkgver/_1/}/EPLICENCE" \
    "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"
}

# vim:set ts=2 sw=2 et:
