# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=polkit-gnome
pkgver=0.105
pkgrel=2
pkgdesc="Legacy polkit authentication agent for GNOME"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/polkit/"
license=('LGPL')
depends=('polkit' 'gtk3')
makedepends=('intltool')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        polkit-gnome-authentication-agent-1.desktop)
sha256sums=('1784494963b8bf9a00eedc6cd3a2868fb123b8a5e516e66c5eda48df17ab9369'
            '5074c723a4eab274830587d799ba781ff57f4fbe4ac99fbdc5aac5009c441ee7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/polkit-gnome \
      --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/polkit-gnome-authentication-agent-1.desktop" \
      "$pkgdir/usr/share/applications/polkit-gnome-authentication-agent-1.desktop"
}
