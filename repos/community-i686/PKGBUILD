# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: djgera
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.24
pkgrel=2
pkgdesc='Plugins for Geany'
arch=('x86_64' 'i686')
url='http://plugins.geany.org/'
license=('GPL')
depends=("geany>=$pkgver" 'gtkspell' 'webkitgtk2' 'ctpl' 'gpgme' 'vte'
         'desktop-file-utils' 'gtk-update-icon-cache' 'lua51')
makedepends=('intltool' 'vala' 'gdb' 'cppcheck')
optdepends=('hspell: hebrew spell checker')
install="$pkgname.install"
source=("http://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('dcc0414b29ae0160fc3a7adb0a1d6cbd5bd6bfb1900dab03e8774b3cebf6f2ec')

build() {
  cd "$pkgname-$pkgver"

  # Disabling geanypy because it doesn't compile
  # Disabling debugger because it makes geany crash, see FS#42417
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --disable-geanypy \
    --disable-debugger
  make 
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
