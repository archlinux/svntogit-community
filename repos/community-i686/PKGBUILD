# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: PelPix <kylebloss@pelpix.info>

pkgname=fdkaac
pkgver=0.5.1
pkgrel=2
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=('i686' 'x86_64')
url="https://github.com/nu774/${pkgname}"
license=('custom')
depends=('libfdk-aac')
source=("${url}/archive/v${pkgver}.tar.gz")
sha256sums=('95d6e9675ab03f42aa3cb642a686ffaced782a7b440563bd37ef2d4517391348')

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -if
  ./configure --prefix='/usr' --disable-rpath

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
