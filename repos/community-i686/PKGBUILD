# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=0.83.0
_pkgver=83.0
pkgrel=1
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis')
makedepends=('boost' 'cmake' 'zip' 'lzma-utils' 'p7zip' 'python2')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=(http://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${_pkgver}_src.tar.lzma)
md5sums=('05f1c625f2dd81b0d7f30269435ab2f7')

build() {
  bsdtar -xf ${pkgname}_${_pkgver}_src.tar.lzma
  
  cd spring_${_pkgver}
  
  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
	-DDATADIR=share/spring
  make
}

package() {
  cd spring_${_pkgver}

  make DESTDIR=$pkgdir install

  install -d $pkgdir/etc/spring
  echo '$HOME/.spring' > $pkgdir/etc/spring/datadir
}

# vim sw=2:ts=2 et:
