# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: phrakture <aaronmgriffin--gmail--com>
# Contributor: Fazlul Shahriar
# Contributor: Chris Brannon <cmbrannon79@gmail.com>

pkgname=plan9port
pkgver=20130927
pkgrel=3
pkgdesc='Port of many programs from Plan 9 to Unix-like operating systems'
arch=('x86_64' 'i686')
url='http://swtch.com/plan9port/'
license=('custom')
depends=('xorg-server' 'libxt' 'libxext' 'fuse')
optdepends=('python2: for the codereview script')
provides=('plan9')
install='plan9.install'
options=('!zipman' 'staticlibs')
source=("http://swtch.com/$pkgname/$pkgname-$pkgver.tgz"
        'plan9.sh')
sha256sums=('580927688ae2df295bf7f78d0ba19588104c6b0403558ffa3cd9ed0b7b110542'
            'c216efa3455ba0b22b1723ae62e4c1e21b3a776a760d8fcedb902ff994b701c1')

package() {
  cd plan9

  d=/usr/local # /opt

  ./INSTALL -b
  install -Dm755 ../plan9.sh "$pkgdir/etc/profile.d/plan9.sh"
  install -d "$pkgdir$d"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r "$srcdir/plan9" "$pkgdir/$d/"
  cd "$pkgdir$d/plan9"
  ./INSTALL -c -r "$pkgdir$d/plan9"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Clean up
  rm -rf .hg/
  rm -f .hgignore .hgtags
  find . -name '.cvsignore' -print0 |xargs -0 rm -f
  rm -f config install.log install.sum install.txt configure Makefile INSTALL \
    LICENSE

  # Fix hardcoded paths
  for f in `grep -H -r "$pkgdir$d/plan9" | cut -d: -f1`; do
    echo -n "\t$f"
    [ -e "$f" ] && sed -i "s:$pkgdir$d/plan9:$d/plan9:" "$f" || true
  done

  # Fix python scripts
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'

  # Package text files
  for i in CHANGES CONTRIBUTORS README TODO; do
    install -m644 $i "$pkgdir/usr/share/doc/$pkgname"
    rm -f $i
  done

  # Decompress the plan9 man pages
  for i in `find $pkgdir$d/plan9/man -type f`; do
    if [ ${i##*.} = "gz" ]; then
      gunzip "$i"
    fi
  done
}

# vim:set ts=2 sw=2 et:
