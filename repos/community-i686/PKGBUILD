# $Id: PKGBUILD 100035 2013-10-31 11:20:48Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>

pkgname=powerdns-recursor
pkgver=3.7.3
pkgrel=3
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
license=('GPL')
arch=('x86_64' 'i686')
depends=('gcc-libs' 'lua')
makedepends=('boost' 'inetutils')
replaces=('pdns-recursor')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(http://downloads.powerdns.com/releases/pdns-recursor-$pkgver.tar.bz2{,.asc}
        'pdns-recursor.service')
sha256sums=('859ca6071147dd2e2ac1b2a5c3d5c2cbff0f5cbc501660db4259e7cbf27fea11'
            'SKIP'
            'd4f545e85dffa86bf23c89ff0f352c74ec3524dfc53871f129656299222e4fba')
validpgpkeys=('FBAE0323821C7706A5CA151BDCF513FA7EED19F3') # Peter van Dijk <peter.van.dijk@powerdns.com>

build() {
  cd "pdns-recursor-$pkgver"

  ./configure --prefix=/usr
  make -j1 LUA=1 \
    LUA_CPPFLAGS_CONFIG="$(pkg-config --cflags lua)" \
    LUA_LIBS_CONFIG="$(pkg-config --libs lua)"
}

package() {
  cd "pdns-recursor-$pkgver"

  make SBINDIR="/usr/bin" DESTDIR="$pkgdir" install
  mv "$pkgdir/etc/powerdns/recursor.conf-dist" \
    "$pkgdir/etc/powerdns/recursor.conf"
  rm -r "$pkgdir/etc/init.d"
  install -Dm644 "$srcdir/pdns-recursor.service" \
    "$pkgdir/usr/lib/systemd/system/pdns-recursor.service"
}

# vim:set ts=2 sw=2 et:
