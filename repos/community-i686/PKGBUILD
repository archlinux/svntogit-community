# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgbase=python-pillow
pkgname=(python-pillow python2-pillow)
pkgver=4.0.0
pkgrel=3
# consider splitting out sane if this gets weird enough
_sanever=2.8.3
_appname=Pillow
_py2basever=2.7
_py3basever=3.6m
pkgdesc="Python Imaging Library (PIL) fork. Python3 version."
arch=('i686' 'x86_64')
url="http://python-pillow.github.io/"
license=('BSD')
depends=('python' 'lcms2' 'libwebp' 'openjpeg2' 'python-olefile')
optdepends=('tk: for the ImageTK module'
            'sane: for the Sane module'
            'python-pyqt5: for the ImageQt module'
            'python-pyqt4: for the ImageQt module'
            'python-pyside: for the ImageQt module')
makedepends=('python-setuptools' 'python2-setuptools' 'lcms2' 'libwebp' 'openjpeg2' 'tk' 'sane')
source=("https://files.pythonhosted.org/packages/source/P/$_appname/$_appname-$pkgver.tar.gz"
        "Sane-$_sanever.tar.gz::https://github.com/python-pillow/Sane/archive/v$_sanever.tar.gz")
md5sums=('d5af224b0fa2c66dacc9814785fef9e7'
         '96877da43524fdab2c367541da547d2b')

# future optional things:
# libimagequant

prepare() {
  cd "$srcdir"
  # allow sane to build
  sed -i "s|os.path.join|'../libImaging', &|" "$srcdir/Sane-$_sanever/setup.py"
  cp -r "$srcdir/Sane-$_sanever" "$srcdir/$_appname-$pkgver/Sane"
  # py2 and py3
  cp -r "$srcdir/$_appname-$pkgver" "$srcdir/${_appname}2-$pkgver"
}  

package_python-pillow() {
  depends=('python' 'lcms2' 'libwebp' 'openjpeg2' 'python-olefile')
  optdepends=('tk: for the ImageTK module'
              'sane: for the Sane module'
              'python-pyqt4: for the ImageQt module')
  cd "$srcdir/$_appname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  pushd Sane
    python3 setup.py install --root="$pkgdir/" --optimize=0
  popd
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py3basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py3basever/" libImaging/*.h

  # clean up bins
  cd "$pkgdir/usr/bin"
  for f in *.py; do
    mv "$f" "${f%.py}"
  done
}

package_python2-pillow() {
  pkgdesc="Python Imaging Library (PIL) fork. Python2 version."
  depends=('python2' 'lcms2' 'libwebp' 'openjpeg2' 'python2-olefile')
  optdepends=('tk: for the ImageTK module'
              'sane: for the Sane module'
              'python2-pyqt5: for the ImageQt module'
              'python2-pyqt4: for the ImageQt module'
              'python2-pyside: for the ImageQt module')
  provides=('python-imaging' 'python2-imaging')
  conflicts=('python-imaging' 'python2-imaging')
  cd "$srcdir/${_appname}2-$pkgver"
  sed -i 's|python$|python2|' PIL/OleFileIO.py
  python2 setup.py install --root="$pkgdir/" --optimize=0
  pushd Sane
    python2 setup.py install --root="$pkgdir/" --optimize=0
  popd

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -dm755 "$pkgdir/usr/include/python$_py2basever/"
  install -m644 -t "$pkgdir/usr/include/python$_py2basever/" libImaging/*.h

  # clean up bins
  cd "$pkgdir/usr/bin"
  for f in *.py; do
    mv "$f" "${f%.py}2"
  done
}

