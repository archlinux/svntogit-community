# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: furester <xfurester@hotmail.com> 
# Contributor: Firmicus <firmicus gmx net> 
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=texmaker
pkgver=2.3
pkgrel=1
pkgdesc="Free cross-platform latex editor"
arch=('i686' 'x86_64')
url="http://www.xm1math.net/texmaker/index.html"
license=('GPL')
depends=('poppler-qt' 'ghostscript' 'openssl')
source=("http://www.xm1math.net/texmaker/${pkgname}-${pkgver}.tar.bz2"
        'texmaker.desktop')
md5sums=('a9cb4725243333aedae9254ef7439a91'
         '48ca54ffd5eb98a83cbf3626205b8639')

build() {
	cd ${pkgname}-${pkgver}

	export INSTALL_ROOT=${pkgdir}

	qmake -unix PREFIX=//usr texmaker.pro

	make
}

package() {
	cd ${pkgname}-${pkgver}

	export INSTALL_PATH=${pkgdir}
	make install
	
	rm -f ${pkgdir}/usr/share/texmaker/{*.desktop,COPYING}
	install -D -m644 images/appicon.png \
		${pkgdir}/usr/share/pixmaps/texmaker.png
	install -D -m644 ${srcdir}/${pkgname}.desktop \
		${pkgdir}/usr/share/applications/${pkgname}.desktop
}
