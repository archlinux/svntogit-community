# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Tomas Schertel<tschertel@gmail.com>

pkgbase=python-logbook
pkgname=(python-logbook python2-logbook)
_pypiname=Logbook
pkgver=1.0.0
pkgrel=2
pkgdesc="Logging sytem for Python that replaces the standard libraryâ€™s logging module"
arch=('i686' 'x86_64')
url="http://packages.python.org/Logbook/index.html"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'git'
             'python-six' 'python2-six')
checkdepends=('redis' 'python-pytest' 'python2-pytest' 'python-sqlalchemy' 'python2-sqlalchemy'
              'python-mock' 'python2-mock' 'python-redis' 'python2-redis' 'python-pyzmq'
              'python2-pyzmq' 'python-execnet' 'python2-execnet' 'python-jinja' 'python2-jinja')
source=("git+https://github.com/mitsuhiko/logbook.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a logbook{,-py2}
}

build() {
  cd "$srcdir/logbook"
  cython logbook/_speedups.pyx
  python setup.py build

  cd "$srcdir/logbook-py2"
  cython2 logbook/_speedups.pyx
  python2 setup.py build
}

check() {
  rm -rf redis_tmp
  mkdir redis_tmp
  echo "dir $srcdir/redis_tmp" > redis.conf
  redis-server redis.conf &

  cd "$srcdir/logbook"
  LC_CTYPE=en_US.UTF-8 py.test tests

  cd "$srcdir/logbook-py2"
  LC_CTYPE=en_US.UTF-8 py.test2 tests

  redis-cli shutdown
}

package_python-logbook() {
  depends=('python-six')

  cd "${srcdir}/logbook"
  python setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-logbook() {
  depends=('python2-six')

  cd "${srcdir}/logbook-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
