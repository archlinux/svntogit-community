# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=0.2.4
pkgrel=2
pkgdesc='The Pantheon File Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-files'
license=('GPL3')
depends=('atk' 'cairo' 'dbus-glib' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2'
         'glibc' 'gtk3' 'libgee' 'libnotify' 'pango' 'plank' 'sqlite'
         'zeitgeist'
         'libgranite.so')
makedepends=('cmake' 'gnome-common' 'vala')
optdepends=('contractor: Various context menu entries'
            'tumbler: Thumbnails generation')
source=("https://launchpad.net/pantheon-files/${pkgver%.*}.x/${pkgver}/+download/pantheon-files-${pkgver}.tar.xz")
sha256sums=('7eaf1ecd076d46bc2e43373982dd02b62663c2d2f1d4430ff771314cf4366b81')

prepare() {
  cd pantheon-files-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-files-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DCMAKE_SKIP_RPATH='TRUE' \
    -DGSETTINGS_COMPILE='FALSE'
  make
}

package() {
  cd pantheon-files-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
