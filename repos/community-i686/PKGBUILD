# $Id$ 
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-sqlalchemy
pkgname=('python-sqlalchemy' 'python2-sqlalchemy')
pkgver=1.0.10
pkgrel=1
arch=('i686' 'x86_64') # python2 package contain .so
url="http://www.sqlalchemy.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("https://pypi.python.org/packages/source/S/SQLAlchemy/SQLAlchemy-$pkgver.tar.gz"{,.asc})
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')
md5sums=('84f01631cc4813a0298bf68ce27d9c5d'
         'SKIP')

prepare() {
  cp -a SQLAlchemy-$pkgver SQLAlchemy2-$pkgver
}

build() {
  cd SQLAlchemy-$pkgver
  python setup.py build

  cd ../SQLAlchemy2-$pkgver
  python2 setup.py build
}

check() {
  cd SQLAlchemy-${pkgver}
  PYTHONPATH="$(pwd)/build/lib.linux-$CARCH-3.5:$PYTHONPATH" py.test
 
  cd ../SQLAlchemy2-$pkgver  
  PYTHONPATH="$(pwd)/build/lib.linux-$CARCH-2.7:$PYTHONPATH" py.test2
}

package_python-sqlalchemy() {
  pkgdesc='Python SQL toolkit and Object Relational Mapper'
  depends=('python')
  optdepends=('python-psycopg2: connect to PostgreSQL database')

  cd SQLAlchemy-${pkgver}
  python setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-sqlalchemy() {
  pkgdesc='Python 2 SQL toolkit and Object Relational Mapper'
  depends=('python2')
  optdepends=('python2-psycopg2: connect to PostgreSQL database')

  cd SQLAlchemy2-$pkgver
  python2 setup.py install --root="$pkgdir"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 ft=sh et:
