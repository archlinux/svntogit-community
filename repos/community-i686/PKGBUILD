# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Pedro Martinez-Julia (pedromj@um.es)
# Contributor: Matt Monaco <net 0x01b dgbaley27>

pkgname=openvswitch
pkgver=2.1.2
pkgrel=2
pkgdesc="Production Quality, Multilayer Open Virtual Switch"
url="http://openvswitch.org"
license=('APACHE')
arch=(x86_64 i686)
install=openvswitch.install
source=("http://openvswitch.org/releases/openvswitch-$pkgver.tar.gz"
	openvswitch.tmpfiles
	ovsdb-server.service
	ovs-vswitchd.service)
depends=('openssl')
makedepends=('python2')
optdepends=('python2')
md5sums=('430ff6d1e1fc81e9c5ebc2e641cfc953'
         '0534c19ed27d2ff8c6b32d87c07bc76f'
         '69b29d231bcc3002528fd29f1f78a37f'
         'c5338742b6667f7be611176afe209230')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./boot.sh
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--with-rundir=/run/openvswitch \
		--sbindir=/usr/bin \
		PYTHON=/usr/bin/python2
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm0644 $srcdir/openvswitch.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/openvswitch.conf"
	install -Dm0644 $srcdir/ovsdb-server.service "$pkgdir/usr/lib/systemd/system/ovsdb-server.service"
	install -Dm0644 $srcdir/ovs-vswitchd.service "$pkgdir/usr/lib/systemd/system/ovs-vswitchd.service"
	install -dm0755 "$pkgdir/etc/openvswitch"
	rm -rf $pkgdir/run
}
