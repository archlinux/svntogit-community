# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: TingPing <tingping@tingping.se>

pkgname=hexchat
pkgver=2.12.4
pkgrel=3
pkgdesc='A popular and easy to use graphical IRC (chat) client'
arch=('i686' 'x86_64')
url='https://hexchat.github.io/'
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'libcanberra' 'libnotify' 'libproxy' 'openssl' 'pango' 'pciutils')
makedepends=('intltool' 'iso-codes' 'lua' 'perl' 'python' 'autoconf-archive')
optdepends=('enchant: Spell check'
            'iso-codes: Display language names instead of codes'
            'lua: Lua plugin'
            'perl: Perl plugin'
            'python: Python plugin')
source=("https://dl.hexchat.net/hexchat/hexchat-${pkgver}.tar.xz"{,.asc})
sha256sums=('fa35913158bbc7d0d99de79371b6df3e8d21802f1d2c7c92f0e5db694acf2c3a'
            'SKIP')
validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis

build() {
  cd hexchat-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-python='python3' \
    --enable-textfe
  make
}

package() {
  cd hexchat-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
