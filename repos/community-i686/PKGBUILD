# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=lxtask
pkgver=0.1.4
pkgrel=5
pkgdesc='Task manager of the LXDE Desktop'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.gz
        fix-integer-overflow.patch
        fix-sort-by-user.patch
        fix-cpu-count.patch)
md5sums=('c4ab47d03d41a88696d64d3f1e78cf7f'
         '0d4d325bd20dc7dcf2489bf78f18efb5'
         'ecb4b04fac25c7533b8760696b742330'
         'd18064be20c653274e31a73426e06abb')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-integer-overflow.patch
  patch -Np1 -i ../fix-sort-by-user.patch
  patch -Np1 -i ../fix-cpu-count.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
