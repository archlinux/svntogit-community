# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=libnsutils
pkgver=0.0.2
_netsurf=3.4
pkgrel=1
pkgdesc='NetSurf Utilities'
arch=('x86_64' 'i686')
url='http://source.netsurf-browser.org/libnsutils.git/'
license=('MIT')
makedepends=('netsurf-buildsystem')
depends=('glibc')
source=("http://download.netsurf-browser.org/netsurf/releases/source-full/netsurf-all-$_netsurf.tar.gz")
sha256sums=('c62cbecfb19b78b27b9065858484696c179ff17f828b848e28d6617fcdaafdd6')

build() {
  make -C "netsurf-all-$_netsurf/$pkgname" PREFIX=/usr INCLUDEDIR=include \
    LIBDIR=lib COMPONENT_TYPE="lib-shared"
}

package() {
  cd "netsurf-all-$_netsurf/$pkgname"

  make PREFIX=/usr INCLUDEDIR=include LIBDIR=lib COMPONENT_TYPE="lib-shared" \
    DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libnsutils.git
# vim:set ts=2 sw=2 et:
