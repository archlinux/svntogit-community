# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ilya Medvedev <medved55rus [at] gmail [dot] com>

pkgname=modem-manager-gui
pkgver=0.0.17.1
pkgrel=2
pkgdesc="Frontend for ModemManager daemon able to control specific modem functions"
arch=('i686' 'x86_64')
url="http://linuxonly.ru/cms/page.php?7"
license=('GPL3')
depends=('gdbm' 'gtk3' 'libnotify' 'modemmanager')
makedepends=('po4a' 'itstool')
optdepends=('networkmanager: monitor network traffic')
options=(!emptydirs)
source=("http://download.tuxfamily.org/gsf/source/$pkgname-$pkgver.tar.gz"
        modem-manager-gui-0.0.17.1-fix-libebook-api-break-v2.patch)
md5sums=('6d07afae90bcdb1fb1e54489a3fa71aa'
         'cc9287d50f291e6fc4506014db0db35a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Compatibility patch with libebook 3.16
  # http://linuxonly.ru/cms/e107_plugins/bug_tracker/bugs.php?0.item.3.31.0
  patch -Np1 -i ../modem-manager-gui-0.0.17.1-fix-libebook-api-break-v2.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}
 
package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
