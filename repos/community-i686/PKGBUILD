# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Matthias Bauch <matthias.bauch@gmail.com>
# Contributor: Laszlo Papp <djszapi2 at gmail com>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

pkgname=openocd
pkgver=0.8.0_rc1
pkgrel=4
pkgdesc='Debugging, in-system programming and boundary-scan testing for embedded target devices'
arch=('i686' 'x86_64')
url='http://openocd.berlios.de'
license=('GPL')
depends=('libftdi' 'libftdi-compat' 'libusb' 'libusb-compat')
options=(!strip)
install=openocd.install
_features=(amtjtagaccel arm-jtag-ew at91rm9200 buspirate ep93xx ftdi gw16012 jlink oocd_trace opendous osbdm parport presto_libftdi remote-bitbang rlink stlink ti-icdi ulink usbprog vsllink)
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-${pkgver/_/-}.tar.bz2)
md5sums=('7cbf6b556f020298eab80df94b73d52c')

build() {
  cd $pkgname-${pkgver/_/-}
  export LIBFTDI_CFLAGS="-I/usr/include/libftdi1"
  export LIBFTDI_LIBS="-lftdi1"
  ./configure --prefix=/usr ${_features[@]/#/--enable-} --disable-werror
  make LIBS="-ldl -lftdi1"
}

package() {
  cd $pkgname-${pkgver/_/-}
  make DESTDIR="$pkgdir" install
}
