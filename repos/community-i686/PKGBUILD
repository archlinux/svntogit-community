# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab-workhorse
pkgver=0.7.2
pkgrel=1
pkgdesc="HTTP server to unload Git HTTP traffic from GitLab Rails app (Unicorn)"
arch=('i686' 'x86_64')
url="https://gitlab.com/gitlab-org/gitlab-workhorse"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitlab-workhorse/repository/archive.tar.gz?ref=${pkgver}"
        "gitlab-workhorse.service")
sha256sums=('d0572a901568ba5cf74776dc5cbd0ee140fdacd62c7140c9f5f7cba0dcc456e2'
            '95fd491795ed837012e71d6c8bececb82b6790a94b96615d4ffb0b018b5b7ea4')

build() {
  cd "${pkgname}-${pkgver}-"*

  make
}

package() {
  cd "${pkgname}-${pkgver}-"*

  install -Dm755 "gitlab-workhorse" "${pkgdir}/usr/bin/gitlab-workhorse"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-workhorse.service" "${pkgdir}/usr/lib/systemd/system/gitlab-workhorse.service"
}

# vim:set ts=2 sw=2 et:
