# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
pkgver=1.5.5
pkgrel=1
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('i686' 'x86_64')
url="http://www.poedit.net/"
license=('custom')
depends=('wxgtk2.9' 'gtkspell' 'db>=5.1' 'hicolor-icon-theme' 'gettext')
makedepends=('pkgconfig' 'boost')
install=poedit.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('da86cb73cee9d006a42f9574811d13ab')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    export WX_CONFIG_PATH=/usr/bin/wx-config-2.9
    ./configure --prefix=/usr
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
