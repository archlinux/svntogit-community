# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libcss
pkgver=0.7.0
pkgrel=1
pkgdesc='CSS parser and selection engine'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libcss/' # no https available
license=('MIT')
depends=('libparserutils>=0.2.3' 'libwapcaplet>=0.4.0')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/libcss-$pkgver-src.tar.gz")
sha256sums=('dfb2a3d4967339c35b45b04fb7a95c3b4782302adcbeed2be2f1c88de0d0b69a')

build() {
  export CFLAGS="$CFLAGS -Wno-error=unused-but-set-variable -w" 
  sed 's^_BSD_SOURCE^_DEFAULT_SOURCE^' -i "$pkgname-$pkgver/Makefile"
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared" \
    install
  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
