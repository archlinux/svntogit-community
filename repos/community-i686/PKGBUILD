# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=pypy
pkgver=2.3.1
pkgrel=4
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
provides=('python2')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'tk' 'sqlite' 'mercurial')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
source=("hg+http://bitbucket.org/pypy/pypy#revision=3cf384e")
md5sums=('SKIP')

build() {
  cd pypy/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone
}

package() {
  cd pypy

  install -Dm755 pypy/goal/pypy-c "${pkgdir}"/opt/pypy/bin/pypy-c
  install -Dm755 pypy/goal/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so
  cp -r include lib_pypy site-packages "${pkgdir}"/opt/pypy/

  mkdir "${pkgdir}"/opt/pypy/lib-python/
  cp -r lib-python/2.7 "${pkgdir}"/opt/pypy/lib-python/

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy-c "${pkgdir}"/usr/bin/pypy

  install -Dm644 LICENSE "${pkgdir}"/opt/pypy/LICENSE
  install -Dm644 README.rst "${pkgdir}"/opt/pypy/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE

  # Compile binary modules
  for module in _curses syslog gdbm sqlite3 _tkinter; do
    LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH} "${pkgdir}"/opt/pypy/bin/pypy-c -c "import ${module}"
  done
}
# vim: ts=2 sw=2 et:
