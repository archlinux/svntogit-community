# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=dante
pkgver=1.3.2
pkgrel=5
pkgdesc="SOCKS v4 and v5 compatible proxy server and client"
url="http://www.inet.no/dante"
arch=(i686 x86_64)
license=('custom')
depends=('pam' 'krb5')
backup=('etc/socks.conf'
	'etc/sockd.conf')
options=('!libtool')
source=(ftp://ftp.inet.no/pub/socks/${pkgname}-${pkgver}.tar.gz
	sockd.service)
md5sums=('250c6456cd3fefa17f07fa80c9ccf6bd'
         '9bb18a0165764092bd01da0ff6303fbf')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc \
	--sbindir=/usr/bin \
	--disable-libwrap
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  mkdir -p ${pkgdir}/etc/conf.d
  cp example/{socks,sockd}.conf ${pkgdir}/etc
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  echo 'SOCKD_OPTS="-D"' >${pkgdir}/etc/conf.d/sockd.conf
  install -Dm0644 $srcdir/sockd.service $pkgdir/usr/lib/systemd/system/sockd.service
}
