# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=openbox
pkgver=3.5.2
pkgrel=4
pkgdesc='Highly configurable and lightweight X11 window manager'
arch=('i686' 'x86_64')
url='http://openbox.org'
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
         'libxcursor' 'pango' 'imlib2' 'librsvg')
optdepends=('gnome-flashback-session: for the GNOME/Openbox xsession'
            'kdebase-workspace: for the KDE/Openbox xsession'
            'python2-xdg: for the openbox-xdg-autostart script')
groups=('lxde')
options=('!libtool')
backup=('etc/xdg/openbox/menu.xml' 'etc/xdg/openbox/rc.xml'
        'etc/xdg/openbox/autostart' 'etc/xdg/openbox/environment')
source=(http://www.icculus.org/openbox/releases/$pkgname-$pkgver.tar.gz
        openbox-3.5.0-title-matching.patch
        openbox-3.5.0-which-2.20.patch
        openbox-3.5.0-gnome-session.patch)
md5sums=('93df606606053b7e8578a5c116afb8ec'
         '0a11d7149da210a31ef88f8a9c717711'
         '7ca3b5244bb092d46f5bcf1e2bdf4a18'
         '47fb1a16af05b6aba3b07051e6027f93')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i "$srcdir"/openbox-3.5.0-title-matching.patch  # OB#5277
  patch -Np1 -i "$srcdir"/openbox-3.5.0-which-2.20.patch # FS#11455

  # Fix GNOME/Openbox xsession with GNOME 3.8
  patch -Np1 -i "$srcdir"/openbox-3.5.0-gnome-session.patch

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    data/autostart/openbox-xdg-autostart
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --with-x \
    --enable-startup-notification \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/openbox
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/bin/gdm-control \
    "$pkgdir"/usr/share/gnome-session/sessions/openbox-gnome-fallback.session

  sed -i 's:startkde:/usr/bin/\0:' \
    "$pkgdir"/usr/share/xsessions/openbox-kde.desktop
  sed -i 's:=gnome-session:=/usr/bin/gnome-session:' \
    "$pkgdir"/usr/share/xsessions/openbox-gnome.desktop
}
