# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: fauno <fauno@parabola.nu>
pkgname=miniupnpd
pkgver=1.9.20150307
pkgrel=1
pkgdesc="Lightweight UPnP IGD daemon"
arch=('i686' 'x86_64')
url="http://miniupnp.free.fr"
license=('BSD')
backup=(etc/miniupnpd/miniupnpd.conf)
depends=('libnfnetlink' 'iptables' 'net-tools' 'util-linux')
makedepends=('lsb-release')
source=("http://miniupnp.free.fr/files/$pkgname-$pkgver.tar.gz"
        miniupnpd-1.8.20140401-foreground.patch
        miniupnpd.systemd)
sha256sums=('7d8c9b1f7ed73e288b4e7e52e0af67de73ba07994a6984008a1a688568153409'
            '79350f2dbb695b58744aa9036d34a7dfdcfc97cf0f5f3419b86bd9ca58482f8e'
            'baac6223eb35729ea301bd658dc2df6aad5396c7c0b1ec15c8aac1803ef37f6a')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p0 -i "$srcdir"/miniupnpd-1.8.20140401-foreground.patch
}

build() {
  cd "$pkgname-$pkgver"

  CONFIG_OPTIONS="--strict --ipv6 --leasefile" make -f Makefile.linux config.h
  make -f Makefile.linux
}

package() {
  cd "$pkgname-$pkgver"
  mkdir -p "$pkgdir/usr/share/man/man8"

  make PREFIX="$pkgdir/" SBININSTALLDIR="$pkgdir/usr/bin" -f Makefile.linux install

  rm -r "${pkgdir}/etc/init.d"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/miniupnpd.systemd" "${pkgdir}/usr/lib/systemd/system/miniupnpd.service"

  sed -i 's:/s\?bin/iptables:/usr/bin/iptables:
          s:eth0:"`cat /etc/miniupnpd/miniupnpd.conf | '"awk -F= '/^ext_ifname/ { print \$2 }'"'`":' "${pkgdir}"/etc/miniupnpd/*.sh
  sed -i -e "s/^uuid=[-0-9a-f]*/uuid=00000000-0000-0000-0000-000000000000/
             s/make genuuid/uuidgen/" "${pkgdir}/etc/miniupnpd/miniupnpd.conf"
}
