# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hoogle
pkgver=5.0.9
pkgrel=15
pkgdesc="Haskell API Search"
url="http://www.haskell.org/hoogle/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib')
makedepends=("ghc=8.0.1" "haskell-quickcheck" "haskell-aeson" "haskell-cmdargs" "haskell-conduit"
             "haskell-conduit-extra" "haskell-connection" "haskell-extra" "haskell-src-exts"
             "haskell-http-conduit" "haskell-http-types" "haskell-js-flot" "haskell-js-jquery"
             "haskell-mmap" "haskell-network" "haskell-network-uri" "haskell-old-locale"
             "haskell-process-extras" "haskell-resourcet" "haskell-tar" "haskell-text"
             "haskell-uniplate" "haskell-utf8-string" "haskell-vector" "haskell-wai"
             "haskell-wai-logger" "haskell-warp" "haskell-warp-tls" "haskell-zlib")
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('93f584c5f7fc6a57ee50803ae8df5e6c41051a3177044b273cb7fbcd39d11874')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        -fnetwork-uri
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -r "$pkgdir"/usr/{lib,share/doc}
}
