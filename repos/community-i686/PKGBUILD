# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Mark Coolen <mark.coolen@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=squid
pkgver=3.4.6
pkgrel=2
pkgdesc='Full-featured Web proxy cache server'
arch=('x86_64' 'i686')
url='http://www.squid-cache.org'
depends=('openssl' 'pam' 'cron' 'perl' 'libltdl' 'libpcap')
makedepends=('libcap' 'krb5')
license=('GPL')
options=('emptydirs')
backup=('etc/squid/squid.conf'
        'etc/squid/mime.conf'
        'etc/squid/errorpage.css'
        'etc/squid/cachemgr.conf'
        'etc/squid/msntauth.conf')
install=$pkgname.install
source=("http://www.squid-cache.org/Versions/v3/3.4/$pkgname-$pkgver.tar.bz2"
        'squid.pam'
        'squid.cron'
        'squid.service'
        'squid.tmpfiles'
        'squid-r13407.patch')
md5sums=('d3ca4ce0a039bbba8258d6b67d6afaa1'
         '270977cdd9b47ef44c0c427ab9034777'
         'a71425c4951f2e5b640d19e6a5048531'
         'ceeb57c69ebb165676219222f109a24e'
         'd243da117c1aee03c0cc6052f023a380'
         '06e107bc303aca86550a9041fb0ab7a8')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 <$srcdir/squid-r13407.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --datadir=/usr/share/squid \
    --sysconfdir=/etc/squid \
    --libexecdir=/usr/lib/squid \
    --localstatedir=/var \
    --with-logdir=/var/log/squid \
    --with-pidfile=/run/squid.pid \
    --enable-auth \
    --enable-auth-basic \
    --enable-auth-ntlm \
    --enable-auth-digest \
    --enable-auth-negotiate \
    --enable-removal-policies="lru,heap" \
    --enable-storeio="aufs,ufs,diskd" \
    --enable-delay-pools \
    --enable-arp-acl \
    --enable-ssl \
    --enable-snmp \
    --enable-linux-netfilter \
    --enable-ident-lookups \
    --enable-useragent-log \
    --enable-cache-digests \
    --enable-referer-log \
    --enable-arp-acl \
    --enable-htcp \
    --enable-carp \
    --enable-epoll \
    --with-large-files \
    --enable-arp-acl \
    --with-default-user=proxy \
    --enable-async-io \
    --enable-truncate \
    --enable-icap-client \
    --enable-ssl-crtd \
    --disable-arch-native \
    --disable-strict-error-checking
  make
}

package() {
  cd "$srcdir"

  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/squid.cron" "$pkgdir/etc/cron.weekly/squid"
  install -Dm644 "$srcdir/squid.pam" "$pkgdir/etc/pam.d/squid"
  install -Dm644 "$srcdir/squid.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/squid.conf"
  install -Dm644 "$srcdir/squid.service" \
    "$pkgdir/usr/lib/systemd/system/squid.service"
  rm -rf "$pkgdir/run" "$pkgdir/var/run"
}

# vim: ts=2 sw=2 et ft=sh
