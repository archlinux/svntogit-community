# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=onboard
pkgver=0.99.0
pkgrel=1
pkgdesc="On-screen keyboard useful on tablet PCs or for mobility impaired users"
arch=('i686' 'x86_64')
url="https://launchpad.net/onboard"
license=('GPL')
depends=('libxkbfile' 'libcanberra' 'hunspell' 'python-dbus' 'python-gobject' 'gtk3' 'librsvg' 'gsettings-desktop-schemas' 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('python-distutils-extra')
optdepends=('mousetweaks: hover click with mouse')
options=('!emptydirs')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('456d1e32a6f7143200993b0819386a0d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python3 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python3 setup.py install --root=$pkgdir/ --optimize=1
}
