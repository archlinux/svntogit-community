# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgbase=python-xapian
pkgname=('python2-xapian')
_realname=xapian-bindings
pkgver=1.2.19
epoch=1
pkgrel=1
pkgdesc="Python bindings for Xapian"
arch=('i686' 'x86_64')
url="http://xapian.org/docs/bindings/python/"
license=('GPL')
makedepends=('python' 'python2' "xapian-core=$epoch:$pkgver")
source=("http://oligarchy.co.uk/xapian/${pkgver}/${_realname}-${pkgver}.tar.xz")
sha512sums=('ac72ec35ae3b767e04e848a9782b41d74e99b6b1f795d5096373911ff549b3bf2e602a91ed0e5f7dce2e43e1dd308af710719e5a80c6a179393aac4af316de30')

build() {
  cd "$srcdir/${_realname}-$pkgver"
  ./configure XAPIAN_CONFIG=/usr/bin/xapian-config \
    --prefix=/usr --with-python \
    PYTHON=/usr/bin/python2
  make
}

package_python2-xapian() {
  depends=('python2' "xapian-core=$epoch:$pkgver")

  cd ${_realname}-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
