# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=20120119
_pkgver=${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}
_pkgbzr=3256
pkgrel=2
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
url="http://iut-tice.ujf-grenoble.fr/kicad/"
license=('GPL')
depends=('mesa' 'wxgtk')
makedepends=('boost' 'cmake' 'zlib')
optdepends=('kicad-docs-bzr: for documentation' 
            'kicad-library-bzr: for footprints')
install=kicad.install
source=(http://iut-tice.ujf-grenoble.fr/cao/${pkgname}_sources-$_pkgver-BZR$_pkgbzr-stable.zip
        kicad-boost-polygon-declare-gtlsort-earlier.patch)
md5sums=('d65574c42efd72638aed80adef367c3b'
         'a2c39704238946e74a5ed0c38326345f')

build() {
  cd "$srcdir/KiCad_sources"

  patch -p0 < "$srcdir/kicad-boost-polygon-declare-gtlsort-earlier.patch"

  # build and install kicad
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON    \
              -DCMAKE_BUILD_TYPE=Release   \
              -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/KiCad_sources/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"
}
