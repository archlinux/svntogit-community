# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgbase=kid3
pkgname=('kid3' 'kid3-qt')
pkgver=3.2.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://kid3.sourceforge.net/"
license=('GPL')
makedepends=('xorg-server-xvfb' 'automoc4' 'cmake' 'docbook-xml' 'docbook-xsl' 'phonon-qt4'
             'chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'kdebase-runtime')
install=$pkgbase.install
changelog=$pkgbase.changelog
source=(http://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz)
sha256sums=('0197ee6484e9b80ffae39867833fce04ac23ec143d4ff6f1a074af0efba1d503')

build() {
  cd "${srcdir}"
  [[ ! -d build ]] && mkdir build
  mkdir build-qt

#kid3
  cd build

  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_APPS="CLI;KDE"

#https://sourceforge.net/p/kid3/bugs/108/
  xvfb-run -a make

#kid3-qt
  cd "$srcdir"/build-qt

  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_APPS="CLI;Qt"

#https://sourceforge.net/p/kid3/bugs/108/
  xvfb-run -a make
}

package_kid3() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, KDE version"
depends=('chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'kdebase-runtime')
conflicts=('kid3-qt')

  cd "$srcdir"/build

  make DESTDIR="${pkgdir}" install
}

package_kid3-qt() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, QT version"
depends=('chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'qt4' 'phonon-qt4')
conflicts=('kid3')
provides=('kid3')

  cd "$srcdir"/build-qt

  make DESTDIR="${pkgdir}" install
}
