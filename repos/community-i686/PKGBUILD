# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>

pkgname=perl-inline-java
pkgver=0.58
pkgrel=2
pkgdesc="The Inline::Java module allows you to put Java source code directly inline in a Perl script or module."
arch=('i686' 'x86_64')
depends=('perl' 'perl-inline' 'java-runtime')
makedepends=('java-environment')
license=("GPL" "PerlArtistic")
url="http://search.cpan.org/dist/Inline-Java"
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/E/ET/ETJ/Inline-Java-$pkgver.tar.gz)
md5sums=('6f3b7d1fe8c7d263f44c52b94205023a')

build() {
  cd $srcdir/Inline-Java-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor J2SDK=/usr/lib/jvm/java-7-openjdk
  make java
  make
}
package() {
  cd $srcdir/Inline-Java-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
