# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
pkgver=0.6.1
pkgrel=2
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('gconf' 'libgee' 'libgphoto2' 'libunique' 'libwebkit' 'libgexiv2' 'libraw' 'librsvg')
makedepends=('intltool' 'vala')
install=shotwell.install
source=(http://yorba.org/download/shotwell/0.6/${pkgname}-${pkgver}.tar.bz2)
md5sums=('440436e8f1479a5da2599065705b0ca4')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr \
	--with-gconf-schema-file-dir=/usr/share/gconf/schemas \
	--disable-schemas-install \
	--disable-desktop-update \
	--disable-icon-update
     make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
