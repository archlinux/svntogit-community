# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org> 
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: SÃ¸ren Holm <sgh@sgh.dk> 

pkgname=vym
pkgver=2.5.0
pkgrel=4
pkgdesc="A mindmapping tool"
arch=('i686' 'x86_64')
url="http://www.insilmaril.de/vym/"
license=('GPL')
depends=('qt5-svg' 'desktop-file-utils')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
        $pkgname.desktop)
sha256sums=('8412c3ce8de3330fb61e7c2c75dfcde0cc955c0804d5a5f384f726eb32cc23dd'
            'e299c69c213e7aac3f5b5d0ab088132b4ec7cb63a391f272e75ed64f049d541b')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  qmake PREFIX=/usr/share
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

# install files
  make INSTALL_ROOT="${pkgdir}" install

# fix executable path
  mv "${pkgdir}"/usr/share/bin "${pkgdir}"/usr/bin
  
# install documentation
  install -Dm644 "${srcdir}"/$pkgname-$pkgver/doc/$pkgname.1.gz \
    "${pkgdir}"/usr/share/man/man1/$pkgname.1.gz
  install -Dm644 "${srcdir}"/$pkgname-$pkgver/doc/$pkgname.pdf \
    "${pkgdir}"/usr/share/doc/$pkgname/$pkgname.pdf  
  rm -rf "${pkgdir}"/usr/share/doc/packages

# .desktop and icon file
   install -Dm644 "${srcdir}"/$pkgname.desktop \
     "${pkgdir}"/usr/share/applications/$pkgname.desktop
   install -Dm644 "${pkgdir}"/usr/share/vym/icons/$pkgname.png \
     "${pkgdir}"/usr/share/pixmaps/$pkgname.png
}
