# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Arthur Țițeică arthur.titeica/gmail/com
# Contributor: Hao Zhang <theivorytower [at] gmail [dot] com>

pkgname=opendmarc
pkgver=1.3.2
pkgrel=2
pkgdesc="Free open source software implementation of the DMARC specification"
arch=('i686' 'x86_64')
url="http://www.trusteddomain.org/opendmarc/"
license=('custom')
depends=('smtp-server' 'libspf2' 'libbsd' 'libidn')
makedepends=('libmilter')
optdepends=('opendbx: acts as a middleware layer between OpenDMARC and a SQL backend of choice'
            'python: run opendmarc scripts at /usr/share/doc/opendmarc'
            'perl: run opendmarc scripts at /usr/share/doc/opendmarc'
            'perl-switch: generate DMARC reports'
            'perl-dbd-mysql: generate DMARC reports'
            'perl-libwww: generate DMARC reports')
install=${pkgname}.install
backup=('etc/opendmarc/opendmarc.conf')
# unfortunately the gpg key used for signing (2D55C45B89CFFD42) is not public
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}.tar.gz" #{,.asc}
        'opendmarc.service'
        'opendmarc.conf')
sha512sums=('6045fb7d2be8f0ffdeca07324857d92908a41c6792749017c2fcc1058f05f55317b1919c67c780827dd7094ec8fff2e1fa4aeb5bab7ff7461537957af2652748'
            '738de0cd286dd30713f32034f9ecf9009b6f64038c573c9f8aedaf10df8293bb9eec9d19492a03a2ebf2d2960289bdf48be9b1eb25395dbe9a490f7e3b25cb34'
            'a55540cca6f968072ce8ddb9e53ce226300f2bbf730e7affe775a3ea96fad2cf438c88bc9f96c0de352dfb217eb82e7b4fc1ab05666ac6fd063b434cd335bc54')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LDFLAGS="${LDFLAGS//,--as-needed}"
  ./configure --prefix=/usr \
              --bindir=/usr/bin \
              --sbindir=/usr/bin \
              --sysconfdir="/etc/${pkgname}" \
              --with-spf \
              --with-spf2-include=/usr/include/spf2 \
              --with-spf2-lib=/usr/lib/
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  # config
  install -D -m644 "${srcdir}/opendmarc.conf" "${pkgdir}/etc/${pkgname}/opendmarc.conf"
  # License
  install -D -m644 "${srcdir}/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
  # systemd service
  install -D -m644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
}
