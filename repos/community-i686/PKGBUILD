# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.1.2
pkgrel=1
pkgdesc="A general-purpose subtitle editor with ASS/SSA support"
arch=('i686' 'x86_64')
url="http://www.aegisub.org"
license=('GPL' 'BSD')
depends=('boost-libs' 'desktop-file-utils' 'ffms2' 'fftw' 'hunspell' 'lua51' 'wxgtk')
makedepends=('boost' 'intltool' 'mesa')
install="${pkgname}.install"
source=("http://ftp.aegisub.org/pub/archives/releases/source/${pkgname}-${pkgver}.tar.xz")
sha256sums=('36ebfd05cc642b050da1e91ce2d2082b317a37bdfab109e191f861170d7cff86')

build() {
  cd ${pkgname}-${pkgver}/${pkgname}

  ./configure --prefix='/usr' --without-{portaudio,openal,oss}
  make
}

package() {
  cd ${pkgname}-${pkgver}/${pkgname}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
