# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>
# Contributor: rabyte <gmail.com: rabyte>

pkgname=freebasic
pkgver=1.05.0
pkgrel=2
pkgdesc="A free BASIC compiler"
arch=('i686' 'x86_64')
url="http://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('ncurses')
makedepends=('libxpm' 'gpm' 'libffi' 'libxrandr' 'mesa')
source=("https://downloads.sourceforge.net/fbc/FreeBASIC-$pkgver-source.tar.xz")
source_i686=("http://downloads.sourceforge.net/fbc/FreeBASIC-${pkgver}-linux-x86.tar.xz")
source_x86_64=("http://downloads.sourceforge.net/fbc/FreeBASIC-${pkgver}-linux-x86_64.tar.xz")
md5sums=('a0b89405678c831af2d28e566758aeae')
md5sums_i686=('50aec8fcfa577dc7a76cffc6dfe50476')
md5sums_x86_64=('4bf93ad31b378df4897699a5a0744b11')

prepare() {
  [[ "$CARCH" = "x86_64" ]] && _arch=x86_64
  [[ "$CARCH" = "i686" ]] && _arch=x86

  mkdir -p bootstrap
  cd "${srcdir}/FreeBASIC-${pkgver}-linux-$_arch"
  ./install.sh -i "${srcdir}/bootstrap"
  # Hack!
  ln -s /usr/lib/libncursesw.so "${srcdir}/bootstrap/lib/libtinfo.so.5"
}

build() {
  export PATH="${srcdir}/bootstrap/bin:$PATH"
  export LD_LIBRARY_PATH="${srcdir}/bootstrap/lib"

  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make CFLAGS="$CFLAGS $(pkg-config --cflags libffi)"
}

package() {
  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make install prefix="${pkgdir}/usr"
}
