# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=pypy
pkgver=2.3.1
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('libffi')
provides=('python2')
options=(!buildflags)
makedepends=('python2' 'mercurial' 'pypy' 'tk' 'mercurial')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module'
            'tk: tk module')
license=('custom:MIT')
source=("hg+ssh://hg@bitbucket.org/pypy/pypy#revision=3cf384e")
md5sums=('SKIP')

build() {
  cd pypy/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone
}

package() {
  cd pypy/pypy/tool/release

  LD_LIBRARY_PATH="${srcdir}"/pypy/pypy/goal python2 package.py ../../../ pypy pypy-c "${srcdir}"/${pkgname}.tar.bz2

  install -Dm755 "${srcdir}"/pypy/pypy/goal/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy-c "${pkgdir}"/usr/bin/pypy

  install -Dm644 "${pkgdir}"/opt/pypy/LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE
}
# vim: ts=2 sw=2 et:
