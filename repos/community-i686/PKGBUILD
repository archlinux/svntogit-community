# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

_langs=(afr ara aze bel ben bul cat ces chi_sim chi_tra chr dan deu ell
eng enm epo epo_alt equ est eus fin fra frk frm glg grc heb hin hrv hun
ind isl ita ita_old jpn kan kor lav lit mal mkd mlt msa nld nor pol por
ron rus slk slv spa spa_old sqi srp swa swe tam tel tgl tha tur ukr vie)

pkgname=tesseract
pkgver=3.03rc1
_pkgver=3.03
pkgrel=2
pkgdesc="An OCR programm"
arch=(i686 x86_64)
url="http://code.google.com/p/tesseract-ocr"
license=("APACHE")
depends=(libpng libtiff libjpeg zlib giflib gcc-libs leptonica)
makedepends=(icu cairo pango)
install=tesseract.install
optdepends=('icu' 'cairo' 'pango'
	    $(for l in ${_langs[@]}; do echo tesseract-data-${l}; done))
source=("http://arch.p5n.pp.ru/~sergej/dl/2014/$pkgname-$pkgver.tar.gz"
	"https://tesseract-ocr.googlecode.com/files/tesseract-ocr-3.01.osd.tar.gz")
md5sums=('d69ceca9ae70e0b7020d0f92d60b8565'
         '683486e01f5b87c17f2f5815f770ccb3')

build() {
  cd $srcdir/$pkgname-${_pkgver}
  [ -f Makefile ] || ./configure --prefix=/usr
  (cd training && make clean)
  make clean
  make
  make training
}

package() {
  cd $srcdir/$pkgname-${_pkgver}
  make DESTDIR=$pkgdir install
  make DESTDIR=$pkgdir training-install
  mkdir -p $pkgdir/usr/share/tessdata
  install -Dm0644 $srcdir/tesseract-ocr/tessdata/osd.traineddata $pkgdir/usr/share/tessdata/osd.traineddata
}
