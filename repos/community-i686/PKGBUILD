# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: graysky
# Contributor: Tomasz Å»ok <tomasz dot zok at gmail dot com>

pkgname=pymol
pkgver=1.8.2.0
pkgrel=2
pkgdesc="Molecular visualization system on an Open Source foundation"
arch=('i686' 'x86_64')
url="http://pymol.org/"
license=('custom')
depends=('glew' 'glut' 'libgl' 'libxml2' 'python2-numpy' 'python2-pmw' 'tcsh')
makedepends=('desktop-file-utils' 'gendesk')
source=("http://downloads.sourceforge.net/project/pymol/pymol/1.8/pymol-v${pkgver}.tar.bz2"
        ${pkgname}.png::"https://c.fsdn.com/allura/p/pymol/icon"
        "${pkgname}-38899.patch"
        "apbstools_tcltk8.6.patch")
md5sums=('68dca95bc60f5979f7a0cb22839d95ae'
         '0e9df23150d39cbcb8f79c38ce2a52e5'
         '1f8152c4604ba2939b24e25a022937ca'
         '138550367b74fd62a7c7bc48d339eb2d')
changelog=ChangeLog

prepare() {
  # create desktop file
  gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" \
          --name="PyMOL Molecular Graphics System" \
          --categories="Science;Chemistry"

  # suppress non-zero exit code that breaks makepkg
  sed -i '/sys.exit/ s,2,0,' "${srcdir}/pymol/setup.py"
  # change python to python2
  find "${pkgname}" -name '*.py' -type f -exec \
       sed -i 's|env python|env python2|g' {} \;
  find "${pkgname}/test" -type f -exec \
       sed -i 's|bin/python|bin/python2|g' {} \;
  # fix FS#38899
  patch -p0 -i "${pkgname}-38899.patch"
  mv "${srcdir}/${pkgname}/modules/web" "${srcdir}/${pkgname}/modules/pymolweb"
  # fix FS#39526
  cd "${srcdir}/${pkgname}"
  patch -p0 -i "${srcdir}/apbstools_tcltk8.6.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  # required c++11
  export CPPFLAGS="$CPPFLAGS -std=c++11"
  python2 setup.py build
}

package() {
  cd "${srcdir}/${pkgname}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/pymol.desktop" "${pkgdir}/usr/share/applications/pymol.desktop"
  install -Dm644 "${srcdir}/pymol.png" "${pkgdir}/usr/share/pixmaps/pymol.png"
}

# vim:set ts=2 sw=2 et:
