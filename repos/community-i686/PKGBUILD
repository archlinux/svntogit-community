# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gitname=yara-python
pkgbase=python-yara
pkgname=('python-yara' 'python2-yara')
pkgver=3.5.0
pkgrel=1
pkgdesc='Tool aimed at helping malware researchers to identify and classify malware samples'
url='https://github.com/VirusTotal/yara-python'
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('git' 'yara' 'python-setuptools' 'python2-setuptools')
source=(${pkgbase}::git+https://github.com/VirusTotal/${_gitname})
sha512sums=('SKIP')

package_python-yara() {
  depends=('python' 'yara')
  cd ${pkgbase}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

package_python2-yara() {
  depends=('python2' 'yara')
  cd ${pkgbase}
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

# vim: ts=2 sw=2 et:
