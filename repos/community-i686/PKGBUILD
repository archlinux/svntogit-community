# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.2.2
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('deepin-tool-kit' 'libqtxdg' 'gtk2')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'qt5-xcb-private-headers')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        use-system-qt5-headers.patch)
sha512sums=('af665a4156b20a9686c18956305ffd8def8e7eadf8ba970415bd5c41e25d97f61d1408d5ec4cb28fff29af8a660d66907280b62db2caa47b9cdbf1984fb622d6'
            '29152204a36efc5fc2376b74be2ebe1d9cb253f9371ca8b0e9bf95c415c903f956d0d400eaffb3f708f81b19acd0a5383647af3a5a10cf6a61dfa586eb1bd57b')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../use-system-qt5-headers.patch
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
