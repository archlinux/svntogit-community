# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-panel
pkgver=0.10.0
pkgrel=2
pkgdesc="The LXQt desktop panel"
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=(
	"libxcomposite" "libdbusmenu-qt5" "lxmenu-data"
	"lxqt-common" "lxqt-globalkeys" "solid"
)
optdepends=(
	"alsa-lib: Alsa support in volume control plugin"
	"libpulse: PulseAudio support in volume control plugin"
	"libstatgrab: Network and CPU monitor plugin"
	"libsysstat: Network/CPU monitor plugin"
	"menu-cache: Improved performance for main menu plugin"
)
makedepends=("cmake" "qt5-tools" "alsa-lib" "libpulse" "libstatgrab" "libsysstat")
source=(
	"http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
	"http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"df83db7789daecd358f074db2100d7b251c00e48aa3aa62957d5a657a4309d5e"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373") 


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
