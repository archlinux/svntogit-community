# $Id$
# Maintainer: Lukas Fleischer <archlinux@cryptocrack.de>
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>
# Contributor: Ricardo Catalinas Jiménez <jimenezrick@gmail.com>

pkgbase=erlang
pkgname=('erlang' 'erlang-unixodbc')
pkgver=R16B02
pkgrel=2
arch=('x86_64' 'i686')
url='http://www.erlang.org/'
license=('custom')
makedepends=('perl' 'mesa' 'lksctp-tools' 'unixodbc' 'wxgtk' 'wxgtk2.9')
options=('staticlibs')
source=("http://www.erlang.org/download/otp_src_${pkgver/_/-}.tar.gz"
        "http://www.erlang.org/download/otp_doc_man_${pkgver/_/-}.tar.gz"
        'epmd.service'
        'epmd.socket'
        'epmd.conf')
sha256sums=('6ab8ad1df8185345554a4b80e10fd8be06c4f2b71b69dcfb8528352787b32f85'
            'b79ceb0383415088b5f040f2f3705632b425cb3f29c3955ef2219556abec5789'
            'b121ec9053fb37abca5f910a81c526f93ec30fe13b574a12209223b346886a9e'
            '998a759e4cea4527f9d9b241bf9f32527d7378d63ea40afa38443c6c3ceaea34'
            '78ce5e67b21758c767d727e56b20502f75dc4385ff9b6c6db312d8e8506f2df2')

build() {
  cd "otp_src_${pkgver/_1/}"

  ./configure --prefix=/usr --enable-smp-support --with-odbc
  make
}

package_erlang() {
  pkgdesc='General-purpose concurrent functional programming language developed by Ericsson'
  depends=('ncurses' 'glu' 'wxgtk' 'wxgtk2.9' 'openssl')
  optdepends=('erlang-unixodbc: database support'
              'java-environment: for Java support'
              'lksctp-tools: for SCTP support')
  provides=('erlang-nox')
  conflicts=('erlang-nox')

  cd "otp_src_${pkgver/_1/}"

  make DESTDIR="$pkgdir" install

  # Documentation
  install -d "$pkgdir/usr/share/doc/erlang"
  install -m0644 "$srcdir/otp_src_${pkgver/_1/}/README.md" \
    "$srcdir"/{README,COPYRIGHT} \
    "$pkgdir/usr/share/doc/erlang"

  # Compressed man pages
  for page in "$srcdir/man/man?/*"; do gzip $page; done
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang/"

  # License
  install -Dm0644 "$srcdir/otp_src_${pkgver/_1/}/EPLICENCE" \
    "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"

  # Move over files that will be packaged as erlang-unixodbc
  mkdir "$srcdir/unixodbc"
  mv "$pkgdir/usr/lib/erlang/lib/odbc"* "$srcdir/unixodbc/"
  mv "$pkgdir/usr/lib/erlang/man/man3/odbc.3.gz" "$srcdir"

  # epmd service, socket and conf
  cd "$srcdir"
  install -Dm644 epmd.service "$pkgdir/etc/systemd/system/epmd.service"
  install -Dm644 epmd.socket "$pkgdir/etc/systemd/system/epmd.socket"
  install -Dm644 epmd.conf "$pkgdir/etc/conf.d/epmd"
}

package_erlang-unixodbc() {
  pkgdesc='Unixodbc support for Erlang'
  depends=('unixodbc' 'erlang-nox')

  # Get the files that should be packaged as erlang-unixodbc
  mkdir -p "$pkgdir/usr/lib/erlang/"{lib,man/man3}
  mv "$srcdir/unixodbc/"* "$pkgdir/usr/lib/erlang/lib/"
  mv "$srcdir/odbc.3.gz" "$pkgdir/usr/lib/erlang/man/man3/"

  # License
  install -Dm0644 "$srcdir/otp_src_${pkgver/_1/}/EPLICENCE" \
    "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"
}

# vim:set ts=2 sw=2 et:
