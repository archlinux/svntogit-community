# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Yannick LM <LMyannicklm1337@gmail.com>

pkgname=tint2
pkgver=0.12.8
pkgrel=1
pkgdesc='Basic, good-looking task manager for WMs'
arch=('x86_64' 'i686')
url='https://gitlab.com/o9000/tint2'
license=('GPL2')
conflicts=('ttm-svn')
depends=('gtk2' 'imlib2' 'startup-notification')
makedepends=('cmake' 'startup-notification' 'git' 'ninja' 'setconf')
source=("$pkgname-$pkgver.tgz::https://gitlab.com/o9000/tint2/repository/archive.tar.gz?ref=$pkgver")
sha256sums=('f25faad5ca9b895e5e7aa28290de0ea0a1578119414f91e916c21b5f0c0b8ad7')

prepare() {
  mv "$pkgname-$pkgver-"* "$pkgname"
  setconf "$pkgname/get_version.sh" VERSION "$pkgver"
}

build() {
  cd "$pkgname"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_TINT2CONF=1 -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C "$pkgname/build" install
}

# vim:set ts=2 sw=2 et:
