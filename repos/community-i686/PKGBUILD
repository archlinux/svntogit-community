# $Id$
#Maintainer:  Ionut Biru <ibiru@archlinux.org>
#Contributor: Hugo Doria <hugo@archlinux.org>
#Contributor: Sergej Chodarev <sergejx@centrum.sk>
#Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=gedit-plugins
pkgver=3.0.5
pkgrel=2
pkgdesc="Set of plugins for gedit"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GeditPlugins"
depends=('gedit' 'dbus-python')
optdepends=('gucharmap: for charmap plugin')
makedepends=('intltool' 'gnome-doc-utils')
options=('!libtool' '!emptydirs')
install=gedit-plugins.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/gedit-plugins/3.0/$pkgname-$pkgver.tar.xz)
sha256sums=('e878d4c1bdf9ee38bf23440f3550f854078727233432bfe2f4062794e4b0f3a9')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-plugins=all \
      --disable-schemas-compile

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
