# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>

pkgname=unhide
pkgver=20110113
pkgrel=2
pkgdesc='A forensic tool to find processes hidden by rootkits, LKMs or by other techniques.'
arch=('i686' 'x86_64')
url='http://www.unhide-forensics.info/'
license=('GPL3')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver}.tgz")
md5sums=('0dff8ee5029615f1f81db96dcf6758f7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cc -Os -static unhide.c -o unhide-posix
  cc -Os -static -pthread unhide-linux26.c -o unhide-linux26
  cc -Os -static unhide-tcp.c -o unhide-tcp
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm0755 unhide-posix "${pkgdir}/usr/sbin/unhide-posix"
  install -Dm0755 unhide-linux26 "${pkgdir}/usr/sbin/unhide-linux26"
  install -Dm0755 unhide-tcp "${pkgdir}/usr/sbin/unhide-tcp"
  ln -fs ./unhide-linux26 "${pkgdir}/usr/sbin/unhide"
  install -Dm0644 man/unhide.8 "${pkgdir}/usr/share/man/man8/unhide.8"
}

