# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=xchm
pkgver=1.23
pkgrel=6
pkgdesc="A cross-platform GUI front-end to chmlib"
arch=('i686' 'x86_64')
url="http://xchm.sourceforge.net/"
license=('GPL')
depends=('chmlib' 'wxgtk3')
source=("http://downloads.sourceforge.net/sourceforge/xchm/${pkgname}-${pkgver}.tar.gz"
        'xchm.desktop'
        'xchm.xml'
        xchm-wx3.0-compat.patch)
md5sums=('486d029bd81071a2d04e7181909b1602'
         '31fb9692b9ee9f2a205f58b9ff4bc4fe'
         '4102b0a393f5c60236143cc034664dd0'
         '80fa0eec2acb73860ccadff21aa5c71c')

prepare() {
  cd $pkgname-$pkgver
# Fix build with WxGTK 3 (Debian)
  patch -p1 -i ../xchm-wx3.0-compat.patch
}

build() {
  cd ${pkgname}-${pkgver}

  export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/xchm.desktop "${pkgdir}/usr/share/applications/xchm.desktop"
  install -D -m644 ${srcdir}/xchm.xml "${pkgdir}/usr/share/mime/packages/xchm.xml"
}
