# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: sda <sda00@himki.net>

pkgname=igmpproxy
pkgver=0.1.20161003next
pkgrel=1
_commit=17de79cc3ec459f5660616573d6edd0b2288b664
pkgdesc="a simple multicast router for Linux only using the IGMP protocol"
arch=(i686 x86_64)
url="https://github.com/pali/igmpproxy"
license=('GPL')
depends=()
makedepends=('git')
backup=("etc/igmpproxy.conf")
source=("git://github.com/pali/igmpproxy.git#commit=${_commit}"
	"igmpproxy.service")
md5sums=('SKIP'
         '2a4387442ce00297d97371f729f8b21e')

prepare() {
  cd $srcdir/$pkgname
  ./bootstrap
}

build() {
  cd $srcdir/$pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/$pkgname
  make DESTDIR=$pkgdir install
  install -Dm644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
