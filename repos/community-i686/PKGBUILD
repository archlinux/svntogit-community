# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnewt
pkgver=0.52.16
pkgrel=2
_tclver=8.6
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=('x86_64' 'i686')
url='https://fedorahosted.org/newt/'
license=('GPL')
depends=('slang' 'popt' 'gpm')
makedepends=("tcl>=$_tclver" 'python2')
optdepends=('tcl: whiptcl support' \
            'python2: libnewt api through _snack module')
options=('!makeflags')
source=("https://fedorahosted.org/releases/n/e/newt/newt-$pkgver.tar.gz")
sha256sums=('1b9574bc9c8fb7b25cd26e5c3f2840e8c17fde5dd09c759604925919b3589cd3')

prepare() {
  cd "newt-$pkgver"
  
  sed -i "s:tcl8.4:tcl$_tclver:" Makefile.in
  sed -i 's:bin/python:bin/python2:' *.py
  echo '#define USE_INTERP_RESULT 1' >> config.h
}

build() {
  cd "newt-$pkgver"

  ./configure --prefix=/usr --with-gpm-support
  make
}

package() {
  cd "newt-$pkgver"
  
  make prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
