# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: sh0 <mee@sh0.org>
# Contributor: Maxwel <msg.maxwel@gmail.com>

pkgname=pax-utils
pkgver=1.2.2
pkgrel=2
pkgdesc='ELF related utils for ELF 32/64 binaries that can check files for security relevant properties'
url='http://hardened.gentoo.org/pax-utils.xml'
arch=('i686' 'x86_64')
license=('GPL')
depends=('bash' 'libcap' 'python-pyelftools')
source=("http://distfiles.gentoo.org/distfiles/${pkgname}-${pkgver}.tar.xz")
sha256sums=('7f4a7f8db6b4743adde7582fa48992ad01776796fcde030683732f56221337d9')

_make() {
  make USE_CAP='yes' USE_PYTHON='yes' "$@"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  _make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  LD_LIBRARY_PATH="/usr/lib" _make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  _make DESTDIR="${pkgdir}" install
}
