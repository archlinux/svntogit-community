# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=nsd
pkgver=3.2.10
pkgrel=1
pkgdesc='Authoritative only, high performance and simple DNS server'
url='http://www.nlnetlabs.nl/nsd/'
license=('BSD')
depends=('openssl')
makedepends=('flex')
arch=('i686' 'x86_64')
source=("http://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('04657cffe5087d97213b245f8281296cac86b868')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/ --exec-prefix=/usr --datarootdir=/usr/share
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}"/{etc/rc.d,usr/share/{doc,licenses}/"${pkgname}"}
	rm doc/differences.pdf
	install -m644 doc/* "${pkgdir}"/usr/share/doc/"${pkgname}"
	ln -s ../../doc/"${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
	ln -s ../../usr/sbin/nsdc "${pkgdir}"/etc/rc.d/nsd
}
