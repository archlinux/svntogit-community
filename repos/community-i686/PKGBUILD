# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-psutil
pkgname=('python-psutil' 'python2-psutil')
pkgver=0.5.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://code.google.com/p/psutil/'
license=('custom: BSD')
makedepends=('python' 'python-distribute' 'python2' 'python2-distribute')
source=("https://psutil.googlecode.com/files/psutil-$pkgver.tar.gz") 
sha1sums=('e4276dd11b5f03c4397490736f6c6445a7e64798')

build() {
  cd psutil-$pkgver
  python setup.py build --build-lib=build/python
  python2 setup.py build --build-lib=build/python2
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

package_python-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('glibc' 'python')

  cd psutil-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

package_python2-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python2'
  depends=('glibc' 'python2')

  cd psutil-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 ft=sh et:
