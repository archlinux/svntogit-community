# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=2.4.0
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('i686' 'x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=("http://www.xdebug.org/files/$pkgname-${pkgver,,}.tgz"
	'xdebug.ini')
md5sums=('f49fc01332468f8b753fb37115505fb5'
         '0e601dfb867b248f28d6a647611e4400')

build() {
  cd $srcdir/$pkgname-$pkgver
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd $srcdir/$pkgname-$pkgver/debugclient
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/debugclient
  make DESTDIR=$pkgdir install

  cd $srcdir/$pkgname-$pkgver
  make INSTALL_ROOT=$pkgdir install
  install -D -m 644 $srcdir/xdebug.ini $pkgdir/etc/php/conf.d/xdebug.ini
}
