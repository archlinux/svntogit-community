# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: said <atvordhosbn@gmail.com>
# Contributor: clinx <clinx.hacker@googlemail.com>

pkgname=rbutil
pkgver=1.3.1
pkgrel=4
pkgdesc="Rockbox Utility"
arch=('i686' 'x86_64')
url="http://www.rockbox.org/twiki/bin/view/Main/RockboxUtility"
license=("GPL")
depends=('qt4' 'libusbx')
makedepends=('gendesk')
optdepends=('speex: voice files support')
source=("http://download.rockbox.org/${pkgname}/source/RockboxUtility-v${pkgver}-src.tar.bz2")
sha256sums=('6c04b5c7eaad1762577908dedb9e40f5b0cee675150ae5ba844ea2c9bea294ca')

prepare() {
  gendesk -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" --name="Rockbox Utility" --exec="RockboxUtility" --categories="Application;Utility;" --comment="Rockbox automated installer"
}

build() {
  cd RockboxUtility-v${pkgver}/${pkgname}/rbutilqt

  lrelease-qt4 rbutilqt.pro
  qmake-qt4
  make
}

package(){
  cd RockboxUtility-v${pkgver}/${pkgname}/rbutilqt

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,pixmaps}}
  install -m 755 {,"${pkgdir}"/usr/bin/}RockboxUtility
  install -m 644 icons/rockbox-256.png "${pkgdir}"/usr/share/pixmaps/rbutil.png
  install -m 644 {"${srcdir}","${pkgdir}"/usr/share/applications}/rbutil.desktop
}

#vim: ts=2 sw=2 et:
