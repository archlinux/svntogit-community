# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=opencc
pkgname=(opencc opencc-doc)
pkgver=1.0.5
_tag=ver.1.0.5
pkgrel=1
url="https://github.com/BYVoid/OpenCC"
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('cmake' 'doxygen' 'python' 'git')
source=("git+https://github.com/BYVoid/OpenCC.git#tag=$_tag")
sha512sums=('SKIP')

build() {
  cd OpenCC
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_DOCUMENTATION:BOOL=ON
  make
}

package_opencc() {
  pkgdesc="Library for Open Chinese Convert"
  depends=('gcc-libs')

  cd OpenCC
  make DESTDIR="${pkgdir}" install
  
  # Remove docs - install in splitted package
  rm -r "$pkgdir/usr/share/opencc/doc"
}

package_opencc-doc() {
  pkgdesc="Documentation for Library for Open Chinese Convert"

  cd OpenCC/doc
  make DESTDIR="${pkgdir}" install
}
