# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=libfm
pkgver=1.2.0
pkgrel=6
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'menu-cache' 'desktop-file-utils' 'libexif' 'dbus-glib')
makedepends=('udisks' 'intltool' 'gtk-doc')
replaces=('lxshortcut')
provides=('lxshortcut')
conflicts=('lxshortcut')
install=libfm.install
source=(http://downloads.sourceforge.net/pcmanfm/libfm-$pkgver.tar.xz
        hide-lxshortcut.patch)
md5sums=('07d1361bc008db46b0fd4c775f5696de'
         '49f7f47a3c59c83d1e7a7691eb1cccfe')

prepare() {
  cd $pkgname-$pkgver

  # Hide desktop entry of lxshortcut
  patch -p1 -i ../hide-lxshortcut.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-udisks \
    --with-gnu-ld
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Temporary fix to FS#32361
  rm -rf "$pkgdir"/usr/include/libfm
  mv "$pkgdir"/usr/include/libfm-1.0/ "$pkgdir"/usr/include/libfm
}
