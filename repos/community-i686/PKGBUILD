# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>

pkgname=qmapshack
pkgver=1.8.1
pkgrel=2
pkgdesc='Plan your next outdoor trip'
arch=('x86_64' 'i686')
url='https://bitbucket.org/maproom/qmapshack/'
license=('GPL3')
depends=('hicolor-icon-theme' 'gdal' 'routino>=3.1' 'qt5-script'
         'qt5-webkit' 'proj' 'quazip')
makedepends=('cmake' 'qt5-tools')
source=(https://bitbucket.org/maproom/qmapshack/downloads/$pkgname-$pkgver.tar.gz)
sha256sums=('f6c6562247e205db90637cd45a8f41780765701092c898a2099978bd943ca350')

build() {
  cd "$pkgname-$pkgver"

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
