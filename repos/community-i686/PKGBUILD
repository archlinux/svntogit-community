# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=26.0
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('i686' 'x86_64')
url="https://launchpad.net/deja-dup"
license=('GPL')
depends=('duplicity' 'libpeas' 'libnotify' 'python2-gobject' 'gvfs' 'xdg-utils')
makedepends=('vala' 'intltool' 'itstool' 'nautilus')
optdepends=('nautilus: backup extension'
            'python2-boto: Amazon S3 backend'
            'python2-oauthlib: Ubuntu One backend'
            'ubuntuone-client: Ubuntu One backend')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz
        libsecret-1.vapi)
md5sums=('0430bbf03deac852651599a979a4cc32'
         '27ca0a3c9efbe93e86ef563c77f6ddf3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's/\"python\", \"-c\"/\"python2\", \"-c\"/' common/PythonChecker.vala

  # Add missing Vala API file
  cp "$srcdir/libsecret-1.vapi" "vapi/libsecret-1.vapi"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --without-unity
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
