# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=libdom
pkgver=0.3.2
pkgrel=1
pkgdesc='Implementation of the W3C DOM'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libdom/' # no https available
license=('MIT')
depends=('expat' 'libhubbub>=0.3.4' 'libparserutils>=0.2.3' 'libwapcaplet>=0.4.0')
makedepends=('git' 'libparserutils>=0.2.3' 'netsurf-buildsystem')
source=("git://git.netsurf-browser.org/libdom.git#tag=release/$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  sed 's:_BSD_SOURCE:_DEFAULT_SOURCE:' -i Makefile
  export CFLAGS="$CFLAGS -Wno-error=unused-but-set-variable -w"
  make INCLUDEDIR=include LIBDIR=lib PREFIX=/usr COMPONENT_TYPE="lib-shared"
}

package() {
  make -C "$pkgname" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared" \
    install
  install -Dm644 "$pkgname/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libdom.git
# vim: ts=2 sw=2 et:
