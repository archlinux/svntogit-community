# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Le_suisse <lesuisse.dev+aur at gmail dot com>
# Contributor: Jason Rodriguez <jason-aur@catloaf.net>

pkgname=sslh
pkgver=1.18
pkgrel=1.2
pkgdesc='SSL/SSH/OpenVPN/XMPP/tinc port multiplexer'
arch=('i686' 'x86_64')
url='http://www.rutschle.net/tech/sslh.shtml'
license=('GPL2')
makedepends=('systemd')
depends=('libcap' 'libconfig' 'libsystemd')
backup=('etc/sslh.cfg')
install=$pkgname.install
source=("http://www.rutschle.net/tech/sslh/$pkgname-v$pkgver.tar.gz"{,.asc}
        'sslh-master.zip'
        'sslh.cfg'
        'sslh.sysusers'
        'sslh.service'
        'sslh-select.service'
        'sslh-fork.service')
validpgpkeys=('CDDDBADBEA4B72748E007D326C056F7AC7934136') # Yves Rutschle <yves@rutschle.net>
md5sums=('0e3568d5d234516c634d4df156473298'
         'SKIP'
         '5cfde7884d5c9d7a8b7b971b3f37e94b'
         'd5405c7ca7e1813e4d49a473e5834640'
         'f39544277a30595d4b7476b3f87ebbcf'
         'e66490eacc9cb586e48e4e0562ac25e3'
         '7f95dc8ce4c8dfbe5cd0b1cabb8acb31'
         'ea5733ec4c68709a813e84b4403e342a')

build() {
  #cd $pkgname-v$pkgver
  cd sslh-master
  #FIXME: https://github.com/yrutschle/sslh/issues/103
  export CFLAGS=''
  make VERSION=\"v$pkgver\" USELIBCAP=1 USESYSTEMD=1 all systemd-sslh-generator
}

#check() {
#  cd $pkgname-v$pkgver
#  make test
#}

package() {
  # default arch config
  install -Dm 644 sslh.cfg "$pkgdir/etc/sslh.cfg"
  # manually install to have both ssl-fork and ssl-select
  #cd $pkgname-v$pkgver
  cd sslh-master
  install -Dm 755 sslh-fork "$pkgdir/usr/bin/sslh-fork"
  install -Dm 755 sslh-select "$pkgdir/usr/bin/sslh-select"
  ln -s sslh-fork "$pkgdir/usr/bin/sslh"
  # install manpage
  install -Dm 644 sslh.8.gz "$pkgdir/usr/share/man/man8/sslh.8.gz"
  ln -s sslh.8.gz "$pkgdir/usr/share/man/man8/sslh-fork.8.gz"
  ln -s sslh.8.gz "$pkgdir/usr/share/man/man8/sslh-select.8.gz"
  # install examples files
  install -Dm 644 basic.cfg "$pkgdir/usr/share/doc/$pkgname/basic.cfg"
  install -Dm 644 example.cfg "$pkgdir/usr/share/doc/$pkgname/example.cfg"
  # systemd
  install -dm 755 "$pkgdir"/usr/lib/{systemd/system,sysusers.d}
  install -Dm 755 systemd-sslh-generator "$pkgdir/usr/lib/systemd/system-generators/systemd-sslh-generator"
  cd "$pkgdir"
  install -Dm 644 "$srcdir"/sslh{,-fork,-select}.service usr/lib/systemd/system
  install -Dm 644 "$srcdir"/sslh.sysusers usr/lib/sysusers.d/sslh.conf
}

# vim:set ts=2 sw=2 et:
