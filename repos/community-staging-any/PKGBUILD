# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=2.1.2
pkgrel=3
pkgdesc='Terminal emulator that supports tabs and grids'
arch=('any')
url='https://github.com/gnome-terminator/terminator'
license=('GPL2')
depends=('gsettings-desktop-schemas' 'libkeybinder3' 'libnotify' 'python-cairo' 'python-configobj'
         'python-dbus' 'python-psutil' 'python-gobject' 'vte3' 'xdg-utils')
makedepends=('intltool' 'python-pytest-runner' 'python-setuptools' 'python-wheel')
source=(${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc}
        0001-fixed-validate-module-import-for-newer-version-of-co.patch)

validpgpkeys=('6FBDB41587919A0F4BA96EBA9A358BAB04A4ED27'  #Markus Frosch <markus@lazyfrosch.de>
              'D11A7596F61705480C711598F2FAC7C7BAE930A5') #Matt Rose <mattrose@folkwolf.net>
sha512sums=('070bf49979dd93e694b536877bac2d6ad274a1e961ef74df90348da6520fc411e7d0a73cd0d0c8fa2989c94ea75758fd15c09357f4f2ca8e7f6a4ab6c1236748'
            'SKIP'
            'da609bbb1f2fce64bd345ec43c05f6b8d76046cfbd1be764d6fcd555d306dfca1dd5786b0fc65c0984e49a85454c9eeabf5a1f8c960412c14a97c5350912f8a9')
b2sums=('2ca98c1caeadfffd2c7f737947b216a1db438a426a95cb9871504b827c3e85384df157d96a47c5f3d2cdc33e5e6b0e35cb8019b7a4bbc92fe59603379610e2e0'
        'SKIP'
        '7cde90cefde34346ef20b50ebd5b930cb47df650c649e199adbb1b6235db0b40c0c2ad2b0e30bc764d52469357ce0b0424648f70da93b10d39ed0793cbbfe648')

prepare () {
    cd "${pkgname}-${pkgver}"

    #Fix for python configobj validate
    patch -Np1 < $srcdir/0001-fixed-validate-module-import-for-newer-version-of-co.patch
}

build() {
    cd "${pkgname}-${pkgver}"

    python setup.py build
}

package() {
    cd "${pkgname}-${pkgver}"

    python setup.py install --skip-build --root="${pkgdir}" --optimize=1

    rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"
}
