# Maintainer: David Runge <dvzrv@archlinux.org>

_name=unearth
pkgname=python-unearth
pkgver=0.8.1
pkgrel=2
pkgdesc="A utility to fetch and download python packages"
arch=(any)
url="https://github.com/frostming/unearth"
license=(MIT)
depends=(python-packaging python-requests)
makedepends=(python-build python-installer python-pdm-pep517 python-wheel)
checkdepends=(python-flask python-pytest python-pytest-httpserver python-requests-wsgi-adapter python-trustme)
optdepends=('python-keyring: use keyring for authentication')
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
)
sha256sums=('62d3493619d181746c0e0586dfcf6cef957b95f4d3b042698eda312fdbf0617b')
b2sums=('bbdfc83d5b25386e49bc21e25c5a0f8daf7d31f0afa8828576b06b366a093589ba23f5a7cc36a3ac47a254bd0734eeb8da9f14d0844d5a43b163dc9551c1cb8e')

build() {
  cd $_name-$pkgver
  export PDM_PEP517_SCM_VERSION=$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
