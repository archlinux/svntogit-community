# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgname=python-venusian
pkgver=3.0.0
pkgrel=2
pkgdesc="A library for deferring decorator actions"
arch=('any')
license=('custom:BSD')
url="https://pylonsproject.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Pylons/venusian/archive/$pkgver.tar.gz")
sha512sums=('5cb765d42c19341917bfe8b97c4c117140060a2c6b12492daebbae5e48cbc8b9db0c85593714c1b3754f7921c522d99b0feaf79291088a5abd83f979a3a938c0')

build() {
  cd venusian-$pkgver
  python setup.py build
}

check() {
  cd venusian-$pkgver
  python setup.py pytest
}

package() {
  cd venusian-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
