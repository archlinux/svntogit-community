# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-tinycss2
pkgname=(python-tinycss2 python2-tinycss2)
pkgver=0.6.1
pkgrel=2
pkgdesc="Low-level CSS parser for Python"
url="https://pypi.python.org/pypi/tinycss2"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-webencodings' 'python2-webencodings')
checkdepends=('python-pytest-flake8' 'python2-pytest-flake8' 'python-pytest-cov'
              'python2-pytest-cov' 'python-pytest-isort' 'python2-pytest-isort'
              'python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/t/tinycss2/tinycss2-$pkgver.tar.gz")
md5sums=('8dea5867b810e30a029c54e29a4dce4d')

prepare() {
  cp -a tinycss2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/tinycss2-$pkgver
  python setup.py build

  cd "$srcdir"/tinycss2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/tinycss2-$pkgver
  python setup.py pytest

  cd "$srcdir"/tinycss2-$pkgver-py2
  python2 setup.py pytest
}

package_python-tinycss2() {
  depends=('python-webencodings')

  cd tinycss2-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-tinycss2() {
  depends=('python2-webencodings')

  cd tinycss2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
