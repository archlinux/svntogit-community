# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-docopt
_pkgname=docopt
pkgname=('python-docopt' 'python2-docopt')
pkgver=0.6.2
pkgrel=5
pkgdesc="Pythonic argument parser, that will make you smile"
arch=('any')
url="https://github.com/docopt/docopt"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('49b3a825280bd66b3aa83585ef59c4a8c82f2c8a522dbe754a8bc8d08c85c491')

prepare(){
  cp -dpr --no-preserve=owner "${_pkgname}-${pkgver}" "${_pkgname}-${pkgver}-2"
}

build() {
  # Build Python 3.x module
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build
  # Build Python 2.x module
  cd "${srcdir}/${_pkgname}-${pkgver}-2"
  python2 setup.py build
}

package_python-docopt() {
  depends=('python')
  cd "${_pkgname}-${pkgver}"
  python setup.py install --skip-build --optimize=1 --root="${pkgdir}"
  install -D -m 0644 "LICENSE-MIT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-docopt() {
  depends=('python2')
  cd "${_pkgname}-${pkgver}-2"
  python2 setup.py install --skip-build --optimize=1 --root="${pkgdir}"
  install -D -m 0644 "LICENSE-MIT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
