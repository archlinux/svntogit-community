# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=jsonmerge
pkgname=python-$_pkgname
pkgver=1.9.0
pkgrel=2
pkgdesc='Merge a series of JSON documents'
arch=('any')
url='https://github.com/avian2/jsonmerge'
license=('MIT')
depends=('python-jsonschema')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgname-$pkgver.tar.gz")
sha512sums=('87879d3397fba34d7fd37bbd43d6f6ed7028fba702154e40a5d8c01ea0978cd77d0636b3a9cc9fffe14a62a3b86e64777a84b3a56253082efacb66b351b35433')

build() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py test
}

package() {
  cd $_pkgname-$_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
