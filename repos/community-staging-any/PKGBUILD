# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sqlalchemy-migrate
pkgname=(python-sqlalchemy-migrate python2-sqlalchemy-migrate)
pkgver=0.13.0
pkgrel=3
pkgdesc="Database schema migration for SQLAlchemy"
arch=('any')
url="https://pypi.python.org/pypi/sqlalchemy-migrate/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-sqlalchemy'
             'python2-sqlalchemy' 'python-decorator' 'python2-decorator' 'python-sqlparse'
             'python2-sqlparse' 'python-tempita' 'python2-tempita')
checkdepends=('python-mock' 'python-testrepository' 'python-scripttest' 'python-ibm-db-sa')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://opendev.org/x/sqlalchemy-migrate/archive/$pkgver.tar.gz")
sha512sums=('3ae1140cb17ea24db7423a8cb62d667620333c8ed559f6bfd6eecfb35f829c9f9c4918f41f8f723d89bcb3c51d6b803e5cbdc30cb485c8cdfc140caaf1d981f0')

prepare() {
  cp -a sqlalchemy-migrate{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/sqlalchemy-migrate
  python setup.py build

  cd "$srcdir"/sqlalchemy-migrate-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sqlalchemy-migrate
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr || warning "Tests failed"
}

package_python-sqlalchemy-migrate() {
  depends=('python-six' 'python-pbr' 'python-sqlalchemy' 'python-decorator' 'python-sqlparse'
           'python-tempita')

  cd "$srcdir"/sqlalchemy-migrate
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sqlalchemy-migrate() {
  depends=('python2-six' 'python2-pbr' 'python2-sqlalchemy' 'python2-decorator' 'python2-sqlparse'
           'python2-tempita')

  cd "$srcdir"/sqlalchemy-migrate-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/migrate{,2}
  mv "$pkgdir"/usr/bin/migrate-repository{,2}
}

# vim:set ts=2 sw=2 et:
