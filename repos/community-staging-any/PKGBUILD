# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-curtsies
pkgname=(python-curtsies python2-curtsies)
_pkgname=curtsies
pkgver=0.3.0
pkgrel=2
pkgdesc="Curses-like terminal wrapper with a display based on compositing 2d arrays of text"
arch=('any')
url="https://pypi.python.org/pypi/curtsies"
license=('MIT')
depends=('python-blessings' 'python-wcwidth')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/c/curtsies/curtsies-$pkgver.tar.gz")
md5sums=('20e7295c9592b4101915131a685725f0')

prepare() {
  cd "$srcdir"
  cp -r curtsies-$pkgver curtsies2-$pkgver

  # 0.3.0 bug?
  cd "$srcdir/curtsies-$pkgver"
  sed -i 's/^.*typing.*$//' curtsies.egg-info/requires.txt setup.py
}

build() {
  cd "$srcdir/curtsies-$pkgver"
  python3 setup.py build

  cd "$srcdir/curtsies2-$pkgver"
  python2 setup.py build
}

package_python-curtsies() {
  cd "$srcdir/curtsies-$pkgver"
  python3 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python-curtsies/LICENSE"
}

package_python2-curtsies() {
  depends=('python2-blessings' 'python2-wcwidth' 'python2-typing')
  cd "$srcdir/curtsies2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --prefix=/usr --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/python2-curtsies/LICENSE"
}
