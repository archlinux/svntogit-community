# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org

_pkgname=sqlalchemy-continuum
pkgname=python-sqlalchemy-continuum
pkgver=1.3.11
pkgrel=2
pkgdesc='Versioning and auditing extension for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-continuum'
arch=('any')
license=('BSD')
depends=('python' 'python-sqlalchemy1.3' 'python-sqlalchemy-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('sqlite' 'python-pytest' 'python-flexmock' 'python-psycopg2' 'python-mysql-connector'
              'python-six' 'python-anyjson' 'python-flask' 'python-flask-login' 'python-flask-sqlalchemy'
              'python-sqlalchemy-i18n')
optdepends=('python-flask: flask plugin'
            'python-flask-login: flask plugin'
            'python-flask-sqlalchemy: flask plugin'
            'python-sqlalchemy-i18n: internationalization')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kvesteri/sqlalchemy-continuum/archive/${pkgver}.tar.gz)
sha512sums=('297464a3e954a4cace33f6e0829d6384dfca224495060f62a6ae05260b8a7e313ff1f981d8bf3c28555836cc68cba855c0f676f9edc66fcdd59a931613c1c838')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  make -C docs text man
}

check() {
  cd ${_pkgname}-${pkgver}/tests
  DB=sqlite pytest -k 'not test_flask' || true
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/sqlalchemy-continuum.1 -t "${pkgdir}/usr/share/man/man1"
  ln -sf /usr/share/man/man1/sqlalchemy-continuum.1.gz "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
}

# vim: ts=2 sw=2 et:
