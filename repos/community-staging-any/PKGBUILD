# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-perl-critic
pkgver=1.140
pkgrel=3
pkgdesc="Critique Perl source code for best-practices"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Perl-Critic"
depends=('perl-b-keywords' 'perl-config-tiny' 'perl-exception-class' 'perl-file-homedir'
         'perl-file-which' 'perl-io-string' 'perl-list-moreutils' 'perl-module-build'
         'perl-module-pluggable' 'perl-ppi' 'perl-ppix-quotelike' 'perl-ppix-regexp'
         'perl-ppix-utilities' 'perl-pod-spell' 'perl-tidy' 'perl-readonly' 'perl-string-format'
         'perl-task-weaken')
makedepends=('perl-test-deep')
checkdepends=('perl-test-memory-cycle')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PE/PETDANCE/Perl-Critic-$pkgver.tar.gz")
sha512sums=('46afc8260d2b500edbb29836be993f065270ed9ffa6aa00611a980b294025b794286bc820afeabb94b7d1cdd9bfb4e715e44e466fb0340cb9d938e031b54a6f0')

build() {
  cd Perl-Critic-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Perl-Critic-$pkgver
  perl Build test
}

package() {
  cd Perl-Critic-$pkgver
  perl Build install destdir="$pkgdir"
}
