# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=devede
pkgver=3.16.9
pkgrel=2
pkgdesc="A program to create VideoDVDs and CDs"
arch=('any')
url="http://www.rastersoft.com/programas/devede.html"
license=('GPL3')
depends=('mplayer' 'dvdauthor' 'vcdimager' 'cdrkit' 'pygtk' 'ttf-dejavu')
source=(http://www.rastersoft.com/descargas/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f35c5e72264ee272194cfa558b43e598')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # python2 fix
    for file in *.py; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}

    ./install.sh --DESTDIR=${pkgdir} \
        --prefix=/usr \
        --unistall=no \
        --targeted=yes

    install -D -m644 ${pkgname}.desktop \
        ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
