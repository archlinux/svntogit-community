# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Artem Vorotnikov <artem at vorotnikov dot me>

_gemname=addressable
pkgname=ruby-addressable
pkgver=2.7.0
pkgrel=3
pkgdesc='URI Implementation'
arch=('any')
url='https://github.com/sporkmonger/addressable'
license=('Apache')
depends=('ruby' 'ruby-public_suffix')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sporkmonger/addressable/archive/addressable-${pkgver}.tar.gz)
sha256sums=('b0baa4caf354135d756b81821ef076935fa807cb97cc60174b481794fd0290b5')
sha512sums=('507a9757a93b6cc7047a6d8226221242509a8895fce961d7b7e86c62c864eb646584cc1e3b8f6634a8578bc06a098403404bbee9b94f1c53d59503ca7408781f')

prepare() {
  cd ${_gemname}-${_gemname}-${pkgver}
  sed -E 's|~>|>=|g' -i ${_gemname}.gemspec
  sed -E 's|"< [0-9.]+", ||g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
