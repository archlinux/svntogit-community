# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-curio
pkgver=1.6
pkgrel=3
pkgdesc='Concurrent I/O'
arch=(any)
url=https://github.com/dabeaz/curio
license=(BSD)
depends=(python)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-pytest
  python-sphinx
)
provides=(python-multio-provider)
_tag=5d8ecb2333761b4fd629e14d940f3807d143c0ed
source=(git+https://github.com/dabeaz/curio.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd curio
  git describe --tags
}

build() {
  cd curio
  python setup.py build
}

check() {
  cd curio
  PYTHONPATH=$PWD/build/lib pytest
}

package() {
  cd curio
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-curio/
}

# vim: ts=2 sw=2 et:
