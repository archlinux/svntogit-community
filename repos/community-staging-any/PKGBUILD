# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-praw
pkgver=7.7.0
pkgrel=2
pkgdesc="Python Reddit API Wrapper that allows for simple access to Reddit's API"
arch=('any')
license=('GPL')
url="https://praw.readthedocs.org"
depends=('python-prawcore' 'python-update-checker' 'python-websocket-client')
makedepends=('python-setuptools' 'python-pytest-runner')
checkdepends=('python-mock' 'python-betamax-serializers' 'python-betamax-matchers')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('40a8882cdcaa10f538f9cf9552414184050b16008eed4c4e31fc52dfc0d337daad6bd1041d462d26476c692ae8997f675cf5ce2630d8d8be091023b7deb98181')

build() {
  cd praw-$pkgver
  python setup.py build
}

check() {
  cd praw-$pkgver
  python setup.py pytest
}

package() {
  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
