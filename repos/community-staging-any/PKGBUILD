# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-fs
pkgver=2.4.11
pkgrel=3
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('BSD')
url="https://pypi.python.org/pypi/fs/"
depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')
checkdepends=('python-pytest-runner' 'fuse2' 'python-mock' 'python-pyftpdlib' 'python-psutil'
              'python-pysendfile')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyFilesystem/pyfilesystem2/archive/v$pkgver.tar.gz"
        $pkgname-python3.9.patch::https://github.com/PyFilesystem/pyfilesystem2/pull/424.patch)
sha512sums=('fe4596bf213f9b1721cd3c665e6af180d6617eea07afbaa9d83847dc3f7dfe64bca16c8ccb337d48bbb49bf0d2fef90903e3c4aecc63f61faac6a2b6f6c59cf6'
            '2d1088f46174700ef36d0759993650feccdbd231830d316fdda6dd8fd03e4831ba009cc37a3ed19a6ae1a3cbb6fcce292212b56cdbe87fd3d2af261de4648120')

prepare() {
  patch -d pyfilesystem2-$pkgver -p1 < $pkgname-python3.9.patch || :
}

build() {
  cd pyfilesystem2-$pkgver
  python setup.py build
}

check() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" python setup.py pytest
}

package() {
  cd pyfilesystem2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
