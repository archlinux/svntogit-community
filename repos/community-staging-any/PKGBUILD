# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Muflone http://www.muflone.com/contacts/english/

pkgbase=python-pysmi
_pyname="${pkgbase/*-/}"
pkgname=('python-pysmi' 'python2-pysmi')
pkgver=0.3.4
pkgrel=3
pkgdesc='SNMP/SMI MIB parsing and conversion library designed to turn ASN.1 MIBs into various formats'
url='http://snmplabs.com/pysmi/'
arch=('any')
license=('BSD')
makedepends=('python' 'python-sphinx' 'python-ply'
             'python2' 'python2-sphinx' 'python2-ply')
checkdepends=('python-pytest' 'python-pysnmp'
              'python2-pytest' 'python2-pysnmp')
source=(${_pyname}-${pkgver}.tar.gz::https://github.com/etingof/pysmi/archive/v${pkgver}.tar.gz)
sha512sums=('e6be1f0117f069cb9d67bb0dbc1cd8fa37b40865094ee33a60f794f6721cef14a72326fffd2fc43f22d6a959daf2d42147270421f86a57bde46081b5839880bf')

prepare() {
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -C docs text man
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -C docs text man
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    py.test
  )
  (cd ${_pyname}-${pkgver}-py2
    py.test2
  )
}

package_python-pysmi() {
  depends=('python' 'python-ply')
  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -Dm 644 docs/build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${_pyname}.1"
  install -Dm 644 docs/build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"

  mv "${pkgdir}/usr/bin/mibdump"{.py,}
  mv "${pkgdir}/usr/bin/mibcopy"{.py,}
}

package_python2-pysmi() {
  depends=('python2' 'python2-ply')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples -t "${pkgdir}/usr/share/doc/${pkgname}"

  install -Dm 644 docs/build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"

  rm -r "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
