#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2020.9
pkgrel=2
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.9.tar.xz
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.9.tar.xz.sig
)
sha512sums=(
  2cfad4919cd7cd0c3e4be2d99f2ef7d2df3b39254fc5494b2f858fb8f07f22278688a4b3708aecd109f0602d4b93a335ef15e08c3b48f61c2f2090bbaa58fc4e
  1758ba6cc35f90b30848b67a4c1fc437fef18338ba23e25188a12b767ef83f6d544269291a7774300acf95d7bccdb3feb1eb62b86fdf9cb2fdb715101fe1c524
)
md5sums=(
  3dd0a13b05d0fc7b2cc568b8b1762624
  466785e6214609d3dad5faaf44b39be5
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 "$srcdir/$pkgname-$pkgver/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
