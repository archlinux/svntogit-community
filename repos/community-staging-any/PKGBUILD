# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.6.3
pkgrel=2
pkgdesc="Tmux session manager built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ea543ffb70ae62ecfeb1b0eff65b40b070ba6b79e46e7d48da85d998cee47d3869bed6fb756345db83184eec27593b51426f4b8dda48871664a3478e90e88386')
b2sums=('c1bbbe66b0377124eb90189c112550753f53ca908f464bf6ab22cd6589a1942e670c8d532e3db0c53b29c8b1ca6930ee792e6bb242aca46b1c71cccb9d14a644')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove all hard version pinning:
  # https://github.com/tmux-python/tmuxp/issues/432
  sed -e 's/==/>=/g' -i requirements/*.txt
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CHANGES,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.{json,yaml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}

# vim:set ts=2 sw=2 et:}
