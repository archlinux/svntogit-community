# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plop
pkgver=0.4.0
pkgrel=5
pkgdesc="Python Low-Overhead Profiler"
arch=('any')
license=('MIT')
url="https://github.com/bdarnell/plop"
depends=('python-six' 'python-tornado')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/p/plop/plop-${pkgver}.tar.gz")
sha256sums=('2b583cc2e34f6bb10832793b87b58eaa555ea31a2c8b62b42fac65411cf90139')

build() {
  cd plop-$pkgver
  python setup.py build
}

check() {
  cd plop-$pkgver
  python -m plop.test.runtests
}

package() {
  cd plop-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
