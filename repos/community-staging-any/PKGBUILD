# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=stdlibs
pkgname=python-$_pkgname
pkgver=2022.10.9
pkgrel=2
pkgdesc="Simple list of top-level packages in Python's stdlib"
arch=('any')
url='https://github.com/omnilib/stdlibs'
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-flit-core' 'python-wheel')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('3b09a3048bc07e1ff520f83da7bf54b7a3fdc5b19022db380c8af785ae0d1896a0c6d10ea1102520b6cc0babad5327a930e7f7a61b1e1e21f52ac21805b53059')

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
