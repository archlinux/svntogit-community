# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-flask-socketio
_pkgname=Flask-SocketIO
pkgver=3.2.1
pkgrel=2
pkgdesc='Socket.IO integration for Flask applications'
url='https://flask-socketio.readthedocs.org/'
arch=('any')
license=('MIT')
depends=('python-flask' 'python-eventlet' 'python-socketio')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-coverage' 'python-pytest')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}.tar.gz)
sha256sums=('aa2ce476d70600fb8fef04107677612e370602ac0d1c5ece5a6e01f297b3d8b0')
sha512sums=('8f2009a34142f669fbc2711a12f202554f01da695d0f62a4dcc0d3b5a1f55d8b21a9d66fa0651d316e31979dd0400d5c2f9d4b24c3ff915e5a54682e7d877a06')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pkgname}-${pkgver}
  py.test test_socketio.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
