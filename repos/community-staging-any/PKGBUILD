# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-sugar
pkgver=0.9.2
_commit=2764da41c431f6f32ade45b2de0310010117efd7
pkgrel=2
pkgdesc='A plugin for py.test that changes the default look and feel of py.test'
arch=('any')
license=('BSD')
url='https://github.com/Frozenball/pytest-sugar'
depends=('python-pytest' 'python-termcolor')
source=("$pkgname-$_commit.tar.gz::https://github.com/Frozenball/pytest-sugar/archive/$_commit.tar.gz")
sha512sums=('ea54eeb6adaff5e8548e4fffec3fc6dfb8a5e186ede77291180efe0ab445f6aab4608c7d08b753a5e15630c1f5caa0689dccca7cd4f3f734ba83a02b9c482df3')

prepare() {
  mv pytest-sugar-{$_commit,$pkgver}
}

build() {
  cd "$srcdir"/pytest-sugar-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-sugar-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-sugar-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
