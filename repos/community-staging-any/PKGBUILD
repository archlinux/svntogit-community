# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-apispec-webframeworks
pkgver=0.4.0
pkgrel=4
pkgdesc="Web framework plugins for apispec"
url="https://github.com/marshmallow-code/apispec-webframeworks"
license=('MIT')
arch=('any')
depends=('python-apispec' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-bottle' 'python-flask' 'python-tornado')
source=("$pkgname-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec-webframeworks/archive/$pkgver.tar.gz")
sha512sums=('f831c4e8d494a67a2ce0f88624fa7770bb7e3731d57158f9d622c07f1f87b7c0f55375cd0bffd36d3a8aa204925ae8dbf478a749376320ce8aec324f86c23306')

build() {
  cd apispec-webframeworks-$pkgver
  python setup.py build
}

check() {
  cd apispec-webframeworks-$pkgver
  python setup.py pytest || warning "https://github.com/marshmallow-code/apispec-webframeworks/issues/45"
}

package() {
  cd apispec-webframeworks-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
