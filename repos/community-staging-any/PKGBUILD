# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=ana
pkgname=python-${_pyname}
pkgver=0.06
pkgrel=2
pkgdesc='Python module to provide easy distributed data storage'
url='https://github.com/zardus/ana'
arch=('any')
license=('custom:BSD2')
depends=('python' 'python-future')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha512sums=('d4944f96379d399040e191d3058d35e5e7ca2cd7193b8dfb3b7b78e0dbf26c8c5dc48acb7c570e51fead0387ee708246de8cf9231ef35028bdd7686f732df875')
b2sums=('a94c37c2bd845b4f2f5402296487c4c29ff5f7f3a1b88b3a77a24add48efc5364bf0f07705a9db72e6d34579bece535ac3b230f173da3b262a50f4bf48dcc02c')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
