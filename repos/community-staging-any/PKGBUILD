# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ukpostcodeparser
pkgver=1.1.2
pkgrel=11
pkgdesc="UK Postcode parser"
arch=('any')
license=('MIT')
url="https://github.com/hamstah/ukpostcodeparser"
depends=('python')
makedepends=('python-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/hamstah/ukpostcodeparser/archive/v$pkgver.tar.gz")
sha512sums=('8113522b728c095f937a9f53f21c369130e3e63c4fc3546dbea4ad1c53648742fb0e766b1254c76ee65d69dec585f1a90caa7d39ca540de1765ff5f989a702de')

build() {
  cd "$srcdir"/ukpostcodeparser-$pkgver
  python setup.py build
}

check() {
  # Expected failure: https://github.com/hamstah/ukpostcodeparser/issues/8

  cd "$srcdir"/ukpostcodeparser-$pkgver
  python -m unittest discover -s ukpostcodeparser/test -p '*.py' || echo "Tests failed"
}

package() {

  cd ukpostcodeparser-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
