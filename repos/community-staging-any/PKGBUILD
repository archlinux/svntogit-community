# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbclient
pkgver=0.7.2
pkgrel=2
pkgdesc="A tool for running Jupyter Notebooks in different execution contexts."
arch=('any')
url="https://github.com/jupyter/nbclient"
license=('BSD')
depends=('python-nest-asyncio' 'python-traitlets' 'python-async_generator' 'python-jupyter_client' 'jupyter-nbformat' 'python-testpath')
makedepends=('python-build' 'python-installer' 'python-hatchling')
checkdepends=('python-pytest-asyncio' 'python-xmltodict' 'jupyter-nbconvert' 'python-ipywidgets')
source=("https://github.com/jupyter/nbclient/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('06d2d73ab1b8e929f52a20e65986f4c36bd410d964681c8a36a72ddf0e3c7661')

build() {
  cd nbclient-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbclient-$pkgver
  pytest -v
}

package() {
  cd nbclient-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
