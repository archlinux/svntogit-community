# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Sarboni <max-k@post.com>

pkgbase=python-pydispatcher
pkgname=('python-pydispatcher' 'python2-pydispatcher')
pkgver=2.0.5
pkgrel=3
pkgdesc="Multi-producer-multi-consumer signal dispatching mechanism"
url="http://sourceforge.net/projects/${pkgbase}/"
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/P/PyDispatcher/PyDispatcher-$pkgver.tar.gz")
md5sums=('1b9c2ca33580c2770577add7130b0b28')

prepare() {
  cp -a PyDispatcher-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PyDispatcher-$pkgver
  python setup.py build

  cd "$srcdir"/PyDispatcher-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/PyDispatcher-$pkgver
  python setup.py pytest

  cd "$srcdir"/PyDispatcher-$pkgver-py2
  python2 setup.py pytest
}

package_python-pydispatcher() {
  depends=('python')

  cd PyDispatcher-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-pydispatcher() {
  depends=('python2')

  cd PyDispatcher-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
