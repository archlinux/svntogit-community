# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gabriel Rauter <rauter.gabriel@gmail.com>

pkgname=python-fnvhash
pkgver=0.1.0
pkgrel=4
pkgdesc='Pure Python FNV hash implementation'
url=https://github.com/znerol/py-fnvhash
arch=(any)
license=(MIT)
depends=(python)
makedepends=(
  git
  python-build
  python-installer
  python-setuptools
  python-wheel
)
_tag=3e3f09a36d09809d99fd63579e2a75b315948dea
source=(git+https://github.com/znerol/py-fnvhash.git#tag=${_tag})
b2sums=('SKIP')

build() {
  cd py-fnvhash
  python -m build --wheel --no-isolation
}

package() {
  cd py-fnvhash
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-fnvhash
}

# vim: ts=2 sw=2 et:
