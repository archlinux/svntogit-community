# Maintainer: George Rawlinson <george@rawlinson.net.nz>

_name=flake8-black
pkgname=python-flake8-black
pkgver=0.2.1
pkgrel=3
pkgdesc='Flake8 plugin to call black as a code style validator'
arch=('any')
url=https://github.com/peterjc/flake8-black
license=('MIT')
depends=('flake8' 'python-black' 'python-setuptools')
source=("$url/archive/v$pkgver/$_name-v$pkgver.tar.gz")
b2sums=('46cf95349f683d3c4f411425c1e6e70dc0a0036036eccb736362b9888b4b0cb4c6d215fdb78bf59bbe8d9f3edca24959121c628426570dc1917f6ef9ad5ffbca')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  cd tests
  PATH="${PWD%/tests}/temp/usr/bin:$PATH" PYTHONPATH="${PWD%/tests}/temp/$sitepackages" ./run_tests.sh
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.rst
}
