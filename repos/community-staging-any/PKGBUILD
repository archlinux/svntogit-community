# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=libcst
pkgname=python-libcst
pkgver=0.4.9
pkgrel=1
pkgdesc="A concrete syntax tree parser and serializer library for Python that preserves many aspects of Python's abstract syntax tree"
arch=('any')
url='https://github.com/Instagram/LibCST'
license=('MIT')
depends=('python-typing_extensions' 'python-typing_inspect' 'python-yaml')
makedepends=('python-setuptools-scm' 'python-setuptools-rust' 'python-isort' 'python-black' 'ufmt')
checkdepends=('python-hypothesis' 'python-hypothesmith')
source=("https://pypi.io/packages/source/l/libcst/libcst-$pkgver.tar.gz")
sha512sums=('f4f6b89ae06b319d8a7ce29e3a2446318b587684adcc80fae32fd4cf4cb3744ef20e11543d38b27622b4b748df1ca4f829d23cf0327f0f4639c93a4fc118c4df')

prepare() {
  cd $_pkgname-$pkgver

#  sed -i 's/import AbstractBaseMatcherNodeMeta/import Optional, AbstractBaseMatcherNodeMeta/' libcst/codegen/gen_matcher_classes.py
#  python -m libcst.codegen.generate matchers
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m unittest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
