# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-number-bytes-human
pkgver=0.11
pkgrel=2
pkgdesc='Convert byte count to human readable format'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Number-Bytes-Human
source=("http://search.cpan.org/CPAN/authors/id/F/FE/FERREIRA/Number-Bytes-Human-$pkgver.tar.gz")
md5sums=(43d2c86a55924aa71535a41de0311efd)
sha512sums=(045bfe3d26b3460cb45ef69aeb1398c897605e621f559c43cf199829b1715e12781ad0330d80bed8b7c15b332ca6c9e620e580c71a0f1cf68c7d2fcf4b023936)
_ddir="Number-Bytes-Human-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
