# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=treelib
pkgname=python-$_pkgname
pkgver=1.6.1
pkgrel=2
pkgdesc='An efficient implementation of tree data structure'
arch=('any')
url='https://github.com/caesar0301/treelib'
license=('Apache2')
depends=('python' 'python-future')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('2b7007f759eefd8cdca0a43ca57e33bbfc3e832fc0a723386d369a9eae831af1270bb5d6c6a6b48178a9f7503a7acf702cfb7308cf74514930f589fe9b0a7a79')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
