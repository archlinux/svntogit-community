# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-tblib
pkgname=('python-tblib' 'python2-tblib')
_pyname=tblib
pkgver=1.4.0
pkgrel=3
pkgdesc='Python traceback fiddling library'
url='https://github.com/ionelmc/python-tblib'
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=(https://github.com/ionelmc/python-tblib/archive/v${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('7338abe80d82e319643064848f6d1cbd32d80c0d444c6e8de0088d170ed6cd95')
sha512sums=('955ccbeaade32b36681d66bff59c777a5dc03465b0e93d1de25f8f2156f485c82a0e6d9578b2a00b2e1b2c21ac182d86568b2b61221cf46a2196aa77b0fc3620')

prepare() {
  cp -ra ${pkgbase}-${pkgver}{,-py2}
}

check() {
  (cd ${pkgbase}-${pkgver}
    python setup.py test
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-tblib() {
  depends=('python-six')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-tblib() {
  depends=('python2-six')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
