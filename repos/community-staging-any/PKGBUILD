# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=cooldict
pkgname=python-cooldict
pkgver=1.04
pkgrel=2
pkgdesc='Some useful dict-like structures'
url='https://github.com/zardus/cooldict'
arch=('any')
license=('custom:BSD2')
depends=('python' 'python-ana')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/${_pyname}/${_pyname}-${pkgver}.tar.gz")
sha512sums=('ad23eb05167582a3d8772ff8680f29651614e2694a452316e5b5c5d7f6dc50281b86ffc821015abd79ba49f78411a023f63645617491a241a1e1428140afd4d1')
b2sums=('5b3ee1227e734075a18dfbd841ed63dee14aeb5ee201e6f7e7e19844eb15930c0e4826f53e54db292422a70cfa724a2ece28e09b9b65b0872fe94f6b6d09b02c')

build() {
  cd "${_pyname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "${_pyname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
