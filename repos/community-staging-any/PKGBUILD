# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pyte
pkgver=0.8.1
pkgrel=2
pkgdesc='Simple VTXXX-compatible terminal emulator'
arch=('any')
license=('LGPL3')
url='https://github.com/selectel/pyte'
depends=('python-wcwidth')
makedepends=('python-setuptools' 'python-pytest-runner')
source=("https://github.com/selectel/pyte/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f49d9394cca92c27fdd5319bdce164d5cac63b77d13156de7105bee6087a615e844fafa8507089c8bf6cf31a796cc858a3ccf597aa23cfa595d0d9e0adfab5d4')

build() {
  cd pyte-$pkgver
  python setup.py build
}

check() {
  cd pyte-$pkgver
  python setup.py pytest
}

package() {
  cd pyte-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
