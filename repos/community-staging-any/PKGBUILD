# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgbase=python-traitlets
pkgname=(python-traitlets python2-traitlets)
pkgver=4.3.2
pkgrel=2
pkgdesc="A configuration system for Python applications."
arch=('any')
url="https://traitlets.readthedocs.io/en/stable/"
license=('BSD')
depends=('python-decorator')
makedepends=('python-setuptools' 'python2-setuptools')

source=("https://files.pythonhosted.org/packages/source/t/traitlets/traitlets-$pkgver.tar.gz"
        "https://pypi.python.org/packages/source/i/ipython_genutils/ipython_genutils-0.1.0.tar.gz")
md5sums=('3068663f2f38fd939a9eb3a500ccc154'
         '9a8afbe0978adbcbfcb3b35b2d015a56')


# ipython_genutils does not deserve a package
# "Pretend this doesnâ€™t exist. Nobody should use it."
# "Vestigial IPython utilities: DO NOT USE"
# (still required for now though, but drop at first chance)

prepare() {
  cd "$srcdir"
  cp -r traitlets-$pkgver      python2-traitlets-$pkgver
  cp -r ipython_genutils-0.1.0 ipython2_genutils-0.1.0
}

build() {
  cd "$srcdir"
}

package_python-traitlets() {
  # TEMPORARY!
  cd "$srcdir/ipython_genutils-0.1.0"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/traitlets-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-traitlets() {
  depends=('python2-decorator' 'python2-enum34')

  # TEMPORARY!
  cd "$srcdir/ipython2_genutils-0.1.0"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  cd "$srcdir/python2-traitlets-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

