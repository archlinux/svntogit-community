# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=strip-nondeterminism
pkgver=1.9.0
pkgrel=2
pkgdesc='Tool for stripping bits of non-deterministic information from files'
url='https://salsa.debian.org/reproducible-builds/strip-nondeterminism'
arch=('any')
license=('GPL3')
depends=("perl" 'perl-archive-zip' 'perl-archive-cpio' 'perl-sub-override')
makedepends=('git')
options=('!emptydirs')
source=(https://salsa.debian.org/reproducible-builds/strip-nondeterminism/-/archive/${pkgver}/strip-nondeterminism-${pkgver}.tar.bz2
        https://salsa.debian.org/reproducible-builds/reproducible-lfs/raw/master/releases/strip-nondeterminism/strip-nondeterminism-${pkgver}.tar.bz2.asc)
sha512sums=('e47188d2883531205d5fd5a6d3aca14093ad8f2ad8f70384fdc185804fcd2c27b7f2d0aa22e4e5afbf0c8870d2003f1dffe5cebed8faf6bfac0fa2ed7cb85ad4'
            'SKIP')
validpgpkeys=('C2FE4BD271C139B86C533E461E953E27D4311E58'  # Chris Lamb <lamby@debian.org>
              'EF5D84C1838F2EB6D8968C0410378EFC2080080C'  # Andrew Ayer <agwa@andrewayer.name>
              '2F5DAF3FC1F793D94F3D900CA721DA055374AA4F'  # Reiner Herrmann <reiner@reiner-h.de>
              '66AE2B4AFCCF3F52DA184D184B043FCDB9444540') # Mattia Rizzolo <mattia@debian.org>

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|bin/dh_strip_nondeterminism ||g' -i Makefile.PL
}

build() {
  cd ${pkgname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  make -C ${pkgname}-${pkgver} test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  # Don't install the internal handler documentation generated from pod documents
  # https://salsa.debian.org/reproducible-builds/strip-nondeterminism/-/commit/b9b84287c654bd14d6489282475868977dec50c6
  rm -r "${pkgdir}/usr/share/man/man3"
}

# vim: ts=2 sw=2 et:
