# Maintainer: Maxime Gauduin <alucryd@archinux.org>
# Contributor: xiretza <xiretza+aur@xiretza.xyz>

pkgname=python-cmake-build-extension
pkgver=0.5.1
pkgrel=2
pkgdesc='Setuptools extension to build and package CMake projects'
arch=(any)
url=https://github.com/diegoferigo/cmake-build-extension
license=(MIT)
depends=(
  cmake
  python
  python-gitpython
  ninja
)
makedepends=(
  git
  python-setuptools
)
_tag=2c3d822e40fe3fd2d769ad09dba367e6617dcc82
source=(git+https://github.com/diegoferigo/cmake-build-extension.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd cmake-build-extension
  git describe --tags | sed 's/^v//'
}

build() {
  cd cmake-build-extension
  python setup.py build
}

package() {
  cd cmake-build-extension
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-cmake-build-extension
}

# vim: ts=2 sw=2 et
