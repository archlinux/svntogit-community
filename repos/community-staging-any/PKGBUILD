# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgbase=python-pudb
pkgname=(python-pudb python2-pudb)
pkgver=2019.1
pkgrel=2
pkgdesc="A full-screen, console-based Python debugger"
url="https://pypi.python.org/pypi/pudb"
arch=('any')
license=('MIT')
makedepends=('python-urwid' 'python-pygments' 'python2-urwid' 'python2-pygments')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/inducer/pudb/archive/v$pkgver.tar.gz")
sha512sums=('9f9110965e7bfdce819cd73cfae1453022f29322b833751ce443bf92d9750e9a6ede291264cec691323374a5b3a4d311d9e11bd736a196f755050c730cf86dbd')

prepare() {
  cp -a pudb-$pkgver{,-py2}

  sed -i 's,^#! /usr/bin/env python$,#! /usr/bin/env python2,' pudb-$pkgver-py2/pudb/debugger.py
}

build() {
  cd "$srcdir"/pudb-$pkgver
  python setup.py build

  cd "$srcdir"/pudb-$pkgver-py2
  python2 setup.py build
}

check() {
  # TODO: make it work...

  cd "$srcdir"/pudb-$pkgver
  # python -m pytest --tb=native -rxs test/

  cd "$srcdir"/pudb-$pkgver-py2
  # python2 -m pytest --tb=native -rxs test/
}

package_python-pudb() {
  depends=('python-urwid' 'python-pygments')

  cd pudb-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pudb() {
  depends=('python2-urwid' 'python2-pygments')

  cd pudb-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: set ft=sh ts=2 sw=2 et:
