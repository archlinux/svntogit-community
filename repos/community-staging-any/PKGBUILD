# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsbeautifier
pkgname=('python-jsbeautifier' 'python2-jsbeautifier')
pkgver=1.7.5
pkgrel=2
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
makedepends=('python-setuptools' 'python2-setuptools' 'python-editorconfig' 'python2-editorconfig')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/beautify-web/js-beautify/archive/v$pkgver.tar.gz")
sha512sums=('873b84ee7d10d0db9e6de6e6ba170d7beebafcb16bca262b5ad6db644d6bfbac6c4ad7cd3b6774ad002224720d6a1e520f78c019a9c846cea5a3dab73e1a26b8')

prepare() {
  cp -a js-beautify-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/js-beautify-$pkgver/python
  python setup.py build

  cd "$srcdir"/js-beautify-$pkgver-py2/python
  python2 setup.py build
}

check() {
  cd "$srcdir"/js-beautify-$pkgver/python
  python setup.py test

  cd "$srcdir"/js-beautify-$pkgver-py2/python
  python2 setup.py test
}

package_python-jsbeautifier() {
  depends=('python-six' 'python-editorconfig')

  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jsbeautifier() {
  depends=('python2-six' 'python2-editorconfig')

  cd js-beautify-$pkgver-py2/python
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/js-beautify{,2}
}

# vim:set ts=2 sw=2 et:
