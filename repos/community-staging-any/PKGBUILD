# Maintainer: David Runge <dvzrv@archlinux.org>

_name=torrentool
pkgname=python-torrentool
pkgver=1.1.1
pkgrel=4
pkgdesc="The tool to work with torrent files"
arch=('any')
url="https://github.com/idlesign/torrentool"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-datafixtures'
'python-pytest-responsemock')
optdepends=('python-click: for torrentool')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7f884b5bf78d1b43954822574116323a2dddf35cb4cd8135f70aaf56091fa47f36e2cc9b15f8c9e95f8a4e15547956d9ff2efdb41c1270bfbb76b96e31d020ec')
b2sums=('d1e434fc99fedf71a7a14f60bacd573f42a4801909bee695a72300a0e95da8b32009e828cbdf312e0e62f03a7a11d183c9bd04e6158eb33dcac4886bab9d40ce')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGELOG,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
