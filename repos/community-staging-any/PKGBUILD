# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-test-base
pkgver=0.88
pkgrel=2
pkgdesc="A Data Driven Testing Framework"
arch=('any')
url="http://search.cpan.org/dist/Test-Base"
license=('GPL' 'PerlArtistic')
depends=('perl-spiffy>=0.30')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/I/IN/INGY/Test-Base-$pkgver.tar.gz)
md5sums=('ced0cd86b099f9fd2cd73df1f46ab5e9')

build() {
  cd  "$srcdir"/Test-Base-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  "$srcdir"/Test-Base-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
