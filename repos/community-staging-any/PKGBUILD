# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Amr Hassan <amr.hassan@gmail.com>

pkgbase=python-pylast
pkgname=('python-pylast' 'python2-pylast')
pkgver=1.6.0
pkgrel=2
pkgdesc='A Python interface to Last.fm and Libre.fm'
arch=('any')
url='https://github.com/pylast/pylast'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-pylast-${pkgver}.tar.gz::https://github.com/pylast/pylast/archive/${pkgver}.tar.gz")
sha256sums=('14e611d231b25a9b2bd1b59baf6fb248834a6377c8dc3162ecf0cb57206d1321')

prepare() {
  cp -r pylast-${pkgver} python2-pylast-${pkgver}
}

build() {
  cd pylast-${pkgver}

  python setup.py build

  cd ../python2-pylast-${pkgver}

  python2 setup.py build
}

package_python-pylast() {
  depends=('python-six')

  cd pylast-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-pylast() {
  depends=('python2-six')

  cd python2-pylast-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
