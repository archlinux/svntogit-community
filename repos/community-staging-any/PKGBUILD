# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rtree
pkgver=0.8.3
pkgrel=3
pkgdesc="Rtree: spatial index for Python GIS"
url="https://toblerity.github.com/rtree/"
license=('LGPL')
arch=('any')
depends=('spatialindex' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Toblerity/rtree/archive/$pkgver.tar.gz")
sha512sums=('4f6311ab5a24badb702fed6ff9dc7dc7f81caec0696ea0479e3620ba4be17c9ca6c1ecdd36ba4fd6c1f260f01795bb522bf10e1be9c132b767ed2bb0a4183566')

build() {
  cd rtree-$pkgver
  python setup.py build
}

check() {
  cd rtree-$pkgver
  python setup.py pytest
}

package() {
  cd rtree-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
