# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=('python-stevedore' 'python2-stevedore')
pkgver=1.28.0
pkgrel=2
pkgdesc="Manage dynamic plugins for Python applications"
arch=('any')
url="https://github.com/openstack/stevedore"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
options=('!emptydirs')
source=(https://pypi.org/packages/source/s/stevedore/stevedore-$pkgver.tar.gz)
sha256sums=('f1c7518e7b160336040fee272174f1f7b29a46febb3632502a8f2055f973d60b')

prepare() {
  cd "$srcdir/stevedore-$pkgver"

  # Remove dependency on pbr; it's not a runtime dependency
  sed -i '/pbr/d' requirements.txt
}

package_python-stevedore() {
  depends=('python-setuptools' 'python-six')

  cd "$srcdir/stevedore-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-stevedore() {
  depends=('python2-setuptools' 'python2-six')

  cd "$srcdir/stevedore-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
