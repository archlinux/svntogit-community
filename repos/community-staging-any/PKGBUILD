# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>

pkgname=patatt
pkgver=0.6.3
pkgrel=2
pkgdesc="A simple library to add cryptographic attestation to patches sent via email"
arch=("any")
depends=('python-pynacl')
makedepends=('python-setuptools')
url="https://git.kernel.org/pub/scm/utils/patatt/patatt.git/about/"
license=("MIT")
source=("https://git.kernel.org/pub/scm/utils/$pkgname/$pkgname.git/snapshot/$pkgname-$pkgver.tar."{gz,asc})
validpgpkeys=('DE0E66E32F1FDD0902666B96E63EDCA9329DD07E')
sha256sums=('d7cc136273541d659fa8f5a67e217ca5ad5685d7a180020f02e7e12029dcfd12'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -d "$pkgdir/usr/share/man/man5"
  install -m644 man/*.5 "$pkgdir/usr/share/man/man5"
}
