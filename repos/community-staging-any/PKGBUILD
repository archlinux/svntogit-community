# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=nox
pkgname=python-$_pkgname
pkgver=2022.8.7
pkgrel=2
pkgdesc='Flexible test automation for Python'
arch=('any')
url='https://github.com/wntrblm/nox'
license=('Apache')
depends=('python' 'python-argcomplete' 'python-colorlog' 'python-py' 'python-virtualenv' 'python-packaging')
optdepends=('python-jinja: tox_to_nox'
            'python-tox: tox_to_nox')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest-runner' 'python-jinja' 'python-tox')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('53a66964a99fff6f9f72ebe3dafc6fb52188f0af7f7c4fab1eaa5f35e8c641532477636303050d059825a505492e718cb915a2d6d9a9062bec4d6a0c85931205')

build() {
  cd $_pkgname-$pkgver

  python -m build -nw
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest
}

package() {
  cd $_pkgname-$pkgver

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
