# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailman-hyperkitty
pkgname=mailman3-hyperkitty
pkgver=1.2.1
pkgrel=2
pkgdesc="Mailman plugin to archive emails with HyperKitty"
arch=(any)
url="https://gitlab.com/mailman/mailman-hyperkitty"
license=(GPL3)
depends=(mailman3 python-requests python-zope-interface)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-nose2)
replaces=(python-mailman-hyperkitty-plugin)
backup=(etc/mailman-hyperkitty.cfg)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz{,.asc}
  $pkgname.tmpfiles
)
sha512sums=('6b7e93b3557d4feb7220459bb71319d82f1d38858903aca131b8b9c3fc8abbf08749730bf89f8f70ed517b8fb66173b1cb98b1b473d5e1b5f5c7984c157aa472'
            'SKIP'
            'bc97105a6208d951cbb884a18fea9f1aeb94cb1dab5966ef707c4af526d6bcf869c6c75321fa834f66ca20d315f73a071bb62aba9353c320d7ba84ba935cce6b')
b2sums=('c1a88cec713c6c0828ba9aee280f84155f138b838dbf3f02ea9369f67a91686b47198e40d572c39c843a2d5b34bc92378cafa05982e9c2a3b685c9cae61e17ea'
        'SKIP'
        '973b3ef36a3707fdefb36a94a7b5d4a2556f1dfd0d1c70fc648b90226203d33e488d8b79526b837a99ab0acbd4562894b8b62fe84021485180e03f9a874e66bf')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C')  # Abhilash Raj <raj.abhilash1@gmail.com>

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  nose2 -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 640 $_name.cfg -t "$pkgdir/etc/"
  install -vDm 644 ../$pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
