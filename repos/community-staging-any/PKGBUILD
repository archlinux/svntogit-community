# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgname=python-icalendar
pkgver=5.0.4
pkgrel=2
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=(any)
url="https://github.com/collective/icalendar"
license=(BSD)
depends=(python-dateutil python-pytz)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-hypothesis python-pytest)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('f0aa86d6f5bc110ed3b91e96c48c70351d7a09fbed25366f673dc0b799c83975')
b2sums=('5bb618f8abfa8c96cdf45b0fd85f97ecfbe99361fb2eda08fe6ba43da1d01642e116218bc01353a745f6dc401278887d5ffc616fcf666691360174459c896362')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v src/$_name/tests
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"

  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  ln -s "$site_packages/$_name-$pkgver.dist-info/LICENSE.rst" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"

  rm -frv "$pkgdir/$site_packages/$_name/tests/"
}
