# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-deferredimport
pkgname=('python-zope-deferredimport' 'python2-zope-deferredimport')
pkgver=4.3.1
pkgrel=3
pkgdesc="Allows you to perform imports names that will only be resolved when used in the code"
arch=('any')
url="https://github.com/zopefoundation/zope.deferredimport"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools'  'python-zope-proxy' 'python2-zope-proxy')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.deferredimport/archive/$pkgver.tar.gz")
sha512sums=('1c367def232ec79ad10a9d8f1e068598d2fdb49b067c2c3f44c9ad25c198afbbf2d52659c3795aad76f8c82d212e49086b81ab1c1dcc6aac2e3ce66c35ff5750')

prepare() {
  # Build in separated dir to avoid polluting .pyc files
  cp -a zope.deferredimport-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.deferredimport-$pkgver
  python setup.py build

  cd "$srcdir"/zope.deferredimport-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.deferredimport-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src

  cd "$srcdir"/zope.deferredimport-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-deferredimport() {
  depends=('python-setuptools' 'python-zope-proxy')

  cd zope.deferredimport-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-deferredimport() {
  depends=('python2-setuptools' 'python2-zope-proxy')

  cd zope.deferredimport-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
