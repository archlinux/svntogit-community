# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-grequests
_libname=grequests
pkgver=0.6.0
pkgrel=5
pkgdesc="Allows you to use Requests with Gevent to make asyncronous HTTP Requests easily."
url="https://pypi.python.org/pypi/grequests/"
license=('custom: ISC')
arch=('any')
depends=('python-requests' 'python-gevent')
makedepends=('python-setuptools')
source=(https://files.pythonhosted.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz
        $_libname-$pkgver-LICENSE::https://github.com/kennethreitz/grequests/raw/v$pkgver/LICENSE)
sha256sums=('7dec890c6668e6755a1ea968565535867956639301268394d24df67b478df666'
            '4063882a8913e4b37457d58fe6600bd975ba053c8db2e8c6b218e7c9c8575ca8')

build() {
    cd "$srcdir"/$_libname-$pkgver
    python setup.py build
}

package_python-grequests() {
    cd "$srcdir"/$_libname-$pkgver
    python setup.py install --skip-build -O1 --root="$pkgdir"
    install -m0644 -D "$srcdir"/$_libname-$pkgver-LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
