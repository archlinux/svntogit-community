# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-sekizai
pkgname=python-django-sekizai
pkgver=4.0.0
pkgrel=2
pkgdesc="Django Template Blocks with extra functionality"
arch=(any)
url="https://github.com/django-cms/django-sekizai"
license=(BSD)
depends=(python-django python-django-classy-tags)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('076895930de24bdba69e7720ca2f8dd78b07ec96bb7a66c78a21d89945711dd318644216ab7c1d75d753ee7161027fb5b3940d4f64994975b65b2960d837b7d7')
b2sums=('844461088e450b5be1ab5300ee7953bcc6f3692e3e5791be89cf25617d2b42f389cedc9face93df818a9fc6da8ad97e9b47258e95c44259d857a7e91c2b44618')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  # install to temporary location, as importlib is used
  python -m installer --destdir=test_dir dist/*.whl

  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  python tests/settings.py
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
