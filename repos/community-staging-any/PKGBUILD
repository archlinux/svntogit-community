# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Jason St. John <jstjohn .. purdue . edu>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname=perl-file-sharedir-install
pkgver=0.13
pkgrel=2
pkgdesc="File::ShareDir::Install - Install read-only data files from a distribution"
arch=('any')
url="http://search.cpan.org/dist/File-ShareDir-Install"
license=('GPL' 'PerlArtistic')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("http://cpan.org/modules/by-module/File/File-ShareDir-Install-${pkgver}.tar.gz")
sha256sums=('45befdf0d95cbefe7c25a1daf293d85f780d6d2576146546e6828aad26e580f9')

build() {
	cd "File-ShareDir-Install-${pkgver}"
	PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
	make
}

check() {
	cd "File-ShareDir-Install-${pkgver}"
	make test
}

package() {
	cd "File-ShareDir-Install-${pkgver}"
	make install DESTDIR="${pkgdir}"
}
