# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonschema-spec
pkgver=0.1.2
pkgrel=2
pkgdesc="JSONSchema Spec with object-oriented paths"
url="https://github.com/p1c2u/jsonschema-spec"
license=('Apache')
arch=('any')
depends=('python-jsonschema' 'python-pathable' 'python-yaml' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest')
source=("https://github.com/p1c2u/jsonschema-spec/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0cba83a14e88d4fe2fcce9e4d39fa4284f23366a1f8acab16fa4b7b824d0c02bf04429d61c51a39a7a188c4e2b79f4481bba14ca688edee71beb0060e16617e0')

prepare() {
  cd jsonschema-spec-$pkgver
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd jsonschema-spec-$pkgver
  python -m build -wn
}

check() {
  cd jsonschema-spec-$pkgver
  PYTHONPATH="$PWD" pytest
}

package() {
  cd jsonschema-spec-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
}
