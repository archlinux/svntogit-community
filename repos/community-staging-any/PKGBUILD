# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-term-progressbar
pkgver=2.22
pkgrel=2
pkgdesc="Perl/CPAN Module Term::ProgressBar : Progress bar in just ASCII  using Term"
arch=("any")
url="http://search.cpan.org/dist/Term-ProgressBar"
license=("GPL" "PerlArtistic")
depends=("perl-class-methodmaker" "perl-term-readkey")
source=("http://search.cpan.org/CPAN/authors/id/M/MA/MANWAR/Term-ProgressBar-$pkgver.tar.gz")
sha256sums=('2642ecca5b0b038c14812bcad3a9611ff7911dc59c9104d220797f837a880c49')

build() {
  cd "$srcdir"/Term-ProgressBar-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Term-ProgressBar-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
