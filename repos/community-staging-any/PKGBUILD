# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-entrypoints
_name=entrypoints
pkgver=0.4
pkgrel=3
pkgdesc="Discover and load entry points from installed packages."
arch=('any')
url="https://github.com/takluyver/entrypoints"
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-flit-core')
checkdepends=('python-pytest')
source=(https://github.com/takluyver/entrypoints/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d2197a94aa73e70a7f60600e654ac9918ca2a0ee5480eb875296c42220a02272')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
