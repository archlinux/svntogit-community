# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>
# Contributor: Francois Charette <firmicus@gmx.net>

pkgname=perl-mail-box
pkgver=3.002
pkgrel=3
pkgdesc="Mail folder manager and MUA backend"
arch=(any)
url="http://search.cpan.org/dist/Mail-Box"
license=('GPL' 'PerlArtistic')
depends=('perl-timedate' 'perl-digest-hmac' 'perl-file-remove>=0.20'
	 'perl-io-stringy' 'perl-mime-types>=1.004' 'perl-mailtools'
	 'perl-object-realize-later>=0.14' 'perl-uri>=1.23'
	 'perl-devel-globaldestruction' 'perl-user-identity>=0.90'
	 'perl-mail-message')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/M/MA/MARKOV/Mail-Box-$pkgver.tar.gz)
sha256sums=('4410ac0f1122bdd3ed2d7aeee44929506a695a3793b8afd00d514beba62033ad')

build() {
  cd  "$srcdir"/Mail-Box-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "$srcdir"/Mail-Box-$pkgver

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd  "$srcdir"/Mail-Box-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
