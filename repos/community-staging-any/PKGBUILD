# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=pastel
pkgname=python-pastel
pkgver=0.2.1
pkgrel=3
pkgdesc="Bring colors to your terminal"
arch=('any')
url="https://github.com/sdispater/${_pkgname}"
license=('MIT')
depends=('python')
makedepends=('python-dephell' 'python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('e6581ac04e973cac858828c6202c1e1e81fee1dc7de7683f3e1ffe0bfd8a573d')
b2sums=('064d30404600e3ad6896deb397f05f597faf550d595f7bf6f38e0e3465681ad4bf7601a85bd1e044a889fc1d32e56475a1bb16e5e81380b89238c3b70d8ad7dd')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # poetry does not generate correct setup.py, nuke it and recreate from scratch
    dephell deps convert --from pyproject.toml --to setup.py
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
