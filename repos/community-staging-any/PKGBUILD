# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Rafael G. Martins <rafael@rafaelmartins.com>
# Contributor: Iván Pulido <ijpulidos@riseup.net>

pkgname=python-mpmath
pkgver=1.3.0
pkgrel=2
pkgdesc='Python library for arbitrary-precision floating-point arithmetic.'
arch=(any)
url='http://mpmath.org/'
license=(BSD)
depends=(python-gmpy2)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://pypi.python.org/packages/source/m/mpmath/mpmath-$pkgver.tar.gz)
sha256sums=('7a28eb2a9774d00c7bc92411c19a89209d5da7c4c9a9e227be8330a23a25b91f')

build() {
  cd mpmath-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd mpmath-$pkgver  
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
