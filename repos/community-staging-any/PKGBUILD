# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

_gemname=rspec
pkgname=ruby-${_gemname}
pkgver=3.4.0
pkgrel=2
pkgdesc='RSpec meta-gem that depends on the other components'
url='https://github.com/rspec/rspec'
arch=('any')
license=('MIT')
depends=('ruby-rspec-core' 'ruby-rspec-expectations' 'ruby-rspec-mocks')
options=('!emptydirs')
source=(https://rubygems.org/downloads/${_gemname}-${pkgver}.gem)
noextract=(${_gemname}-${pkgver}.gem)
sha512sums=('b73040fd946931d5dcdf6d48456fda50f614ecab5853308b7c02432614c080cde05bc0eeeb0302f81dae3f25767eb2d4529ae0ae48c824ef077d299e5fea25de')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
