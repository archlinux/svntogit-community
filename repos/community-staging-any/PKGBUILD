# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cycler
pkgname=('python-cycler' 'python2-cycler')
pkgver=0.10.0
pkgrel=3
pkgdesc="Composable style cycles"
arch=('any')
license=('BSD')
url="http://github.com/matplotlib/cycler"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-coverage' 'python2-coverage')
source=("git+https://github.com/matplotlib/cycler.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a cycler{,-py2}
}

build() {
  cd "$srcdir/cycler"
  python setup.py build

  cd "$srcdir/cycler-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/cycler"
  python run_tests.py

  cd "$srcdir/cycler-py2"
  python2 run_tests.py
}

package_python-cycler() {
  depends=('python-six')

  cd cycler
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cycler() {
  depends=('python2-six')

  cd cycler-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
