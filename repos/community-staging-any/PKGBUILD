# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-deprecation
pkgname=('python-zope-deprecation' 'python2-zope-deprecation')
pkgver=4.4.0
pkgrel=3
pkgdesc="Zope Deprecation Infrastructure"
arch=('any')
url="https://github.com/zopefoundation/zope.deprecation"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.deprecation/archive/$pkgver.tar.gz")
sha512sums=('d8632a3442eee059383014bb3341b30897ad8f9206b71bc586c873c4cccf5f87296edaf18f1e1c93878f480e3b0e8e1c1936ac19ed327f4ab227402ebbc73e0e')

prepare() {
  cp -a zope.deprecation-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.deprecation-$pkgver
  python setup.py build

  cd "$srcdir"/zope.deprecation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.deprecation-$pkgver
  nosetests3

  cd "$srcdir"/zope.deprecation-$pkgver-py2
  nosetests2
}

package_python-zope-deprecation() {
  depends=('python-setuptools')

  cd zope.deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-deprecation() {
  depends=('python2-setuptools')

  cd zope.deprecation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
