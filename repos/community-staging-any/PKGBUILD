# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: nblock <nblock [/at\] archlinux DOT us>
# Contributor: neithere <neithere at gmail>
# Contributor: mitsuse <mitsuse at gmail>

_pyname=wtforms
pkgname=python-wtforms
pkgver=3.0.1
pkgrel=2
pkgdesc='Flexible forms validation and rendering library for python web development'
url='https://wtforms.readthedocs.io/'
arch=('any')
license=('BSD')
depends=('python' 'python-markupsafe')
makedepends=('python-setuptools' 'python-babel')
checkdepends=('python-pytest' 'python-email-validator')
optdepends=('python-email-validator: Email validation')
source=(https://github.com/wtforms/wtforms/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3e8b78026e3f0252113951aca20699477a753bdc0b0f7f01b934ddc8281ee89fc7a559ee519bc8501ff61dc7984b7ec3248d63ea57dd1c1b2c71d845a4479a49')
b2sums=('57bd60cad3955a6097108c6f5bd151ee044812bd3830213a4c8470a168ef27d42336cb75341bdfa8a8b779faab318585f0760bd652115480b8441bf11c07c912')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py compile_catalog build
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH="build/lib" pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  find "$pkgdir" -type f -name \*.po -delete
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 CHANGES.rst README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
