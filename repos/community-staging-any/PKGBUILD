# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-libevdev
pkgver=0.8
pkgrel=2
arch=('any')
url='https://gitlab.freedesktop.org/libevdev/python-libevdev'
license=('MIT')
pkgdesc='Python wrapper for libevdev'
depends=('python' 'libevdev')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('python-pytest' 'python-py' 'python-pluggy')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('dbade600f2f0887c6702e5569b5b8132d636411c589395bb9770efd261c5a44479d43faecb683df45c03233e715c713daa9e70400c03ba4e7e1e2ada11128d61')

build() {
  cd $pkgname-$pkgver

  python setup.py build

  make doc
}

check() {
  cd $pkgname-$pkgver

  PYTHONPATH=. pytest test/*.py
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root "$pkgdir" --skip-build -O1

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$pkgname
}

