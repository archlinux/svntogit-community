# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-astor
pkgname=('python-astor' 'python2-astor')
pkgver=0.6.2
pkgrel=2
pkgdesc="Read/rewrite/write Python ASTs"
arch=('any')
license=('BSD')
url="http://astor.rtfd.org/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python2-unittest2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/berkerpeksag/astor/archive/$pkgver.tar.gz"
        do-not-use-async-as-a-keyword-argument.patch)
sha512sums=('ccda3f0432522be1f8ada5589e56a7cb6da83a29fb8d9e0f4167fd492d7d6f55c0f4ed18af81d60340147894978b6ee8efb24edb68b92cdd78d053d5d53c6d66'
            'ebe7126a25694b3b7df65fa5311632cec973802dd38a6d7a57c415f4b3925a1177c4d3f702e1d49b4c203296ff2fd09b4992a36c26084bb1939cd4593d903a20')

prepare() {
  # https://github.com/berkerpeksag/astor/issues/86
  patch -d astor-$pkgver -Np1 < do-not-use-async-as-a-keyword-argument.patch
  cp -a astor-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/astor-$pkgver
  python setup.py build

  cd "$srcdir"/astor-$pkgver-py2
  python2 setup.py build
}

check() {
  # https://github.com/berkerpeksag/astor/issues/89
  cd "$srcdir"/astor-$pkgver
  nosetests3 || warning "Tests failed"

  cd "$srcdir"/astor-$pkgver-py2
  nosetests2
}

package_python-astor() {
  depends=('python')

  cd astor-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-astor() {
  depends=('python2')

  cd astor-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
