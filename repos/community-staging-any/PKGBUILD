# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Dmitry Kharitonov <darksab0r@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: eagletmt <eagletmt@gmail.com>

_gemname=mini_portile2
pkgname=ruby-mini_portile2
pkgver=2.5.0
pkgrel=2
pkgdesc='Simple autoconf builder for developers'
url='https://github.com/flavorjones/mini_portile'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/flavorjones/mini_portile/archive/v${pkgver}.tar.gz)
sha256sums=('3f644e67e529c9a2f0af98cded2a511766e402e3e506455a922adc121383f2bf')
sha512sums=('4e07a2a510b189468967212cdd3307ddc97322849834a4e7d099d3866fd259e27519e923367865138f121b3401fd847e0fed1468dbe7270eb35e4bcdb86339ff')

prepare() {
  cd mini_portile-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0|' -i ${_gemname}.gemspec
}

build() {
  cd mini_portile-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd mini_portile-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
