# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: John Gerritse <tyrannis dot hawk at gmail dot com>

_name=pyspellchecker
pkgname=python-pyspellchecker
pkgver=0.7.1
pkgrel=2
pkgdesc='Pure python spell checker based on work by Peter Norvig'
arch=('any')
url=https://github.com/barrust/pyspellchecker
license=('MIT')
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('ed46a7218a363ef1a348fac14c9fef95b0aca5daa7744389d70843fcc0961b31')
b2sums=('fc15792eab6234c2574e7c0c9b8767df64bd87884ca1c7140b5b947f5315312d707381fa54f432a811638ad883295725cb05dd0f8a24aeb4d82508541c9316ec')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
