# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

_pkgname=mechanize
pkgbase=python-mechanize
pkgname=(python-mechanize python2-mechanize)
pkgver=0.4.3
pkgrel=2
epoch=1
pkgdesc='Stateful programmatic web browsing in Python'
arch=('any')
url="https://github.com/python-mechanize/mechanize"
license=('BSD')
makedepends=('python-setuptools' 'python-html5lib'
              'python2-setuptools' 'python2-html5lib')
checkdepends=('python-zope-interface' 'python-twisted'
              'python2-zope-interface' 'python2-twisted')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('d7d7068be5e1b3069575c98c870aaa96dd26603fe8c8697b470e2f65259fddbf'
            'SKIP')
b2sums=('fac712bb2cfe55270a2a8830c084e7d6e5e8fa279ce0eca46af6860b12a61863e5668ee1c92a129d4f28858eb485428ffdea59d1091da29871ce592753ec9a3a'
        'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
  cd "mechanize-$pkgver"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "mechanize-$pkgver"

  python setup.py test
  python2 setup.py test
}

package_python-mechanize() {
  depends=('python-html5lib')
  optdepends=('python-html5-parser: faster parsing of HTML')
  cd "mechanize-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-mechanize() {
  depends=('python2-html5lib')
  optdepends=('python2-html5-parser: faster parsing of HTML')
  cd "mechanize-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
