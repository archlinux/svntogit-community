# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_name=bowler
pkgname=python-bowler
pkgver=0.9.0
pkgrel=7
pkgdesc="Safe code refactoring for modern Python."
arch=(any)
url="https://github.com/facebookincubator/${_name}"
license=(MIT)
depends=(
  ipython
  python
  python-attrs
  python-click
  python-fissix
  python-moreorless
  python-volatile
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
source=(https://files.pythonhosted.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('cdb85ce2e7bd545802a15d755d1daf2b6a125429355c50d2019a9f35d63e45db')
b2sums=('4e4a1b39e23cb76ad74edcd371b3bb1e4b52aae2342d026bdf07cb3f1598b62092e946ac5f8ddec7a882ff918ea44be12dc06646a775a557df260622910ca593')

build(){
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m $_name.tests
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
