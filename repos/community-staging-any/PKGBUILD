# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Canto
pkgname=canto-daemon
pkgver=0.9.8
pkgrel=4
pkgdesc="Next-gen console RSS/Atom reader"
arch=('any')
url="https://github.com/themoken/canto-next"
license=('GPL2')
depends=('python-feedparser')
makedepends=('python-setuptools')
optdepends=('python-requests: for inoreader plugin')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('cc052a4774d9cb65d78cf845d581e491ef4e035dc5b267b3843edb3b400c04297d418a00e8685fcd6faed4b8d3ca5dfc844717134045f643bf149d93d22a1374')
b2sums=('79f8a2d1ad4f9095c753660c2ef03db798f088a2eeb5b1cc105b82ed684afee7f2bb72bc010587031d633f6b6c9d4f5f6e49368f0e1e536e3edef44d761d7602')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
}
