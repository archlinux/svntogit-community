# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-seedir
pkgver=0.4.2
_commit=00e34d515fa4590214a2988c129e8fa3ac1e501f
pkgrel=3
pkgdesc="A Python package for creating, editing, and reading folder tree diagrams"
url="https://github.com/earnestt1234/seedir"
license=('MIT')
arch=('any')
depends=('python-natsort')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
optdepends=('python-emoji: for emoji style')
checkdepends=('python-emoji')
source=("git+https://github.com/earnestt1234/seedir.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd seedir
  python -m build -nw
}

check() {
  cd seedir
  python -m tests.tests
}

package() {
  cd seedir
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
