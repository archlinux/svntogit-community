# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-nav
pkgver=0.6
pkgrel=7
pkgdesc="Easily create navigation for Flask applications"
url="https://github.com/mbr/flask-nav"
license=('MIT')
arch=('any')
depends=('python-flask' 'python-visitor' 'python-dominate')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mbr/flask-nav/archive/$pkgver.tar.gz"
        python310.patch)
sha512sums=('a7930687425a53fac9ad9a7cb65c2e2a759a0c9660189965bf37ea6e9925dd869074d37a283801631b3e6c3391f9f4167e0a36294104f72970e876b5a7a0930c'
            '27841a43aa185e0cd7ddd49bdef7f7d1884f3b8068dcb59e7902d16f02d4920b05e38a83f6873c8d735bc8884c7e02f5cb29c3368474ccd289e9e52686b491e1')

prepare() {
  cd flask-nav-$pkgver
  patch -Np1 -i ../python310.patch
}

build() {
  cd flask-nav-$pkgver
  python setup.py build
}

check() {
  cd flask-nav-$pkgver
  python setup.py pytest
}

package() {
  cd flask-nav-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
