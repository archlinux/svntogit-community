# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
pkgver=0.13.1
pkgrel=1
pkgdesc="All the stuff that isn't good/big enough for a real library"
arch=('any')
url='https://github.com/flori/tins'
license=('MIT')
depends=('ruby')
source=("https://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('c53bf237462be8d61cfa2a5f4b48f372d010825b0dc594c952d9857fe4c3ef30')

prepare() {
  gem install --no-{document,user-install} --ignore-dependencies -i . ${pkgname#*-}-${pkgver}.gem
}

package() {
  cd gems/${pkgname#*-}-${pkgver}

  local _rubyver="$(ruby --version | sed 's/.* \(.*\..*\..*\)p.*/\1/')"

  install -dm 755 "${pkgdir}"/usr/lib/ruby/{gems/${_rubyver},vendor_ruby}
  mv lib "${pkgdir}"/usr/lib/ruby/vendor_ruby/${_rubyver}

# Gem compatibility
  mv ../../specifications "${pkgdir}"/usr/lib/ruby/gems/${_rubyver}/

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 {,"${pkgdir}"/usr/share/licenses/${pkgname}/}COPYING
}

# vim: ts=2 sw=2 et:
