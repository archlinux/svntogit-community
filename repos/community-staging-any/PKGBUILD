# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=2.0.0
pkgrel=2
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-argcomplete' 'python-colorama' 'python-termcolor')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/tldr-pages/tldr-python-client/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('02be3dad8aeeb7153438f1ecd7e218ec4c66fece3265935d6f0a06857e10eb8d0b34306294f1888f5109b713306b219449fd91c6681ec3c955c9a59d0bf49abd')

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python -m pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
