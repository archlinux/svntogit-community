# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgname=python-faker
pkgver=4.14.2
pkgrel=3
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='https://faker.readthedocs.io/en/master/'
license=('MIT')
provides=("python-fake-factory=$pkgver")
conflicts=('python-fake-factory')
replaces=('python-fake-factory')
depends=('python-setuptools' 'python-dateutil' 'python-text-unidecode')
makedepends=('python-pip' 'python-wheel' 'python-pytest-runner')
checkdepends=('python-validators' 'python-ukpostcodeparser' 'python-random2'
              'python-freezegun')
source=("$pkgname-$pkgver.tar.gz::https://github.com/joke2k/faker/archive/v$pkgver.tar.gz")
sha512sums=('d425c2995fd48c4d07bf89cb7bf35372494cd7beb183fcde4eab4e96686dd87aca84460042d1cc0d7022902e1aae366c3dd4b8afd88fa97cb2275603d0bb3ff6')

prepare() {
  sed -i -e 's/==/>=/' faker-$pkgver/setup.py
}

build() {
  cd faker-$pkgver
  python setup.py build
}

check() {
  cd faker-$pkgver
  python setup.py egg_info
  python -m pytest
}

package() {
  cd faker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}
