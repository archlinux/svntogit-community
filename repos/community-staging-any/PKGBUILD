# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgname=python-zope-component
pkgver=5.1.0
pkgrel=4
pkgdesc="Represents the core of the Zope Component Architecture"
arch=('any')
url="https://github.com/zopefoundation/zope.component"
license=('ZPL')
depends=('python-setuptools' 'python-zope-deferredimport' 'python-zope-deprecation'
         'python-zope-event' 'python-zope-hookable' 'python-zope-interface')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-zope-testrunner' 'python-persistent' 'python-zope-location'
              'python-zope-proxy' 'python-zope-security' 'python-zope-configuration' 'python-zope-i18nmessageid')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.component/archive/$pkgver.tar.gz")
sha512sums=('6fac757f13dbffc6d1b8aa7bef7d196b59d70f7aa78a4a5ed7260179e263819e6bb9b82cb9cf7e31ab3d33c7d49fdb32ecbbcfe2a76696e7ebbd8c1dbb38b987')

build() {
  cd zope.component-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd zope.component-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.component-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set ts=2 sw=2 et:
