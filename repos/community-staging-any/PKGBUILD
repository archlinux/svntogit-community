# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-term-table
pkgver=0.008
pkgrel=3
pkgdesc="Format a header and rows into a table"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Term-Table"
# Specify version bounds to pick up perl-test-simple in [community], since perl includes an old one.
depends=('perl-importer' 'perl-test-simple>=1.300000')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Term-Table-$pkgver.tar.gz")
sha512sums=('d741313933c8925ca3451056d05172c440ab4a46f5a4cbaeae3c6d4b82d29314e69643e8c3e60dced42768f2102277461b2d2dc4bf945d982df9265f49339d06')

build() {
  cd Term-Table-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Term-Table-$pkgver
  make test
}

package() {
  cd Term-Table-$pkgver
  make DESTDIR="$pkgdir" install
}
