# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alexander Fasching <fasching.a91@gmail.com>
# Contributor: Johannes Loethberg <johannes@kyriasis.com>

_pyname=geojson
pkgname="python-$_pyname"
pkgver=2.5.0
pkgrel=5
pkgdesc='Python bindings and utilities for GeoJSON'
url="https://github.com/frewsxcv/$pkgname"
arch=(any)
license=(BSD)
depends=(python)
makedepends=(python-{build,installer,wheel}
             python-setuptools)
_archive="$_pyname-$pkgver"
source=("$_archive.tgz::$url/archive/$pkgver.tar.gz"
        0001-Remove-encoding-parameter-from-json.load-for-Python-.patch)
sha256sums=('e70c9af97cf01abf88e350f08bb2b42a3dd0bfdb9dddb26d80155ecf2065215c'
            'cf701e826a0cda61484ebc1ac06e4aeb0f9af2ec718d284d481f9870c7d1452b')

prepare() {
	cd "$_archive"
	patch -p1 < ../0001-Remove-encoding-parameter-from-json.load-for-Python-.patch
}

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	python -m unittest discover
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE.rst
}
