# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-par
pkgver=1.017
pkgrel=3
pkgdesc='Perl Archive Toolkit'
arch=('any')
url='https://metacpan.org/release/PAR'
license=('GPL')
depends=('perl-archive-zip' 'perl-par-dist')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/R/RS/RSCHUPP/PAR-${pkgver}.tar.gz")
b2sums=('99acabab41bff84b9445c38dbceffe5731a20afb16cecef20a6d6fa48babfebdba42d675fd813c91cde59b4b932fff00571957bcebb1001c194e90dbc59648ea')

build() {
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}
check(){
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}
package() {
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
}
