# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Pieter Goetschalckx <3.14.e.ter <at> gmail <dot> com>

_name=flake8-docstrings
pkgname=python-flake8-docstrings
pkgver=1.6.0
pkgrel=4
pkgdesc='Plugin to Flake8 to include checks provided by pydocstyle'
arch=('any')
url=https://gitlab.com/pycqa/flake8-docstrings
license=('MIT')
depends=('flake8' 'python-pydocstyle')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('9fe7c6a306064af8e62a055c2f61e9eb1da55f84bb39caef2b84ce53708ac34b')
b2sums=('ee147e27f226675a2a59375b9e7812d111a34bef1bc35843096f8e7ca9cd94ac805ca49a8d83d1f6d7a78dbf6338a0a1270d668b51775a2b91ad7c3172ad41da')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/flake8_docstrings-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
