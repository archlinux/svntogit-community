# $Id: PKGBUILD 134777 2015-06-04 13:33:01Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@osvaldobarrera.com.ar>

pkgbase=python-ndg-httpsclient
pkgname=(python-ndg-httpsclient python2-ndg-httpsclient)
_pkgname=ndg_httpsclient
pkgver=0.5.1
pkgrel=2
pkgdesc="Provides enhanced HTTPS support for httplib and urllib2 using PyOpenSSL"
arch=(any)
url="https://pypi.python.org/pypi/ndg-httpsclient"
license=('BSD')
makedepends=(python2-setuptools python2-pyopenssl python2-pyasn1
             python-setuptools python-pyopenssl python-pyasn1)
options=(!emptydirs)
source=(https://files.pythonhosted.org/packages/source/n/ndg-httpsclient/$_pkgname-$pkgver.tar.gz)
sha256sums=('d72faed0376ab039736c2ba12e30695e2788c4aa569c9c3e3d72131de2592210')

build() {
  cd "$srcdir"/$_pkgname-$pkgver

  rm -rf ../buildpy3; mkdir ../buildpy3
  python setup.py build -b ../buildpy3

  rm -rf ../buildpy2; mkdir ../buildpy2
  python2 setup.py build -b ../buildpy2
}

package_python-ndg-httpsclient() {
  depends=(python-pyopenssl python-pyasn1)
  _site_packages=$(python -sSc 'import site; print(site.getsitepackages()[0])')

  cd "$srcdir"/$_pkgname-$pkgver
  rm -rf build; ln -s ../buildpy3 build
  python setup.py install --skip-build -O1 --root="$pkgdir"
  install -Dm0644 "$pkgdir$_site_packages"/ndg/httpsclient/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-ndg-httpsclient() {
  depends=(python2-pyopenssl python2-pyasn1)
  _site_packages=$(python2 -sSc 'import site; print(site.getsitepackages()[0])')

  cd "$srcdir"/$_pkgname-$pkgver
  rm -rf build; ln -s ../buildpy2 build
  python2 setup.py install --skip-build -O1 --root="$pkgdir"
  install -Dm0644 "$pkgdir$_site_packages"/ndg/httpsclient/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  mv "$pkgdir"/usr/bin/ndg_httpclient{,2}
}
