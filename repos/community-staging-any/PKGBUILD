# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alex Talker < alextalker at openmailbox dot org >

pkgname=pulsemixer
pkgver=1.5.1
pkgrel=4
pkgdesc='CLI and curses mixer for pulseaudio'
url='https://github.com/GeorgeFilipkin/pulsemixer'
arch=('any')
license=('MIT')
depends=('python' 'pulseaudio')
makedepends=('python-setuptools')
source=(https://github.com/GeorgeFilipkin/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c07b5e5fffa71ca60ed41e4c28a0f4ae5c8b5df9f269ea7376ed835b35536300f34504da724899bab5277f0c39a4c345654b99930ec8bdf4b43374d9b6d331be')
b2sums=('4a20be7b6499501adc64f5ac33b30ad1ea9418190104fbf452e1ed2bd3585552033c042554948b78b41e6a48c22ac62a42f4117b0311300bda738f7556ef1dc3')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
