# $Id$
# Maintainer : Felix Yan <felixonmars@gmail.com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Arnau Sanchez <tokland {at} gmail {dot} com>

pkgbase=python-pyquery
pkgname=('python-pyquery' 'python2-pyquery')
pkgver=1.2.17
pkgrel=2
pkgdesc="A jquery-like library for python."
arch=('any')
url="https://github.com/gawel/pyquery/"
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-lxml' 'python2-lxml' 'python-cssselect' 'python2-cssselect' 'git')
checkdepends=('python-requests' 'python2-requests' 'python-webob' 'python2-webob' 'python-nose' 'python2-nose'
              'python-coverage' 'python2-coverage' 'python-webtest' 'python2-webtest' 'python2-beautifulsoup4'
              'python2-restkit')
source=("git+https://github.com/gawel/pyquery.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pyquery{,-py2}
}

build() {
  cd "$srcdir"/pyquery
  python setup.py build

  cd "$srcdir"/pyquery-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyquery
  nosetests3

  cd "$srcdir"/pyquery-py2
  nosetests2
}

package_python-pyquery() {
  depends=('python-lxml' 'python-cssselect')
  optdepends=('python-webob: query wsgi app')

  cd pyquery
  python setup.py install -O1 --root="$pkgdir/"

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyquery() {
  depends=('python2-lxml' 'python2-cssselect')
  optdepends=('python2-webob: to query wsgi app'
              'python2-restkit: to get url directly with a HostProxy app')

  cd pyquery-py2
  python2 setup.py install -O1 --root="$pkgdir/"

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
