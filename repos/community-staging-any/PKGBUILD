# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-xml-libxml-prettyprint
pkgver=0.006
pkgrel=3
pkgdesc='Add pleasant whitespace to a DOM tree'
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl' 'perl-exporter-tiny' 'perl-xml-libxml')
checkdepends=('perl-test-warnings')
url=https://metacpan.org/release/XML-LibXML-PrettyPrint
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TOBYINK/XML-LibXML-PrettyPrint-$pkgver.tar.gz")
sha512sums=('d00259555cb2cc4bef8e20216798a9f800565cc831098320ffb0b47dc7fbd84d89002ec8b9c6df3808e5c5ddf2ccb2f26a1f3839c57224bc2cccfd0725ad3fcd')
_ddir="XML-LibXML-PrettyPrint-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
