# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
pkgver=0.10.4
pkgrel=2
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-async_generator)
checkdepends=(python-flask python-moto python-pytest python-pytest-asyncio)
source=("https://github.com/aio-libs/aiobotocore/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        moto-1.3.8.patch
        fix-tests-with-proxies.patch)
sha256sums=('2e8fe12f66f028641d5727983bd211598e71b983d601a4d2618915ed8c7c305d'
            '7953b9cf8b4256cf0020098a978ef5c48097dfbaafe1c2cbf22d590ed1b07e5c'
            '248aee05a468848dda2c40185f49714ebe8b789913dd7bb49018d69f1f3606f7')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../moto-1.3.8.patch
  patch -Np1 -i ../fix-tests-with-proxies.patch

  # Disable dependency pinning
  # Upstream tracking issue: https://github.com/aio-libs/aiobotocore/issues/670
  rm -v tests/test_patches.py
  sed -i -r "s#'(botocore.*),<.*',#'\1',#" setup.py
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH=build/lib pytest -v -m moto tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
