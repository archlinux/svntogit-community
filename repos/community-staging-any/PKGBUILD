# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=clikit
pkgname=python-clikit
pkgver=0.6.2
pkgrel=9
pkgdesc='clikit allows you to create beautiful and testable command-line interfaces'
arch=(any)
url="https://github.com/sdispater/${_pkgname}"
license=(MIT)
depends=(python-crashtest
         python-pastel
         python-pylev
         python-typing_extensions)
makedepends=(python-{build,installer,wheel}
             python-poetry-core)
checkdepends=(python-pytest-mock)
_archive="$_pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('4bb1d934e0c0a93f25429919242d11d60dcd1d9c65ac096033092d7973cbfc6e')
b2sums=('b72f245128c698375f97139cf245bdc8c017e3e2d142f6e3183a42620b23a024531e5b14f05163c6defa9ee54c52c19a64932c62484f2ec8da5c75ea1b7d3d27')

prepare() {
	cd "$_archive"
	# Unpin crashtest which we have packaged at 0.4.0
	# https://bugs.archlinux.org/task/75733
	sed -i -e '/^crashtest/s/\^/>=/' pyproject.toml
}

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	PYTHONPATH=$PWD/src python -m pytest
}

package() {
	cd "$_archive"
	python -m installer -d "${pkgdir}" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
