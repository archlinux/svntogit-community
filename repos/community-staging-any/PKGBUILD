# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgname=python-munkres
pkgver=1.1.4
pkgrel=4
pkgdesc='Munkres algorithm for Python'
arch=(any)
url=https://github.com/bmc/munkres
license=(Apache)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=ac8af9e3b6093e4be8e3935829bc7d7fc45a84d2
source=(git+https://github.com/bmc/munkres.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd munkres

  git describe --tags | sed 's/^release-//'
}

build() {
  cd munkres

  python setup.py build
}

package() {
  cd munkres

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
