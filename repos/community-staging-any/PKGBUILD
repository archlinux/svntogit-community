# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-copy
pkgname=('python-zope-copy' 'python2-zope-copy')
pkgver=4.1.0
pkgrel=2
pkgdesc="Pluggable object copying mechanism"
arch=('any')
url="https://github.com/zopefoundation/zope.copy"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner' 'python-zope-testing'
              'python2-zope-testing' 'python-zope-location' 'python2-zope-location')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.copy/archive/$pkgver.tar.gz")
sha512sums=('5fb91b71262709e6f94376b295d8f79cdf7909c5b34145889daee22224c3405dcfe1660362612d8651903c44aff2a120871184922296cb4a40a0c4bca5562421')

prepare() {
  cp -a zope.copy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.copy-$pkgver
  python setup.py build

  cd "$srcdir"/zope.copy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.copy-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.copy-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-copy() {
  depends=('python-setuptools' 'python-zope-interface')

  cd "$srcdir"/zope.copy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-copy() {
  depends=('python2-setuptools' 'python2-zope-interface')

  cd "$srcdir"/zope.copy-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
