# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Danilo Bargen <gezuru@gmail.com>

pkgname=vim-jedi
_reponame=jedi-vim
pkgver=0.11.1
pkgrel=2
pkgdesc='Vim plugin for jedi, an awesome Python autocompletion'
url='https://github.com/davidhalter/jedi-vim'
arch=('any')
license=('MIT')
depends=('vim' 'python-jedi' 'python')
optdepends=('vim-supertab: tab completion'
            'python2-jedi: Python 2 support')
checkdepends=('python-pytest')
groups=('vim-plugins')
options=('!emptydirs')
source=(https://github.com/davidhalter/${_reponame}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b20e7ffadf73c929e250ea9de6d4752b4bb3fbcd58c2a82b3677d4fd4d1e3713')
b2sums=('7a069188c6c2886d483ecf98e970fe84be9edf88a4a666d6766c663efdd9febb39c6af058a2ef9e42f4c878f964b8b1a645c31f31b16e1904287e148e9c4f916')

check() {
  cd ${_reponame}-${pkgver}
  py.test
}

package() {
  cd ${_reponame}-${pkgver}

  install -d "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership pythonx "${pkgdir}/usr/share/vim/vimfiles"
  cp -dpr --no-preserve=ownership doc "${pkgdir}/usr/share/vim/vimfiles/doc"
  cp -dpr --no-preserve=ownership after "${pkgdir}/usr/share/vim/vimfiles/after"
  cp -dpr --no-preserve=ownership autoload "${pkgdir}/usr/share/vim/vimfiles/autoload"
  cp -dpr --no-preserve=ownership ftplugin "${pkgdir}/usr/share/vim/vimfiles/ftplugin"
  cp -dpr --no-preserve=ownership plugin "${pkgdir}/usr/share/vim/vimfiles/plugin"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
