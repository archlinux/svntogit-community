# Maintainer: David Runge <eolianoe At GoogleMAIL DoT com>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=python-atomicwrites
pkgbase=python-atomicwrites
pkgname=('python-atomicwrites' 'python2-atomicwrites')
pkgver=1.1.5
pkgrel=5
pkgdesc="Atomic file writes on POSIX"
arch=('any')
url="https://github.com/untitaker/python-atomicwrites"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=(${pkgbase}-${pkgver}.tar.gz::"https://github.com/untitaker/${pkgbase}/archive/${pkgver}.tar.gz")
sha512sums=('8fba7bf6270c2a5d2925c14c4620e3b391fe333617ee3ddee5d3559d39ad9cbdd61fafc715bb9c1802b35079c18ae1a8330c29c24a9c980886b4a73660d3941a')

prepare() {
  cp -a "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test

  cd ../"${pkgname[1]}-${pkgver}"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test2
}

package_python-atomicwrites() {
  depends=('python')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {README,CONTRIBUTING}.rst
}

package_python2-atomicwrites() {
  depends=('python2')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {README,CONTRIBUTING}.rst
}

