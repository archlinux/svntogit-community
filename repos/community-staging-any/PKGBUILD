# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Tarmo Heiskanen <turskii@gmail.com>

pkgbase=python-flask
pkgname=('python-flask' 'python2-flask')
pkgver=1.1.1
pkgrel=3
pkgdesc='Micro webdevelopment framework for Python'
url='http://flask.pocoo.org/'
arch=('any')
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-werkzeug' 'python2-werkzeug'
             'python-jinja' 'python2-jinja' 'python-itsdangerous' 'python2-itsdangerous'
             'python-click' 'python2-click')
source=("https://github.com/pallets/flask/archive/${pkgver}.tar.gz")
sha512sums=('1d417e571695615262a52a4627e722090aedf06d7966b9cb3a8e6c80ffe36958c2d22ce4bbcba67a7b04fc29f8906aa0aeb3940e0c9b872f967765470f38eae3')

prepare() {
  cp -r "flask-$pkgver" "python-flask-$pkgver"
  cp -r "flask-$pkgver" "python2-flask-$pkgver"
}

buildk() {
  cd "$srcdir/python-flask-$pkgver"
  python setup.py build

  cd "$srcdir/python2-flask-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/python-flask-$pkgver"
  python setup.py test

  cd "$srcdir/python2-flask-$pkgver"
  python2 setup.py test
}

package_python-flask() {
  depends=('python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
  cd "$srcdir/python-flask-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

package_python2-flask() {
  depends=('python2-werkzeug' 'python2-jinja' 'python2-itsdangerous' 'python2-click')
  cd "$srcdir/python2-flask-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/flask "$pkgdir"/usr/bin/flask2
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
