# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Stefan Husmann  <stefan-husmann@t-online.de>
# Contributor: Manuel "ekerazha" C. (www.ekerazha.com)

pkgbase=python-configobj
pkgname=('python-configobj' 'python2-configobj')
pkgver=5.0.6
pkgrel=4
pkgdesc='Simple but powerful config file reader and writer for Python'
arch=('any')
url='https://github.com/DiffSK/configobj'
license=('BSD')
makedepends=('python' 'python2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/DiffSK/configobj/archive/v$pkgver.tar.gz")
sha256sums=('2e140354efcca6f558ff9ee941b435ae09a617bc071797bef62c8d6ed2033d5e')

package_python-configobj() {
  depends=('python-six')
  cd "configobj-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-configobj() {
  depends=('python2-six')
  cd "configobj-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
