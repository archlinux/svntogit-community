# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=15.0.0
pkgrel=3
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/path/archive/v$pkgver.tar.gz"
        $pkgname-fix-pytest.patch::https://github.com/jaraco/path/commit/8117892ea5da.patch)
sha512sums=('3d499cf9a02381bd4f1aa4c515b91163881b928a2cdd19c6644239061c104bed22244f39a3a19d8376c296488bd6155d61308d33ddbc8a5a98e1d0d66d2515b5'
            'bfed90a657aa2a85eb03f88ae1bfb28a0322bcb30a87431ce30b31849ce938eb0bb98565d49aa07c8ccddb100e8d7dadd81e730da546649a87449508768aca9c')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd path-$pkgver
  sed -n '/--- a\/pytest.ini/,/junit_family/p' ../$pkgname-fix-pytest.patch |
    patch -Np1
}

build() {
  cd path-$pkgver
  python setup.py build
}

check() {
  cd path-$pkgver
  python -m pytest
}

package() {
  cd path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
