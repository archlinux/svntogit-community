# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Amr Hassan <amr.hassan@gmail.com>

pkgbase=python-pylast
pkgname=('python-pylast' 'python2-pylast')
pkgver=2.3.0
pkgrel=2
pkgdesc='A Python interface to Last.fm and Libre.fm'
arch=('any')
url='https://github.com/pylast/pylast'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-pylast-${pkgver}.tar.gz::https://github.com/pylast/pylast/archive/${pkgver}.tar.gz")
sha256sums=('d82f57c7d1ecccfe6ab8137cae7bee7bb591b4d9f829704eea2a11271e200a94')

prepare() {
  cp -r {,python-}pylast-${pkgver}
  mv {,python2-}pylast-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-pylast-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-pylast() {
  depends=('python-six')

  cd python-pylast-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1' --skip-build
}

package_python2-pylast() {
  depends=('python2-six')

  cd python2-pylast-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
