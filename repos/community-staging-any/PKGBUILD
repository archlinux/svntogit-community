# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

_name=show-in-file-manager
pkgname=python-$_name
pkgver=1.1.4
pkgrel=2
pkgdesc="Python module to open the system file manager and select files in it"
arch=('any')
url="https://github.com/damonlynch/showinfilemanager"
license=('MIT')
depends=('python-pyxdg' 'python-packaging')
makedepends=('python-setuptools')
source=(https://files.pythonhosted.org/packages/source/s/$_name/$_name-$pkgver.tar.gz)
sha256sums=('15d16e4a875b9e217b038d02f029c3800c4a6ad645e3f73c9e107ea26bab3adb')

build() {
  cd $_name-$pkgver
  python setup.py build
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
