# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=aiohttp-openmetrics
pkgname=python-aiohttp-openmetrics
pkgver=0.0.10
pkgrel=2
pkgdesc='OpenMetrics provider for aiohttp'
arch=('any')
url=https://github.com/jelmer/aiohttp-openmetrics
license=('Apache')
depends=('python-aiohttp' 'python-prometheus_client')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('4e372b6ba8f17a0e27792282ee26ed1a69791aa0e33474fa7b6a162ca0d7c655')
b2sums=('f8f56885e19286014ee8466eba0adbbc6fa9cd54774b98b3056e8c2de6d0cbc4f04b5bd7bd2c7129579e602daa830b63a3a83cef46e59081ec40a04cee25cf8a')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
