# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=guake
pkgver=3.9.0
pkgrel=3
pkgdesc='Drop-down terminal for GNOME'
arch=('any')
url="http://guake-project.org/"
license=('GPL')
depends=('libkeybinder3' 'libnotify' 'libutempter' 'libwnck3' 'python-cairo'
         'python-dbus' 'python-gobject' 'python-pbr' 'vte3' 'python-yaml')
makedepends=('git' 'python-setuptools-scm')
_commit=1e8ea6f14dd8ba511aab929a601d1a8fa3a910dd
source=("git+https://github.com/Guake/guake#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n b1c6d8c54a008a12d05396d9da2181584656108f # Fix metainfo install dir
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  sed -i '/-m pip install -r requirements.txt/d' Makefile
  make DESTDIR="$pkgdir" PREFIX='/usr' COMPILE_SCHEMA=0 install
}
