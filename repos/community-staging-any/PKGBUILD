# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pastedeploy
pkgname=('python-pastedeploy' 'python2-pastedeploy')
pkgver=2.0.1
pkgrel=3
pkgdesc="Load, configure, and compose WSGI applications and servers"
arch=('any')
license=('MIT')
url="http://pythonpaste.org/deploy/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Pylons/pastedeploy/archive/$pkgver.tar.gz")
sha512sums=('be50c434765d5a913f9456527a72bd88e778e947b1db99ee4ea2f3325366038d941bdab6c826bd61f0c1eb1618395e6bca2081ac99b5ca50aa7120406d5f4ce3')

prepare() {
  cp -a pastedeploy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pastedeploy-$pkgver
  python setup.py build

  cd "$srcdir"/pastedeploy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pastedeploy-$pkgver
  python setup.py pytest

  cd "$srcdir"/pastedeploy-$pkgver-py2
  python2 setup.py pytest
}

package_python-pastedeploy() {
  depends=('python-setuptools')

  cd pastedeploy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-pastedeploy() {
  depends=('python2-setuptools')

  cd pastedeploy-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
