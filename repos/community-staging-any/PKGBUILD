# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-helper
pkgver=2.5.0
pkgrel=3
pkgdesc="Development library for quickly writing configurable applications and daemons"
arch=('any')
license=('BSD')
url="https://helper.readthedocs.org"
depends=('python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-coverage')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gmr/helper/archive/$pkgver.tar.gz")
sha512sums=('bd0b7a46a3b1219d2099793c8ff63f17bcd82371176b6ee50e4f1c924d1bb20d8e30f70e548e8fe575b71960bd4120dc9c13487fedccf3f8cca675a2ad3aeddf')

build() {
  cd helper-$pkgver
  python setup.py build
}

check() {
  cd helper-$pkgver
  nosetests3
}

package() {
  cd helper-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
