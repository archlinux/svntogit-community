# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Emiliano Vavassori <syntaxerrormmm@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz@mail.ru>

pkgname=ruby-mail
pkgver=2.7.0
pkgrel=3
pkgdesc='A really Ruby Mail handler'
arch=('any')
url='http://github.com/mikel/mail'
license=('MIT')
depends=('ruby-mini_mime')
options=('!emptydirs')
source=("http://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('1f2ae391ca04b0a3c78905ce5b2ebc951332ee211781dd7e7b3569fd958bbe81')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} ${pkgname#*-}-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s ${_gemdir}/gems/${pkgname#*-}-${pkgver}/MIT-LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
