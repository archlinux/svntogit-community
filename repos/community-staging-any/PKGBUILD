# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Contributor: Pappa < jcberg @ gmail . com >

pkgname=('python-xlib' 'python2-xlib')
pkgbase=python-xlib
pkgver=0.18
pkgrel=2
pkgdesc="A fully functional X client library for Python programs"
url="https://github.com/python-xlib/python-xlib"
arch=('any')
license=('GPL')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/python-xlib/python-xlib/releases/download/$pkgver/python-xlib-$pkgver.tar.bz2")
sha256sums=('a4b05187cc850e49c1c8c683d89dd2a93fcbf07a7029d1db929a4c1591617ad7')

package_python2-xlib() {
  depends=('python2')

  cd "$srcdir"/python-xlib-$pkgver
  python2 setup.py install --root="$pkgdir"
}

package_python-xlib() {
  depends=('python')

  cd "$srcdir"/python-xlib-$pkgver
  python setup.py install --root="$pkgdir"
}
