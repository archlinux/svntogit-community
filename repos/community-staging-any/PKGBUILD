# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jose
pkgver=3.3.0
pkgrel=4
pkgdesc="A JOSE implementation in Python"
arch=('any')
license=('MIT')
url="https://github.com/mpdavis/python-jose"
depends=('python-rsa' 'python-six' 'python-ecdsa' 'python-future')
optdepends=('python-cryptography: one option for better performance'
            'python-pycryptodome: one option for better performance')
makedepends=('python-setuptools' 'python-rsa' 'python-six' 'python-ecdsa' 'python-future' 'python-pytest-runner')
checkdepends=('python-cryptography' 'python-pycryptodome' 'python-pytest-cov')
source=("https://github.com/mpdavis/python-jose/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('de7147cc6e12406d6e459c32829acae46395acba727c53e65bc05f4794ee3050eecd355e3ae2e87f4ebdbd871f53822eea08a10f25d7ca6088fc6128dc7d9637')

prepare() {
  cd python-jose-$pkgver
  sed -i 's/<0.15//' setup.py
}

build() {
  cd python-jose-$pkgver
  python setup.py build
}

check() {
  cd python-jose-$pkgver
  # https://github.com/mpdavis/python-jose/issues/176
  python setup.py pytest --addopts "--deselect tests/algorithms/test_EC.py::TestECAlgorithm::test_key_too_short"
}

package() {
  cd python-jose-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
