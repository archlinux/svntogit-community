# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-keystoneclient
pkgver=5.1.0
_commit=4763cd8052f51393063cc8706fdc0f0c9b017b24
pkgrel=2
pkgdesc="Client Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/python-keystoneclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-iso8601' 'python-prettytable' 'python-debtcollector'
         'python-keystoneauth1' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization' 'python-oslo-config' 'python-requests' 'python-stevedore')
makedepends=('git' 'python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-pycodestyle' 'python-lxml'
              'python-testresources')
source=("git+https://github.com/openstack/python-keystoneclient.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd python-keystoneclient
  python setup.py build
}

check() {
  cd python-keystoneclient
  stestr run
}

package() {
  cd python-keystoneclient
  python setup.py install --root="$pkgdir" --optimize=1
}
