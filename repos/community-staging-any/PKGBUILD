# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Johannes Dewender   arch at JonnyJD dot net

_name=discid
pkgname=python-discid
pkgver=1.2.0
pkgrel=8
pkgdesc="Python binding of MusicBrainz libdiscid"
url="https://github.com/JonnyJD/python-discid"
license=('LGPL3')
arch=('any')
depends=('libdiscid' 'python')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d004b7e4b5416310c8fb21bb9e686b5e5f5651366fb78785e299dcd17aaa99e7c2e115cc1c5175d8fbf853d3f7b8813555be32bf387d7ce82271b5e57bdff0dd')
b2sums=('4ea53ab28c6404ae1890b74757bbc1812043096eb251db1d0183eda9b0f3becb6e4f3108a2ea715604de02fd0b30ec5ff76c2e1a1af3ea28eeaa6df6a197a15a')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # disable tests relying on /dev/cdrom:
  # https://github.com/JonnyJD/python-discid/issues/51
  pytest -v -k "not test_read_features and not test_read_put and not test_read_simple"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
