# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Bermond <danielbermond@yahoo.com>

pkgname=ffnvcodec-headers8.1
pkgver=8.1.24.11
pkgrel=2
pkgdesc='FFmpeg version of headers required to interface with Nvidias codec APIs'
arch=(any)
url=https://git.videolan.org/?p=ffmpeg/nv-codec-headers.git
license=(MIT)
makedepends=(git)
source=(
  git+https://git.videolan.org/git/ffmpeg/nv-codec-headers.git#tag=0cd5c2b7c30d580d09348c92d8facd7a39957f01
  ffnvcodec-install.patch
)
sha256sums=(
  SKIP
  248dc04818d010c470a2dc4436bd4091f6e2ef3e09d1b3fcb4d52ed08cd7809b
)

pkgver() {
  cd nv-codec-headers

  git describe --tags | sed 's/^n//'
}

prepare() {
  cd nv-codec-headers

  patch -Np1 -i ../ffnvcodec-install.patch
}

build() {
  make PREFIX=/usr -C nv-codec-headers
  sed -n '4,25p' nv-codec-headers/include/ffnvcodec/nvEncodeAPI.h > LICENSE # Extract license
  sed -i '1,22s/^.\{,3\}//' LICENSE # Delete C comments
}

package() {
  make PREFIX=/usr DESTDIR="${pkgdir}" -C nv-codec-headers install
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/ffnvcodec-headers8.1/
}

# vim: ts=2 sw=2 et:
