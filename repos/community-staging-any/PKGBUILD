# Maintainer: David Runge <dvzrv@archlinux.org>

_name=lazr.config
pkgname=python-lazr.config
pkgver=2.2.3
pkgrel=4
pkgdesc="Create configuration schemas, and process and validate configurations"
arch=(any)
url="https://launchpad.net/lazr.config"
license=(LGPL3)
depends=(
  python
  python-lazr.delegates
  python-zope-interface
)
makedepends=(
  python-build
  python-installer
  python-setuptools
  python-wheel
)
checkdepends=(
  python-nose
)
provides=(python-lazr-config)
replaces=(python-lazr-config)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz{,.asc})
sha512sums=('cd8edc29a811b658045f314b2de4317bd750a701c2596d89912948fe0e58bdc39ad3d22cdf34bb7e46658af7ac6d32249e454c35bdcf72bc5053094343cd37bb'
            'SKIP')
b2sums=('029bdec3ac4f937f439f833f51e44a58f4604f337979c9f06695e7a243b2cde0af39a46139af5e88052e8585d2729c1b328df89979c9ff100338ec557e841b71'
        'SKIP')
validpgpkeys=('AC0A4FF12611B6FCCF01C111393587D97D86500B') # Colin Watson <cjwatson@chiark.greenend.org.uk>

prepare() {
  cd $_name-$pkgver
  # we do not care about coverage in testing
  # doctests with nose (lol) are broken
  # we *do not* want to use PDB in packaging either... -_-
  sed -e '/coverage/d;/doctest/d;/pdb/d' -i setup.cfg
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  # NOTE: tests are broken: https://bugs.launchpad.net/lazr.config/+bug/2015628
  # nosetests -P lazr
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {HACKING,NEWS,README}.rst -t "$pkgdir/usr/share/doc/$pkgname/"
  rm -frv "$pkgdir/$site_packages/lazr/config/tests"
}
