# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pifpaf
pkgver=3.0.0
pkgrel=3
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-fixtures'
         'python-psutil' 'python-xattr')
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
makedepends=('python-setuptools')
checkdepends=('python-testrepository' 'python-requests' 'python-testtools' 'python-oslotest'
              'memcached' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb' 'httpbin' 'rabbitmq')
# 'elasticsearch': test fails
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz")
sha512sums=('78d5daae08e6c5ee664619ca7683c04379502c6f667ff9ca7dd07e4dee798c1ec7c8adafb48bddcc8ba33dd0e6b09790da2f6e6d7181468450d800186faa41f4')

export PBR_VERSION=$pkgver

build() {
  cd pifpaf-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr
}

package() {
  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
