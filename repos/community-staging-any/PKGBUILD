# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pkgconfig
pkgver=0.23026
pkgrel=3
pkgdesc="Pure-Perl Core-Only replacement for pkg-config"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PkgConfig"
depends=('perl')
checkdepends=('perl-capture-tiny' 'perl-test-minimumversion')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/PkgConfig-$pkgver.tar.gz")
sha512sums=('d72eb3c88d0c62b3863b1bdca9344b97051e58248ac0d9334ae02ba64cb3f5773228e88d34017e20681f641043a561eb90da1d34c7eb0b20519eecb5a0d87fa9')

build() {
  cd PkgConfig-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PkgConfig-$pkgver
  make test
}

package() {
  cd PkgConfig-$pkgver
  make DESTDIR="$pkgdir" install
}
