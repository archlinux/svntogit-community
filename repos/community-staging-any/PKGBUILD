# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-pallets-sphinx-themes
_pyname=${pkgname/python-/}
pkgver=2.0.2
pkgrel=2
pkgdesc='Sphinx themes for Pallets and related projects'
url='https://github.com/pallets/pallets-sphinx-themes'
arch=('any')
license=('BSD')
depends=('python' 'python-sphinx' 'python-importlib-metadata')
makedepends=('python-setuptools')
source=(https://github.com/pallets/pallets-sphinx-themes/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('a37b0f4fe38c098c0508988faa54ed8b2e1ad9ba41f95f862734cf38fd093e2b7c42d408fc10e529aca810f4cc37d16e6d707ed9affa433adfcb481006cd4577')
b2sums=('a5e2d3a02a5996df6e648b8cf9943260d6e0e475108921493300c7d9437837cc66e83b489ad335596c39eac29f3a6e23b7b4abc3cf1fe3bbb018fd874b44aefd')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
