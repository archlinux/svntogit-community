# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsonlines
pkgname=(python-jsonlines python2-jsonlines)
pkgver=1.2.0
pkgrel=2
pkgdesc="Library with helpers for the jsonlines file format"
url="https://github.com/wbolster/jsonlines"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/wbolster/jsonlines/archive/$pkgver.tar.gz")
sha512sums=('21af1c74024e1db9e301be0cd025b4cd82899059ecb3fde4c9a6ab4087ef413b7ecb6b5c41bdbb45ed1a48247c88c532c3671a4407890f96207616d2f7467c34')

prepare() {
  cp -a jsonlines-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/jsonlines-$pkgver
  python setup.py build

  cd "$srcdir"/jsonlines-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/jsonlines-$pkgver
  python setup.py pytest

  cd "$srcdir"/jsonlines-$pkgver-py2
  python2 setup.py pytest
}

package_python-jsonlines() {
  depends=('python-six')

  cd jsonlines-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-jsonlines() {
  depends=('python2-six')

  cd jsonlines-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
