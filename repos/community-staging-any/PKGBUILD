# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-email-validator
pkgver=1.0.5
pkgrel=1
pkgdesc="A robust email syntax and deliverability validation library for Python 2.x/3.x."
arch=('any')
url="https://github.com/JoshData/python-email-validator"
license=('custom:CC0')
depends=('python-dnspython' 'python-idna')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/JoshData/python-email-validator/archive/v$pkgver.tar.gz")
sha512sums=('e9dd0ee8dfc603e40591eb8cd4e58c32331c1b9450feb4ae0ff27e1d655d0abb2c8a46b7d0cb43d7e037964f0c120dbad6bf32a60dfe297fb552058733ffd173')

build() {
  cd "$srcdir"/python-email-validator-$pkgver
  python setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/python-email-validator-$pkgver
  python email_validator/__init__.py --tests < test_pass.txt
}

package() {
  cd "$srcdir"/python-email-validator-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
