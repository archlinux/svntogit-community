# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=qpageview
pkgname=python-${_pkg}
pkgver=0.6.2
pkgrel=2
pkgdesc="Page based document viewer widget for Qt5/PyQt5"
arch=(any)
url="https://qpageview.org/"
license=(GPL3)
depends=(python python-pyqt5 python-poppler-qt5 qt5-svg)
makedepends=(python-setuptools)
optdepends=('python-pycups: printing support')
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('1c1e3d6dc5374264e1f7c7d4a78616a811e8131c7ed214aed7e08a71f12923e9')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}
