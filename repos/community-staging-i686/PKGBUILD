# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=idris
pkgver=1.1.0
pkgrel=12
pkgdesc="Functional Programming Language with Dependent Types"
url="http://www.idris-lang.org/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-aeson" "haskell-annotated-wl-pprint" "haskell-ansi-terminal"
         "haskell-ansi-wl-pprint" "haskell-async" "haskell-base64-bytestring" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-cheapskate" "haskell-code-page" "haskell-fingertree"
         "haskell-fsnotify" "haskell-ieee754" "haskell-mtl" "haskell-libffi" "haskell-network"
         "haskell-optparse-applicative" "haskell-parsers" "haskell-regex-tdfa" "haskell-safe"
         "haskell-split" "haskell-terminal-size" "haskell-text" "haskell-transformers-compat"
         "haskell-trifecta" "haskell-uniplate" "haskell-unordered-containers" "haskell-utf8-string"
         "haskell-vector" "haskell-vector-binary-instances" "haskell-zip-archive")
source=("$pkgname-$pkgver.tar.gz::https://github.com/idris-lang/Idris-dev/archive/v$pkgver.tar.gz"
        cheapskate-0.1.1.patch)
sha512sums=('689f98315a835a623d5bfdb8419ff6888ab82e357f243f0adad47f496ee388cf521172d147855ccf63f5d95c6d4ef0977b1ec99c61b755772bd316d49daa1c44'
            'bbde8342befd769657e5b1e19e52abd190d18c4512bec26a8431090d9e9bb97b59c4395ad89fa54990332c34b2933aee0643e46f3b37a1f747d2e12c30e549a0')

prepare() {
    cd Idris-dev-$pkgver
    patch -p1 -i ../cheapskate-0.1.1.patch

    sed -e 's/, trifecta >= 1.6 && < 1.7/, trifecta >= 1.6 \&\& < 1.8/' \
        -e 's/, aeson >= 0.6 && < 1.2/, aeson >= 0.6 \&\& < 1.3/' \
        -i idris.cabal

    sed -i '1ioverride IDRIS := env LD_PRELOAD=$(shell ls ../../dist/build/libHSidris-*-ghc*.so) $(IDRIS)' \
        libs/*/Makefile
}

build() {
    cd "$srcdir"/Idris-dev-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fFFI -fGMP -frelease -f-freestanding -f-CI -f-execonly
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "$srcdir"/Idris-dev-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
