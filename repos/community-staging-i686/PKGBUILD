# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@phare.normalesup.org>

pkgname=givaro
pkgver=4.0.2
pkgrel=1
pkgdesc="C++ library for arithmetic and algebraic computations"
arch=(i686 x86_64)
url="http://givaro.forge.imag.fr/"
license=(GPL)
depends=(gmp)
source=("https://github.com/linbox-team/givaro/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('9f798d715e37734ee66176eb1fd644e1')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-shared 
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
