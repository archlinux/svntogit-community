# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgname=guvcview
pkgver=2.0.5
pkgrel=2
pkgdesc="A video viewer and capturer for the linux uvc driver"
arch=('i686' 'x86_64')
url="http://guvcview.sourceforge.net/"
license=('GPL')
depends=('portaudio' 'ffmpeg' 'gtk3' 'sdl2' 'gsl' 'libusb')
makedepends=('pkg-config' 'intltool')
optdepends=('pulseaudio: for PulseAudio support')
options=('!docs' '!buildflags')
source=("http://downloads.sourceforge.net/project/${pkgname}/source/${pkgname}-src-${pkgver}.tar.gz")
sha256sums=('a86beb5993a8449ed3cbcc6ec2a238ef0b90138b6cbe2afab4456d37f44c41a0')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  ./configure --prefix=/usr \
              --disable-debian-menu
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
