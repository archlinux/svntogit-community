# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=tuxcards
pkgver=2.2.1
pkgrel=3
pkgdesc="A hierarchical notebook to enter and manage notes and ideas in a structured manner"
arch=('i686' 'x86_64')
url="http://www.tuxcards.de"
license=('GPL')
depends=('qt4')
source=(http://www.tuxcards.de/src/tuxcards-${pkgver}/${pkgname}-${pkgver}.tar.gz tuxcards.desktop)
sha1sums=('c0410f28b51d58b4f372ffae0ce0d2a7329f2c45'
          '565419ac6b24d61fc038462a783cb80d44b4fe7e')

build() {
  cd "${srcdir}/${pkgname}"
  sed -i 's|/usr/local/doc/tuxcards/flowers/|/usr/share/tuxcards/flowers/|' src/CTuxCardsConfiguration.cpp
  qmake tuxcards.pro
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  install -D -m755 tuxcards "${pkgdir}/usr/bin/tuxcards"
  install -D -m644 src/icons/lo16-app-tuxcards.png "${pkgdir}/usr/share/icons/locolor/16x16/apps/tuxcards.png"
  install -D -m644 src/icons/lo32-app-tuxcards.png "${pkgdir}/usr/share/icons/locolor/32x32/apps/tuxcards.png"
  install -D -m644 "${srcdir}/tuxcards.desktop" "${pkgdir}/usr/share/applications/tuxcards.desktop"
  install -D -m644 src/translations/tuxcards_de.qm "${pkgdir}/usr/share/tuxcards/i18/tuxcards_de.qm"
  install -d "${pkgdir}/usr/share/tuxcards/flowers/"
  install -m644 src/gui/cactusbar/flowers/*.gif "${pkgdir}/usr/share/tuxcards/flowers/"
}
