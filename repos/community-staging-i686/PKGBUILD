# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace
pkgver=7.0
pkgrel=3
pkgdesc="Graphics API Tracing"
arch=('i686' 'x86_64')
url="https://github.com/apitrace/apitrace"
license=('custom')
makedepends=('cmake' 'mesa' 'libgl' 'python2' 'procps-ng' 'qt5-webkit')
source=("$pkgname-$pkgver.tar.gz::https://github.com/apitrace/apitrace/archive/${pkgver}.tar.gz")
md5sums=('3c0b3ebae4fda123c0bffcd6eb61d161')

build() {
  cd ${srcdir}/apitrace-*

  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DENABLE_GUI=TRUE
  make -C build
}

package() {
  depends=('python2' 'libgl' 'procps-ng')
  optdepends=('qt5-webkit: GUI support')

  cd ${srcdir}/apitrace-*

  make -C build DESTDIR="${pkgdir}/" install

  install -m755 -d "${pkgdir}/usr/share/licenses/apitrace"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/apitrace/"
}
