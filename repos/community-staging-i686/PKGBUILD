# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar
pkgver=0.16
pkgrel=5
pkgdesc="A Minimalistic Text Based Status Bar"
url="http://hackage.haskell.org/package/xmobar"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('gmp' 'libxft' 'libxinerama' 'wireless_tools' 'libxrandr')
makedepends=('ghc=7.6.2-1' 'haskell-x11=1.6.0.2-2' 'haskell-x11-xft=0.3.1-6' 'haskell-utf8-string=0.3.7-4'
	     'haskell-stm=2.4.2-1' 'haskell-parsec=3.1.3-2' 'haskell-mtl=2.1.2-2')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/xmobar/$pkgver/xmobar-$pkgver.tar.gz)
md5sums=('4742f1556a8e9b292f18df1176dcd378')

build() {
    cd ${srcdir}/xmobar-$pkgver

    runhaskell Setup configure --disable-optimization --prefix=/usr --flags="with_utf8 with_xft with_iwlib"
    runhaskell Setup build
}

package() {
    cd ${srcdir}/xmobar-$pkgver
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 license ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

