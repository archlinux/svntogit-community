# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=pandoc-citeproc
pkgname=(pandoc-citeproc haskell-pandoc-citeproc)
pkgver=0.9.1.1
pkgrel=18
pkgdesc="Supports using pandoc with citeproc"
url="http://hackage.haskell.org/package/${pkgbase}"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=("ghc=7.10.3" "haskell-aeson" "haskell-aeson-pretty" "haskell-attoparsec"
             "haskell-data-default" "haskell-hs-bibutils" "haskell-mtl"
             "haskell-old-locale" "haskell-pandoc" "haskell-pandoc-types" "haskell-parsec"
             "haskell-rfc5051" "haskell-setenv" "haskell-split" "haskell-syb"
             "haskell-tagsoup" "haskell-temporary" "haskell-text" "haskell-text-icu"
             "haskell-unordered-containers" "haskell-vector" "haskell-xml-conduit" "haskell-yaml")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('15c89a9aa6bce4efd6b728ea16151eb6390cad0495eb82c50cbac490591c8f86')

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-debug -f-test_citeproc -funicode_collation -f-embed_data_files -fbibutils
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_pandoc-citeproc() {
    depends=('pandoc' 'icu')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_haskell-pandoc-citeproc() {
    pkgdesc="Supports using pandoc with citeproc (docs and libraries)"
    depends=("${makedepends[@]}" 'pandoc-citeproc')
    install="haskell-pandoc-citeproc.install"

    cd "${srcdir}/${pkgbase}-${pkgver}"    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgbase}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgbase}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/share/{pandoc-citeproc,man} "$pkgdir"/usr/bin
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
