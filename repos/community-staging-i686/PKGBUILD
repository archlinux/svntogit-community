# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.16
pkgrel=2
pkgdesc='Minimalistic Mumble server'
arch=('i686' 'x86_64')
url='https://github.com/umurmur/umurmur'
license=('custom')
depends=('mbedtls' 'libconfig' 'protobuf-c')
install=$pkgname.install
backup=('etc/umurmur/umurmur.conf')
source=($pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
        umurmur.service)
md5sums=('5d6f5b01a0fc357e26ea9ed433031bb9'
         'd9d556e4ffa77e193fb40ce508804720')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-ssl=polarssl
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 umurmur.conf.example "$pkgdir"/etc/umurmur/umurmur.conf
  install -Dm644 ../umurmur.service "$pkgdir"/usr/lib/systemd/system/umurmur.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
