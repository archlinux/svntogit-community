# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-globalkeys
pkgver=0.10.0
pkgrel=3
pkgdesc="LXQt daemon and library for global keyboard shortcuts registration."
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt")
makedepends=("cmake" "qt5-tools")
source=(
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"9a9d6a0299a26f91d391a601123e78e18cbe56ee6086cc27ccf800920e146e1a"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
