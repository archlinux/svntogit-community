# $Id$
# Maintainer: Antonio Rojas < nqn76sw@gmail.com >

pkgname=linbox
pkgver=1.4.2
pkgrel=4
pkgdesc="A template library for exact, high-performance linear algebra computation with dense, sparse, and structured matrices over the integers and over finite fields"
arch=(i686 x86_64)
url="http://linalg.org/"
license=(LGPL)
depends=(m4rie lapack flint fplll iml givaro) 
makedepends=(fflas-ffpack)
source=("https://github.com/linbox-team/linbox/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
        linbox-fplll5.patch::"https://github.com/fingolfin/linbox/commit/95355873.patch")
md5sums=('c0daf8a74b373cf577dec975bca4fb9c'
         '47d5fa608972cd6807b9facc5d828e53')

prepare() {
  cd $pkgname-$pkgver
# Fix build with fplll 5.0
  patch -p1 -i ../linbox-fplll5.patch
  sed -e 's|fplll/dpe.h|fplll/nr/dpe.h|g' -i linbox/algorithms/short-vector.h
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-sage --enable-optimization --with-fplll=/usr
  make
}

check() {
  cd $pkgname-$pkgver
# make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
