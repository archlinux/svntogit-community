# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: arch3y <arch3y[at]archstrike[dot]org>

_gitname=yara-python
pkgbase=python-yara
pkgname=('python-yara' 'python2-yara')
pkgver=3.6.0
_gitcommit=2d8baac160aea1dddfe8039f10b3891ee049293a
pkgrel=1
pkgdesc='Tool aimed at helping malware researchers to identify and classify malware samples'
url='https://github.com/VirusTotal/yara-python'
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('git' 'yara' 'python-setuptools' 'python2-setuptools')
source=(${pkgbase}::git+"https://github.com/VirusTotal/${_gitname}#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgbase}
  git describe --always | sed 's/^v//;s/-/./g'
}

prepare() {
  cp -a ${pkgbase}{,-py2}
}

build() {
  (cd ${pkgbase}
    python setup.py build --dynamic-linking
  )
  (cd ${pkgbase}-py2
    python2 setup.py build --dynamic-linking
  )
}

check() {
  (cd ${pkgbase}
    local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python tests.py
  )
  (cd ${pkgbase}-py2
    local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python2 tests.py
  )
}

package_python-yara() {
  depends=('python' 'yara' 'python-magic')
  cd ${pkgbase}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

package_python2-yara() {
  depends=('python2' 'yara' 'python2-magic')
  cd ${pkgbase}-py2
  python2 setup.py install --root="${pkgdir}" -O1
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

# vim: ts=2 sw=2 et:
