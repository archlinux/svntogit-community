# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=9.1.1
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(i686 x86_64)
url="http://www.shoup.net/ntl/"
license=(GPL)
depends=(gf2x gmp)
makedepends=()
options=(!libtool)
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz" 'ntl-sage.patch')
md5sums=('22beee5ee1fe7c09f623d0f0cf08e7af'
         '4cb5f97080281109bf358959ab993691')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i $srcdir/ntl-sage.patch
}

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NTL_GMP_LIP=on NTL_THREADS=on NTL_EXCEPTIONS=on
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}

