# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hoogle
pkgver=5.0.12
pkgrel=26
pkgdesc="Haskell API Search"
url="http://www.haskell.org/hoogle/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-quickcheck" "haskell-aeson" "haskell-cmdargs" "haskell-conduit"
         "haskell-conduit-extra" "haskell-connection" "haskell-extra" "haskell-src-exts"
         "haskell-http-conduit" "haskell-http-types" "haskell-js-flot" "haskell-js-jquery"
         "haskell-mmap" "haskell-network" "haskell-network-uri" "haskell-old-locale"
         "haskell-process-extras" "haskell-resourcet" "haskell-tar" "haskell-text"
         "haskell-uniplate" "haskell-utf8-string" "haskell-vector" "haskell-wai"
         "haskell-wai-logger" "haskell-warp" "haskell-warp-tls" "haskell-zlib")
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d40c13674ad113079b23386d2fc5ae3a61dc79012e88f4e1bc2e2123106412b96cc32955e94ec0f294451feadd5bd06ad5b03ab1a40dd7172c5344bdd520e225')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
