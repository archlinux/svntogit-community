# $Id$
# Maintainer:  Mateusz Herych <heniekk@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Vasco Costa <vasco.costa@meiodigital.com>

pkgname=pokerth
_realname=PokerTH
pkgver=1.0
pkgrel=3
pkgdesc="Poker game written in C++/QT4"
arch=('i686' 'x86_64')
url="http://www.pokerth.net/"
license=('GPL' 'custom')
depends=('curl' 'boost-libs' 'gsasl' 'gnutls' 'protobuf'
         'qt4' 'sdl_mixer' 'libircclient' 'tinyxml')
makedepends=('boost')
source=(http://downloads.sourceforge.net/sourceforge/pokerth/$_realname-$pkgver-src.tar.bz2)
md5sums=('1d8686b8968475cfc76e873a17a53efc')

build() {
  cd "$srcdir/$_realname-$pkgver-src"

  sed -i '31 i #include <libircclient/libirc_rfcnumeric.h>' src/net/common/ircthread.cpp

  qmake $pkgname.pro
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver-src"

  make INSTALL_ROOT="$pkgdir" install

  install -D -m644 docs/pokerth.1 "$pkgdir/usr/share/man/man1/pokerth.1"
  install -D -m644 data/data-copyright.txt "$pkgdir/usr/share/licenses/pokerth/data-copyright.txt"
}
