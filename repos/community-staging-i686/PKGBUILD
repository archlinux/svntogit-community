# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.0rc3
_tag=2.0-rc3
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('i686' 'x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('gcc-libs>=5.3.0-3')
makedepends=('git' 'mercurial' 'gcc-go')
source=("git+https://github.com/ginuerzh/gost.git#tag=$_tag"
        LICENSE)
md5sums=('SKIP'
         '24a6686f7498d7af0b51690f712dd618')

prepare() {
  cd gost

  mkdir -p .gopath/src/github.com/ginuerzh
  ln -sf "$PWD" .gopath/src/github.com/ginuerzh/gost
  export GOPATH="$PWD/.gopath"

  go get github.com/ginuerzh/gosocks5 github.com/gorilla/websocket github.com/shadowsocks/shadowsocks-go/shadowsocks github.com/golang/glog
}

build() {
  cd gost
  export GOPATH="$PWD/.gopath"

  go build -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost
  install -Dm755 gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
