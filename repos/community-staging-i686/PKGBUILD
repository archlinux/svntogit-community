# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=csfml
pkgver=2.3
pkgrel=2
pkgdesc='C bindings for sfml'
arch=('i686' 'x86_64')
url='http://www.sfml-dev.org/'
license=('zlib')
depends=("sfml")
makedepends=('cmake' 'doxygen')
source=("${pkgname}-${pkgver}::https://github.com/LaurentGomila/CSFML/archive/${pkgver}.tar.gz")
md5sums=('eb99a783e367e018333e622945d232d2')

build() {
  cd "$srcdir"/CSFML-${pkgver}

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCSFML_BUILD_DOC=true
  make
  make doc
}

package() {
  cd "$srcdir"/CSFML-${pkgver}/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

