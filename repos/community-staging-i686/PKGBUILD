# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=xtrabackup
pkgver=2.3.2
pkgrel=4
pkgdesc='Non-blocking backup tool for MySQL'
arch=('i686' 'x86_64')
url='http://www.percona.com/software/percona-xtrabackup/'
license=('GPL')
depends=('libaio' 'libev' 'libgcrypt' 'curl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib' 'vim-minimal' 'libedit' 'libevent')
optdepends=('qpress: for compressed backups')
source=(http://www.percona.com/downloads/XtraBackup/LATEST/source/tarball/percona-xtrabackup-$pkgver.tar.gz)
sha256sums=('5680500d513324b2e034969c5cf1b17790588cce628a31607d55a637b38c80e5')

build() {
  cd percona-xtrabackup-$pkgver
  cmake -DBUILD_CONFIG=xtrabackup_release \
        -DBUILD_TESTING=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DMYSQL_DATADIR=/var/lib/mysql \
    		-DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
    		-DWITH_SSL=system \
    		-DWITH_LIBEVENT=system \
    		-DWITH_EDITLINE=system \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd percona-xtrabackup-$pkgver
  make DESTDIR="$pkgdir" install

  cd  "$pkgdir"
  rm -rf usr/xtrabackup-test
  install -dm755 usr/share
  mv build/$pkgname/src/percona-xtrabackup-$pkgver/man usr/share
  rm -rf build
}
