# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Adam Griffiths <adam_griffithsAATTdart.net.au>

pkgname=ode
pkgver=0.13.1
pkgrel=2
pkgdesc="An open source, high performance library for simulating rigid body dynamics"
arch=('i686' 'x86_64')
url="https://bitbucket.org/odedevs/ode/"
license=('LGPL' 'BSD')
depends=('gcc-libs' 'bash')
source=("https://bitbucket.org/odedevs/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
md5sums=('00f6613b3d8e5249be60e3a513d6aebd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-shared \
              --enable-libccd \
              --enable-double-precision
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir}/ install

  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/ou/LICENSE-BSD.TXT \
                  ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-BSD.TXT
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/GIMPACT/GIMPACT-LICENSE-BSD.TXT \
                  ${pkgdir}/usr/share/licenses/${pkgname}/GIMPACT-LICENSE-BSD.TXT
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/libccd/BSD-LICENSE \
                  ${pkgdir}/usr/share/licenses/${pkgname}/BSD-LICENSE
}
