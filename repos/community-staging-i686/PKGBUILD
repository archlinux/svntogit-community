# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=ghc-mod
pkgver=5.8.0.0
_commit=9ab25526b8690daecaa3d34a7e30f022f7b388bc
pkgrel=12
pkgdesc="Happy Haskell Programming"
url="http://www.mew.org/~kazu/proj/ghc-mod/"
license=("AGPL3")
arch=('i686' 'x86_64')
depends=('ghc' "cabal-helper" "haskell-djinn-ghc" "haskell-extra" "haskell-fclabels"
         "haskell-ghc-paths" "haskell-ghc-syb-utils" "haskell-src-exts" "hlint"
         "haskell-monad-control" "haskell-monad-journal" "haskell-mtl" "haskell-old-time"
         "haskell-optparse-applicative" "haskell-pipes" "haskell-safe" "haskell-semigroups"
         "haskell-split" "haskell-syb" "haskell-temporary" "haskell-text"
         "haskell-transformers-base")
conflicts=('haskell-ghc-mod')
replaces=('haskell-ghc-mod')
source=("$pkgname-$_commit.tar.gz::https://github.com/DanielG/ghc-mod/archive/$_commit.tar.gz")
sha256sums=('75d83356999c2a1e6ea1d9c4340dcf3d260d2b462c4e9691ceb1f28385b3e70b')

prepare() {
    cd $pkgname-$_commit
    sed -i 's/, optparse-applicative == 0.13.0.*/, optparse-applicative == 0.13.*/' ghc-mod.cabal
}

build() {
    cd $pkgname-$_commit
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $pkgname-$_commit

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "COPYING.BSD3" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.BSD3"
}
