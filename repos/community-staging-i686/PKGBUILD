# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Maintainer: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.11
pkgrel=2
pkgdesc="A basic, good-looking task manager for WMs"
arch=('i686' 'x86_64')
url="http://code.google.com/p/tint2/"
license=('GPL2')
conflicts=('ttm-svn' 'tint')
replaces=('tint')
depends=('gtk2' 'imlib2')
optdepends=('pygtk: for using tint2conf')
makedepends=('pygtk' 'cmake')
provides=('tint')
source=("http://$pkgname.googlecode.com/files/$pkgname-${pkgver}.tar.bz2")
md5sums=('6fc5731e7425125fa84a2add5cef4bff')

build() {
  cd $srcdir/$pkgname-${pkgver}

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' src/tint2conf/tintwizard.py

  [ -d build ] || mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../
  make 
}
package() {
  cd $srcdir/$pkgname-${pkgver}/build
  make DESTDIR=$pkgdir install
}
