# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=qgit
pkgver=2.3
pkgrel=4
pkgdesc="A GIT GUI viewer built on Qt/C++"
url="http://digilander.libero.it/mcostalba/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt4' 'git')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'qgit.desktop' 'qgit.png'
		'0001-Cause-qgit-to-use-git-config-everywhere.patch')
md5sums=('1f6cd60cc1b92973718b108d542e3270'
         'ecb8b35e17163a7ec6893ffa71445c40'
         '85138f44d577b03dfc738d3f27e04992'
		 '8b8eda62d6f4d04ed57f87d4dbda9fa9')

build() {
  cd ${srcdir}/${pkgname}
  # patch taken from http://sourceforge.net/tracker/?func=detail&aid=3299672&group_id=139897&atid=744829
  patch -Np1 < ${srcdir}/0001-Cause-qgit-to-use-git-config-everywhere.patch
  qmake qgit.pro
  make
}

package() {
  cd ${srcdir}/${pkgname}
  install -Dm755 bin/qgit ${pkgdir}/usr/bin/qgit
  install -Dm644 ${srcdir}/qgit.desktop ${pkgdir}/usr/share/applications/qgit.desktop
  install -Dm644 ${srcdir}/qgit.png ${pkgdir}/usr/share/pixmaps/qgit.png
}
