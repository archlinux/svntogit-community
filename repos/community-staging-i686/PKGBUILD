# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

_hkgname=X11-xft
pkgname=haskell-x11-xft
pkgver=0.3.1
pkgrel=17
pkgdesc="Bindings to the Xft, X Free Type interface library, and some Xrender parts"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('ghc=7.10.3' 'haskell-x11=1.6.1.2' 'haskell-utf8-string' 'libxft')
options=('strip')
install=haskell-x11-xft.install
source=(http://hackage.haskell.org/packages/archive/X11-xft/$pkgver/X11-xft-$pkgver.tar.gz)
options=('staticlibs')
md5sums=('ad885150a59f63de328e73abe5ffc79e')

build() {
  cd ${srcdir}/${_hkgname}-${pkgver}
  runhaskell Setup configure -O  --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/${pkgname} --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${srcdir}/${_hkgname}-${pkgver}
  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
  runhaskell Setup copy --destdir=${pkgdir}
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
