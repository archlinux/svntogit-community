# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Dwight Schauer <dschauer@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Michael 'manveru' Fellinger <m.fellinger@gmail.com>
# Contributor: Caleb McCombs <erdrick016+aur@gmail.com>
# Contributor: Christian Hesse <arch@eworm.de>

pkgname=neko
pkgver=2.0.0
pkgrel=8
pkgdesc='High-level dynamically typed programming language'
url='http://nekovm.org/'
license=('LGPL')
arch=('x86_64' 'i686')
depends=('gc' 'gtk2' 'libmariadbclient' 'sqlite' 'apache')
makedepends=('pkgconfig' 'apr' 'git')
options=('!strip')
source=("git://github.com/HaxeFoundation/neko.git#tag=v${pkgver/.0.0/-0}"
        'neko.patch')
sha256sums=('SKIP'
            '306e70795a59600d30b5bfe448b57686bd6c3df11c7fe6413e0c7c48d83f6431')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/neko.patch"
}

build() {
  cd "$pkgname"

  [ $CARCH = x86_64 ] && export CFLAGS+=" -D_64BITS"

  # The Neko Makefile is a bit broken
  for cmd in clean libneko neko std createbin compiler libs; do
    make "$cmd"
  done
}

check() {
  make -C "$pkgname" test
}

package() {
  install -d "$pkgdir/usr/"{bin,lib}
  make -C "$pkgname" INSTALL_PREFIX="$pkgdir/usr" install
  find "$pkgdir/usr/lib" -type f -exec chmod 644 {} \;
}

# getver: nekovm.org/download
# vim:set ts=2 sw=2 et:
