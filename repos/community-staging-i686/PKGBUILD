# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=aws
pkgname=haskell-aws
pkgver=0.14.1
pkgrel=11
pkgdesc="Amazon Web Services (AWS) for Haskell"
url="http://github.com/aristidb/aws"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-attoparsec" "haskell-base16-bytestring"
         "haskell-base64-bytestring" "haskell-blaze-builder" "haskell-byteable"
         "haskell-case-insensitive" "haskell-cereal" "haskell-conduit"
         "haskell-conduit-extra" "haskell-cryptohash" "haskell-data-default"
         "haskell-http-conduit" "haskell-http-types" "haskell-lifted-base"
         "haskell-monad-control" "haskell-mtl" "haskell-network" "haskell-old-locale"
         "haskell-resourcet" "haskell-safe" "haskell-scientific" "haskell-tagged"
         "haskell-text" "haskell-unordered-containers" "haskell-utf8-string"
         "haskell-vector" "haskell-xml-conduit")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        http-conduit-2.2.patch)
sha256sums=('6a2079853ddc781b46fe3ddce31e88c0b6b2441f458141bca3cd1c7216cbe579'
            '51123da7d47793cbc8b45b1bc494b1f322b96f8f809b659fd229a7075f74160f')

prepare() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    patch -p1 -i ../http-conduit-2.2.patch

    sed -i 's/xml-conduit          >= 1.2     && <1.4/xml-conduit >= 1.2 \&\& <1.5/' aws.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-examples
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
