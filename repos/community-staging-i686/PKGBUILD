# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=opencc
pkgname=(opencc opencc-doc)
pkgver=1.0.1
pkgrel=1
url="http://code.google.com/p/opencc/"
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('cmake' 'doxygen' 'python')
source=("https://github.com/BYVoid/OpenCC/archive/ver.${pkgver}.tar.gz")
sha512sums=('22e5a38f984b19bae3f4653700fec58e66add9e6867a3260183fb1d96ff6042bf64f8f437323732f1a76e88be772666f5924fed30fa5a8d0c2ff1e268b7c0ab3')

build() {
  cd OpenCC-ver.${pkgver}
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DBUILD_DOCUMENTATION:BOOL=ON
  make
}

package_opencc() {
  pkgdesc="Library for Open Chinese Convert"
  depends=('gcc-libs')

  cd OpenCC-ver.${pkgver}
  make DESTDIR="${pkgdir}" install
  
  # Remove docs - install in splitted package
  rm -r "$pkgdir/usr/share/opencc/doc"
}

package_opencc-doc() {
  pkgdesc="Documentation for Library for Open Chinese Convert"

  cd OpenCC-ver.${pkgver}/doc
  make DESTDIR="${pkgdir}" install
}

