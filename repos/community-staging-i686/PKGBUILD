# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Colin Woodbury <colingw@gmail.com>

pkgname=haskell-zlib
pkgver=0.6.1.2
pkgrel=2
pkgdesc="Compression and decompression in the gzip and zlib formats"
url="https://hackage.haskell.org/package/zlib"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc' 'zlib')
source=(https://hackage.haskell.org/packages/archive/zlib/${pkgver}/zlib-${pkgver}.tar.gz)
sha256sums=('e4eb4e636caf07a16a9730ce469a00b65d5748f259f43edd904dd457b198a2bb')

build() {
    cd "$srcdir"/zlib-${pkgver}
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-use-bytestring-builder -f-non-blocking-ffi
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/zlib-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "$pkgdir"/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html "$pkgdir"/usr/share/doc/ghc/html/libraries/zlib
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
    rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
