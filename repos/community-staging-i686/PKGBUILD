# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=4.1.1
pkgrel=10
pkgdesc="KDE Photo Album, picture collection manager"
url="http://www.kphotoalbum.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdebase-runtime' 'libkipi' 'libkexiv2' 'libkdcraw')
makedepends=('cmake' 'automoc4' 'kdeedu-marble' 'kdemultimedia-mplayerthumbs')
optdepends=('kdeedu-marble: integration with Marble'
	'kipi-plugins: add extra functionality'
	'kdemultimedia-mplayerthumbs: video thumbnails')
install=${pkgname}.install
source=("ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'fix-docbook-build.patch'
        'fix-with-exiv2-0.21.patch')
md5sums=('c00a5f179e487ac89fccf8dfb3cd6ef0'
         '1b48553af74f0488fdad55758e911e3a'
         '9238101a2d7450e6dd822778807dd7bd')

build() {
  cd ${srcdir}

  patch -Np0 -i ${srcdir}/fix-docbook-build.patch
  patch -Np0 -i ${srcdir}/fix-with-exiv2-0.21.patch

  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
