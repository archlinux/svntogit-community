# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.0.2
pkgrel=6
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg-compat' 'fontconfig' 'libpng'
	 'libtiff' 'libdv' 'libmng' 'cairo' 'pango' 'boost-libs' 'mlt')
makedepends=('boost')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
#source=(http://download.tuxfamily.org/synfig/releases/$pkgver/synfig-$pkgver.tar.gz
source=(http://downloads.sourceforge.net/project/synfig/releases/$pkgver/source/synfig-$pkgver.tar.gz
	build-fix.patch
	ffmpeg-0.8.patch)
md5sums=('a7b19f9eb22f2bd82067ebc1401b8808'
         '16b852a3ff44765d8efd2ce597f5ca0e'
         'ebf41046097cfd778dee9a9854561dae')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/build-fix.patch"
  patch -Np1 -i "$srcdir/ffmpeg-0.8.patch"
  sed -i 's|.*set_remove_alpha.*||g' src/modules/mod_libavcodec/trgt_av.cpp
}

build() {
  cd $pkgname-$pkgver
  export PKG_CONFIG_PATH=/usr/lib/ffmpeg-compat/pkgconfig:$PKG_CONFIG_PATH
  LDFLAGS="$LDFLAGS -Wl,-rpath -Wl,/usr/lib/ffmpeg-compat"
  CFLAGS="$CFLAGS -D__STDC_CONSTANT_MACROS"
  CXXFLAGS="$CXXFLAGS -D__STDC_CONSTANT_MACROS -std=gnu++11"
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
