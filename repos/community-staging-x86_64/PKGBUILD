# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=trifecta
pkgname=haskell-trifecta
pkgver=2
pkgrel=61
pkgdesc="A modern parser combinator library with convenient diagnostics"
url="https://github.com/ekmett/trifecta/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-ansi-terminal" "haskell-ansi-wl-pprint" "haskell-blaze-builder"
         "haskell-blaze-html" "haskell-blaze-markup" "haskell-charset" "haskell-comonad"
         "haskell-fingertree" "haskell-hashable" "haskell-lens" "haskell-parsers"
         "haskell-profunctors" "haskell-reducers" "haskell-unordered-containers"
         "haskell-utf8-string")
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('41d5790efef0e6c138dcd2306b1369ba91e4db5a7f0b2a9c51eb11f83908925989ec8b71ce4a61e77ac34b42f635195674497a4d170102e588b3a9fa38d1b0bc')

prepare() {
    sed -i -e '/semigroups/d' -e 's/< *0.9/<1/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
