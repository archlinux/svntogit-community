# Maintainer: Anatol Pomozov

pkgname=aws-c-io
pkgver=0.9.0
pkgrel=1
pkgdesc='AWS SDK module to handle all IO and TLS work for application protocols'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-io'
license=(Apache)
depends=(aws-c-common aws-c-cal s2n-tls)
makedepends=(cmake)
source=(aws-c-io-$pkgver.zip::https://github.com/awslabs/aws-c-io/archive/v$pkgver.zip)
sha256sums=('a72d640d301f128b0e719b92c86a7315caac52c0db658cef0329f764c2f15969')

build() {
  cd aws-c-io-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-io-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-io-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
