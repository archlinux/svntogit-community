# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.22.0
pkgrel=2
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL3)
depends=(
  glibc
  gumbo-parser
  jbig2dec
  libmupdf
  openjpeg2
  python
)
makedepends=(
  freetype2
  git
  libjpeg-turbo
  python-build
  python-distro
  python-installer
  python-setuptools
  python-wheel
  swig
  systemd
)
checkdepends=(
  python-fonttools
  python-pillow
  python-pytest
)
optdepends=(
  'python-fonttools: for building font subsets using fontTools'
  'python-pillow: for image file saving using pillow'
)
source=(
  https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz
)
sha512sums=('0e261411f1bf73785a537d4f276fcdcf73fa27de19cdd587d20404881ccda9bfc6f1c55b64af4805a5820cae81b932f7cf768f15f955a185d9c8f291371f6bb7')
b2sums=('298d1647d575b6a20f4bc8fecfdde747bfb398d0a228196668c9d9b826d55bb1dd257302ab6eee3cadc18d08d0bd1b12c2150d3d3d133e18267576ed0b61e62c')

prepare() {
  # remove bundled mupdf sources
  rm -frv $pkgname/*.tgz
  # we package swig and don't need python-swig
  sed -e 's/, "swig"//' -i $_name-$pkgver/pyproject.toml
}

build() {
  cd $_name-$pkgver
  # NOTE: build against system libmupdf
  export PYMUPDF_SETUP_MUPDF_BUILD=""
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    -vv
    # disable broken test, that upstream is not interested in fixing or disabling: https://github.com/pymupdf/PyMuPDF/issues/2127
    --deselect tests/test_pixmap.py::test_color_count
  )
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="$PWD/test_dir/$_site_packages:$PYTHONPATH"
  pytest "${pytest_options[@]}" tests/
}

package() {
  depends+=(
    freetype2 libfreetype.so
    libjpeg-turbo libjpeg.so
  )

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
