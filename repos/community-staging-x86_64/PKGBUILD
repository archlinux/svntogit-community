# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgname='python-bottleneck'
pkgver=1.2.1
pkgrel=6
pkgdesc="Fast NumPy array functions written in Cython"
arch=('x86_64')
url="https://github.com/kwgoodman/bottleneck"
license=('BSD')
makedepends=('python-setuptools')
depends=('python-numpy')
optdepends=('python-nose: needed for integrated test suite'
            'python-scipy: needed for integrated benchmarks')
source=("https://github.com/kwgoodman/bottleneck/archive/v${pkgver}.tar.gz")
sha256sums=('268a21f451f1fe3749619c64d36e9feda84fefdcb82a4701a3e63a9430874b22')

build() {
  cd "$srcdir"/bottleneck-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/bottleneck-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
