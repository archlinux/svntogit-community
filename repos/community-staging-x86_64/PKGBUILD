# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.17.0
pkgrel=3
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qca' 'qt5-script' 'qt5-webkit' 'libofx'
         'grantlee' 'kross' 'knewstuff' 'krunner' 'knotifyconfig'
         'sqlcipher' 'python' 'hicolor-icon-theme')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt5-tools' 'patch')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        sqlcipher4-fix.patch::https://cgit.kde.org/skrooge.git/patch/?id=b370575abe8d80b90e4552f1c027c9b58947a9f1)
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('19f2d7939b0333a95a129d110c11514f07a06f39b1ec08b42b5f43b6145b036d'
            'SKIP'
            '398806991fb099f8bbd72860f35a11b9ea88c8c5e3f6c6137c3f027087f584a5')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np1 -i "${srcdir}"/sqlcipher4-fix.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

#   export
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_LIBDIR=/usr/lib \
          -DSKG_DESIGNER=OFF
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
