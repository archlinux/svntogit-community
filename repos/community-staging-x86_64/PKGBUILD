# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.3.1
pkgrel=1
pkgdesc="Curses-style user interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc-libs' 'hledger' "haskell-hledger-lib" "haskell-ansi-terminal" "haskell-async"
         "haskell-base-compat" "haskell-cmdargs" "haskell-data-default" "haskell-fsnotify"
         "haskell-hunit" "haskell-microlens" "haskell-microlens-platform" "haskell-megaparsec"
         "haskell-pretty-show" "haskell-safe" "haskell-split" "haskell-text" "haskell-text-zipper"
         "haskell-vector" "haskell-brick" "haskell-vty")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2990e46739d1831a3848cb394133bfb676ee7cc0ef3fbd1271915dfc984a192544bbc3f3a594f820f4cff0a5935d682579979c5b57eeae4e16ec58edeaacf24e')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -e 's/, vty >= 5.5 && < 5.16/, vty >= 5.5/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    runhaskell Setup configure -O --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -f-oldtime -fthreaded
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
