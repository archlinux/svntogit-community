# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Tidal
pkgname=haskell-tidal
pkgver=1.7.10
pkgrel=13
pkgdesc="A domain specific language for live coding pattern."
arch=(x86_64)
url="https://tidalcycles.org/"
license=(GPL3)
groups=(pro-audio)
depends=(ghc ghc-libs glibc haskell-async haskell-bifunctors haskell-clock
haskell-colour haskell-hint haskell-hosc haskell-mwc-random haskell-network
haskell-primitive haskell-random haskell-vector supercollider)
makedepends=(uusi)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=("$pkgname-$pkgver.tar.gz::https://github.com/tidalcycles/${_name}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('0bf201e2344788f09c603163021213f40df016d1575c5ef44c9a0b23377d55290c771d268c291377fbb5b9c46cf1a49866e7dbf4a3e8eb9c0d6837b678bb1961')
b2sums=('51da728947966d282584bc8280fbfaeef822ba39b6bfdd99439fc3391aef582002703fa62a0e6990ede4786e69fb0c8fe4b5539efe82a3cb78fe5c5f4e74f547')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  uusi -u hosc $pkgname-$pkgver/tidal.cabal
}

build() {
  cd "$pkgname-$pkgver"
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir="/usr/share/doc/${pkgname}" \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd "$pkgname-$pkgver"
  runhaskell Setup test --show-details=direct
}

package() {
  cd "$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
