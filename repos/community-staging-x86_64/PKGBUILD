# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=DAV
pkgname=haskell-dav
pkgver=1.3.4
pkgrel=545
pkgdesc="RFC 4918 WebDAV support"
url="http://floss.scru.org/hDAV"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' "haskell-case-insensitive" "haskell-data-default"
         "haskell-http-client" "haskell-http-client-tls" "haskell-http-types"
         "haskell-lens" "haskell-network" "haskell-network-uri"
         "haskell-optparse-applicative" "haskell-transformers-base" "haskell-transformers-compat"
         "haskell-utf8-string" "haskell-xml-conduit" "haskell-xml-hamlet")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('95bb567488bed2fef3813597ea9c080781bbc25117995901ff7264c0f04bf4db1bad6c9c94c4d1fbea968245041752b2b720d4c42b4c23191eda5e3dc0c292b6')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-mtl-compat
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
