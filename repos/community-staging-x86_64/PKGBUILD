# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=soapyosmo
_pkgname=SoapyOsmo
pkgver=0.2.5
_gitver=soapy-osmo-$pkgver
pkgrel=16
pkgdesc='Soapy SDR plugins for OsmoSDR devices'
arch=('x86_64')
url='https://github.com/pothosware/SoapyOsmo'
license=('GPL3')
depends=('soapysdr' 'gnuradio-osmosdr' 'boost-libs')
makedepends=('cmake' 'boost' 'airspy' 'hackrf' 'libosmosdr' 'rtl-sdr') # 'bladerf'
optdepends=('airspy: AirSpy support'
            'hackrf: HackRF support'
            'libosmosdr: OsmoSDR support'
            'rtl-sdr: RTL-SDR support')
#            'bladerf: BladeRF support'
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz"
        boost-1.73.patch)
sha512sums=('a1aa5f3c84f5e9cf8cc0329445836162586c3fd301f126310e2a3131c170f1d213fcb214162a28c448a49f57fcf177b7a00d10967ba88a8434a6ac26fa2264e8'
            '98c9152445c919618dbc31e0056129e0734b60cbf2ee86903c6b7206883e8b6ba64dbe64e96fdebdb3f18abbb3b68aefa19ba3b177664d4fa61995be4c05ef64')

prepare() {
  cd $_pkgname-$_gitver
  patch -Np1 -i ../boost-1.73.patch
  sed -i 's|#ADD_DEFINITIONS(-Wconversion)|SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pthread")|' CMakeLists.txt
  sed -i 's|SET(BOOST_REQUIRED_COMPONENTS|SET(BOOST_REQUIRED_COMPONENTS chrono|' CMakeLists.txt gr-osmosdr/CMakeLists.txt
}

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DUSE_OSMO_RTLSDR=ON \
  	-DUSE_OSMO_HACKRF=ON \
  	-DUSE_OSMO_AIRSPY=ON

#  	-DUSE_OSMO_BLADERF=ON

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install
}
