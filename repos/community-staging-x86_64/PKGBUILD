# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=zip-archive
pkgname=haskell-zip-archive
pkgver=0.3.2
pkgrel=1
pkgdesc="Library for creating and modifying zip archives."
url="https://github.com/jgm/zip-archive"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-digest' 'haskell-mtl' 'haskell-old-time' 'haskell-temporary'
         'haskell-text' 'haskell-zlib')
makedepends=('ghc' 'haskell-hunit')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jgm/zip-archive/archive/$pkgver.tar.gz")
sha512sums=('d68679b81d83544c23267f84c7ddccc295f35a19aee9836442e53d2e2b5f5fef4f5ca4560cb9529661ab31ada06b862c8d18da9f66321bb30d7613c922ad9eba')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-executable -fsplitbase
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
