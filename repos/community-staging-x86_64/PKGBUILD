# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cryptol
pkgver=2.5.0
pkgrel=12
pkgdesc="The Language of Cryptography"
url="http://www.cryptol.net"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'z3' 'haskell-base-compat' 'haskell-async' 'haskell-gitrev' 'haskell-graphscc'
         'haskell-heredoc' 'haskell-monad-control' 'haskell-monadlib' 'haskell-old-time'
         'haskell-presburger' 'haskell-quickcheck' 'haskell-random' 'haskell-sbv' 'haskell-smtlib'
         'haskell-simple-smt' 'haskell-syb' 'haskell-text' 'haskell-tf-random'
         'haskell-transformers-base' 'haskell-utf8-string' 'haskell-mtl' 'haskell-ansi-terminal')
makedepends=('ghc' 'alex' 'happy')
source=("https://hackage.haskell.org/packages/archive/$pkgname/${pkgver}/$pkgname-${pkgver}.tar.gz")
sha512sums=('e528c2b75853f559a0e122286734fce373150a458a5af2a74020240629f9f550747f30f50c16d261326e94f4060497af43951f0253dc6885043cf21aab849265')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-static -f-relocatable
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
