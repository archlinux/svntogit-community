# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: vande198
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=supertux
pkgver=0.6.3
pkgrel=6
pkgdesc="A classic 2D jump'n'run sidescroller game in a style similar to the original SuperMario games"
arch=('x86_64')
url="https://github.com/supertux/supertux"
license=('GPL')
depends=('sdl2_image' 'curl' 'openal' 'libvorbis' 'glew' 'physfs' 'freetype2' 'boost-libs' 'hicolor-icon-theme')
makedepends=('cmake' 'boost' 'mesa' 'optipng' 'glm')
options=('!lto')
source=("https://github.com/SuperTux/supertux/releases/download/v${pkgver}/SuperTux-v${pkgver}-Source.tar.gz"
        gcc12.patch
        missing-include-for-FLT_EPSILON.patch)
sha512sums=('c6540bab1b3befbd975756031c4587e5569d9613d9539dc829c728b574d1a4da92816d6a7e68947b32963cc13d9b8b52312701c199138640e9f89e5885433798'
            'a6ff1a0e126a9fcb64a8a86b24dd6622fdb91f156f21504ad87b5b6ab642f5935740b183e62e7b9b764f0be8f6733e9b7fcd679097d6c33e28a71b577912a9c8'
            '4e4116cb0dc7f996cdb96484eac99bd7f5a16829f6b082cc2e4c17b22e6e2f34501b8e3e4caf3c956369c5cd6caa12179b8a4cb1f93c2c8c8b014e3829272cb3')

prepare() {
  cd "SuperTux-v${pkgver}-Source"
  patch -Np1 -i ../gcc12.patch
  patch -Np1 -i ../missing-include-for-FLT_EPSILON.patch
}

build() {
  cd "SuperTux-v${pkgver}-Source"

  cmake \
      -DENABLE_DISCORD=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DINSTALL_SUBDIR_BIN=bin \
      -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "SuperTux-v${pkgver}-Source"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include
}
