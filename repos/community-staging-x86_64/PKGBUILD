# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=status-notifier-item
pkgname=haskell-status-notifier-item
pkgver=0.3.1.0
pkgrel=42
pkgdesc="A wrapper over the StatusNotifierItem/libappindicator dbus specification"
url="https://github.com/IvanMalison/status-notifier-item#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-byte-order' 'haskell-bytestring-to-vector' 'haskell-dbus'
         'haskell-dbus-hslogger' 'haskell-hslogger' 'haskell-lens' 'haskell-optparse-applicative'
         'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('49b5eb5c94e93f5dd7eb9d708d99bbf2904895af4bc0e572ac85eeb484c27ff4')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
