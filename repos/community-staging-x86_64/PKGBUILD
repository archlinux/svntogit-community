# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=scotty
pkgname=haskell-scotty
pkgver=0.12
pkgrel=26
pkgdesc="Haskell web framework inspired by Ruby's Sinatra, using WAI and Warp"
url="https://github.com/scotty-web/scotty"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-compat-batteries' 'haskell-blaze-builder'
         'haskell-case-insensitive' 'haskell-data-default-class' 'haskell-http-types'
         'haskell-monad-control' 'haskell-network' 'haskell-regex-compat'
         'haskell-transformers-base' 'haskell-transformers-compat' 'haskell-wai' 'haskell-wai-extra'
         'haskell-warp')
makedepends=('ghc' 'uusi' 'haskell-async' 'haskell-hspec' 'haskell-hspec-discover' 'haskell-hspec-wai'
             'haskell-lifted-base')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('e1d77ee05eaa4b1871566b33683da9ab15dda8f7c42875701d62caf7db7defd2')

prepare() {
  cd $_hkgname-$pkgver
  sed -i '/nats/d;/fail/d' $_hkgname.cabal
  uusi $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
