# Maintainer: Ivy Foster <iff@archlinux.org>

pkgbase=bemenu
pkgname=(bemenu bemenu-ncurses bemenu-x11 bemenu-wlroots)
pkgver=0.5.0
pkgrel=3

# Wayland without wlroots is not supported.
# https://github.com/Cloudef/bemenu/issues/79#issuecomment-572867783
pkgdesc='Dynamic menu library and client program inspired by dmenu'
url='https://github.com/Cloudef/bemenu'
arch=(x86_64)
license=(GPL3 LGPL3)

makedepends=(libxinerama libxkbcommon ncurses pango wayland wayland-protocols wlroots)

source=(
	"bemenu-$pkgver.tar.gz::https://github.com/Cloudef/bemenu/archive/$pkgver.tar.gz"
	"bemenu-$pkgver.tar.gz.asc::https://github.com/Cloudef/bemenu/releases/download/$pkgver/bemenu-$pkgver.tar.gz.asc"
)
# Checksums provided by packager
sha256sums=(
	56e4a0bd06f93e7dfcdabbf7ee1ed973ce4e77d648132064f26f30662c4055f1
	SKIP
)
validpgpkeys=(08C8361E825F7502797E874F0CBD2CD395613887)

build() {
	cd "bemenu-$pkgver"
	make PREFIX=/usr
}

# make test seems to have been removed with cmake
#check() {
#	cd "bemenu-$pkgver"
#	make test
#}

package_bemenu() {
	depends=(bemenu-renderer)
	provides=(libbemenu)

	cd "bemenu-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install
	rm -rf "$pkgdir"/usr/lib/bemenu/
}

package_bemenu-ncurses() {
	pkgdesc='ncurses renderer for bemenu'
	provides=(bemenu-renderer)
	depends=(libbemenu ncurses)

	cd "bemenu-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install-renderers
	rm -f "$pkgdir"/usr/lib/bemenu/bemenu-renderer-{wayland,x11}.so
}

package_bemenu-wlroots() {
	pkgdesc='Wayland (wlroots-based compositors) renderer for bemenu'
	provides=(bemenu-renderer bemenu-wayland)
	depends=(libbemenu libxkbcommon pango wayland wayland-protocols wlroots)

	cd "bemenu-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install-renderers
	rm -f "$pkgdir"/usr/lib/bemenu/bemenu-renderer-{curses,x11}.so
}

package_bemenu-x11() {
	pkgdesc='X11 renderer for bemenu'
	provides=(bemenu-renderer)
	depends=(libbemenu libxinerama pango)

	cd "bemenu-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install-renderers
	rm -f "$pkgdir"/usr/lib/bemenu/bemenu-renderer-{curses,wayland}.so
}
