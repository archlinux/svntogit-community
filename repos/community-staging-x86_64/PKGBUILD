# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=nageru
pkgver=2.2.0
pkgrel=2
pkgdesc="live video mixer, based around the standard M/E workflow."
arch=(x86_64)
url="https://nageru.sesse.net/"
license=('GPL3')
depends=('libmicrohttpd' 'ffmpeg' 'libx264' 'protobuf' 
         'movit' 'bmusb' 'qcustomplot' 'pciutils' 'zita-resampler'
         'sqlite3' 'luajit' 'cmake')
makedepends=('meson' 'eigen' 'qt5-tools')
source=("https://nageru.sesse.net/nageru-${pkgver}.tar.gz")
sha256sums=('cbdde4d0604b5c241b5a9f876375692a57b0da4535d4b3bbd944e80bd8dcfece')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build -v
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
