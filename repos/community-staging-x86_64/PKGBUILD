# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=rebase
pkgname=haskell-rebase
pkgver=1.2.3
pkgrel=34
pkgdesc='A more progressive alternative to the "base" package'
url="https://github.com/nikita-volkov/rebase"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-stm' 'haskell-hashable' 'haskell-vector' 'haskell-unordered-containers'
         'haskell-text' 'haskell-scientific' 'haskell-uuid' 'haskell-dlist' 'haskell-void'
         'haskell-bifunctors' 'haskell-profunctors' 'haskell-contravariant'
         'haskell-contravariant-extras' 'haskell-semigroups' 'haskell-semigroupoids' 'haskell-mtl'
         'haskell-either' 'haskell-base-prelude')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c6b94c46e1f1f57d4f344fb4a827e498ecd39a2e24525a3aad6e627e4bea265cfe36c3b686ab2ab16f053ad844adfccebf781314eebaaf6ad00c416dfb60c860')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/fail/d' $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
