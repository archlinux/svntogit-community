# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=aws
pkgname=haskell-aws
pkgver=0.21.1
pkgrel=176
pkgdesc="Amazon Web Services (AWS) for Haskell"
url="https://github.com/aristidb/aws"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-base64-bytestring' 'haskell-blaze-builder' 'haskell-byteable'
         'haskell-case-insensitive' 'haskell-cereal' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-cryptonite' 'haskell-data-default' 'haskell-exceptions' 'haskell-http-conduit'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-lifted-base' 'haskell-memory'
         'haskell-monad-control' 'haskell-network' 'haskell-old-locale' 'haskell-resourcet'
         'haskell-safe' 'haskell-scientific' 'haskell-tagged' 'haskell-unordered-containers'
         'haskell-utf8-string' 'haskell-vector' 'haskell-xml-conduit')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-errors' 'haskell-http-client'
             'haskell-http-client-tls' 'haskell-quickcheck-instances' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-transformers-base')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        aws-ghc-8.8.patch::https://github.com/aristidb/aws/pull/263.patch)
sha512sums=('ab22fd2113b579404c218abbef42ebc04e3b5d90c4a1ecc4ad36aa720c8071c07d8b49bd2441bc07d1383122cfdd5e95a04fad6929adedb66e5d5b684eed2f37'
            '12c5c3a7788106e2a7a47140260a04ab72d55a016e2eaf34f50374d21127521ed65128848de3a7ef0357caec153d5fad104e3575466e501ff5d3baccecb164b4')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../aws-ghc-8.8.patch
    sed -i 's/< *0.6/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-examples
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Requires AWS credentials to test"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
