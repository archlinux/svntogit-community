# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Michael Schubert <mschu.dev at gmail>
# Contributor: Panagiotis Mavrogiorgos (pmav99) <> (gmail)

pkgbase=python-bottleneck
pkgname=('python2-bottleneck' 'python-bottleneck')
pkgver=1.2.1
pkgrel=2
pkgdesc="Fast NumPy array functions written in Cython"
arch=('x86_64')
url="http://berkeleyanalytics.com/bottleneck/"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools' 'python-numpy' 'python2-numpy' 'cython' 'cython2')
source=("https://github.com/kwgoodman/bottleneck/archive/v${pkgver}.tar.gz")
md5sums=('9f20fed3625ab995a8344e7b7fa0406f')

build() {
  cd "$srcdir"
  cp -a bottleneck-${pkgver} bottleneck-py2-${pkgver}

  msg "Building Python2"
  cd "$srcdir"/bottleneck-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/bottleneck-${pkgver}
  python setup.py build
}

package_python2-bottleneck() {
  depends=('python2-numpy')
  optdepends=('python2-nose: needed for integrated test suite'
              'python2-scipy: needed for integrated benchmarks')

  cd "$srcdir"/bottleneck-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-bottleneck() {
  depends=('python-numpy')
  optdepends=('python-nose: needed for integrated test suite'
              'python-scipy: needed for integrated benchmarks')

  cd "$srcdir"/bottleneck-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

