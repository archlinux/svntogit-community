# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: SaultDon <sault.don gmail>
pkgname=pdal
pkgver=2.2.0
pkgrel=2
pkgdesc="A C++ library for translating and manipulating point cloud data"
arch=('x86_64')
url="http://www.pdal.io"
license=('BSD')
depends=('gdal' 'libgeotiff' 'laszip' 'jsoncpp' 'hexer-hobu' 'libunwind')
makedepends=('cmake' 'python' 'python-numpy' 'laz-perf' 'ninja')
optdepends=('python-numpy: for the Python plugin'
            'sqlite: for the sqlite plugin'
            'postgresql-libs: for the postgresql plugin')
provides=('pdal')
source=("https://github.com/PDAL/PDAL/releases/download/${pkgver}/PDAL-${pkgver}-src.tar.gz")
sha512sums=('c54770973de714473bf6cfe6a69e2c880d516abcf71cce755495304093acb41471b4264cb84f11973a482f3de80a8922d7d8b54994fb91c6b012a6894a8bdac5')

build() {
    cd "PDAL-$pkgver-src/"

    cmake \
        -B build \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_PLUGIN_PYTHON=ON \
        -DBUILD_PLUGIN_PGPOINTCLOUD=ON \
        -DBUILD_PLUGIN_SQLITE=ON \
        -DWITH_LAZPERF=ON \
        -DWITH_LASZIP=ON \
        -DWITH_COMPLETION=ON

    ninja -C build
}

package() {
    cd "PDAL-$pkgver-src"

    DESTDIR="$pkgdir/" ninja -C build install

    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
