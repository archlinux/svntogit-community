# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.8.18
pkgrel=2
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=('LGPL3')
depends=('glibc' 'gcc-libs' 'libmediainfo' 'qt5-declarative' 'qt5-multimedia'
         'qt5-svg' 'quazip-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('622441497a5d7c96dd91e6eb93c946fe09c0461a81089cd9bd5e8e5a43c06004')
b2sums=('bbc0544dc8ed9729bfd28ed06b9fbfc569588f540ddd9b960bb03db7e3d04989d975bd4a3fb1f991661901faab505151b4933d3b4e024279dc95f160c7914156')

build() {
  cd MediaElch-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DDISABLE_UPDATER=ON \
    -DUSE_EXTERN_QUAZIP=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd MediaElch-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
