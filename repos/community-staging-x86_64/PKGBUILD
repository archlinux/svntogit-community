# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-io-tty
pkgver=1.16
pkgrel=3
pkgdesc="Provide an interface to TTYs and PTYs"
arch=('x86_64')
url="https://search.cpan.org/dist/IO-Tty/"
license=("GPL" "PerlArtistic")
depends=('glibc')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/T/TO/TODDR/IO-Tty-$pkgver.tar.gz")
sha256sums=('8f1a09c070738adc695df903f2e7f74308dd8d991b914c0bc390a0e6021294dd')

build() {
  cd "$srcdir"/IO-Tty-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}
package() {
  cd "$srcdir"/IO-Tty-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
