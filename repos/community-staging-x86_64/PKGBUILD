# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=srslte
pkgname=($pkgbase $pkgbase-avx2)
_pkgname=srsRAN
pkgver=22.04.1
_pkgver=release_${pkgver//./_}
pkgrel=1
pkgdesc='Open source SDR LTE software suite from Software Radio Systems (SRS)'
arch=('x86_64')
url='https://www.srslte.com'
license=('AGPL3')
depends=('fftw' 'mbedtls' 'boost-libs' 'lksctp-tools' 'libconfig'
         'libuhd' 'soapysdr' 'bladerf' 'zeromq' 'pcsclite' 'srsgui')
makedepends=('cmake' 'boost')
provides=('srsue' 'srsenb' 'srsenb')
conflicts=('srsue' 'srsenb' 'srsenb')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/srsran/srsRAN/archive/$_pkgver.tar.gz")
sha512sums=('3f8e3765df8740be4a33457735bfd0f79e42a521076b7226020b4682eb3ddd2ff4fbeffb0eff56b1eddf43252963fc5899d261e6c30a7a558dd4b7667ee93f6d')
options=(!lto)

prepare() {
  cd $_pkgname-$_pkgver

  # Using too old of a version to even attempt to properly fix these
  sed -e '/-Werror\"/d' -i CMakeLists.txt
  CXXFLAGS+=' -Wno-error -Wp,-U_GLIBCXX_ASSERTIONS'

  # Disable failing test
  sed -i '/add_test(ue_rf_failure/d' srsue/src/CMakeLists.txt

  mkdir build

  cp -r "$srcdir"/$_pkgname-$_pkgver "$srcdir"/$_pkgname-avx2-$_pkgver
}

build() {
  echo 'Building srslte...'
  cd "$srcdir"/$_pkgname-$_pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_GENERIC=True

  make

  echo 'Building srslte-avx2...'
  cd "$srcdir"/$_pkgname-avx2-$_pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_GENERIC=True \
    -DHAVE_AVX2=True

  make
}

check() {
  echo 'Running tests for srslte...'
  cd "$srcdir"/$_pkgname-$_pkgver/build

# network_utils_test fails on build server
  ctest $(echo $MAKEFLAGS | grep -Po '\-j[0-9][0-9]*') -E network_utils_test

  echo 'Running tests for srslte-avx2...'
  cd "$srcdir"/$_pkgname-avx2-$_pkgver/build

  ctest $(echo $MAKEFLAGS | grep -Po '\-j[0-9][0-9]*') -E network_utils_test
}

package_srslte() {
  install=$pkgname.install

  cd $_pkgname-$_pkgver/build

  make DESTDIR="$pkgdir" install
}

package_srslte-avx2() {
  pkgdesc="$pkgdesc (with AVX2 extensions)"
  provides=($pkgbase)
  conflicts=($pkgbase)
  install=$pkgname.install

  cd $_pkgname-avx2-$_pkgver/build

  make DESTDIR="$pkgdir" install
}
