# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.15
pkgrel=153
pkgdesc='Lightweight X11 tiled window manager written in Haskell'
url='https://xmonad.org/'
arch=('x86_64')
license=('BSD')
depends=('ghc' 'haskell-x11' 'sh' 'haskell-utf8-string' 'haskell-extensible-exceptions'
         'haskell-data-default' 'haskell-setlocale')
makedepends=('gendesk' 'uusi')
optdepends=('xorg-xmessage: for displaying visual error messages')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/xmonad/xmonad/archive/v${pkgver}.tar.gz
        $pkgname-ghc9.patch::https://github.com/xmonad/xmonad/commit/2a1a18023a15d13247148638edf29ca6bf9c3bce.patch
        dynamic-compilation.patch
        xmonad.svg)
sha512sums=('07bf61a1355548679d264e62423dfe6c4ee886aff28ac748588aa7f4f9a4f1bf2afabb1104451fa56f5a3989d79b6044f479d2804e416940e02f52f18b443996'
            '9b4d20ec1bc24770981ff4848734074d75ffe474d641fa3f3e17e6507bce2aaa0684638a1778cbeea0b209d9cd06793abc8f62fbc0dad0e196f866390bb878b3'
            'abc598ebc4b55917186cd4d549fc970f53905f820d394ada9a092c08d8833050ad18b23e2f4fd7f281487da2db5b0ca058600e995ac97b18d3be4c62071562d6'
            '31f0ceb724a9931aeda75ab706c8908b5d8341af4df50ab1d749814baacb748ad3622601cd99ac03bf936b698fcd854b23d1541e1772306e3337a13a79b110b6')

prepare() {
  gendesk --pkgname "${pkgname}" --pkgdesc "${pkgdesc}"
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/dynamic-compilation.patch"
  patch -p1 -i "${srcdir}/$pkgname-ghc9.patch"
  uusi -u X11 ${pkgname}.cabal
}

build() {
  cd ${pkgname}-${pkgver}

  runhaskell Setup configure -O \
    --enable-shared \
    --enable-executable-dynamic \
    --disable-library-vanilla \
    --prefix=/usr \
    --dynlibdir=/usr/lib \
    --docdir="/usr/share/doc/${pkgname}" \
    --datasubdir="${pkgname}" \
    --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"

  runhaskell Setup.lhs copy --destdir="${pkgdir}"

  # Requires pandoc, regex-posix, Pretty and cabal
  #runhaskell util/GenerateManpage.hs

  install -Dm 644 man/xmonad.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/xmonad"
  install -Dm 644 "$srcdir/xmonad.svg" -t "${pkgdir}/usr/share/pixmaps"
  install -Dm 644 "$srcdir/xmonad.desktop" -t "${pkgdir}/usr/share/xsessions"
}

# vim: ts=2 sw=2 et:
