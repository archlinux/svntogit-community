# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hxt
pkgname=haskell-hxt
pkgver=9.3.1.16
pkgrel=7
pkgdesc="A collection of tools for processing XML with Haskell."
url="https://github.com/UweSchmidt/hxt"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hxt-charproperties' 'haskell-hxt-regex-xmlschema' 'haskell-hxt-unicode'
         'haskell-network-uri' 'haskell-mtl' 'haskell-parsec')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('6cc99a3b9d1ffe33002d35a5c0965919227835e0fb5b0b829b49232443dec175428dab66720a717f8a655e0de9ad4c77c501aa6d3982b43024cb3a866cd12846')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
