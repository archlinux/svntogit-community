# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=websockets
pkgname=haskell-websockets
pkgver=0.12.7.1
pkgrel=10
pkgdesc="A sensible and clean way to write WebSocket-capable servers in Haskell."
url="https://hackage.haskell.org/package/${_hkgname}"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-case-insensitive' 'haskell-clock' 'haskell-entropy' 'haskell-network'
         'haskell-random' 'haskell-sha' 'haskell-streaming-commons')
makedepends=('ghc' 'haskell-hunit' 'haskell-test-framework' 'haskell-test-framework-hunit'
             'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('911464ee2726cd2b328b9793aa09d64ec0d342fdc25d05e244717256ea42deb08bc9ee181677939fccf67489caff50c5867c5f0a83e25cfd82a0027f85ad5e54')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/bytestring-builder/d;s/< *2.14/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-Example
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENCE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENCE"
}
