# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: yk <yk at archlinuxcn dot org>

pkgname=libqtshadowsocks
pkgver=2.0.2
pkgrel=3
pkgdesc="A lightweight library for shadowsocks"
arch=("x86_64")
url="https://github.com/shadowsocks/libQtShadowsocks"
license=("GPL")
depends=('qt5-base' 'botan')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/libQtShadowsocks/archive/v$pkgver.tar.gz")
sha512sums=('7ec1a4d96d86dc419938d39e62b75e2c589304aea74e7648a8d3449731f944bcd47501a96df2473fc6e6e84b232930be92631ecdaf3b0017bfe9d16a9c82dd33')

build() {
  cd libQtShadowsocks-$pkgver
  cmake -DUSE_BOTAN2=On -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd libQtShadowsocks-$pkgver
  make DESTDIR="$pkgdir" install
}
