# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fuzzel
pkgver=1.7.0
pkgrel=1
pkgdesc='Application launcher for wlroots based Wayland compositors'
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
makedepends=(meson
             scdoc
             tllist
             wayland-protocols)
depends=(cairo
         fcft
         libpng
         libxkbcommon
         pixman
         wayland) # nanosvg is bundled
options=(debug)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('f792c9869ec2ab333e99ff49d3e94a81c7ffc45767b74ed98c63404f558a008b')

build() {
	cd "$pkgname"
	arch-meson \
		-Denable-cairo=enabled \
		-Dpng-backend=libpng \
		-Dsvg-backend=nanosvg \
		. build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
