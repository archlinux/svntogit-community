# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-tactics-plugin
pkgname=haskell-hls-tactics-plugin
pkgver=1.0.0.0
pkgrel=24
pkgdesc="Wingman plugin for Haskell Language Server"
url="https://haskellwingman.dev"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-extra' 'haskell-fingertree' 'haskell-generic-lens'
         'haskell-ghc' 'haskell-ghc-exactprint' 'haskell-ghc-source-gen' 'haskell-ghcide'
         'haskell-hls-plugin-api' 'haskell-lens' 'haskell-lsp' 'haskell-refinery' 'haskell-retrie'
         'haskell-shake' 'haskell-syb')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-checkers' 'haskell-data-default'
             'haskell-hie-bios' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-hspec-expectations' 'haskell-lsp-test' 'haskell-lsp-types'
             'haskell-megaparsec' 'haskell-tasty' 'haskell-tasty-ant-xml'
             'haskell-tasty-expected-failure' 'haskell-tasty-golden' 'haskell-tasty-hunit'
             'haskell-tasty-rerun')
# Hackage tarball is missing test data
#source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
source=("https://github.com/haskell/haskell-language-server/archive/ghcide-v1.1.0/haskell-language-server-ghcide-v1.1.0.tar.gz")
sha256sums=('ec611943b46027488d85135eb61245ea7cbd892ed9825b1456a4b74e0eb5a713')

prepare() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname
  gen-setup
}

build() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname
  PATH="$PWD/dist/build/test-server:$PATH" LD_LIBRARY_PATH="$PWD/dist/build" runhaskell Setup test --show-details=direct
}

package() {
  cd haskell-language-server-ghcide-v1.1.0/plugins/$_hkgname

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  rm "$pkgdir"/usr/bin/test-server
  rmdir "$pkgdir"/usr/bin
}
