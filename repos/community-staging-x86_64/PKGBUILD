# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=hamlib
pkgver=4.5.4
pkgrel=2
pkgdesc='Ham radio equipment control libraries'
arch=('x86_64')
url='https://hamlib.github.io'
license=('GPL' 'LGPL')
depends=('perl' 'python' 'lua' 'tcl' 'libxml2' 'libusb-compat' 'libtool' 'libuhd' 'libindi')
makedepends=('swig')
source=("https://github.com/Hamlib/Hamlib/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('882fb517a3e5854cdaad506520e96c7736214fe27d1ac053f0510e3c5c08e1bf40217442f519d1c4be92e3d05135c7bd90bc0d60f334f52994e69b2ce9d3f442'
            'SKIP')
validpgpkeys=('82D64F6B0E67CD41F689BBA6FB2C5130D55A8819') # Nate Bargmann <n0nb@n0nb.us>

build() {
  cd $pkgname-$pkgver

  ./configure \
  	--prefix=/usr \
  	--sbindir=/usr/bin \
  	--with-perl-binding \
  	--with-python-binding \
  	--with-tcl-binding \
  	--with-lua-binding \
  	--with-xml-support \
  	--disable-uhd  # https://github.com/Hamlib/Hamlib/issues/355

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
