# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=xmonad-utils
pkgver=0.1.3.3
pkgrel=86
pkgdesc='Small collection of X utilities'
url='https://hackage.haskell.org/package/xmonad-utils'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ghc-libs' 'gmp' 'libffi' 'libxrandr' 'haskell-x11' 'haskell-random' 'haskell-unix'
         'libxcrypt' 'libcrypt.so' 'libx11')
makedepends=('ghc')
options=('!emptydirs')
source=(https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('45a5db08e2e8fce4a11f929107c29d5073b897cbe83af0ea24ee2e691b72e2f8')
sha512sums=('f969a0b75107266ec5f74edc06075a1ede72857311d8e0494bf5b567d18e0a9d6e5b046dbb2a34783c2eafc18be9576b86aa78415306974718031a7dd652418c')

build() {
  cd ${pkgname}-${pkgver}
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
        --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
}

package() {
  cd ${pkgname}-${pkgver}
  runhaskell Setup copy --destdir="${pkgdir}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
