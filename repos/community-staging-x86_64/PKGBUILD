# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=3.2.0.0
pkgrel=48
pkgdesc="The command-line interface for Cabal and Hackage."
url="https://hackage.haskell.org/package/cabal-install"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base16-bytestring' 'haskell-cryptohash-sha256'
         'haskell-echo' 'haskell-edit-distance' 'haskell-hackage-security' 'haskell-hashable'
         'haskell-http' 'haskell-network' 'haskell-network-uri' 'haskell-random' 'haskell-resolv'
         'haskell-tar' 'haskell-zlib' 'haskell-lukko')
makedepends=('ghc')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz"
        $pkgname-ghc8.10.patch::https://github.com/haskell/cabal/pull/6711.patch)
sha512sums=('ad937b2df42b688e4608aa2b91a25daf1042e5573626202e5182cbfeca9acc5933194853405a6396c60d0d28d6d0d5c5276fd85cb6acf4be2c8cd12afe747062'
            '9e7245f5080e9c1609994b21d13ecbacb78fa9314d80425464f714f9bd68910d3fb4a5101b582461a89ec0b7115ef6acb2eb4ffba7a2477f65dca192f1eac1e7')

prepare() {
    cd $pkgname-$pkgver
    patch -p2 -i ../$pkgname-ghc8.10.patch || :
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/${pkgname}"
  runhaskell Setup build $MAKEFLAGS
}

package() {
  cd $pkgname-$pkgver
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
