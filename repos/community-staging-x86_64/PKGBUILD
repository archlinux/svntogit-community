# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cmarkgfm
pkgname=(python-cmarkgfm python2-cmarkgfm)
pkgver=0.4.2
pkgrel=2
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('x86_64')
makedepends=('cmark-gfm' 'python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/theacodes/cmarkgfm/archive/$pkgver.tar.gz")
sha512sums=('afaf475c54abdb19eaae45be1e4488552d3ef47bfcdaad83ceb84f49230731643bd0c58640b3c51e199d4a449043e3ae5a70abdf676ee5b4b8064825390c0406')

prepare() {
  # Will commit this to upstream some time later
  cd cmarkgfm-$pkgver
  sed -e 's/cmark.h/cmark-gfm.h/' -e 's/cmark_extension_api.h/cmark-gfm-extension_api.h/' \
      -e 's/core-extensions.h/cmark-gfm-core-extensions.h/' \
      -i src/cmarkgfm/cmark_module.h
  sed -e 's/core_extensions_ensure_registered/cmark_gfm_core_extensions_ensure_registered/' \
      -i src/cmarkgfm/cmark.cffi.h
  sed -e 's/lib.core_extensions_ensure_registered/lib.cmark_gfm_core_extensions_ensure_registered/' \
      -i src/cmarkgfm/cmark.py
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd cmarkgfm-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-cmarkgfm() {
  depends=('cmark-gfm' 'python-cffi')

  cd cmarkgfm-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-cmarkgfm() {
  depends=('cmark-gfm' 'python2-cffi')

  cd cmarkgfm-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
