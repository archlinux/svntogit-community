# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-mitmproxy-wireguard
_pyname=mitmproxy_wireguard
pkgver=0.1.21
pkgrel=2
pkgdesc='WireGuard frontend for mitmproxy'
arch=('x86_64')
url='https://github.com/decathorpe/mitmproxy_wireguard'
license=('MIT')
depends=('python')
makedepends=('maturin' 'python-installer' 'cargo')
options=(!lto)
source=("https://github.com/decathorpe/mitmproxy_wireguard/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('fb4c072a6e5807d8f30f28bb1f15414b5a68cc302aa66f6e0ee5df1da391a26298f5efe82e99d62f4d44cebcad89c5631b2ab6b94f51c1f0dbc8fa485fd61f69')

build() {
  cd ${_pyname}-${pkgver}
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd ${_pyname}-${pkgver}
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  python -c 'import mitmproxy_wireguard'
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
