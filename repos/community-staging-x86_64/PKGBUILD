# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=QuickCheck
pkgname=haskell-quickcheck
pkgver=2.12.6.1
pkgrel=1
pkgdesc='Automatic testing of Haskell programs'
url='https://hackage.haskell.org/package/QuickCheck'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-erf' 'haskell-random' 'haskell-tf-random')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('bbd11ea7f22af02ad1ab4d59aecc0dc57d3b9d63e42c1a73beff94f645778858fe22d8d8234669c96ce901508b85d3b42b2f7647b81510bcdd9eaada937016c1')

build() {
  cd "$srcdir/$_hkgname-$pkgver"

  runhaskell Setup configure \
    -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/$pkgname" \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    -ftemplateHaskell

  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script

  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/$_hkgname-$pkgver"

  install -D -m744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -D -m744 unregister.sh "$pkgdir/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
