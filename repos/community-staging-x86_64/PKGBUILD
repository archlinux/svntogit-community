# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=unicode-collation
pkgname=haskell-unicode-collation
pkgver=0.1.3.3
pkgrel=4
pkgdesc="Haskell implementation of the Unicode Collation Algorithm"
url="https://github.com/jgm/unicode-collation"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-lift-instances')
makedepends=('ghc' 'uusi' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-unicode-transforms')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('bbb8fb3d52fa4906ddb92fb605f49b4c726cfcffd1f950c8a78bc495594a2e7d85fa78e2009da850f0218fce09a57f829ce0f3d6a6000b9b6484754f8c8260e8')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
