# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=r2ghidra
_gitcommit=9b2687d08e27bee754da0efd83f8ed4d26eb485b
_ghidra_native=6ce2dc2fee1ec1262bd0622c879ded8cb74e4ead
pkgver=5.8.2
pkgrel=1
pkgdesc='Deep ghidra decompiler integration for radare2 and iaito'
url='https://github.com/radareorg/r2ghidra'
arch=('x86_64')
license=('LGPL3')
depends=('radare2' 'glibc')
makedepends=('git')
provides=('radare2-r2ghidra')
replaces=('r2ghidra-dec')
source=("git+https://github.com/radareorg/r2ghidra#commit=${_gitcommit}"
        "git+https://github.com/radareorg/ghidra-native.git#commit=${_ghidra_native}"
        git+https://github.com/zeux/pugixml.git)
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --match '5*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git -c protocol.file.allow=always submodule update --recursive
  ln -sf "${srcdir}/ghidra-native" ghidra-native
}

build() {
  cd ${pkgname}
  make -C ghidra-native patch
  ./configure PREFIX=/usr
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="$pkgdir" install
  r2version=$(r2 -v|grep radare2|sed -E 's/radare2 ([0-9\.]+) .*/\1/')
  ln -s /usr/lib/radare2/${r2version} "${pkgdir}/usr/lib/radare2/last"
}

# vim: ts=2 sw=2 et:
