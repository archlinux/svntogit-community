# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=reducers
pkgname=haskell-reducers
pkgver=3.12.3
pkgrel=135
pkgdesc="Semigroups, specialized containers and a general map/reduce framework"
url="https://github.com/ekmett/reducers/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-fingertree" "haskell-hashable" "haskell-semigroupoids"
         "haskell-unordered-containers")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7bb78ccf107d7bc6bce8d68c3766b799ce2c2a41587ee4d2b7197c2dae1d27a6ce47110f1abd655108d7fcc306e9069a25562e53b00ff97704af0b8aac9139d9')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/semigroups/d' -e 's/< *1.3/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
