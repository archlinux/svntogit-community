# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger
pkgver=1.22.2
pkgrel=39
pkgdesc="Command-line interface for the hledger accounting system"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-base-compat-batteries'
         'haskell-cmdargs' 'haskell-data-default' 'haskell-decimal' 'haskell-diff' 'haskell-githash'
         'haskell-hashable' 'haskell-hledger-lib' 'haskell-lucid' 'haskell-math-functions'
         'haskell-megaparsec' 'haskell-old-time' 'haskell-regex-tdfa'
         'haskell-safe' 'haskell-shakespeare' 'haskell-split' 'haskell-tabular' 'haskell-tasty'
         'haskell-temporary' 'haskell-unordered-containers' 'haskell-utf8-string'
         'haskell-utility-ht' 'haskell-wizards')
makedepends=('ghc' 'uusi' 'haskell-extra' 'haskell-timeit')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('dc60c6f4ac7e9934ccb09293c447cb7af0f91f8ebae32233046d07e2e1358da5b6c7e8281d34e2a093e7ed2ad937d816ae731bfdde0c40b8a23cbdae0a0e9a52')

prepare() {
    cd $pkgname-$pkgver
    uusi -u megaparsec $pkgname.cabal
}

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -fterminfo -fthreaded --ghc-option='-pie'
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    install -Dm644 hledger.1 -t "$pkgdir"/usr/share/man/man1/
    install -Dm644 shell-completion/hledger-completion.bash "$pkgdir"/usr/share/bash-completion/completions/hledger
}
