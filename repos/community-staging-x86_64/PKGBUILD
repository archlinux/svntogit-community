# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=dbus
pkgname=haskell-dbus
pkgver=1.2.22
pkgrel=108
pkgdesc="A client library for the D-Bus IPC system"
url="https://john-millikin.com/software/haskell-dbus/"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-conduit' 'haskell-lens'
         'haskell-network' 'haskell-random' 'haskell-split' 'haskell-th-lift'
         'haskell-vector' 'haskell-xml-conduit' 'haskell-xml-types')
makedepends=('ghc' 'uusi' 'haskell-extra' 'haskell-quickcheck' 'haskell-resourcet' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
checkdepends=('dbus')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('81f9fe1420939ebb5b03602ba59dd86656e4443690d8be460684dcb461ff7df1ff4cdf846a5a15ea6193b6f64b08526b27d3e12825a375eecbded3d0a99a59b9')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u vector $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || : # https://github.com/rblaze/haskell-dbus/issues/51
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
