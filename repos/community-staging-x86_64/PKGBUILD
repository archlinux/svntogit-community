# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=quickcheck-instances
pkgname=haskell-quickcheck-instances
pkgver=0.3.25.2
pkgrel=75
pkgdesc="Common quickcheck instances"
url="https://github.com/haskellari/qc-instances"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-case-insensitive' 'haskell-data-fix'
         'haskell-hashable' 'haskell-integer-logarithms' 'haskell-old-time' 'haskell-scientific'
         'haskell-splitmix' 'haskell-strict' 'haskell-tagged' 'haskell-these'
         'haskell-time-compat' 'haskell-transformers-compat' 'haskell-unordered-containers'
         'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1e4df0c814cfdd12370d25ba66e0d9698e25cd480d9609ab5a45d4492df7a4600b79f5412f8224d90976c3accd59b16f8941b351bf8980e70e846ec08e6efced')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u base -u transformers-compat $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-bytestring-builder
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
