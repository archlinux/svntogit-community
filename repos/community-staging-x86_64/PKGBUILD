# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=stellarsolver
pkgver=2.4
pkgrel=3
pkgdesc='The cross platform Sextractor and Astrometry.net-Based internal astrometric solver'
arch=(x86_64)
url='https://github.com/rlancaste/stellarsolver'
license=(GPL3)
depends=(qt5-base gsl wcslib hicolor-icon-theme)
makedepends=(cmake)
source=(https://github.com/rlancaste/stellarsolver/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('e9cf7e73edb96a959f75dbbba16bd5e270f6e95c0d5c3dfa8727dfaf77d0617f')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
