# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyxDamerauLevenshtein
pkgname=python-${_pkg,,}
pkgver=1.7.0
pkgrel=3
pkgdesc="Implements the Damerau-Levenshtein (DL) edit distance algorithm"
arch=(x86_64)
url="https://github.com/gfairchild/pyxDamerauLevenshtein"
license=(BSD)
depends=(python)
makedepends=(python-setuptools cython)
checkdepends=(python-pytest)
# No tests in PyPi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('a85ec151f59455462ae62244d3fc16c16b33ddcf662be3e75bb8f89a862a942b')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="${PWD}"/build/lib.linux-${CARCH}-${python_version} pytest
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
