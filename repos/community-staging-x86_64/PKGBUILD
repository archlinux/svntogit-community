# $Id: PKGBUILD 124882 2014-12-29 22:07:40Z spupykin $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

_hkgname=haddock-library
pkgname=haskell-haddock-library
pkgver=1.5.0.1
pkgrel=16
pkgdesc="A documentation-generation tool for Haskell libraries"
url="https://www.haskell.org/haddock/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-base-compat' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-quickcheck' 'haskell-optparse-applicative' 'haskell-tree-diff')
source=("https://hackage.haskell.org/packages/archive/haddock-library/${pkgver}/haddock-library-${pkgver}.tar.gz")
sha512sums=('832f21280ab2dce56c668dcf087c884bc450455d8198fb75ee856c19ce1b547f4a2c1403f4f20e044382f140fed848ae7657da9d05a0ab0f0752433b20397559')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/\^>=/>=/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # TODO: doesn't look like my fault :/
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
