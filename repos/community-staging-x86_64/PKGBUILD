# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=mmark
pkgname=haskell-mmark
pkgver=0.0.7.6
pkgrel=59
pkgdesc="Strict markdown processor for writers"
url="https://github.com/mmark-md/mmark"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-case-insensitive' 'haskell-dlist'
         'haskell-email-validate' 'haskell-foldl' 'haskell-hashable' 'haskell-html-entity-map'
         'haskell-lucid' 'haskell-megaparsec' 'haskell-microlens' 'haskell-microlens-th'
         'haskell-modern-uri' 'haskell-parser-combinators' 'haskell-text-metrics'
         'haskell-unordered-containers' 'haskell-yaml')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-megaparsec')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('8c78a5aa7a1806c315bf8c0aa68321f04f508d72e4cac412ea401371fc7c50fd')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct || echo "Tests failed with new hashable"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
