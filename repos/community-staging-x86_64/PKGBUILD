# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-random
pkgname=haskell-generic-random
pkgver=1.4.0.0
pkgrel=11
pkgdesc="Generic random generators for QuickCheck"
url="https://github.com/lysxia/generic-random"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('fdf8c3865a785dd09adc88a2e3b54a6712ccfff244b7b8e6df73402ab71d4f34ff525c9ad393d61ba7619fb8a751c1a73a9f223a434c747c218800fb2c2d75bb')

build() {
    cd $_hkgname-$pkgver

    # Tests disabled: https://github.com/Lysxia/generic-random/issues/22
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --disable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    #runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
