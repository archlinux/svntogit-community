# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=dante
pkgver=1.4.2
pkgrel=3
pkgdesc="SOCKS v4 and v5 compatible proxy server and client"
url="https://www.inet.no/dante"
arch=(x86_64)
license=('custom')
depends=('pam' 'krb5')
backup=('etc/socks.conf'
        'etc/sockd.conf')
source=(ftp://ftp.inet.no/pub/socks/$pkgname-$pkgver.tar.gz
        sockd.tmpfiles.d
        sockd.service)
sha256sums=('4c97cff23e5c9b00ca1ec8a95ab22972813921d7fbf60fc453e3e06382fc38a7'
            '40d578ef1b802e3813236459b2dfb13fbec6537ae8f55c0c7cbd4401d65da878'
            '7d5f1fddc7e81e5447db662bcc02bbe14f616b403da92e5183e82661d4bc92ae')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --disable-libwrap
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm0755 "$pkgdir"/etc
  cp example/{socks,sockd}.conf "$pkgdir"/etc
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm0644 "$srcdir"/sockd.service "$pkgdir"/usr/lib/systemd/system/sockd.service
  install -Dm0644 "$srcdir"/sockd.tmpfiles.d "$pkgdir"/usr/lib/tmpfiles.d/sockd.conf
}
