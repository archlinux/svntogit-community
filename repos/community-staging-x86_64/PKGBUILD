# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.13
_commit=b483b644b6033e539f303509f5ee85bebf178a73
pkgrel=2
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=('x86_64')
depends=('python-lxml' 'xmlsec')
makedepends=('git' 'python-pkgconfig' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-hypothesis' 'python-pytest')
source=("git+https://github.com/mehcode/python-xmlsec.git#commit=$_commit")
sha512sums=('SKIP')

build() {
  cd python-xmlsec
  python setup.py build
}

check() {
  cd python-xmlsec
  # https://github.com/mehcode/python-xmlsec/issues/84
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-311" pytest \
    --deselect 'tests/test_doc_examples.py::test_doc_example[encrypt.py]' \
    --deselect 'tests/test_doc_examples.py::test_doc_example[sign.py]' \
    --deselect 'tests/test_doc_examples.py::test_doc_example[verify.py]'
}

package() {
  cd python-xmlsec
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
