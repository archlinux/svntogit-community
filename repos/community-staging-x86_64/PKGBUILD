# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.1.1
pkgrel=2
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url='https://www.mixxx.org'
license=('GPL')
depends=('fftw' 'libid3tag' 'libmad' 'libogg' 'libshout' 'libsndfile' 'portaudio' 'portmidi'
         'taglib' 'qt4' 'vamp-plugin-sdk' 'libusbx' 'protobuf' 'faad2' 'libmp4v2'
         'rubberband' 'chromaprint' 'sqlite' 'opus' 'opusfile' 'upower')
makedepends=('mesa' 'scons' 'libshout' 'glu')
source=("https://github.com/${pkgname}dj/${pkgname}/archive/release-${pkgver}.tar.gz")
md5sums=('6336d6568bbf860c4e03295ae51ee286')

build() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  scons build=release optimize=portable virtualize=0 localecompare=1 qt_sqlite_plugin=0 opus=1 \
    qtdir=/usr/lib/qt4 prefix=/usr faad=1
}

package() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  scons qtdir=/usr/lib/qt4 prefix=/usr install_root="${pkgdir}/usr" install
}
