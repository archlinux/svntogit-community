# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>

pkgname=symengine
pkgver=0.8.0
pkgrel=1
pkgdesc='Fast symbolic manipulation library, written in C++'
url='http://sympy.org/'
arch=(x86_64)
license=(MIT)
depends=(arb libmpc gmp-ecm primesieve gperftools zlib)
makedepends=(cmake boost llvm)
source=(https://github.com/symengine/symengine/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('ebe71d3c42661d12fa991040097ff383e193006af4e76d1adaabb8cee45cf94c')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_TCMALLOC=ON \
    -DWITH_PTHREAD=ON \
    -DWITH_SYMENGINE_THREAD_SAFE=ON \
    -DWITH_ARB=ON \
    -DWITH_ECM=ON \
    -DWITH_MPFR=ON \
    -DWITH_MPC=ON \
    -DWITH_LLVM=ON \
    -DWITH_CPP14=ON \
    -DWITH_PRIMESIEVE=ON \
    -DWITH_BOOST=ON \
    -DWITH_COTIRE=OFF
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
