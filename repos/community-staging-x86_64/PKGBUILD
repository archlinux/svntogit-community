# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Stefan Husmann <stefan-husmann at t-online dot de>
# Contributor: Alexander RÃ¸dseth <rodseth at gmail dot com>
# Contributor: William Rea <sillywilly at gmail dot com>

pkgname=libmatio
pkgver=1.5.6
pkgrel=2
pkgdesc='C library with a fortran 90/95 module interface for reading/writing MATLAB MAT-files'
arch=('x86_64' 'i686')
license=('custom:BSD')
url='http://sourceforge.net/projects/matio'
depends=('zlib' 'hdf5')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/matio/matio-${pkgver}.tar.gz"
        "hdf5-1.10.patch")
changelog=ChangeLog
md5sums=('bb53fd06f7ebb74589e9dd684b1b66a6'
         '7f1793012c3093851cbfad38f213d3fd')

prepare() {
  # update for newest hdf5
  patch -p0 -i "hdf5-1.10.patch"
}

build() {
  cd "matio-${pkgver}"
  ./configure --prefix=/usr --enable-shared --with-hdf5
  make
}

package() {
  cd "matio-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
