# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.5.3
pkgrel=3
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
url='http://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'webkit2gtk' 'libesmtp' 'libnotify'
         'gpgme' 'gtksourceview3' 'gspell' 'desktop-file-utils')
makedepends=('yelp-tools' 'intltool')
changelog=$pkgname.changelog
source=(https://pawsa.fedorapeople.org/balsa/$pkgname-$pkgver.tar.bz2 balsa-openssl-1.1.patch)
sha256sums=('b896ea6b16b31725e0d81ba4eac398b39ad5c33f2337a260448d2feac7e55396'
            '53f5639ffe5195803b8526c7a1e1ef2b96f6ba11c21b2a6b67e43c6433b3dca2')

prepare() {
  cd $pkgname-$pkgver
  # Fix build with openssl 1.1 (Fedora)
  patch -p1 -i ../balsa-openssl-1.1.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-gpgme=gpgme-config \
      --with-gss \
      --with-ldap \
      --with-gtksourceview \
      --with-spell-checker=gspell \
      --with-rubrica \
      --with-sqlite \
      --with-gnome \
      --with-html-widget=webkit2
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
