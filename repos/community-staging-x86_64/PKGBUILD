# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=esqueleto
pkgname=haskell-esqueleto
pkgver=2.6.0
pkgrel=5
pkgdesc="Type-safe EDSL for SQL queries on persistent backends."
url="https://github.com/bitemyapp/esqueleto"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-html' 'haskell-conduit' 'haskell-monad-logger'
         'haskell-persistent' 'haskell-resourcet' 'haskell-tagged' 'haskell-unliftio'
         'haskell-unordered-containers')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b1b50b91c14e7fc72077a1575b4b1df538d6da6b4cf4a34d13b8b94b4ad62d9049c97cc15d272547580e255a62a8a7c70725ee35d23aa69474c7a2224b06b5b8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-mysql -f-postgresql
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
