# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=idris
pkgver=1.3.3
pkgrel=42
pkgdesc="Functional Programming Language with Dependent Types"
url="https://www.idris-lang.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-annotated-wl-pprint' 'haskell-ansi-terminal'
         'haskell-ansi-wl-pprint' 'haskell-async' 'haskell-base64-bytestring' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-cheapskate' 'haskell-code-page' 'haskell-fingertree'
         'haskell-fsnotify' 'haskell-ieee754' 'haskell-libffi' 'haskell-megaparsec'
         'haskell-network' 'haskell-optparse-applicative' 'haskell-parser-combinators'
         'haskell-regex-tdfa' 'haskell-safe' 'haskell-split' 'haskell-terminal-size'
         'haskell-uniplate' 'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector'
         'haskell-vector-binary-instances' 'haskell-zip-archive')
makedepends=('ghc' 'haskell-tagged' 'haskell-tasty' 'haskell-tasty-golden' 'haskell-tasty-rerun'
             'nodejs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/idris-lang/Idris-dev/archive/v$pkgver.tar.gz"
        idris-haskeline-0.8.patch::https://github.com/idris-lang/Idris-dev/pull/4871.patch)
sha512sums=('fc5c65847c8021ed691c7968043a04fcffaed3a44a6339f611fed616fddefa1b5bc2da8e7e6662dfa552981688a1c96571eadc197e4e50ba060de3c6ddbd03de'
            '5f2efe359d5626c44f0c146f108dae4635ae2c8babf53841d5147d17d5be2460ab19a0b952492c68c93fa6470989bcf396cd7fbf6c8d44ac77792a54c3bbb141')

prepare() {
    cd Idris-dev-$pkgver
    patch -p1 -i ../idris-haskeline-0.8.patch
    sed -i '1ioverride IDRIS := env LD_PRELOAD=$(shell ls ../../dist/build/libHSidris-*-ghc*.so) $(IDRIS)' \
        libs/*/Makefile
    sed -i -e 's/< *1.5/<2/' idris.cabal
}

build() {
    cd Idris-dev-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fFFI -fGMP -f-release -f-freestanding -f-CI -f-execonly --ghc-option='-pie'
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd Idris-dev-$pkgver
    # TODO: figure out the tests
    PATH="$PWD/dist/build:$PWD/dist/build/idris:$PATH" LD_LIBRARY_PATH="$PWD/dist/build" IDRIS_LIBRARY_PATH="$PWD/libs" runhaskell Setup test || warning "Tests failed"
}

package() {
    cd Idris-dev-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
