# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=hedgewars
pkgver=0.9.21.1
pkgrel=3
pkgdesc="Turn-based strategy artillery game similiar to Worms"
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL' 'custom')
depends=('qt4' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'lua51' 'ffmpeg' 'glut' 'physfs')
makedepends=('fpc' 'cmake'
	     # haskell specific
	     'ghc'
	     'haskell-network'
	     'haskell-bytestring-show'
	     'haskell-utf8-string'
	     'haskell-dataenc'
	     'haskell-hslogger'
	     'haskell-vector'
	     'haskell-entropy'
	     'haskell-sha'
	     'haskell-zlib'
	     'haskell-random')
source=(http://download.gna.org/hedgewars/hedgewars-src-$pkgver.tar.bz2
        hedgewars.png)
md5sums=('5c87b1ff54c861ae0cae6a9c1198f957'
         'eeb14d50df39063549ac5eca9dbc65d1')

prepare() {
  cd $pkgname-src-*
  sed -i 's|instance NFData (Chan a)$|instance NFData (Chan a) where rnf x = seq x ()|' gameServer/CoreTypes.hs
}

build() {
  cd $pkgname-src-*
  mkdir -p $srcdir/bin
  ln -sf /usr/bin/lrelease-qt4 $srcdir/bin/Qt4::lrelease
  export PATH=$srcdir/bin:$PATH
  cmake \
	-DCMAKE_BUILD_TYPE="Release" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DDATA_INSTALL_DIR=/usr/share/hedgewars \
	-DNOSERVER=0 .
  make
}

package() {
  cd $pkgname-src-*
  make DESTDIR=$pkgdir install
  install -D -m644 Fonts_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/Fonts_LICENSE.txt
  install -D -m644 $srcdir/hedgewars.png $pkgdir/usr/share/pixmaps/hedgewars.png
  install -D -m644 share/hedgewars/Data/misc/hedgewars.desktop $pkgdir/usr/share/applications/hedgewars.desktop
}
