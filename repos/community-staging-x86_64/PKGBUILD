# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=warp
pkgname=haskell-warp
pkgver=3.2.13
pkgrel=12
pkgdesc="A fast, light-weight web server for WAI applications."
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-async" "haskell-auto-update" "haskell-blaze-builder"
         "haskell-case-insensitive" "haskell-hashable" "haskell-http-date" "haskell-http-types"
         "haskell-http2" "haskell-iproute" "haskell-network" "haskell-simple-sendfile" "haskell-stm"
         "haskell-streaming-commons" "haskell-text" "haskell-unix-compat" "haskell-vault"
         "haskell-wai" "haskell-word8")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('598b1a3d2521a483001602c059685f28e5c1ed6729e35243c82b98e879cc4d448dd5b42542bef3b3df9d1181c829e3fc582fa1454f9c4daada44961bc5c6546e')

prepare() {
    sed -i '/bytestring-builder/d' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-warp-debug -fallow-sendfilefd -f-network-bytestring
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
