# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.8.27
pkgrel=6
pkgdesc="Produce PDF or Postscript from HTML documents including TOCs and Indices"
arch=(i686 x86_64)
url="http://www.htmldoc.org"
license=('GPL')
depends=('libjpeg' 'libpng' 'openssl' 'fltk' 'gcc' 'libxft' 'libxpm')
source=(ftp://ftp.easysw.com/pub/${pkgname}/${pkgver}/${pkgname}-$pkgver-source.tar.bz2)
md5sums=('35589e7b8fe9c54e11be87cd5aec4dcc')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=${pkgdir}/usr/share/man
  sed -i 's|^bindir.*$|bindir  = ${prefix}/bin|' Makedefs
  make
}

package() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  make prefix=${startdir}/pkg/usr install
 # fix freedesktop stuff
  install -Dm 644 desktop/htmldoc-128.png \
    ${startdir}/pkg/usr/share/pixmaps/htmldoc.png
  echo "MimeType=application/vnd.htmldoc-book;" >> desktop/htmldoc.desktop
  sed -i 's|X-Red-Hat.*$||' desktop/htmldoc.desktop
  install -Dm 644 desktop/htmldoc.desktop \
    ${startdir}/pkg/usr/share/applications/htmldoc.desktop
}
