# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=libvolk
epoch=2
pkgver=2.5.2
_cpuver=0.6.0
pkgrel=2
pkgdesc="The Vector-Optimized Library of Kernels from Gnuradio"
arch=('x86_64')
url="http://libvolk.org/"
license=('GPL3')
depends=('gcc-libs' 'boost-libs' 'orc' 'python' 'python-six')
makedepends=('boost' 'cmake' 'python-mako')
#source=("http://libvolk.org/releases/volk-$pkgver.tar.gz"{,.asc})
#source=("http://libvolk.org/releases/volk-$pkgver.tar.gz")
#source=("https://github.com/gnuradio/volk/releases/download/v$pkgver/volk-v$pkgver.tar.gz"{,.asc})
source=("volk-$pkgver.tgz::https://github.com/gnuradio/volk/archive/v$pkgver.tar.gz"
        "cpufeatures-$_cpuver.tgz::https://github.com/google/cpu_features/archive/v$_cpuver.tar.gz")
# new upstream maintainer, no sig for 2.1.0 or real release?
sha256sums=('fb447b8053f574a72697a26f08d51fb70d3b54b4e90e72b705138f8bc7ae6722'
            '95a1cf6f24948031df114798a97eea2a71143bd38a4d07d9a758dda3924c1932')
validpgpkeys=('09E749D885FA881A7E84E823385323EE6402091D'
              'D74F9F146E7F755783583158B343B2BA293E5174')

# doxygen for docs

prepare() {
  cd "$srcdir/volk-$pkgver"
  rmdir cpu_features
  ln -sf "$srcdir/cpu_features-$_cpuver" cpu_features
}

build() {
  export PYTHON=python3
  cd "$srcdir/volk-$pkgver"

  mkdir -p build
  cd build
  cmake \
    -DPYTHON_EXECUTABLE=$(which python3) \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev ../
  make
}

check() {
  cd "$srcdir/volk-$pkgver/build"
  export PYTHON=python3
  return
  if [[ "$CARCH" == "x86_64" ]]; then
    make test
  fi
}

package() {
  cd "$srcdir/volk-$pkgver/build"
  make DESTDIR="$pkgdir" install
}
