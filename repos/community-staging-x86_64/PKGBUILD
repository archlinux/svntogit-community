# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.7.15
pkgrel=2
pkgdesc='Movie player based on mpv'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration'  'ffmpegthumbnailer' 'libxtst' 'mpv' 'qtdbusextended' 'qtmpris')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
options=('!lto')
source=("https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1d0c714a8ec16be3a89e28656920c787778e13bdbb4b63e6767c30436bce7f90a2b7d9759ff30f0bed004a11e268bd57ed8f197b22709921898a4d49b9d707fb')

prepare() {
  cd deepin-movie-reborn-$pkgver
  # https://github.com/linuxdeepin/deepin-movie-reborn/issues/76
  sed -i '/setCanShowInUI/d' src/vendor/presenter.cpp
}

build() {
  cd deepin-movie-reborn-$pkgver
  export CXXFLAGS+=' -fpermissive'
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
