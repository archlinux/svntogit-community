# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=opengv
pkgver=1.0.git1
_commit=91f4b19
pkgrel=4
pkgdesc="An efficient C++ library for calibrated camera pose computation using geometric computer vision algorithms"
arch=('x86_64')
url="https://laurentkneip.github.io/opengv/"
license=('BSD')
depends=('eigen' 'python-numpy' 'pybind11')
makedepends=('cmake' 'git' 'ninja')
source=("git+https://github.com/laurentkneip/opengv.git#commit=$_commit"
        https://salsa.debian.org/science-team/opengv/-/raw/master/debian/patches/no-march-options.patch
        https://salsa.debian.org/science-team/opengv/-/raw/master/debian/patches/use-system-pybind11.patch)
sha256sums=('SKIP'
            'e483d0c1a4a76920ac3260999978154b12422c42e9991e69afc9230643d5c24a'
            '20832cd52808bd7ef2f6bee8a5c85f69fc0fe6c82169e2b2297781f4bdcdd3ae')

prepare() {
  cd opengv

  patch -Np1 -i "${srcdir}/no-march-options.patch"
  patch -Np1 -i "${srcdir}/use-system-pybind11.patch"
}

build() {
  cd opengv

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=None \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DBUILD_SHARED_LIBS=ON \
  	-DBUILD_PYTHON=ON
  ninja -C build
}

check() {
  cd opengv

  ninja -C build test
}

package() {
  cd opengv

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 License.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
