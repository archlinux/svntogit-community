# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hindent
pkgver=5.2.4.1
pkgrel=7
pkgdesc="Extensible Haskell pretty printer"
url="https://github.com/commercialhaskell/hindent"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-descriptive" "haskell-exceptions" "haskell-monad-loops" "haskell-mtl"
         "haskell-path" "haskell-path-io" "haskell-src-exts" "haskell-text" "haskell-unix-compat"
         "haskell-utf8-string" "haskell-yaml")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('262db860d554f617d9317b6a32af2944e2d7b98d4c0c43baa9c7a90c08ffb0b34ba41701796f22d7a562ec7ba07b53bd6bb3e4ebfaf320ba1c009434fc8a3943')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
