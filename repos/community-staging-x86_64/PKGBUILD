# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2017.03
pkgrel=2
pkgdesc='U-Boot bootloader utility tools'
arch=(i686 x86_64)
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig} 0001-Add-Openssl-1.1-support.patch 0001-rsa-Fix-deprecated-warnings-for-OpenSSL-1.1.x.patch)
validpgpkeys=('E872DB409C1A687EFBE8633687F9F635D31D7652')
sha1sums=('93dbd99beb9e4848303702e9c0bc794f3efa3e6f'
          'SKIP'
          'e23531ec52835bf6257e07fbf1cff0ecc49a1098'
          '30c80f75d54f8995ff57cfd7114d8b979a238f91')

prepare() {
  cd u-boot-$pkgver
  patch -Np1 -i $srcdir/0001-Add-Openssl-1.1-support.patch
  patch -Np1 -i $srcdir/0001-rsa-Fix-deprecated-warnings-for-OpenSSL-1.1.x.patch

}
build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -D u-boot-$pkgver/doc/mkimage.1 "$pkgdir"/usr/share/man/man1/mkimage.1
}
