# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Benzo <mb_benzo@yahoo.es>

pkgname=osmo
pkgver=0.2.10
pkgrel=8
pkgdesc="A handy personal organizer"
arch=('i686' 'x86_64')
url="http://clayo.org/osmo/"
license=('GPL')
depends=('gtk2>=2.12' 'gtkspell' 'hicolor-icon-theme' 'libical' 'libnotify'  'libgtkhtml' 'libgringotts>=1.2.1' 'libxml2')
makedepends=('pkgconfig' 'libtar')
optdepends=('sox: to play sounds')
install=$pkgname.install
source=("http://downloads.sourceforge.net/osmo-pim/$pkgname-$pkgver.tar.gz"
        'osmo-0.2.10-libnotify-0.7.patch')
md5sums=('a774db748228efee96186158d553ade9'
         '0b146a548f2d3208dc88e6501a8f3448')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/osmo-0.2.10-libnotify-0.7.patch"

  #libical 0.4.4 workaround
  LIBICAL_CFLAGS='-I/usr/include/libical' ./configure --prefix=/usr

  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
