# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=gpgme
pkgname=ruby-gpgme
pkgver=2.0.20
pkgrel=2
pkgdesc='Ruby interface to GnuPG Made Easy (GPGME)'
url='https://github.com/ueno/ruby-gpgme'
arch=('x86_64')
license=('GPL2')
depends=('ruby' 'ruby-mini_portile2' 'gpgme' 'libgpg-error' 'libassuan')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-yard')
options=('!emptydirs')
source=(https://github.com/ueno/ruby-gpgme/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3267cc77b57c22de3e505c34dfb0a8a4a76cda1fe6dbe547dfdb9ca1a75cf949ca75b3718fe6b6190c06dfdddf21d76b0d0a71631e53e4f439284ce7cf95f6df')
b2sums=('f5d4c58fd9ef38e0e843e3c59e9653166666ce3635cf479fcd007581b6c6c42ce7852bd36542b378e4152bd471af0131685c8a9f1024953f41832a54c477e1ac')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  export RUBY_GPGME_USE_SYSTEM_LIBRARIES=1
  rake compile
  rake build
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem -- --use-system-libraries
  install -Dm 644 README.rdoc NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
