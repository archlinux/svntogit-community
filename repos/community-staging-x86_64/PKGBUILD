# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-lift-instances
pkgname=haskell-th-lift-instances
pkgver=0.1.20
pkgrel=22
pkgdesc="Lift instances for template-haskell for common data types."
url="https://github.com/bennofs/th-lift-instances/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-lift' 'haskell-vector')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('bb43b13df8fb939a79a3fc1099900fcaa8741eb61b8f5574b7d7b68c220acc0cbb9fa7517b22ad34f006da7a1c25c08d1237309f8f98d79ca4d00dad345b28cb')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
