# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=pantry
pkgname=haskell-pantry
pkgver=0.1.1.1
pkgrel=39
pkgdesc="Content addressable Haskell package management"
url="https://github.com/commercialhaskell/pantry"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-base-orphans'
         'haskell-base64-bytestring' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-contravariant' 'haskell-cryptonite' 'haskell-cryptonite-conduit'
         'haskell-digest' 'haskell-filelock' 'haskell-generic-deriving' 'haskell-hackage-security'
         'haskell-hashable' 'haskell-hpack' 'haskell-http-client' 'haskell-http-client-tls'
         'haskell-http-conduit' 'haskell-http-download' 'haskell-http-types' 'haskell-memory'
         'haskell-mono-traversable' 'haskell-network' 'haskell-network-uri' 'haskell-path'
         'haskell-path-io' 'haskell-persistent' 'haskell-persistent-sqlite'
         'haskell-persistent-template' 'haskell-primitive' 'haskell-resourcet' 'haskell-rio'
         'haskell-rio-orphans' 'haskell-rio-prettyprint' 'haskell-safe' 'haskell-syb'
         'haskell-tar-conduit' 'haskell-text-metrics' 'haskell-th-lift' 'haskell-th-lift-instances'
         'haskell-th-orphans' 'haskell-th-reify-many' 'haskell-th-utilities' 'haskell-unix-compat'
         'haskell-unliftio' 'haskell-unordered-containers' 'haskell-vector' 'haskell-yaml'
         'haskell-zip-archive')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-hspec' 'haskell-quickcheck' 'haskell-raw-strings-qq')
checkdepends=('git' 'mercurial')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('aee544d5c18f7f1d8614470d236fb8e0f3d9260991b8ee895a5c7c863dd3023677260330fd34d9d30789780efb84f3faa49823827fe7aae69165eb9468b13a62')

prepare() {
    cd $_hkgname-$pkgver
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
