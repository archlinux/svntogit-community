# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=monad-dijkstra
pkgname=haskell-monad-dijkstra
pkgver=0.1.1.3
pkgrel=273
pkgdesc="A monad transformer for weighted graph searches"
url="https://github.com/ennocramer/monad-dijkstra"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-free' 'haskell-psqueues')
makedepends=('ghc' 'uusi' 'hlint' 'haskell-tasty' 'haskell-tasty-hspec')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('6c6270f94d27203b6974563398e4b7e81ae53e6110cffaecf8ff6297c11ceb8f')

prepare() {
  cd $_hkgname-$pkgver
  uusi -r tasty-hspec:tasty-hspec,hspec $_hkgname.cabal
  sed -i '/Test.Tasty.Hspec/a import Test.Hspec' test/Main.hs
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
