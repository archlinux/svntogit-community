# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Florian Loitsch <archlinux@florian.loitsch.com>

pkgname=miniupnpc
pkgver=2.1.20190408
pkgrel=2
pkgdesc='Small UPnP client library/tool to access Internet Gateway Devices'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
depends=('sh')
provides=('libminiupnpc.so')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('a0c46bcf6065d6351a8fa6a0a18dc57d10a16908dbb470908fd2e423511514ec'
            'SKIP')
sha512sums=('faa0f1363ebb59ca636b6c974188c52903ad07652f8268563c9c1a5826fafc2458d9f521efd3546fbbd4b71b3b14a50ba89dda52757adad1db81c31d9ba0987a'
            'SKIP')
validpgpkeys=('BEB7EA42900FF505B7907AE50FF11B67A5C0863C') # miniupnp (miniupnp !) <miniupnp@free.fr>

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 man3/miniupnpc.3 -t "${pkgdir}/usr/share/man/man3"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
