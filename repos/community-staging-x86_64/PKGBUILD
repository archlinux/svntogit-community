# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox
_pkgname=qTox
pkgver=1.17.6
pkgrel=2
_gitver='c0e9a3b79609681e5b9f6bbf8f9a36cb1993dc5f'
pkgdesc='a chat, voice, video, and file transfer IM client using the encrypted peer-to-peer Tox protocol'
arch=('x86_64')
url='https://github.com/qTox/qTox'
license=('GPL3')
depends=('desktop-file-utils'
         'libxss'
         'openal'
         'ffmpeg'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'libexif'
         'toxcore'
         'sonnet'
         'snorenotify'
         'libgl')
makedepends=('qt5-tools' 'git' 'check' 'cmake' 'mesa' 'libglvnd')

source=( 
        "$pkgname-$pkgver.tar.lz::https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz"
        "$pkgname-$pkgver.tar.lz.asc::https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz.asc"
        )
sha512sums=('2dc761248bf937044d3135c196a2e7b6986846369d493ecc831fd8519cfac22a7b5472f452482c2afccbe2ad73b7a36c174408f75ab142789dc06613b9e8c9f3'
            'SKIP')

## GPG key fingerprints taken from https://github.com/qTox/qTox/blob/$_gitver/README.md
validpgpkeys=('DA262CC93C0E1E525AD21C8596775D454B8EBF44'  # sudden6 <sudden6@gmx.at>
              'BA7883E22F9D35945BA3376053137C3033F09008'  # Zetok Zalbavar <zetok@openmailbox.org>
	      '1157616BBD860C539926F8139591A163FF9BE04C'  # - antis81
	      '31039166FA902CA50D05D6085AF9F2E29107C727'  # - Diadlo
	      'C7A2552D0B250F983827742C133203A3AC399151'  # - initramfs
	      '2880C860D95C909D3DA45C687E086DD661263264'  # - tux3
          '74ADB7A7F8ECC1009916C4E42AA8E0DA1B31FB3C'  # - AnthonyBilinski subkey
          '7EB339FE881747E701B7D472EBE36E66A8429B99'  # - AnthonyBilinski
          'CA9221C5389B7C50AA5F779352A50775BE13DF17'  # - noavarice
          '141C880E8BA25B198D0F850F7C132143C1A3A7D4'  # – tox-user
             )

prepare() {
  cd $_pkgname
  # disable -Werror for release packaging
  sed -e 's|-Werror||' -i CMakeLists.txt
}

build() {
  cd $_pkgname
  mkdir -p build
  cd build
  export CXXFLAGS+=' -fpermissive'
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
          -DDESKTOP_NOTIFICATIONS=ON \
	  -DGIT_VERSION=$_gitver \
	  -DGIT_DESCRIBE=v$pkgver
  make
}

check() {
  cd $_pkgname
  cd build
  make test
}

package() {
  cd $_pkgname
  cd build
  make DESTDIR="$pkgdir" install
}
