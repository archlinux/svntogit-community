# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=retrie
pkgname=haskell-retrie
pkgver=1.1.0.0
pkgrel=111
pkgdesc="A powerful, easy-to-use codemodding tool for Haskell."
url="https://github.com/facebookincubator/retrie"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-data-default'
         'haskell-ghc-exactprint' 'haskell-src-exts' 'haskell-list-t'
         'haskell-optparse-applicative' 'haskell-random-shuffle' 'haskell-syb'
         'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hunit' 'haskell-ghc-paths' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-temporary')
checkdepends=('git' 'mercurial')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('71a27d5a6c0a17b353927c19b19b2f5e94ac33fc1c2cb988324d8a21b449bfdc3c08c5f06d9ea251eedb0888c5db9a59cbf728833e7b9c84f669148ca782dc23')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u optparse-applicative $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
