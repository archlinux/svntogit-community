# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsboat
pkgver=2.10.2
pkgrel=1
pkgdesc="An RSS/Atom feed reader for text terminals"
arch=('x86_64')
url="https://newsboat.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext' 'asciidoc')
replaces=('newsbeuter')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(https://newsboat.org/releases/$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('B8B1756A0DDBF0760CE67CCF4ED6CD61932B9EBE') #Newsboat project <newsboat@googlegroups.com>
sha256sums=('fda6b44f5c3a23ce46c85b40a6f4f0257c024d79bb8de325aa2cd73d43ef96f4'
            'SKIP')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  # Upstream should fix all the latest GCC warnings :-)
  sed -i 's/\-Werror//g' Makefile
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr
  make doc
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
