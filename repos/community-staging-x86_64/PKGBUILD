# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant
pkgname=haskell-servant
pkgver=0.19
pkgrel=19
pkgdesc="A family of combinators for defining webservices APIs"
url="http://haskell-servant.readthedocs.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-aeson' 'haskell-attoparsec'
         'haskell-bifunctors' 'haskell-case-insensitive' 'haskell-constraints'
         'haskell-http-api-data' 'haskell-http-media' 'haskell-http-types' 'haskell-mmorph'
         'haskell-network-uri' 'haskell-quickcheck' 'haskell-tagged' 'haskell-sop-core'
         'haskell-singleton-bool' 'haskell-string-conversions' 'haskell-vault')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-quickcheck-instances')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('5f7a7a03e7cb1f729cf4265a767006b4d65dfadea878540df3ffb8347e1d2ff5d8faaa58a5e032535a489fd45e1da81dc9424c2d931b6d26adf156ee59f1c003')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u base-compat -u http-api-data -u hspec -u mmorph $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
