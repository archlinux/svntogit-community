# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis.arch at gmail dot com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas Dziedzic <gostrc at gmail dot com>
# Contributor: Osman Ugus <ugus11 at yahoo dot com>
# Contributor: Stefan Husmann <stefan-husmann at t-online dot de>

pkgname=sagemath
pkgver=9.3
pkgrel=9
pkgdesc='Open Source Mathematics Software, free alternative to Magma, Maple, Mathematica, and Matlab'
arch=(x86_64)
url='http://www.sagemath.org'
license=(GPL)
depends=(palp brial cliquer maxima-ecl gfan sympow nauty python-rpy2 python-fpylll python-psutil python-cypari2
  python-matplotlib python-scipy python-sympy python-networkx python-pplpy python-sphinx python-ipywidgets
  gap flintqs lcalc lrcalc arb eclib zn_poly gd python-cvxopt pynac linbox m4rie pari-galdata pari-seadata-small planarity rankwidth tachyon
  sage-data-combinatorial_designs sage-data-elliptic_curves sage-data-graphs sage-data-polytopes_db sage-data-conway_polynomials
  iml giac libhomfly libbraiding symmetrica threejs-sage)
optdepends=('cython: to compile cython code' 'python-pkgconfig: to compile cython code'
  'jmol: alternative 3D plot engine' 'jupyter-jsmol: alternative 3D plot engine in the Jupyter notebook'
  'sagemath-doc: HTML documentation' 'python-igraph: igraph backend for graph theory'
  'sage-numerical-backends-coin: COIN mixed integer linear programming backend'
  'coin-or-csdp: for computing Lov√°sz theta-function of graphs'
  'buckygen: for generating fullerene graphs' 'plantri: for generating some classes of graphs' 'benzene: for generating fusenes and benzenoids'
  'ffmpeg: to export animations to video' 'imagemagick: to show animations'
  'coxeter: Coxeter groups implementation'
  'rubiks: Rubiks cube algorithms'
  'lrs: Algorithms for linear reverse search used in game theory and for computing volume of polytopes'
  'python-pynormaliz: Normaliz backend for polyhedral computations'
  'latte-integrale: integral point count in polyhedra' 'python-jupymake: polymake backend for polyhedral computations'
  'shared_meataxe: faster matrix arithmetic over finite fields' 'openblas: faster linear algebra'
  'sirocco: for computing the fundamental group of the complement of a plane curve' 'primecount: faster prime_pi implementation'
  'dot2tex: for displaying some diagrams' 'cryptominisat5: SAT solver' 'python-pycosat: picosat SAT solver'
  'python-pip: to install optional packages with sage -pip' 'sage-notebook-exporter: convert flask notebooks to Jupyter')
makedepends=(cython boost ratpoints python-jinja sirocco mcqd coxeter bliss tdlib python-pkgconfig shared_meataxe primecount)
conflicts=(sagemath-jupyter)
replaces=(sagemath-jupyter)
source=(https://github.com/sagemath/sage/archive/$pkgver/$pkgname-$pkgver.tar.gz
        sagemath-optional-packages.patch
        latte-count.patch
        test-optional.patch
        sagemath-pari-2.13.patch
        sagemath-lrcalc2.patch
        sagemath-eclib-20210310.patch
        sagemath-matplotlib-3.4.patch
        sagemath-lrs-071.patch
        sagemath-versioned-threejs.patch
        sagemath-lcalc2.patch)
sha256sums=('d3b104a83e140cda563ae0b44fa3ec2b3545660f120fab78dde6ceb5fb1ed991'
            'c100a61c8dfade43bebc622a363abcb3d935a2f40958371ad87a9eb00689f8b0'
            '88e944f23c3b2391dc2e9f9be8e1131152d837dc8c829dfc714663869a272e81'
            'af984186f852d2847d770a18fb6822296c50a652dbf55a1ed59d27517c3d3ee4'
            '32d586609dd6970f103574f75287bb641eb0a93618e84a1f671a2816e9420012'
            '240ac4c29d96d56407a20e1b7f9846e342a7eb2bb4edd6e5c86b3b5a8ff462f9'
            'e7b31f5e7ea88681c6eda41e5a74a2859a12dd128e75c00db3cfbd1d8ddf080d'
            '5e6f919a386e1a9e9caf7528088a3a3f3f3fc51b158a4daf435771afb1212384'
            '4220a65e40f0f9df3a1667d7e5ac6bec36cfb7d2c3056fc02e5a6beb7dc882fa'
            '3b4c38210b11e04a207a523f7bd7c5695393a2b1aa5c953a4647e862849a8530'
            '7858a35095f7ff34bdaf1e56d815d81cccce8c5931cf1a5c7848c4db42f6c481')

prepare(){
  cd sage-$pkgver

# Upstream patches
# Port to PARI 2.13 https://trac.sagemath.org/ticket/30801
  patch -p1 -i ../sagemath-pari-2.13.patch
# Replace lrcalc.pyx with a wrapper over lrcalc's python bindings https://trac.sagemath.org/ticket/31355
  patch -p1 -i ../sagemath-lrcalc2.patch
# Fix build with eclib 20210310 https://trac.sagemath.org/ticket/31443
  patch -p1 -i ../sagemath-eclib-20210310.patch
# Fix deprecation warnings with matplotlib 3.4 https://trac.sagemath.org/ticket/31580
  patch -p1 -i ../sagemath-matplotlib-3.4.patch
# Fixes for lrs 071 https://trac.sagemath.org/ticket/27745
  patch -p1 -i ../sagemath-lrs-071.patch
# Use versioned three.js path https://trac.sagemath.org/ticket/30972
  patch -p1 -i ../sagemath-versioned-threejs.patch
# Port to lcalc 2 https://trac.sagemath.org/ticket/32037
  patch -p1 -i ../sagemath-lcalc2.patch

# Arch-specific patches
# assume all optional packages are installed
  patch -p1 -i ../sagemath-optional-packages.patch
# don't list optional packages when running tests
  patch -p1 -i ../test-optional.patch
# use correct latte-count binary name
  patch -p1 -i ../latte-count.patch
}

build() {
  cd sage-$pkgver/build/pkgs/sagelib/src

  export SAGE_NUM_THREADS=10
  python setup.py build
}

package() {
  cd sage-$pkgver/build/pkgs/sagelib/src

  python setup.py install --root="$pkgdir" --optimize=1

# Remove sage_setup
  rm -r "$pkgdir"/usr/lib/python*/site-packages/sage_setup

# fix symlinks to assets
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  for _i in $(ls "$srcdir"/sage-$pkgver/src/sage/ext_data/notebook-ipython); do
    rm "$pkgdir"/usr/share/jupyter/kernels/sagemath/$_i
    ln -s $_pythonpath/sage/ext_data/notebook-ipython/$_i "$pkgdir"/usr/share/jupyter/kernels/sagemath/
  done

# adjust threejs version
  rm "$pkgdir"$_pythonpath/sage/ext_data/threejs/threejs-version.txt
  ln -s /usr/share/threejs-sage/version "$pkgdir"$_pythonpath/sage/ext_data/threejs/threejs-version.txt
}
