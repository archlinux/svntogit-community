# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cmarkgfm
pkgver=0.6.0
pkgrel=3
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('x86_64')
depends=('cmark-gfm' 'python-cffi')
makedepends=('cmark-gfm' 'python-setuptools' 'python-cffi')
checkdepends=('python-pytest-runner')
source=("https://github.com/theacodes/cmarkgfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('de4653ae4b27e55b98f885b67d50f71cffa92f13511f1a5efb885a474f5aefb924ce6eb019f4241499b66ad43a7e0630053a7743e844e5c34c652050c0f83ef2')

prepare() {
  cd cmarkgfm-$pkgver
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python setup.py build
}

check() {
  cd cmarkgfm-$pkgver
  python setup.py pytest
}

package() {
  cd cmarkgfm-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
