# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cblrepo
pkgver=0.24.0
pkgrel=44
pkgdesc="Tool to maintain a database of CABAL packages and their dependencies"
url="https://github.com/magthe/cblrepo"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-ansi-wl-pprint" "haskell-mtl" "haskell-optparse-applicative"
         "haskell-safe" "haskell-stringsearch" "haskell-tar" "haskell-text" "haskell-unixutils"
         "haskell-utf8-string" "haskell-vector" "haskell-zlib")
makedepends=('ghc')
source=("https://github.com/MaskRay/cblrepo/archive/771cb062435b7ef5511f77187f051b1a0e908f69.tar.gz")
sha256sums=('cc671f14ddb9cf1eea5f04be0d97fb8600da8551b56440375838079aa8bccb71')

prepare() {
    mv cblrepo-771cb062435b7ef5511f77187f051b1a0e908f69 cblrepo-$pkgver
    sed -i 's/optparse-applicative ==0.13.*,/optparse-applicative >=0.13,/' cblrepo-$pkgver/cblrepo.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/share
}
