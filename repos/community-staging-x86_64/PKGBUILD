# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-desugar
pkgname=haskell-th-desugar
pkgver=1.9
pkgrel=29
pkgdesc="Functions to desugar Template Haskell"
url="https://github.com/goldfirere/th-desugar"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-syb' 'haskell-th-lift' 'haskell-th-orphans' 'haskell-th-expand-syns')
makedepends=('ghc' 'haskell-hunit' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('00c27e183499af36d6a70570c4ec524563514376e7b239b929666916150b016d8060d79df01fe111c6924ba908d5b2b5dc33311b4e46f21ebe4102cb0795437f')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
