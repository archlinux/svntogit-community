# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=safecopy
pkgname=haskell-safecopy
pkgver=0.10.4.2
pkgrel=196
pkgdesc="Binary serialization with version control."
url="https://github.com/acid-state/safecopy"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-generic-data' 'haskell-old-time' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hunit' 'haskell-lens' 'haskell-lens-action' 'haskell-quickcheck'
             'haskell-quickcheck-instances' 'haskell-tasty' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2b6ae8488ac00bb5048e63886433aea6c2b13c9bea252fb8622ee1b673bce9e259639fbb68ef47a17ceac65edec2631ee95bc34a665c039f5c651d6192418bf5')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u lens -u vector $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
}
