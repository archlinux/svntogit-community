# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-pyicu
pkgver=2.10.2
pkgrel=2
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
depends=('python' 'icu')
makedepends=('python-setuptools')
checkdepends=('python-fonttools' 'python-pytest' 'python-six')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('1ccb43496e1719314c2f1f81f6535cb57688917b19fdbc56b2773f9372f70160d69660a67f63fde3cee0384d7df649aad8539375be5c9be3ede8e1335a126bd7')

build() {
  cd PyICU-$pkgver
  python setup.py build
}

check() {
  cd PyICU-$pkgver
  python setup.py test
}

package() {
  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
