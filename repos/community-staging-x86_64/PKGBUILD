# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: vande198
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=supertux
pkgver=0.3.3
pkgrel=4
pkgdesc="A classic 2D jump'n run sidescroller game in a style similar to the original SuperMario games"
arch=('i686' 'x86_64')
url="http://super-tux.sourceforge.net/"
license=('GPL')
depends=('sdl_image' 'curl' 'physfs' 'openal' 'libvorbis' 'libgl' 'glew')
makedepends=('cmake' 'boost')
source=(http://download.berlios.de/supertux/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f3f803e629ee51a9de0b366a036e393d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i '1i#include <cstddef>' src/supertux/screen_manager.hpp
  sed -i '/types\.h/d' src/addon/addon_manager.cpp
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_SUBDIR_BIN=bin .
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
