# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>
pkgname=gnuradio-iqbal
_pkgname=gr-iqbal
pkgver=0.38.1
pkgrel=3
pkgdesc="Gnuradio I/Q balancing"
arch=('x86_64')
url="https://cgit.osmocom.org/gr-iqbal/"
license=('GPL')
depends=('gnuradio' 'fftw')
replaces=('gr-iqbal')
makedepends=('git' 'cmake' 'boost' 'swig')
source=("git://git.osmocom.org/gr-iqbal/#tag=v$pkgver"
        'gr38-update.diff')
md5sums=('SKIP'
         '18484d0184b37f4e43a6e6f58b5c4c12')

prepare() {
  cd "$srcdir/$_pkgname"
  git submodule init
  git submodule update

  #sed -i 's/LIBOSMODSP_LIBRARIES ""/LIBOSMODSP_LIBRARIES ${FFTW3F_LIBRARIES}/' CMakeLists.txt 
  #sed -i 's/lib64/lib/'   CMakeLists.txt

  # patch from Maitland Bottoms of Debian
  #patch -p1 -i "$srcdir/gr38-update.diff"
  #rm cmake/Modules/GrMiscUtils.cmake

  # upstream mixed tabs and spaces
  sed -i 's/\t/        /' python/__init__.py
}

build() {
  cd "$srcdir/$_pkgname"
  mkdir -p build
  cd build
  cmake \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE=Release \
    -DPYTHON_EXECUTABLE=$(which python3) \
    -DPYTHON_INCLUDE_DIR=$(echo /usr/include/python3*) \
    -DPYTHON_LIBRARY=$(echo /usr/lib/libpython3.*.so) \
    -DGR_PYTHON_DIR=$(echo /usr/lib/python3.*/site-packages/) \
    -DPKG_CONFIG_PATH=/usr/lib/pkgconfig \
    -DCMAKE_LIBRARY_PATH=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr ../
  make
    #-DGnuradio_DIR=/usr/lib/cmake/gnuradio \
    #-DVolk_DIR=/usr/lib/cmake/vol \
}

package() {
  cd "$srcdir/$_pkgname/build/"
  make DESTDIR="${pkgdir}" install
}
