# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=uusi
pkgver=0.0.1.0
pkgrel=6
pkgdesc="Remove all version constraints of dependencies in .cabal file"
url="https://github.com/berberman/uusi"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-colourista' 'haskell-microlens' 'haskell-optparse-applicative')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1064dfa7f92f1b306c5bc7c75822acbf7a9c02fc56e1bb155c2c5902047c2283cb4d7f709c4c919248832cd7f50fbd7d115d915e8aff5aff3cf59340a0a4e37d')

prepare() {
  cd $pkgname-$pkgver
  echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
}

package() {
  cd $pkgname-$pkgver

  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
