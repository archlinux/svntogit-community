# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=chell-quickcheck
pkgname=haskell-chell-quickcheck
pkgver=0.2.5.2
pkgrel=143
pkgdesc="QuickCheck support for the Chell testing library"
url="https://github.com/typeclasses/chell"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-chell' 'haskell-quickcheck' 'haskell-random')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d17b8aeb19adc08debdfccaeceabb480fcbf82ba47c5e805f326f26be8c3266b741c52ab1ab13042b13aaa865ae355812ff067f3a46ff8186cef232143fa8efa')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *2.13/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
