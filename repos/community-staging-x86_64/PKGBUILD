# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=clanlib
pkgver=4.0.0
pkgrel=4
pkgdesc="A multi-platform game development library."
arch=('x86_64')
url="http://clanlib.org/"
license=('zlib')
depends=('alsa-lib' 'libjpeg' 'libmikmod' 'libpng' 'libvorbis' 'libxi' 'libxmu' 'sdl_gfx' 'freetype2' 'pcre' 'sqlite' 'libgl')
makedepends=('doxygen' 'graphviz' 'mesa' 'git')
source=(git://github.com/sphair/ClanLib.git#tag=v${pkgver})
sha512sums=('SKIP')

build() { 
	cd ClanLib

  ./autogen.sh
	./configure --prefix=/usr

	make
	make html
}

package() {
	cd ClanLib

	make DESTDIR=${pkgdir} install
	make DESTDIR=${pkgdir} install-html

	install -d ${pkgdir}/usr/share/licenses/${pkgname}
	install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}

# vim: sw=2:ts=2 et:
