# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=ispc
pkgver=1.8.2
pkgrel=3
pkgdesc="A compiler for high-performance SIMD programming on the CPU"
arch=('i686' 'x86_64')
url="https://ispc.github.io/"
license=('BSD')
depends=('zlib')
makedepends=('llvm' 'clang' 'python2')
source=("$pkgname-${pkgver}.tar.gz::https://github.com/ispc/ispc/archive/v${pkgver}.tar.gz"
        "ispc-1.8.2-llvm-3.7.patch.gz")
md5sums=('ef99fb9e5fe5ca59d429ea8d0e6affd0'
         '44fc2588209fafe8efee71c482a282cc')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # fix python version
  find . -name "*.py" -exec sed -i 's|python$|python2|' '{}' ';'
  sed -i 's|python |python2 |' Makefile
  # fix build with llvm 3.7
  # https://github.com/ispc/ispc/issues/1106#issuecomment-149623230
  patch -Np1 -i ../ispc-1.8.2-llvm-3.7.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -D -m755 ispc "$pkgdir/usr/bin/ispc"
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

