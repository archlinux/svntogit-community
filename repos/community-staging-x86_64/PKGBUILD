# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=network-ip
pkgname=haskell-network-ip
pkgver=0.3.0.3
pkgrel=42
pkgdesc="Internet Protocol data structures"
url="https://github.com/mvv/network-ip"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default-class' 'haskell-data-dword' 'haskell-data-endian'
         'haskell-data-serializer' 'haskell-data-textual' 'haskell-hashable' 'haskell-parsers'
         'haskell-text-printer' 'haskell-type-hint')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0d628e514d3696db04350e54f87f179d5468e983001bfc3865ac65f6f430416c5c111b1979b368dd8115f06dd94c675b28b632ca75b22341fcc1522b7f1f2e36')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
