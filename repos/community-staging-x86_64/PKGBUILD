# Maintainer: Anatol Pomozov
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.7.0
pkgrel=2
pkgdesc='Device Tree Compiler'
url='https://www.devicetree.org/'
arch=(x86_64)
license=(GPL2)
depends=(libyaml)
makedepends=(swig)
checkdepends=(valgrind)
source=(https://www.kernel.org/pub/software/utils/dtc/dtc-$pkgver.tar.xz)
sha256sums=('29edce3d302a15563d8663198bbc398c5a0554765c83830d0d4c0409d21a16c4')

prepare() {
  cd dtc-$pkgver
  sed -i 's/-Werror//' Makefile
}

build() {
  cd dtc-$pkgver
  # python build fails as it requires a lot of extra dependencies.
  # someone with more python experience needs to look at the proper way to build the python extension.
  NO_PYTHON=1 make
}

check() {
  cd dtc-$pkgver
  NO_PYTHON=1 make check
}

package() {
  cd dtc-$pkgver
  NO_PYTHON=1 DESTDIR="$pkgdir" make SETUP_PREFIX="$pkgdir/usr" PREFIX="$pkgdir/usr" install
}
