# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Agda
pkgname=agda
pkgver=2.5.1.1
pkgrel=1
pkgdesc="A dependently typed functional programming language and proof assistant"
url="http://wiki.portal.chalmers.se/agda/"
license=("custom")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'zlib')
makedepends=("ghc=8.0.1" "alex" "happy" "haskell-boxes" "haskell-cpphs"
             "haskell-data-hash" "haskell-edisonapi" "haskell-edisoncore"
             "haskell-edit-distance" "haskell-equivalence" "haskell-filemanip"
             "haskell-geniplate-mirror" "haskell-hashable" "haskell-hashtables"
             "haskell-monadplus" "haskell-mtl" "haskell-parallel"
             "haskell-quickcheck" "haskell-src-exts" "haskell-strict"
             "haskell-text" "haskell-transformers-compat"
             "haskell-unordered-containers" "haskell-zlib")
optdepends=('agda-stdlib: for standard library')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('563b8063fc94349b56ae1867e973f1751db0e9a8997af7ede93d3c3a8c66a6b0')

prepare() {
    sed -i 's/, EdisonAPI == 1.3/, EdisonAPI >= 1.3/' $_hkgname-$pkgver/$_hkgname.cabal

    # TODO: Find a better way!
    # Build it twice to compile the agdai file.
    cp -a ${_hkgname}-${pkgver}{,-tmp}
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fcpphs -f-uhc
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build

    cd "${srcdir}/${_hkgname}-${pkgver}-tmp"
    runhaskell Setup configure --prefix="$PWD/target" --datasubdir="$pkgname"
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    runhaskell Setup copy

    target/bin/agda target/share/agda/lib/prim/Agda/Primitive.agda
    for _file in target/share/agda/lib/prim/Agda/Builtin/*.agda; do
        target/bin/agda "$_file"
    done
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    install -m644 "${srcdir}/${_hkgname}-${pkgver}-tmp"/target/share/agda/lib/prim/Agda/Primitive.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Primitive.agdai  
    install -m644 "${srcdir}/${_hkgname}-${pkgver}-tmp"/target/share/agda/lib/prim/Agda/Builtin/*.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Builtin/

    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
