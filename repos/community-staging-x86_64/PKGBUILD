# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-music
pkgver=5.1.1
pkgrel=2
pkgdesc='The Pantheon Music Player'
arch=(x86_64)
url=https://github.com/elementary/music
license=(GPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gst-plugins-base-libs
  gstreamer
  gtk3
  json-glib
  libaccounts-glib
  libgda
  libgee
  libgpod
  libgranite.so
  libhandy-1.so
  libpeas
  libsignon-glib
  libsoup
  pango
  taglib
  zeitgeist
)
makedepends=(
  git
  gobject-introspection
  intltool
  meson
  vala
)
optdepends=(
  'gst-plugins-base: "Base" plugin libraries'
  'gst-plugins-good: "Good" plugin libraries'
  'gst-plugins-bad: "Bad" plugin libraries'
  'gst-plugins-ugly: "Ugly" plugin libraries'
)
_tag=93559f49682ad012e846408163851d0f671e1650
source=(
  pantheon-music::git+https://github.com/elementary/music.git#tag=${_tag}
  pantheon-music-meson.patch
)
b2sums=('SKIP'
        '16ddefafd2a5957e355fb53cafbd3c0f728f33b0c0097969896a14908f951c23dfb7268b440b53f8f41a018cbdecb445bb77957b3b6ebe1ee099cca7c7b98fcf')

prepare() {
  cd pantheon-music
  git cherry-pick -n 9ed3bbb3a0d68e289a772b4603f58e52a4973316
  git cherry-pick -n 84bbdf1b0fa55a3980aef69bea13ca0431c8e022
  patch -Np1 -i ../pantheon-music-meson.patch
}

pkgver() {
  cd pantheon-music
  git describe --tags
}

build() {
  arch-meson pantheon-music build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
