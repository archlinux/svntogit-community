# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=tellico
pkgver=2.3.10
pkgrel=4
pkgdesc="A collection manager for KDE"
arch=('i686' 'x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('kdebase-runtime' 'qimageblitz' 'kdepimlibs' 'yaz' 'exempi' 'libksane4' 'taglib' 'libkcddb' 'poppler-qt4' 'kdemultimedia-audiocd-kio')
makedepends=('automoc4' 'cmake')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://tellico-project.org/files/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e916101f86b6f843b85be42e8545aa7441cd496cbb9b81590e2e0bd03a58faaa')

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DWITH_Nepomuk=OFF
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

# fix python 2.7 path
  find "${pkgdir}" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
