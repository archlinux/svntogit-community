# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall-json
pkgver=1.7.3
pkgrel=21
pkgdesc='Convert between Dhall and JSON or YAML'
url='https://dhall-lang.org'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-aeson-yaml' 'dhall'
         'haskell-lens-family-core' 'haskell-optparse-applicative' 'haskell-prettyprinter'
         'haskell-scientific' 'haskell-unordered-containers' 'haskell-prettyprinter-ansi-terminal')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-silver')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('06787ef0eca44e4151498a02b0528f04a69c1d7e82b152d3e1c1c78012d4a656eb09256faff41b577ce235c6028cd2927e64684af7e2270fe81208ae956ab0ad')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
