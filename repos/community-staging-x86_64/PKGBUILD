# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=3.6
pkgrel=2
pkgdesc='Personal and small-business financial-accounting application'
arch=(x86_64)
url='https://gnucash.org/'
license=(GPL)
depends=(aqbanking boost-libs guile libdbi webkit2gtk)
makedepends=(boost cmake gmock libdbi-drivers mariadb-libs postgresql-libs)
optdepends=('gnucash-docs: documentation'
            'libdbi-drivers: open and save data into SQL database'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        Fix-CMakeLists-error.patch)
sha256sums=('3ac6627834bb163653eccf4873dabe2adf4d6feb40e0d9f6f6f4d8d91a321677'
            '1161e9b7e972eaf881376841ec7fbaa68e97d23da6b7694298040ddb721435ac')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../Fix-CMakeLists-error.patch
}

build() {
  cd $pkgname-$pkgver

  # Do not treat warnings as errors
  sed -e 's|-Werror||g' -i CMakeLists.txt

  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DWITH_PYTHON=ON \
        -DHAVE_GWEN_GTK3=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 2 tests failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
