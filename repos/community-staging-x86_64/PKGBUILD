# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: spambanane <happy.house@gmx.de>
# Contributor: Matteo <matteo.dek@gmail.com>
# Contributor: Matthew Zimmerman <mzimmerman@gmail.com>

pkgname=liteide
pkgver=27.2.1
pkgrel=2
pkgdesc='IDE for editing and building projects written in the Go programming language'
license=('LGPL')
arch=('x86_64' 'i686')
url='https://github.com/visualfc/liteide'
depends=('go' 'qt5-webkit')
replaces=('golangide')
conflicts=('golangide')
makedepends=('gendesk' 'git' 'go' 'mercurial' 'setconf')
options=('!strip' '!emptydirs')
#source=("$pkgname-x$pkgver::git://github.com/visualfc/liteide.git#commit=c2e2ac2301"
source=("$pkgname-x$pkgver::git://github.com/visualfc/liteide.git#tag=x$pkgver"
        'liteide.png' qt-5.5.patch::"https://github.com/visualfc/liteide/commit/08e75925.patch")
md5sums=('SKIP'
         '7f459be003d754a3a2c34be91b8c0b1f'
         'ee7426bbc73708799a0fe87d5b91c273')

prepare() {
  gendesk -f -n --name 'LiteIDE' --pkgname "$pkgname" --pkgdesc "$pkgdesc"
  chmod +x "liteide-x$pkgver/build/"*_*.sh

# Fix build against Qt 5.5
  pushd liteide-x$pkgver
  patch -p1 -i ../qt-5.5.patch
  popd

  cd "liteide-x$pkgver/liteidex/os_deploy/linux/liteenv"
  for bits in 32 64; do
    # Fix for FS#4662 until fixed by upstream.
    sed -i 's/^GOROOT/#GOROOT/g' "linux$bits.env"
  done
}

build() {
  cd "liteide-x$pkgver/build"

  export QTDIR=/usr

  mkdir -p go
  export GOPATH=`pwd`/go

  ./update_pkg.sh
  ./build_linux.sh
}

package() {
  cd "liteide-x$pkgver/build/liteide"

  msg2 'Creating directories...'
  mkdir -p \
    "$pkgdir/usr/lib/liteide" \
    "$pkgdir/usr/share/liteide" \
    "$pkgdir/usr/share/doc/$pkgname"

  msg2 'Packaging executables...'
  for binary in gotools gocode liteide; do # goimports
    install -Dm755 "bin/$binary" "$pkgdir/usr/bin/$binary"
  done
  ln -s /usr/bin/liteide "$pkgdir/usr/bin/golangide"

  cd "$srcdir/liteide-x$pkgver/liteidex"

  msg2 'Packaging resources...'
  cp -r deploy/* os_deploy/* "$pkgdir/usr/share/liteide"

  msg2 'Packaging libraries and plugins...'
  cp -r liteide/lib/liteide/* "$pkgdir/usr/lib/liteide"
  chmod -x "$pkgdir/usr/lib/liteide/plugins/"*

  msg2 'Packaging license and license exception...'
  install -Dm644 LICENSE.LGPL \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 LGPL_EXCEPTION.TXT \
    "$pkgdir/usr/share/licenses/$pkgname/LGPL_EXCEPTION"

  cd ../..

  msg2 'Packaging menu entry and icon...'
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 'Cleaning up...'
  rm -rf "$pkgdir/usr/share/$pkgname/doc"

  # Fix for FS#38781
  mv "$pkgdir/usr/share/liteide/linux/liteenv" "$pkgdir/usr/share/liteide/liteenv"
}

# vim:set ts=2 sw=2 et:
