# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kyle Keen <keenerd at gmail dot com>

pkgname=python-pyzmq
pkgver=25.0.2
pkgrel=2
pkgdesc='Python bindings for ZeroMQ'
arch=('x86_64')
url='https://zeromq.org/languages/python/'
license=('LGPL')
depends=('zeromq' 'python')
makedepends=(
  'git'
  'python-build'
  'python-installer'
  'python-setuptools'
  'python-wheel'
  'cython'
)
_commit='1f1d6bd84db823067f2c24cd9f5bb563fdc4181d'
source=("$pkgname::git+https://github.com/zeromq/pyzmq#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  python -m build --wheel --no-isolation
}

package() {
  cd "$pkgname"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
