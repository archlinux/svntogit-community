# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_prj=trellis
pkgname=prj$_prj
pkgver=1.0
pkgrel=2
pkgdesc='Documentation and definitions for the Lattice ECP5 bit-stream format'
arch=('x86_64')
url='https://github.com/YosysHQ/prjtrellis'
license=('custom:ISC' 'MIT')
depends=('boost-libs')
makedepends=('cmake' "$pkgname-db" 'boost' 'python-sphinx' 'python-sphinx_rtd_theme' 'python-recommonmark')
optdepends=('python: Python support')
provides=('libtrellis')
conflicts=('libtrellis')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        "fix-undefined-runtime_error.patch::$url/commit/d323f5f3834d23a7d070ccc64baf7ead527b9eba.patch")
sha512sums=('9418cd8b20e0121ca8fce3978b25209df5211b13692db9f86c08c01fc28e40b15dce5c1aed73e09f5e4d9b038259a2a4a8d346bc689d9f6f20782be170728b84'
            '6043610695cd18891bbce9215ff301e3c05f4a7f4427c931cda32352012deb4ef57c1f24196f500b7e73a18b41210d6fb20fbe8f49b09993c9b4b617e7cd1b7d')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../fix-undefined-runtime_error.patch

  sed -i 's|set(LIBDIR "lib64")|set(LIBDIR "lib")|
          s|project(libtrellis)|project(libtrellis VERSION 1.0)|' libtrellis/CMakeLists.txt
}

build() {
  mkdir $pkgname-$pkgver/libtrellis/build
  cd $pkgname-$pkgver/libtrellis/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/lib \
    -DCMAKE_BUILD_TYPE=Release

  sed -i '/Boost::python-NOTFOUND/d' CMakeFiles/*.dir/build.make
  sed -i 's|Boost::python-NOTFOUND||g' CMakeFiles/*.dir/link.txt

  make

  cd ../../docs

  make html
}

package() {
  cd $pkgname-$pkgver

  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r --no-preserve=ownership docs/_build/* "$pkgdir"/usr/share/doc/$pkgname/
  rm -rf "$pkgdir"/usr/share/doc/$pkgname/html/.doctrees

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd libtrellis/build

  make DESTDIR="$pkgdir" install

  # The database is provided in a separate package
  rmdir "$pkgdir"/usr/share/$_prj/database
}

