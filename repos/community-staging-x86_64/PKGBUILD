# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=haskell-aeson
_hkgname=aeson
pkgver=1.3.1.1
pkgrel=18
pkgdesc="A JSON parsing and encoding library optimized for ease of use and high performance."
url="https://github.com/bos/aeson"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-compat' 'haskell-dlist' 'haskell-hashable'
         'haskell-scientific' 'haskell-tagged' 'haskell-th-abstraction' 'haskell-time-locale-compat'
         'haskell-unordered-containers' 'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-integer-logarithms'
             'haskell-base-orphans' 'haskell-base16-bytestring' 'haskell-generic-deriving'
             'haskell-hashable-time' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-quickcheck-instances')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('ab506e0aa453542c1b609e65d36ed56758c43b772c1eaa13274bf4f9e72366bdf02ba31de0a7a8673c7a0d200837dd0192e1269fbe51d3b7d0149dce6d0f6968')

prepare() {
  cd $_hkgname-$pkgver
  sed -i -e 's/<.*2.11/<3/' -e 's/<.*0.7/<1/' $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-developer -f-fast -f-bytestring-builder -f-cffi
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
