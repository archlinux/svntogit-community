# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=haskell-gi
pkgname=haskell-gi
pkgver=0.26.3
pkgrel=12
pkgdesc="Generate Haskell bindings for GObject Introspection capable libraries"
url="https://github.com/haskell-gi/haskell-gi"
license=("LGPL2")
arch=('x86_64')
depends=('ghc-libs' 'glib2' 'gobject-introspection-runtime' 'haskell-gi-base'
         'haskell-ansi-terminal' 'haskell-attoparsec' 'haskell-pretty-show' 'haskell-safe'
         'haskell-xdg-basedir' 'haskell-xml-conduit' 'haskell-regex-tdfa')
makedepends=('ghc' 'gobject-introspection' 'haskell-cabal-doctest' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        gunichar-array.patch::https://github.com/haskell-gi/haskell-gi/pull/401.patch)
sha512sums=('65423d5b0b91e70150fc5ba3952f67b23eb8539f4472978cd1d20701a9dd63c010071325e54aa8bb58f0a0ae42ff702aac2f1aaf00c463da2d93ea4ad697a468'
            '362f9282bc0c13e2b21952258ac1ed4570be6241a69f756b60e3f666bfac8743b26290a38e1516636fd1d5e955bfa78a699c856b7cfd68e774cb59d36cb66c58')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../gunichar-array.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
