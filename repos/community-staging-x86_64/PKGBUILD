# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-citeproc
pkgver=0.17.0.1
pkgrel=23
pkgdesc="Supports using pandoc with citeproc"
url="https://hackage.haskell.org/package/$pkgname"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-attoparsec'
         'haskell-base-compat' 'haskell-data-default' 'haskell-hs-bibutils' 'haskell-libyaml'
         'haskell-network' 'haskell-old-locale' 'pandoc' 'haskell-pandoc-types' 'haskell-rfc5051'
         'haskell-safe' 'haskell-setenv' 'haskell-split' 'haskell-syb' 'haskell-tagsoup'
         'haskell-temporary' 'haskell-text-icu' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-xml-conduit' 'haskell-yaml' 'haskell-hsyaml' 'haskell-hsyaml-aeson')
conflicts=('haskell-pandoc-citeproc')
replaces=('haskell-pandoc-citeproc')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/${pkgver}/$pkgname-${pkgver}.tar.gz")
sha512sums=('b616007c8b105e3245bae87f60e2ad55809435c82cf91cf32498dad8f2303df48d4555e68c3fbd9f78dc49fd1042ff46061273031c3352df130fc735f443be96')

prepare() {
    cd "${srcdir}/$pkgname-${pkgver}"
    # TODO: find a better solution
    sed -i "s|(\"HOME\",\".\")|(\"HOME\",\".\"),(\"LD_LIBRARY_PATH\",\"$PWD/dist/build\"),(\"pandoc_citeproc_datadir\",\"$PWD\")|" tests/test-pandoc-citeproc.hs
}

build() {
    cd "${srcdir}/$pkgname-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-debug -f-test_citeproc -funicode_collation -f-embed_data_files -fbibutils -f-static
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    # https://github.com/jgm/pandoc-citeproc/issues/342
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd "${srcdir}/$pkgname-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/$pkgname.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/$pkgname.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/$pkgname/LICENSE"
}
