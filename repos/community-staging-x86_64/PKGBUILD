# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=swagger2
pkgname=haskell-swagger2
pkgver=2.1.5
pkgrel=2
pkgdesc="Swagger 2.0 data model"
url="https://github.com/GetShopTV/swagger2"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-base-compat" "haskell-hashable" "haskell-generics-sop"
         "haskell-http-media" "haskell-insert-ordered-containers" "haskell-lens" "haskell-mtl"
         "haskell-network" "haskell-scientific" "haskell-text" "haskell-transformers-compat"
         "haskell-unordered-containers" "haskell-vector" "haskell-uuid-types")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('45be798944574e9c5bc3bcbf0c8053cfc0feb9b9b9eaf580602f9f95672ceb399d43984de2eedf207337c018d637a65774be59787892cb10df04caa07f5046a0')

prepare() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    sed -i 's/import Data.Aeson/import Data.Aeson hiding (constructorTagModifier)/' src/Data/Swagger/Internal/ParamSchema.hs
    sed -i 's/import Data.Aeson/import Data.Aeson hiding (constructorTagModifier, unwrapUnaryRecords, fieldLabelModifier, allNullaryToStringTag)/' src/Data/Swagger/Internal/Schema.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
