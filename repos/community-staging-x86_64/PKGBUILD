# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=text-conversions
pkgname=haskell-text-conversions
pkgver=0.3.0
pkgrel=16
pkgdesc="Safe conversions between textual types"
url="https://github.com/cjdev/text-conversions"
license=('ISC')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base16-bytestring' 'haskell-base64-bytestring' 'haskell-errors')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-discover')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('767816f953468932ab579d0cbdd20f7bbe1fc72c4ed8a857bc7bfa52a2d07abafc2065e9844ddea49f44bcf32b06e5c1abd64a222498cc6435129f2b36c036ec')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
