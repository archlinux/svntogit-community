# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>
# Contributor: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=hivex
pkgver=1.3.21
pkgrel=8
pkgdesc="System for extracting the contents of Windows Registry."
arch=("x86_64")
url="http://libguestfs.org"
license=("LGPL2.1")
depends=("libxml2" "perl")
makedepends=("python" "ruby" "ruby-rake" "ruby-rdoc" "perl-io-stringy" "perl-test-simple" "ocaml-findlib" "ocaml" "ocaml-compiler-libs" "chrpath")
optdepends=("python: python bindings"
	    "ruby: ruby bindings"
	    "ocaml: ocaml bindings")
source=("http://libguestfs.org/download/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
sha512sums=('e893d9fa77655540d29ef6657764f701f9cddb33d11b2bfef36ce0c2caebab92b55a32b7e1c99efbab0d53f558a44bf1c4c6f6da13daa37d568ff96d7d28f842'
            'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

build() {
    cd "$pkgname-$pkgver"

    CFLAGS+=' -ffat-lto-objects' \
    ./configure \
	--bindir=/usr/bin \
	--libdir=/usr/lib \
	--prefix=/usr \
	--disable-rpath \
	--disable-static PYTHON=python

    make
    chrpath -d perl/blib/arch/auto/Win/Hivex/Hivex.so
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
