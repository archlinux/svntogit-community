# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=mustache
pkgname=haskell-mustache
pkgver=2.4.0
pkgrel=17
pkgdesc="A mustache template parser library."
url="https://github.com/JustusAdam/mustache"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-scientific' 'haskell-th-lift' 'haskell-cmdargs' 'haskell-yaml')
makedepends=('ghc' 'haskell-hspec' 'haskell-base-unicode-symbols' 'haskell-wreq' 'haskell-zlib'
             'haskell-tar' 'haskell-lens' 'haskell-temporary')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        $pkgname-unordered-containers-0.2.17.patch::https://github.com/JustusAdam/mustache/pull/59.patch)
sha512sums=('897cd682648a363a4fe4225a65f782f743fb9631dc9e58d99eb443931989975b42bee0815037ee839fc70e1a40887e5417a05b105e4ca79565e2bd52b5fcd385'
            '01a3754ff9d626eda4fe24619fb746b50a2570757be1f2ec67975a74629c6ab02a751d67da31969d9256257406ab5c752b14ee29e431374e547ed52399a3e4e4')

prepare() {
    patch -d $_hkgname-$pkgver -p1 < $pkgname-unordered-containers-0.2.17.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
