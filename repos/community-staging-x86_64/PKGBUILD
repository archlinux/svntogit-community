# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf
pkgver=4.5.0
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=('i686' 'x86_64')
url="https://www.unidata.ucar.edu/software/netcdf/"
depends=('hdf5' 'curl')
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=('!makeflags')
license=('custom')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-c/archive/v${pkgver}.tar.gz"
        'fix-tst_h_dimscales.patch')
sha256sums=('f7d1cb2a82100b9bf9a1130a50bc5c7baf0de5b5022860ac3e09a0a32f83cf4a'
            'b2b18ddd4dc0c0e08e59454a6e141e4ba6547ffd5df50d5fe758df45ec77aa6c')

prepare() {
    cd ${pkgname}-c-${pkgver}
    patch -p1 -i ../fix-tst_h_dimscales.patch
}

build() {
    cd ${pkgname}-c-${pkgver}
    ./configure \
        --prefix=/usr \
        --enable-shared \
        --disable-parallel4
    make
}

check() {
    cd ${pkgname}-c-${pkgver}
    make check
}

package() {
    cd ${pkgname}-c-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
