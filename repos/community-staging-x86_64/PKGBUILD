# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Sebastian Sareyko <public@nooms.de>

pkgname=gnustep-base
pkgver=1.22.0
pkgrel=3
pkgdesc="The GNUstep base package"
arch=('i686' 'x86_64')
url="http://www.gnustep.org/"
license=("GPL" "LGPL")
depends=(libxslt avahi gmp gcc-libs openssl libffi gnustep-make gnutls)
makedepends=(gcc-objc)
conflicts=('gnustep-base-svn')
groups=('gnustep-core')
options=('!emptydirs' '!makeflags')
source=(ftp://ftp.gnustep.org/pub/gnustep/core/$pkgname-$pkgver.tar.gz)
md5sums=('7faab4891ae6c1e7a6c806d6da368410')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  source /etc/profile.d/GNUstep.sh
  ./configure --prefix=/usr --sysconfdir=/etc/GNUstep --with-ffi-include=/usr/lib/libffi-3.0.9/include/
  # fix file ownership
  sed -i 's/tar -xf $(TIMEZONE_ARCHIVE);/tar -xf $(TIMEZONE_ARCHIVE);chown -R root:root * ;/' NSTimeZones/Makefile.postamble
  make VERBOSE=1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  chown -R root.root "$pkgdir/"
}
