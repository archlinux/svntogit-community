# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=wl-pprint-extras
pkgname=haskell-wl-pprint-extras
pkgver=3.5.0.5
pkgrel=366
pkgdesc="A free monad based on the Wadler/Leijen pretty printer"
url="https://github.com/ekmett/wl-pprint-extras/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-semigroupoids' 'haskell-utf8-string')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('705107842ccbf4eb23d0b9e8069fd4d5bb1b03ffecbf9efaa8a579fd977380913d590fa370cc42476d5b28fde98fc44e7879fe343f761cd1751ebeb0b8c0e242')

prepare() {
    cd $_hkgname-$pkgver
    sed -e '/nats/d' -e '/semigroups/d' -e 's/< *0.6/<1/' -i $_hkgname.cabal
    sed -i -e '/fail _ = empty/d' src/Text/PrettyPrint/Free/Internal.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
