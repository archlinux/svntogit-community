# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-lift
pkgname=haskell-th-lift
pkgver=0.7.7
pkgrel=2
pkgdesc="Derive Template Haskell's Lift class for datatypes."
url="http://github.com/ekmett/th-lift/"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('490d349ef2985defcf58eb49686c89ab79f8f9a3e29f3edb195440fe7995ecec6b2538767855ae0a5b9f43e5b00fe0bee167513486be99c111776ab7855d37d2')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "BSD3" "${pkgdir}/usr/share/licenses/${pkgname}/BSD3"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/BSD3"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
