# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primesieve
pkgver=11.0
pkgrel=1
pkgdesc='Fast prime number generator'
arch=(x86_64)
url='https://github.com/kimwalisch/primesieve'
license=(BSD)
depends=(gcc-libs)
makedepends=(cmake)
source=(https://github.com/kimwalisch/primesieve/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('b29a7ec855764ce7474d00be03e1d83209bd097faa3778382dfb73a06866097e')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
