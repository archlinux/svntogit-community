# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark-gfm
pkgver=0.29.0.gfm.5
pkgrel=1
pkgdesc="GitHub's fork of cmark, a CommonMark parsing and rendering library and program in C"
arch=('x86_64')
url="https://github.com/github/cmark"
license=('custom:BSD2')
depends=('glibc')
makedepends=('cmake' 'python')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        remove-broken-include-on-config-h.patch::https://github.com/github/cmark-gfm/pull/248.patch)
sha512sums=('ecba7434c104bae2592044c6031ce5cc3aca89abdb3cc962074b421473fc7fc06d5fde548b7e45684a57baa346c0355d12185b03e472af436821c8610b9f60c6'
            '90726e254268cdcbd80dc3e7e5aee6ced7a13785eb16aa585413bd279079d40c790d631e3bff1c073468d0aa822157cbbbc15f60af9c466c5451c2e1fd31092b')

prepare() {
  mkdir -p build
  patch -d cmark-gfm-$pkgver -Np1 < remove-broken-include-on-config-h.patch
}

build() {
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../cmark-gfm-$pkgver
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"
  install -Dm0644 -t "$pkgdir"/usr/share/licenses/$pkgname/ "$srcdir"/cmark-gfm-$pkgver/COPYING
}
