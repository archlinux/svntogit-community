# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hopenpgp-tools
pkgver=0.19.5
pkgrel=30
pkgdesc="hOpenPGP-based command-line tools"
url="http://floss.scru.org/hopenpgp-tools"
license=("AGPL3")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-ansi-wl-pprint" "haskell-attoparsec"
         "haskell-base16-bytestring" "haskell-binary-conduit" "haskell-conduit"
         "haskell-conduit-extra" "haskell-crypto-pubkey" "haskell-cryptohash" "haskell-errors"
         "haskell-fgl" "haskell-graphviz" "haskell-hopenpgp" "haskell-http-client"
         "haskell-http-client-tls" "haskell-http-types" "haskell-ixset-typed" "haskell-lens"
         "haskell-monad-loops" "haskell-openpgp-asciiarmor" "haskell-optparse-applicative"
         "haskell-resourcet" "haskell-text" "haskell-time-locale-compat" "haskell-wl-pprint-extras"
         "haskell-wl-pprint-terminfo" "haskell-yaml")
makedepends=('alex' 'happy' 'ghc')
source=(https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0ecba903607ef27f14c43bf1f6a2383a740c44802dcecf6ac093f41f8d754d77ef6f14e8200316cd2d7e56b8c63934299c521b6693baf1ca219b65485fd6d52a')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    rm "$pkgdir/usr/share/doc/hopenpgp-tools/LICENSE"
    rmdir "$pkgdir/usr/share/doc/hopenpgp-tools" "$pkgdir/usr/share/doc" "$pkgdir/usr/share"
}
