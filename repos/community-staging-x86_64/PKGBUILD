# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin
pkgver=3.0.0rc1
_tag=v3.0.0-rc1
pkgrel=3
arch=('x86_64')
pkgdesc="Statistical Language Model based pinyin IME by Sun"
license=('LGPL')
url="http://sunpinyin.googlecode.com"
depends=('sqlite' 'make' 'gcc-libs')
optdepends=('sunpinyin-data: Statistical language model data from open-gram project for sunpinyin')
makedepends=('git' 'scons' 'intltool' 'python2')
source=("git://github.com/sunpinyin/sunpinyin.git#tag=$_tag" scons-python3.patch)
md5sums=('SKIP'
         '268a91412d5e5c8f3e7a092b0daa8220')

prepare() {
  cd "$srcdir/$pkgname"
  sed -i -e "1s|python|python2|" python/*.py python/importer/*.py

  patch -p1 -i ../scons-python3.patch
}

build() {
  cd "$srcdir/$pkgname"
  scons \
    --prefix=/usr
}

package() {
  cd "$srcdir/$pkgname"
  scons \
    --prefix=/usr \
    --install-sandbox="$pkgdir" \
    install
}
