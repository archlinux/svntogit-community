# Maintainer: Anatol Pomozov
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=pulseview
pkgver=0.3.0
pkgrel=5
pkgdesc='A Qt based logic analyzer GUI for sigrok'
arch=(i686 x86_64)
url='http://sigrok.org/wiki/Main_Page'
license=(GPL3)
depends=(libsigrok libsigrokdecode qt5-base qt5-svg boost-libs)
makedepends=(cmake boost)
source=(http://sigrok.org/download/source/pulseview/pulseview-$pkgver.tar.gz)
sha1sums=('6a829e37c7ccefe84265080d36a7dc43cfec3672')

build() {
  cd pulseview-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_CXX_FLAGS=-fext-numeric-literals -DDISABLE_WERROR=ON -DENABLE_TESTS=OFF .
  make
}

package() {
  cd pulseview-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 contrib/pulseview.desktop "$pkgdir/usr/share/applications/pulseview.desktop"
  install -Dm644 icons/sigrok-logo-notext.svg "$pkgdir/usr/share/pixmaps/sigrok-logo-notext.svg"
}
