# Maintainer: Anatol Pomozov
# Contributor: Nicolas Quiénot < niQo at aur >
# Contributor: Damián Nohales <damiannohales at gmail.com>
 
pkgname=evemu
pkgver=2.7.0
pkgrel=9
pkgdesc='Tools and bindings for kernel input event device emulation and data capture and replay'
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Evemu'
license=(GPL3)
depends=(libevdev)
makedepends=(asciidoc python xmlto)
optdepends=('python: Python 3 bindings')
source=(https://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=(0A75E35E0FAEE97EC769103E2F2670AC164DB36F) # Benjamin Tissoires <benjamin.tissoires@gmail.com>
sha256sums=('78c9400d55eeeb5ab75161360543f9376438c4da4934cb34cdda5b46021ae379'
            'SKIP')
 
build() {
  cd $pkgname-$pkgver
  PYTHON=python3 ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  # Tests have to be run as 'root'
  # make check
}
 
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
