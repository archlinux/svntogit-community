# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=binary-conduit
pkgname=haskell-binary-conduit
pkgver=1.2.5
pkgrel=21
pkgdesc="Data serialization/deserialization conduit library"
url="https://github.com/qnikst/binary-conduit/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-conduit' 'haskell-resourcet' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('131c5fbca74ca5043fd65826e27dd9d5f2407fc8c1ea8f7c803083f565b33661d0768d0638d452409b2218949e2f4b1c6ccc5a4acad5c14948b59713b8435d70')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
