# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-glib
pkgver=0.13.5.0
pkgrel=2
pkgdesc="Binding to the GLIB library for Gtk2Hs."
url="https://hackage.haskell.org/package/glib"
license=('LGPL2.1')
arch=('x86_64')
depends=('ghc-libs' 'glib2' 'haskell-utf8-string' 'haskell-text')
makedepends=('ghc' "haskell-gtk2hs-buildtools")
provides=('gtk2hs-glib')
replaces=('gtk2hs-glib')
conflicts=('gtk2hs-glib')
source=(https://hackage.haskell.org/packages/archive/glib/$pkgver/glib-$pkgver.tar.gz)
sha512sums=('33619e6681dbc6cc204c0bac21db051f05f3044912fed34035859583b6d447e91b537e68eb0994ade66ad4f276c3a9b7c88823e0ba4c43c087e604f2ea90cdbd')

build() {
    cd "${srcdir}/glib-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/glib-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
}
