# $Id$
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf-fortran
pkgver=4.4.4
pkgrel=5
pkgdesc="NetCDF fortran bindings"
arch=('x86_64')
url="https://github.com/Unidata/netcdf-fortran"
license=('custom')
depends=('gcc-libs' 'netcdf' 'curl' 'gcc-fortran')
makedepends=('doxygen')
options=('!makeflags' 'staticlibs')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('44b1986c427989604df9925dcdbf6c1a977e4ecbde6dd459114bca20bf5e9e67')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --enable-shared
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
