# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=2.0.0
pkgrel=3
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=(i686 x86_64)
url='https://code.google.com/p/cantata/'
license=(GPL)
depends=(qt5-svg libmtp libcddb libmusicbrainz5 mpg123 vlc taglib-extras cdparanoia media-player-info udisks2)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback' 'ffmpeg: ReplayGain support')
makedepends=(cmake qt5-tools ffmpeg)
install=$pkgname.install
source=("https://github.com/CDrummond/cantata/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('dc7a65145692c5b2cac4c2c8a7c767c5')

prepare() {
  mkdir -p build

# Fix build against ffmpeg 3.0
  sed -e '/audioconvert.h/d' -i $pkgname-$pkgver/replaygain/ffmpeginput.cpp
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TOUCH_SUPPORT=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
