# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=http-common
pkgname=haskell-http-common
pkgver=0.8.2.0
pkgrel=78
pkgdesc="Common types for HTTP clients and servers"
url="https://github.com/afcowie/http-common"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base64-bytestring' 'haskell-blaze-builder' 'haskell-case-insensitive'
         'haskell-network' 'haskell-unordered-containers')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e02e9b08235483220856ed1cacebea1910ec41451d7ec68b86a2193f5c050f74ef3747a4a2c35c560795727d9f62ee6485cfd1ed5597fb7645ce6339f969851a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENCE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENCE"
}
