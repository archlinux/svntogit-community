# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-static
pkgname=haskell-yesod-static
pkgver=1.6.0.1
pkgrel=303
pkgdesc="Static file serving subsite for Yesod Web Framework."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-blaze-builder' 'haskell-conduit' 'haskell-cryptonite'
         'haskell-cryptonite-conduit' 'haskell-css-text' 'haskell-data-default'
         'haskell-file-embed' 'haskell-hashable' 'haskell-hjsmin' 'haskell-http-types'
         'haskell-memory' 'haskell-mime-types' 'haskell-unix-compat' 'haskell-unordered-containers'
         'haskell-wai' 'haskell-wai-app-static' 'haskell-yesod-core')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit' 'haskell-wai-extra' 'haskell-yesod-test')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2e1e4873248d435a9d58f3075b6c06cde2edcdd520f700dc7f0427039c236d28787f62a643a8cf57b74d85a885625474a5662b91a56c25e8f4d1737be525bc2f')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
