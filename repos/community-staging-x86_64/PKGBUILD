# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=test-framework-th
pkgname=haskell-test-framework-th
pkgver=0.2.4
pkgrel=15
pkgdesc="Automagically generate the HUnit- and Quickcheck-bulk-code using Template Haskell"
url="https://github.com/finnsson/test-generator"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-test-framework' 'haskell-language-haskell-extract' 'haskell-src-exts'
         'haskell-regex-posix')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0e8b6898eb7bf481bafb0c9ff9caa615dbf768e538e26b15ab899c5c9a1ad6bd6ed76ef93cf9a823706046d41f49fbb0cbe660bf846e59a608a0e5d21e89686b')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "BSD3.txt" "${pkgdir}/usr/share/licenses/${pkgname}/BSD3.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/BSD3.txt"
}
