# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tagstream-conduit
pkgname=haskell-tagstream-conduit
pkgver=0.5.6
pkgrel=19
pkgdesc="Streamlined html tag parser"
url="https://github.com/yihuang/tagstream-conduit"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-attoparsec" "haskell-blaze-builder" "haskell-case-insensitive"
         "haskell-conduit" "haskell-conduit-extra" "haskell-data-default" "haskell-resourcet"
         "haskell-xml-conduit")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bd389b9db3a179b4b58a0e3c0f57953a3eeb4ad216b4f29cd452ce73b76cebf8f77c4510115b5840919d63b7e32fdd0839736e7f522596679a5d7dcadfc003d0')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
