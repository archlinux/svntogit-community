# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: said <atvordhosbn@gmail.com>
# Contributor: clinx <clinx.hacker@googlemail.com>

_name=rockbox
pkgname=rbutil
pkgver=1.4.1.r381.g94eb1df58b
pkgrel=3
pkgdesc='Rockbox Utility'
arch=(x86_64)
url=https://www.rockbox.org/twiki/bin/view/Main/RockboxUtility
license=(GPL)
depends=(
  crypto++
  qt5-base
  qt5-multimedia
  libxkbcommon-x11
  zlib
)
makedepends=(
  gendesk
  git
  libusb
  qt5-tools
)
optdepends=('speex: voice files support')
# NOTE: commit with latest Qt fixes
_commit=94eb1df58b
# NOTE: using git sources, as the build fails during linking with 1.4.1
source=("git+https://git.${_name}.org/cgit/${_name}.git#commit=${_commit}")
sha256sums=('SKIP')

prepare() {
  gendesk -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" --name="Rockbox Utility" --exec="RockboxUtility" --categories="Application;Utility;" --comment="Rockbox automated installer"
}

pkgver() {
  cd "${_name}"
  git describe --long --exclude '*archos_retired*' --tags | sed 's/\([^-]*-g\)/r\1/;s/rbutil_//;s/-/./g'
}

build() {
  cd "${_name}/${pkgname}/rbutilqt"

  lrelease-qt5 rbutilqt.pro
  qmake-qt5
  make
}

package(){
  depends+=('libusb-1.0.so')
  cd "${_name}/${pkgname}/rbutilqt"

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,pixmaps}}
  install -m 755 {,"${pkgdir}"/usr/bin/}RockboxUtility
  install -m 644 icons/rockbox-256.png "${pkgdir}"/usr/share/pixmaps/rbutil.png
  install -m 644 {"${srcdir}","${pkgdir}"/usr/share/applications}/rbutil.desktop
}

# vim: ts=2 sw=2 et:
