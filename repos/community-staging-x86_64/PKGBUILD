# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=html5-parser
pkgbase='python-html5-parser'
pkgname=('python-html5-parser' 'python2-html5-parser')
pkgver=0.4.8
pkgrel=2
pkgdesc="Fast C based HTML 5 parsing for python"
arch=('x86_64')
url="https://github.com/kovidgoyal/${_pkgname}"
license=('Apache')
checkdepends=('python-beautifulsoup4' 'python2-beautifulsoup4')
makedepends=('python-chardet' 'python-lxml' 'python-setuptools'
             'python2-chardet' 'python2-lxml' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('b1ead2784c69cee5d481eb6d75587cc5ea08fc292b127ae08afe7771a6fba101'
            'SKIP')
b2sums=('83957f2995b86970063759b610620d82d41c25d787c48d48ead751c006977eec0662641488c6dfe35cfc833dd0626841f2559a1de2700978e4c9634f5b0ebab8'
        'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
    python2 setup.py test
}

package_python-html5-parser() {
    depends=('python-chardet' 'python-lxml')
    optdepends=('python-beautifulsoup4: to use the soup treebuilder')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-html5-parser() {
    depends=('python2-chardet' 'python2-lxml')
    optdepends=('python2-beautifulsoup4: to use the soup treebuilder')

    cd "${srcdir}"/${_pkgname}-${pkgver}
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
