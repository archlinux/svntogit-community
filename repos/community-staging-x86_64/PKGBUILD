# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.3.11
pkgrel=2
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('x86_64')
url="https://git.merproject.org/mer-core/fingerterm"
license=('LGPL')
depends=('qt5-feedback')
makedepends=('nemo-qml-plugin-notifications')
source=("$pkgname-$pkgver.tar.bz2::https://git.merproject.org/mer-core/fingerterm/repository/archive.tar.bz2?ref=$pkgver")
sha512sums=('d75e9b341ed2ad3ccf9e933c97893d5d37c91fa4d2fb7588097dd6e8899109b30cc5a9616672919117425b1a396b1a7f2023f2af7fbc0ef938219d7e44ecdc4b')

prepare() {
  rm fingerterm-$pkgver.tar.bz2
  mv fingerterm-* fingerterm-$pkgver
}

build() {
  cd fingerterm-$pkgver

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm-$pkgver

  make INSTALL_ROOT="${pkgdir}" install
}
