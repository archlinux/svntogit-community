# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=haskell-gi
pkgname=haskell-gi
pkgver=0.24.6
pkgrel=10
pkgdesc="Generate Haskell bindings for GObject Introspection capable libraries"
url="https://github.com/haskell-gi/haskell-gi"
license=("LGPL2")
arch=('x86_64')
depends=('ghc-libs' 'glib2' 'gobject-introspection-runtime' 'haskell-gi-base'
         'haskell-ansi-terminal' 'haskell-attoparsec' 'haskell-pretty-show' 'haskell-safe'
         'haskell-xdg-basedir' 'haskell-xml-conduit' 'haskell-regex-tdfa')
makedepends=('ghc' 'gobject-introspection' 'haskell-cabal-doctest' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('21d6abe2c32bbb5eeb114d22c05050cc1b3be5bae7fbd7af310a494040d9beea5ab22b87f5109f2116376e3d8cd391d21cf84d0c84194059d630993d9dfa9ce2')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
