# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.3.1
pkgrel=2
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('procps-ng' 'hwdata' 'quazip-qt5' 'qt5-charts' 'qt5-quickcontrols2' 'qt5-base>=5.9'
         'botan>=2.2.0' 'qt5-svg' 'qt5-xmlpatterns' 'qt5-base' 'dbus' 'polkit')
makedepends=('cmake>=3.3' 'qt5-tools' 'quazip-qt5' 'gcc>=8' 'extra-cmake-modules' 'ninja')
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha512sums=('fc603226470932f92d495da4a219b511ae11cd2e6918fb86f37cf557b140ce5ed574f5673e69d08b7348037e4de89c8db7ce2dece512f7b0ac7a3dd065639ca4')

build() {
  cd "${pkgname}-v${pkgver}"
  cmake \
	-Bbuild \
	-GNinja \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-Wno-dev
  cmake --build build --verbose
}

check () {
  cd "${pkgname}-v${pkgver}"
  ninja -C build test
}

package() {
  cd "${pkgname}-v${pkgver}"
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
