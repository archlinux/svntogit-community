# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=linear-base
pkgname=haskell-linear-base
pkgver=0.3.0
pkgrel=19
pkgdesc="Standard library for linear types"
url="https://github.com/tweag/linear-base"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-linear-generics' 'haskell-primitive'
         'haskell-storable-tuple' 'haskell-vector')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-hedgehog' 'haskell-inspection-testing'
             'haskell-mmorph' 'haskell-tasty' 'haskell-tasty-hedgehog'
             'haskell-tasty-inspection-testing')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('65e4dafd21f5e2bc709cd65526e09090f348b594814f1068dd68aba48cd0302c')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
