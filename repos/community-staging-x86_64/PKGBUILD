# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=apply-refact
pkgname=haskell-apply-refact
pkgver=0.9.1.0
pkgrel=5
pkgdesc="Perform refactorings specified by the refact library."
url="https://github.com/mpickering/apply-refact"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-filemanip' 'haskell-ghc-exactprint'
         'haskell-optparse-applicative' 'haskell-refact' 'haskell-syb' 'haskell-uniplate'
         'haskell-unix-compat')
makedepends=('ghc' 'haskell-silently' 'haskell-tasty' 'haskell-tasty-expected-failure'
             'haskell-tasty-golden')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('3ff390ada93b79a989d79bfd9bc113ec9ba458a4b8c4b3715d35a032faaba3f9ab980a209ce873760c6ba77025752a7387b755785db60863158bae2e9d8babf8')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
