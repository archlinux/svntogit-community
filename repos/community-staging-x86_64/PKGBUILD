# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=contourpy
pkgname=python-$_pyname
pkgver=1.0.7
pkgrel=3
pkgdesc='Python library for calculating contours in 2D quadrilateral grids'
arch=(x86_64)
url='https://github.com/contourpy/contourpy'
license=(BSD)
makedepends=(python-build python-installer python-setuptools pybind11 python-wheel)
depends=(python-numpy)
checkdepends=(python-pytest python-matplotlib)
optdepends=('python-matplotlib: matplotlib renderer')
source=(https://pypi.python.org/packages/source/${_pyname:0:1}/$_pyname/$_pyname-$pkgver.tar.gz)
sha256sums=('d8165a088d31798b59e91117d1f5fc3df8168d8b48c4acc10fc0df0d0bdbcc5e')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
