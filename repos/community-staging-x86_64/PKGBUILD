# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=uri-bytestring
pkgname=haskell-uri-bytestring
pkgver=0.3.2.2
pkgrel=67
pkgdesc="Haskell URI parsing as ByteStrings"
url="https://github.com/Soostone/uri-bytestring"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-blaze-builder' 'haskell-th-lift-instances')
makedepends=('ghc' 'haskell-hunit' 'haskell-hedgehog' 'haskell-tasty' 'haskell-tasty-hedgehog'
             'haskell-tasty-hunit' 'haskell-base-compat' 'haskell-safe')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('82565afbdb00b9b4a56f8eaa3ccd42732a16a79f894c950c67cede6346af5112dcb2d226f4b11bde135cf6cfacc687799d33f660d98556d031d262ffe65cf422')

prepare() {
    sed -e '/, semigroups/d' -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --disable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lib-Werror
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/Soostone/uri-bytestring/issues/50
    # runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
