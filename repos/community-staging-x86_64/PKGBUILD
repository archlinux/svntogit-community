# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.4.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=(
  'sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib' 'lz4' 'xxhash'
  'libxxhash.so' 'file' 'libuv' 'tree-sitter' 'libtree-sitter.so'
  'libzip.so' 'libmagic.so')
makedepends=('meson' 'ninja' 'python')
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('9d576bee48e5ceae46b4e309c53e01b82edf89a95046f576e1c38fefa73353d6d1f04e6d444332324387d4e115bc87659a358629f7277a41dfef44c82364547b')
b2sums=('095ca9d317296afca9514a70f208e9aa87fc426e6b7e41163f64d5a1192a4d9ccd8ece9c4f13a24fc973668dece625fa13ad75542695a66c542fe2f0d9e66733')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v4 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_libuv=true \
    -D local=disabled
  ninja -C build
}

package() {
  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
}

# vim: ts=2 sw=2 et:
