# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
epoch=1
pkgver=0.9.2
pkgrel=8
pkgdesc="A BibTeX editor for KDE"
arch=(x86_64)
url='https://userbase.kde.org/KBibTeX'
license=(GPL)
depends=(poppler-qt5 qt5-webengine qt5-xmlpatterns qoauth ktexteditor)
optdepends=('okular: Document preview')
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/KBibTeX/$pkgver/$pkgname-$pkgver.tar.xz{,.asc}
        kbibtex-qsortfilterproxymodel-crash.patch::"https://invent.kde.org/office/kbibtex/-/commit/84b0551e.patch")
sha256sums=('d98651fba99de8ebaf1a19bd8b3c3ac660a34cea33e4c587be30fd61466fac27'
            'SKIP'
            'b4390a9361dc3f734f158d82afb586ddcee92ac5412eb69f4d0734c364c6471b')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

prepare() {
  patch -d $pkgname-$pkgver -p1 < kbibtex-qsortfilterproxymodel-crash.patch # FS#70366
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
