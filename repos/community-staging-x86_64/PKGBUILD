# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_module=broadcom-wl
_kernelname=              # Build against stock -ARCH kernel
#_kernelname=-custom      # Build against kernel with a different name
pkgname=${_module}${_kernelname}
pkgver=6.30.223.271
pkgrel=118
pkgdesc='Broadcom 802.11 Linux STA wireless driver'
arch=('x86_64')
url='https://www.broadcom.com/support/download-search/?pf=Wireless+LAN+Infrastructure'
license=('custom')
depends=("linux${_kernelname}")
makedepends=("linux${_kernelname}-headers" "${_module}-dkms=${pkgver}")

_extramodules="/usr/lib/modules/extramodules${_kernelname:--ARCH}"

build() {
    _kernver="$(<${_extramodules}/version)"

    dkms build --dkmstree "${srcdir}" -m ${_module}/${pkgver} -k ${_kernver}
}

package() {
    _kernver="$(<${_extramodules}/version)"

    install -Dm644 -t "${pkgdir}${_extramodules}" \
        ${_module}/${pkgver}/${_kernver}/${CARCH}/module/*

    _license="/usr/share/licenses/${_module}-dkms"
    if [[ -d ${_license} ]]; then
        install -Dm644 -t "${pkgdir}"/${_license/-dkms/${_kernelname}} ${_license}/*
    fi
    _modprobe="/usr/lib/modprobe.d/${_module}-dkms.conf"
    if [[ -r ${_modprobe} ]]; then
        install -Dm644 ${_modprobe} "${pkgdir}"/${_modprobe/-dkms/${_kernelname}}
    fi
}
