# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Verelst <david dot verelst at gmail com>
# Contributor: richli

pkgname=python-shapely
pkgver=1.7.1
pkgrel=5
pkgdesc="Manipulation and analysis of geometric objects in the Cartesian plane"
arch=('x86_64')
url="https://pypi.python.org/pypi/Shapely"
license=('BSD')
depends=('geos' 'python')
optdepends=('python-numpy: for shapely.vectorized submodule')
makedepends=('python-setuptools' 'python-packaging' 'python-numpy' 'cython')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/S/Shapely/Shapely-${pkgver}.tar.gz"
        geos-3.9.patch)
sha512sums=('f5991a35692241ff48f7219edd88bdab7da5b9135c22d26ca530cbdb9160717fd88f2913a9da8ae53869c68955418c14ffd5d798df196639de63660226bd6412'
            '9cc2e1a8b1272451ccfde2af287cb4dc31811b67c02c3d94f235c039f99173dcd8e128a0a7c57a8c098b45290e4acc9d957914c6719c1c7076bb62ff2aac837d')

prepare() {
  cd Shapely-$pkgver
  patch -Np1 -i ../geos-3.9.patch
  rm -r _vendor
  sed -i 's/from _vendor.packaging./from packaging./' setup.py
}

build() {
  cd Shapely-$pkgver
  python setup.py build
}

check() {
  # TODO: make the hack less dirty...

  cd Shapely-$pkgver
  mv shapely shapely.bak
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10:$PYTHONPATH" py.test
  mv shapely.bak shapely
}

package() {
  cd Shapely-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
