# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=passenger
pkgver=4.0.53
pkgrel=2
pkgdesc="mod_rails passenger"
arch=('i686' 'x86_64')
url="http://www.modrails.com"
_watch="https://github.com/phusion/passenger/releases"
license=('GPL')
depends=('ruby' 'ruby-rack' 'curl')
makedepends=('apache')
install=passenger.install
options=('!emptydirs' 'staticlibs')
source=($pkgname-$pkgver.tar.gz::https://github.com/FooBarWidget/passenger/archive/release-$pkgver.tar.gz)
md5sums=('385dceac05c10e59a3dab44d188353aa')

prepare() {
  cd $srcdir/passenger-release-$pkgver
  sed -i '1,1i#include <stdint.h>' \
    ext/common/Utils/MD5.cpp \
    ext/common/Logging.cpp \
    ext/common/Utils/StrIntUtils.cpp \
    ext/common/Utils.cpp \
    ext/common/ApplicationPool2/AppTypes.cpp \
    ext/boost/libs/thread/src/pthread/once.cpp \
    ext/boost/libs/thread/src/pthread/once_atomic.cpp \
    ext/boost/libs/thread/src/pthread/thread.cpp \
    ext/boost/atomic/atomic.hpp
}

build(){
  cd $srcdir/passenger-release-$pkgver
  ./bin/passenger-install-apache2-module -a
  rake nginx
}

package() {
  cd $srcdir/passenger-release-$pkgver

  mkdir -p $pkgdir/usr/lib/passenger/
  cp -R * $pkgdir/usr/lib/passenger/

  mkdir -p $pkgdir/usr/lib/httpd/modules
  ln -sf /usr/lib/passenger/buildout/apache2/mod_passenger.so $pkgdir/usr/lib/httpd/modules/mod_passenger.so
}
