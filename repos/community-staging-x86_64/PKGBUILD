# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cryptonite-conduit
pkgname=haskell-cryptonite-conduit
pkgver=0.2.2
pkgrel=235
pkgdesc="Conduit bridge for cryptonite"
url="https://github.com/haskell-crypto/cryptonite-conduit"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-cryptonite'
         'haskell-exceptions' 'haskell-memory' 'haskell-resourcet')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('5a313d1ff64992fc8f45ebf06f5f439aa5dfe745ce6f2bfc31b3ee06b489b0a145a9e6b5cf00fb557ddbebf1aa5c226dc0add9c591f62a06bd6f8776cf52dbdf')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
