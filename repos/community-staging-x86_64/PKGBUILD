# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=critbit
pkgname=haskell-critbit
pkgver=0.2.0.0
pkgrel=96
pkgdesc="Crit-bit maps and sets"
url="https://github.com/bos/critbit"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-vector")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        https://github.com/bos/critbit/commit/0111bc3c0848d411c169696b6d2176dea40e9417.patch)
sha512sums=('b5d9acdfa94938d7ce9bae21d21e2865e880fe9e61098ce873878627b6618cfdb6b26083ccde3d1be85e00097c68307b8a04f4900d752e6cf645608ae1f38f95'
            '6210839b52aa9c592678e7691c47234e31e0fe63d0a5c2980c898da25b796ca81f325d97c2d4b4b99581ff46ecd778151de8eabc6d73f867becd763410fbd267')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../0111bc3c0848d411c169696b6d2176dea40e9417.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-developer
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
