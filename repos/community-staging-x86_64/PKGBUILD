# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

_hkgname=pango
pkgname=haskell-pango
pkgver=0.13.6.0
pkgrel=23
pkgdesc="Binding to the pango library for Gtk2Hs."
url="https://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('x86_64')
depends=('ghc-libs' 'pango' 'haskell-glib' 'haskell-cairo')
makedepends=('ghc' "haskell-gtk2hs-buildtools" "mesa")
provides=('gtk2hs-pango')
replaces=('gtk2hs-pango')
conflicts=('gtk2hs-pango')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('03a3e133499d202e6d6b1471db287ef1f0f8933b6112e367ce20450d171e8713ffe0caea46ba60fc7ead6c47548cd8bcaf1fe39bb2423099f1c2980484864a8d')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
}
