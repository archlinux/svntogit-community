# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=suil
pkgver=0.10.0
pkgrel=3
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('x86_64')
url="https://drobilla.net/software/suil/"
license=('custom:ISC')
makedepends=('gtk2' 'gtk3' 'lv2' 'python2' 'qt4' 'qt5-base')
optdepends=('qt4: Qt4 UI wrapping support'
            'qt5-base: Qt5 UI wrapping support'
            'gtk2: GTK+ 2.x UI wrapping support'
            'gtk3: GTK+ 3.x UI wrapping support')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('721b73d987c1712e82493386e5d71957002c8a713595fce4372f1a5eb306c64a559db1ca794cec96295f72e22a9c0e9193f9a68b7f5846f2db45ce16b253fbea'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove local call to ldconfig
  sed -i "/ldconfig/d" wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python2 waf configure --prefix=/usr
  python2 waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "$pkgdir/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,NEWS,PACKAGING,README}
}

# vim:set ts=2 sw=2 et:
