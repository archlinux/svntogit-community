# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=bitwise
pkgname=haskell-bitwise
pkgver=1.0.0.1
pkgrel=168
pkgdesc="Fast multi-dimensional unboxed bit packed Bool arrays"
url="https://code.mathr.co.uk/bitwise"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('b8653549ef0d0f4eb608802f6124cd969c16cbeb6f26dd83839bbaab7d56cf0c1b39a7ac3dba40f3fa00f3f67f38bad091ee0f01e7f6aea7f9c0bafa34efb443')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *4/<5/;s/< *2/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
