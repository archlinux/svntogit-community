# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=git-repair
pkgver=1.20170626
pkgrel=85
pkgdesc="Repairs a damaged git repository"
url="https://git-repair.branchable.com/"
license=("GPL")
arch=('x86_64')
depends=('git' 'ghc-libs' 'haskell-async' 'haskell-data-default' 'haskell-exceptions'
         'haskell-hslogger' 'haskell-ifelse' 'haskell-network' 'haskell-network-uri'
         'haskell-optparse-applicative' 'haskell-quickcheck' 'haskell-split'
         'haskell-unix-compat' 'haskell-utf8-string')
makedepends=('ghc')
source=("$pkgname::git://git-repair.branchable.com/#tag=$pkgver"
        ghc-8.4.patch)
sha512sums=('SKIP'
            '7aefb60b5857252bb5b9212865ecc6da0727ec24422fa75fd7442108483e63560bb71d5782c580735ac640d9ec1131cda68882d097ebbd5e2681a9fab4ccebd2')

prepare() {
  cd git-repair
  patch -p1 -i ../ghc-8.4.patch
}

build() {
  cd git-repair
  
  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/$pkgname" \
      -fnetwork-uri --ghc-option='-pie'
  runhaskell Setup build
}

package() {
  cd git-repair
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 git-repair.1 "$pkgdir"/usr/share/man/man1/git-repair.1

  rm "$pkgdir"/usr/share/doc/git-repair/GPL
  rmdir "$pkgdir"/usr/share/doc/git-repair "$pkgdir"/usr/share/doc
}
