# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall
pkgver=1.33.1
pkgrel=20
pkgdesc='A configuration language guaranteed to terminate'
url='https://dhall-lang.org'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-ansi-terminal'
         'haskell-atomic-write' 'haskell-case-insensitive' 'haskell-cborg' 'haskell-cborg-json'
         'haskell-contravariant' 'haskell-data-fix' 'haskell-diff' 'haskell-dotgen'
         'haskell-either' 'haskell-half' 'haskell-hashable' 'haskell-lens-family-core'
         'haskell-megaparsec' 'haskell-memory' 'haskell-network-uri' 'haskell-optparse-applicative'
         'haskell-parsers' 'haskell-parser-combinators' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal' 'haskell-pretty-simple' 'haskell-profunctors'
         'haskell-repline' 'haskell-serialise' 'haskell-scientific' 'haskell-text-manipulate'
         'haskell-th-lift-instances' 'haskell-transformers-compat' 'haskell-unordered-containers'
         'haskell-uri-encode' 'haskell-vector' 'haskell-cryptonite' 'haskell-http-types'
         'haskell-http-client' 'haskell-http-client-tls')
makedepends=('ghc' 'haskell-foldl' 'haskell-generic-random' 'haskell-mockery' 'haskell-quickcheck'
             'haskell-quickcheck-instances' 'haskell-special-values' 'haskell-spoon'
             'haskell-tasty' 'haskell-tasty-expected-failure' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-turtle' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c306b059c9ace17d09515e2ed7bbd137854e1a76bce632c8e34dddcb3af5819bf0415288a96bbefc61ec0651b63a43e9bdc8ea1c402d7aad9f535107697ad30c')

prepare() {
    cd $pkgname-$pkgver
    sed -i '/semigroups/d' $pkgname.cabal
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
