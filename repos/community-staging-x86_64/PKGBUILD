# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=9.2.0
pkgrel=2
pkgdesc="A Library for doing Number Theory"
arch=(i686 x86_64)
url="http://www.shoup.net/ntl/"
license=(GPL)
depends=(gf2x gmp)
makedepends=()
options=(!libtool)
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz" 'ntl-sage.patch')
md5sums=('bf419b48ea567858e47e0aac3c849fbe'
         '4cb5f97080281109bf358959ab993691')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i $srcdir/ntl-sage.patch
}

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NTL_GMP_LIP=on NTL_DISABLE_LONGLONG=on
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}

