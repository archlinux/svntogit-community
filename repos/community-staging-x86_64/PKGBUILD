# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=microlens-th
pkgname=haskell-microlens-th
pkgver=0.4.1.3
pkgrel=1
pkgdesc="Automatic generation of record lenses for microlens"
url="https://github.com/aelve/microlens"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-microlens')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('96ac7ee4e9b09d6eb67a033332eff405b6a52889f988cf4216efdb18abfdcf11d52cdcd3ccc45ae9c7c65575f4b9a2fe4015f9b9b95ce98efa7503999ffe5bb8')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -finlining
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
