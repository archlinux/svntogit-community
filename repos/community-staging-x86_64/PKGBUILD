# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rapidjson
pkgver=0.6.3
pkgrel=1
pkgdesc="Python wrapper around rapidjson"
arch=('x86_64')
url="https://pypi.python.org/pypi/python-rapidjson"
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'rapidjson')
checkdepends=('python-pytest-runner' 'python-pytest-benchmark' 'python-pytz')
source=("https://pypi.io/packages/source/p/python-rapidjson/python-rapidjson-$pkgver.tar.gz")
sha512sums=('958871dc005ec56111d2b9deedfceac5e2d863f3baee4551c1079c733d75949f975ab312b77bd78ef2eec83d708fb056b07192d0f8a8b3e68b57fecada58be86')

prepare() {
  #rm -r python-rapidjson-$pkgver/rapidjson
  #sed -i 's/if not os.path.isdir/if os.path.isdir/' python-rapidjson-$pkgver/setup.py
  # Shared rapidjson fails tests.

  cp -a python-rapidjson-$pkgver{,-py2}
}

build() {
  cd python-rapidjson-$pkgver
  python setup.py build # --rj-include-dir=/usr/include
}

check() {
  cd python-rapidjson-$pkgver
  python setup.py pytest --addopts '--ignore benchmarks/'
}

package() {
  cd python-rapidjson-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
