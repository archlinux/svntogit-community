# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=haskell-aeson
_hkgname=aeson
pkgver=1.5.6.0
pkgrel=54
pkgdesc="A JSON parsing and encoding library optimized for ease of use and high performance."
url="https://github.com/haskell/aeson"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-compat-batteries' 'haskell-data-fix'
         'haskell-dlist' 'haskell-hashable' 'haskell-primitive' 'haskell-scientific'
         'haskell-strict' 'haskell-tagged' 'haskell-th-abstraction' 'haskell-these'
         'haskell-time-compat' 'haskell-unordered-containers' 'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-integer-logarithms'
             'haskell-base-orphans' 'haskell-base16-bytestring' 'haskell-diff'
             'haskell-generic-deriving' 'haskell-hashable-time' 'haskell-tasty'
             'haskell-tasty-golden' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-quickcheck-instances')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz"
        ghc9.patch)
sha512sums=('31e0339945aec6d84aec230c09001fe6e7926f008d3444d12264bb323d0a050515303fdc8eeb6563733594209d930cdd2f82eb4dfe4dd4d5c573748aaa71813d'
            'b6f9d49f1427543a907a2b432889f2fef64c50dbaae36133dbb328d6ff7f314959d35ef154f085a42f72af384eaa107b6f7f2b67a2d318f972e2cce04b65d35e')

prepare() {
  uusi -u attoparsec -u hashable-time $_hkgname-$pkgver/$_hkgname.cabal
  patch -d $_hkgname-$pkgver -p1 < ghc9.patch
}

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-developer -f-fast -f-cffi
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
