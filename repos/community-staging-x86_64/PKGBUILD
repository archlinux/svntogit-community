# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shelly
pkgname=haskell-shelly
pkgver=1.8.1
pkgrel=92
pkgdesc="Shell-like (systems) programming in Haskell"
url="https://github.com/yesodweb/Shelly.hs"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-unix-compat' 'haskell-system-filepath'
         'haskell-system-fileio' 'haskell-monad-control' 'haskell-lifted-base'
         'haskell-lifted-async' 'haskell-exceptions' 'haskell-enclosed-exceptions'
         'haskell-async' 'haskell-transformers-base')
makedepends=('ghc' 'haskell-hunit' 'haskell-hspec' 'haskell-hspec-contrib')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        shelly-ghc-8.8.patch::https://github.com/gregwebs/Shelly.hs/commit/5093c266200ce4ee61075af310b12f5c9cc7faa6.patch)
sha512sums=('75a6939ed0c395950738d569d2452b55acc373ead0361e97651218ec464816fffa1af5808b7af4d2ea7cd4339c028a4a1bee02f0bfd88fecb73a6888c083d651'
            '973a51bc176bb5fd36aad8e34934b84be999b1e74865a9d48277508e618ce698394affaf904edee4f4ac4f21a16b45dd05a22fce5939cca631be97858d44ff5b')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../shelly-ghc-8.8.patch
    sed -i 's/< *1.9/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-lifted -f-build-examples
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
