# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=fpylll
pkgname=(python-fpylll python2-fpylll)
pkgver=0.2.3dev
pkgrel=2
pkgdesc="A Python interface for fplll"
arch=(i686 x86_64)
url="https://github.com/fplll/fpylll"
license=(GPL2)
makedepends=(fplll python-numpy python-cysignals cython python2-numpy python2-cysignals cython2)
source=("https://github.com/fplll/fpylll/releases/download/$pkgver/$pkgbase-$pkgver.tar.gz")
md5sums=('bebeead2e4a6f2fdb82410f04f0d1f4f')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}

# workaround encoding error
  sed -e '/long_description/d' -i $pkgbase-$pkgver/setup.py
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-fpylll() {
  depends=(fplll python-numpy python-cysignals)

  cd $pkgbase-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}

package_python2-fpylll() {
  depends=(fplll python2-numpy python2-cysignals)
  optdepends=('sagemath: use Sage integers implementation')  
  conflicts=(fpylll)
  provides=(fpylll)
  replaces=(fpylll)

  cd $pkgbase-$pkgver-py2 
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}
