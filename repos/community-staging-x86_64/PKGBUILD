# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=statistics
pkgname=haskell-statistics
pkgver=0.15.0.0
pkgrel=30
pkgdesc="A library of statistical types, data, and functions"
url="https://github.com/bos/statistics"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-orphans' 'haskell-data-default-class'
         'haskell-dense-linear-algebra' 'haskell-math-functions' 'haskell-monad-par'
         'haskell-mwc-random' 'haskell-primitive' 'haskell-vector' 'haskell-vector-algorithms'
         'haskell-vector-binary-instances' 'haskell-vector-th-unbox')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-erf' 'haskell-ieee754'
             'haskell-test-framework' 'haskell-test-framework-hunit'
             'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('89b44ab8662308dc17769c47c9791b1b673fd37363483333ef0e005b0e00ec120145ee8a1174d2e5a92fdb6e00fd441e389407cdcbd99c55b6523cc79ba82949')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
