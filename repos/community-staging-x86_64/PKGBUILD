# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Maxim Baz <$pkgbase at maximbaz dot com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: fauno <fauno at kiwwwi.com.ar>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
# Contributor: Richard Murri <admin@richardmurri.com>

pkgbase=notmuch
pkgname=('notmuch' 'notmuch-vim' 'notmuch-mutt' 'notmuch-runtime')
pkgver=0.37
pkgrel=3
arch=('x86_64')
url="https://notmuchmail.org/"
license=('GPL3')
makedepends=('python' 'python-setuptools' 'python-sphinx' 'python-cffi' 'emacs' 'gnupg' 'ruby' 'pkgconfig' 'xapian-core' 'gmime3' 'talloc' 'sfsexp')
options=(!distcc !makeflags)
source=("https://notmuchmail.org/releases/${pkgname}-${pkgver}.tar.xz")
sha512sums=('31cac1ff03b139b18a4fec5e29907ba0e3a965f0a7bf5f2f2b0f75980f9663173745be854e83943f344a09cdb7f5615fa48218e327c4ade7a55b3a22b2a9c45e')

build() {
    cd "$srcdir/$pkgbase-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc --includedir=/usr/include --zshcompletiondir=/usr/share/zsh/site-functions
    make

    make -C "contrib/${pkgbase}-mutt"

    make ruby-bindings

    cd "$srcdir/$pkgbase-$pkgver/bindings/python"
    python setup.py build

    cd "$srcdir/$pkgbase-$pkgver/bindings/python-cffi"
    python setup.py build
}

package_notmuch-runtime(){
    pkgdesc="Runtime for notmuch and notmuch-mutt"
    depends=('xapian-core' 'gmime3' 'talloc' 'sfsexp')

    cd "$srcdir/$pkgbase-$pkgver"

    make DESTDIR="$pkgdir/" LIBDIR_IN_LDCONFIG=0 WITH_EMACS=0 install
    make DESTDIR="$pkgdir/" WITH_BASH=1 install-completion

    # Install manpages
    make DESTDIR="$pkgdir" install-man

    # this is provided both with and without a statically included libnotmuch.so ???
    install -Dm755 notmuch-shared "$pkgdir/usr/bin/notmuch"
}


package_notmuch-vim(){
    pkgdesc="Vim plugins for notmuch"
    depends=('notmuch-runtime' 'ruby')
    optdepends=('ruby-mail: for sending mail'
                'elinks: for previewing html mail')

    make -C "$srcdir/$pkgbase-$pkgver/vim" DESTDIR="$pkgdir" prefix="/usr/share/vim/vimfiles" install
}


package_notmuch(){
    pkgdesc="Notmuch is not much of an email program"
    depends=('notmuch-runtime')
    optdepends=('emacs: for using the emacs interface'
                'vim: for using the vim interface'
                'python-cffi: for using the python-cffi bindings'
                'ruby: for using the ruby bindings'
                'gnupg: for email encryption')
    install=notmuch.install

    cd "$srcdir/$pkgbase-$pkgver"

    # Install emacs parts
    make DESTDIR="$pkgdir" install-emacs


    # Install ruby bindings
    sed -i 's:INSTALL = .*[^D]$:& -D:' bindings/ruby/Makefile
    make -C bindings/ruby exec_prefix="$pkgdir"/usr install

    # Install python bindings
    cd "$srcdir/$pkgbase-$pkgver/bindings/python"
    python setup.py install --root="$pkgdir" --optimize=1

    cd "$srcdir/$pkgbase-$pkgver/bindings/python-cffi"
    python setup.py install --root="$pkgdir" --optimize=1

    # Remove mimeinfo.cache (fixes FS#53629).
    rm "$pkgdir/usr/share/applications/mimeinfo.cache"
}

package_notmuch-mutt(){
    pkgdesc="The mail indexer"
    depends=('notmuch-runtime' 'perl-mailtools' 'perl-mail-box' 'perl-term-readline-gnu' 'perl-string-shellquote' 'perl-file-which')

    cd "$srcdir/$pkgbase-$pkgver"

    install -Dm755 "contrib/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"
    install -Dm644 "contrib/$pkgname/${pkgname}.rc" "${pkgdir}/etc/Muttrc.d/${pkgname}.rc"
    install -Dm644 "contrib/$pkgname/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
