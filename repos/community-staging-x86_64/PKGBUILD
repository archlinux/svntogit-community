# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <greg.sutcliffe@gmail.com>>

pkgname=ruby-shadow
pkgver=2.5.0
pkgrel=3
pkgdesc="Shadow password module"
arch=('x86_64')
url="https://github.com/apalmblad/ruby-shadow"
license=('CPL')
depends=('ruby')
source=($pkgname-$pkgver.tar.gz::https://github.com/apalmblad/$pkgname/archive/$pkgver.tar.gz)
md5sums=('e9c35620f6c25233b7b54dcab8ee955e')

build() {
  cd $pkgname-$pkgver

  ruby extconf.rb
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir"/ sitedir='$(rubylibprefix)/vendor_ruby' \
    sitehdrdir='$(rubyhdrdir)/vendor_ruby' install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
