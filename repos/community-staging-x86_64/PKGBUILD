# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.20
pkgrel=3
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse2' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('6a0e0abbe07329daf810fddf2557892ec69659a381c445f7a3ada4a69e422c371d587623a9cb5f079f0738d00fca579af56f37036a2ba8060891d167fa9bb947')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
