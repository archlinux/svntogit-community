# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=znc
pkgver=1.6.5
pkgrel=7
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://wiki.znc.in/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libsasl' 'icu')
makedepends=('tcl' 'python' 'perl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')
install=$pkgname.install
source=("http://znc.in/releases/$pkgname-$pkgver.tar.gz"{,.sig}
        "$pkgname.tmpfiles"
        "$pkgname.sysusers")
validpgpkeys=('D5823CACB477191CAC0075555AE420CC0209989E') #Alexey Sokolov <alexey@asokolov.org>
md5sums=('ab22e4e94cdd04c5644c4d9213149af0'
         'SKIP'
         '6404881ac7fd1db331029071be319d0d'
         '919705dccc42d3bff1549d3b3680e55a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-cyrus \
    --enable-python \
    --enable-perl \
    --enable-tcl \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
