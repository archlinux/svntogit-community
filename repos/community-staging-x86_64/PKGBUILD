# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=gobby
pkgver=0.5.0
pkgrel=3
pkgdesc="A collaborative text editor"
arch=('i686' 'x86_64')
url="http://gobby.github.io/"
license=('GPL')
depends=('obby' 'libxml++2' 'gtkmm' 'gnome-vfs' 'libbonobo' 'libgtksourceviewmm2'
	 'gtksourceview2' 'libxml++' 'libunique' 'libinfinity' 'gsasl')
makedepends=('gettext' 'intltool' 'gnome-doc-utils')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gobby/gobby/archive/v$pkgver.tar.gz")
md5sums=('fcc25c3b5e3150f4c6cffa3ad6cbac9d')

build() {
  cd $srcdir/gobby-$pkgver
  [ -x configure ] || ./autogen.sh
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr --with-gnome
  make
}

package() {
  cd $srcdir/gobby-$pkgver
  make DESTDIR=$pkgdir install
}
