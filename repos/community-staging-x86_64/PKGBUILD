# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=0.12.0
pkgrel=3
pkgdesc='A lightweight email client for the GNOME desktop'
arch=('x86_64')
url='http://www.yorba.org/projects/geary/'
license=('GPL3')
depends=('cairo' 'enchant' 'gcr' 'gdk-pixbuf2' 'glib2' 'glibc' 'gmime'
         'gnome-keyring' 'gtk3' 'iso-codes' 'libcanberra' 'libgee' 'libnotify'
         'libsecret' 'libsoup' 'libxml2' 'pango' 'sqlite' 'webkit2gtk')
makedepends=('cmake' 'gnome-doc-utils' 'gobject-introspection' 'intltool'
             'vala')
source=("https://download.gnome.org/sources/geary/${pkgver%.*}/geary-${pkgver}.tar.xz"
        'geary-enchant2.patch')
sha256sums=('cef1093033ddfb52942b45c901562539cee7a192e981c7f19e2075ecb0c22446'
            'd90243e5ede36399562b5c2e53350e8f0f1a6a56f8a062910a4d4e83b006acb7')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  cd geary-${pkgver}

  patch -Np1 -i ../geary-enchant2.patch
}

build() {
  cd build

  cmake ../geary-${pkgver} \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DDESKTOP_UPDATE='FALSE' \
    -DICON_UPDATE='FALSE' \
    -DGSETTINGS_COMPILE='FALSE' \
    -DGSETTINGS_COMPILE_IN_PLACE='FALSE'
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
