# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=dbus-hslogger
pkgname=haskell-dbus-hslogger
pkgver=0.1.0.1
pkgrel=401
pkgdesc="Expose a dbus server to control hslogger"
url="https://github.com/IvanMalison/dbus-hslogger"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-dbus' 'haskell-hslogger' 'haskell-optparse-applicative')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('34d3d2f9f99f99671bb3cf11eefa0f347918b4cfb69d2188be985e5abf2867d1b33b6473bb8e311f67426778f2611e008a78e99bd2d2e1f5a2c25b7ab5568d1f')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
