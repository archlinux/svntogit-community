# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ChasingBottoms
pkgname=haskell-chasingbottoms
pkgver=1.3.1.3
pkgrel=2
pkgdesc="For testing partial and infinite values"
url="https://hackage.haskell.org/package/ChasingBottoms"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-mtl' 'haskell-quickcheck' 'haskell-random' 'haskell-syb')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('6c47f3e086a68f43826ba0bf01144391e8173af4eb3e4fd16773db2e2d561823d842bc21ca19a2f50a6fe6d2a008af35500192e50d9b31cf8e575ed92408feb2')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENCE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENCE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENCE"
}
