# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-web
pkgver=1.4
pkgrel=37
pkgdesc="Web interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-base-compat' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-clientsession' 'haskell-cmdargs' 'haskell-data-default'
         'haskell-hjsmin' 'haskell-http-conduit' 'haskell-http-client' 'haskell-hunit'
         'haskell-conduit-extra' 'haskell-safe' 'haskell-shakespeare' 'haskell-text' 'haskell-wai'
         'haskell-wai-extra' 'haskell-wai-handler-launch' 'haskell-warp' 'haskell-yesod'
         'haskell-yesod-core' 'haskell-yesod-form' 'haskell-yesod-static' 'haskell-json'
         'haskell-megaparsec' 'haskell-mtl' 'haskell-parsec')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2c890a2f45b0ab5ffb781887bdd4e9f48465bb63e70118e59ec64f3ea5077df67462f52c0a6f292a970d893fd03a870b1d0a1093d32c1d3ed4a56a8a59d8c57f')

prepare() {
    sed -e 's/, megaparsec >=5.0 && < 6.2/, megaparsec >=5/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev -f-library-only -fthreaded
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
