# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pendulum
pkgname=python-pendulum
pkgver=2.1.2
pkgrel=8
pkgdesc="Python datetimes made easy"
arch=(x86_64)
url="https://pendulum.eustace.io/"
license=(MIT)
depends=(python python-dateutil python-pytzdata)
makedepends=(python-build python-installer python-poetry-core python-wheel)
checkdepends=(python-babel python-freezegun python-pytest python-pytz)
source=($pkgname-$pkgver.tar.gz::https://github.com/sdispater/$_name/archive/$pkgver.tar.gz)
sha512sums=('c367320ade0d10b7eb9c880286176161b925d7df052f7de85ddd169594bbe778776ea74df451f00186df98be3e32b7399c68ca4b40123e2c456206410e3a1ab2')
b2sums=('c95cd131fc95a0d19ef11f180cc42126cb8a6ea3aaf4b3bbedbc5ce4c1b37a6624f2a1be92f1a6fd6b1f14f5d20fa5f32e8230f896ab7a216f02f00e85b98738')

build() {
  cd $_name-$pkgver
  # NOTE: calling pyproject-build as we otherwise call the project's build.py script:
  # https://github.com/sdispater/pendulum/issues/665
  pyproject-build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {CHANGELOG.md,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
