# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: <clu>
# Contributor: Jose San Juan <sanjuan@tng.iac.es>

pkgname=ccfits
pkgver=2.5
pkgrel=3
pkgdesc="Object Oriented Interface to the CFITSIO Library"
arch=('x86_64')
url="http://heasarc.gsfc.nasa.gov/docs/software/fitsio/ccfits/"
license=('custom')
depends=('cfitsio')
source=(http://heasarc.gsfc.nasa.gov/docs/software/fitsio/${pkgname}/CCfits-${pkgver}.tar.gz)
sha256sums=('938ecd25239e65f519b8d2b50702416edc723de5f0a5387cceea8c4004a44740')

build() {
  cd "${srcdir}"/CCfits

  ./configure --with-cfitsio-libdir=/usr/lib --prefix=/usr
  make 
}

package() {
  cd "${srcdir}"/CCfits

  make DESTDIR="${pkgdir}" install

#install license
  install -Dm644 License.txt "${pkgdir}/usr/share/licenses/$pkgname/License.txt"
}
