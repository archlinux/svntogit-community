# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cgrep
pkgver=6.6.32
pkgrel=334
pkgdesc="A context-aware grep for source codes"
url="https://awgn.github.io/cgrep/"
license=("GPL2")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-async' 'haskell-cmdargs'
         'haskell-dlist' 'haskell-either' 'haskell-extra' 'haskell-regex-base'
         'haskell-regex-pcre' 'haskell-regex-posix' 'haskell-safe' 'haskell-split'
         'haskell-stringsearch' 'haskell-unix-compat' 'haskell-unicode-show'
         'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-yaml')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('401ca49fbd2903b810c1873a3c52c3863a42b431d1a905a781636386c3ac0aac4e2ed9934fb24f1e9996629a2b578483a7c3a7a8a4d61ecb7ac7cfc9152f43ba')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --prefix=/usr --ghc-option='-pie' \
                               --enable-executable-dynamic --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname
    runhaskell Setup build $MAKEFLAGS
}

package() {
    cd $pkgname-$pkgver
    runhaskell Setup copy --destdir="$pkgdir"
}
