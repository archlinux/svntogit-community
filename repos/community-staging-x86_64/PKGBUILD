# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-api
pkgver=1.4
pkgrel=35
pkgdesc="Web API server for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-aeson' 'haskell-decimal'
         'haskell-docopt' 'haskell-either' 'haskell-microlens' 'haskell-microlens-platform'
         'haskell-safe' 'haskell-servant-server' 'haskell-servant-swagger' 'haskell-swagger2'
         'haskell-text' 'haskell-wai' 'haskell-wai-extra' 'haskell-warp')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e71231c8d4602affdfcdd229fe488331c4f827c9274d17e0e711eea853e4dbc51bb47652081ae7d822e836b8932970fc01169649f1652716c00036be45a8e547')

prepare() {
    sed -i 's/, swagger2 >= 2.0 && < 2.2/, swagger2 >= 2.0/' $pkgname-$pkgver/$pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
