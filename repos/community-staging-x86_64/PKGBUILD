# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=DAV
pkgname=haskell-dav
pkgver=1.3.3
pkgrel=106
pkgdesc="RFC 4918 WebDAV support"
url="http://floss.scru.org/hDAV"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' "haskell-case-insensitive" "haskell-data-default" "haskell-exceptions"
         "haskell-haskeline" "haskell-http-client" "haskell-http-client-tls" "haskell-http-types"
         "haskell-lens" "haskell-network" "haskell-network-uri"
         "haskell-optparse-applicative" "haskell-transformers-base" "haskell-transformers-compat"
         "haskell-utf8-string" "haskell-xml-conduit" "haskell-xml-hamlet")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bbc9a4d685e25ac763c05dd8dc72d7db39196dbbb0cd898b66f644abc8333cd82950439fd446770c5aae737fd863afe881129ce551dadf9d0f75ef52df738fae')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri -f-mtl-compat
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
