# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=incremental-parser
pkgname=haskell-incremental-parser
pkgver=0.2.5.3
pkgrel=19
pkgdesc="Generic parser library capable of providing partial results from partial input."
url="https://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-monoid-subclasses')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-checkers' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('88dfd5a64419937005e40e9635a30b6d48bc076a082b88246385bdb2730ea09c10f2be3903e3aeb663b37c086ee75250a6f982ac89c3c0070aa5d1c0e48733f8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.txt"
}
