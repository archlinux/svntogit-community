# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cgrep
pkgver=6.6.15
pkgrel=3
pkgdesc="A context-aware grep for source codes"
url="http://awgn.github.io/cgrep/"
license=("GPL2")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi' 'pcre')
makedepends=("ghc=8.0.1" "haskell-ansi-terminal" "haskell-async" "haskell-cmdargs"
             "haskell-dlist" "haskell-either" "haskell-mtl" "haskell-regex-base"
             "haskell-regex-pcre" "haskell-regex-posix" "haskell-safe" "haskell-split"
             "haskell-stm" "haskell-stringsearch" "haskell-unix-compat"
             "haskell-unordered-containers")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f58a557fecdc7066ec60653e7c902b8baedcf4f44a81f890265374b6dab9affa')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
}
