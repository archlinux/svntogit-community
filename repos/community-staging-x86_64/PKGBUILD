# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-conduit
pkgname=haskell-http-conduit
pkgver=2.3.7.3
pkgrel=160
pkgdesc="HTTP client package with conduit interface and HTTPS support"
url="https://www.yesodweb.com/book/http-conduit"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types' 'haskell-resourcet'
         'haskell-unliftio-core')
makedepends=('ghc' 'haskell-blaze-builder' 'haskell-case-insensitive' 'haskell-connection'
             'haskell-cookie' 'haskell-data-default-class' 'haskell-hunit' 'haskell-hspec'
             'haskell-lifted-base' 'haskell-network' 'haskell-streaming-commons' 'haskell-temporary'
             'haskell-unliftio' 'haskell-utf8-string' 'haskell-wai' 'haskell-wai-conduit'
             'haskell-warp' 'haskell-warp-tls')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        $pkgname-http-client-0.7.patch::https://github.com/snoyberg/http-client/commit/a09b8ac914f57f9782cc4230cb0528fc63ac54ab.patch)
sha512sums=('0b3733ce031299ee3b93ca44d45a913e384dbfcd43efe5c65076efb5ff8266058ecb07e90f699ebb54d2d37f4a299be79717c3988e72cce631d417ba21ed4d08'
            '292d33d8dd7591500eba2c1e2ee4a5733a10977c7d715fe3b3c1875fb1a2d482a604cbc4c70a35da1a82bd4acc57c4f33cf51c0ea1038f3cb1c75ecc7989696f')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 -i ../$pkgname-http-client-0.7.patch
    sed -i 's/< *0.7/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
