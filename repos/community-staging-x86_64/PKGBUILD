# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.18.4
pkgrel=6
pkgdesc='OpenStreetMap editor'
arch=('x86_64')
url='http://merkaartor.be/'
license=('GPL2')
depends=('exiv2' 'gdal' 'gpsd' 'hicolor-icon-theme' 'qt5-svg')
makedepends=('qt5-tools')
source=("https://github.com/openstreetmap/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        'qt-5.15.patch'
        'gpsd-3.20.patch'
        'merkaartor-proj8.patch'
        '0001-Add-AppStream-metadata.patch')
sha256sums=('16e790507a5bb54e4b3c09ea063ebb095a4b014ce8ba937b771706d4c940976a'
            'e8466ebecbb9336ade8363b9badcf43d0c2b7f54d0fdad8b053c072aa3dce44e'
            '6a19357c21e895e48775d0017d953e1d3b53b2cb795861d08984da3a4788005d'
            '2b0acbd3d557af25a4b726ac814dd4a7e3aff094caaeac5838696245daf4abff'
            'f8e2a77c53a063349e48bc64a60a4ee22297020b2fa65ef0021b2a7105fa89e8')

prepare() {
  cd $pkgname-$pkgver

  # Fix build with gpsd 3.20
  # https://github.com/openstreetmap/merkaartor/issues/204
  patch -Np1 -i ../gpsd-3.20.patch

  # Fix build with Qt 5.15
  patch -Np1 -i ../qt-5.15.patch

  # Fix build with proj 8
  patch -Np1 -i ../merkaartor-proj8.patch

  # Add AppStream metadata
  patch -Np1 -i ../0001-Add-AppStream-metadata.patch
}

build() {
  cd $pkgname-$pkgver
  lrelease src/src.pro
  qmake Merkaartor.pro PREFIX=/usr GPSDLIB=1 GEOIMAGE=1
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
