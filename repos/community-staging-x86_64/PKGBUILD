# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=structured
pkgname=haskell-structured
pkgver=0.1
pkgrel=8
pkgdesc="Structure (hash) of your data types"
url="https://github.com/phadej/binary-tagged"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base16-bytestring' 'haskell-hashable'
         'haskell-scientific' 'haskell-tagged' 'haskell-time-compat' 'haskell-unordered-containers'
         'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('52a1834a7c9ac1b62241bc9f424d63b1a541e791b13937f61d15da35b9ab024d3df39949f156b2f23293d397d961cb91c8fa38a5585db4f740a20b52b6dcc49d')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *1.5/<2/' $_hkgname.cabal
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
