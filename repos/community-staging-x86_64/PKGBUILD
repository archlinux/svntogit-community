# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Canto-curses
pkgname=canto-curses
pkgver=0.9.9
pkgrel=5
pkgdesc="Next-gen console RSS/Atom reader"
arch=('x86_64')
url="https://github.com/themoken/canto-curses"
license=('GPL2')
depends=('canto-daemon' 'glibc' 'libncursesw.so' 'libreadline.so' 'python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('cf476f67d6b7de9dcaf742dc983310a1ecfb324d7b104748a6fd9406821a02e36270c175b85e9bcb5c03b7ca0df25d49d306882500b0a8699b2bd0367756be9b')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
}
