# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=skylighting
pkgname=haskell-skylighting
pkgver=0.4.3.2
pkgrel=1
pkgdesc="Syntax highlighting library"
url="https://github.com/jgm/skylighting"
license=('GPL2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-blaze-html' 'haskell-case-insensitive' 'haskell-hxt' 'haskell-mtl'
         'haskell-pretty-show' 'haskell-regex-pcre' 'haskell-safe' 'haskell-text'
         'haskell-utf8-string')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ffd616abbf42641fda7168dc740ca04f994fb3a1f9b60ff47e8cd158af30eb7f70126d93e5204ee02b82139259aa0401b9dfaec53daa45073d602cfa25a4cd33')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsystem-pcre
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
