# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=optparse-generic
pkgname=haskell-optparse-generic
pkgver=1.4.6
pkgrel=4
pkgdesc="Auto-generate a command-line parser for your datatype"
url="https://github.com/Gabriel439/Haskell-Optparse-Generic-Library"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-system-filepath' 'haskell-only' 'haskell-optparse-applicative'
         'haskell-transformers-compat' 'haskell-void')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('4e2a2e40684cc57dfa63d698764d8e248ccd8ec9b2e23a119fca3d1e81eeac8789d774c10b435d62c9aa480ddc52de646ab1116948cfd55b340e19a946f8d32c')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
