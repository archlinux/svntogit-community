# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hedgehog
pkgname=haskell-hedgehog
pkgver=0.5.3
pkgrel=6
pkgdesc="A modern property-based testing system"
url="https://hedgehog.qa"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-concurrent-output'
         'haskell-exceptions' 'haskell-lifted-async' 'haskell-mmorph' 'haskell-monad-control'
         'haskell-mtl' 'haskell-pretty-show' 'haskell-primitive' 'haskell-random'
         'haskell-resourcet' 'haskell-semigroups' 'haskell-stm' 'haskell-text' 'haskell-th-lift'
         'haskell-transformers-base' 'haskell-wl-pprint-annotated')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('07b71a25876759941307ab0c69d90d03ca60302149649fdf5dff260c8dec505187b43607c5e9080aeafa90eb0c3a102c21a1d5a8e38bafcc4d34cfb4ff7a23b9')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
