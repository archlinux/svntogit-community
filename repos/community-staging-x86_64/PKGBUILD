# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=integer-roots
pkgname=haskell-integer-roots
pkgver=1.0
pkgrel=7
pkgdesc="Integer roots and perfect powers"
url="https://github.com/Bodigrim/integer-roots"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'uusi' 'haskell-smallcheck' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-quickcheck' 'haskell-tasty-smallcheck')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        $_hkgname-smallcheck-1.2.patch::https://github.com/Bodigrim/integer-roots/commit/e513864343b0464702333b9470d924e1522d1f9b.patch)
sha256sums=('c13f79d3a887e132e6e5157eea26a00121c301b3b6680ac6a445ca343203a788'
            '126481194ae1edd465b1fe787b61366a704db5834812eb0ebb774880b3715572')

prepare() {
  cd $_hkgname-$pkgver
  patch -p1 -i ../$_hkgname-smallcheck-1.2.patch
  gen-setup
  uusi -u integer-gmp $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
