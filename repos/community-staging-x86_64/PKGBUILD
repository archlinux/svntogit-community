# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=fsnotify
pkgname=haskell-fsnotify
pkgver=0.4.0.0
pkgrel=7
pkgdesc="Cross platform library for file change notification."
url="https://github.com/haskell-fswatch/hfsnotify"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-hinotify' 'haskell-monad-control'
         'haskell-safe-exceptions' 'haskell-unix-compat')
makedepends=('ghc' 'haskell-hunit' 'haskell-hspec' 'haskell-hspec-core' 'haskell-hspec-expectations'
             'haskell-retry' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('bdc3f6e5c5d1cfe45f09b7515f1a36d63dee578a0f0a5dde033db75060ff31f063aa444eb4600c545a35675f2c6bf344adb4672fbc3fd4dd3747bb33f2ea16b3')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
