# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=test-framework-quickcheck2
pkgname=haskell-test-framework-quickcheck2
pkgver=0.3.0.5
pkgrel=136
pkgdesc="QuickCheck-2 support for the test-framework package"
url="https://haskell.github.io/test-framework/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-test-framework' 'haskell-quickcheck' 'haskell-extensible-exceptions'
         'haskell-random')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('58c6177d52829ba1ea1858a11101e5b21ae4e4543f8f829779f62208ad16083b41e9f3b2275392693698ef3d46912096c368d90db0d67ff136af4fa73d5e1d43')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *2.13/<3/;s/< *1.2/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
