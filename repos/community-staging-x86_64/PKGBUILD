# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yaml
pkgname=haskell-yaml
pkgver=0.10.4.0
pkgrel=3
pkgdesc="Support for parsing and rendering YAML documents."
url="https://github.com/snoyberg/yaml/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'libyaml' 'haskell-aeson' 'haskell-attoparsec' 'haskell-conduit' 'haskell-vector'
         'haskell-resourcet' 'haskell-scientific' 'haskell-unordered-containers')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit' 'haskell-mockery' 'haskell-base-compat'
             'haskell-raw-strings-qq' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('7c29785b62246bd78420e7378397a40c6100b0825fb92da959d7d42244978c94a0bb3a6476123a3651b3a461455e8877fed67506768336691bb6ae63fe0062c4')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-no-unicode -fsystem-libyaml -f-no-exe -fno-examples
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test || warning "Tests failed"
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
