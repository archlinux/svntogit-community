# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Dominik Heidler <dheidler@gmail.com>

pkgname=libuhd
pkgver=3.8.3
_verstring='003_008_003-1'  # hopefully -X is temporary
pkgrel=1
pkgdesc="Universal Software Radio Peripheral (USRP) userspace driver"
arch=('x86_64' 'i686')
url="http://code.ettus.com/redmine/ettus/projects/uhd/wiki"
license=('GPL')
depends=('boost-libs' 'orc' 'libusbx')
optdepends=('python2: usrp utils')
makedepends=('cmake' 'boost' 'python2-cheetah')

source=("libuhd-$pkgver.tar.gz::https://github.com/EttusResearch/uhd/archive/release_${_verstring}.tar.gz")
md5sums=('7780250fec465db5081a387f1216c128')

build() {
  cd "$srcdir/uhd-release_$_verstring/host"
  # fix for py2
  find -name "*.py" -or -name '*.py.in' | xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"

  # fix for boost 1.58, already in git
  sed -i 's|return NULL;|return uhd::msg_task::msg_type_t(0, uhd::msg_task::msg_payload_t());|' lib/usrp/b200/b200_io_impl.cpp
  
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr/ \
           -DPYTHON_EXECUTABLE=/usr/bin/python2 \
           -DENABLE_EXAMPLES=OFF \
           -DENABLE_UTILS=ON \
           -DENABLE_TESTS=OFF \
           -DENABLE_E100=ON \
           -DENABLE_E300=ON
  make
}

check() {
  cd "$srcdir/uhd-release_$_verstring/host/build"
  make test
}

package() {
  cd "$srcdir/uhd-release_$_verstring/host/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "../utils/uhd-usrp.rules" "$pkgdir/usr/lib/udev/rules.d/10-uhd-usrp.rules"
} 

