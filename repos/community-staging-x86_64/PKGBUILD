# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=HsOpenSSL
pkgname=haskell-hsopenssl
pkgver=0.11.7
pkgrel=2
pkgdesc="Partial OpenSSL binding for Haskell"
url="https://github.com/vshabanov/HsOpenSSL"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-network' 'openssl')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('99c3c5ad707a7ad970a0b488258f89a6f4b5df311aeaa9cce50a0f09fefb9db6350121cb3734b72501b72a86cd150c7d3003e78d8158f5f760a6c13c74ecfa26')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
        -fuse-pkg-config
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING"
}
