# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=RSA
pkgname=haskell-rsa
pkgver=2.4.1
pkgrel=130
pkgdesc="Implementation of RSA, using the padding schemes of PKCS#1 v2.1."
url="https://github.com/GaloisInc/rsa"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-crypto-api' 'haskell-crypto-pubkey-types' 'haskell-sha')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-tagged' 'haskell-test-framework'
             'haskell-test-framework-quickcheck2')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('2cd3145f405bdc7dd1ddef3e93ef70a214b02607dc7f9648d36c258ba16eed881936816f4ae5ea3b7abd77b7b65d36a984e68dd19e3346fcf1d5231bca6cccb3')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '1i{-# language NoMonadFailDesugaring #-}' Test.hs
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
