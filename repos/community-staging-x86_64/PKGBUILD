# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant-swagger
pkgname=haskell-servant-swagger
pkgver=1.1.7
pkgrel=2
pkgdesc="Generate Swagger specification for your servant API."
url="https://github.com/haskell-servant/servant-swagger"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-http-media'
         'haskell-insert-ordered-containers' 'haskell-lens' 'haskell-servant'
         'haskell-singleton-bool' 'haskell-swagger2' 'haskell-unordered-containers'
         'haskell-hspec' 'haskell-quickcheck')
makedepends=('ghc' 'haskell-base-compat' 'haskell-cabal-doctest' 'haskell-doctest'
             'haskell-hspec-discover' 'haskell-utf8-string')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1c9d6a481a361950cb6f345af37698f686a00a66b534cf729aef735fd5fa3720eced7a67b837d03af4b230c7ef6683eb226e00d455cba7fddb9e57cb26763183')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/< *2.7/<3/' -e 's/>=2.3/>=2.2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
