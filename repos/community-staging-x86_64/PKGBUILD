# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=2.8.0
pkgrel=2
pkgdesc="Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=(x86_64)
url="https://www.ospray.org/"
license=(Apache)
depends=(tbb embree ispc libgl glfw glu openmpi openimagedenoise openvkl rkcommon snappy)
optdepends=('benchmark: benchmarks'
            'gtest: tests suite')
makedepends=(cmake benchmark gtest)
source=(https://github.com/ospray/OSPRay/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('2dabc75446a0e2e970952d325f930853a51a9b4d1868c8135f05552a4ae04d39')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOSPRAY_BUILD_ISA=ALL \
    -DOSPRAY_MODULE_DENOISER=ON \
    -DOSPRAY_MODULE_MPI=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
