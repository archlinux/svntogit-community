# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=2.6.0
pkgrel=2
pkgdesc='Switchboard Power Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-power
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libswitchboard-2.0.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_tag=889233a4447c3c1e034dd766c588bd38c5e253e3
source=(git+https://github.com/elementary/switchboard-plug-power.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-power

  git describe --tags
}

build() {
  arch-meson switchboard-plug-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
