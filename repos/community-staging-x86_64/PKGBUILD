# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: nfnty
pkgbase='kea'
pkgname=("${pkgbase}" "${pkgbase}-docs")
pkgver=2.2.0
_upstreamver="${pkgver}"
pkgrel=4
pkgdesc='High-performance, extensible DHCP server engine from ISC, supporting both DHCPv4 and DHCPv6'
arch=('x86_64')
url='https://kea.isc.org'
license=('MPL2')
depends=('boost-libs' 'log4cplus' 'mariadb-libs' 'openssl' 'postgresql-libs')
makedepends=(
  'boost'
  'mariadb'
  'postgresql'
  'python'  # kea-shell
  'python-sphinx'
  'python-sphinx_rtd_theme'
  'doxygen'
  # texlive-most group
  'texlive-bibtexextra'
  'texlive-core'
  'texlive-fontsextra'
  'texlive-formatsextra'
  'texlive-games'
  'texlive-humanities'
  'texlive-latexextra'
  'texlive-music'
  'texlive-pictures'
  'texlive-pstricks'
  'texlive-publishers'
  'texlive-science'
)
optdepends=('mariadb: lease information database'
            'postgresql: lease information database'
            'python: to use kea-shell'
            'krb5: Kerberos support'
            'kea-docs: user and developer documentation')
# Checks are disabled for now
# We can ignore mysql/pgsql, but admin tests use custom scripts that break everything
checkdepends=('benchmark' 'gtest' 'procps-ng') # procps-ng needed for 'pgrep'
backup=(
  # New config files as of Kea 1.3
  'etc/kea/kea-dhcp4.conf'
  'etc/kea/kea-dhcp6.conf'
  'etc/kea/kea-dhcp-ddns.conf'
  'etc/kea/keactrl.conf'
  'etc/kea/kea-ctrl-agent.conf'
  'etc/kea/kea-netconf.conf'
)
install=kea.install
source=(
  "https://ftp.isc.org/isc/${pkgbase}/${_upstreamver}/${pkgbase}-${_upstreamver}.tar.gz"{,.asc}
  'tmpfiles-kea.conf'
  'kea-dhcp4.service'
  'kea-dhcp6.service'
  'kea-dhcp-ddns.service'
  'kea-ctrl-agent.service'
  'kea.install'
)
sha512sums=('82cd44efea8c968ef097de242e1ca59e0183c80df25050017fd45538d35da64a9a0d2f4a5249ad3bd3d30b1f8895c360d301518bc22e60dfddd966fe020dc773'
            'SKIP'
            'b83479f0b90caf4022b1c858f99d8310848a90b0cc45aaa222cb2bf08b2a97312816b8f64c8c0232b612795bc7a0bbe5b05dbc508b2f1214c1ea88dc84200a2d'
            '1b62dace9f9a955b21c5dd5b5466fb3a4f1d8057ad2a30ea89e222a769dc721ad2fe4913c41ec407e018b1b38ddde757e9b7b8fc12d4763ddde077a0abb0c971'
            '8f0e8d059338c415ae322e971e243b259d9b2ef7d37b6be57b409bf5f0f3392e519c9bb94c5b548021d000b944a5da82096608cd9ff3ee58061059ad519b0068'
            '1da9f02d5f6b3e2f9f2f896b1085c141b8739bd6614706fe0544a31b278f4d3c6616cb1ab3915c650262ea3592df86e95b53ce944ade2c80bb72fcae796f8fd4'
            'db329fd83e458e4a77f6df8569074088bed152ffc1a188059d868d7178a6d3530efe368ce870a2e0be894f87fa0e5ba43562bdc3ad6372ddfcfedae37c90f491'
            '758371f20a977f8e42e9d43d2d065c6aa294866070e54868dc329c0875b2eeafa05dcdf93590d5b429e262e2350a42c7616a3bc2e6f3994f53886870f4910251')
validpgpkeys=('BE0E9748B718253A28BB89FFF1B11BF05CF02E57'  # Internet Systems Consortium, Inc. (Signing key, 2017-2018) <codesign@isc.org>
              'AE3FAC796711EC59FC007AA474BB6B9A4CBB3D38'  # Internet Systems Consortium, Inc. (Signing key, 2019-2020) <codesign@isc.org>
              '7E1C91AC8030A5A59D1EFAB9750F3C87723E4012') # Internet Systems Consortium, Inc. (Signing key, 2021-2022) <codesign@isc.org>

build() {
  cd "${srcdir}/${pkgbase}-${_upstreamver}"
  # make sure Google Benchmark can be linked to
  sed -e 's/dir\/lib\/libbenchmark.a/dir\/lib\/libbenchmark.so/g' \
      -e 's/BENCHMARK_LDADD="$dir\/lib\/libbenchmark.so/BENCHMARK_LDADD="-lbenchmark/' \
      -i configure.ac
  autoreconf --install
  ./configure \
      --prefix='/usr' \
      --sbindir='/usr/bin' \
      --libexecdir='/usr/bin' \
      --sysconfdir='/etc' \
      --localstatedir='/var' \
      --enable-benchmark \
      --enable-perfdhcp \
      --enable-generate-messages \
      --enable-generate-parser \
      --enable-generate-docs \
      --enable-shell \
      --with-openssl \
      --with-gtest \
      --with-benchmark \
      --with-mysql \
      --with-pgsql \
      --with-gssapi \
      --with-log4cplus
  # do not treat sphinx warnings as errors
  sed -e 's/sphinxopts = -v -E -a -W/sphinxopts = -v -E -a/g' \
      -i doc/Makefile.in \
      -i doc/Makefile \
      -i doc/sphinx/Makefile.in \
      -i doc/sphinx/Makefile
  make
  make -C doc
  make -C doc/sphinx
  make -C doc/devel devel
}

check() {
  cd "${srcdir}/${pkgname}-${_upstreamver}"
  # Disable mysql and postgresql for now, tests require live DBs
  export GTEST_FILTER='-mysql.*:*mysql*:*MySql*:*MySQL*:pgsql.*:*pgsql*:*PgSql*:*PgSQL*'
  # the above works, but admin tests use custom scripts that ignore it and break everything
  # make check
}

package_kea() {
  cd "${srcdir}/kea-${_upstreamver}"
  make DESTDIR="${pkgdir}" install
  # Handle /var/run -> /run symlink
  rmdir "${pkgdir}"/var/run/kea
  rmdir "${pkgdir}"/var/run
  install -Dm644 "${srcdir}"/tmpfiles-kea.conf "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  # Systemd service files
  install -Dm644 "${srcdir}"/kea-dhcp4.service "${pkgdir}"/usr/lib/systemd/system/kea-dhcp4.service
  install -Dm644 "${srcdir}"/kea-dhcp6.service "${pkgdir}"/usr/lib/systemd/system/kea-dhcp6.service
  install -Dm644 "${srcdir}"/kea-dhcp-ddns.service "${pkgdir}"/usr/lib/systemd/system/kea-dhcp-ddns.service
  install -Dm644 "${srcdir}"/kea-ctrl-agent.service "${pkgdir}"/usr/lib/systemd/system/kea-ctrl-agent.service
  # split docs in a separate package
  rm -rf "${pkgdir}"/usr/share/doc
}

package_kea-docs() {
  depends=()
  optdepends=()
  provides=('kea-devel-docs')
  replaces=('kea-devel-docs')
  cd "${srcdir}/${pkgbase}-${_upstreamver}"
  install -d "${pkgdir}/usr/share/doc/${pkgbase}/"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,var,usr/{bin,lib,include,share/kea}}
  make DESTDIR="${pkgdir}" -C doc install
  make DESTDIR="${pkgdir}" -C doc/devel install
  cp -r doc/devel/html "${pkgdir}/usr/share/doc/${pkgbase}/devel"
}
