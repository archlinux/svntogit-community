# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.3.0
pkgrel=3
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'qt5-styleplugins' 'deepin-qt5dxcb-plugin')
makedepends=('xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz")
sha512sums=('7ee7b5d59a4093736de59207ff5f9a750cbea60c5cf7ca35edbddb4f2746b0247b338c2422e4a5fb8ef9643f153d4eb5bc65aa3d727f913a7dc005c7a857f6f1')

prepare() {
  cd qt5integration-$pkgver
  sed -i 's|qt5xdgiconloader/2.0.0|qt5xdgiconloader/3.2.0|' platformthemeplugin/main.cpp
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
