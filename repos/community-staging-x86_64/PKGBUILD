# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=haskell-src-exts
pkgname=haskell-src-exts
pkgver=1.23.1
pkgrel=184
pkgdesc="Manipulating Haskell source: abstract syntax, lexer, parser, and pretty-printer"
url="https://github.com/haskell-suite/haskell-src-exts"
license=("custom:BSD")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'happy' 'haskell-pretty-show' 'haskell-smallcheck' 'haskell-tasty'
             'haskell-tasty-golden' 'haskell-tasty-smallcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c9e6e4d93c2b4ee84d57707aed526291b5c4e297f06a3921257feb0c1df3bb095addd4b1d9bb187650490364647385ab6322e4fc54f9a7648da439a34329d734')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/haskell-suite/haskell-src-exts/issues/448
    runhaskell Setup test || :
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
