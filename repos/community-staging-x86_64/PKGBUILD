# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-metee
pkgname=(intel-metee intel-metee-doc)
pkgver=3.1.4
pkgrel=1
pkgdesc="Access library for Intel CSME HECI interface"
arch=(x86_64)
url="https://github.com/intel/metee/"
license=(Apache)
makedepends=(cmake doxygen)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('03002ddd1b8b4208515ca14f3650879378c6482ebfb513c99d1a115b724c02ea')

build() {
  cmake -B build -S metee-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -Wno-dev
  make -C build
}

package_intel-metee() {
  depends=(glibc)
  
  make -C build DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/share/doc .
}

package_intel-metee-doc() {
  pkgdesc+=' (documentation)'
    
  install -dm755 "${pkgdir}"/usr/share
  mv doc "${pkgdir}"/usr/share
}
