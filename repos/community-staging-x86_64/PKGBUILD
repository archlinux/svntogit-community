# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.13.20180419
_commit=ecf1a0ca0d094c76a18c2c1b77ac7c1dcac10f5e
pkgrel=1
pkgdesc="Lightweight X11 tiled window manager written in Haskell"
arch=('x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc' 'haskell-x11' 'sh' 'haskell-utf8-string' 'haskell-extensible-exceptions'
         'haskell-data-default' 'haskell-setlocale')
makedepends=('gendesk')
optdepends=('xorg-xmessage: for displaying visual error messages')
source=($pkgname-$_commit.tar.gz::https://github.com/xmonad/xmonad/archive/${_commit}.tar.gz
        dynamic-compilation.patch
        xmonad.svg)
sha512sums=('12a35f837d7b2437e91c00cb88d5b2f7b98e6f26b8329c06cb5ec5d265da61644820a43e7b07c260ff52321ffa82214478a267471f36238baa70574366a0396d'
            'abc598ebc4b55917186cd4d549fc970f53905f820d394ada9a092c08d8833050ad18b23e2f4fd7f281487da2db5b0ca058600e995ac97b18d3be4c62071562d6'
            '31f0ceb724a9931aeda75ab706c8908b5d8341af4df50ab1d749814baacb748ad3622601cd99ac03bf936b698fcd854b23d1541e1772306e3337a13a79b110b6')

prepare() {
  mv $pkgname-{$_commit,$pkgver}
  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"
  cd $pkgname-$pkgver
  patch -p1 -i "${srcdir}/dynamic-compilation.patch"

  sed -i -e '/semigroups/d' -e 's/< 1.9,/< 1.9/' $pkgname.cabal
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"

  runhaskell Setup.lhs copy --destdir="${pkgdir}"

  # Requires pandoc, regex-posix, Pretty and cabal
  #runhaskell util/GenerateManpage.hs

  install -Dm 644 man/xmonad.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/xmonad"
  install -Dm 644 "$srcdir/xmonad.svg" -t "${pkgdir}/usr/share/pixmaps"
  install -Dm 644 "$srcdir/xmonad.desktop" -t "${pkgdir}/usr/share/xsessions"
}

# vim: ts=2 sw=2 et:
