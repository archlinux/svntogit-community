# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Michael Migliore <mcmigliore+aur@gmail.com>
# Contributor: Mathieu Wespthal <mathieu.westphal+aur@gmail.com>
pkgname=f3d
pkgver=2.0.0
pkgrel=4
pkgdesc='A fast and minimalist 3D viewer'
arch=('x86_64')
url="https://github.com/${pkgname}-app/${pkgname}"
license=('BSD')
depends=('vtk' 'glew' 'pugixml' 'netcdf' 'ospray' 'assimp' 'opencascade' 'alembic'
         'fmt' 'nlohmann-json' 'draco')
makedepends=('cmake' 'help2man' 'openmp' 'pybind11' 'ninja' 'eigen' 'utf8cpp' 'python' 'jdk-openjdk')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname-app/$pkgname/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('5b335de78a9f68903d7023d947090d4b36fa15b9e165749906a82153e0f56d05')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DF3D_BINDINGS_JAVA=ON \
    -DF3D_BINDINGS_PYTHON=ON \
    -DF3D_LINUX_GENERATE_MAN=ON \
    -DF3D_MODULE_RAYTRACING=ON \
    -DF3D_MODULE_EXTERNAL_RENDERING=ON \
    -DF3D_PLUGINS_STATIC_BUILD=ON \
    -DF3D_PLUGIN_BUILD_ALEMBIC=ON \
    -DF3D_PLUGIN_BUILD_ASSIMP=ON \
    -DF3D_PLUGIN_BUILD_DRACO=ON \
    -DF3D_PLUGIN_BUILD_OCCT=ON
  ninja -C build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  DESTDIR="$pkgdir" cmake --install build
  DESTDIR="$pkgdir" cmake --install build --component mimetypes
  DESTDIR="$pkgdir" cmake --install build --component sdk
  DESTDIR="$pkgdir" cmake --install build --component configuration
  install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
