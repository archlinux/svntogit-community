# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Sandy Carter <bwrsandman@gmail.com>

pkgname=openmw
pkgver=0.46.0
pkgrel=4
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'bullet' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt')
makedepends=('cmake' 'boost' 'doxygen' 'ninja' 'git')
source=("git+https://github.com/OpenMW/openmw.git#tag=openmw-$pkgver"
        openmw-0.46.0-missing-include.patch)
sha512sums=('SKIP'
            'd0d47fe249e8fb366e00e272dc62853f8a2aaa75d45d83d270891e76674c1382fd3405e674e3b6c383bbc17125c4471893e77251744deae4d17bc373d847a638')

prepare() {
  cd openmw
  patch -Np1 -i ${srcdir}/openmw-0.46.0-missing-include.patch
}

build() {
  cd openmw
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDESIRED_QT_VERSION=5 \
    -GNinja \
    -Bbuild
  ninja -C build
}

package() {
  cd openmw
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
