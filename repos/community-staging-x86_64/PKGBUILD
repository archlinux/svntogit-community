# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-2
_patchver=4
#pkgver=${_majver//-/.}.p${_patchver}
pkgver=${_majver//-/.}
pkgrel=1
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="https://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen python2 polymake)
optdepends=('python2: Python interface' 'polymake: Polymake module')
#source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz"
        singular-polymake-3.3.patch)
sha256sums=('5807d8da6f51a9e33bf7ff9c7bb956b07091a98b3c848435d3a80d73b23e7340'
            '683fd857562928833209f70765a6348a547d41bda6293b59a6e8ced54d062efc')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  patch -p1 -i ../singular-polymake-3.3.patch # Fix build with polymake 3.3
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info "$pkgdir"/usr/share/doc/singular.idx
  # Fix singular.idx location https://www.singular.uni-kl.de:8002/trac/ticket/842
  mv "$pkgdir"/usr/share/doc/singular.idx "$pkgdir"/usr/share/singular
  rm -r "$pkgdir"/usr/share/doc
}
