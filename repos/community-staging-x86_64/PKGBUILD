# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_hkgname=mono-traversable-instances
pkgname=haskell-mono-traversable-instances
pkgver=0.1.1.0
pkgrel=154
pkgdesc='Extra typeclass instances for mono-traversable'
arch=(x86_64)
url="https://hackage.haskell.org/package/$_hkgname"
license=(MIT)
depends=(ghc-libs haskell-dlist haskell-comonad haskell-mono-traversable
         haskell-semigroupoids haskell-vector-instances haskell-dlist-instances)
makedepends=(ghc uusi)
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('367b92fc60c397816fd71d88133a4dd14f91ee927470b6c7967cf1f8f72de8f91c475a6a504fd4b3e520ceb18950ff300bb9530fccda7a7a81573ac93af41513')

prepare() {
  cd $_hkgname-$pkgver
  uusi -d semigroups $_hkgname.cabal
  sed -i 's/tailEx = DL.tail/tailEx = DL.fromList . DL.tail/' src/Data/MonoTraversable/Instances.hs
}

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
