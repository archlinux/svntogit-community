# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=gnuidn
pkgname=haskell-gnuidn
pkgver=0.2.2
pkgrel=191
pkgdesc="Bindings for GNU IDN"
url="https://john-millikin.com/software/haskell-gnuidn/"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' "libidn")
makedepends=('c2hs' 'ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4ac894ba4cc4fb9fd90ccea55fda342cd0dadc863a091d31287b19117ec1e545a23b7fb69db02bbb8ee9eb84856acb51f3ab0d7778f4ca3da290dfcbc7ef27ac')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
