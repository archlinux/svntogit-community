# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Vasco Costa <vasco.costa@meiodigital.com>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=pokerth
_realname=PokerTH
pkgver=1.1.1
pkgrel=10
pkgdesc="Open source Texas Hold'em Poker simulator"
arch=('i686' 'x86_64')
url="http://www.pokerth.net/"
license=('GPL' 'custom')
depends=('curl' 'boost-libs' 'gsasl' 'gnutls' 'protobuf' 'libxkbcommon-x11'
         'qt5-base' 'sdl_mixer' 'libircclient' 'tinyxml')
makedepends=('boost')
source=("http://downloads.sourceforge.net/sourceforge/pokerth/$_realname-$pkgver-src.tar.bz2"
        '0001-Qt-5.5.0-patch-for-qtsingleapplication.patch'
        'pokerth-1.1.1-Qualify-std-ifstream-and-std-ofstream.patch')
md5sums=('a7f76f95782099f966e5f2b6809f502a'
         'f4ecab0a6435f1b3b0654145b75d6e7c'
         '26b02c7e5c50484485570ace9b63be5b')

prepare() {
  cd "$srcdir/$_realname-$pkgver-src"

  # https://github.com/pokerth/pokerth/commit/731f5f05f540
  patch -Np1 -i ../0001-Qt-5.5.0-patch-for-qtsingleapplication.patch

  # https://github.com/pokerth/pokerth/commit/69f820bb3
  patch -p1 -i ../pokerth-1.1.1-Qualify-std-ifstream-and-std-ofstream.patch
}

build() {
  cd "$srcdir/$_realname-$pkgver-src"

  qmake-qt5 "$pkgname.pro"
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver-src"

  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 "docs/pokerth.1" "$pkgdir/usr/share/man/man1/pokerth.1"
  install -Dm644 "data/data-copyright.txt" "$pkgdir/usr/share/licenses/pokerth/data-copyright.txt"
}
