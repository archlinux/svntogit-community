# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Luca Weiss
# Contributor: Michael Yang

pkgname=spdlog
pkgver=1.4.2
pkgrel=2
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=('x86_64')
url='https://github.com/gabime/spdlog'
license=('MIT')
depends=('fmt')
makedepends=('cmake')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/gabime/spdlog/archive/v$pkgver.tar.gz"
    "include_cassert.patch"
)
sha256sums=('821c85b120ad15d87ca2bc44185fa9091409777c756029125a02f81354072157'
            '917916a246a784535bd2141de299ed396b497a24c6f1bc72222c9d69b7cbc6e2')

prepare() {
    mkdir build
    cd "$pkgname-$pkgver"
    patch -p1 < ../include_cassert.patch
}

build() {
    cd build
    cmake ../"$pkgname-$pkgver" \
        -DSPDLOG_BUILD_BENCH=OFF \
        -DSPDLOG_BUILD_EXAMPLES=OFF \
        -DSPDLOG_FMT_EXTERNAL=ON \
        -DSPDLOG_BUILD_SHARED=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 ../"$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
