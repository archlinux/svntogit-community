# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=jami-qt
pkgver=20220726
pkgrel=2
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (Qt client)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(jami-daemon glib2 gdk-pixbuf2 libnm libnotify qrencode
         qt6-declarative qt6-multimedia qt6-svg qt6-5compat qt6-webengine
         qt6-webchannel)
makedepends=(git cmake python qt6-networkauth qt6-tools)
replaces=(jami-gnome jami-libclient)
conflicts=(jami-gnome jami-libclient)
_commit=e481bb4eb0380276f6f1097d256ffdd51b87967b
source=(git+https://git.jami.net/savoirfairelinux/jami-client-qt.git#commit=${_commit}
        drop-qt-version-check.patch
        fix-libavutil-linking.patch)
sha256sums=(SKIP d0118269d637b984f13755a5c3548b2509215789aeda2c3b9ac7850c2bd7cf3e
            03bd2fd7b23b109bef2cb9d94d2d19613c26a7e3d05bf356cb808a79fa6d1658)

pkgver() {
  cd jami-client-qt
  TZ=UTC git show -s --pretty=%cd --date=format-local:%Y%m%d HEAD
}

prepare() {
  # Drop strict Qt version check. Qt is ABI stable and jami is not using any private API
  patch -p1 -d jami-client-qt < drop-qt-version-check.patch
  # https://git.jami.net/savoirfairelinux/jami-client-qt/-/issues/762 | https://review.jami.net/c/jami-client-qt/+/21688
  patch -p1 -d jami-client-qt < fix-libavutil-linking.patch
}

build() {
  cmake -B build -S jami-client-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_LIBWRAP=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
