# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

_hkgname=path-io
pkgname=haskell-path-io
pkgver=1.6.3
pkgrel=27
pkgdesc="Interface to ‘directory’ package for users of ‘path’"
url="https://github.com/mrkkrp/path-io"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-path' 'haskell-temporary'
         'haskell-unix-compat')
makedepends=('ghc' 'uusi' 'haskell-hspec')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('d9d4e0d3a0b74677b8e29608254b1a4b5f306378400e8ec6d2b96ec4fd5b340ca20652684af558ec5ec5c401a18c4c9203adf7afbb1a602df0786459bc5bc774')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u path $_hkgname.cabal
}

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-dev
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver
  
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
