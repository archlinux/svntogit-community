# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=pretty-simple
pkgname=haskell-pretty-simple
pkgver=3.2.2.0
pkgrel=6
pkgdesc="Pretty printer for data types with a 'Show' instance"
url="https://github.com/cdepillabout/pretty-simple"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-glob' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ecc1ab3cb61d5ae7ff355657d49418d839d34affadb3a01e572daa5a7fde62fc7b58b7fc8b7016d31aff7bc941c57fc30e0d3fb9e37372cdf2fa092862b5f6ab')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
