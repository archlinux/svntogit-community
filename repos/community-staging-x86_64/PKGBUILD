# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=parameterized-utils
pkgname=haskell-parameterized-utils
pkgver=2.1.5.0
pkgrel=80
pkgdesc="Classes and data structures for working with data-kind indexed types"
url="https://github.com/GaloisInc/parameterized-utils"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-constraints' 'haskell-hashable'
         'haskell-hashtables' 'haskell-indexed-traversable' 'haskell-lens' 'haskell-profunctors'
         'haskell-th-abstraction' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hedgehog' 'haskell-hedgehog-classes' 'haskell-tasty'
             'haskell-tasty-ant-xml' 'haskell-tasty-hunit' 'haskell-tasty-hedgehog')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        hashable-1.4.patch)
sha512sums=('f3cf077c50e18d426b63e86c90a719709db30ed5c85ee3ec6e2ab263fc5be3c939a27069bc83f09bb97176236a186e599051099c7653db8f7a5e33b98b8b88ed'
            '84b1888a89adc682d56e7889e45d69075831442828777ce6be487930e76d9dda2f7adf0cc6b0631694b753b7e09ade34e3a4c1ca5444a6e9ceaab58cbf477274')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../hashable-1.4.patch
    gen-setup
    uusi -u vector $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
