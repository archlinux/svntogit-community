# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-tactics-plugin
pkgname=haskell-hls-tactics-plugin
pkgver=1.7.0.0
pkgrel=3
pkgdesc="Wingman plugin for Haskell Language Server"
url="https://haskellwingman.dev"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-extra' 'haskell-fingertree' 'haskell-generic-lens'
         'haskell-ghc' 'haskell-ghc-exactprint' 'haskell-ghc-source-gen' 'haskell-ghcide'
         'haskell-hls-graph' 'haskell-hls-plugin-api' 'haskell-hls-refactor-plugin'
         'haskell-hyphenation' 'haskell-lens' 'haskell-lsp' 'haskell-megaparsec'
         'haskell-parser-combinators' 'haskell-prettyprinter' 'haskell-refinery' 'haskell-retrie'
         'haskell-syb' 'haskell-unagi-chan' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hls-test-utils' 'haskell-hspec'
             'haskell-hspec-discover' 'haskell-hspec-expectations' 'haskell-lsp-types'
             'haskell-tasty-hunit' 'haskell-tasty-hspec')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('64bef8fd9d3d53ddff478891293df0998181e4471d67a6e6bf4e7b70a40e9fb83302f9c0b1b28b8fe1dd28dcaafcd8ab8627b7bc98f0a4b8484b43d09667dcd6')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # TODO
  runhaskell Setup test --show-details=direct || echo "Tests failed"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
