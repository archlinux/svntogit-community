# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.4
pkgrel=16
pkgdesc="Curses-style user interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' "haskell-hledger-lib" "haskell-ansi-terminal" "haskell-async"
         "haskell-base-compat" "haskell-cmdargs" "haskell-data-default" "haskell-fsnotify"
         "haskell-hunit" "haskell-microlens" "haskell-microlens-platform" "haskell-megaparsec"
         "haskell-pretty-show" "haskell-safe" "haskell-split" "haskell-text" "haskell-text-zipper"
         "haskell-vector" "haskell-brick" "haskell-vty")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('25712f7254933cd14cc1876b4c5b1eae2ea8c07324191b34e8c875cbe4e5ee60389db5cf804d556d91cc99d1baccf7dedd3cfe094a29caa03ee0981b12e89937')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -e 's/, megaparsec >=5.0 && < 6.2/, megaparsec >=5/' \
        -e 's/vty >= 5.5 && < 5.18/vty >= 5.5/' \
        -e 's/brick >= 0.12 && < 0.25/brick >= 0.12/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -fthreaded
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
