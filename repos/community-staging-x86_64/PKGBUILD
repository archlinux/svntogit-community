# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=th-lift-instances
pkgname=haskell-th-lift-instances
pkgver=0.1.15
pkgrel=2
pkgdesc="Lift instances for template-haskell for common data types."
url="https://github.com/bennofs/th-lift-instances/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-lift' 'haskell-vector')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d5fe76a0f6af3247111a67c15f2cee7c466081fb6a5ed088df2ff76e91899c2f90bf43c59fcb38e7a3944bc877dc5d6654d6fec1d0950485f8a9b71ee34c7c5b')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
