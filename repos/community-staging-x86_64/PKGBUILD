# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=binary-tagged
pkgname=haskell-binary-tagged
pkgver=0.1.4.2
pkgrel=36
pkgdesc="Tagged binary serialisation."
url="https://github.com/phadej/binary-tagged#readme"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-base16-bytestring" "haskell-generics-sop"
         "haskell-hashable" "haskell-scientific" "haskell-semigroups" "haskell-sha" "haskell-tagged"
         "haskell-text" "haskell-unordered-containers" "haskell-vector")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('311fab8c2bac00cb6785cb144e25ed58b2efce85e5dc64e30e2b5a2a16cdc784')

prepare() {
    # nats is an empty package in new GHC
    sed -e '/nats/d' \
        -e 's/, vector                   >=0.10 && <0.12/, vector                   >=0.10/' \
        -e 's/, aeson                    >=0.8  && <1.1/, aeson                    >=0.8/' \
        -e 's/, generics-sop             >=0.1  && <0.3/, generics-sop             >=0.1/' \
        -e 's/, time                     >=1.4  && <1.7/, time                     >=1.4/' \
        -e 's/base                     >=4.7  && <4.10/base                     >=4.7/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
