# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Keith Hughitt <khughitt@umd.edu>

# TODO: move python-xarray from AUR to community (converting Panels to xarray)
# TODO: move python-pyarrow from AUR to community (Feather Format and Apache Parquet)
# TODO: move python-fastparque from AUR to community (Apache Parquet)
# TODO: move python-s3fs from AUR to community (Amazon S3 access)
# TODO: move python-gcsfs from AUR to community (Google Cloud Storage access)
# TODO: package pandas-gbq (Google BigQuery I/O)
# TODO: package python-pyreadstat (SPSS files reading)

pkgname=python-pandas
pkgver=0.25.2
pkgrel=3
pkgdesc="Library providing high-performance, easy-to-use data structures and data analysis tools"
arch=('x86_64')
url="https://pandas.pydata.org/"
license=('BSD')
depends=('python-numpy' 'python-dateutil' 'python-pytz' 'python-setuptools')
makedepends=('cython')
optdepends=('python-pandas-datareader: pandas.io.data replacement (recommended)'
            'python-numexpr: needed for accelerating certain numerical operations (recommended)'
            'python-bottleneck: needed for accelerating certain types of nan evaluations (recommended)'
            'python-scipy: needed for miscellaneous statistical functions'
            'python-pytables: needed for HDF5-based storage'
            'python-sqlalchemy: needed for SQL database support'
            'python-matplotlib: needed for plotting'
            'python-openpyxl: needed for Excel XLSX input/output'
            'python-xlrd: needed for Excel XLS input'
            'python-xlwt: needed for Excel XLS output'
            'python-xlsxwriter: alternative Excel XLSX output'
            'python-jinja: needed for conditional HTML formatting'
            'python-blosc: for msgpack compression using blosc'
            'python-qtpy: needed for read_clipboard function (only one needed)'
            'python-pyqt5: needed for read_clipboard function (only one needed)'
            'xsel: needed for read_clipboard function (only one needed)'
            'xclip: needed for read_clipboard function (only one needed)'
            'python-beautifulsoup4: needed for read_html function'
            'python-lxml: needed for read_html function (and/or python-html5lib)'
            'python-html5lib: needed for read_hmlt function (and/or python-lxml)'
            'zlib: needed for compression for msgpack')
source=("https://github.com/pandas-dev/pandas/releases/download/v${pkgver}/pandas-${pkgver}.tar.gz")
sha256sums=('ca91a19d1f0a280874a24dca44aadce42da7f3a7edb7e9ab7c7baad8febee2be')

build() {
  cd "$srcdir"/pandas-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package() {
  cd "$srcdir"/pandas-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
