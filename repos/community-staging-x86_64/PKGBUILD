# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cmarkgfm
pkgname=(python-cmarkgfm python2-cmarkgfm)
pkgver=0.4.2
pkgrel=5
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('x86_64')
makedepends=('cmark-gfm' 'python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/theacodes/cmarkgfm/archive/$pkgver.tar.gz"
        cmark-gfm-0.28.3.gfm.17.patch
        cmark-gfm-0.28.3.gfm.19.patch)
sha512sums=('afaf475c54abdb19eaae45be1e4488552d3ef47bfcdaad83ceb84f49230731643bd0c58640b3c51e199d4a449043e3ae5a70abdf676ee5b4b8064825390c0406'
            '4905cb73c9f370527faed603f7af19f3d526b758ecc234f876707b5597d2c3ab56c7315a73d8bd0bc76e819b359eda45ee67fcaa9bbf271f1920c6237e20a996'
            'f729b314ba85d74361ab4c341fee553a5986c7e6c6521a8a7bc88892cc8421a48ee5e9c9c98ffb69e2869176057156f864c26aa73bc0c1a4340715ef63c2e781')

prepare() {
  cd cmarkgfm-$pkgver
  patch -p1 -i ../cmark-gfm-0.28.3.gfm.17.patch
  patch -p1 -i ../cmark-gfm-0.28.3.gfm.19.patch
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd cmarkgfm-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-cmarkgfm() {
  depends=('cmark-gfm' 'python-cffi')

  cd cmarkgfm-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-cmarkgfm() {
  depends=('cmark-gfm' 'python2-cffi')

  cd cmarkgfm-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
