# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=lens
pkgname=haskell-lens
pkgver=4.15.4
pkgrel=8
pkgdesc="Lenses, Folds and Traversals"
url="https://github.com/ekmett/lens/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-base-orphans" "haskell-bifunctors" "haskell-call-stack" "haskell-comonad"
         "haskell-contravariant" "haskell-distributive" "haskell-exceptions" "haskell-free"
         "haskell-hashable" "haskell-kan-extensions" "haskell-mtl" "haskell-parallel"
         "haskell-profunctors" "haskell-reflection" "haskell-semigroupoids" "haskell-semigroups"
         "haskell-tagged" "haskell-text" "haskell-th-abstraction" "haskell-transformers-compat"
         "haskell-unordered-containers" "haskell-vector" "haskell-void")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1f1333829eba740d406413c6d59fb16398b56198cd16a2506631487e20ce128163892a3934d0f4a59b9151b39c1ef19e80fb425f8d59013004118ad08a697530')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-j -ftrustworthy -f-safe -ftest-templates -ftest-properties -ftest-hunit \
            -ftest-doctests -f-dump-splices -f-old-inline-pragmas -finlining \
            -f-benchmark-uniplate
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
