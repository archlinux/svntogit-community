# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=bsb-http-chunked
pkgname=haskell-bsb-http-chunked
pkgver=0.0.0.3
pkgrel=5
pkgdesc="Chunked HTTP transfer encoding for bytestring builders"
url="https://github.com/sjakobi/bsb-http-chunked"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-attoparsec' 'haskell-blaze-builder' 'haskell-hedgehog' 'haskell-tasty'
             'haskell-tasty-hedgehog' 'haskell-tasty-hunit' 'haskell-doctest')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ea3eb15b1d02e770a54df61daac1db044008ce204eeb68c4b7333a0799d5a1a2001f51927bc8fa824ffae93f2b8c85dcb69e9506430f7fc2dbb7a7ff452373ca')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/bytestring-builder < 0.11/bytestring < 0.11/' \
           -e '/, bytestring-builder/d' \
           $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
