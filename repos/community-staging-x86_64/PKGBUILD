# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Tidal
pkgname=haskell-tidal
pkgver=1.8.0
pkgrel=10
pkgdesc="A domain specific language for live coding pattern."
arch=(x86_64)
url="https://tidalcycles.org/"
license=(GPL3)
groups=(pro-audio)
depends=(ghc ghc-libs glibc haskell-async haskell-bifunctors haskell-clock
haskell-colour haskell-hint haskell-hosc haskell-mwc-random haskell-network
haskell-primitive haskell-random haskell-vector supercollider)
makedepends=(uusi)
checkdepends=(haskell-microspec)
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=(tidalcycles)
replaces=(tidalcycles)
source=($pkgname-$pkgver.tar.gz::https://github.com/tidalcycles/$_name/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('e29622009bbe1e527d3df16cc78fc03fdc33a1c35e230f4138cc54eb226bf36dee998250241227eab89ca8b2c991e6f801de33871848a0b1b10d7e7f6a72ee60')
b2sums=('102266e464f48195421b08e53f78b506d3b9be41a10e0c6c922d84adac4dce0fd3bee8f8080df82c8ede3346a924dab7adb0785b11a82db17fc5f2e3551e0eb9')

prepare() {
  uusi -u hosc $_name-$pkgver/tidal.cabal
}

build() {
  cd $_name-$pkgver
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
                             --datasubdir=$pkgname \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
                             --ghc-option=-fllvm
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_name-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_name-$pkgver
  runhaskell Setup copy --destdir="$pkgdir"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
