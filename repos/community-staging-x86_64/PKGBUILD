# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cborg
pkgname=haskell-cborg
pkgver=0.2.7.0
pkgrel=10
pkgdesc="Concise Binary Object Representation (CBOR)"
url="https://github.com/well-typed/cborg"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-half' 'haskell-primitive')
makedepends=('ghc' 'haskell-aeson' 'haskell-base-orphans' 'haskell-base16-bytestring'
             'haskell-base64-bytestring' 'haskell-quickcheck' 'haskell-scientific' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('f034bb498a1e7d69dd3587b804f076afdc87d98af494da18385b8ac4271e9257e51c3ff4504bd208e1b856c19558c35f27137adced681dc846b9489892453306')

prepare() {
    cd $_hkgname-$pkgver
    # https://github.com/well-typed/cborg/issues/290
    sed -i 's/hiding (copyByteArrayToPtr)/hiding (copyByteArrayToPtr, copyPtrToMutableByteArray)/' src/Codec/CBOR/Magic.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.txt
}
