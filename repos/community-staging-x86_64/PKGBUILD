# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Contributor: Alexander Diana <alexander@rouk.org>

pkgbase=python-bcrypt
pkgname=("python-bcrypt" "python2-bcrypt")
pkgver=3.1.4
pkgrel=2
pkgdesc="Modern password hashing for your software and your servers"
arch=('x86_64')
url="https://github.com/pyca/bcrypt"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi'
             'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pyca/bcrypt/archive/$pkgver.tar.gz")
sha512sums=('f4b18095ee1ea09a4a1ae4d970353d4743b84e8b2637132857339febc8fd25697359c8a3308578db623ee0c900a5711a693d9bfd21625bf93c1e6437bfb24f6c')

prepare() {
  cp -a "$srcdir"/bcrypt-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/bcrypt-$pkgver
  python setup.py build

  cd "$srcdir"/bcrypt-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/bcrypt-$pkgver
  python setup.py pytest

  cd "$srcdir"/bcrypt-$pkgver-py2
  python2 setup.py pytest
}

package_python-bcrypt() {
  depends=('python-cffi' 'python-six')

  cd bcrypt-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 "$srcdir"/bcrypt-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}

package_python2-bcrypt() {
  depends=('python2-cffi' 'python2-six')

  cd bcrypt-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 "$srcdir"/bcrypt-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
