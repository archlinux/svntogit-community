# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hjsonpointer
pkgname=haskell-hjsonpointer
pkgver=1.3.0
pkgrel=2
pkgdesc="JSON Pointer library"
url="https://github.com/haskell-foundation/foundation"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-hashable' 'haskell-quickcheck'
         'haskell-unordered-containers' 'haskell-semigroups' 'haskell-text' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-http-types')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('44fad6d263a8455cf6230e7e2ad8607a98e4368be3ddeeef538043ba205f4efad545338faee931283fe7e61732908347e017d9708b07192840206d665e5e7e5c')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "MIT-LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/MIT-LICENSE.txt"
}
