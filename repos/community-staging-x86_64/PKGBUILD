# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-crossref
pkgver=0.3.14.0
pkgrel=3
pkgdesc="Pandoc filter for cross-references"
url="https://hackage.haskell.org/package/${pkgname}"
license=("GPL2")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-gitrev' 'haskell-microlens'
         'haskell-microlens-mtl' 'haskell-microlens-th' 'haskell-open-browser'
         'haskell-optparse-applicative' 'pandoc' 'haskell-pandoc-types' 'haskell-syb'
         'haskell-utility-ht')
makedepends=('ghc' 'haskell-hspec')
conflicts=('haskell-pandoc-crossref')
replaces=('haskell-pandoc-crossref')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lierdakil/pandoc-crossref/archive/v$pkgver.tar.gz")
sha512sums=('c456fbc20b835c2520ed201db3cf9bd6c7c4d2d2e469b77766c237025ec357cb7426a7437819e037444319d2aa8e2706c5d1bf5619a829d0b72de60def391da6')

build() {
  cd $pkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    -f-enable_flaky_tests
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="${pkgdir}"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
