# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sigil
pkgver=1.9.30
pkgrel=3
pkgdesc='multi-platform EPUB2/EPUB3 ebook editor'
arch=('x86_64')
url=https://sigil-ebook.com
_url=https://github.com/Sigil-Ebook/Sigil
license=(GPL3)
depends=(hicolor-icon-theme
         hunspell
         mathjax2
         minizip
         python-css-parser
         python-dulwich
         python-lxml
         qt5-svg
         qt5-webengine)
makedepends=(cmake
             qt5-tools)
optdepends=('hunspell-en_US: for English dictionary support'
            'hyphen-en: for English hyphenation support in plugins'
            'pageedit: external editor to replace BookView'
            'python-html5lib: recommended for plugins'
            'python-chardet: recommended for plugins'
            'python-cssselect: recommended for plugins'
            'python-pillow: recommended for plugins'
            'python-regex: recommended for plugins'
            'python-pyqt5: recommended for plugins'
            'python-pyqt5-webengine: recommended for plugins'
            'tk: recommended for plugins')
_archive="${pkgname^}-$pkgver"
source=("$_url/archive/$pkgver/$_archive.tar.gz"
        "$_url/releases/download/${pkgver}/$_archive.tar.gz.sig"
        "$_url/commit/8e21405f5d2dfdc7da60f845ae7cf5e2bca6d029.patch") # FS77888
sha256sums=('262c43ec0ce9ea9486fc19074dd0a687a39d9df1829732e9006d8c5fbb242458'
            'SKIP'
            '96fcfa8bdc04d79cb76b05f0bf042e4612b132027486459e4f08c0e5b4d8c29a')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

prepare() {
	cd "$_archive"
	mkdir -p build
	patch -p1 < "../${source[2]##*/}"
}

build() {
	cd "$_archive/build"
	cmake -G "Unix Makefiles" \
		-D USE_SYSTEM_LIBS=1 \
		-D SYSTEM_LIBS_REQUIRED=1 \
		-D INSTALL_BUNDLED_DICTS=0 \
		-D INSTALL_HICOLOR_ICONS=1 \
		-D DISABLE_UPDATE_CHECK=1 \
		-D MATHJAX_DIR=/usr/share/mathjax2 \
		-D CMAKE_INSTALL_PREFIX=/usr \
		-D CMAKE_INSTALL_LIBDIR=lib \
		-D CMAKE_C_FLAGS="$CFLAGS" \
		-D CMAKE_CXX_FLAGS="$CXXFLAGS" \
		-D CMAKE_SKIP_RPATH=ON ..
	make
}

package() {
	cd "$_archive/build"
	make install DESTDIR="$pkgdir"
	python -m compileall "$pkgdir/usr/share/sigil/"{plugin_launchers/python/,python3lib}
	python -O -m compileall "$pkgdir/usr/share/sigil/"{plugin_launchers/python/,python3lib}
}
