# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=invariant
pkgname=haskell-invariant
pkgver=0.5
pkgrel=2
pkgdesc="Haskell98 invariant functors"
url="https://github.com/nfrisby/invariant-functors"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-comonad' 'haskell-contravariant'
         'haskell-profunctors' 'haskell-statevar' 'haskell-tagged' 'haskell-th-abstraction'
         'haskell-transformers-compat' 'haskell-unordered-containers')
makedepends=('ghc' 'haskell-hspec' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ba54f52c29f276f5240eade00c9621c14a6b3fad6a2da0cbddb732bb6b85b4e9ac60a115515ccce9ef5a7a56fd2ba94daae1a7db8741d0c63cd64e9d64fd2ea3')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/semigroups/d' -e 's/<.*2.13/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
