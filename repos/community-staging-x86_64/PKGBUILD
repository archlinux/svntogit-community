# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=casa-types
pkgname=haskell-casa-types
pkgver=0.0.0.20191219
_commit=3dee7dc1594e4d0ea2b101ad933ce23b4bc3c451
pkgrel=17
pkgdesc="Content Addressable Storage Archive types"
url="https://github.com/fpco/casa"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-hashable' 'haskell-path-pieces' 'haskell-persistent')
makedepends=('ghc')
source=(casa-$_commit.tar.gz::https://github.com/fpco/casa/archive/$_commit.tar.gz)
sha512sums=('8f8bd2ffbd250de61aeeafa85c3b3921599aa3fef031966d30581d5abf29bff2f3749350c312d10145331fc054642bf184eabcea69be9baad9be226cb5e4991f')

prepare() {
    cd casa-$_commit/$_hkgname
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd casa-$_commit/$_hkgname

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd casa-$_commit/$_hkgname
    runhaskell Setup test
}

package() {
    cd casa-$_commit/$_hkgname

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "../LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
