# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=couchdb
pkgver=3.2.0
pkgrel=2
pkgdesc="Document-oriented database that can be queried and indexed in a MapReduce fashion using JSON"
arch=(x86_64)
url="https://couchdb.apache.org"
license=(Apache)
depends=(icu js78 zlib)
optdepends=('erlang-nox: for weatherreport')
makedepends=(erlang-nox)
backup=('etc/couchdb/local.ini'
        'etc/couchdb/vm.args')
source=("https://www-eu.apache.org/dist/couchdb/source/${pkgver}/apache-couchdb-${pkgver}.tar.gz"{,.asc}
        couchdb.service
        couchdb.sysusers
        couchdb.tmpfiles
        datadirs.ini)
sha256sums=('8bea574faa6bb0926c670542d8318c322268cf7e6614dab318dea734ccf1b00c'
            'SKIP'
            '38f3e489c031b8c6eacd8b9da3ca91362d7929e8f5e3c8b0e4cf5401c67bf7bb'
            '3ed1ad2a37a068ce194b03fb72eb35285d60fa7faf2d2c2bb710703d229108a8'
            '0ce806cbc5e18e60b17be9fd2cdbd4c7f12cc84ca95b079efdede16ddb5f3efd'
            '937ca3498aab47b3f2226d027fa8a1a95de55cbb463373099e28cb9a6c7046ac')
validpgpkeys=(2EC788AE3F239FA13E82D215CDE711289384AE37  # Joan Touzet (CODE SIGNING KEY) <wohali@apache.org>
              D2B17F9DA23C0A10991AF2E3D9EE01E47852AEE4  # Jan Lehnardt <jan@apache.org>
              0BD7A98499C4AB41C910EE65FC04DFBC9657A78E) # Nick Vatamaniuc <vatamane@gmail.com>

prepare() {
  cd apache-couchdb-${pkgver}
  sed -i 's|$ROOTDIR/etc/vm.args|/etc/couchdb/vm.args|' rel/files/couchdb.in
}

build() {
  cd apache-couchdb-${pkgver}
  ./configure --spidermonkey-version 78
  make release
}

package() {
  cd apache-couchdb-${pkgver}
  install -dm755 "${pkgdir}"/usr/lib/
  install -dm755 "${pkgdir}"/etc/couchdb/

  cp -r rel/couchdb "${pkgdir}"/usr/lib/couchdb
  mv "${pkgdir}"/usr/lib/couchdb/etc/local.ini "${pkgdir}"/etc/couchdb/local.ini
  mv "${pkgdir}"/usr/lib/couchdb/etc/vm.args "${pkgdir}"/etc/couchdb/vm.args

  cd "${srcdir}"
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
  install -Dm644 datadirs.ini -t "${pkgdir}"/usr/lib/couchdb/etc/
}
