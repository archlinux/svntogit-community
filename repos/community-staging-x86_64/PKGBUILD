# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http2
pkgname=haskell-http2
pkgver=3.0.2
pkgrel=38
pkgdesc="HTTP/2 library"
url="https://github.com/kazu-yamamoto/http2"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-case-insensitive' 'haskell-http-types' 'haskell-network'
         'haskell-network-byte-order' 'haskell-psqueues' 'haskell-time-manager'
         'haskell-unix-time')
makedepends=('ghc' 'haskell-async' 'haskell-aeson' 'haskell-aeson-pretty'
             'haskell-base16-bytestring' 'haskell-cryptonite' 'haskell-glob' 'haskell-hspec'
             'haskell-hspec-discover' 'haskell-network-run' 'haskell-typed-process'
             'haskell-unordered-containers' 'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1dd6e12904b11964d3c3c4ee4767e6dfd0edff87a7f2396a4f7e680d672a52a3ee478b5eea5f5ac0840a9f2cffae38fe4ceb13f660f51e5d87603f90f626c39c')

prepare() {
    cd $_hkgname-$pkgver
    # Choose another port to bind, 8080 is too easy to conflict with build host
    sed -i 's/"8080"/"59824"/' test2/ServerSpec.hs test/HTTP2/ServerSpec.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-devel
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
