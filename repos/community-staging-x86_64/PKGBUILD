# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openshadinglanguage
pkgver=1.10.9
pkgrel=4
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'python' 'llvm' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz
        0001-Allow-OSL-to-compile-with-LLVM-10.x-1128.patch)
sha512sums=('aed137bfed55dad86087f39c67c03a62a5c61495cb3b20395c8b6b86463a2c23e78fa502970274960522cd7d2dd5ae6fefe84b1f7fe932cc63c2bcb8772d96de'
            '23c9230915ac40dfa4567807a0b0d54e2cfb3ddeb4b104d765e00f69e863b25c396494e714a0e416f358233e63c86fea7f01edc8202d13f07104ad6c901e3af7')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver
  mkdir src/build
  patch -Np1 -i ../0001-Allow-OSL-to-compile-with-LLVM-10.x-1128.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=14 \
    -DLLVM_STATIC=0 \
    -DENABLERTTI=ON \
    -DSTOP_ON_WARNING=OFF
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install
  mkdir "$pkgdir"/usr/share/OSL
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders

  install -Dm644 "$pkgdir"/usr/share/doc/OSL/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
