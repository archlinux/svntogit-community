# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant
pkgname=haskell-servant
pkgver=0.16
pkgrel=7
pkgdesc="A family of combinators for defining webservices APIs"
url="http://haskell-servant.readthedocs.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-aeson' 'haskell-attoparsec'
         'haskell-bifunctors' 'haskell-case-insensitive' 'haskell-http-api-data'
         'haskell-http-media' 'haskell-http-types' 'haskell-mmorph' 'haskell-network-uri'
         'haskell-quickcheck' 'haskell-tagged' 'haskell-singleton-bool'
         'haskell-string-conversions' 'haskell-vault')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-hspec' 'haskell-quickcheck-instances'
             'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c6052295085064f06e892c1172a273fff28487e061107775eb8cfe83d044ef3705c6f83997d191a30256c6d83afc438ee4795d6f14f616369f02aff809ccfa22')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
