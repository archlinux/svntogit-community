# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Jan Hicken <firstname [dot] lastname [at] posteo [dot] de>

pkgname=networkmanager-strongswan
_pkgname=NetworkManager-strongswan
pkgver=1.4.5
pkgrel=2
pkgdesc="Strongswan NetworkManager plugin"
arch=('x86_64')
url="https://wiki.strongswan.org/projects/strongswan/wiki/NetworkManager"
license=('GPL')
depends=('libnm' 'libsecret' 'strongswan>=5.5.1')
makedepends=('libnma' 'intltool')
optdepends=('libnma: GUI support')
source=("https://download.strongswan.org/NetworkManager/$_pkgname-$pkgver.tar.bz2"{,.sig})
sha512sums=('b1c8958ec40065ec251f625ac69707d3e86e2d3b0466bd33a23edbdabefa952582ee066fc7e61bfd5921ed8340a7233353a219cff50fdc279a67c8269920cb86'
            'SKIP')
validpgpkeys=('12538F8F689B5F1F15F07BE1765FE26C6B467584') # Tobias Brunner

build() {
  cd "$_pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --disable-more-warnings \
    --without-libnm-glib --with-charon=/usr/lib/strongswan/charon-nm
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$_pkgname-$pkgver"
  make check
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
