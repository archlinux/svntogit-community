# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=openpgp-asciiarmor
pkgname=haskell-openpgp-asciiarmor
pkgver=0.1.2
pkgrel=214
pkgdesc="OpenPGP (RFC4880) ASCII Armor codec"
url="http://floss.scru.org/openpgp-asciiarmor"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base64-bytestring')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8dc41995d2a8db54304c558efd7ab083f519be35908fc9d69c980ce943c5c1de89e0476bcc275d10ae1bdb1d4c1bc838f1aff5d5ed108c780d29ac7f786f2f05')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
