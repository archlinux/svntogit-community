# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hledger-lib
pkgname=haskell-hledger-lib
pkgver=1.22.2
pkgrel=24
pkgdesc="A reusable library providing the core functionality of hledger"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-ansi-terminal'
         'haskell-base-compat-batteries' 'haskell-blaze-markup' 'haskell-call-stack'
         'haskell-cassava' 'haskell-cassava-megaparsec' 'haskell-cmdargs' 'haskell-data-default'
         'haskell-decimal' 'haskell-extra' 'haskell-file-embed' 'haskell-glob'
         'haskell-hashtables' 'haskell-megaparsec' 'haskell-old-time' 'haskell-parser-combinators'
         'haskell-pretty-simple' 'haskell-regex-tdfa' 'haskell-safe' 'haskell-tabular'
         'haskell-tasty' 'haskell-tasty-hunit' 'haskell-timeit' 'haskell-uglymemo'
         'haskell-unordered-containers' 'haskell-utf8-string')
makedepends=('ghc' 'uusi' 'haskell-doctest')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('5536f118c5dfbcae1e5da2ae8f9bc80f7425b4f8ad4ebfd70d1b52a9fd5486e810f5bfd8b89fe741b5fddc79216dcf9cb4923cbd1db0d986490bea0cc662dc67')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u megaparsec $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
