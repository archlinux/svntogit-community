# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.4.3
pkgrel=2
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://github.com/elementary/terminal'
license=('GPL3')
depends=('glib2' 'glibc' 'gtk3' 'pango' 'vte3'
         'libgranite.so')
makedepends=('cmake' 'intltool' 'vala')
groups=('pantheon')
source=("pantheon-terminal-${pkgver}.tar.gz::https://github.com/elementary/terminal/archive/${pkgver}.tar.gz")
sha256sums=('b529d367b809e0d344795a68278bc430b5bcd9160103cd920928eaf0cdeece55')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../terminal-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
