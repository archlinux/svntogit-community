# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=keys
pkgname=haskell-keys
pkgver=3.12.3
pkgrel=254
pkgdesc="Keyed functors and containers"
url="https://github.com/ekmett/keys/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-comonad' 'haskell-free' 'haskell-hashable' 'haskell-semigroupoids'
         'haskell-tagged' 'haskell-transformers-compat' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e0bab964eb38388dfb2fe7dcb4999da0263a2162f171f7ed629a2e1de33eb1cb99dc37a6fe32045f136466cebb5aed69e533417256877316c81a02abc700c28d')

prepare() {
    cd $_hkgname-$pkgver
    uusi -d semigroups -u hashable $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
