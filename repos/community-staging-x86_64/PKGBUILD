# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glirc
pkgver=2.39.0.1
pkgrel=45
pkgdesc="Console IRC client"
url="https://github.com/glguy/irc-core"
license=("ISC")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hsopenssl' 'haskell-async' 'haskell-attoparsec'
         'haskell-base64-bytestring' 'haskell-config-schema' 'haskell-config-value'
         'haskell-curve25519' 'haskell-free' 'haskell-githash' 'haskell-hashable' 'haskell-hookup'
         'haskell-irc-core' 'haskell-kan-extensions' 'haskell-lens' 'haskell-network'
         'haskell-psqueues' 'haskell-random' 'haskell-regex-tdfa' 'haskell-split'
         'haskell-unordered-containers' 'haskell-vector' 'haskell-vty')
makedepends=('ghc' 'uusi' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('99401ae7d75a5315848af3586818d481bfd6ab001390f2fd9f663b39c8e25e49')

prepare() {
  cd $pkgname-$pkgver
  uusi -u config-schema -u vector -u vty $pkgname.cabal
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
