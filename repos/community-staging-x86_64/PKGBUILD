# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pynormaliz
pkgname=(python2-pynormaliz python-pynormaliz)
pkgver=1.17
pkgrel=2
pkgdesc="A Python interface to Normaliz"
arch=(x86_64)
url="https://github.com/sebasguts/PyNormaliz"
license=(GPL2)
makedepends=(python-setuptools python2-setuptools normaliz boost)
source=("https://pypi.io/packages/source/P/PyNormaliz/PyNormaliz-$pkgver.tar.gz")
sha256sums=('17d11c44c65a4e1ae9ee642eb8a4db1454d6f3b3c4ea08ff263106ab245cee72')

prepare() {
  cp -r PyNormaliz-$pkgver PyNormaliz-$pkgver-py2
}

build() {
  cd PyNormaliz-$pkgver
  python setup.py build

  cd ../PyNormaliz-$pkgver-py2
  python2 setup.py build
}

package_python2-pynormaliz() {
  depends=(python2 normaliz)
  cd PyNormaliz-$pkgver-py2

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python-pynormaliz() {
  depends=(python normaliz)
  cd PyNormaliz-$pkgver
  
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

