# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=exim
pkgver=4.80.1
pkgrel=4
pkgdesc="Message Transfer Agent"
arch=('x86_64' 'i686')
url='http://www.exim.org/'
license=('GPL')
backup=(etc/mail/aliases etc/mail/exim.conf etc/logrotate.d/exim)
install=exim.install
depends=('db' 'pcre' 'pam' 'openssl' 'libldap')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
options=('!makeflags')
source=("http://mirror.switch.ch/ftp/mirror/exim/exim/exim4/exim-$pkgver.tar.bz2"
        aliases
        exim.logrotate
        exim.Makefile
        exim-submission@.service
        exim.service
        exim@.service
        exim.socket
        exim-submission.socket)
sha256sums=('9565b10f06be224fd03adafae2e07e6fdbb479f8873e3894ddb13f98eeebe78f'
            '932c9149b6809c70e94c1256e28325d197bbf80d27322793e217d4b692d49c5a'
            '7f1408f9c5d905968e665941f5c5efcf9da53e7a0bbef6c66220343bc2ae994b'
            'd33b8595e92ec812c9f6961f98e50b6a3986d5bd966ed4efec2c52c1fce47636'
            '55719e8b823702b0054584326ca28349b7487e5be9e567ddc7a6e6259767e05b'
            '652250d99479b58b7127ff60c29f28ca09ebac09cab7e54771d62ce38407012d'
            'faad96dadbb2750faa5652d830f10d5a14be487a42e8db1cec797164acf14b73'
            '3e3d8b6be2741d2587a496196c08b3f2ffa05b5803b2bf9fb49359cef3a98d26'
            'd3bb58f0fbeaaa33c812a823708664bbcd828da7d24e2a098f84a15aee443fee')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cp "$srcdir/$pkgname.Makefile" Local/Makefile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0644 $srcdir/exim.logrotate "${pkgdir}/etc/logrotate.d/exim"
  install -Dm0644 doc/exim.8 "${pkgdir}/usr/share/man/man8/exim.8"

  mkdir -p "${pkgdir}/var/spool/exim/db" "${pkgdir}/etc/mail" \
    "${pkgdir}/var/log/exim" "${pkgdir}/usr"/{lib,bin}

  chmod 770 "${pkgdir}/var/spool/exim" "${pkgdir}/var/spool/exim/db" "${pkgdir}/var/log/exim"

  cd build-Linux-*
  for i in exicyclog exim_checkaccess exim_dumpdb exim_lock exim_tidydb exipick exiqsumm exigrep \
    exim_dbmbuild exim exim_fixdb eximstats exinext exiqgrep exiwhat; do
    install -m0755 "$i" "$pkgdir/usr/bin"
  done

  cd "$srcdir/exim-$pkgver/src"
  sed -e "s|/etc/aliases|/etc/mail/aliases|g" -e "s|SYSTEM_ALIASES_FILE|/etc/mail/aliases|g" \
    configure.default > "$pkgdir/etc/mail/exim.conf"

  cp "$srcdir/aliases" "$pkgdir/etc/mail"

  cd "$pkgdir/usr/bin"
  for i in mailq rmail rsmtp runq sendmail; do
    ln -s exim "$i"
  done

  # fhs compliancy
  ln -s ../bin/exim ../lib/sendmail

  install -Dm0644 "$srcdir/exim-submission@.service" \
    "${pkgdir}/usr/lib/systemd/system/exim-submission@.service"
  install -Dm0644 "$srcdir/exim.service" "${pkgdir}/usr/lib/systemd/system/exim.service"
  install -Dm0644 "$srcdir/exim@.service" "${pkgdir}/usr/lib/systemd/system/exim@.service"
  install -Dm0644 "$srcdir/exim.socket" "${pkgdir}/usr/lib/systemd/system/exim.socket"
  install -Dm0644 "$srcdir/exim-submission.socket" \
    "${pkgdir}/usr/lib/systemd/system/exim-submission.socket"
}
