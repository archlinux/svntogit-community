# $Id$
# CPAN Name  : Term-ReadLine-Gnu
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: AUR Perl <aurperl@juster.info>
# Generator  : CPANPLUS::Dist::Arch 1.15

pkgname=perl-term-readline-gnu
pkgver=1.31
pkgrel=2
pkgdesc="GNU Readline XS library wrapper"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.7')
url='http://search.cpan.org/dist/Term-ReadLine-Gnu'
source=("http://search.cpan.org/CPAN/authors/id/H/HA/HAYASHI/Term-ReadLine-Gnu-${pkgver}.tar.gz"
        'termcap-bad-ncurses-good.patch')
md5sums=('1f659d9ca65db69391c59f52a876a52d'
         'a000706b89792f822b5ec20baa370910')

build() {
   export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/Term-ReadLine-Gnu-$pkgver"
    patch --forward -p1 < "${srcdir}/termcap-bad-ncurses-good.patch"
    /usr/bin/perl Makefile.PL
    make
}

check() {
  cd "${srcdir}/Term-ReadLine-Gnu-$pkgver"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
}

package() {
  cd "${srcdir}/Term-ReadLine-Gnu-$pkgver"
  make pure_install PERL_INSTALL_ROOT="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

