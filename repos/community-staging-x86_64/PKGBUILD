# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=polymake
pkgver=4.2
pkgrel=1
_perlver=`perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]).".".($v->[2]);'`
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt
         perl-term-readline-gnu perl-term-readkey perl-json sympol perl=$_perlver)
makedepends=(ninja cddlib ant boost bliss libnsl jdk8-openjdk permlib)
optdepends=('java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-${pkgver/.r/r}-minimal.tar.bz2")
sha256sums=('e9228eda8ae3d94a4c1f869103dd332c18b033ff26edeb19cedac4f513a3f406')

build() {
  cd polymake-$pkgver
  ./configure \
    --prefix=/usr \
    --with-cdd=/usr \
    --with-lrs=/usr \
    --with-permlib=/usr \
    --with-sympol=/usr
  make
}

package() {
  cd polymake-$pkgver
  make DESTDIR="$pkgdir" install
}
