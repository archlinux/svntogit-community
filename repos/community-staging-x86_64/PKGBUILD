# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=retrie
pkgname=haskell-retrie
pkgver=1.0.0.0
pkgrel=27
pkgdesc="A powerful, easy-to-use codemodding tool for Haskell."
url="https://github.com/facebookincubator/retrie"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-async' 'haskell-data-default'
         'haskell-ghc-exactprint' 'haskell-src-exts' 'haskell-optparse-applicative'
         'haskell-random-shuffle' 'haskell-syb' 'haskell-unordered-containers')
makedepends=('ghc' 'haskell-hunit' 'haskell-ghc-paths' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-temporary')
checkdepends=('git' 'mercurial')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('aae30c8d69ac32217aa9daa21cfdc99d285fbf9c413fe0c08f76c2f8b38a5909a49358330242252ce1554af5bfea10c8e4d523efcfb9efdbe2ebc124d40a6afa')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

  rm "$pkgdir"/usr/bin/demo
}
