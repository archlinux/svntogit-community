# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-prelude
pkgname=haskell-clash-prelude
pkgver=1.0.1
pkgrel=12
pkgdesc="CAES Language for Synchronous Hardware - Prelude library"
url="https://github.com/clash-lang/clash-prelude"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bifunctors' 'haskell-constraints' 'haskell-data-binary-ieee754'
         'haskell-data-default-class' 'haskell-ghc-typelits-extra' 'haskell-ghc-typelits-knownnat'
         'haskell-ghc-typelits-natnormalise' 'haskell-half' 'haskell-hashable' 'haskell-lens'
         'haskell-quickcheck' 'haskell-reflection' 'haskell-singletons' 'haskell-th-lift'
         'haskell-th-orphans' 'haskell-type-errors' 'haskell-vector')
makedepends=('ghc' 'haskell-doctest' 'haskell-hint' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('9edf5775b114e00d62e4a9330494ff367d60c8162c68e0a1b7a92867dbb0310336bf684ceb560f6681742a734086ad728cea8e452a97217d4fc4e830d8dcfc92')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/0.16.1/0.16.0/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # TODO
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
