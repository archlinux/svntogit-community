# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=yesod-test
pkgname=haskell-yesod-test
pkgver=1.6.7
pkgrel=2
pkgdesc="Integration testing for WAI/Yesod Applications"
url="https://www.yesodweb.com"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-attoparsec' 'haskell-blaze-builder'
         'haskell-blaze-html' 'haskell-case-insensitive' 'haskell-conduit'
         'haskell-cookie' 'haskell-hspec-core' 'haskell-html-conduit' 'haskell-http-types'
         'haskell-memory' 'haskell-network' 'haskell-pretty-show' 'haskell-wai'
         'haskell-wai-extra' 'haskell-xml-conduit' 'haskell-xml-types' 'haskell-yesod-core')
makedepends=('ghc' 'haskell-yesod-form' 'haskell-hspec' 'haskell-unliftio')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('7aaef5d2e6885a83bd7c346722bbc8ff2bdb0de26e55151140586f68c3d44dc8905e5493ee5e7fe6e3e15121fb0a93a5cb7dc10e4b6380c2afb8e23a27486466')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
