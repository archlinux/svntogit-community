# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=config-ini
pkgname=haskell-config-ini
pkgver=0.2.2.0
pkgrel=75
pkgdesc="A library for simple INI-based configuration files."
url="https://github.com/aisamanra/config-ini"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-unordered-containers' 'haskell-megaparsec')
makedepends=('ghc' 'haskell-ini' 'haskell-hedgehog' 'haskell-doctest' 'haskell-microlens')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('901745b9e6f2eb5d6458c8702a4a112f115448a89152eeec6610096ffb82577f132f0ab748835a9a55c293840674652791a54c746289fd718f37bd41ea3b4ed1')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-enable-doctests
    # https://github.com/aisamanra/config-ini/issues/12
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
