# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=ffms2
pkgver=2.23.1
pkgrel=4
pkgdesc='A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access'
arch=('x86_64')
url='https://github.com/FFMS/ffms2'
license=('GPL')
depends=('ffmpeg')
makedepends=('git')
provides=('vapoursynth-plugin-ffms2'
          'libffms2.so')
source=("ffms2-${pkgver}.tar.gz::https://github.com/FFMS/ffms2/archive/${pkgver%.*}.tar.gz")
sha256sums=('b09b2aa2b1c6f87f94a0a0dd8284b3c791cbe77f0f3df57af99ddebcd15273ed')

build() {
  cd ffms2-${pkgver%.*}

  ./configure \
    --prefix='/usr' \
    --enable-avresample \
    --enable-shared \
    --disable-static
  make
}

package() {
  cd ffms2-${pkgver%.*}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/vapoursynth
  ln -s ../libffms2.so "${pkgdir}"/usr/lib/vapoursynth/
}

# vim: ts=2 sw=2 et:
