# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=1.2.0
pkgrel=4
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('x86_64')
url="https://performous.org/"
license=('GPL')
depends=('boost-libs' 'libepoxy' 'libxml++2.6' 'portaudio' 'portmidi' 'librsvg' 'ffmpeg' 'opencv' 'sdl2' 'aubio')
makedepends=('cmake' 'pkgconfig' 'help2man' 'boost' 'mesa' 'git' 'glm')
optdepends=('performous-freesongs: free songs for performous')
source=($pkgname-$pkgver.tar.gz::https://github.com/performous/performous/archive/refs/tags/${pkgver}.tar.gz
        ffmpeg5.patch)
sha512sums=('a8fbbe3768f46915eacb65adcc1592295e4c6a84775ca55def01ebcff2218b1860daee0d4d5fcccc407d8df71da1ac25da667b47dd4a17239041a3d5fbfa1c56'
            '4affb318c7fd6ac65ea4facfb2c5270ed6ec1816de69f1e77204c036cfb1c8cd548bb3e2e28bcd6169d531183fc8799669a86de95f4325e0b58254e596a0d5dc')

prepare() {
  cd performous-$pkgver
  patch -p1 -i ../ffmpeg5.patch # Fix build with FFMpeg 5
}

build() {
  cd performous-${pkgver}

  mkdir -p build
  cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  make
}

package() {
  cd performous-${pkgver}/build

  make DESTDIR="$pkgdir" install
}
