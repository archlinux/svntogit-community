# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Co-Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-prctl
pkgver=1.8.1
pkgrel=2
pkgdesc='Python(ic) interface to the linux prctl syscall'
arch=('x86_64')
license=('GPL')
url='https://github.com/seveas/python-prctl'
depends=('python' 'libcap')
makedepends=('python-setuptools')
checkdepends=('iputils' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/seveas/python-prctl/archive/v$pkgver.tar.gz"
        "$pkgname-kernel-5.10.patch::https://github.com/seveas/python-prctl/pull/33.patch"
        "skip-tests.diff"
        "python310.diff")
sha512sums=('a251b0aefbd6a16d69fd664cadf87ba16af87e7551ae474846858876007028c13b619e227518688e876f95726d327eb4793c37a34317ec8a1bf2c6ea02a31b21'
            '839bd732ebe4318dd11a2f0037fd60e6b79de84374ef575278cd787eac87c19b00290f5871e70ebb14301fb8a7eb2dc718a754290285cc6f2e9abe6ef161b802'
            'd7aa5bf2a1854892d2a62fd78f713e6a9df5ff7b6d44a676316970dba348590151d6db5e0583d039db816d361793c8d2f143836bd4113f881e823c741f462bed'
            '31095f7d60cce5b5e8800e7b06e464b9149a114cd544864825b7f4bfb14892bfbbf212923c8f0c687446dd78bcb2109f6f3f08351fe0885a45dad8982b7e57d4')

prepare() {
  cd python-prctl-$pkgver
  patch -Np1 -i ../$pkgname-kernel-5.10.patch
  patch -Np1 -i ../skip-tests.diff
  patch -Np1 -i ../python310.diff
}

build() {
  cd python-prctl-$pkgver
  python setup.py build
}

check() {
  cd python-prctl-$pkgver
  python -m unittest discover
}

package() {
  cd python-prctl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
