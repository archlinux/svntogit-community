# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=freeradius
pkgver=3.0.15
pkgrel=5
pkgdesc='The premier open source RADIUS server'
arch=('x86_64')
url="https://freeradius.org/"
license=('GPL')
depends=('krb5' 'pth' 'net-snmp' 'postgresql-libs' 'libmariadbclient' 'talloc' 'libpcap')
makedepends=('unixodbc' 'python2' 'curl' 'json-c')
optdepends=('unixodbc: for ODBC'
	    'python2: for Python'
	    'json-c: rlm_rest module'
	    'curl: for REST')
options=('!makeflags')
validpgpkeys=('BF2384EC6938B9744B03E2A620E37C25995B4F85') # FreeRADIUS - Package Signing [...] <packages@freeradius.org>
source=("ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2"{,.sig}
	'freeradius-sysusers.conf'
	'freeradius-tmpfiles.conf'
	'freeradius.service')
sha256sums=('23267d8505e7b2909f5bdbf3938ca077c1fe122290dc969304d4f3b594f7e3ba'
            'SKIP'
            '573c05df429405d8647f5b52f68f7ad13bf7392314d30154df770ab170d793f8'
            'f536a9aa972e3e42a6b1a6d8ee17166eb721c7cba2e80f60473811497c7bd8bc'
            'c469e1a3f9edad769da01c324779babe783ee85a9b53ce4638d3d2d09c7c8d4b')

build() {
  cd "$srcdir"/freeradius-server-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib/freeradius \
    --localstatedir=/var \
    --enable-heimdal-krb5 \
    --with-system-libltdl \
    --with-system-libtool \
    --with-udpfromto
  make
}

package() {
  cd "$srcdir"/freeradius-server-$pkgver

  make install R="$pkgdir"
  chmod o+r "$pkgdir"/etc/raddb/*
  mv "$pkgdir"/etc/raddb "$pkgdir"/etc/raddb.default
  rm -rf "$pkgdir"/var/run

  install -D -m0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -D -m0644 "$srcdir"/$pkgname-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -D -m0644 "$srcdir"/$pkgname-tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf
}
