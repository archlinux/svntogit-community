# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eclib
pkgver=20180710
pkgrel=4
pkgdesc="Includes mwrank (for 2-descent on elliptic curves over Q) and modular symbol code used to create the elliptic curve database"
arch=(x86_64)
url="https://github.com/JohnCremona/eclib/"
license=(GPL)
depends=(flint pari boost-libs)
makedepends=(boost)
source=("https://github.com/JohnCremona/eclib/archive/v$pkgver.tar.gz"
        eclib-types.h.patch::"https://github.com/JohnCremona/eclib/commit/c9b96429.patch")
sha256sums=('176840c9dbed6156f291da3c877ce680f746acf0dcd1b952174f1e046f28b71d'
            '32ff367b173fce44cf9b2f6a79b29fe13f00f4fcdbf4904115b9ea30d57ec28e')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../eclib-types.h.patch # Add missing include
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  LDFLAGS+=' -lpthread' ./configure --prefix=/usr --with-flint=/usr --with-boost
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}

