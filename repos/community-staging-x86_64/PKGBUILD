# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=what4
pkgname=haskell-what4
pkgver=1.2
pkgrel=1
pkgdesc="Solver-agnostic symbolic values support for issuing queries"
url="https://github.com/GaloisInc/what4"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-bimap' 'haskell-bifunctors' 'haskell-bv-sized'
         'haskell-config-value' 'haskell-deriving-compat' 'haskell-data-binary-ieee754'
         'haskell-extra' 'haskell-fingertree' 'haskell-hashable' 'haskell-hashtables'
         'haskell-io-streams' 'haskell-lens' 'haskell-libbf' 'haskell-panic'
         'haskell-parameterized-utils' 'haskell-prettyprinter' 'haskell-scientific'
         'haskell-temporary' 'haskell-th-abstraction' 'haskell-th-lift' 'haskell-th-lift-instances'
         'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector' 'haskell-versions'
         'haskell-zenc')
makedepends=('ghc' 'uusi' 'haskell-contravariant' 'haskell-exceptions' 'haskell-hedgehog'
             'haskell-lumberjack' 'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-checklist'
             'haskell-tasty-hedgehog' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-tasty-sugar')
checkdepends=('cvc4' 'yices' 'z3')
optdepends=('cvc4' 'yices' 'z3')
# https://github.com/GaloisInc/what4/pull/135
#source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
source=(https://github.com/GaloisInc/what4/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('c2b29b7cf0d78f606f5cebebf9661b5863e6dc926724ce073757705754fa67b39422659d27dc806101988e6b373dcea71b8f4525e403983cda011f66de7877d1')

prepare() {
    cd $_hkgname-$pkgver/$_hkgname
    gen-setup
}

build() {
    cd $_hkgname-$pkgver/$_hkgname
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver/$_hkgname
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver/$_hkgname
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
