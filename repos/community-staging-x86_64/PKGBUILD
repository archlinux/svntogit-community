# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-ant-xml
pkgname=haskell-tasty-ant-xml
pkgver=1.1.8
pkgrel=110
pkgdesc="Render tasty output to XML for Jenkins"
url="https://github.com/ocharles/tasty-ant-xml"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-generic-deriving' 'haskell-tagged' 'haskell-tasty' 'haskell-xml')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8cbe0648c42e2e8e324ef474ee7da1dde4fbfc3942a988a6835cb9ffca5fbaa8ac501e305389b4125543e48c1ade9caf5559c91c69874bba7fefc9f9be9cc3f9')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
