# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.5.1
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=(
  sh
  capstone libcapstone.so
  openssl
  libzip libzip.so
  zlib
  lz4
  xz
  xxhash libxxhash.so
  file libmagic.so
  tree-sitter libtree-sitter.so
  libmspack libmspack.so
)
makedepends=('meson' 'ninja' 'python' 'python-yaml')
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('c856112651bec91bb6e9f5580f60e3168f02a9f194bf567186a66398c162f59953ca8c3bf43441315356ca6ef077b7c56e4150e2dd8a79d4d08c5f789c549596')
b2sums=('31369497d5aa425aa6030ce404baa5d4a10ef2453a93f9b2c9f7d47135b5ffbc61618ef7e743acef3939bca2838a94637b7a1794ff44998608aee3f988301483')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v4 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_lzma=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_sys_libmspack=enabled \
    -D use_zlib=true \
    -D use_lzma=true \
    -D local=disabled
  ninja -C build
}

package() {
  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
}

# vim: ts=2 sw=2 et:
