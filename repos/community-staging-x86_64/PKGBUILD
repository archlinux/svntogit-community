# Maintainer:  Jonathan Steel <jsteel at archlinux.org>
# Contributor: Christian Krause ("wookietreiber") <kizkizzbangbang@googlemail.com>

pkgname=pixz
pkgver=1.0.6
pkgrel=3
pkgdesc="Parallel, indexed xz compressor"
arch=('x86_64')
url="https://github.com/vasi/pixz"
license=('custom')
depends=('libarchive')
source=(https://github.com/vasi/pixz/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('1041ed269aa9ea58f1fb296c4a1e24f6')

build() {
  cd $pkgname-$pkgver

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
