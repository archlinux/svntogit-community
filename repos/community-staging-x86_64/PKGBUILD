# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall-bash
pkgver=1.0.28
pkgrel=13
pkgdesc='Compile Dhall to Bash'
url='https://dhall-lang.org'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'dhall' 'haskell-neat-interpolation' 'haskell-shell-escape'
         'haskell-optparse-generic')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('083b3607f4d186c6059538d73bc206f1093b1cc434bc7f316030785e8e8e9ca58b27ca05b56d1775c413dad42f2a069e1c3878ae35edd14865682b67c103a5ac')

prepare() {
    cd $pkgname-$pkgver
    sed -i 's/< *0.5/<1/' $pkgname.cabal
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
