# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-api
pkgver=1.11.1
pkgrel=11
pkgdesc="Web API server for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-aeson' 'haskell-data-default'
         'haskell-decimal' 'haskell-docopt' 'haskell-either' 'haskell-microlens'
         'haskell-microlens-platform' 'haskell-safe' 'haskell-servant-server'
         'haskell-servant-swagger' 'haskell-swagger2' 'haskell-wai' 'haskell-wai-extra'
         'haskell-warp')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b3483f3120fd3de38cf36e6c374af722e65ba03560c42806e5143088125da5f9993c800fa8aac98cb4db8851f20a6058d581868600187f9de4070951c4a836df')

prepare() {
    cd $pkgname-$pkgver
    sed -i 's/< *4.12/<5/' $pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
