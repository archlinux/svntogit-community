# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=hledger-lib
pkgname=haskell-hledger-lib
pkgver=1.27.1
pkgrel=14
pkgdesc="A reusable library providing the core functionality of hledger"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-ansi-terminal'
         'haskell-blaze-markup' 'haskell-breakpoint' 'haskell-call-stack' 'haskell-cassava'
         'haskell-cassava-megaparsec' 'haskell-cmdargs' 'haskell-data-default' 'haskell-decimal'
         'haskell-doclayout' 'haskell-extra' 'haskell-file-embed' 'haskell-glob'
         'haskell-hashtables' 'haskell-megaparsec' 'haskell-microlens' 'haskell-microlens-th'
         'haskell-parser-combinators' 'haskell-pretty-simple' 'haskell-regex-tdfa' 'haskell-safe'
         'haskell-tabular' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-timeit' 'haskell-uglymemo'
         'haskell-unordered-containers' 'haskell-utf8-string')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('522569af54936ab6c1558f668ace12494d7dd0d7446594bbaacfbf704a32ca67c62bffdc15b269935bf1a25ece93658d3d0b0f1a2b67962cd17d582154e34dcc')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u megaparsec $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
