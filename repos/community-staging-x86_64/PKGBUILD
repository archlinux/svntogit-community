# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.10.12.2
pkgrel=2
pkgdesc="Efficient Arrays"
url="http://code.haskell.org/vector"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=()
# deps adjusted to specific versions in package()
depends=("ghc" "haskell-primitive")
options=('strip' 'staticlibs')
install="${pkgname}.install"
source=("http://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
sha256sums=('205656e9741aad406e427e988dbf1665bf5dbdf3e9d574e309c274be4e06bde5')
sha256sums=('455427a217e98cc20af71906700bf36fed265ac15b7fb21b78a0a61f66380c06')
sha256sums=('455427a217e98cc20af71906700bf36fed265ac15b7fb21b78a0a61f66380c06')

build() {
  cd ${srcdir}/vector-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
  depends=("ghc=${_ghcver}" "haskell-primitive")

  cd ${srcdir}/vector-${pkgver}
  install -Dm744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install -Dm744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
  install -dm755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/vector
  runhaskell Setup copy --destdir=${pkgdir}
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
