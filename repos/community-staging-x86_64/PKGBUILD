# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=time-compat
pkgname=haskell-time-compat
pkgver=1.9.6
pkgrel=9
pkgdesc="Compatibility package for time"
url="https://github.com/phadej/time-compat"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-hashable')
makedepends=('ghc' 'uusi' 'haskell-base-compat' 'haskell-hunit' 'haskell-quickcheck' 'haskell-tagged'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('810b9f73b3e667df48e300613716bfed59dd46fe0278fd46c17b17c1d57fc4516cb3e36e07dc7bb7b3b71991a4fc88fe54982e8ea683c838c69efd3c4b757b8a')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
