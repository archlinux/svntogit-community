# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=strictures
pkgname=perl-${_pkg}
pkgver=2.000006
pkgrel=5
pkgdesc="Turn on strict and make most warnings fatal"
arch=(any)
url="https://metacpan.org/release/${_pkg}"
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
checkdepends=(perl-test-simple)
source=("https://cpan.metacpan.org/authors/id/H/HA/HAARG/${_pkg}-${pkgver}.tar.gz")
sha256sums=('09d57974a6d1b2380c802870fed471108f51170da81458e2751859f2714f8d57')

build() {
    cd ${_pkg}-${pkgver}
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make test
}

package() {
    cd ${_pkg}-${pkgver}
    make INSTALLDIRS=vendor DESTDIR="$pkgdir" install
}
