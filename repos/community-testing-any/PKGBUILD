# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=rz-pipe
pkgname=(python-rzpipe)
pkgver=0.4.0
pkgrel=2
pkgdesc='Access rizin via pipe from any programming language'
url='https://github.com/rizinorg/rz-pipe'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'rizin')
source=(${url}/archive/python-v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('831fdb764f97137764eee50ce4dcec01f06a9ee456e6066bc44bd78fd4178781ac3aa160c6290584b63403a7ea67c4d3ad850661a6466c196d2db2463e91734f')
b2sums=('7aa451a8d04216d86a7f5515c5146b34f689a8e0efcb0975792839f86eca315fc4bc1bfc8ea8736cbdcc3c040bbbcbc080183cbaf38ea0fb89056d3154317969')

build() {
  cd rz-pipe-python-v${pkgver}/python
  python setup.py build
}

package_python-rzpipe() {
  depends=('python' 'rizin')
  cd rz-pipe-python-v${pkgver}/python
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
