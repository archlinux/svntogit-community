# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-notebook
_pipname=notebook
pkgver=6.5.1
pkgrel=1
pkgdesc="The language-agnostic HTML notebook application for Project Jupyter"
arch=('any')
url="https://github.com/jupyter/notebook"
license=('BSD')
depends=('python-jinja' 'python-tornado' 'python-terminado'
         'python-traitlets' 'jupyter-nbformat' 'python-jupyter_core'
         'python-jupyter_client' 'python-send2trash' 'jupyter-nbconvert'
         'python-prometheus_client' 'python-argon2_cffi' 'python-ipython-genutils' 'jupyter-nbclassic')
# Much of their official dependency tree appears inverted and circular?
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-jupyter_packaging' 'python-setuptools')
checkdepends=('python-pytest' 'python-requests')
optdepends=('pandoc: notebook export' 'jupyter-widgetsnbextension: interactive widgets')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz
        https://github.com/jupyter/notebook/commit/ab716106.patch)
md5sums=('6975a28fb97b5b144d5a2b8f89fea617'
         'abfa21489a538078aa285b32351bf04f')

# todo: report bug that mathjax and nbconvert is no longer optional

prepare() {
  patch -d notebook-$pkgver -p1 < ab716106.patch # Compatibility with nbclassic 0.4.6
}

build() {
  cd notebook-$pkgver
  python -m build --wheel --no-isolation
}

#check() {
#  cd notebook-$pkgver
#  pytest -v
#}

package() {
  cd notebook-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 jupyter-notebook.desktop "$pkgdir/usr/share/applications/jupyter-notebook.desktop"
}
