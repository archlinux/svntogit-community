# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-net-openssh
pkgver=0.80
pkgrel=3
pkgdesc='Perl SSH client package implemented on top of OpenSSH'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Net-OpenSSH
source=("https://cpan.metacpan.org/authors/id/S/SA/SALVA/Net-OpenSSH-$pkgver.tar.gz")
md5sums=(1ab4470af03f8c15baa50c8b11f4bd52)
sha512sums=(3b68b73b0d5ee5db2096e0ac3ec3c3f12b92ee1865217a81eff0378f416ffdaac252d4ae4de601da7cd4a771bbab6a4768be4cafa0eaa2ff93b31a3cf824875d)
_ddir="Net-OpenSSH-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
