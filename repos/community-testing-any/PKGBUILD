# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-pyfakefs
pkgver=4.2.1
pkgrel=3
pkgdesc='pyfakefs implements a fake file system that mocks the Python file system modules'
url=https://github.com/jmcgeheeiv/pyfakefs/
license=(APACHE)
arch=(any)
depends=(python-pytest)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-tox
)
_tag=59769a8b3cacd51f2386d37ccf9b3f6cf49b7b67
source=(git+https://github.com/jmcgeheeiv/pyfakefs.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pyfakefs

  git describe --tags | sed 's/^v//'
}

build() {
  cd  pyfakefs

  python setup.py build
}

check() {
  cd  pyfakefs

  tox -e py39
}

package() {
  cd pyfakefs

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
