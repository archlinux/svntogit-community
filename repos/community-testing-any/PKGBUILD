# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcversioner
pkgver=2.16.0.0
pkgrel=7
pkgdesc="Use version control tags to discover version numbers"
url="https://github.com/habnabit/vcversioner"
license=('ISC')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.bz2::https://github.com/habnabit/vcversioner/archive/$pkgver.tar.gz")
sha512sums=('0ce7edf28d0626707f977b565f938a754d374d0cada8683f05fa6d1088930d4d858213dbcb5dba149d828e76341eec4a168ef1998ce9894b72c35c1eef59bd08')

prepare() {
  echo -n "$pkgver-0-UNKNOWN" > vcversioner-$pkgver/version.txt
}

build() {
  cd vcversioner-$pkgver
  python setup.py build
}

check() {
  cd vcversioner-$pkgver
  python setup.py pytest
}

package() {
  cd vcversioner-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
