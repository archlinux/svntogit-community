# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Patrick Ulbrich <zulu99 at gmx . net>
# Contributor: rasmus <rasmus . pank at gmail . com>

pkgname=mailnag
pkgver=2.2.0
pkgrel=6
pkgdesc='Extensible mail notification daemon'
arch=('any')
url='https://github.com/pulb/mailnag'
license=('GPL')
depends=('dbus-python' 'gst-plugins-base' 'gst-python' 'gtk3' 'libnotify' 'libsecret'
         'python-gobject' 'python-pyxdg')
optdepends=('mailnag-goa-plugin: GNOME Online Accounts integration'
            'mailnag-gnome-shell: GNOME Shell integration')
source=("https://github.com/pulb/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('27cff6844f3601cf903505fcd50c2443a7b39720429737583f1b2392bc69a3e9')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
