# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
# https://github.com/spulec/moto/blob/master/CHANGELOG.md
pkgver=2.2.11
pkgrel=1
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
# NOTE: Keep the order of dependencies matching setup.py
# setup.py includes setuptools, which is a work around for missing dependencies in aws-xray-sdk [1].
# I use the correct dependency in python-aws-xray-sdk, so python-setuptools is not needed here.
# [1] https://github.com/spulec/moto/pull/4142
depends=(python-boto3 python-botocore python-cryptography python-requests python-xmltodict
         python-werkzeug python-pytz python-dateutil python-responses
         # Below dependencies varied with Python versions
         python-jinja python-more-itertools)
# See reqquirements-test.txt, excluding pytest-cov
checkdepends=(python-pytest python-sure python-freezegun
              # boto is vendored (#3503), but still imported in tests
              python-boto podman)
# Below are optional dependencies. The order matches _dep_* items in upstream `setup.py`.
# Note that _dep_python_jose_ecdsa_pin and _dep_idna are excluded as they are pins and
# not used by moto.
optdepends=(
  'python-yaml: for cloudformation, s3 and ssm'
  'python-jose: for apigateway and cognitoidp'
  # SNS and SQS still uses docker indirectly, while upstream explicitly removes them
  # https://github.com/spulec/moto/pull/4094
  'python-docker: for awslambda, batch, cloudformation, dynamodb2 and dynamodbstreams'
  'python-jsondiff: for iotdata'
  'python-aws-xray-sdk: for xray'
  'python-cfn-lint: for cloudformation'
  'python-sshpubkeys: for ec2 and efs'
  'python-flask: for moto_server'
  'python-flask-cors: for moto_server'
)
checkdepends+=(python-yaml python-jose python-docker python-jsondiff python-aws-xray-sdk
               python-cfn-lint python-sshpubkeys python-flask python-flask-cors)
source=("https://files.pythonhosted.org/packages/source/m/moto/moto-${pkgver}.tar.gz"
        sdist-no-pyc.diff)
sha256sums=('3d962ba1646e8a417bfbf17ccc4dc7d2ca48a1bf7e847db7daae0690fc70537c'
            '4193bd88380f6ab8dac49be37d940993dee4e31351ffd60c8167b7c8e5ec3208')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../sdist-no-pyc.diff

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#,?<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # XXX: there are too many tests that depend on docker/podman, and there is no simple way
  # other than listing one-by-one for exclusion. To make things worse, running docker/podman
  # in systemd-nspawn containers appears impossible without special configurations. I gave
  # it up and running podman _outside_ the container for testing.
  # $ systemctl --user start podman.socket
  # $ extra-x86_64-build -- -d "$XDG_RUNTIME_DIR/podman:/tmp/podman"
  PODMAN_SOCK=/tmp/podman/podman.sock
  if [ -S $PODMAN_SOCK ]; then
    # XXX: a workaround to make tests work with podman
    # Images are not pulled before usage since version 2.2.1 [1][2], and
    # that is needed for podman [3]
    # [1] https://github.com/spulec/moto/commit/338420c3b979b85501193d948ab442c1ebd0ae4a
    # [2] https://github.com/spulec/moto/pull/4082
    # [3] https://github.com/spulec/moto/pull/3702
    podman-remote --url "unix:$PODMAN_SOCK" pull docker.io/library/busybox:latest
    DOCKER_HOST="unix://$PODMAN_SOCK" TZ=UTC pytest tests
  fi
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
