# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=multipart
pkgname=python-$_pkgname
pkgver=0.2.2
pkgrel=2
pkgdesc='Parser for multipart/form-data'
arch=('any')
url='https://github.com/defnull/multipart'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('17d58e1c18a7b70381113b395cc527f31648d9d977aebf1851cd1c06ce25a9cd65b6811326aa012d901f5009683356a48bc2e22aa1000ddff66873fdffc4aa79')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
