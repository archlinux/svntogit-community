# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Chris Fordham <chris [at] fordham-nagy [dot] id [dot] au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/python-jmespath/PKGBUILD

pkgname=python-jmespath
pkgver=0.10.0
pkgrel=1
pkgdesc='A query language for JSON'
arch=('any')
url="https://github.com/jmespath/jmespath"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jmespath/jmespath.py/archive/$pkgver.tar.gz")
sha512sums=('84d787a9547bf0d33dfb4cf42514d6a7a1e69fb8867ccb9d84aa0555ea52b3064a9c6a49b3c51564090bb1d1d2d067fe1887105d744a0f25a991d9e9595c85c5')

prepare() {
  cp -a jmespath.py-$pkgver{,-py2}
}

build() {
  cd jmespath.py-$pkgver
  python setup.py build
}

check() {
  cd jmespath.py-$pkgver
  LC_CTYPE=en_US.utf8 nosetests3
}

package() {
  cd jmespath.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
