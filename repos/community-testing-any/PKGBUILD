# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=precis_i18n
pkgname=python-precis_i18n
pkgver=1.0.4
pkgrel=2
pkgdesc='PRECIS framework (RFC 8264, RFC 8265, RFC 8266)'
url='https://github.com/byllyfish/precis_i18n'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('eb4940f4a1c138381c7525b9374c27893fc671c2449da6ee5eb98df83489e50f8e6db7c9fe32e82aeadc9d1df1a3c2902462640ff5a32b3ac0221227959a8e3c')
b2sums=('f4daaf4340b9ece365382362e3784282adf06b2cb1218ce7e4702bd104d85d5db84899fe0f929dad7bc18289bacf2c46646f8a3f4fb4337c20dbbb85682e36ce')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGELOG.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
