# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-planout
pkgname=(python-planout python2-planout)
pkgver=0.6.0
_commit=6ba0aba6addb08e03ed6e2d21f9b224b3d56d503
pkgrel=3
pkgdesc="A framework for online field experimentation"
arch=('any')
url='http://facebook.github.io/planout'
license=('custom:BSD')
makedepends=('python-six' 'python2-six' 'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/facebook/planout.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  # https://github.com/facebook/planout/pull/104
  sed -e '42s/\t/        /' \
      -e '43s/range(8)/list(range(8))/' \
      -i planout/python/planout/test/test_assignment.py
  cp -a planout{,-py2}
}

build() {
  cd "$srcdir"/planout/python
  python setup.py build

  cd "$srcdir"/planout-py2/python
  python2 setup.py build
}

check() {
  cd "$srcdir"/planout/python/planout/test
  py.test

  cd "$srcdir"/planout-py2/python/planout/test
  py.test2
}

package_python-planout() {
  depends=('python-six')

  cd planout/python
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-planout() {
  depends=('python2-six')

  cd planout-py2/python
  python2 setup.py install -O1 --root "$pkgdir"

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
