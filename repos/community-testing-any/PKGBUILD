# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-joblib
pkgver=0.16.0
pkgrel=3
pkgdesc="Set of tools to provide lightweight pipelining in Python"
arch=('any')
url="https://joblib.readthedocs.io/"
license=('BSD')
depends=('python')
optdepends=('python-numpy: for array manipulation'
            'python-lz4: for compressed serialization')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/joblib/joblib/archive/${pkgver}.tar.gz")
sha256sums=('172f59cc7381149063c808ef8a644ad18bcb9290f1729d49f1368fc976f57662')

build() {
  cd "$srcdir"/joblib-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/joblib-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
