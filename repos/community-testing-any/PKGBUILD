# $Id$
# Contributor: Johannes Dewender	 arch at JonnyJD dot net
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgbase=python-discid
pkgver=1.1.0
pkgrel=4
_pkgname=discid
pkgname=('python-discid' 'python2-discid')
pkgdesc='Python binding of MusicBrainz libdiscid'
url='https://python-discid.readthedocs.org/'
license=('LGPL3')
arch=('any')
depends=('libdiscid')
makedepends=('python' 'python2')
validpgpkeys=('8BB230F6C5C308B31FE7C82568304838CC686607')
source=("https://pypi.python.org/packages/source/d/$_pkgname/$_pkgname-$pkgver.tar.gz"{,.asc})
sha1sums=('de2d657e42d281dad593e0673af6ba6039ef58bc' 'SKIP')

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py test
}

package_python-discid() {
	depends+=('python')
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-discid() {
	depends+=('python2')
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python2 setup.py install --root="${pkgdir}" --optimize=1
}
