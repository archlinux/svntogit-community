# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-venusian
pkgname=('python-venusian' 'python2-venusian')
pkgver=1.0
pkgrel=2
pkgdesc="A library for deferring decorator actions"
arch=('any')
license=('custom:BSD')
url="http://pylonsproject.org"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose-exclude' 'python2-nose-exclude')
source=("git+https://github.com/Pylons/venusian.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a venusian{,-py2}
}

build() {
  cd "$srcdir/venusian"
  python setup.py build

  cd "$srcdir/venusian-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/venusian"
  python setup.py nosetests

  cd "$srcdir/venusian-py2"
  python2 setup.py nosetests
}

package_python-venusian() {
  depends=('python')

  cd venusian
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}

package_python2-venusian() {
  depends=('python2')

  cd venusian-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
