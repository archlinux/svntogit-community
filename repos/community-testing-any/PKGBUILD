# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pod-spell
pkgver=1.20
pkgrel=5
pkgdesc="A formatter for spellchecking Pod"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Pod-Spell"
depends=('perl-class-tiny' 'perl-file-sharedir' 'perl-lingua-en-inflect' 'perl-path-tiny'
         'perl-pod-parser')
makedepends=('perl-file-sharedir-install' 'perl-test-deep')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DO/DOLMEN/Pod-Spell-$pkgver.tar.gz")
sha512sums=('9a676a897dae7dfe37e9bfca2e5c11bcdc3221ed532b79970e6cc4d0de8dec0eca754cb9fbc5cbbbf6533a213c284d691609dd86d246ae146187bd9cd37d292e')

build() {
  cd Pod-Spell-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pod-Spell-$pkgver
  make test
}

package() {
  cd Pod-Spell-$pkgver
  make DESTDIR="$pkgdir" install
}
