# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=perl-module-install
pkgver=1.19
pkgrel=2
pkgdesc="Module::Install - Standalone, extensible Perl module installer"
arch=('any')
url='http://search.cpan.org/dist/Module-Install'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-file-remove' 'perl-json' 'perl-module-build' 'perl-module-scandeps' 'perl-par-dist'
         'perl-cpan-meta' 'perl-yaml-tiny')
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Module-Install-$pkgver.tar.gz")
sha512sums=('68a255402c98955cfcb5a8a99555fe511b89d5fccf96ee1c498cab347c8945f3abe53485ea936f7419420d9c7beb52c861516f4cfd299812cebf80eab50fa5ba')

build() {
  cd Module-Install-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Module-Install-$pkgver
  make test
}

package() {
  cd Module-Install-$pkgver
  make install DESTDIR="$pkgdir"
}
