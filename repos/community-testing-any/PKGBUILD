# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgbase=python-sympy
pkgname=('python-sympy' 'python2-sympy')
pkgver=1.0
pkgrel=2
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='http://sympy.org/en/index.html'
license=('BSD')
makedepends=('python2-mpmath' 'python-mpmath' 'python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/sympy/sympy.git#tag=sympy-$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a sympy{,-py2}
}

build() {
  cd "$srcdir"/sympy
  python setup.py build

  cd "$srcdir"/sympy-py2
  python2 setup.py build
}

check() {
  # Broken, waiting for upstream fixes.

  cd "$srcdir"/sympy
  python setup.py test || warning "Exit code: $?"

  cd "$srcdir"/sympy-py2
  python2 setup.py test || warning "Exit code: $?"
}

package_python-sympy() {
  depends=('python-mpmath')
  optdepends=('ipython: user friendly interface for isympy')

  cd sympy

  python setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sympy() {
  depends=('python2-mpmath')
  optdepends=('python2-pyglet: plotting'
              'ipython2: user friendly interface for isympy')

  cd sympy-py2

  python2 setup.py install --root "$pkgdir" --optimize=1

  # rename files that exists in both 'python-sympy' and 'python2-sympy'
  mv "$pkgdir"/usr/bin/isympy{,2}
  mv "$pkgdir"/usr/share/man/man1/isympy{,2}.1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
