# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexandre Bouvier <contact AT amb DOT tf>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: Tommaso Sardelli <lacapannadelloziotom AT gmail DOT com>
# Contributor: Philipp Joram <phijor AT t-online DOT de>

pkgname=python-axolotl
pkgver=0.2.3
pkgrel=6
pkgdesc='Python port of libaxolotl'
url='https://github.com/tgalal/python-axolotl'
arch=('any')
license=('GPL3')
depends=('python-protobuf' 'python-cryptography' 'python-axolotl-curve25519')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(https://github.com/tgalal/python-axolotl/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('effddbe6ac12b3065defae553a1fd3e3f43033bb7bc9f871744a2d122b3abd41')
sha512sums=('1a10dc9df2f95d19b49909d4d136b5266aae19e24ac84f8b263ae7d4de18f77353c737616ac0648b600e05699a90d348f95c6c1659849986becefb4a0277fc80')

build() {
  echo 'Building python...'
  cd ${pkgbase}-${pkgver}
  python setup.py build
}

check() {
  echo 'Testing python...'
  cd ${pkgbase}-${pkgver}
  py.test
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
