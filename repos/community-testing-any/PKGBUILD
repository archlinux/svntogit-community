# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-xdist
pkgname=('python-pytest-xdist' 'python2-pytest-xdist')
pkgver=1.15.0
pkgrel=2
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools_scm' 'python2-setuptools_scm' 'python-execnet' 'python2-execnet' 'git')
source=("git+https://github.com/pytest-dev/pytest-xdist#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a pytest-xdist{,-py2}
}

build() {
  cd "$srcdir"/pytest-xdist
  python setup.py build

  cd "$srcdir"/pytest-xdist-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-xdist
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-xdist-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-xdist() {
  depends=('python-pytest' 'python-execnet')

  cd "$srcdir/pytest-xdist"
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-xdist() {
  depends=('python2-pytest' 'python2-execnet')

  cd "$srcdir/pytest-xdist-py2"
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
# vim:set ts=2 sw=2 et:
