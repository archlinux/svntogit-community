# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-apipkg
pkgver=3.0.1
pkgrel=3
pkgdesc="Namespace control and lazy-import mechanism"
arch=('any')
license=('MIT')
url="https://github.com/pytest-dev/apipkg"
depends=('python')
makedepends=('python-build' 'python-installer' 'python-hatchling' 'python-hatch-vcs')
checkdepends=('python-pytest-runner')
source=("https://github.com/pytest-dev/apipkg/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('89251f02a49b5191879bcf6e35c6a8755d6b29f1ca1c6061eab004385b11433ec86e2968ab23994c6a74515d6a673e445b3bcf8cbdd21b2ec644fcef0d19b419')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  cd apipkg-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local pytest_options=(
    # NOTE: these tests are likely broken because of circular deps with python-py
    --deselect test_apipkg.py::test_chdir_with_relative_imports_support_lazy_loading
    --deselect test_apipkg.py::test_get_distribution_version
  )
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd apipkg-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv "${pytest_options[@]}"
}

package() {
  cd apipkg-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
