# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=postorius
pkgver=1.3.6
pkgrel=1
pkgdesc="The New Mailman Web UI"
arch=('any')
url="https://gitlab.com/mailman/postorius"
license=('GPL3')
depends=('python-cmarkgfm' 'python-django' 'python-django-mailman3'
'python-mailmanclient' 'python-readme-renderer')
makedepends=('git' 'python-setuptools')
checkdepends=('mailman3' 'python-beautifulsoup4' 'python-isort' 'python-mock'
'python-pytest' 'python-pytest-django' 'python-vcrpy')
optdepends=('mailman3: for configuring a local mailman instance'
            'uwsgi-plugin-python: for running inside uwsgi')
replaces=('python-django-postorius')
backup=("etc/uwsgi/${pkgname}.ini"
        "etc/webapps/${pkgname}/settings_local.py"
        "etc/webapps/${pkgname}/urls.py"
)
install="${pkgname}.install"
# the signature for the pypi sdist tarball for 1.3.6 is missing the chain of trust:
# https://gitlab.com/mailman/postorius/-/issues/537
# source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
source=("git+https://gitlab.com/mailman/${pkgname}#tag=${pkgver}?signed"
        "${pkgname}-1.3.2-settings.patch"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles"
        "${pkgname}.uwsgi"
)
sha512sums=('SKIP'
            '53713874043e368c71543966e878ff7a49f03088b7ebcff9487935bef2c20a57a561a122dd6b4cbde126c25b935871288dadd7693b831a94b8f27da52afeaa0c'
            '3884b818da2deb148279e50316f720c95f0aaa529777b1b3662d8ab253cda823a90682a9eb47e941ca5ba80eefcb9a2d4afacd29f0340d7f9828b36ebcee23b3'
            '44925c7ce6d1c13bd5bdd2cb449fa978b8eaadaac7ecff74075c23a13478decc4acd1f2cd0399bf55c312d6a08985a3e80f6e7a6e85be9e3dcb93ffb526ca9fb'
            'c042f00d0d8dc72dadd19023152e55246c4d7a4b9d53bca3a05e7b8cb38cac5116417440d4ae82350820c7cb0cd414dcce45fa6af9be19296c4ebd0862dba896')
b2sums=('SKIP'
        'c4017fe87b8743ae516e360be97efd767d726b043733a6dd880503e796e28f65c9ad5924b0fef53281e4fa73a1a329e818baedbe5dbbc0dc07db2af0cf25f1b7'
        '1ef6dc98c6751e6d65e49812d08d813879b791a7f13622ea31ecadae6dd1859a918761d7723732ed2e4872b9cae61b84c8dbfec2dd2522de9f0929a1ee1cec24'
        '502d59cb0009b7cdf108a1611d17470d1a73c2d06bf6d9509799b63d4bbb4bb0a2cb71a39eddb0dc5990ee6489625174de6ce2d7e04e77a601c90a6da6d69c84'
        '4b80d60c88d46edd0e6e61ad2ff37ba3568c6c56a2abd449f903f7302aeec4a3bc91bbd3ee846731513fa1906df2e3d7d1c52b75a55214051fb96b048f9b4999')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
#  cd "$pkgname-$pkgver"
  cd "$pkgname"
  # patch global paths into settings.py
  patch -Np1 -i "../${pkgname}-1.3.2-settings.patch"
  touch settings_local.py
}

build() {
#  cd "$pkgname-$pkgver"
  cd "$pkgname"
  python setup.py build
}

check() {
#  cd "$pkgname-$pkgver/example_project"
  cd "$pkgname/example_project"
  export PYTHONPATH="../build/lib:${PYTHONPATH}"
  PYTHONDONTWRITEBYTECODE=1 \
  pytest -vv ../build/lib/${pkgname}
}

package() {
#  cd "$pkgname-$pkgver"
  cd "$pkgname"
  python setup.py install --optimize=1 --root="${pkgdir}"
  # django project
  install -vDm 644 example_project/{__init__,manage,settings,wsgi}.py \
    -t "${pkgdir}/usr/share/webapps/${pkgname}"
  # symlink locale and static dirs to state dir
  ln -svf "/var/lib//${pkgname}/locale/" \
    "${pkgdir}/usr/share/webapps/${pkgname}"
  ln -svf "/var/lib/${pkgname}/static/" \
    "${pkgdir}/usr/share/webapps/${pkgname}"
  # state dir
  install -vdm 750 "${pkgdir}/var/lib/${pkgname}/data"
  install -vdm 755 "${pkgdir}/var/lib/${pkgname}/"{locale,static}
  # log dir
  install -vdm 750 "${pkgdir}/var/log/${pkgname}"
  # config
  install -vDm 640 settings_local.py example_project/urls.py \
    -t "${pkgdir}/etc/webapps/${pkgname}"
  ln -svf "/etc/webapps/${pkgname}/settings_local.py" \
    "${pkgdir}/usr/share/webapps/${pkgname}/settings_local.py"
  ln -svf "/etc/webapps/${pkgname}/urls.py" \
    "${pkgdir}/usr/share/webapps/${pkgname}/urls.py"
  # uwsgi
  install -vDm 644 "../${pkgname}.uwsgi" "${pkgdir}/etc/uwsgi/${pkgname}.ini"
  # tmpfiles.d
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # sysusers.d
  install -vDm 644 "../${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
