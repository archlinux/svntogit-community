# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-profilestats
pkgname=('python-profilestats' 'python2-profilestats')
pkgver=2.0
pkgrel=3
pkgdesc="Decorator for profiling individual functions and converting profiling data to the kcachegrind/qcachegrind format."
arch=('any')
license=('BSD')
url="https://pypi.python.org/pypi/profilestats"
makedepends=('python-setuptools' 'python2-setuptools' 'pyprof2calltree' 'python2-pyprof2calltree' 'git')
source=("git+https://github.com/hannosch/profilestats.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a profilestats{,-py2}
}

build() {
  cd "$srcdir"/profilestats
  python setup.py build

  cd "$srcdir"/profilestats-py2
  python2 setup.py build
}

package_python-profilestats() {
  depends=('python-setuptools' 'pyprof2calltree')

  cd profilestats
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-profilestats() {
  depends=('python2-setuptools' 'python2-pyprof2calltree')

  cd profilestats-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
