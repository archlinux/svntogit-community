# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fields
pkgname=('python-fields' 'python2-fields')
pkgver=2.4.0
_commit=d1ec90e559ea18c7bc332cc9eb0fd95b2e27f4eb
pkgrel=1
arch=('any')
pkgdesc='A totally different take on container boilerplate'
url='https://github.com/ionelmc/python-fields'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-capturelog' 'python2-pytest-capturelog'
              'python-pytest-benchmark' 'python2-pytest-benchmark'
              'python-characteristic' 'python2-characteristic')
source=("git+https://github.com/ionelmc/python-fields.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a python-fields{,-py2}
}

build() {
  cd python-fields
  python setup.py build

  cd ../python-fields-py2
  python2 setup.py build
}

check() {
  cd python-fields
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test tests || warning "Tests failed"

  cd ../python-fields-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2 tests || warning "Tests failed"

  # Compatibility issues with pytest 2.8
}

package_python-fields() {
  depends=('python')

  cd python-fields
  python setup.py install --root "${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-fields() {
  depends=('python2')

  cd python-fields-py2
  python2 setup.py install --root "${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
