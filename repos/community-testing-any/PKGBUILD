# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-canonicaljson
pkgver=1.6.4
pkgrel=1
pkgdesc='Canonical JSON'
url='https://pypi.python.org/pypi/canonicaljson/'
arch=('any')
license=('Apache')
depends=('python' 'python-simplejson' 'python-frozendict')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("python-canonicaljson-$pkgver.tar.gz::https://github.com/matrix-org/python-canonicaljson/archive/v${pkgver}.tar.gz")
sha512sums=('7be1300c17d51f917b1e81931f6d0dd681b1eef9b7f4e82ba3ae683609d74fd924eb3a832c8261611f556c190329748966bf15abe34bc1d22f2ecdf29372b560')
b2sums=('4d55bb90cd4bbd5e019ffd0d505651c99dc6b296e9c0a52582265438586890cd6b663116ba0b66807b7a665c54eea001e039ca86c2fef9175b72a318a11a79a7')

build() {
	cd python-canonicaljson-$pkgver
	python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
	cd python-canonicaljson-$pkgver
	python -m venv --system-site-packages test-env
	test-env/bin/python -m installer dist/*.whl
	PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
}

package() {
	cd python-canonicaljson-$pkgver
	python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
