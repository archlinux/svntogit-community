# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=python-qt.py
_pkgname=Qt.py
pkgver=1.2.0.b3
pkgrel=3
pkgdesc='Python compatibility wrapper around all Qt bindings'
arch=('any')
url='https://github.com/mottosso/Qt.py'
license=('MIT')
depends=('python')
optdepends=('python-pyqt5: Qt 5 bindings'
	    'pyside2: LGPL Qt 5 bindings')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('0e195c2d08837f60ddfbb182c4048bb21b7aed27d042e8e77c2abcf653c60a5139eef1fd369735088781830b739beac6da0afa1c6b04a197b403a70ae1ce2f77')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  rm "$pkgdir"/usr/LICENSE # Weird

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

