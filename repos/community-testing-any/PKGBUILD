# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: svoufff <svoufff at gmail dot com>

pkgname=jsampler
_name=JSampler
pkgver=0.9
pkgrel=3
pkgdesc="LinuxSampler Java GUI"
arch=('any')
url="http://jsampler.sourceforge.net/"
license=('GPL')
depends=('java-runtime' 'linuxsampler')
makedepends=('java-environment' 'apache-ant')
source=("https://downloads.sourceforge.net/${pkgname}/${_name}-${pkgver}.tar.bz2"
        "${pkgname}.sh"
        "${pkgname}-classic.sh"
        "${pkgname}.desktop"
        "${pkgname}-classic.desktop")
sha512sums=('cbb1d476b1f3882592cc0fb7a61348802c0c399dfc3fcc35f4a8ec3396e8417754cafc3e1a14a90b0014bebd29d28b9d3771dcbb772df40d241eaadedad5299f'
            '2f41bc9c41750715ef1ebe5f3c8d2dc36b4fa5fe28d68fdcc1968f38293d3cc835f2695f0a24d2bb5287006687969125f62671d2f3efc7eda8c0bde759a765c7'
            'c3fcab6cb4f296fe94cf7ebec9db5543c2be53d210e490998c219f50917cc932a5ab0172fdf666f2a80a8af34b7f5e11e6c37a223c83be32453aa07f7e2fa207'
            '9fd3b2ea525fc81abb8dc0611fbfc2636852d82811db8b2b66c821346d1cc8f84bd4d13123af5ee8993689e9c9f201302dbbe6c707883a900e87d0413b9278f6'
            '490c95e62b73d0d708d75c0d7f95074e2547ee3d4bb28996105c58d860067e8b7279413c7106adf19f59d899f2c6551eeb4f68d902446b211ce49e1340d51b68')

build() {
  cd "${_name}-${pkgver}"

  ant build-fantasia
  ant build-jsclassic
}

package() {
  cd "${_name}-${pkgver}"

  # install java files
  install -Dm644 dist/Fantasia-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/fantasia.jar"
  install -Dm644 dist/JS_Classic-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/js-classic.jar"

  # install shell scripts
  install -Dm755 "${srcdir}/${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 "${srcdir}/${pkgname}-classic.sh" \
    "${pkgdir}/usr/bin/${pkgname}-classic"

  # install freedesktop.org compatibility
  install -Dm644 res/fantasia/icons/app_icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 res/js-classic/icons/app-icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}-classic.png"

  install -Dm644 "${srcdir}/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/$pkgname.desktop"
  install -Dm644 "${srcdir}/$pkgname-classic.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}-classic.desktop"
}

# vim:set ts=2 sw=2 et:
