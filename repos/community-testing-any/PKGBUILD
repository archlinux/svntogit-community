# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-astor
pkgname=('python-astor' 'python2-astor')
pkgver=0.5
pkgrel=2
pkgdesc="Read/rewrite/write Python ASTs"
arch=('any')
license=('BSD')
url="http://astor.rtfd.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/berkerpeksag/astor.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a astor{,-py2}
}

build() {
  cd "$srcdir/astor"
  python setup.py build

  cd "$srcdir/astor-py2"
  python2 setup.py build
}

check() {
  # https://github.com/berkerpeksag/astor/issues/48
  cd "$srcdir/astor"
  nosetests3 || warning "Tests failed"

  cd "$srcdir/astor-py2"
  nosetests2
}

package_python-astor() {
  depends=('python')

  cd astor
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-astor() {
  depends=('python2')

  cd astor-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
