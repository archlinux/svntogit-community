# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgname=prettytable
pkgname=(python-prettytable python2-prettytable)
pkgver=0.7.2
pkgrel=9
pkgdesc="A simple Python library for easily displaying tabular data"
url="https://pypi.python.org/pypi/PrettyTable"
arch=('any')
license=('BSD')
makedepends=('python2-setuptools' python-setuptools)
source=("https://files.pythonhosted.org/packages/source/P/PrettyTable/prettytable-${pkgver}.tar.gz"
        '0001-Fix-column-spacing-with-xterm-reset-escape-sequence.patch')
md5sums=('a6b80afeef286ce66733d54a0296b13b'
         '62c2acf4a81ba9e6ac60ba06ff282068')

prepare() {
  pushd $_pkgname-$pkgver
  patch -p1 -i "$srcdir"/0001-Fix-column-spacing-with-xterm-reset-escape-sequence.patch
  popd

  cp -a "$srcdir"/$_pkgname-$pkgver "$srcdir"/$_pkgname-$pkgver-2
  find $_pkgname-$pkgver-2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build
  cd "$srcdir"/$_pkgname-$pkgver-2
  python2 setup.py build
}

package_python-prettytable() {
  cd "$srcdir/$_pkgname-$pkgver"
  chmod 644 -R prettytable.egg-info
  depends=(python)
  python setup.py install --root="$pkgdir/"
}
package_python2-prettytable() {
  cd "$srcdir/$_pkgname-$pkgver-2"
  chmod 644 -R prettytable.egg-info
  depends=(python2)
  python2 setup.py install --root="$pkgdir/"
}
