# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=pyaxmlparser
pkgname=python-${_pyname}
pkgver=0.3.27
pkgrel=2
pkgdesc='Parser for Android XML file and get Application Name without using Androguard'
url='https://github.com/appknox/pyaxmlparser'
arch=('any')
license=('Apache')
depends=('python' 'python-lxml' 'python-click' 'python-asn1crypto' 'python-magic')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('816caa495da50d19bc091477ad8d1b914594b679a2bad284317762a47d68813e7b68c7f22a5515c71eb67b98c26b79c923393451dd97ec5c56b310e49da3edda')
b2sums=('cc8308bd5ca42994858a850960fb025a12bb0dd7d3051c3fd433363be3f39a46abf46d0e0af58183389da6145a4777a39008e20e9ec717a6b2c6c64d2f7f58dc')

prepare() {
  cd ${_pyname}-${pkgver}
  sed -i "s|man/man1|share/man/man1|g" -i setup.py
}

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
