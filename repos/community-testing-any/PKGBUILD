# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-ipykernel
pkgver=5.1.3
pkgrel=2
pkgdesc="The ipython kernel for Jupyter"
arch=('any')
url="https://pypi.org/project/ipykernel/"
license=('BSD')
  depends=('python-traitlets' 'python-tornado' 'ipython' 'python-jupyter_client')
makedepends=('python-setuptools' 'ipython' 'python-jupyter_client')
checkdepends=('python-pytest' 'python-mock' 'python-nose')
source=("ipykernel-$pkgver.tgz::https://github.com/ipython/ipykernel/archive/v$pkgver.tar.gz")
md5sums=('adebdeae3181851486a0eefd79aa2cf5')

# dep cycle with python-jupyter_client

build() {
  cd "$srcdir/ipykernel-$pkgver"
  python3 setup.py build
}

check() {
  cd "$srcdir/ipykernel-$pkgver"
  #python3 -m pytest
  # tests fail both locally and in a chroot, for different reasons
}

package() {
  cd "$srcdir/ipykernel-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

