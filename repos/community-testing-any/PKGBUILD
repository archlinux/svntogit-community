# $Id$
# Maintainer: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=2.11.1
pkgrel=3
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
makedepends=('git')
depends=('python' 'python-psutil' 'python-setuptools')
optdepends=('hddtemp: HDD temperature monitoring support'
            'python-bottle: web server support'
            'python-docker: for the Docker monitoring support'
            'python-matplotlib: for graphical/chart support'
            'python-netifaces: for the IP plugin'
            'python-zeroconf: for the autodiscover mode')
source=("git+https://github.com/nicolargo/glances.git#tag=v$pkgver"
        'glances.service')
md5sums=('SKIP'
         '193fe4123456b0a9567d8d5952c4c606')

package() {
  cd glances
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "$srcdir"/glances.service \
    "$pkgdir"/usr/lib/systemd/system/glances.service
}

# vim:set ts=2 sw=2 et:
