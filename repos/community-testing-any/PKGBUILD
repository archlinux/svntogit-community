# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-karellen-geventws
pkgver=1.0.2
pkgrel=2
pkgdesc="Websocket handler for the gevent pywsgi server, a Python network library"
arch=('any')
url="https://github.com/karellen/karellen-geventws"
license=('Apache')
depends=('python-gevent')
makedepends=('python-pybuilder' 'python-karellen-pyb-plugin' 'python-cram' 'flake8' 'python-sphinx'
             'python-unittest-xml-reporting' 'python-wsaccel' 'git')
optdepends=('python-wsaccel: for better performance')
source=("git+https://github.com/karellen/karellen-geventws.git#tag=$pkgver")
sha512sums=('SKIP')

check() {
  cd "$srcdir"/karellen-geventws
  pyb -v analyze
}

package() {
  cd karellen-geventws
  pyb -v -o package
  cd target/dist/karellen-geventws-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
