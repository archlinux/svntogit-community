# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mypy
pkgver=0.7.0
pkgrel=3
pkgdesc='Mypy static type checker plugin for Pytest'
arch=('any')
license=('MIT')
url='https://github.com/dbader/pytest-mypy'
depends=('mypy' 'python-filelock' 'python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dbader/pytest-mypy/archive/v$pkgver.tar.gz")
sha512sums=('b1a544d1f0fe759e7dabd6282603334f8095f7381ee99479e5f04eb8fdd46df55213317f984b4ae9c0e89445a405478d6860bdd06185426a2f033ae4e4d19544')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-mypy-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-mypy-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-mypy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
