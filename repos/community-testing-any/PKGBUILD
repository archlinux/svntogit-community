# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-ukpostcodeparser
pkgname=('python-ukpostcodeparser' 'python2-ukpostcodeparser')
pkgver=1.1.0
_commit=4040f2cdbf884e41a336271a0d654e785953b44b
pkgrel=2
pkgdesc="UK Postcode parser"
arch=('any')
license=('MIT')
url="https://github.com/hamstah/ukpostcodeparser"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/hamstah/ukpostcodeparser.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a ukpostcodeparser{,-py2}
}

build() {
  cd "$srcdir"/ukpostcodeparser
  python setup.py build

  cd "$srcdir"/ukpostcodeparser-py2
  python2 setup.py build
}

check() {
  # Expected failure: https://github.com/hamstah/ukpostcodeparser/issues/5

  cd "$srcdir"/ukpostcodeparser
  python -m unittest discover -s ukpostcodeparser/test -p '*.py' || warning "Tests failed"

  cd "$srcdir"/ukpostcodeparser-py2
  python2 -m unittest discover -s ukpostcodeparser/test -p '*.py' || warning "Tests failed"
}

package_python-ukpostcodeparser() {
  depends=('python')

  cd ukpostcodeparser
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-ukpostcodeparser() {
  depends=('python2')

  cd ukpostcodeparser-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

