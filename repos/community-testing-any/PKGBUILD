# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-passwordsafe
pkgver=7.3
pkgrel=2
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
arch=(any)
url="https://gitlab.gnome.org/World/PasswordSafe"
license=(GPL3)
depends=(gtk4 libadwaita libpwquality python-gobject python-pykeepass python-pyotp python-cairo python-validators python-zxcvbn)
makedepends=(git gobject-introspection meson)
_commit=85d2ad2b0f472feca2ec4263709e0e1494ce9d80
source=("git+https://gitlab.gnome.org/World/PasswordSafe.git/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd PasswordSafe
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson PasswordSafe build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
