# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=termdown
pkgver=1.18.0
pkgrel=4
pkgdesc='Countdown timer and stopwatch in your terminal'
arch=('any')
url=https://github.com/trehn/termdown
license=('GPL3')
depends=('python-click' 'python-dateutil' 'python-pyfiglet')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
optdepends=('espeak-ng: for spoken countdown')
source=("https://files.pythonhosted.org/packages/source/t/termdown/termdown-$pkgver.tar.gz")
sha256sums=('9165df7afd74a442432ba1755c6da3e017801995cf0a17f794295fcb2fd6dd1e')
b2sums=('6a04dba81de96bb7197c68ac0cea981b19f8ba5e0be683d2f748df9289dba85ef2394648e8a0cfd593c93567d4e182d148d967f7944c44743fa8e809c94525e7')

build() {
  cd termdown-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd termdown-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
