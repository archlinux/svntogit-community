# $Id$
# Maintainer:

pkgname=perl-file-path-tiny
pkgver=0.9
pkgrel=2
pkgdesc='recursive versions of mkdir() and rmdir() without as much overhead as File::Path'
arch=('any')
url="https://metacpan.org/release/File-Path-Tiny"
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-exception')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/D/DM/DMUEY/File-Path-Tiny-$pkgver.tar.gz")
sha1sums=('1327cb5795b558fef931b46b7b8c8961bdb72fa0')

build() {
  cd File-Path-Tiny-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-Path-Tiny-$pkgver
  make test
}

package() {
  cd File-Path-Tiny-$pkgver
  make install DESTDIR="$pkgdir"
}
