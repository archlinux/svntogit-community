# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=6.0.0
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-attrs' 'python-iniconfig' 'python-more-itertools' 'python-packaging' 'python-pluggy'
         'python-py' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-argcomplete' 'python-hypothesis' 'python-mock' 'python-nose' 'python-requests'
              'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'python-decorator' 'python-docutils' 'python-jinja' 'python-numpy' 'python-pexpect'
              'python-twisted' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('2c42c54a16a02b5d033527c3632e4e49d6fb82deee8a05ae1665fcd5f3978098c87599e8fb1bcd14a098d977c7d8588dc93b3b84398b5dc965d1b0c2740b446e')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  cd pytest-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD"/build/lib python src/pytest/__main__.py
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
