# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-mockmodule
pkgver=0.11
pkgrel=3
pkgdesc="Override subroutines in a module for unit testing"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-super')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
url='http://search.cpan.org/dist/Test-MockModule'
source=("http://search.cpan.org/CPAN/authors/id/G/GF/GFRANKS/Test-MockModule-$pkgver.tar.gz")
sha512sums=('417a0de701458e3e26ea194cb2de7756d9a1665b2b7283a375ee95362ee2d7b77996801d4dafd8ead12e97d59c2a57ffd1e98620446d5f47a1836c8ef18983b4')

build() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Test-MockModule-$pkgver"
  perl Build install destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
