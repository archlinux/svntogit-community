# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-termstyle
pkgver=0.1.11
_commit=ed18c5ed4bfee21267843f758094a5620d5f7929
pkgrel=7
pkgdesc="Console colouring for python"
url="https://github.com/timbertson/termstyle"
license=('BSD')
arch=('any')
depends=('python-setuptools')
source=("$pkgname-$_commit.tar.gz::https://github.com/timbertson/termstyle/archive/$_commit.tar.gz")
sha512sums=('02849055614caf37d3b836bef52b6c5ad281570ad5577aa253e079ecd3d82ed1f2168394edbf4760e1a9ba16e7f23aecd7191c39d3dc1de2aeb4ab9599b0717a')

prepare() {
  mv termstyle-{$_commit,$pkgver}
}

build() {
  cd "$srcdir"/termstyle-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/termstyle-$pkgver
  python test3.py
}

package() {
  cd termstyle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
