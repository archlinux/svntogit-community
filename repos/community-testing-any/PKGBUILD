# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyhamcrest
pkgname=('python-pyhamcrest' 'python2-pyhamcrest')
pkgver=1.9.0
pkgrel=3
pkgdesc="Hamcrest framework for matcher objects"
arch=('any')
license=('BSD')
url="https://github.com/hamcrest/PyHamcrest"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-cov'
              'python2-pytest-cov' 'python-mock' 'python2-mock')
source=("https://pypi.io/packages/source/P/PyHamcrest/PyHamcrest-$pkgver.tar.gz")
sha512sums=('3b8393d0079b619b208ec7f06ed5133c4ff9d9dba050f818adf56347fc2225fd1c85d60f3dc262b7b9722061f0b8e4966180b6b96245a371e578d0d51633b4b0')

prepare() {
  cp -a PyHamcrest-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PyHamcrest-$pkgver
  python setup.py build

  cd "$srcdir"/PyHamcrest-$pkgver-py2
  python2 setup.py build
}

# Tests not included in release tarball
check_disabled() {
  cd "$srcdir"/PyHamcrest-$pkgver
  python setup.py pytest

  cd "$srcdir"/PyHamcrest-$pkgver-py2
  python2 setup.py pytest
}

package_python-pyhamcrest() {
  depends=('python-six')

  cd PyHamcrest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyhamcrest() {
  depends=('python2-six')

  cd PyHamcrest-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
