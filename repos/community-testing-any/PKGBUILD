# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-graphql-server-core
pkgver=2.0.0
pkgrel=4
pkgdesc='Core package for using GraphQL in a custom server easily'
url=https://github.com/graphql-python/graphql-server-core
arch=(any)
license=(MIT)
depends=(
  python-graphql-core
  python-promise
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/graphql-python/graphql-server-core.git#tag=340236fa759917a51803583ba193074b34d584e1)
sha256sums=(SKIP)

pkgver() {
  cd graphql-server-core

  git describe --tags | sed 's/^v//'
}

build() {
  cd graphql-server-core

  python setup.py build
}

package() {
  cd graphql-server-core

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-graphql-server-core/
}

# vim: ts=2 sw=2 et:
