# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-curio
pkgver=0.9
pkgrel=3
pkgdesc='Concurrent I/O'
arch=(any)
url=https://github.com/dabeaz/curio
license=(BSD)
depends=(python)
makedepends=(
  git
  python-pytest
  python-setuptools
  python-sphinx
)
provides=(python-multio-provider)
source=(git+https://github.com/dabeaz/curio.git#tag=${pkgver})
sha256sums=(SKIP)

build() {
  cd curio

  python setup.py build
}

check() {
  cd curio

  python setup.py test
}

package() {
  cd curio

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-curio/
}

# vim: ts=2 sw=2 et:
