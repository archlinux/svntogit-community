# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Jonas Witschel <diabonas@archlinux.org>

pkgname=python-aiohttp-socks
pkgver=0.7.1
_tag=db40abf65b3ad12debb2d18178f614fef904b5e3 # git rev-parse "v$pkgver"
pkgrel=5
pkgdesc="SOCKS proxy connector for aiohttp. SOCKS4(a) and SOCKS5 are supported."
arch=('any')
url="https://github.com/romis2012/aiohttp-socks/"
license=('Apache')
depends=('python' 'python-aiohttp' 'python-attrs' 'python-async-timeout' 'python-python-socks')
makedepends=('git' 'python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-flask' 'python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/romis2012/aiohttp-socks.git?signed#tag=$_tag")
sha256sums=('SKIP')
validpgpkeys=('706A8935FED55F3BFB7C188B5EEAD7400A5D80ED') # Roman Snegirev (maintainer)

pkgver() {
	cd "${pkgname//python-}"
	git describe | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
    cd "${pkgname//python-}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${pkgname//python-}"
    pytest
}

package() {
    cd "${pkgname//python-}"
    python -m installer --destdir="$pkgdir" dist/*.whl
}
