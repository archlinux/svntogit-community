# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: gmes78 <gmes.078 at gmail dot com>

pkgname=python-qasync
pkgver=0.10.0
pkgrel=2
pkgdesc="Python library for using asyncio in Qt-based applications"
arch=(any)
url="https://github.com/CabbageDevelopment/qasync"
license=('BSD')
depends=('python' 'qt5-python-bindings')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pyqt5' 'pyside2' 'xorg-server-xvfb')
source=("https://github.com/CabbageDevelopment/qasync/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1111b5beee8e12c0c595ac2927f47c69d647bf46181e459d8ca19a694a562b4a4bbf928188c508ee2454ea30c231d3f3ee64fd910aca9ff84bdce1b898f5d4b1')

build() {
  cd qasync-$pkgver
  python setup.py build
}

check() {
  cd qasync-$pkgver
  QT_API=PyQt5 xvfb-run python -m pytest
  QT_API=PySide2 xvfb-run python -m pytest
}

package() {
  cd qasync-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
