# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-subunit2sql
pkgname=(python-subunit2sql python2-subunit2sql)
pkgver=1.10.0
pkgrel=3
pkgdesc="Command to Read a subunit file or stream and put the data in a SQL DB"
arch=('any')
url="https://pypi.python.org/pypi/subunit2sql"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-alembic'
             'python2-alembic' 'python-stevedore' 'python2-stevedore' 'python-sqlalchemy'
             'python2-sqlalchemy' 'python-oslo-config' 'python2-oslo-config' 'python-dateutil'
             'python2-dateutil' 'python-subunit' 'python2-subunit' 'python-oslo-db'
             'python2-oslo-db')
checkdepends=('python-stestr' 'python2-stestr' 'python-mock' 'python2-mock'
              'python-testresources' 'python2-testresources' 'python-testscenarios'
              'python2-testscenarios' 'python-oslo-concurrency' 'python2-oslo-concurrency'
              'pifpaf' 'percona-server' 'mariadb-clients' 'postgresql')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack-infra/subunit2sql/archive/$pkgver.tar.gz"
        fix-migrations-test.patch)
sha512sums=('8efe7f53606a4927f1bb97a9329e1d25c16baad32a64ea7df59cc4958af9111c5227e29c205f5bbf8d5fbd1babc0a6242fd067e00bfa1e06e32aeca17c065363'
            '0891880f0ff329a4236a73f88a90ffc4522c123b52e08675609816649934b20fc7ccbd9d5141341bc7a96c6acd32e40b227e61634fbd803e8b7c659e42ea0668')

prepare() {
  patch -Np1 -d subunit2sql-$pkgver <fix-migrations-test.patch
  cp -a subunit2sql-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/subunit2sql-$pkgver
  python setup.py build

  cd "$srcdir"/subunit2sql-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/subunit2sql-$pkgver
  pifpaf run mysql pifpaf run postgresql stestr run

  cd "$srcdir"/subunit2sql-$pkgver-py2
  PYTHON=python2 pifpaf run mysql pifpaf run postgresql stestr2 run
}

package_python-subunit2sql() {
  depends=('python-six' 'python-pbr' 'python-alembic' 'python-stevedore' 'python-sqlalchemy'
           'python-oslo-config' 'python-dateutil' 'python-subunit' 'python-oslo-db')

  cd "$srcdir"/subunit2sql-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-subunit2sql() {
  depends=('python2-six' 'python2-pbr' 'python2-alembic' 'python2-stevedore' 'python2-sqlalchemy'
           'python2-oslo-config' 'python2-dateutil' 'python2-subunit' 'python2-oslo-db')

  cd "$srcdir"/subunit2sql-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/sql2subunit{,2}
  mv "$pkgdir"/usr/bin/subunit2sql{,2}
  mv "$pkgdir"/usr/bin/subunit2sql-db-manage{,2}
  mv "$pkgdir"/usr/bin/subunit2sql-graph{,2}
}

# vim:set ts=2 sw=2 et:
