# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pastedeploy
pkgname=('python-pastedeploy' 'python2-pastedeploy')
pkgver=1.5.2
pkgrel=3
pkgdesc="Load, configure, and compose WSGI applications and servers"
arch=('any')
license=('MIT')
url="http://pythonpaste.org/deploy/"
makedepends=('python-setuptools' 'python2-setuptools' 'mercurial')
checkdepends=('python-nose' 'python2-nose')
source=("hg+https://bitbucket.org/ianb/pastedeploy#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pastedeploy{,-py2}
}

build() {
  cd "$srcdir/pastedeploy"
  python setup.py build

  cd "$srcdir/pastedeploy-py2"
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir/pastedeploy"
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" nosetests3

  cd "$srcdir/pastedeploy-py2"
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" nosetests2
}

package_python-pastedeploy() {
  depends=('python-setuptools')

  cd pastedeploy
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}

package_python2-pastedeploy() {
  depends=('python2-setuptools')

  cd pastedeploy-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
}
