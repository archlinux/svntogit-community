# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pifpaf
pkgver=2.2.2
pkgrel=4
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-six'
         'python-fixtures' 'python-psutil' 'python-xattr')
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
makedepends=('python-setuptools')
checkdepends=('python-testrepository' 'python-requests' 'python-testtools' 'python-oslotest'
              'memcached' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb')
# 'elasticsearch': test fails
# 'rabbitmq': test hangs
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz")
sha512sums=('ca06da6b641eca723e6818c9bd81bd68fb0e1f8687e7ca32e4c7c36439b769f52d23a0e1a14e74b7731c69210d7de2b9ce2f68daf992424d54f4a3c7f0e6877f')

export PBR_VERSION=$pkgver

build() {
  cd pifpaf-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr
}

package() {
  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
