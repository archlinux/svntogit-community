# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tabulate
pkgname=python-tabulate
pkgver=0.8.9
pkgrel=4
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-pandas' 'python-pytest' 'python-wcwidth')
optdepends=('python-numpy: NumPy array support'
            'python-pandas: pandas.DataFrame support'
            'python-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4170bb4ded836997ea053b039f19e084818c40e8ae2ae4f56c686ed3d33a341986bc311dd0370256b1af815db7b1ed8ba89008f87f8634b216412d0fd5fa76d9')
b2sums=('6c2a939d2e5e1c128545ee9d7c2ad6e51c2a0512b7cf97858dbd2bc9df9e58198b74441c5963bde64fa851558e1146dc4252951ed47d4c56630b89e5210293ac')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  pytest -v
}

package_python-tabulate() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
