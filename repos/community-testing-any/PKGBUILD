# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=three-merge
pkgname=python-${_pkg}
pkgver=0.1.1
pkgrel=4
pkgdesc="Simple Python library to perform a 3-way merge between strings"
arch=(any)
url="https://github.com/spyder-ide/three-merge/"
license=(MIT)
depends=(python-diff-match-patch)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
# PyPi tarballs do not have the tests
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(https://github.com/spyder-ide/three-merge/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('b6ab2231fd349cdf03be5be04f28c1d59d5a475f7396dcf8fd8b0597981d113e')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest -x -v three_merge/tests
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  rm -r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/${_pkg//-/_}/tests
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
