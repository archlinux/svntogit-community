# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=afl-utils
pkgver=1.33a
pkgrel=2
pkgdesc='Utilities for automated crash sample processing/analysis, easy afl-fuzz job management and corpus optimization'
url='https://github.com/rc0r/afl-utils'
arch=('any')
license=('Apache')
depends=('afl' 'coreutils' 'python' 'rsync')
optdepends=('sqlite: database support'
            'screen: interactive/screen mode support'
            'gdb: gdb script execution support')
makedepends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rc0r/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('e15bcaf7ed114696c09243b3e145eb24c89802abfc8430131c4d9888b824b0b3')
sha512sums=('d2524d6e34d296c7b73dcc4999d74dcbb7057a2de60f557c8af78a5fc3fca715afa13564358b3b388c43efede15c4e05fa18ba6d852eca1909bae0f4d099f620')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  rm -rf "${pkgdir}"/usr/lib/python*/site-packages/tests
  install -Dm 644 README.md docs/CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 config/* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
