# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hacking
pkgname=('python-hacking' 'python2-hacking')
pkgver=2.0.0
pkgrel=1
arch=('any')
pkgdesc='OpenStack Hacking Guideline Enforcement'
url='https://docs.openstack.org/developer/hacking'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'flake8' 'python2-flake8')
checkdepends=('python-oslotest' 'python2-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack-dev/hacking/archive/$pkgver.tar.gz")
sha512sums=('33cb147afe122fa55636122fc858704f052e469c9091fe8bc5763937f9e6150d7686c2c253fca6319ff3108dae1ae19eebfe9d425c722762599033696f9180c0')

export PBR_VERSION=$pkgver

prepare() {
  cp -a hacking-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/hacking-$pkgver
  python setup.py build

  cd "$srcdir"/hacking-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hacking-$pkgver
  stestr run

  cd "$srcdir"/hacking-$pkgver-py2
  PYTHON=python2 stestr run
}

package_python-hacking() {
  depends=('python-six' 'flake8')

  cd hacking-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-hacking() {
  depends=('python2-six' 'python2-flake8')

  cd hacking-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
