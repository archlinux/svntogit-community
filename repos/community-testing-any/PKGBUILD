# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-pydocstyle
pkgver=5.1.1
pkgrel=3
pkgdesc="Docstring style checker"
arch=("any")
url="http://www.pydocstyle.org/"
license=("MIT")
depends=("python-snowballstemmer")
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-pytest-runner" "python-mock" "python-pip" "python-snowballstemmer")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pydocstyle/archive/${pkgver}.tar.gz")
sha256sums=('904bce3084dc4cb2f3802d1795f3fa8aa1e01e83823e117e44c6a93c4aa60a17')

build() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/pydocstyle-${pkgver}"
    python setup.py pytest --addopts "-k 'not test_integration'"
}

package() {
    cd "pydocstyle-${pkgver}"
    python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
