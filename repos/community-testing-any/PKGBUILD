# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=locket
pkgname=python-${_pkg}
pkgver=0.2.0
pkgrel=3
pkgdesc="File-based locks for Python"
arch=(any)
url="https://github.com/mwilliamson/locket.py"
license=(BSD)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(python-nose python-spur)
# No tests in pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('85ae716ac9e929e03dc8ed5e2725159d2d7570735fb2d0981ad453910b574ed1')

build() {
  cd ${_pkg}.py-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}.py-${pkgver}
  # Failures with 3.8, dead upstream
  nosetests || echo "Tests failed"
}

package() {
  cd ${_pkg}.py-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
