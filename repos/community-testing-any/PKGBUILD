# Maintainer: David Runge <dvzrv@archlinux.org>

_name=nose2
pkgname=python-nose2
pkgver=0.12.0
pkgrel=3
pkgdesc="The next generation of nicer testing for python"
arch=(any)
url="https://github.com/nose-devs/nose2"
license=(BSD)
depends=(python-coverage python-six)
makedepends=(python-build python-installer python-setuptools python-wheel)
optdepends=(
  'python-coverage: for coverage_plugin'
  'python-setuptools: for eggdiscovery plugin'
  'python-sphinx: for doc'
  'python-sphinx_rtd_theme: for doc'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('73ddcf62af6db7c1b34fef733d8efc4bd223470cdac3983c483a08625dbb06c18dd6e3418ebbf85db7497cf05ab22c08653b3a65973212c158a27590265b738f')
b2sums=('834a9b2ad09d3e411b5e80a9eaa05204a1f3d0ea43feee2240498c1642e9039e3b0a510ec5cf1c2e81c510b8975430a81302f305a23345bd504fea0d860318e5')

prepare() {
  # devendor python-six
  find $_name-$pkgver -type f -iname "*.py" -exec sed -e 's/from nose2._vendor //' -i {} +
  rm -frv $_name-$pkgver/$_name/_vendor
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="$PWD/test_dir"

  cd $_name-$pkgver
  # install to temporary location to be able to run tests with nose2 itself
  python -m installer --destdir="$_test_dir" dist/*.whl
  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  cd "$_test_dir/$_site_packages/nose2"
  PATH="$_test_dir/usr/bin:$PATH" nose2 -v --start-dir tests/unit
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 license.txt -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 {AUTHORS,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
}
