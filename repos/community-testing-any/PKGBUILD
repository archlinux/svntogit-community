# Maintainer: Andrew Crerar <crerar@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hideaki Takahashi <mymelo+aur@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=python-docker
pkgver=4.3.1
pkgrel=3
pkgdesc="A Python library for the Docker Engine API"
arch=('any')
url="https://github.com/docker/docker-py"
license=("Apache")
depends=('python-requests' 'python-six' 'python-websocket-client' 'python-docker-pycreds' 'python-paramiko')
makedepends=('python-pip' 'python-requests' 'python-six' 'python-websocket-client'
             'python-docker-pycreds')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-timeout'
              'python-mock' 'python-coverage' 'flake8')
provides=('python-docker-py')
conflicts=('python-docker-py')
replaces=('python-docker-py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/docker-py/archive/$pkgver.tar.gz")
sha512sums=('f82d8681edc866e73dcc0557a2be7ccfcb68e6add3cfd034d62f9639646c5d396e8e2b29df2b42006ee92adc8b432ca6a80bf2a10b54af42f33fe5ea8ed4a912')

prepare() {
  sed -i 's/==.*$//' docker-py-"$pkgver"/{,test-}requirements.txt
}

build() {
  cd "$srcdir"/docker-py-"$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir"/docker-py-"$pkgver"
  py.test --cov=docker tests/unit
}

package() {
  cd docker-py-"$pkgver"
  python setup.py install -O1 --root="$pkgdir"
}
