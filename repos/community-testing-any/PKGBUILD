# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

_pkgname=requests-aws4auth
pkgname=python-requests-aws4auth
pkgver=1.1.2
pkgrel=2
pkgdesc='Amazon Web Services version 4 authentication for the Python Requests module'
url='https://github.com/sam-washington/requests-aws4auth'
arch=('any')
license=('MIT')
depends=('python' 'python-requests' 'python-six')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-httpx')
optdepends=('python-httpx')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('73207343f1b43d06f20cd998502046141e462c39a1bd0c6421039805afac3b2802cfa585afb8fcab0383f9159f1756b038de4d856ab9fd2cc7fd60b3722ba896')
b2sums=('b9a6b49bae014f86cbfdba5cfd8596b90ba71ffc6595592ccad8692c5c3df9206a075e08b60f85df14c8b7b55191354d86a37ae6350c122980712ddcfc9c8510')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed '/./{H;$!d} ; x ; s/package_data=.*\n.*]},\n//' -i setup.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  python -m pytest -k 'not test_headers_amz_example'
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
