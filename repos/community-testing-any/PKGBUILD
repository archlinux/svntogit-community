# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-qiniu
pkgname=(python-qiniu python2-qiniu)
pkgver=7.1.0
_commit=e0c79e5070ebe2220df1ed3da73062d0fad498eb
pkgrel=2
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("qiniu::git+https://github.com/qiniu/python-sdk#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a qiniu{,-py2}
}

check() {
  # Extracted from .travis.yml
  export QINIU_ACCESS_KEY="QWYn5TFQsLLU1pL5MFEmX3s5DmHdUThav9WyOWOm"
  export QINIU_SECRET_KEY="Bxckh6FA-Fbs9Yt3i3cbKVK22UPBmAOHJcL95pGz"
  export QINIU_TEST_BUCKET="pythonsdk"
  export QINIU_TEST_DOMAIN="pythonsdk.qiniudn.com"
  export QINIU_TEST_ENV="travis"

  cd "$srcdir"/qiniu
  python setup.py ptr

  cd "$srcdir"/qiniu-py2
  python2 setup.py ptr
}

package_python-qiniu() {
  depends=('python-requests' 'python-setuptools')

  cd qiniu
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-qiniu() {
  depends=('python2-requests' 'python2-setuptools')

  cd qiniu-py2
  python2 setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Don't conflict with python 3.x version
  mv "$pkgdir"/usr/bin/qiniupy{,2}
}
