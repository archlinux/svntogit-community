# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rouge
pkgname=ruby-rouge
pkgver=3.26.0
pkgrel=2
pkgdesc='Pure-ruby code highlighter that is compatible with pygments'
url='http://rouge.jneen.net/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/jneen/rouge/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('bd6006b11924b3705dc142922878dc87af25ceed8a153ad9691ad2595144ae1f3f909574799002601928e985a6650fe4c8d93e05a1e8e26bdb10e9b2f9a28765')
b2sums=('9739c229788d615f4875caf5d38c91ca30b4fe5703c577b2e7aa5d54bd625f33e998ee887d375d357e72c9f05ac9a284b88ce7902c6fae422e8196098a993259')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
