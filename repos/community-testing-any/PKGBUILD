# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=RangeHTTPServer
pkgname=python-rangehttpserver
pkgver=1.2.0
pkgrel=6
pkgdesc='SimpleHTTPServer with support for Range requests'
url='https://github.com/danvk/RangeHTTPServer/'
arch=('any')
license=('Apache')
depends=('python')
makedepends=('python' 'python-setuptools')
checkdepends=('python-nose' 'python-requests')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/danvk/RangeHTTPServer//archive/${pkgver}.tar.gz)
sha256sums=('17aad58bec8ccd95f9d5bc9cff2bf3597d002eff08e1ced558c8756b61467a94')
sha512sums=('5964687bb3e8524ab3eea0b71eaefe189f795ce73ba438510936480bbdfda3dcfafe97ace66b35b3b235864f71744fe20c8fb90f881628ad67a53162e22b59bb')

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    nosetests
  )
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}
