# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=colorclass
pkgbase=python-colorclass
pkgname=('python-colorclass' 'python2-colorclass')
pkgver=2.2.0
pkgrel=4
pkgdesc='Yet another ANSI color text library for colorful worry-free console applications'
url='https://github.com/Robpol86/colorclass'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
checkdepends=('python-pytest' 'python-docopt'
              'python2-pytest' 'python2-docopt')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/Robpol86/colorclass/archive/v${pkgver}.tar.gz)
sha512sums=('c627cdf6abdb5a60e9ecc1fb3b669898edc027fdc3f8cde31be2e10402ee308c2067fcd50d1228759e93fc58c521b7fc12e6b259233a17cf99f55cda26279fed')

prepare() {
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    py.test
  )
  (cd ${_pyname}-${pkgver}-py2
    py.test2
  )
}

package_python-colorclass() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst example.py -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-colorclass() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst example.py -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
