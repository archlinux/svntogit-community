# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-location
pkgname=('python-zope-location' 'python2-zope-location')
pkgver=4.1.0
pkgrel=2
pkgdesc="Special objects that have a structural location"
arch=('any')
url="https://github.com/zopefoundation/zope.location"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-schema' 'python2-zope-schema' 'python-zope-proxy'
             'python2-zope-proxy')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner' 'python-zope-copy'
              'python2-zope-copy' 'python-zope-configuration' 'python2-zope-configuration'
              'python-zope-component' 'python2-zope-component')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.location/archive/$pkgver.tar.gz")
sha512sums=('3bfc10ac3072c5b696c19d54e6238627e959932d9f9ca1ae841440f92d7382f7508e6a3f5bc89ef83417dd23b69018c0469683573e5103cc5826cb6630323f01')

prepare() {
  cp -a zope.location-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.location-$pkgver
  python setup.py build

  cd "$srcdir"/zope.location-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.location-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.location-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-location() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema' 'python-zope-proxy')

  cd zope.location-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-location() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-schema' 'python2-zope-proxy')

  cd zope.location-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
