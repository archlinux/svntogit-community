# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Ã‰tienne Deparis <etienne [at] depar.is>
# Contributor: Patrice Peterson <runiq at archlinux dot us>
# Contributor: Patrick Burroughs <celticmadman at gmail dot com>

_name=tzlocal
pkgbase=python-tzlocal
pkgname=("python-tzlocal" "python2-tzlocal")
pkgver=1.5.1
pkgrel=4
pkgdesc="Tzinfo object for the local timezone"
arch=('any')
url="https://pypi.python.org/pypi/tzlocal"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-mock' 'python2-mock')
source=("${_name}-${pkgver}.tar.gz::https://github.com/regebro/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('30dd1e3973c6fa0e0bde5a0365223506032177e96f997116edfe1fbdcff25cd735c90f205f0b95359a095749f1d1ac077903201533d460a72277891174cc1975')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -a "${pkgname}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-tzlocal() {
  depends=('python-pytz')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm644 {CHANGES.txt,README.rst}
}

package_python2-tzlocal() {
  depends=('python2-pytz')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {CHANGES.txt,README.rst}
}

# vim:set ts=2 sw=2 et:

