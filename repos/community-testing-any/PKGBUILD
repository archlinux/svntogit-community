# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-boto3
# UPDATE_BLOCKED: blocked as python-botocore is blocked
pkgver=1.26.89
pkgrel=3
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
depends=('python' 'python-botocore' 'python-jmespath' 'python-s3transfer')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('02ee02b05023e9184f779ab8ff21afa6f7e56f7d2e883d021b2567676c5fc57cad6b475d55316a2017c2a837c41d2f5604824f440060e2f96804c5a61ccdd143')

build() {
  cd boto3-$pkgver
  python setup.py build
}

check() {
  cd boto3-$pkgver
  # Many integration tests need real credentials
  pytest tests -n auto --ignore=tests/integration
}

package() {
  cd boto3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
