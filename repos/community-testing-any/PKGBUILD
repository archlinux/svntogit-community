# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslosphinx
pkgname=(python-oslosphinx python2-oslosphinx)
pkgver=4.18.0
pkgrel=2
pkgdesc="OpenStack Sphinx Extensions and Theme"
arch=('any')
url="https://github.com/openstack/oslosphinx"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-requests' 'python2-requests' 'python-six' 'python2-six')
checkdepends=('python-sphinx' 'python2-sphinx')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslosphinx/archive/$pkgver.tar.gz")
sha512sums=('47bd8912c47400538839e5b9ac0a81ecb2b7bbf30e83daade32046b3a1a70d892b53b7418ee59270fb36cddf6103214cef64d86c52602070c04ade2af29b3122')

prepare() {
  cp -a oslosphinx-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslosphinx-$pkgver
  python setup.py build

  cd "$srcdir"/oslosphinx-$pkgver-py2
  python2 setup.py build
}

package_python-oslosphinx() {
  depends=('python-six' 'python-requests')

  cd "$srcdir"/oslosphinx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslosphinx() {
  depends=('python2-six' 'python2-requests')

  cd "$srcdir"/oslosphinx-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
