# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-osc-lib
pkgver=1.14.1
pkgrel=3
pkgdesc="OpenStackClient Library"
arch=('any')
url="https://docs.openstack.org/osc-lib/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneauth1'
         'python-os-client-config' 'python-oslo-i18n' 'python-oslo-utils' 'python-stevedore'
         'python-simplejson' 'python-openstacksdk')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/osc-lib/archive/$pkgver.tar.gz")
sha512sums=('9e980c8d91ed35d9fd988c924c693fedce259b5d12ee9456dd0e76cf3bd6c9a22d378aa002bff9e9d9e8bcdd6836c773e9f366e56f4ec16f4f5702f133069130')

export PBR_VERSION=$pkgver

prepare() {
  # https://storyboard.openstack.org/#!/story/2003142
  sed -i 's/issubclass/isinstance/' osc-lib-$pkgver/osc_lib/utils/__init__.py

  sed -i 's/assertItemsEqual/assertCountEqual/' osc-lib-$pkgver/osc_lib/tests/{cli,utils}/*.py
}

build() {
  cd osc-lib-$pkgver
  python setup.py build
}

check() {
  cd osc-lib-$pkgver
  stestr run
}

package() {
  cd osc-lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
