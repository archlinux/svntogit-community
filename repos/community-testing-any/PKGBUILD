# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.4.1
pkgrel=2
pkgdesc='Efficient and thread-safe code loader for Ruby'
arch=('any')
url="https://github.com/fxn/zeitwerk"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-rdoc')
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=('ruby-minitest' 'ruby-minitest-focus' 'ruby-minitest-reporters')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fxn/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('12d513fd16532ce510d83cf32710d734d24002bd5e77e0a37157e6521f3af67d379e915498c61114b5f9943763b59bbf38b7b6b2ea82957858129a6120c756bd')
b2sums=('619673331f866b1e566603727189cac02fb08e856ca10504c97dd6dcc2cff8e24d0b2bb6b462f62dc88696b96beb1df04161f72da691118c0d8414313d2ba290')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname-$pkgver"
  gem build "${_name}.gemspec"
}

# check() {
#   cd "$pkgname-$pkgver"
#   rake test
# }

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
