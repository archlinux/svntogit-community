# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Igor Ramos Tiburcio <irtigor@yahoo.com.br>
# Contributor: Wieland Hoffmann <themineo+aur@gmail.com>
# Contributor: Hervé Cauwelier <herve ¤ oursours.net>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-py
pkgname=('python-py' 'python2-py')
pkgver=1.8.1
pkgrel=1
pkgdesc="library with cross-python path, ini-parsing, io, code, log facilities"
arch=('any')
license=('MIT')
url="https://py.readthedocs.io/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
             'python2-setuptools-scm' 'python-iniconfig' 'python2-iniconfig'
             'python-apipkg' 'python2-apipkg')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/py/archive/$pkgver.tar.gz"
        py-pytest-4.patch::https://github.com/pytest-dev/py/pull/222.patch)
sha512sums=('6ebe06c50e1279e1a73cf0ef97d75bafc8cd4c7d7990ed5add84aaba52523046f989cf29a11ae86ec56acbb5f0cb43147a0738108902c5eeaddaa8497e5ab32e'
            'cabfdb8cd2e229bfbd5e770a145dde60f9e649f683f8e3d57d2cf393a4d246fcb0eae4596cbbee010de97bfca26470a5dd4280d0f9e34d7f30df19b09f5ea8b6')

prepare() {
  patch -d py-$pkgver -p1 -i ../py-pytest-4.patch
  rm -r py-$pkgver/py/_vendored_packages
  cp -a py-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/py-$pkgver
  python setup.py build

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py build
}

check() {
  # Known failure: https://github.com/pytest-dev/py/issues/104

  cd "$srcdir"/py-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/py-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-py() {
  depends=('python-iniconfig' 'python-apipkg')

  cd py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-py() {
  depends=('python2-iniconfig' 'python2-apipkg')

  cd py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
