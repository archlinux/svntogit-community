# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=python-argparse-manpage
pkgver=4.1
pkgrel=2
pkgdesc='Automatically build man-pages for your Python project'
url='https://github.com/praiskup/argparse-manpage'
arch=('any')
license=('Apache')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest' 'python-pip')
source=("https://github.com/praiskup/argparse-manpage/releases/download/v${pkgver}/argparse-manpage-${pkgver}.tar.gz")
sha512sums=('99c5b2acc9c36eb5ae58e1270ab3178139f646fff99737c7aba64c4665d6f11659631d17af4e74ab103affcf86ea552999ea94f5a15aa504e8514853cb4325ae')

build() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python -m build --wheel
}

check() {
  cd "$srcdir/argparse-manpage-$pkgver"
  pytest -v
}

package() {
  cd "$srcdir/argparse-manpage-$pkgver"
  python -m installer --prefix=/usr --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
