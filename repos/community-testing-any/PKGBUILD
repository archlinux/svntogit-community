# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-wsproto
pkgver=1.0.0
pkgrel=3
pkgdesc="WebSockets state-machine based protocol implementation"
url="https://github.com/python-hyper/wsproto"
license=('MIT')
arch=('any')
depends=('python-h11')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/w/wsproto/wsproto-$pkgver.tar.gz")
sha512sums=('36f8c4eebf84bfe869f7124330898d22416b423f47396f2e3f72069e14f1763514e1df700c3e8ee81467cf973197e10143e088978d0688930fa98518af8773fb')

build() {
  cd wsproto-$pkgver
  python setup.py build
}

check() {
  cd wsproto-$pkgver
  python setup.py pytest
}

package() {
  cd wsproto-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
