# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=3.6.2
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py' 'python-pluggy'
             'python2-pluggy' 'python-attrs' 'python2-attrs' 'python-more-itertools'
             'python2-more-itertools' 'python-atomicwrites' 'python2-atomicwrites'
             'python2-funcsigs')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-tox'
              'python2-tox' 'python-yaml' 'python2-yaml' 'python-pytest-xdist'
              'python2-pytest-xdist' 'python-twisted' 'python2-twisted' 'python-requests'
              'python2-requests' 'python-hypothesis' 'python2-hypothesis')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('14044722226623a37668f49d9e9916e0abc29b87b85c58d34fb4d697017d5c64adb85185d58449493859cd1632f89318b80baed667a452efabff744153ce3106')

prepare() {
  cp -a pytest-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytest-$pkgver
  PYTHONPATH="$PWD"/build/lib python src/pytest.py

  cd "$srcdir"/pytest-$pkgver-py2
  PYTHONPATH="$PWD"/build/lib python2 src/pytest.py
}

package_python-pytest() {
  depends=('python-py' 'python-setuptools' 'python-pluggy' 'python-attrs' 'python-more-itertools'
           'python-atomicwrites')

  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest() {
  depends=('python2-py' 'python2-setuptools' 'python2-pluggy' 'python2-attrs'
           'python2-more-itertools' 'python2-atomicwrites' 'python2-funcsigs')

  cd pytest-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/py.test{,2}
  mv "$pkgdir"/usr/bin/pytest{,2}
}
