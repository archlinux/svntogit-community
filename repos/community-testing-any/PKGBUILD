# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-promise
pkgver=2.3.0
pkgrel=6
pkgdesc='Ultra-performant Promise implementation in Python'
url=https://github.com/syrusakbary/promise
arch=(any)
license=(MIT)
depends=(
  python-six
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/syrusakbary/promise.git#tag=ad8ebe68cd9c9686793e3449457f175a0fe43226)
sha256sums=(SKIP)

pkgver() {
  cd promise

  git describe --tags | sed 's/^v//'
}

build() {
  cd promise

  python setup.py build
}

package() {
  cd promise

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-promise/
}

# vim: ts=2 sw=2 et:
