# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=jedi-language-server
pkgver=0.31.2
pkgrel=1
pkgdesc='Language server for Jedi'
arch=('any')
url=https://github.com/pappasam/jedi-language-server
license=('MIT')
depends=('python-docstring-to-markdown' 'python-jedi' 'python-pygls')
makedepends=('python-pip' 'python-poetry')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('da9cc9375167f2e38bfef5f79d66829cc864f47967e3eb45206ad77e1d315f95')
b2sums=('f7ca80512562f911944b58932d85819185ea339a06875e7c7d5e85958cf6e790548bc2521a6101d3f5d270ae5326db2dca1edd4fb40d44c1b9e62e6735361835')

prepare() {
  cd $pkgname-$pkgver
  # Avoid installing files directly under site-packages/
  sed -i '/include = \["README.md"\]/d' pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  poetry build --format wheel
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  export PIP_CONFIG_FILE=/dev/null
  pip install --isolated \
              --no-deps \
              --root="$pkgdir" \
              --ignore-installed \
              --no-warn-script-location dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s $site_packages/${pkgname//-/_}-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
