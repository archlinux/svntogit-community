# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Danilo Bargen <gezuru@gmail.com>

pkgname=vim-jedi
_reponame=jedi-vim
pkgver=0.8.0
pkgrel=2
pkgdesc='Vim plugin for jedi, an awesome Python autocompletion.'
arch=('any')
url="https://github.com/davidhalter/${_reponame}"
license=('MIT')
depends=('vim' 'python-jedi' 'python')
optdepends=('vim-supertab: for tab completion'
            'python2-jedi: for Python 2 support')
groups=('vim-plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/davidhalter/${_reponame}/archive/${pkgver}.tar.gz")
sha256sums=('d1f000b8c93c915a1bcc379a4784e68e58192747a6fddc992af04245f68743b6')
install=${pkgname}.install

package() {
  cd "$srcdir/$_reponame-$pkgver"

  install -d "${pkgdir}/usr/share/vim/vimfiles/doc"

  cp -dp --no-preserve=ownership jedi_vim.py initialize.py "${pkgdir}/usr/share/vim/vimfiles/"
  cp -dp --no-preserve=ownership doc/jedi-vim.txt "${pkgdir}/usr/share/vim/vimfiles/doc/"
  cp -dpr --no-preserve=ownership after "${pkgdir}/usr/share/vim/vimfiles/after"
  cp -dpr --no-preserve=ownership autoload "${pkgdir}/usr/share/vim/vimfiles/autoload"
  cp -dpr --no-preserve=ownership ftplugin "${pkgdir}/usr/share/vim/vimfiles/ftplugin"
  cp -dpr --no-preserve=ownership plugin "${pkgdir}/usr/share/vim/vimfiles/plugin"

  # install license file
  install -Dm0644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
