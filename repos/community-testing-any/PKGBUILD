# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ekin Dursun <ekindursun@gmail.com>
# Contributor: Gregoire Seux <grego_aur@familleseux.net>

pkgname=python-astral
pkgver=3.2
pkgrel=2
pkgdesc='Python calculations for the position of the sun and moon'
arch=(any)
url=https://github.com/sffjunkie/astral
license=(APACHE)
depends=(python-requests)
makedepends=(
  git
  python-build
  python-installer
  python-poetry-core
)
_tag=0be1187d09aadfdadc1b7331b918082213764b5d
source=(git+https://github.com/sffjunkie/astral.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd astral
  git describe --tags
}

build() {
  cd astral
  python -m build --wheel --no-isolation
}

package() {
  python -m installer --destdir="${pkgdir}" astral/dist/*.whl
}

# vim: ts=2 sw=2 et:
