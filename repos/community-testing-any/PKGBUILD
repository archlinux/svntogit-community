# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=('python-virtualenv-clone' 'python2-virtualenv-clone')
pkgver=0.3.0
pkgrel=2
pkgdesc="A script for cloning a non-relocatable virtualenv."
arch=('any')
url="https://github.com/edwardgeorge/virtualenv-clone"
license=('GPL')
makedepends=('python-distribute' 'python2-distribute')
options=('!emptydirs')
source=(https://files.pythonhosted.org/packages/source/v/virtualenv-clone/virtualenv-clone-$pkgver.tar.gz)
sha256sums=('b5cfe535d14dc68dfc1d1bb4ac1209ea28235b91156e2bba8e250d291c3fb4f8')

package_python-virtualenv-clone() {
  depends=('python-distribute')

  cd "$srcdir/virtualenv-clone-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-virtualenv-clone() {
  depends=('python2')

  cd "$srcdir/virtualenv-clone-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1

  # virtualenv-clone is provided by the python-virtualenv-clone package
  rm "$pkgdir/usr/bin/virtualenv-clone"
}

# vim:set ts=2 sw=2 et:
