# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol 'Kenji Takahashi' Wo≈∫niak <kenji.sx>
# Contributor: Pierre Gueth <pierre.gueth@gmail.com>

pkgbase=python-redis
pkgname=('python-redis' 'python2-redis')
pkgver=2.10.6
pkgrel=2
pkgdesc='The Python interface to the Redis key-value store'
arch=('any')
url="http://github.com/andymccurdy/redis-py"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock' 'python-pifpaf' 'redis')
source=("https://pypi.io/packages/source/r/redis/redis-$pkgver.tar.gz")
sha512sums=('e7df464bc3b26e23f6a0d2d2896306c1e4792b9a2a4ecaea6dd8690ffa17853cc85345f063307295dd3c2da399f7f203f4b21d785f7e073c0501732257419dad')

prepare() {
  cp -a redis-$pkgver{,-py2}
}

build() {
  cd redis-$pkgver
  python setup.py build

  cd ../redis-$pkgver-py2
  python2 setup.py build
}

check() {
  cd redis-$pkgver
  pifpaf run redis py.test

  cd ../redis-$pkgver-py2
  pifpaf run redis py.test2
}

package_python-redis() {
  depends=('python')

  cd redis-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-redis() {
  depends=('python2')

  cd redis-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
