# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-file-tail
pkgver=1.3
pkgrel=4
pkgdesc='Perl extension for reading from continously updated files'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-io)
url=https://metacpan.org/release/File-Tail
source=("http://search.cpan.org/CPAN/authors/id/M/MG/MGRABNAR/File-Tail-$pkgver.tar.gz")
md5sums=('252635f89ec18cb871c0b45ed64b3f63')
sha512sums=('942f87880d0d8b79c521248cdbc449daafbd117a2a3700e2dbcc7fe7a6046e5783e8616b6c32de513f85a56663b63c4dde217e9e124216ba6a605c645db2bde9')
_ddir="File-Tail-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
