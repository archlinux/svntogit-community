# $Id$
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Pappa <jcberg@gmail.com >

pkgbase=python-xlib
pkgname=('python-xlib' 'python2-xlib')
pkgver=0.23
pkgrel=2
pkgdesc="A fully functional X client library for Python programs"
url="https://github.com/python-xlib/python-xlib"
arch=('any')
license=('LGPL')
makedepends=('python2' 'python2-setuptools'
             'python' 'python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-xlib/python-xlib/archive/$pkgver.tar.gz")
sha256sums=('a345fad5003adf6d06c296a6fb917b6b7cc21f17135f11418bf603e15bbde118')

prepare() {
  cp -a python-xlib-$pkgver{,-py2}
}

build(){
  cd "$srcdir/python-xlib-$pkgver"
  python setup.py build

  cd "$srcdir/python-xlib-$pkgver-py2"
  python2 setup.py build
}

package_python2-xlib() {
  depends=('python2' 'python2-six')

  cd "python-xlib-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python-xlib() {
  depends=('python' 'python-six')

  cd "python-xlib-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
