# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
pkgver=1.2.1
pkgrel=2
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-aioitertools)
makedepends=(python-setuptools)
checkdepends=(python-moto python-pytest python-pytest-asyncio
              # moto optdepends
              python-docker python-yaml python-flask python-flask-cors)
source=("https://github.com/aio-libs/aiobotocore/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        unittest-mock.diff
        fix-sqs-tests.diff)
sha256sums=('bdd142e30c73232d17e257b8f01d1b6efd42bd7ef084b44957d86cbd830d6aab'
            'ffb6b7e922632f4c2de5c93f61ab53e1730cfe151b0a2dde5dfce6260405866e'
            'd8446453cb399893224d508ea1055d9a5401c0bf2c95f6fc5f09d400f56975dc')

prepare() {
  cd $_pkgname-$pkgver
  # community/python-mock is outdated and does not include needed AsyncMock.
  # Use mock from stdlib instead. It has AsyncMock since 3.8
  patch -Np1 -i ../unittest-mock.diff

  # Correctly receive SQS messages. Discovered by https://github.com/spulec/moto/pull/3303
  patch -Np1 -i ../fix-sqs-tests.diff

  # Disable dependency pinning
  # Upstream tracking issue: https://github.com/aio-libs/aiobotocore/issues/670
  sed --in-place=.orig -r "s#'(botocore.*),<.*',#'\1',#" setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  export PYTHONPATH="$PWD"
  # test_lambda uses moto.awslambda, which requires a running Docker service
  # See: https://github.com/spulec/moto/issues/3276
  pytest -m moto tests \
      --ignore=tests/test_patches.py \
      --ignore=tests/test_lambda.py
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
