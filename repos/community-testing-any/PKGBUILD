# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-eventlet
_pyname=eventlet
pkgver=0.31.1
pkgrel=2
pkgdesc='Highly concurrent networking library'
url='https://eventlet.net'
arch=('any')
license=('MIT')
depends=('python' 'python-greenlet' 'python-monotonic' 'python-dnspython')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq')
optdepends=(
  'python-psycopg2: non-blocking PostgreSQL support'
  'python-pyopenssl: non-blocking SSL support'
  'python-httplib2: non-blocking HTTP support'
  'python-pyzmq: non-blocking ZeroMQ support'
  'python-dnspython: non-blocking DNS support'
)
options=('!makeflags')
source=(https://github.com/eventlet/eventlet/archive/v${pkgver}/${_pyname}-${pkgver}.tar.gz
        dnspython.patch::https://github.com/eventlet/eventlet/pull/722.patch)
sha512sums=('ec284e9184d1901fa4b179af685df7559d699e827b7e5da869a7ee419a36c190163c0fdfe8f72254b308662d3ce3c6c0e128f338d5147bf8daa2b5f21ebce960'
            'e5c8aad3bc4074867e9c854f182c414d5a49f48a667ef619472cd0f8e6eabff483a908d325f6822f2cc612fd4ddff5bf5a068640d7074ce264abfc1f9838aedf')
b2sums=('20ed00f4e7b07fd51218125058ed045ae3730c882f1c957127d29f0006e63b8998e65863ee33c36912cddf4efde3fa91b7797a2da63ac36009c4f7165155e854'
        '0384edd8bed3b7f772765002dfe0b2d36d4871579753c0ac47644c9f0478c6a091b961b41374f015b94c0ad88a035ff4707bb1f73cd813684915fcb9d071efcc')

prepare() {
  cd ${_pyname}-${pkgver}
  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i tests/*_test.py

  # dns python 2.1.0 compatibility https://github.com/eventlet/eventlet/pull/722/files
  patch -Np1 -i ${srcdir}/dnspython.patch
}

build() {
  cd ${_pyname}-${pkgver}
  export PYTHONHASHSEED=0
  python setup.py build
  make -C doc text
}

check() {
  cd ${_pyname}-${pkgver}
  nosetests -sv tests
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
