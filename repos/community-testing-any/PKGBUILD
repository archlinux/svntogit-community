# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcrpy
pkgver=4.2.1
pkgrel=3
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-httpbin')
source=("https://github.com/kevin1024/vcrpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('59231e5164cda3099b18d099f0ef97e062e6401e52e5dfc4022aa89ca02881e9e6e3c782f12c288f5281b3b3bf175dc33a267bc6d2b40791582a1221f5d7aaab')

build() {
  cd vcrpy-$pkgver
  python setup.py build
}

check() {
  cd vcrpy-$pkgver
  python -m pytest
}

package() {
  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
