# Maintainer: Moritz Lipp <mlq@pwmt.org>
# Co-Maintainer: fordprefect <fordprefect@dukun.de>
# Contributor: Thor77 <thor77@thor77.org>

pkgname=radicale
_name=Radicale
pkgver=2.1.8
pkgrel=3
pkgdesc='Simple calendar (CalDAV) and contact (CardDAV) server'
arch=('any')
url='http://www.radicale.org/'
license=('GPL3')
depends=('python-bcrypt' 'python-passlib' 'python-vobject')
makedepends=('python-setuptools')
checkdepends=('python-bcrypt' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-isort' 'python-pytest-runner')
backup=('etc/radicale/config')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Kozea/${pkgname}/archive/${pkgver}.tar.gz"
        'fix-flake8-test.diff'
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf"
        "${pkgname}.service")
sha512sums=('70d7c890a733fb27d007fa593d0140bfd3bde1ff46235252a2cdd3ceaf3bc9f998acb62e131f9bc71aa1c55f51c297e4f494cb22f8cba3c5d8d90069e4957c87'
            'f951fff9187448ea3052395b6999f19cfc48dbf5cfb24359f646e1fba8a3556b40c7174f91deee4a18f5794c5fd8e45ee555a569e4320d758712ee581df6feb1'
            '444489ba929680490884e338acc311fb61d81f9d9f0282c92c40a80317af9e61123e4764e803d1698876417170efd40bfe7f56408570891c4e426de3be17c302'
            '4570ed393b73bc5a3fd571947acae78b96b5e17f659e3169f542b9c481635680c724d5c6c0952661fae063caf04c786afe7043c5052945e7a00bb66326dec016'
            '6a3452009bebc0f8680ea8e64b9a398c04f3fb6b20752c377392e22641845a04dbfda78d586271c9a25bd7d5ba42b7a8f74774afed68732f19ddd764f53e11cc')

prepare() {
  mv -v ${_name}-${pkgver} ${pkgname}-${pkgver}
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-flake8-test.diff"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  python setup.py test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"

    install -vDm644 config "${pkgdir}/etc/${pkgname}/config"
    install -vDm644 "${pkgname}.fcgi" "${pkgdir}/usr/share/${pkgname}/${pkgname}.fcgi"
    install -vDm644 "${pkgname}.wsgi" "${pkgdir}/usr/share/${pkgname}/${pkgname}.wsgi"
    install -t "${pkgdir}/usr/share/doc/${pkgname}/" -vDm644 {NEWS,README}.md
    install -vDm644 "${srcdir}/${pkgname}.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
    install -vDm644 "${srcdir}/${pkgname}-sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
    install -vDm644 "${srcdir}/${pkgname}-tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}

