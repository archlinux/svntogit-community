# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Roger Duran <rogerduran@gmail.com>

pkgname=python-xcffib
pkgver=0.10.1
pkgrel=3
pkgdesc="Python3 drop in replacement for xpyb, an XCB python binding"
arch=('any')
url="https://pypi.python.org/pypi/xcffib"
license=('Apache')
depends=('python-six' 'python-cffi' 'libxcb')
makedepends=('python-setuptools' 'python-cffi' 'python-six' 'libxcb' 'haskell-xcffib')
checkdepends=('python-nose' 'xorg-server-xvfb' 'xorg-xeyes')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tych0/xcffib/archive/v$pkgver.tar.gz")
sha512sums=('62bf10d35b1afc42f91e4f404a2eba6355ca55567dc1a1e8f5dc5b49bec8e1713637ada1c299fab638df1f729085bba3c64a9868ad85486574fa4689745c9d04')

build() {
  cd "$srcdir"/xcffib-$pkgver
  make xcffib GEN=xcffibgen CABAL=true
  python setup.py build
}

check() {
  cd "$srcdir"/xcffib-$pkgver
  xvfb-run nosetests3
}

package() {
  cd xcffib-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}
