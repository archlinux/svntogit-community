# $Id$
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lilydjwg <lilydjwg@gmail.com>

pkgbase=python-dateutil
pkgname=('python-dateutil' 'python2-dateutil')
pkgver=2.7.3
pkgrel=2
pkgdesc="Provides powerful extensions to the standard datetime module"
arch=('any')
license=('BSD' 'Apache')
url="https://github.com/dateutil/dateutil"
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest' 'python-freezegun' 'python-hypothesis'
              'python2-pytest' 'python2-freezegun' 'python2-hypothesis')
source=("https://files.pythonhosted.org/packages/source/${pkgname:0:1}/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('e27001de32f627c22380a688bcc43ce83504a7bc5da472209b4c70f02829f0b8'
            'SKIP')
validpgpkeys=('6B49ACBADCF6BD1CA20667ABCD54FCE3D964BEFB') # Paul Ganssle <paul@ganssle.io>

prepare() {
  cd "$srcdir"

  cp -r  python-dateutil-$pkgver python2-dateutil-$pkgver
}

build() {
  cd "$srcdir"/python-dateutil-$pkgver
  python setup.py build

  cd "$srcdir"/python2-dateutil-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-dateutil-$pkgver
  pytest

  cd "$srcdir"/python2-dateutil-$pkgver
  pytest2
}

package_python-dateutil() {
depends=('python-six')
  cd "$srcdir"/python-dateutil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-dateutil() {
depends=('python2-six')
  cd "$srcdir"/python2-dateutil-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
