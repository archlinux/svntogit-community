# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-kaitaistruct
pkgname=('python-kaitaistruct' 'python2-kaitaistruct')
pkgver=0.8
pkgrel=2
pkgdesc="Kaitai Struct declarative parser generator for binary data: runtime library for Python"
arch=('any')
license=('MIT')
url="http://kaitai.io"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/kaitai-io/kaitai_struct_python_runtime/archive/$pkgver.tar.gz")
sha512sums=('a676a048dd96366dbc89a981dc51c1f6aeb76b9e4fc2d3ebfea527c27dc201803e4f3d8fceff96686e3964b1f3040e4ee9dd2f857a422da1998d98dc325bdbc7')

prepare() {
  cp -a kaitai_struct_python_runtime-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/kaitai_struct_python_runtime-$pkgver
  python setup.py build

  cd "$srcdir"/kaitai_struct_python_runtime-$pkgver-py2
  python2 setup.py build
}

package_python-kaitaistruct() {
  depends=('python')

  cd kaitai_struct_python_runtime-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-kaitaistruct() {
  depends=('python2')

  cd kaitai_struct_python_runtime-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
