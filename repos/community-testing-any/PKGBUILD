# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-passwordsafe
pkgver=3.32.0
pkgrel=2
pkgdesc="Password manager for GNOME which makes use of the KeePass v.4 format"
arch=(any)
url="https://gitlab.gnome.org/World/PasswordSafe"
license=(GPL3)
depends=(libhandy libpwquality python-gobject python-pykeepass)
makedepends=(git gobject-introspection meson)
_commit=f229d4c45546bcd85934659c206c2340531af9d1  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/World/PasswordSafe.git/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd PasswordSafe
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson PasswordSafe build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
