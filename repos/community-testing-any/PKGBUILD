# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgbase=python-paramiko
pkgname=(python-paramiko python2-paramiko)
pkgver=2.7.1
pkgrel=1
pkgdesc="Python module that implements the SSH2 protocol"
url="https://github.com/paramiko/paramiko/"
license=('LGPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-bcrypt' 'python2-bcrypt'
             'python-cryptography' 'python2-cryptography' 'python-pynacl' 'python2-pynacl')
checkdepends=('python-pytest-runner' 'python-pytest-relaxed' 'python-pyasn1' 'python-invoke'
             'python-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/paramiko/paramiko/archive/$pkgver.tar.gz")
sha512sums=('2cebed2420cf9af77cb0d459b64a74adcffcdb15bd58c8fc9243855ae91f43e16706665b64ce2851f6e99e59ca6a47d7299a2aae35a4c9d01ab97b343569c4b2')

prepare() {
  cp -a paramiko-$pkgver{,-py2}

  find paramiko-$pkgver-py2 -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
        -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

check() {
  cd paramiko-$pkgver
  LANG=en_US.UTF-8 python setup.py pytest
}

package_python-paramiko() {
  depends=('python-bcrypt' 'python-cryptography' 'python-pynacl')

  cd paramiko-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/demos
  install -m644 demos/* "$pkgdir"/usr/share/doc/$pkgname/demos
  chmod 755 "$pkgdir"/usr/share/doc/$pkgname/demos/*.py
}

package_python2-paramiko() {
  depends=('python2-bcrypt' 'python2-cryptography' 'python2-pynacl')

  cd paramiko-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/demos
  install -m644 demos/* "$pkgdir"/usr/share/doc/$pkgname/demos
  chmod 755 "$pkgdir"/usr/share/doc/$pkgname/demos/*.py
}
