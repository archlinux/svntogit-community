# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=certbot-dns-hetzner
pkgdesc="Certbot plugin enabling dns-01 challenge on the Hetzner DNS API"
pkgver=1.0.5
pkgrel=1
arch=("any")
url="https://github.com/ctrlaltcoop/certbot-dns-hetzner"
license=("APACHE")
depends=("certbot" "python-requests" "python-zope-interface")
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-requests-mock")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('c2c2398bec560ee5607ee849c89f22fb38c0874c1620bc60c8aa20cf7148afbd')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	python setup.py build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	PYTHONPATH=. pytest
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
	install -Dm644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
	python setup.py install --root="${pkgdir}" -O1 --skip-build
}
