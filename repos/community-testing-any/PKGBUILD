# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Otreblan <otreblain@gmail.com>

_name=pygls
pkgname=python-pygls
pkgver=1.0.2
pkgrel=1
pkgdesc='Pythonic generic implementation of the Language Server Protocol'
arch=('any')
url=https://github.com/openlawlibrary/pygls
license=('APACHE')
depends=('python-lsprotocol' 'python-typeguard')
makedepends=('python-build' 'python-installer' 'python-setuptools-scm'
             'python-toml' 'python-wheel')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('888ed63d1f650b4fc64d603d73d37545386ec533c0caac921aed80f80ea946a4')
b2sums=('0274f5033c8a482ee82afd57944f3bb1e59230fb46654c8f3d5a039f3481b0ede368b6bca373915af78ecec1a79fad50d3b14cb8db5340086ac2c9acf9a6b87f')

prepare() {
  cd $_name-$pkgver
  sed -i 's/typeguard>=3,<4/typeguard>=3/' setup.cfg
}

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
