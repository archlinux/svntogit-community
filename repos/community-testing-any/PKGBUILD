# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=sqlalchemy-i18n
pkgname=python-sqlalchemy-i18n
pkgver=1.1.0
pkgrel=4
pkgdesc='Internationalization extension for SQLAlchemy models'
url='https://github.com/kvesteri/sqlalchemy-i18n'
arch=('any')
license=('BSD')
depends=('python' 'python-sqlalchemy' 'python-sqlalchemy-utils' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-psycopg2' 'python-flexmock' 'python-jinja' 'python-docutils')
source=(https://github.com/kvesteri/sqlalchemy-i18n/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3fd11815366c3e971e0531d684715c4c8998f7f20d69a8b596bd4ca147da6f85ddf4448c99631031ccf60b5f09de6d1daa1e08e94d5c580268e31bf322af97d5')
b2sums=('6554d731768d22389a926f6b33ef49844fc7fddb5cb1b844a9150f9c274ea7f3ac905e02ca5bbf9be99d4ace47ef3848336fbe5062d1bddcb2ee8f75507806fd')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
