# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-prawcore
pkgname=('python-prawcore' 'python2-prawcore')
pkgver=0.13.0
pkgrel=1
pkgdesc="Low-level communication layer for PRAW 4+."
arch=('any')
license=('BSD')
url="https://github.com/praw-dev/prawcore"
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-matchers' 'python2-betamax-matchers'
              'python-betamax-serializers' 'python2-betamax-serializers' 'python-testfixtures'
              'python2-testfixtures')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/praw-dev/prawcore/archive/v$pkgver.tar.gz")
sha512sums=('90e5d2182a4d707685bc3d3b33f6d08e66d030e65faf9de1d2038ac9b1ab6f9955378599c218ddb4849d213074f49f069a2746fd9223227010f3b67d863a4dfe')

prepare() {
  cp -a prawcore-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/prawcore-$pkgver
  python setup.py test

  cd "$srcdir"/prawcore-$pkgver-py2
  python2 setup.py test
}

package_python-prawcore() {
  depends=('python-requests')

  cd prawcore-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-prawcore() {
  depends=('python2-requests')

  cd prawcore-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
