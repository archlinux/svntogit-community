# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
pkgname=python-atpublic
pkgver=2.1.1
pkgrel=3
pkgdesc="@public decorator for Python"
arch=('any')
url="https://gitlab.com/warsaw/public"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
# upstream removed tests in sdist tarball: https://gitlab.com/warsaw/public/-/issues/8
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9621cd08676e57b626ceb4296f4bd4e23261ce51c1e6db3dbba1f3117f629ff9ceccba449fe62c652b7a04fffb63fd641c29840267be7739f6d11ae5d8ef2ebf')
b2sums=('2194b41d25aeb3e3996a1b8f938673dd990dd28f3fe2e9de30bf39ff576d6fa05a78f3d03874edc76e3e52353da11060fdb4d338a1639fa5f1f9f130172840ec')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
