# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2013.5.6
pkgrel=2
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools')
source=(http://pypi.python.org/packages/source/p/pytools/pytools-${pkgver}.tar.gz)

build() {
   cd "${srcdir}"

   cp -a pytools-${pkgver}{,-python2}

   cd "$srcdir/pytools-${pkgver}"
   python setup.py build

   cd "${srcdir}/pytools-${pkgver}-python2"
   python2 setup.py build
}

package_python2-pytools() {
   depends=('python2' 'python2-decorator')
   cd "${srcdir}/pytools-${pkgver}-python2"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}

package_python-pytools(){
   depends=('python' 'python-decorator')
   cd "${srcdir}/pytools-${pkgver}"
   python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}

sha512sums=('47c049360e4b9c11ffbb2225a1ac0882fc491b016638c3d8841280d7ffa4d9fab0fa1f187f42dc1f88ed837e11a866c334805a329f478b0db02b6db3a2746aac')
