# Contributor: Astro Benzene <universebenzene at sina dot com>

_name=jquery
pkgname=python-sphinxcontrib-jquery
pkgver=3.0.0
pkgrel=2
pkgdesc="Extension to include jQuery on newer Sphinx releases"
arch=('any')
url=https://github.com/sphinx-contrib/jquery
license=('custom:0BSD')
depends=('python-sphinx')
makedepends=('python-build' 'python-flit-core' 'python-installer')
checkdepends=('python-pytest')
provides=("python-$_name")
replaces=("python-$_name")
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('5b160fa1434b111683824442a0ecb0da230dba7f29161495859845282e1b6471faa256be6fffc2d34c6fc97e4e188bb1247284613bf65bf322fcb77463b6cf83')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.rst
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENCE
}
