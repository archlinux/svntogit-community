# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Fabius
# Contributor: Adria Arrufat <swiftscythe @t gmail d@t com>
# Contributor: Gordin <9ordin @t gmail d@t com>

pkgname=screenkey
pkgver=1.5
pkgrel=3
pkgdesc="A screencast tool to display your keys inspired by Screenflick"
arch=('any')
url="https://www.thregr.org/~wavexx/software/screenkey/"
license=('GPL3')
depends=('python-gobject' 'gtk3' 'python-cairo' 'libx11' 'dbus-python')
makedepends=('python-setuptools' 'python-wheel' 'python-babel' 'python-distutils-extra')
optdepends=('slop' 'ttf-font-awesome' 'libappindicator-gtk3')
source=("https://www.thregr.org/~wavexx/software/screenkey/releases/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('41958F4116FDD1B43DEE754A2BB7D6F2153410EC')
sha256sums=('cc8471b83f7ba7a754e1da0631cfa9c32b9217da93597afc1c3283e3a1ae4112'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build --optimize=1 --root="$pkgdir/"
}
