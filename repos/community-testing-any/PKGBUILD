# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-localserver
pkgname=('python-pytest-localserver' 'python2-pytest-localserver')
pkgver=0.5.0
pkgrel=3
pkgdesc='py.test plugin to test server connections locally'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-localserver'
makedepends=('python-pytest' 'python2-pytest' 'python-werkzeug' 'python2-werkzeug')
checkdepends=('python-requests' 'python2-requests')
source=("$pkgbase-$pkgver.tar.gz::https://bitbucket.org/pytest-dev/pytest-localserver/get/$pkgver.tar.bz2")
sha512sums=('78993ee0245c9eace723c21eee6de359089f1e1895eac9c531268353b3a3d2045006d29656ea99733ad380f33af2b88f7332e83dcd4c7a97ced611cdbce5a673')

prepare() {
  mv pytest-dev-pytest-localserver-* pytest-localserver-$pkgver

  cp -a pytest-localserver-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-localserver-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-localserver-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-localserver-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-localserver-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-localserver() {
  depends=('python-pytest' 'python-werkzeug')

  cd pytest-localserver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-localserver() {
  depends=('python2-pytest' 'python2-werkzeug')

  cd pytest-localserver-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
