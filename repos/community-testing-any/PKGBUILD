# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-concurrency
pkgver=4.3.1
pkgrel=3
pkgdesc="OpenStack library for all concurrency-related code"
arch=('any')
url="https://pypi.python.org/pypi/oslo.concurrency/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-oslo-config' 'python-oslo-i18n' 'python-oslo-utils'
         'python-fasteners')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest' 'python-oslo-config' 'python-eventlet')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.concurrency/archive/$pkgver.tar.gz")
sha512sums=('25eafc4a0279addc2d74ba9b56fd85911c216cd8ab6199197abb9e9136b1075988d0eca964794227aa4bd76eedb05f5c11fc5c4ec253468186fbc4f86b80abcf')

export PBR_VERSION=$pkgver

build() {
  cd oslo.concurrency-$pkgver
  python setup.py build
}

check() {
  cd oslo.concurrency-$pkgver
  stestr run || : # "1 failure about raising rlimit"
}

package() {
  cd oslo.concurrency-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
