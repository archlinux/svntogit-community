# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=clikit
pkgname=python-clikit
pkgver=0.4.2
pkgrel=3
pkgdesc="clikit allows you to create beautiful and testable command-line interfaces"
arch=('any')
url="https://github.com/sdispater/${_pkgname}"
license=('MIT')
depends=('python-pastel' 'python-pylev')
makedepends=('python-dephell')
checkdepends=('python-pytest-mock')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('cf0f337f3a4477ac684cbbee83a74afa35945fefbcdfe898f58aa1f5646c5c23')
b2sums=('53d0b95805f269548b2dfeaa481564f17207e407c31b350917facd0664c94a686456f719096597f21f0d74117165cc347c90474a6cc1f74692798bffcbd60bf7')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # poetry-generated setup.py are fatally broken, see:
    # https://github.com/sdispater/poetry/issues/866
    dephell deps convert --from pyproject.toml --to setup.py
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    PYTHONPATH=$PWD/src python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
