# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
_pkgver=1.5.2
#_fixupver=2
pkgver=${_pkgver}
#pkgver=${_pkgver}.r${_fixupver}
pkgrel=3
pkgdesc='Full featured and easy to use XMPP (Jabber) client'
url='https://gajim.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'gtksourceview4' 'python-cairo' 'python-gobject' 'python-keyring' 'python-nbxmpp'
         'python-pyopenssl' 'python-precis_i18n' 'python-css-parser'
         'python-distro' 'hicolor-icon-theme' 'python-pillow' 'python-gssapi' 'python-netifaces'
         'python-packaging')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
optdepends=('farstream: start audio and video chat'
            'gst-plugins-good: for video/voice support'
            'gst-plugins-bad: for video/voice support'
            'gst-plugins-ugly: for video/voice support'
            'gst-libav: for video/voice support'
            'gst-python: for video/voice support'
            'gspell: for spell checking support'
            'geoclue: share current location'
            'libxss: measure idle time, in order to set auto status'
            'python-gnupg: encrypting chat messages with OpenPGP'
            'python-axolotl: OMEMO support'
            'python-qrcode: generate QR codes for OMEMO keys'
            'gsound: Notification Sounds')
source=(https://www.gajim.org/downloads/${_pkgver%.*}/gajim-${_pkgver}.tar.gz
        0001-remove-upnp.patch)
#source=(https://www.gajim.org/downloads/${_pkgver%.*}/gajim-${_pkgver}-${_fixupver}.tar.gz)
sha512sums=('e6edd81de822cae27fdcea573d08a46cd3c83aaa68b0a66f87314440d606149a5813a5ddb48872c77bcf1892db742c901543f104f57a1551d8140b801b0cb8f3'
            'be46e7bfd3bda5961d4d3ceb826f36768fb7e027b49308b5d5190f1fb9380cca95f451a37d1527cfe543520237600abfbd7b7dbe8343f906dc41b881c8a3ead2')
b2sums=('605bc5acfe05949bdc31b09b2e3c750f66badcf2e534a0ac152a9c1b0fc52f987a5fd4f64ab4a871c9b9a83be076c3b7bf76e87dbe16fd59baa82c371745cdb0'
        '048839b7d264c877fb382d58264403a9a847611b1a50a39fe4fcdf4474114d00b110f79134794f45ca39274477be83837a08fe6160e146d23f0455d1f2323c76')

prepare() {
  # https://dev.gajim.org/gajim/gajim/-/issues/11183
  # https://bugzilla.opensuse.org/show_bug.cgi?id=1203676
  patch -Np0 -d ${pkgname}-${_pkgver} -i ../0001-remove-upnp.patch
}

build() {
  cd ${pkgname}-${_pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}-${_pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
