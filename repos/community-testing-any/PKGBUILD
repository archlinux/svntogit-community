# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=react-native-debugger
pkgver=0.12.1
_commit=816daff45247715a09bc657e13555d89bb5cb3dd
pkgrel=3
pkgdesc='The standalone app for React Native Debugger, with React DevTools / Redux DevTools'
arch=('any')
url='https://github.com/jhen0409/react-native-debugger'
license=('MIT')
depends=('electron')
makedepends=('asar' 'git' 'yarn')
options=(!emptydirs)
source=("git+https://github.com/jhen0409/react-native-debugger.git#commit=${_commit}"
        'iconPath.patch'
        'react-native-debugger.desktop'
        'react-native-debugger.js')
sha256sums=('SKIP'
            '75cd9814325bfcdfc5dc9f85a7ce75a021e4b3c8cd105b851eb804e7b580d474'
            'f63e79516db0feb2e76c366402cf266165c0f7eb282dc55703b800b91152867d'
            '949661b3c14081d1e59b8036dd63f8895d195c17a259eb9df8fdf2239a7739dd')

prepare() {
    cd ${pkgname}

    patch -Np1 -i ../iconPath.patch
}

build() {
    cd ${pkgname}

    yarn --ignore-scripts
    cd dist
    yarn
    node ../scripts/patch-modules.js
    cd ..
    yarn build
}

package() {
    cd ${pkgname}

    install -dm755 "${pkgdir}/usr/lib"
    rm dist/logo.png  # see iconPath.patch
    # asar pack dist "${pkgdir}/usr/lib/${pkgname}.asar"
    cp -r dist "${pkgdir}/usr/lib/${pkgname}"

    install -Dm755 ../${pkgname}.js "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/applications" ../${pkgname}.desktop
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.md
    install -Dm644 electron/logo.png \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

    rm "${pkgdir}/usr/lib/${pkgname}/node_modules/loose-envify/cli.js"
    rm -rf "${pkgdir}/usr/lib/${pkgname}/node_modules/apollo-client-devtools/"{assets,build,development,shells/dev,src}
    find "${pkgdir}/usr/lib/${pkgname}/node_modules" \
      -name 'bin' -prune -exec rm -r '{}' \; \
      -or -name 'example' -prune -exec rm -r '{}' \; \
      -or -name 'examples' -prune -exec rm -r '{}' \; \
      -or -name 'test' -prune -exec rm -r '{}' \; \
      -or -name 'tests' -prune -exec rm -r '{}' \; \
      -or -name '*.md' -exec rm '{}' \; \
      -or -name '*.markdown' -exec rm '{}' \; \
      -or -name 'CHANGELOG*' -prune -exec rm -r '{}' \; \
      -or -name 'Makefile' -exec rm '{}' \; \
      -or -name '.*' -prune -exec rm -r '{}' \;
}
