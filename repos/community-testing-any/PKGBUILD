# $Id$
# Contributor: Johannes Dewender	 arch at JonnyJD dot net
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgbase=python-discid
pkgver=1.1.0
pkgrel=3
_pkgname=discid
pkgname=('python-discid' 'python2-discid')
pkgdesc='Python binding of MusicBrainz libdiscid'
url='https://python-discid.readthedocs.org/'
license=('LGPL3')
arch=('any')
depends=('libdiscid')
makedepends=('python' 'python2')
source=("http://pypi.python.org/packages/source/d/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha1sums=('de2d657e42d281dad593e0673af6ba6039ef58bc')

check() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py test
}

package_python-discid() {
	depends+=('python')
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-discid() {
	depends+=('python2')
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python2 setup.py install --root="${pkgdir}" --optimize=1
}
