# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com
# Contributor: Chris Fordham <chris [at] fordham-nagy [dot] id [dot] au> aka flaccid
# Package Source: https://github.com/flaccid/archlinux-packages/blob/master/python-jmespath/PKGBUILD

pkgbase=python-jmespath
pkgname=(python-jmespath python2-jmespath)
pkgver=0.9.4
pkgrel=1
pkgdesc='A query language for JSON'
arch=('any')
url="https://github.com/jmespath/jmespath"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-py' 'python2-py')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jmespath/jmespath.py/archive/$pkgver.tar.gz")
sha512sums=('38b8d27dbd13d5ab056e115f4e8048fd54d649955dfcb2c296fe14dc684cd729351618c06c5b9a4b4d27798cfec6c2b7b340f498e961475e6fb3c9640b4e51ae')

prepare() {
  cp -a jmespath.py-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/jmespath.py-$pkgver
  python setup.py build

  cd "$srcdir"/jmespath.py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/jmespath.py-$pkgver
  LC_CTYPE=en_US.utf8 nosetests3

  cd "$srcdir"/jmespath.py-$pkgver-py2
  LC_CTYPE=en_US.utf8 nosetests2
}

package_python-jmespath() {
  depends=('python')

  cd jmespath.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-jmespath() {
  depends=('python2')

  cd jmespath.py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/jp{,2}.py
}
