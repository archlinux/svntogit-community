# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Dan Serban
# Contributor: Richard Murri

pkgname=('python-werkzeug' 'python2-werkzeug')
pkgver=0.14.1
pkgrel=3
pkgdesc='Swiss Army knife of Python web development'
url='http://werkzeug.pocoo.org/'
arch=('any')
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytest' 'python2-pytest' 'python-requests' 'python2-requests')
source=("https://github.com/pallets/werkzeug/archive/${pkgver}.tar.gz")
sha512sums=('71ec293b702f247b68bf82b4ff9e4351e21aa1949221c3cba46cd0db3c3f10ca2009029bf26869230ad32cec29109b279876b71f4aa0de64dd6635725c9baecb')

prepare() {
  cp -r "werkzeug-$pkgver" "python-werkzeug-$pkgver"
  cp -r "werkzeug-$pkgver" "python2-werkzeug-$pkgver"
}

build() {
  cd "$srcdir/python-werkzeug-$pkgver"

  python setup.py build

  cd "$srcdir/python2-werkzeug-$pkgver"

  python2 setup.py build
}

# check() {
#   cd "python-werkzeug-$pkgver"
#   python setup.py test
#
#   cd "python2-werkzeug-$pkgver"
#   python2 setup.py test
# }

package_python-werkzeug() {
  depends=('python')
  cd "python-werkzeug-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-werkzeug() {
  depends=('python2')
  cd "python2-werkzeug-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
