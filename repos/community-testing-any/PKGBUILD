# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Clint Valentine <valentine.clint@gmail.com>

_pkgname=shellingham
pkgname=python-shellingham
pkgver=1.3.1
pkgrel=6
pkgdesc="Detect what shell the current Python executable is running in."
arch=('any')
url="https://github.com/sarugaku/${_pkgname}"
license=('ISC')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-mock')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('b1d83d3004c4126a86a42ca42c1e5095695c585a18e814bff13aa91ee8398c1e')
b2sums=('eab42fe8ccf203c4107f3a89046efbf8bd45b5731a1117a431af4932950cb56665582346916d55ada00cbec4800c47aeb3d6673dc0c7ce09db09f48fa7da08f9')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
