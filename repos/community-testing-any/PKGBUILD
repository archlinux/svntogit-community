# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Kogan <michael dot kogan at gmx dot net>

pkgname=perl-data-random
pkgver=0.13
pkgrel=2
pkgdesc="Data::Random - Perl module to generate random data"
arch=('any')
url="http://search.cpan.org/~barefoot/Data-Random/lib/Data/Random.pm"
license=('GPL' 'PerlArtistic')
depends=('perl-gd' 'perl-yaml-tiny')
makedepends=('perl-file-sharedir-install' 'perl-test-mocktime')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/B/BA/BAREFOOT/Data-Random-${pkgver}.tar.gz")
sha512sums=('7cf3811ee26a3b93a8170cc9c863ad2cf03f5997cabe66cd78646a4d93a6568ef247f636782b8c707c3064120b30609f3562d8654b31e5e31890aa302b03cdb5')

build() {
  cd "$srcdir/Data-Random-${pkgver}"
  PERL_USE_UNSAFE_INC=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Data-Random-${pkgver}"
  make test
}

package() {
  cd "$srcdir/Data-Random-${pkgver}"
  make DESTDIR=$pkgdir install
}
