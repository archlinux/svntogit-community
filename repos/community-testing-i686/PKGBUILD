# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=yesod-static
pkgname=haskell-yesod-static
pkgver=1.5.3
pkgrel=8
pkgdesc="Static file serving subsite for Yesod Web Framework."
url="http://www.yesodweb.com/"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-async" "haskell-attoparsec" "haskell-base64-bytestring"
         "haskell-blaze-builder" "haskell-byteable" "haskell-conduit" "haskell-conduit-extra"
         "haskell-cryptohash" "haskell-cryptohash-conduit" "haskell-css-text" "haskell-data-default"
         "haskell-exceptions" "haskell-file-embed" "haskell-hashable" "haskell-hjsmin"
         "haskell-http-types" "haskell-mime-types" "haskell-old-time" "haskell-resourcet"
         "haskell-text" "haskell-unix-compat" "haskell-unordered-containers" "haskell-wai"
         "haskell-wai-app-static" "haskell-yesod-core")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('50f8115e0f0a4495e27ea13aa8cbf4ec6bc596f6a758338ae0f4b4aaf9a5161dbca77ba1770467e830c0ef8984a5375d0c53b282ce5572d731c3887050472a8a')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
