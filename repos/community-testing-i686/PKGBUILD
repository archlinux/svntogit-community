# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=clxclient
pkgver=3.9.0
pkgrel=2
pkgdesc="C++ wrapper library around the X Window System API"
arch=('i686' 'x86_64')
url="http://kokkinizita.linuxaudio.org/linuxaudio/"
license=('LGPL')
depends=('gcc-libs' 'libxft')
makedepends=('clthreads')
source=("${url}downloads/$pkgname-$pkgver.tar.bz2")
md5sums=('bd6df73f688c9be1b3afef58283d7ef5')

prepare() {
  cd "$pkgname-$pkgver"
  # call ldconfig locally
  sed -i 's/ldconfig/& -N $(DESTDIR)$(PREFIX)\/$(LIBDIR)/' Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr" LIBDIR=lib
}

package(){
  cd "$pkgname-$pkgver"
  install -d "$pkgdir/usr/include"
  make PREFIX="$pkgdir/usr" LIBDIR=lib install
}

