# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.3.0.1
pkgrel=2
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-terminal'
license=('GPL3')
depends=('desktop-file-utils' 'granite' 'libnotify' 'vte3')
makedepends=('cmake' 'vala')
install='pantheon-terminal.install'
source=("https://launchpad.net/pantheon-terminal/0.3.x/${pkgver}/+download/pantheon-terminal-${pkgver}.tgz"
        'pantheon-terminal-vte2.91.patch')
sha256sums=('ea9718b409baa3ebdc175b5d51ed7825f0199e3c3de467a5f7bea6733bbe9a93'
            'a1f78afe58c47827c292e768d09578edd2f96124d26a19295e2954d5994a9513')

prepare() {
  cd pantheon-terminal-${pkgver}

  patch -Np1 -i ../pantheon-terminal-vte2.91.patch
}

build() {
  cd pantheon-terminal-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd pantheon-terminal-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
