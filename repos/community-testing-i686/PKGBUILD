# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ian Yang <doit.ian@gmail.com>

pkgname=google-glog
pkgver=0.3.4
pkgrel=2
pkgdesc="Logging library for C++"
arch=('i686' 'x86_64')
license=('custom:BSD3')
url='https://github.com/google/glog'
depends=('gcc-libs')
makedepends=('make' 'gcc')
source=("glog-$pkgver::https://github.com/google/glog/archive/v$pkgver.tar.gz")

build() {
  cd "$srcdir/glog-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/glog-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Lazy way of dealing with conflicting man and info pages...
  rm -rf "${pkgdir}/usr/share"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

md5sums=('df92e05c9d02504fb96674bc776a41cb')
