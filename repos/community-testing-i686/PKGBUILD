# $Id$
# Maintainer: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Mitch Bigelow <ipha00@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>

pkgname=pianobar
pkgver=2013.09.15
pkgrel=2
pkgdesc="console-based frontend for Pandora"
url="http://6xq.net/0017"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libao' 'faad2' 'libmad' 'gnutls' 'json-c' 'libgcrypt')
source=(http://6xq.net/projects/pianobar/$pkgname-$pkgver.tar.bz2)
sha256sums=('3f8cf25ef054acf46e519d02453a6e214cfa09661b3adc03da99ace1c6f10066')

build() {
    make -C "${srcdir}/${pkgname}-${pkgver}"
}

package() {
    make -C "${srcdir}/${pkgname}-${pkgver}" DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 "${srcdir}/${pkgname}-${pkgver}"/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm755 $srcdir/$pkgname-$pkgver/contrib/headless_pianobar $pkgdir/usr/bin/pianoctl
}
