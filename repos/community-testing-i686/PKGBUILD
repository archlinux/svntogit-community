# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar
pkgver=0.23
pkgrel=2
pkgdesc="A Minimalistic Text Based Status Bar"
url="http://hackage.haskell.org/package/xmobar"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('gmp' 'libxft' 'libxinerama' 'wireless_tools' 'libxrandr' 'libxpm')
makedepends=('ghc=7.10.1' 'haskell-x11=1.6.1.2' 'haskell-x11-xft=0.3.1' 'haskell-utf8-string=1' 'haskell-network-uri'
	     'haskell-stm=2.4.4' 'haskell-parsec=3.1.9' 'haskell-mtl=2.2.1' 'haskell-regex-base' 'haskell-regex-compat' 'haskell-http')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/xmobar/$pkgver/xmobar-$pkgver.tar.gz ghc-7.10.1-update-haskell-deps.patch disable-ewmh-plugin.patch)

prepare() {
  cd ${srcdir}/xmobar-$pkgver
  patch -Np1 -i $srcdir/ghc-7.10.1-update-haskell-deps.patch
  patch -Np1 -i $srcdir/disable-ewmh-plugin.patch
}
build() {
    cd ${srcdir}/xmobar-$pkgver

    runhaskell Setup configure --disable-optimization  --prefix=/usr --flags="with_utf8 with_xft with_iwlib with_xpm"
    runhaskell Setup build
}

package() {
    cd ${srcdir}/xmobar-$pkgver
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 license ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

md5sums=('5ab73783a6a8f97753a263652e1c141c'
         'c2229e749626ae28132129fc2cb80bad'
         '9d3951d4a6e12f48b5bd9735575ee2a4')
