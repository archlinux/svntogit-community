# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.041.00
pkgrel=7
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
#url="http://www.realtek.com.tw"
url="https://code.google.com/p/r8168/"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' "linux-lts>=4.4.9" "linux-lts<4.5")
makedepends=("linux-lts-headers>=4.4.9" "linux-lts-headers<4.5")
install=$pkgname.install
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d56f60e4157ccbf8f8717270b1acb391b0a959d9a96c2f0b4a91c683aa8b83a5')

_extramodules=extramodules-4.4-lts

build() {
	cd "$_pkgname-$pkgver"
	local _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
	make -C /usr/lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dm644 src/$_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
