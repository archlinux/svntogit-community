# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=c2hs
pkgver=0.27.1
pkgrel=2
pkgdesc="C->Haskell FFI tool that gives some cross-language type safety"
url="https://github.com/haskell/c2hs"
license=("GPL2")
arch=('i686' 'x86_64')
depends=('libffi' 'gmp')
makedepends=("ghc=7.10.3" "haskell-dlist" "haskell-language-c")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('668af07f261c7c6c2537921ba58870cfb1114b33670f2c182e6f9a8794ffe41f')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        -f-regression -fbase3
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
}
