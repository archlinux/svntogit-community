# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnewt
pkgver=0.52.17
pkgrel=2
_tclver=8.6
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=('x86_64' 'i686')
url='https://fedorahosted.org/newt/'
license=('GPL')
depends=('slang' 'popt' 'gpm')
makedepends=("tcl>=$_tclver" 'python' 'python2')
optdepends=('tcl: whiptcl support' \
            'python: libnewt support with the _snack module'
            'python2: libnewt support with the _snack module')
options=('!makeflags')
source=("https://fedorahosted.org/releases/n/e/newt/newt-$pkgver.tar.gz")
sha256sums=('69837973ef2ee2fa644426f1c3e48d2b18785ebcd382ef7fd01eb2e67d2d632b')

prepare() {
  cd "newt-$pkgver"

  sed -i "s:tcl8.4:tcl$_tclver:" Makefile.in
  echo '#define USE_INTERP_RESULT 1' >> config.h
}

build() {
  cd "newt-$pkgver"

  ./configure --prefix=/usr --with-gpm-support
  make
}

package() {
  make -C "newt-$pkgver" prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
