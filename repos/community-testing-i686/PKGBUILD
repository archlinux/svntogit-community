# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pyproj
pkgname=('python-pyproj' 'python2-pyproj')
pkgver=1.9.4
pkgrel=2
pkgdesc="Python interfaces to PROJ.4 library"
arch=('i686' 'x86_64')
url="http://jswhit.github.io/pyproj/"
license=('custom')
makedepends=('python' 'python2')
source=($pkgname-$pkgver.tar.gz::https://github.com/jswhit/pyproj/archive/v${pkgver}rel.tar.gz)
sha256sums=('b820fa1d52be82469571cd167f533b7d621342818cb971b4a13d8d0c2e454f48')

prepare() {
  cd "${srcdir}"
  cp -a pyproj-${pkgver}rel{,-python2}
}

build() {
  cd "${srcdir}"

# Build python 3 module
  cd pyproj-${pkgver}rel
  python setup.py build

# Build python 2 module
  cd ../pyproj-${pkgver}rel-python2
  python2 setup.py build
}

package_python-pyproj() {
  depends=('proj' 'python>=3.5')

  cd "${srcdir}"/pyproj-${pkgver}rel
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyproj() {
  depends=('proj' 'python2>=2.7')
  replaces=('pyproj')

  cd "${srcdir}"/pyproj-${pkgver}rel-python2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
