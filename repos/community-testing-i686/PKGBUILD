# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=megaglest
pkgver=3.11.0
pkgrel=2
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/megaglest/"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl' 'libvorbis' 'openal' 'libgl' 'lua51' 'icu' 'ftgl' 'glew'
'libircclient' 'miniupnpc' 'wxgtk2.8' 'glu')
makedepends=('ftjam' 'cmake' 'mesa' 'git')
source=("git+https://github.com/MegaGlest/megaglest-source.git#tag=${pkgver}")
md5sums=('SKIP')

build() {
  cd megaglest-source

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWANT_GIT_STAMP=0 \
    -DwxWidgets_CONFIG_EXECUTABLE="/usr/bin/wx-config-2.8" \
    -DwxWidgets_wxrc_EXECUTABLE="/usr/bin/wxrc-2.8"
  make
}

package() {
  cd megaglest-source/build

  make DESTDIR="$pkgdir" install
}

# vim: sw=2:ts=2 et:
