# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cblrepo
pkgver=0.24.0
pkgrel=24
pkgdesc="Tool to maintain a database of CABAL packages and their dependencies"
url="https://github.com/magthe/cblrepo"
license=("Apache")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-aeson" "haskell-ansi-wl-pprint" "haskell-mtl" "haskell-optparse-applicative"
         "haskell-safe" "haskell-stringsearch" "haskell-tar" "haskell-text" "haskell-unixutils"
         "haskell-utf8-string" "haskell-vector" "haskell-zlib")
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('03c1728d5f5ac702e334d3b7ccf0ceb42c00f93dba6cce2cdd655f5d74f4af7a')

prepare() {
    sed -e 's/aeson ==1.0.*,/aeson >=1.0,/' \
        -e 's/directory ==1.2.*,/directory >=1.2,/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/share
}
