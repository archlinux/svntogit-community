# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yangtse <yangtsesu@gmail.com> 
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=2.1.0
_modelver=14
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('kyotocabinet' 'glib2')
makedepends=('gnome-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz"
        "$pkgname-model$_modelver.text.tar.gz::http://downloads.sourceforge.net/libpinyin/model$_modelver.text.tar.gz")
noextract=("$pkgname-model$_modelver.text.tar.gz")
sha512sums=('e6aa3998ac466cb155750b574140dd43673bfeb4f13d1b88c4892b61bb773aee84610dd67ad17871961d51a48067c535eab33a6fb6c9ac28179a9958c4e9b518'
            '792d3cf7ab7fdea701737cea66d18f697015f19d18b6d26273db2da0ae28291f38b3d8a5717f595da63fbe573921c552bdce10c4bec9a0834947dfec92265da9')

prepare() {
  cd $pkgname-$pkgver

  ln -s "$srcdir"/$pkgname-model$_modelver.text.tar.gz data/model$_modelver.text.tar.gz
  sed -i "/wget.*model$_modelver\.text\.tar\.gz/ d" data/Makefile.am
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --with-dbm=KyotoCabinet --enable-libzhuyin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
