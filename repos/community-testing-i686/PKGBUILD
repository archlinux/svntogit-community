# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.14
pkgrel=3
pkgdesc='Minimalistic Mumble server'
arch=('i686' 'x86_64')
url='http://code.google.com/p/umurmur/'
license=('custom')
depends=('polarssl' 'libconfig' 'protobuf-c')
install=$pkgname.install
backup=('etc/umurmur/umurmur.conf')
source=(http://umurmur.googlecode.com/files/umurmur-$pkgver.tar.gz
        umurmur.service
        0001-Update-to-use-Protobuf-C-1.0.0.patch)
md5sums=('e22121b4b91f4c9de43f858f6defa0b0'
         'd9d556e4ffa77e193fb40ce508804720'
         '7e5c345698468eed5763d869f86d731d')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-Update-to-use-Protobuf-C-1.0.0.patch
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-ssl=polarssl
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 umurmur.conf.example "$pkgdir"/etc/umurmur/umurmur.conf
  install -Dm644 ../umurmur.service "$pkgdir"/usr/lib/systemd/system/umurmur.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
