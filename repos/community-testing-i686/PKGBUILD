# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Ivan Bobrov <ibobrik at gmail dot com>
# Contributor: Black_Mage <vleon1 at gmail dot com>

pkgname=astromenace
pkgver=1.3.2
pkgrel=2
pkgdesc="Hardcore 3D space shooter with spaceship upgrade possibilities"
arch=('i686' 'x86_64')
url="http://www.viewizard.com/astromenace/index_linux.php"
license=('GPL3')
depends=('sdl' 'freealut' 'libjpeg' 'libvorbis' 'glu' 'freetype2' 'libxinerama')
makedepends=('cmake' 'mesa')
source=(http://downloads.sourceforge.net/openastromenace/astromenace-src-$pkgver.tar.bz2
        astromenace.desktop)
md5sums=('a1481ca7cf498773fddc16a3b41c9b9b'
         '1edc9367564a6c639a2b8199adf26a08')

build() {
    cd $srcdir/AstroMenace

    [[ -d build ]] && rm -r build
    mkdir build && cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DDATADIR=/usr/share/astromenace
    make
}

package() {
    cd $srcdir/AstroMenace

    build/AstroMenace --pack --rawdata=./RAW_VFS_DATA --dir=build

    install -Dm755 build/AstroMenace $pkgdir/usr/bin/astromenace
    install -Dm644 build/gamedata.vfs $pkgdir/usr/share/astromenace/gamedata.vfs

    install -Dm 644 astromenace_128.png $pkgdir/usr/share/pixmaps/astromenace.png
    install -Dm 644 $srcdir/astromenace.desktop $pkgdir/usr/share/applications/astromenace.desktop
}
