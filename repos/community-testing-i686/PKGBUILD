# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-lchown
pkgver=1.01
pkgrel=2
pkgdesc="use the lchown(2) system call from Perl"
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
url=https://metacpan.org/release/Lchown
source=("http://search.cpan.org/CPAN/authors/id/N/NC/NCLEATON/Lchown-${pkgver}.tar.gz")
md5sums=(e3db31be650437eb5d9bfc4da6252ee3)
sha512sums=(f3b7bf7b3b71a7d92449d4ae8249d76b456869e48105db401a49f39f687ee5d97e079485fb2bbaae991a3a27d408959fc132b843c6f69487eeb2958c85f683a8)

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/Lchown-${pkgver}"
    /usr/bin/perl Build.PL
    ./Build
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/Lchown-${pkgver}"
    ./Build test
)

package() {
    cd "${srcdir}/Lchown-${pkgver}"
    ./Build install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
