# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor:  Yangtse Su<yangtsesu@gmail.com>
# Contributor:  Jekyll Wu<adaptee [at] gmail [dot] com>
# Contributor: riverscn<riverscn at gmail.com>
# Contributor: rainy<rainylau at gmail.com>
# Contributor: Lee.MaRS<leemars at gmail.com>

pkgname=ibus-libpinyin
pkgver=1.7.3
pkgrel=2
pkgdesc="Intelligent Pinyin engine based on libpinyin for IBus"
arch=('i686' 'x86_64')
license=('LGPL')
url="https://github.com/libpinyin/ibus-libpinyin"
depends=('ibus' 'libpinyin' 'python2-xdg')
makedepends=('git' 'intltool' 'gnome-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libpinyin/ibus-libpinyin/archive/$pkgver.tar.gz")
sha512sums=('abb0302bd06111e0135f3cd93a087386d8a48377cae99f50165dd39e69826ce58d02a693289fa7ef96f786557936aeefc0133bfb1218cccc609fddf827b8bb73')

build() {
  cd $pkgname-$pkgver

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  for file in setup/ibus-setup-libpinyin.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  PYTHON=python2 ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname-$pkgver
  make NO_INDEX=true DESTDIR="$pkgdir" install
}
