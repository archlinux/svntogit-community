# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-citeproc
pkgver=0.11.1
pkgrel=1
pkgdesc="Supports using pandoc with citeproc"
url="https://hackage.haskell.org/package/$pkgname"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-aeson-pretty" "haskell-attoparsec" "haskell-data-default"
         "haskell-hs-bibutils" "haskell-mtl" "haskell-old-locale" "pandoc" "haskell-pandoc-types"
         "haskell-parsec" "haskell-rfc5051" "haskell-setenv" "haskell-split" "haskell-syb"
         "haskell-tagsoup" "haskell-temporary" "haskell-text" "haskell-text-icu"
         "haskell-unordered-containers" "haskell-vector" "haskell-xml-conduit" "haskell-yaml")
conflicts=('haskell-pandoc-citeproc')
replaces=('haskell-pandoc-citeproc')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/${pkgver}/$pkgname-${pkgver}.tar.gz")
sha512sums=('d9540d7ec17d12a10620cc26f93b1ef940e17e49896e6044f68823768da9aaa24ce01b7afb68b2e52af928d0ca6e211d6eb557ffac64666137e044531bb59dc6')

build() {
    cd "${srcdir}/$pkgname-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-debug -f-test_citeproc -funicode_collation -f-embed_data_files -fbibutils
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/$pkgname-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/$pkgname.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/$pkgname.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/$pkgname/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
