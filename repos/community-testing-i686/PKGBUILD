# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant-server
pkgname=haskell-servant-server
pkgver=0.11
pkgrel=21
pkgdesc="A family of combinators for defining webservices APIs and serving them"
url="http://haskell-servant.readthedocs.org/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-base-compat" "haskell-aeson" "haskell-attoparsec"
         "haskell-base64-bytestring" "haskell-exceptions" "haskell-http-api-data"
         "haskell-http-types" "haskell-network-uri" "haskell-monad-control" "haskell-mtl"
         "haskell-network" "haskell-safe" "haskell-servant" "haskell-split"
         "haskell-string-conversions" "haskell-system-filepath" "haskell-resourcet" "haskell-tagged"
         "haskell-text" "haskell-transformers-base" "haskell-transformers-compat" "haskell-wai"
         "haskell-wai-app-static" "haskell-warp" "haskell-word8")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('2b0ee788d4c3aca2234aa147e2b0c5acb6202483a198bdba3d3b9023540c02b1')

prepare() {
    sed -i 's/base               >= 4.7  && < 4.10/base               >= 4.7/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
