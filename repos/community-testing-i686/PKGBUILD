# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+aur@gmail.com>

pkgname=perl-gd
pkgver=2.56
pkgrel=4
pkgdesc="Interface to Gd Graphics Library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/GD"
license=('GPL' 'PerlArtistic')
depends=('gd')
makedepends=('perl-module-build')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/L/LD/LDS/GD-$pkgver.tar.gz)
md5sums=('c4b3afd98b2c4ce3c2e1027d101a8f1e')

prepare() {
  chmod -R u+w $srcdir/GD-$pkgver
}

build() {
  cd  $srcdir/GD-$pkgver
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
}

package() {
  cd  $srcdir/GD-$pkgver
  perl Build install
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
