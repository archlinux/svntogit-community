# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=0.82.7.1
pkgrel=7
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis')
makedepends=('boost' 'cmake' 'zip' 'lzma-utils' 'p7zip' 'python2')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=(http://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma
        gcc46.patch
        awk-java.patch)
md5sums=('378cf0b18a5dd5b840964e5945778503'
         'e9586b611db1ed04fe4f0c5982fda7d2'
         'cd94edf21e49ff6ff7d256442ed9aa3c')

build() {
  bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma
  
  cd spring_$pkgver
  
  sed -i '1i\
	  #include <list>' rts/lib/lobby/Connection.h
  patch -Np1 < $srcdir/gcc46.patch
  patch -Np1 < $srcdir/awk-java.patch
  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
	-DDATADIR=share/spring
  make
}

package() {
  cd spring_$pkgver

  make DESTDIR=$pkgdir install

  install -d $pkgdir/etc/spring
  echo '$HOME/.spring' > $pkgdir/etc/spring/datadir
}

# vim sw=2:ts=2 et:
