# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gpsbabel
pkgver=1.5.2
pkgrel=2
pkgdesc="Reads, writes, and manipulates GPS waypoints in a variety of formats"
arch=('i686' 'x86_64')
url="http://www.gpsbabel.org"
license=('GPL')
depends=('libusb-compat' 'expat' 'qt4')
makedepends=('fop' 'libxml2' 'libxslt' 'gtk2' 'lcms2' 'qtwebkit' 'java-environment' 'docbook-xsl')
optdepends=('qtwebkit')
options=('docs')
source=("http://arch.p5n.pp.ru/~sergej/dl/gpsbabel-$pkgver.tar.gz")
md5sums=('a5f04f0b32554567118bbac1f4dbb379')

build() {
  cd $srcdir/gpsbabel-$pkgver
  ./configure --prefix=/usr
  make
  make doc
  (cd gui && qmake-qt4 && make)
}

package() {
  cd $srcdir/gpsbabel-$pkgver
  make DESTDIR=$pkgdir install

  # GUI
  install -Dm755 gui/objects/gpsbabelfe-bin ${pkgdir}/usr/bin/gpsbabelfe-bin
  install -Dm644 gui/images/appicon.png $pkgdir/usr/share/pixmaps/gpsbabel.png
  install -Dm644 gui/gpsbabel.desktop $pkgdir/usr/share/applications/gpsbabel.desktop

  # Header install
  mkdir -p $pkgdir/usr/include/gpsbabel
  cp *.h $pkgdir/usr/include/gpsbabel

  # Doc install
  mkdir -p $pkgdir/usr/share/doc/$pkgname
  cp -r $srcdir/babelweb/* $pkgdir/usr/share/doc/$pkgname/
  cp README* $pkgdir/usr/share/doc/$pkgname/
}
