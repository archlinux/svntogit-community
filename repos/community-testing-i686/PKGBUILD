# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.18.0
pkgrel=1
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64')
url="http://mate-desktop.org"
license=('GPL')
depends=('gtksourceview3' 'libgtop' 'libnotify' 'mate-panel' 'polkit'
         'python2-dbus' 'python2-gobject' 'upower' 'wireless_tools')
makedepends=('intltool' 'itstool' 'gucharmap')
optdepends=('fortune-mod: for displaying fortune cookies in the Wanda the Fish applet' 'gucharmap: character picker applet')
groups=('mate-extra')
conflicts=('mate-applets-gtk3' 'mate-netspeed' 'mate-netspeed-gtk3')
replaces=('mate-applets-gtk3' 'mate-netspeed' 'mate-netspeed-gtk3')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('9f9af431c6030bed92191fff167c5e8520196c2b')

prepare() {
    cd ${pkgname}-${pkgver}
    sed -i 's/env python/env python2/' invest-applet/invest/{chart.py,invest-applet.py,mate-invest-chart}
}

build() {
    cd ${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-ipv6
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
