# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

_madwifiver=0.9.4.4133
pkgname=hostapd
pkgver=0.7.3
pkgrel=7
pkgdesc="daemon for wireless software access points"
arch=('i686' 'x86_64')
url="http://hostap.epitest.fi/hostapd/"
license=('custom')
depends=('openssl' 'libnl')
makedepends=()
provides=('hostapd')
conflicts=('hostapd')
backup=('etc/hostapd/hostapd.conf'
	'etc/conf.d/hostapd'
	'etc/hostapd/hlr_auc_gw.milenage_db'
	'etc/hostapd/hostapd.accept'
	'etc/hostapd/hostapd.deny'
	'etc/hostapd/hostapd.eap_user'
	'etc/hostapd/hostapd.radius_clients'
	'etc/hostapd/hostapd.sim_db'
	'etc/hostapd/hostapd.vlan'
	'etc/hostapd/hostapd.wpa_psk'
	'etc/hostapd/wired.conf')
install=hostapd.install
source=(http://hostap.epitest.fi/releases/$pkgname-$pkgver.tar.gz
	ftp://ftp.archlinux.org/other/madwifi/madwifi-${_madwifiver}.tar.bz2
	hostap_allow-linking-with-libnl-3.2.patch
	config
	hostapd
	hostapd.conf.d)
md5sums=('91a7c8d0f090b7104152d3455a84c112'
         'ca3c3504d000e7b3d7063af46271c932'
         'e904e62b12c5d64132dd9441e8b45dce'
         'bfe5701bc93d5d091e6f1a93f1a32dd6'
         'd570327c385f34a4af24d3a0d61cea19'
         'f169534b0f59b341f6df1a21e0344511')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/hostap_allow-linking-with-libnl-3.2.patch"

  cd hostapd
  cp "$srcdir/config" ./.config

  sed -i "s|@@madwifi@@|$srcdir/madwifi|" .config
  sed -i 's#/etc/hostapd#/etc/hostapd/hostapd#' hostapd.conf

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/hostapd"
  
  install -D hostapd "$pkgdir/usr/bin/hostapd"
  install hostapd_cli "$pkgdir/usr/bin/hostapd_cli"

  install -Dm644 hlr_auc_gw.milenage_db "$pkgdir/etc/hostapd/hlr_auc_gw.milenage_db"
  install -m644 -t "$pkgdir/etc/hostapd" \
    hostapd.{accept,conf,deny,eap_user,radius_clients,sim_db,vlan,wpa_psk} \
    wired.conf
  
  install -Dm644 hostapd.8 "$pkgdir/usr/share/man/man8/hostapd.8"
  install -Dm644 hostapd_cli.1 "$pkgdir/usr/share/man/man1/hostapd_cli.1"

  install -D "$srcdir/hostapd" "$pkgdir/etc/rc.d/hostapd"
  install -Dm644 "$srcdir/hostapd.conf.d" "$pkgdir/etc/conf.d/hostapd"

  install -Dm644 ../COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
