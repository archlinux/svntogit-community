# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.14
pkgbase=libmateweather
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2' 'gtk3')
makedepends=('intltool')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('8f94aee849e613f2e869f096bbc618180016d885')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --disable-python \
        --enable-locations-compression

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-python \
        --enable-locations-compression

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package_libmateweather() {
    pkgdesc+=' (GTK2 version)'
    conflicts=("${pkgbase}-gtk3")
    depends=('dconf' 'gtk2' 'libsoup')
    
    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_libmateweather-gtk3() {
    pkgdesc+=' (GTK3 version)'
    conflicts=("${pkgbase}")
    depends=('gtk3')
    
    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
