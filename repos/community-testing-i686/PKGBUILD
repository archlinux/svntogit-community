# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=schroot
pkgver=1.4.23
pkgrel=1
pkgdesc="Allows users to execute shell commands under different root filesystems. \
	 (Successor to dchroot)."
url="http://packages.qa.debian.org/s/schroot.html"
license=('GPL3')
depends=('pam' 'lockdev' 'boost' 'e2fsprogs')
optdepends=('btrfs-progs-unstable' 'lvm2')
arch=('i686' 'x86_64')
conflicts=('dchroot')
replaces=('dchroot')
provides=('schroot' 'sbuild' 'dchroot')
backup=('etc/schroot/schroot.conf'
	'etc/schroot/script-arch32'
	'etc/schroot/mount-arch32'
	'etc/schroot/copyfiles-arch32')
options=(!libtool)
source=("http://ftp.debian.org/debian/pool/main/s/$pkgname/${pkgname}_${pkgver}.orig.tar.bz2"
	'script-arch32'
	'copyfiles-arch32'
	'mount-arch32')
md5sums=('73873e0fee83a45655ed8efa246a1e26'
         '8e86445e188129232e1782d978dfc967'
         'd4558d5691fbdaa46ce669e8f327133b'
         'f0d5d5b5e34a860f6f90b5628c680f46')

build() {
  cd ${pkgname}-${pkgver}

  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"

  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc --localstatedir=/var \
	--enable-dchroot --enable-lvm-snapshot --enable-btrfs-snapshot \
	BTRFS=/sbin/btrfs \
	BTRFSCTL=/sbin/btrfsctl \
	LVCREATE=/sbin/lvcreate \
	LVREMOVE=/sbin/lvremove
# --enable-csbuild

  make
}

check() {
  cd ${pkgname}-${pkgver}

  make -k check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=$pkgdir  install

  install -m 644 ${srcdir}/script-arch32 ${pkgdir}/etc/schroot
  install -m 644 ${srcdir}/mount-arch32 ${pkgdir}/etc/schroot
  install -m 644 ${srcdir}/copyfiles-arch32 ${pkgdir}/etc/schroot
  cat >> $pkgdir/etc/schroot/schroot.conf << EOF

[Arch32]
description=Arch32
type=directory
directory=/opt/arch32
priority=1
#users=username
groups=users
root-groups=root
script-config=script-arch32
personality=linux32
aliases=32,default

EOF
}
