# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Rorschach <r0rschach@lavabit.com>

pkgname=torsocks
pkgver=2.2.0
pkgrel=2
pkgdesc='Wrapper to safely torify applications'
arch=('x86_64')
url='https://gitweb.torproject.org/torsocks.git/'
license=('GPL2')
depends=('tor')
makedepends=('git')
backup=("etc/tor/${pkgname}.conf")
source=(https://people.torproject.org/~dgoulet/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('B74417EDDF22AC9F9E90F49142E86A2A11F48D36')
md5sums=('512bab466f2c026b3c478dee14861ccb'
         'SKIP')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
