# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.5.13
pkgrel=2
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('x86_64')
license=('LGPL')
url='https://github.com/ibus/ibus-anthy'
depends=('ibus' 'anthy' 'python' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('intltool' 'gobject-introspection')
source=("https://github.com/ibus/ibus-anthy/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('058feb49b2fe50ae3ca959f4754b8070f3ebc1e6727fef4bca791aa45a23c95018000e2a8bca496d6592b052298a6fb2869a07c2cc56cc473c81cc437efa175b')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
