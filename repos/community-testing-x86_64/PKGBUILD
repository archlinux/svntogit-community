# $Id$
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2017.0.0
pkgrel=5
pkgdesc="Panorama photo stitcher"
arch=('x86_64')
url="http://hugin.sourceforge.net/"
license=('GPL')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'swig')
depends=('wxgtk2' 'boost-libs' 'enblend-enfuse' 'exiv2' 'libpano13' 'glew'
         'python' 'lensfun' 'lapack' 'make' 'sqlite' 'perl-image-exiftool')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/r/_r}.tar.bz2")
sha256sums=('86646ea27f32b959d38e7d36270703214b1ae961e379dd64b14d027139cd4609')

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LAPACK=yes
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
