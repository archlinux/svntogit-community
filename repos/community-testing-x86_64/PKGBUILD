# Maintainer: Anatol Pomozov
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

pkgname=dtc
pkgver=1.6.1
pkgrel=4
pkgdesc='Device Tree Compiler'
url='https://www.devicetree.org/'
arch=(x86_64)
license=(GPL2)
depends=(libyaml)
makedepends=(swig python)
checkdepends=(valgrind)
source=(https://www.kernel.org/pub/software/utils/dtc/dtc-$pkgver.tar.xz
        python310.patch)
sha256sums=('65cec529893659a49a89740bb362f507a3b94fc8cd791e76a8d6a2b6f3203473'
            '767b6931887018a1dd7d41bfebebd16ab78a35de06a5689f530e376ab8c6b037')

prepare() {
  cd dtc-$pkgver
  patch -Np1 -i ../python310.patch
  sed -i 's/-Werror//' Makefile
}

build() {
  cd dtc-$pkgver
  make
}

check() {
  cd dtc-$pkgver
  make check
}

package() {
  cd dtc-$pkgver
  DESTDIR="$pkgdir" make SETUP_PREFIX="$pkgdir/usr" PREFIX="$pkgdir/usr" install
}
