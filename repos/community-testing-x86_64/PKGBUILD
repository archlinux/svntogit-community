# Maintainer: Christian Hesse <arch@eworm.de>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: Marc St-Laurent <mstlaurent@canada.com>

pkgname=xfmpc
_tag='45bee60326729d65c0b24c64602bf0329e0ce134' # git rev-parse ${pkgver}
pkgver=0.3.1
pkgrel=1
pkgdesc='A graphical GTK+ MPD client focusing on low footprint'
arch=('x86_64')
url='https://goodies.xfce.org/projects/applications/xfmpc/'
license=('GPL')
depends=('libxfce4ui' 'libmpd')
makedepends=('git' 'xfce4-dev-tools' 'vala')
source=("git+https://gitlab.xfce.org/apps/${pkgname}.git#tag=${_tag}")
sha256sums=('SKIP')

build() {
	cd "${srcdir}/${pkgname}"

	./autogen.sh \
		--prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}"

	make DESTDIR="${pkgdir}" install
}

