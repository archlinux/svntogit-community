# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.5.2
pkgrel=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
depends=('dtkcore' 'librsvg' 'qt5-x11extras')
makedepends=('qt5-tools' 'gtest')
source=("https://github.com/linuxdeepin/dtkgui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('964d01b8453a3134d94cb20da5e6dd2ff09561e2a3f06b4bf7f45afad87d924ff0a1ee71a9c766774a6af56217e0fb935abd30917e136b9d3f0937961418be2a')

prepare() {
  cd dtkgui-$pkgver
  # Patch out 5.5 suffix. The version number in dtkcore & friends never matched their tags,
  # and current version of dde-session-shell requires 5.5 explicitly. Let's make qmake and
  # cmake happy while upstream didn't react on this matter.
  find . -name '*.pro' -exec sed -i 's/dtkcore5.5/dtkcore/g;s/dtkgui5.5/dtkgui/g' {} \;
}

build() {
  cd dtkgui-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd dtkgui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
