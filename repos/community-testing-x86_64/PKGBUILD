# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Matthias Ellmer <ellmer.matthias@gmail.com>
# Contributor: Julien Morin <jmorin@cp.dias.ie>

pkgbase=python-basemap
pkgname=('python-basemap' 'python-basemap-common')
pkgver=1.2.2
pkgrel=4
arch=('x86_64')
url="https://matplotlib.org/basemap/"
license=('MIT')
makedepends=('cython' 'python-setuptools' 'python-matplotlib' 'geos' 'shapelib'
             'python-numpy' 'python-pyproj')
source=($pkgname-$pkgver.tar.gz::https://github.com/matplotlib/basemap/archive/v${pkgver}rel.tar.gz
        LICENSE)
sha256sums=('7e6ee5d03b10168862cff82bfa819df8264c04f078eac4549a22dd2631696613'
            '0deac85afb0740df7626b6d8d7aa4b8814bbe9a412fb1a0e5962895d17e63add')

prepare() {
  pushd "${srcdir}"/basemap-${pkgver}rel
# regenerate cythonized sources
  cythonize --force src/_geoslib.pyx
# Remove the bundled libraries and common data
  rm -r geos-*
  sed -i "s/lib64/lib/g" setup.py
  popd
}

build() {
  cd "${srcdir}"/basemap-${pkgver}rel
  python setup.py config
  python setup.py build
}

package_python-basemap() {
  pkgdesc="Toolkit for plotting data on map projections"
  depends=('python-basemap-common' 'python-matplotlib' 'geos' 'shapelib' 'python-numpy' 'python-pyproj')
  optdepends=('python-owslib: needed for the BaseMap.wmsimage function'
              'python-pillow: needed for Basemap warpimage, bluemarble, shadedrelief and etop methods')

  cd "${srcdir}"/basemap-${pkgver}rel

  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  _sitepackages=`python -c "import site; print(site.getsitepackages()[0])"`
  rm -fr "${pkgdir}${_sitepackages}"/mpl_toolkits/basemap/data
  ln -sf /usr/share/basemap/data "${pkgdir}${_sitepackages}"/mpl_toolkits/basemap/data
}

package_python-basemap-common() {
  pkgdesc="Data files for python-basemap"
  license=('GPL2')

  cd "${srcdir}"/basemap-${pkgver}rel

  install -dm755 "${pkgdir}"/usr/share/basemap
  cp -a lib/mpl_toolkits/basemap/data/ "${pkgdir}"/usr/share/basemap/
}
