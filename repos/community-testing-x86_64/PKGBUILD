# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eclib
pkgver=20221012
pkgrel=2
pkgdesc='Includes mwrank (for 2-descent on elliptic curves over Q) and modular symbol code used to create the elliptic curve database'
arch=(x86_64)
url='https://github.com/JohnCremona/eclib/'
license=(GPL)
depends=(flint pari boost-libs)
makedepends=(boost)
source=(https://github.com/JohnCremona/eclib/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2)
sha256sums=('4cf6af5b29fa04cb7b7c0a35f6bae53c808f6cafd72acb41aee07f91ad5a7417')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-flint=/usr --with-boost
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
