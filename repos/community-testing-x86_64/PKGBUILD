# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=open-isns
pkgver=0.102
pkgrel=2
pkgdesc='iSNS server and client for Linux'
arch=('x86_64')
depends=('openssl')
url='https://github.com/gonzoleeman/open-isns'
license=('GPL')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gonzoleeman/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('9611344733c0cdf14395f60880950ea4c3c7d6b765565b6493ad3e1afbe216de')

build() {
	cd "$srcdir"/${pkgname}-${pkgver}

	./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--enable-shared
	make
}

package() {
	cd "$srcdir"/${pkgname}-${pkgver}

	make DESTDIR="${pkgdir}" install install_lib install_hdrs
}
