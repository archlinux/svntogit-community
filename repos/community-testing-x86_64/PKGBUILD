# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Yangtse <yangtsesu@gmail.com> 
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=0.9.94
_modelver=7
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('db' 'glib2')
source=("https://github.com/libpinyin/libpinyin/archive/${pkgver}.tar.gz"
        "http://downloads.sourceforge.net/libpinyin/model${_modelver}.text.tar.gz")
noextract=("model${_modelver}.text.tar.gz")

build() {
  cd ${pkgname}-${pkgver}

  cp ../model${_modelver}.text.tar.gz data/
  sed -i "/wget.*model${_modelver}\.text\.tar\.gz/ d" data/Makefile.am

  aclocal && libtoolize --force && autoheader && automake -a && autoconf
  ./configure --prefix=/usr && make
}

package() {
  cd ${pkgname}-$pkgver
  make DESTDIR="${pkgdir}" install
}

sha512sums=('733083a8ed1dc0d230c8830e67c4d23b28ecf4c2b4adf386aee5f1e3cd9849363951c60720d5ae5fe9c075b4dbdda88969473c19bb1a8c8187cbc21e35445108'
            '74a56a23cdca24124037f688ed85a921089155a1b9a0b61e646f805b2bb698c56682c5d6a47ff86657c8b131a831dbee9d5cc5165d4c1f235b77fcd5b2e480ad')
