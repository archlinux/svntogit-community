# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sh4nks <sh4nks7@gmail.com

pkgname=lightdm-pantheon-greeter
pkgver=6.1.1.r61.7a11f864
pkgrel=1
pkgdesc='Pantheon greeter for LightDM'
arch=(x86_64)
url=https://github.com/elementary/greeter
license=(GPL)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glibc
  libgdk-3.so
  libgee
  libgio-2.0.so
  libglib-2.0.so
  libgl
  libgobject-2.0.so
  libgranite.so
  libgtk-3.so
  libhandy-1.so
  lightdm
  mutter
)
makedepends=(
  cmake
  meson
  git
  libglvnd
  vala
)
install=lightdm-pantheon-greeter.install
#_tag=93bbca3f65e84a1c1a1eadc146df18de9eb2d5a6
_commit=7a11f864cb542ca873cab599b91e19e738d0da47
#source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#tag=${_tag})
source=(lightdm-pantheon-greeter::git+https://github.com/elementary/greeter.git#commit=${_commit})
b2sums=(SKIP)

pkgver() {
  cd lightdm-pantheon-greeter
  git describe --tags | sed 's/-/.r/; s/-g/./'
}

build() {
  arch-meson lightdm-pantheon-greeter build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
