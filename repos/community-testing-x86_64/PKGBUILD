# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=boinctui
pkgver=2.7.0
pkgrel=2
pkgdesc="Curses based fullscreen BOINC manager"
arch=('x86_64')
url="https://sourceforge.net/projects/boinctui/"
license=('GPL')
depends=('expat' 'gcc-libs' 'ncurses' 'openssl')
makedepends=("cmake")
source=("https://sourceforge.net/projects/boinctui/files/${pkgname}_${pkgver}.tar.gz"{,.asc})
sha512sums=('977ebdca8573972bb90dabdf8e59f2327e84e0c1a488ee9c4f8a3b64a334b836f0af80db366766d07fc70e5b074ba2683f5519ac939a67022e447afb039500c6'
            'SKIP')
validpgpkeys=('4D2DBBB4F06EC2C977B26BFD80878E59832BEC15')  # Sergey Suslov

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-gnutls --mandir=/usr/share/man
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
