# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: arkhan <arkhan@disroot.org>

pkgname=ueberzug
pkgver=18.1.9
pkgrel=5
pkgdesc='Command line util which allows to display images in combination with X11'
arch=('x86_64')
url=https://github.com/seebye/ueberzug
license=('GPL3')
depends=('libxext' 'python-attrs' 'python-docopt' 'python-pillow' 'python-xlib')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
provides=("python-$pkgname")
replaces=("python-$pkgname")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('7ce49f351132c7d1b0f8097f6e4c5635376151ca59318540da3e296e5b21adc3')
b2sums=('96a925cc2212512dcbf05aa47015c2611f9f36bcf3dcf65d62dc6212c567165faeb798abff55b092f174fd57ff5740734dde74006af8cd7e974598813c8c3adb')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
