# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin
pkgver=2.0.3.20140820
_commit=9c165f917b05bd929cc6e8b02e3db1af08ac75fc
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Statistical Language Model based pinyin IME by Sun"
license=('LGPL')
url="http://sunpinyin.googlecode.com"
depends=('sqlite' 'make' 'gcc-libs')
optdepends=('sunpinyin-data: Statistical language model data from open-gram project for sunpinyin')
makedepends=('git' 'scons' 'intltool')
source=("git://github.com/sunpinyin/sunpinyin.git#commit=$_commit")
md5sums=("SKIP")

build() {
  cd "$srcdir/$pkgname"
  sed -i -e "1s|python|python2|" python/*.py python/importer/*.py

  scons \
    --prefix=/usr
}

package() {
  cd "$srcdir/$pkgname"

  scons \
    --prefix=/usr \
    --install-sandbox="$pkgdir" \
    install
}

