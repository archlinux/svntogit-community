# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lilv
pkgver=0.24.6
pkgrel=2
pkgdesc="A C library interface to the LV2 plug-in standard"
arch=('x86_64')
url="https://drobilla.net/software/lilv/"
license=('custom:ISC')
depends=('glibc' 'libserd-0.so' 'libsord-0.so' 'libsratom-0.so')
makedepends=('libsndfile' 'lv2' 'python' 'swig' 'waf')
optdepends=('bash-completion: completion for bash'
            'libsndfile: for lv2apply'
            'python: for Python bindings')
provides=('liblilv-0.so')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('0cf89d7dac97727e744579d3f4f4b330f56cf72f1a30bd03abfd645233aaf04e9bbf2abf460a0e4b963ed40e1ae78ed5f36127553bdab16e3e7dd3e93b70cdad'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # let wscript(s) find the custom waf scripts
  mkdir -pv tools
  touch __init__.py
  cp -v waflib/extras/{autoship,autowaf,lv2}.py tools/
  mkdir -pv plugins/tools/
  cp -v waflib/extras/{autoship,autowaf,lv2}.py plugins/tools/
  rm -rv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --no-bash-completion \
                --dyn-manifest \
                --test
  waf build
}

check() {
  cd "${pkgname}-${pkgver}"
  waf test
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # bash completion
  install -vDm 644 utils/lilv.bash_completion\
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
}
# vim:set ts=2 sw=2 et:
