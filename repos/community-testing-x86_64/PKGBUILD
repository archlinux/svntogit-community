# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>
# Contributor: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-params-util
pkgver=1.101
pkgrel=1
pkgdesc='Simple, compact and correct param-checking functions'
arch=('x86_64')
url="https://metacpan.org/release/Params-Util"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RE/REHSACK/Params-Util-$pkgver.tar.gz")
sha512sums=('c132aad3172ecc28e8bc193a71c06c4eefeda163a905d490f7d522b072f389f6e0671012df8e6f85c030567497e17b8f05a04e2910b5a5f8d25e6f62bac8cea0')

build() {
  cd "$srcdir/Params-Util-$pkgver"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/Params-Util-$pkgver"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "$srcdir/Params-Util-$pkgver"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
