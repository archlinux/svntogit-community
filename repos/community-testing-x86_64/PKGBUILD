# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Philipp Sandhaus <philipp.sandhaus@gmx.de>

pkgname=ktoblzcheck
pkgver=1.50
pkgrel=3
pkgdesc="A library to check account numbers and bank codes of German banks"
arch=('x86_64')
url="http://ktoblzcheck.sourceforge.net"
license=('LGPL')
depends=('python')
makedepends=('cmake')
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('5cebeb144cd9f3e089325f919150000802aa0ad03aac13b273d797c6933a989f')

prepare() {
  cd "${srcdir}"

  [[ ! -d build ]] && mkdir build
}

build() {
  cd "${srcdir}"/build

  cmake \
    ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
