# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=0.0.3
_extramodules=extramodules-ARCH
pkgrel=9
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('linux>=4.20' 'linux<4.21')
makedepends=('linux-headers>=4.20')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        linux-4.20.patch)
sha512sums=('8a506ba57d6a2ac9c9089719d18013fbde6443cb4f63de79498269e728b7e8725b4e06e7029ae2d2242aaac955c63e56c680fdd18e3fb0d72e9953a9fc5e8402'
            'dc3c85e9535cc589fdf56d265c4330519c92b5bcc406153e3162ba6ef5e799b702c394e1961132770f4f3ddd288dbbfe74a9d8056329e4585eb5a2094ffe0155')

prepare() {
  cd deepin-anything-$pkgver
  patch -p1 -i ../linux-4.20.patch
}

build() {
  cd deepin-anything-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make -C kernelmod kdir=/usr/lib/modules/$_kernver/build
}

package() {
  cd deepin-anything-$pkgver/kernelmod
  install -Dm644 vfs_monitor.ko "$pkgdir"/usr/lib/modules/$_extramodules/vfs_monitor.ko
  gzip "$pkgdir"/usr/lib/modules/$_extramodules/vfs_monitor.ko
}
