# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongo-c-driver
pkgver=1.14.0
pkgrel=2
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libsasl' 'icu' 'snappy' 'zlib')
makedepends=('cmake')
provides=('libbson' 'libmongoc')
conflicts=('libbson' 'libmongoc')
replaces=('libbson' 'libmongoc')
source=("mongo-c-driver-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('bf2bb835543dd2a445aac6cafa7bbbf90921ec41014534779924a5eb7cbd9fd532acd8146ce81dfcf1bcac33a78d8fce22b962ed7f776449e4357eccab8d6110')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../mongo-c-driver-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_STATIC=OFF -DENABLE_TESTS=OFF
    # Test profiles are not found even with ENABLE_TESTS=ON
  make
}

check() {
  cd build
  # No test profile found
  # pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
