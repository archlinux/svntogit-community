# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-blosc
pkgver=1.9.2
pkgrel=3
pkgdesc="A Python wrapper for the extremely fast Blosc compression library"
arch=('x86_64')
url="http://python-blosc.blosc.org/"
license=('MIT')
depends=('python' 'blosc')
optdepends=('python-numpy: for tests and benchmarks')
makedepends=('python-scikit-build' 'ninja')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Blosc/python-blosc/archive/v${pkgver}.tar.gz")
sha256sums=('fd03dc3845b2434a3eab57369042388a8ea99f254b8ea494b4b8b1a0595c8a9a')

build() {
  cd "$srcdir"/${pkgbase}-${pkgver}
  BLOSC_DIR=/usr
  export BLOSC_DIR
  python setup.py build_clib
  python setup.py build_ext --inplace
  python setup.py build
}

package() {
  cd "$srcdir"/${pkgbase}-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSES/PYTHON-BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
