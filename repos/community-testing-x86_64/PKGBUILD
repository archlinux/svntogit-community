# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=4.13
pkgrel=2
pkgdesc='Personal and small-business financial-accounting application'
arch=(x86_64)
url='https://gnucash.org/'
license=(GPL)
depends=(aqbanking gwenhywfar boost-libs guile libdbi webkit2gtk libofx)
makedepends=(boost cmake gtest libdbi-drivers mariadb-libs postgresql-libs swig python)
optdepends=('gnucash-docs: documentation'
            'libdbi-drivers: open and save data into SQL database'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('401a158086635ea17fbb145325558537289aa1d24d022f7c3317e12f3dabd8e7')

build() {
  cd $pkgname-$pkgver

  # Do not treat warnings as errors
  sed -e 's|-Werror||g' -i CMakeLists.txt

  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DWITH_PYTHON=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 2 tests failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
