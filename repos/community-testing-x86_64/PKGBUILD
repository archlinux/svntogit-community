# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-conduit
pkgname=haskell-http-conduit
pkgver=2.2.3.2
pkgrel=24
pkgdesc="HTTP client package with conduit interface and HTTPS support"
url="http://www.yesodweb.com/book/http-conduit"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-conduit" "haskell-conduit-extra" "haskell-exceptions"
         "haskell-http-client" "haskell-http-client-tls" "haskell-http-types" "haskell-lifted-base"
         "haskell-monad-control" "haskell-mtl" "haskell-resourcet")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('07732465673fc4a42115a4a9d4a2e1eec8f826a36dda5825551adf98840810c2c6a22191621fcf1b1ab09e3efd21d190fb3b44f20555ffd601d3dad3efd58e08')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
