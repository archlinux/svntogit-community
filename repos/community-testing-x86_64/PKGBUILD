# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=1.0.0
arch=('i686' 'x86_64')
pkgrel=1
epoch=1
license=('BSD')
makedepends=('cmake' 'doxygen' 'graphviz' 'git' 'python' 'glm' 'gcc5' 'boost')
depends=('glfw' 'freetype2' 'fontconfig' 'freeimage')
source=(https://github.com/arrayfire/forge/archive/v${pkgver}.tar.gz)
md5sums=('f72023e15fffa806e26565fb4edb4379')

build() {
  cd forge-${pkgver}

  rm -rf build
  mkdir build && cd build

  export CC=gcc-5
  export CXX=g++-5
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_EXAMPLES=ON \
      -DBUILD_DOCUMENTATION=ON

  make
}

package() {
  cd forge-${pkgver}

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd build

  make DESTDIR="${pkgdir}/" install
}
