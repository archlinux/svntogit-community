# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=blaze-textual
pkgname=haskell-blaze-textual
pkgver=0.2.2.0
pkgrel=4
pkgdesc="Fast rendering of common datatypes"
url="https://github.com/bos/blaze-textual"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-old-locale' 'haskell-vector')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-double-conversion' 'haskell-test-framework'
             'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('5831a4fd5900bede759fd5b2a00f9e8df08ab120237ece44641764d513728b115f0537c3a01f7dc1a2ccdee6077696f15af8672ce877bde762ee7b42f5fe0ffb')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
