# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@hoetzel.info>
# Contributor: basilburn (basilburn), Paul Bredbury (brebs)
# Contributor: lolilolicon

pkgname=privoxy
pkgver=3.0.28
pkgrel=2
pkgdesc='A web proxy with advanced filtering capabilities.'
arch=('x86_64')
url='https://www.privoxy.org'
license=('GPL2')
depends=('pcre' 'zlib')
makedepends=('autoconf')
backup=('etc/privoxy/'{config,trust,match-all.action,{default,user}.{action,filter}}
        'etc/logrotate.d/privoxy')
source=("https://downloads.sourceforge.net/ijbswa/${pkgname}-${pkgver}-stable-src.tar.gz"
        'privoxy.logrotate.d'
        'privoxy.sysusers'
        'privoxy.service')
md5sums=('c7e8900d5aff33d9a5fc37ac28154f21'
         'e8196e745c765b398b3bfac68f370279'
         'b16776164ab677282c44a8729743eca1'
         '99ecefbdab8d2260d9b217895b3adc73')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"

  autoheader
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc/privoxy --enable-compression

  make
  sed -i '
    s+^confdir \.+confdir /etc/privoxy+
    s+^logdir \.+logdir /var/log/privoxy+
    s+^#\?user-manual .*+user-manual /usr/share/doc/privoxy/user-manual/+' config
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"

  install -Dm644 "$srcdir/privoxy.service" "$pkgdir/usr/lib/systemd/system/privoxy.service"
  install -Dm644 "$srcdir/privoxy.sysusers" "$pkgdir/usr/lib/sysusers.d/privoxy.conf"
  install -Dm644 "$srcdir/privoxy.logrotate.d" "$pkgdir/etc/logrotate.d/privoxy"

  install -Dm755 {,"$pkgdir"/usr/bin/}privoxy
  install -Dm644 {,"$pkgdir"/usr/share/man/man1/}privoxy.1
  install -d -o42 -g42 "$pkgdir"/var/log/privoxy

  install -d "$pkgdir"/etc/privoxy/
  install -m644 config trust *.{action,filter} "$pkgdir"/etc/privoxy/ # -m0660 upstream
  find templates -type f -exec install -Dm644 '{}' "$pkgdir"/etc/privoxy/'{}' \;

  (d="$pkgdir"/usr/share/doc/privoxy
  cd doc/webserver
  install -Dm644 {privoxy-,"$d"/}index.html
  install -m644 p_doc.css ../../{AUTHORS,README,ChangeLog} "$d"/
  install -Dm644 {,"$d"/user-manual/}p_doc.css
  find user-manual developer-manual faq man-page \( -name '*.html' -o -name '*.jpg' \) \
    -exec install -Dm644 '{}' "$d"/'{}' \;)
}
