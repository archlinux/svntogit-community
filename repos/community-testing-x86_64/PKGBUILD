# $Id$
# Maintainer: Benjamin Bukowski <benjamin.bukowski@gmail.com>
# Contributor: Johannes LÃ¶thberg <demizide@gmail.com>

pkgname=atftp
pkgver=0.7.1
pkgrel=5
pkgdesc='Client/server implementation of the TFTP protocol that implements RFCs 1350, 2090, 2347, 2348, and 2349'
arch=('x86_64')
url='http://sourceforge.net/projects/atftp/'
license=('GPL')
depends=('pcre' 'readline')
backup=('etc/conf.d/atftpd')
source=("https://downloads.sf.net/sourceforge/atftp/atftp-$pkgver.tar.gz"
        'atftp-0.7-cflags.patch'
        'atftpd.service'
        'atftpd.conf'
        'sysusers.conf'
        'tmpfiles.conf')
sha256sums=('ae4c6f09cadb8d2150c3ce32d88f19036a54e8211f22d723e97864bb5e18f92d'
            '56a9bdf1c4f9452db041fce1bc4c994e84ce44c6093ef48588f8d8bc93adaf24'
            '74a030f0fb48e97470d59d767039e44d28cbd6c4722621207684220210b8fa65'
            '0b28125099ffdd6869c6fbcb3167e93ccd71a0c6e465b1b62c7dedf9f628dd4e'
            'e56f601bcdf0d64bf98813cd4a1be323541e33921c7d4f350168f62b56e66d11'
            '2096272445c736ba05529af628cc2d46d0236c8f1ecbd50bb1db6dc6c4a972c5')

prepare() {
  cd atftp-$pkgver

  # Respect our CFLAGS; needed below for building with -std=gnu89
  patch -Np1 -i ../atftp-0.7-cflags.patch

  autoreconf -vi
}

build() {
  cd atftp-$pkgver

  CFLAGS+=' -std=gnu89'

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin \
    --enable-libreadline \
    --disable-libwrap
  make
}

package() {
  cd atftp-$pkgver

  make DESTDIR="$pkgdir" install

  install -D -m0644 ../atftpd.service "$pkgdir"/usr/lib/systemd/system/atftpd.service
  install -D -m0644 ../atftpd.conf "$pkgdir"/etc/conf.d/atftpd
  install -D -m0644 ../sysusers.conf "$pkgdir"/usr/lib/sysusers.d/atftp.conf
  install -D -m0644 ../tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/atftp.conf

  # remove in.tftpd link as it conflicts with extra/tftp-hpa
  rm "${pkgdir}/usr/share/man/man8/in.tftpd.8"
  rm "${pkgdir}/usr/bin/in.tftpd"
}
