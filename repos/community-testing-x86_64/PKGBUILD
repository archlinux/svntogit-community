# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Antoine Damhet

pkgname=kanshi
pkgver=1.2.0
pkgrel=3
pkgdesc='Dynamic output configuration for Wayland WMs'
arch=(x86_64)
url="https://wayland.emersion.fr/kanshi/"
license=('MIT')
makedepends=('meson' 'scdoc')
depends=('wayland')
options=(debug)
source=(
    "https://github.com/emersion/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
    "https://github.com/emersion/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.sig"
)
sha256sums=('144428b9e79209b853c4b38a5efb6f16d581a2b9cb96e419b3b0a0f5914bcad4'
            'SKIP')
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48')  # Simon Ser (emersion)

build() {
    meson --prefix=/usr \
        --buildtype=plain \
        -Dman-pages=enabled \
        "${pkgname}-${pkgver}" build
    ninja -C build
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    cd "$pkgname-$pkgver"
    install -Dm0644 "LICENSE" -t "${pkgdir}/usr/share/licenses/$pkgname/"
    install -Dm0644 "README.md" -t "$pkgdir/usr/share/doc/$pkgname/"
}
