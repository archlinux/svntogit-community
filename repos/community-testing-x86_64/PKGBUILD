# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=tcpflow
pkgver=1.4.5
pkgrel=3
pkgdesc="Captures data transmitted as part of TCP connections then stores the data conveniently"
arch=('i686' 'x86_64')
url="https://github.com/simsong/tcpflow"
license=('GPL')
depends=('libpcap' 'cairo' 'openssl' 'sqlite')
makedepends=('git' 'boost')
source=("git+https://github.com/simsong/$pkgname.git#tag=$pkgname-$pkgver"
        'git+https://github.com/simsong/be13_api.git'
        'git+https://github.com/simsong/dfxml.git'
        'git+https://github.com/joyent/http-parser.git'
        tcpflow-openssl-1.1.patch)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'bd2f6fd8df59a0e93fde5fcb8098398a')

prepare() {
  cd $pkgname
  git submodule init
  git config submodule."src/be13_api".url "$srcdir/be13_api"
  git config submodule."src/dfxml".url "$srcdir/dfxml"
  git config submodule."src/http-parser".url "$srcdir/http-parser"
  git submodule update

 # Fix build with openssl 1.1
  patch -p1 -i ../tcpflow-openssl-1.1.patch
  autoreconf -vi
}

build() {
  cd $pkgname
  sh bootstrap.sh
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

