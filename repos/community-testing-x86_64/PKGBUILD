# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=2.93.1.20151208
_commit=88f227a708c2a790a8ceaee6c93c75cd2d1be8fd
#_golibver=0.4.2
_golibrev=902e89b94c9d657f523bcdabad399dc789dcf86e
#_deepinapiver=2.92.1
_deepinapirev=fb83410f533eec79579ac11386520db3a733c550
#_dbusfactoryver=2.92.1
_deepinfactoryrev=3c2b44c085415de28f2f82825aa36012eeb31042
_desktopbaserev=df35ee9bbe33cca436c256a145b88e58e011b737
pkgrel=2
pkgdesc='Daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'metacity' 'poppler-glib'
         'rfkill' 'acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
         'mobile-broadband-provider-info' 'polkit-gnome' 'udisks2' 'upower' 'gcc-go'
         'libxkbfile' 'accountsservice')
makedepends=('deepin-dbus-generator' 'sqlite' 'git' 'mercurial')
optdepends=('networkmanager: Network Management daemon'
            'deepin-grub2-themes: deepin theme for grub menu')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=(#"git+https://github.com/linuxdeepin/dde-daemon.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/dde-daemon.git#commit=$_commit"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        #"git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver"
        "git+https://github.com/linuxdeepin/dde-api.git#commit=$_deepinapirev"
        #"git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_deepinfactoryrev"
        "git+https://github.com/linuxdeepin/deepin-desktop-base.git#commit=$_desktopbaserev"
        'deepin-daemon.sysusers')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '4482f2c82c3652040021dd43515f131184a0417e341dc37db487117012245e25')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/
  
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         code.google.com/p/graphics-go/graphics code.google.com/p/jamslam-freetype-go/freetype \
         code.google.com/p/jamslam-freetype-go/freetype/truetype
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/dde-daemon"
  make USE_GCCGO=1
}

package() {
  cd dde-daemon
  make USE_GCCGO=1 DESTDIR="${pkgdir}" install

  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
  install -Dm644 "${srcdir}"/deepin-desktop-base/desktop-version "${pkgdir}"/etc/deepin-version
}
