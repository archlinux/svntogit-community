# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

pkgname=tor
pkgver=0.4.4.5
pkgrel=3
pkgdesc='Anonymizing overlay network.'
arch=('x86_64')
url='https://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash' 'libseccomp' 'zstd' 'libcap' 'systemd-libs')
optdepends=('torsocks: for torify')
makedepends=('ca-certificates' 'systemd')
backup=('etc/tor/torrc')
source=("https://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc.patch'
        'tor.sysusers'
        'tor.tmpfiles')
md5sums=('b061b7c4ce6102fb7c800b73c8573388'
         'SKIP'
         '88b17cb8d6c73ec4687c135f9a3c57e5'
         '5de6b0aff2a152a03d8eb7a473edc5ff'
         '30e307d882c818cb44bcb2d6b81789a3')
validpgpkeys=('2133BC600AB133E1D826D173FE43009C4607B1FB'  # Nick Mathewson
              'F65CE37F04BA5B360AE6EE17C218525819F78451') # Roger Dingledine

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  # install arch custom files
  install -Dm0644 tor.sysusers "${pkgdir}"/usr/lib/sysusers.d/tor.conf
  install -Dm0644 tor.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/tor.conf
  
  # invoke upstream install routine
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm0644 contrib/dist/tor.service "${pkgdir}"/usr/lib/systemd/system/tor.service

  # install torrc and uncomment essential config sections
  patch -Np1 < "${srcdir}/torrc.patch"
  install -Dm0644 src/config/torrc.sample "${pkgdir}"/etc/tor/torrc
  rm "${pkgdir}"/etc/tor/torrc.sample

  install -Dm0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
