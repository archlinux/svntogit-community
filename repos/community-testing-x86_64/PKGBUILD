# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libpano13
pkgver=2.9.21
pkgrel=3
pkgdesc="Basic library to calculate panoramical pictures - core functions of the panotools"
arch=(x86_64)
url="http://panotools.sourceforge.net/"
license=(GPL2)
depends=(libjpeg libpng libtiff suitesparse)
makedepends=(cmake)
source=(https://downloads.sourceforge.net/sourceforge/panotools/${pkgname}-${pkgver}.tar.gz)
sha256sums=('79e5a1452199305e2961462720ef5941152779c127c5b96fc340d2492e633590')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SPARSE_LEVMAR=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  mkdir "${pkgdir}"/usr/share/doc
  mv "${pkgdir}"/usr/share/{pano13/doc,doc/pano13}
  rmdir "${pkgdir}"/usr/share/pano13
}
