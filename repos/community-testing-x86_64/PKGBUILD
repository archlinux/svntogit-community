# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=docker-compose
pkgver=1.18.0
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('x86_64')
url="https://www.docker.com/"
license=("Apache")
depends=('python-cached-property' 'python-docopt' 'python-yaml' 'python-requests'
         'python-texttable' 'python-websocket-client' 'python-docker' 'python-dockerpty'
         'python-six' 'python-jsonschema' 'python-setuptools')
depends_x86_64+=('docker')
checkdepends=('python-pytest-runner' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/compose/archive/$pkgver.tar.gz")
sha512sums=('08f02a4bdb421f1a0c37d1966a23d1385371e98fc0186265593d552d8b4b568338d9189db173491508ba14a3672489d6a4b5fb1c59b1aca4f76c6ae5db22dfad')

prepare() {
  cd compose-$pkgver

  # Remove upper bound on requires
  sed -i 's/==/>=/g' requirements.txt
  sed -i "s/, < .*',$/',/" setup.py
}

build() {
  cd compose-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it
  # TODO: need a running docker daemon to test

  cd compose-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  # PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" python setup.py pytest
}

package() {
  cd compose-$pkgver

  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 contrib/completion/bash/docker-compose "$pkgdir"/usr/share/bash-completion/completions/docker-compose
}
