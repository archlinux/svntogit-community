# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: bitwave <aur [aT] oomlu [d0T] de>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=yara
pkgver=3.5.0
pkgrel=3
pkgdesc='Tool aimed at helping malware researchers to identify and classify malware samples'
url='https://github.com/VirusTotal/yara'
arch=('i686' 'x86_64')
license=('BSD')
depends=('openssl' 'file')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/VirusTotal/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('84e3b8248749271f154c05216417be76c56563012c0a2013fde5184c6ed03d2933c456d61f036292c42030b52705d5c6e94824d1fd36dbffe46af625ddedaaf5')

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
  ./configure \
    --prefix=/usr \
    --with-crypto \
    --enable-magic
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
