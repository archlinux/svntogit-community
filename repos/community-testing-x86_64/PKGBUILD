# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pyname=xxhash
pkgname=python-${_pyname}
pkgver=1.4.2
pkgrel=2
pkgdesc='Python binding for xxHash'
arch=('x86_64')
url="https://pypi.python.org/pypi/${_pyname}/"
depends=('python' 'xxhash')
makedepends=('python-setuptools')
license=('BSD')
source=("https://github.com/ifduyue/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('06c27c36786ae044f3265c621d495d470f92b08ba3a607d914851322ca75a2ba9931ac6389d712bc08b94e846264caef0f70ce9840ac0f1f722876683bd6a12e')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # link to xxHash
    export XXHASH_LINK_SO=1
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py test
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
