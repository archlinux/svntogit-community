# Contributor: Martin Wimpress <code@flexion.org>

pkgname=engrampa
pkgver=1.18.0
pkgrel=1
pkgdesc="Archive manipulator for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'gzip' 'libarchive' 'tar' 'unzip' 'zip')
makedepends=('caja' 'intltool' 'itstool')
optdepends=('caja: file archiver extension'
            'p7zip: 7Z and ARJ archive support'
            'unace: ACE archive support'
            'unrar: RAR archive support')
groups=('mate-extra')
conflicts=("engrampa-gtk3")
replaces=("engrampa-gtk3")
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        fr-rpm-bsdtar.patch)
sha1sums=('b35634727ae6de833a4c2ea93c2d628b10f079d5'
          '219b05a979bf6f249aaae27964f02345fd81168d')

prepare() {
    cd ${pkgname}-${pkgver}
    # This patch 'depends' on libarchive
    patch -Np1 -i "${srcdir}/fr-rpm-bsdtar.patch"
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-packagekit

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
