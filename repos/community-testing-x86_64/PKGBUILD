# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
_tag='66664cdb52f548b821b2ea28e49613c068e9375d' # git rev-parse ${pkgver}
pkgver=0.9.2
pkgrel=2
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('x86_64')
url='https://github.com/opensvc/multipath-tools'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs')
makedepends=('git' 'systemd')
source=("multipath-tools::git+https://github.com/opensvc/multipath-tools#tag=${_tag}")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"

  # https://github.com/opensvc/multipath-tools/issues/47
  git merge --ff-only '7da868c008624dd33ef8264a958e9c9f592fe52e'

  sed -i -e '/^bindir/c bindir=/usr/bin' Makefile.inc
}

build() {
  cd "${pkgname}"

  make \
    LIB='/usr/lib' \
    FORTIFY_OPT=''
}

package() {
  cd "${pkgname}"

  make -j1 \
    DESTDIR="${pkgdir}" \
    LIB='/usr/lib' \
    install

  install -D -m0644 -t "${pkgdir}/usr/share/doc/${pkgname}" README*
}
