# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=openvkl
pkgver=1.3.0
pkgrel=2
pkgdesc="IntelÂ® Open Volume Kernel Library"
arch=(x86_64)
url="https://www.openvkl.org/"
license=(Apache)
depends=(embree ispc glfw libglvnd imath openvdb rkcommon tbb zlib)
makedepends=(boost cmake)
source=(https://github.com/openvkl/openvkl/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c6d4d40e6d232839c278b53dee1e7bd3bd239c3ccac33f49b465fc65a0692be9')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DOpenVDB_ROOT=/usr \
    -Wno-dev
  make -C build
}

check() {
  make -C build test
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
