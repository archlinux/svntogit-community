# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hasktags
pkgver=0.69.1
pkgrel=3
pkgdesc="Produces ctags \"tags\" and etags \"TAGS\" files for Haskell programs"
url="http://github.com/MarcWeber/hasktags"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=8.0.1" "haskell-json" "haskell-utf8-string")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        ghc8.patch)
sha256sums=('243f802c581ea7c0327065d2c71f21019dc6d7dc7dfadad9d17fffd8b34df1e6'
            'c2213489d358fdaccc7724c8d6bad2c1ce066b421940da582127331a5d1a4ca7')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../ghc8.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr \
            -f-debug
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    rm -r "$pkgdir"/usr/{lib,share/doc}
}
