# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Lieven Moors <lievenmoors@protonmail.com>
# Contributor: Leonard de Ruijter <dev@systeemdenker.nl>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=mididings
pkgname=('mididings' 'mididings-docs')
pkgver=20230114
pkgrel=2
pkgdesc='A MIDI router & processor'
arch=('x86_64')
url='https://github.com/mididings/mididings'
license=('GPL2' 'FDL1.3')
makedepends=(
  'git'
  'boost'
  'boost-libs'
  'alsa-lib'
  'jack'
  'python'
  'python-decorator'
  'python-build'
  'python-installer'
  'python-wheel'
  'python-setuptools'
  'python-sphinx'
  'python-sphinxcontrib-fulltoc'
  'python-pyinotify'
  'python-pyliblo'
  'dbus-python'
  'scdoc'
)
checkdepends=('python-pytest')
_commit='4186c82ca31e94580eaabd9944887aeb520501d8'
source=("$pkgbase::git+https://github.com/mididings/mididings#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgbase"

  # module
  make wheel

  # documentation
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$(pwd)/build/lib.linux-$CARCH-cpython-$python_version:$PYTHONPATH" make docs
}

check() {
  cd "$pkgbase"

  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$(pwd)/build/lib.linux-$CARCH-cpython-$python_version:$PYTHONPATH" pytest -v
}

package_mididings() {
  license=('GPL2')
  depends=(
    'boost-libs'
    'jack'
    'alsa-lib'
    'python'
    'python-decorator'
  )
  optdepends=(
    'dbus-python: send DBUS messages'
    'python-pyinotify: automatically restart when a script changes'
    'python-pyliblo: send/recieve OSC messages'
    'python-pysmf: read/write standard MIDI files using the process_file() function'
    'python-pyxdg: so mididings knows where to look for config files'
    'tk: for livedings GUI'
    'mididings-docs: documentation'
  )

  cd "$pkgbase"

  python -m installer --destdir="$pkgdir" dist/*.whl
}

package_mididings-docs() {
  pkgdesc+=' (documentation)'

  cd "$pkgbase"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgbase" README.md
  cp -vr doc/examples "$pkgdir/usr/share/doc/$pkgbase"
  cp -vr doc/build/html "$pkgdir/usr/share/doc/$pkgbase"
}
