# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: <boromil@gmail.com>

pkgname=libopenraw
pkgver=0.1.3
pkgrel=2
pkgdesc='Library for decoding RAW files'
arch=('x86_64')
url='https://libopenraw.freedesktop.org/'
license=('LGPL')
depends=('gdk-pixbuf2')
makedepends=('boost' 'libxml2')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere <hub@mozilla.com>
source=("https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2"{.asc,})
sha256sums=('SKIP'
            '6405634f555849eb01cb028e2a63936e7b841151ea2a1571ac5b5b10431cfab9')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: libopenraw.freedesktop.org/wiki
# vim: ts=2 sw=2 et:
