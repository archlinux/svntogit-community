# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giorgio Gilestro <giorgio@gilest.ro>

pkgbase=nautilus-python
pkgname=python-nautilus
pkgver=4.0
pkgrel=2
pkgdesc="Python bindings for the Nautilus Extension API"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/NautilusPython"
license=(GPL2)
depends=(nautilus python-gobject)
makedepends=(gtk-doc meson)
source=(https://download.gnome.org/sources/$pkgbase/$pkgver/$pkgbase-$pkgver.tar.xz)
sha256sums=('17243d62eb7d7d839a9461ab8d0701688805c7161a6705e61413a58ec268281a')

build() {
  meson build $pkgbase-$pkgver \
    --prefix=/usr \
    -D docs=enabled
  meson compile -C build
}

package_python-nautilus() {
  meson install -C build --destdir="$pkgdir"
}
