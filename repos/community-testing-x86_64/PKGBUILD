# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Iwan Timmer <irtimmer@gmail.com>

pkgname=pcp
pkgver=5.2.2
pkgrel=1
pkgdesc="System performance analysis toolkit"
arch=('x86_64')
url="https://pcp.io"
license=('LGPL')
depends=('python' 'avahi' 'procps-ng' 'which')
makedepends=('libmicrohttpd' 'cairo' 'qt5-svg')
optdepends=('libmicrohttpd: support for pmwebd'
            'cairo: support for pmwebd'
            'qt5-svg: support for PCP-GUI and pmchart'
            'perl-xml-tokeparser: support for sar2pcp'
            'perl-date-parse: support for sar2pcp')
source=("https://github.com/performancecopilot/pcp/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "sysuser.conf"
        "tmpfile.conf")
sha256sums=('ba9d43055a84ffa978c6d3902f52ce67acae531aca1c2a3151c3570326d5d001'
            '3e21ef7925296f73c91fe85c8dba341ce00366163845dd8993e2813b0ad5e4a4'
            '0166ffea180527de5a48a8e8f145cc80860b93e8f44bc5f96836c3d1cb4b01a2')

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-rundir=/run/pcp \
    --without-dstat-symlink
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DIST_ROOT="$pkgdir" PCP_USER=root PCP_GROUP=root install

  install -D -m644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/pcp.conf
  install -D -m644 "$srcdir"/tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/pcp.conf

  rm -vrf "$pkgdir"/var/lib/pcp/testsuite "$pkgdir"/run "$pkgdir"/var/tmp
}
