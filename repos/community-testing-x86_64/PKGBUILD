# $Id: PKGBUILD 373818 2018-08-24 16:31:09Z heftig $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgbase=vhba-module
pkgname=(vhba-module vhba-module-dkms)
pkgver=20170610
pkgrel=100
pkgdesc="Kernel module that emulates SCSI devices"
url="http://cdemu.sourceforge.net/"
arch=(x86_64)
license=(GPL)
depends=('linux>=4.18' 'linux<4.19')
makedepends=('linux-headers>=4.18' 'linux-headers<4.19')
_extramodules=extramodules-ARCH
source=("https://downloads.sourceforge.net/cdemu/$pkgbase-$pkgver.tar.bz2"
        60-vhba.rules dkms.conf)
sha256sums=('7970c93f989d9c4f2629371bf5ee7a76f95e4c12342c3320ddc528d0df02d9ec'
            '3052cb1cadbdf4bfb0b588bb8ed80691940d8dd63dc5502943d597eaf9f40c3b'
            '8cab0ebb4fee72069d63616b0983f105b98d1261e72e9bef5509a6e60bc382a7')

prepare() {
  cd $pkgbase-$pkgver
}

build() {
  cd $pkgbase-$pkgver
  make KERNELRELEASE="$(cat /usr/lib/modules/$_extramodules/version)"
}

package_vhba-module() {
  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 *.ko
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"

  find "$pkgdir" -name '*.ko' -exec xz {} +
}

package_vhba-module-dkms() {
  depends=(dkms)
  provides=("vhba-module=$pkgver-$pkgrel")
  conflicts=(vhba-module)

  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/src/$pkgbase-$pkgver" -m644 Makefile vhba.c ../dkms.conf
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"
}

# vim:set ts=2 sw=2 et:
