# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-tcplugins-extra
pkgname=haskell-ghc-tcplugins-extra
pkgver=0.3
pkgrel=5
pkgdesc="Utilities for writing GHC type-checker plugins"
url="https://github.com/clash-lang/ghc-tcplugins-extra"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('67b2787962ed7a7336ca32181f09fc9a7bf045e47fbb77419d848bbe2f635bb5b1b6acbf00a384113e9af0f87f5e101101e685bbb99ea2b3e1741abec0082c88')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *8.6/<9/' $_hkgname.cabal
} 

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
