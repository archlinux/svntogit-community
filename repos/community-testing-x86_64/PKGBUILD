# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=hotdoc
pkgname=hotdoc
pkgver=0.13.7
pkgrel=3
pkgdesc="the tastiest API documentation system"
arch=('x86_64')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('LGPL')
_py_deps=('appdirs' 'cchardet' 'dbus-deviation' 'lxml' 'networkx' 'pkgconfig'
          'schema' 'toposort' 'wheezy-template' 'yaml')
depends=("${_py_deps[@]/#/python-}" 'json-glib')
makedepends=('python-setuptools' 'cmake')
optdepends=('clang: for the C extension'
            'llvm: for the C extension')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('1123a659e2c94972c32813ec301191d333db24712a83f7e3d06dd58d47cf0098')
b2sums=('89a2f2797d8f4ef3a004b37feb29a22d251d2d728d3695da1688dbce58257897305be92ae95397154fb5f8cb8e295e46f7f880bfcfe3266c94ab9c6b30501e94')

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build/lib.linux-$CARCH-3*

    python -m unittest
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
