# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=githash
pkgname=haskell-githash
pkgver=0.1.6.0
pkgrel=5
pkgdesc="Some handy Template Haskell splices for including the current git hash and branch in the code of your project"
url="https://github.com/snoyberg/githash"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-compat' 'git')
makedepends=('ghc' 'haskell-hpack' 'haskell-hspec' 'haskell-temporary' 'haskell-unliftio')
source=("git+https://github.com/snoyberg/githash.git#tag=githash-$pkgver"
        ghc9.patch)
sha512sums=('SKIP'
            'a3ae967a52cb0e110310e6d13cbbf0275e145c1e437a59378a15e270ebe14569a6513e3a6cdfc0f24d1fdf40207a50584a73f8310db774720810264638054ea6')

prepare() {
    cd $_hkgname
    patch -p1 -i ../ghc9.patch
    hpack
}

build() {
    cd $_hkgname
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
