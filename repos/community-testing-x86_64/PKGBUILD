# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=lupa
pkgname=python-lupa
pkgver=1.9
pkgrel=2
pkgdesc="Python wrapper around Lua and LuaJIT"
arch=('x86_64')
url="https://github.com/scoder/lupa"
license=('MIT')
depends=('lua53' 'python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('a3e11d806ca02cf72e490ec1974f8b96a14a1091895c9dccebe0b8d52dd82e8e')
b2sums=('6a0acc6993fb5510c72a04b223247046a8634f62f0c1db38fd195315677d619c8e9e5857d1524f87101ac376fb0493209c22e5b8951646491e06907b6ff06664')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # --no-bundle is not removed from argv if lua is properly detected, so the
    # only working safety net is to rm the bundled sources
    rm -rf third-party
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build --no-luajit
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
