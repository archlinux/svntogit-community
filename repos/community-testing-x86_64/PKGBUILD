# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=opencc
pkgname=(opencc opencc-doc)
pkgver=1.1.1
pkgrel=2
url="https://github.com/BYVoid/OpenCC"
arch=('x86_64')
license=('Apache')
makedepends=('cmake' 'doxygen' 'python-setuptools' 'python-wheel')
source=("https://github.com/BYVoid/OpenCC/archive/ver.$pkgver/$pkgbase-$pkgver.tar.gz")
sha512sums=('48ccf61f63cf0a1c0fc484a52ae1d406a28949a52be7d7d1595e6aaf87f40ac47ea4697308441a9381f0cd6f404ee5a28b85a2fa2f288c4b3933be36f4a41363')

prepare() {
  cd OpenCC-ver.$pkgver/python
  # Use system opencc
  sed -i '/BuildPyCommand,/d' setup.py
  sed -e "s|os.path.join(_thisdir, 'clib', 'lib', _libopenccfilename)|os.path.join('/usr/lib', _libopenccfilename)|" \
      -e "s|os.path.join(_thisdir, 'clib', 'share', 'opencc')|'/usr/share/opencc'|" \
      -i opencc/__init__.py
}

build() {
  cd OpenCC-ver.$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_DOCUMENTATION:BOOL=ON
  make

  cd python
  python setup.py build
}

package_opencc() {
  pkgdesc="Library for Open Chinese Convert"
  depends=('gcc-libs')

  cd OpenCC-ver.$pkgver
  make DESTDIR="${pkgdir}" install

  cd python
  python setup.py install --root="$pkgdir" --optimize=1

  # Remove docs - install in splitted package
  rm -r "$pkgdir/usr/share/opencc/doc"
}

package_opencc-doc() {
  pkgdesc="Documentation for Library for Open Chinese Convert"

  cd OpenCC-ver.$pkgver/doc
  make DESTDIR="${pkgdir}" install
}
