# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python-numexpr
pkgver=2.7.3
pkgrel=3
pkgdesc="Fast numerical array expression evaluator for Python, NumPy, PyTables, pandas"
url="https://github.com/pydata/numexpr"
arch=('x86_64')
license=('MIT')
depends=('python-numpy')
makedepends=('python-setuptools')
source=($pkgname-$pkgver.tar.gz::https://github.com/pydata/numexpr/archive/v$pkgver.tar.gz)
sha256sums=('00d6b1518605afe0ed10417e0ff07123e5d531c02496c6eed7dd4b9923238e1e')

build() {
  cd "$srcdir"/numexpr-$pkgver
  python setup.py build
}
package() {
  cd "$srcdir"/numexpr-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
