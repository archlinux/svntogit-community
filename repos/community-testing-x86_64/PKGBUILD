# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=rkcommon
pkgver=1.8.0
pkgrel=2
pkgdesc="Intel RenderKit common C++/CMake infrastructure"
arch=(x86_64)
url="https://github.com/ospray/rkcommon/"
license=(Apache)
depends=(tbb)
makedepends=(cmake)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('f037c15f7049610ef8bca37500b2ab00775af60ebbb9d491ba5fc2e5c04a7794')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Wno-dev
  make -C build
}

check() {
  make -C build test
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
