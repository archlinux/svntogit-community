# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jose Valecillos <valecillosjg (at) gmail (dot) com>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
pkgname=gource
pkgver=0.53
pkgrel=4
pkgdesc="software version control visualization"
license=(GPL3)
arch=(x86_64)
url=http://gource.io/
depends=('ftgl' 'sdl2' 'sdl2_image' 'pcre2' 'glew' 'boost-libs')
makedepends=('boost' 'glm' 'mesa')
source=(https://github.com/acaudwell/Gource/releases/download/gource-${pkgver}/gource-${pkgver}.tar.gz)
sha512sums=('e4190eb581d9998808d1bb4f18ba5a3ae22bacf643b9a150c7d1607e97fca2eab720592aefdcc6a47f92adb4a8df17db48bb941a67ee3d23a10f300754f473be')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}
# vim: ts=2:sw=2 et:
