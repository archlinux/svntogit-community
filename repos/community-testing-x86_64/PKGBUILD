# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pyproj
pkgver=3.0.1
pkgrel=1
pkgdesc="Python interfaces to PROJ.4 library"
arch=('x86_64')
url="https://github.com/pyproj4/pyproj"
license=('custom')
depends=('proj' 'python')
makedepends=('python-setuptools' 'cython')
source=($pkgname-$pkgver.tar.gz::https://github.com/pyproj4/pyproj/archive/$pkgver.tar.gz)
sha256sums=('f078f1d2e4aee9e2006a985c768741eb152f94e0ada69e9db92015986fb9a4f6')

prepare() {
  cd "${srcdir}"

# Remove RPATH
  sed -i '/runtime_library_dirs=/d' pyproj-$pkgver/setup.py
}

build() {
  cd "${srcdir}"

  export PROJ_DIR=/usr

  cd pyproj-$pkgver
  python setup.py build
}

package() {
  cd "${srcdir}"/pyproj-$pkgver
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
