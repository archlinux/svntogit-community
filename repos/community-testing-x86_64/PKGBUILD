# $Id$
# Maintainer:  Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_extramodules=extramodules-3.7-ARCH
pkgname=ndiswrapper
pkgver=1.57
pkgrel=29
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors."
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
install=ndiswrapper.install
depends=('linux>=3.7' 'linux<3.8' 'wireless_tools' 'perl')
makedepends=('linux-headers')
provides=("$pkgname-utils" "$pkgname-bin")
replaces=("$pkgname-utils" "$pkgname-bin")
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        "linux33.patch"
        "linux37.patch")
options=('!strip')
sha256sums=('208699faa01ab8ca707cb8a28db20417d07887f993c0830dd34c7d9c4f84a56c'
            '05fb73a665567ea4bbc33fb80cb7fa3b6123997a5c46bf24f2bf1bba25725bd0'
            '3e94b838ec38bb43340b4d2b3f887ec3112d93803d4aa51230a6e0b0ff8ef789')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  _kver="$(cat /usr/lib/modules/${_extramodules}/version)"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile
  patch -i "$srcdir/linux33.patch" -N -p0

  # fix linux37 - Thanks to Manjaro Linux
  sed -i "s|/include/linux/version.h|/include/generated/uapi/linux/version.h|g" driver/Makefile
  patch -i $srcdir/linux37.patch -N -p1

  # make sure we point to the right build directory
  sed -i "/^KBUILD/ s,.*,KBUILD = $(readlink -f /usr/lib/modules/$_kver/build)," driver/Makefile

  make KVERS=$_kver
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  _kver="$(cat /usr/lib/modules/${_extramodules}/version)"

  make INST_DIR="usr/lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" install

  gzip "$pkgdir/usr/lib/modules/$_extramodules/$pkgname.ko"
}

# vim:set ts=2 sw=2 et:
