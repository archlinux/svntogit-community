# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jelle van der Waa <jelle at vdwaa dot nl>
# Contributor: Borromini <gotleenucks at g-male dot com>

pkgname=evolution-rss
pkgver=0.3.95
pkgrel=4
pkgdesc="Plugin for Evolution Mail that enables reading of RSS/RDF/ATOM feeds"
arch=('x86_64')
url="http://gnome.eu.org/index.php/Evolution_RSS_Reader_Plugin"
license=('GPL')
depends=('evolution')
makedepends=('intltool')
source=(http://gnome.eu.org/$pkgname-$pkgver.tar.xz
        evo-3.21.90.patch
        evo-3.23.2.patch)
sha256sums=('3fb28eec798b3192ae155b92d17b852264d0e5e35185a4f0c8f526a8d587eb1f'
            '36ed895fcdd6c71998af1ab6b315e6f1ca027f56466c8844920761711e5589ef'
            'b7a5977a5d27321420eac22cb346735338444cb6f4b4805f07d7502f241b29dc')

prepare() {
  cd $pkgname-$pkgver
  # Compatibility patches from Fedora
  patch -Np1 -i ../evo-3.21.90.patch
  patch -Np1 -i ../evo-3.23.2.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
