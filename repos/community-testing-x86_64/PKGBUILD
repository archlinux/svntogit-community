# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
# Contributor: pingplug <pingplug@foxmail.com>
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=magma
pkgver=2.7.1
pkgrel=1
pkgdesc="Matrix Algebra on GPU and Multicore Architectures"
arch=('x86_64')
url="https://icl.utk.edu/magma/"
license=('custom')
depends=('blas' 'lapack' 'cuda')
makedepends=('gcc11-fortran' 'cmake' 'ninja')
optdepends=('python: for examples and tests'
            'gcc11-fortran: Fortran interface')
source=("${pkgname}-${pkgver}.tar.gz::https://icl.utk.edu/projectsfiles/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz"
        'add_cuda_archs.patch')
sha256sums=('d9c8711c047a38cae16efde74bee2eb3333217fd2711e1e9b8606cbbb4ae1a50'
            '1ed3706a65a124ecef71012273687307aac70bfc80bc99a978002dfb0ee000cc')
options=(!lto)

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/add_cuda_archs.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  CC=/usr/bin/gcc-11 \
  CXX=/usr/bin/g++-11 \
  FC=/usr/bin/gfortran-11 \
  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DGPU_TARGET="sm_52 sm_53 sm_60 sm_61 sm_62 sm_70 sm_72 sm_75 sm_80 sm_86 sm_87 sm_89 sm_90"
  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" ninja -Cbuild install

  install -d "${pkgdir}"/usr/share/magma/example
  cp -r "${srcdir}"/magma-${pkgver}/example/* "${pkgdir}"/usr/share/magma/example/
  install -d "${pkgdir}"/usr/share/magma/testing
  cp -r "${srcdir}"/magma-${pkgver}/testing/* "${pkgdir}"/usr/share/magma/testing/
  install -Dm644 "${srcdir}"/magma-${pkgver}/COPYRIGHT "${pkgdir}"/usr/share/licenses/magma/LICENSE
}

# vim:set ts=2 sw=2 et:
