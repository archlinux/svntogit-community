# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=5.0.1
pkgrel=2
pkgdesc="Javascript Bindings for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('gtk3' 'gobject-introspection-runtime' 'js78')
makedepends=('meson' 'samurai' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('a341e8deae33baf51ed21ea0b03484ba459d83be8689f3928acbd286ebe878719cdd6b4c576907b08b117b3c5738a11f95d0e37ae21f94ae6b88631fad5d3375')
b2sums=('7592b5b0be8fed1efc9fe1c7baca081515b8010935e24ba0cb1e30ce78890941f5c3b2384ee3846a02c3ba5a50c6d6ce5df232f02507743cf15ac089e29d7f23')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/builddir
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir=/usr/lib \
          --buildtype=plain \
          -Dinstalled_tests=false \
          ..

    samu
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    # Needs a display
    xvfb-run meson test --print-errorlogs
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    DESTDIR="${pkgdir}" samu install
}
