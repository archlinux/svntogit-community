# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: mortzu <me@mortzu.de>
# Contributor: fnord0 <fnord0@riseup.net>

pkgname=acpi_call-lts
pkgver=1.1.0
pkgrel=124
pkgdesc='A linux kernel module that enables calls to ACPI methods through /proc/acpi/call'
url='https://github.com/mkottman/acpi_call'
arch=('x86_64')
license=('GPL')
makedepends=('linux-lts-headers')
provides=('acpi_call')
source=("acpi_call-${pkgver}.tar.gz::https://github.com/mkottman/acpi_call/archive/v${pkgver}.tar.gz")
sha256sums=('d0d14b42944282724fca76f57d598eed794ef97448f387d1c489d85ad813f2f0')

prepare() {
  cd acpi_call-$pkgver

  # Fix build with Linux >= 3.17
  sed -i 's|acpi/acpi.h|linux/acpi.h|' acpi_call.c

  # Fix build with Linux >= 4.12
  sed -i 's|asm/uaccess.h|linux/uaccess.h|' acpi_call.c
}

build() {
  cd acpi_call-$pkgver
  make KVERSION="$(</usr/src/linux-lts/version)"
}

package() {
  depends=(linux-lts)

  cd acpi_call-$pkgver
  _extradir="/usr/lib/modules/$(</usr/src/linux-lts/version)/extramodules"
  install -Dt "$pkgdir$_extradir" -m644 *.ko
  find "$pkgdir" -name '*.ko' -exec xz {} +

  echo acpi_call | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"

  mkdir -p "$pkgdir/usr/share/$pkgname"
  cp -t "$pkgdir/usr/share/$pkgname" -dr --no-preserve=ownership examples support
}

# vim:set ts=2 sw=2 et:
