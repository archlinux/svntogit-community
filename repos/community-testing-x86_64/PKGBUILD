# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=3.4.3
pkgrel=2
epoch=1
pkgdesc="Desktop Planetarium"
url="https://kde.org/applications/education/kstars/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(wcslib libraw kplotting knewstuff knotifyconfig qt5-datavis3d libindi qt5-quickcontrols qt5-websockets qtkeychain
         breeze-icons hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen)
optdepends=('xplanet: XPlanet support')
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c81119a85e9265991dd0fbfafdc6ba797bf3bedce64f4d5260511bafccebbe48'
            'SKIP')
validpgpkeys=(259D9FCEE9175351965633696D9CE2AEE028C4F3) # Jasem Mutlaq <mutlaqja@ikarustech.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
