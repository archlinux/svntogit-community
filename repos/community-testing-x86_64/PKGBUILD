# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=thin
pkgname=ruby-${_gemname}
pkgver=1.8.0
pkgrel=2
pkgdesc='Thin and fast Ruby web server'
url='https://github.com/macournoyer/thin'
arch=('x86_64')
license=('RUBY')
depends=('glibc' 'ruby' 'ruby-rack' 'ruby-eventmachine' 'ruby-daemons')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/macournoyer/thin/archive/v${pkgver}.tar.gz)
sha512sums=('3166dbefcb4900a4cb839c8e3a36c5e27734980f8516547beb1732d446dd14e64b02936564cc5fe20b20c926185ca6a0114e6b246f038104a4891522e490d97f')
b2sums=('d6e818d3d0c077ee54c8293e66f176e6c4d421af8fd906220371b84791bde8738aa252517582a52f898e0e6e8bc796deb1d31371ac1ebb0d56bfd40c31594175')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a example "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
}

# vim: ts=2 sw=2 et:
