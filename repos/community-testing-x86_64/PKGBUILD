# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-initial-setup
pkgver=44.0
pkgrel=1
pkgdesc='Simple, easy, and safe way to prepare a new system'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/gnome-initial-setup'
license=('GPL')
depends=(
  'cheese'
  'gdm'
  'geocode-glib-2'
  'gnome-online-accounts'
  'ibus'
  'libgweather-4'
  'libnm'
  'libnma-gtk4'
  'libpwquality'
  'librest'
  'malcontent'
  'webkitgtk-6.0'
)
makedepends=('meson')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('593cfc6dc8f82a98671b94c035b97dbe88ef56e708780b2ad1d2324e4d04bbff')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "${pkgdir}"
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # Setup system user and group
  echo 'u gnome-initial-setup - "GNOME Initial Setup" /run/gnome-initial-setup' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /run/gnome-initial-setup 0700 gnome-initial-setup gnome-initial-setup -' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
