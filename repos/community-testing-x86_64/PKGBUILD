# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=sdcv
pkgver=0.5.2
pkgrel=3
pkgdesc="StarDict Console Version"
arch=('x86_64')
url="https://dushistov.github.io/sdcv/"
license=('GPL')
depends=('glib2' 'readline' 'zlib')
makedepends=('cmake')
provides=('stardict')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/Dushistov/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('ec08dc084a0748d5a89df01a20bbf294f09b0366295cd418000f15d2e043c4f7')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  install -d build
  cd build

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
  make lang
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver/build

  make DESTDIR="${pkgdir}" install
}
