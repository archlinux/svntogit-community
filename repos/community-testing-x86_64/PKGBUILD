# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: tectonic-deploy <sasha+tectonic@hackafe.net>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jan Tojnar <jtojnar@gmail.com>

pkgname=tectonic
pkgver=0.11.0
pkgrel=2
pkgdesc='Modernized, complete, self-contained TeX/LaTeX engine, powered by XeTeX and TeXLive'
arch=('x86_64')
url='https://tectonic-typesetting.github.io/'
license=('MIT')
depends=('fontconfig' 'harfbuzz-icu' 'openssl')
makedepends=('cargo' 'pkg-config')
options=(!lto)
source=("https://github.com/tectonic-typesetting/tectonic/archive/refs/tags/tectonic@${pkgver}.tar.gz")
b2sums=('a7bfb54313b134609303d0038c9f0e382e2173d664e8f6233cead0336c2c0c6a6bbf142de073021225a06b28f4134d3b206b53566e887f1f6c498f37baa64223')

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --locked --features external-harfbuzz
}

check() {
  cd ${pkgname}-${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --release --locked --features external-harfbuzz
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver}
  install -Dm755 target/release/tectonic "${pkgdir}"/usr/bin/tectonic
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
