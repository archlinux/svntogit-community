# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-pyicu
pkgver=2.9
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
depends=('python' 'icu')
makedepends=('python-setuptools')
checkdepends=('python-fonttools' 'python-pytest' 'python-six')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('49b0961ed855517f039ac8a863b1af1a58c6716751eb1f5cebb987a16faabc5585ed6ed3ed3d80d4cf45e9091d855cafe4b6683f4d274e5c4d41c9dab9164799')

build() {
  cd PyICU-$pkgver
  python setup.py build
}

check() {
  cd PyICU-$pkgver
  python setup.py test
}

package() {
  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
