# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.10.5
pkgrel=1
pkgdesc='Creates virtual Ethernet networks of almost unlimited size.'
arch=('x86_64')
url='https://www.zerotier.com/'
license=('custom:BSL')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('rust')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/refs/tags/$pkgver.tar.gz")
sha512sums=('fd79fcd6e9ed37e6c173000c7eaaf7156d44479e06981d60e6ced05032ffcda2731d54c8c145741bd4cb5953c4e83c9311775298f3a7f77a9de47c459c1884be')

prepare() {
  cd ZeroTierOne-$pkgver
  sed \
    -e 's/sbin/bin/' \
    -e 's/LDFLAGS=/LDFLAGS?=/' \
    -e 's/RUSTFLAGS=/RUSTFLAGS?=/' \
    -e 's/cargo build $(RUSTFLAGS)/cargo build --release/' \
    -i make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver
  make V=1
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
