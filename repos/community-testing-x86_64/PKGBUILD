# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Wallace <daniel.wallace@gatech.edu>

pkgname=plex-home-theater
_pkgver=1.3.5.431-7966a4df
pkgver=${_pkgver%-*}
pkgrel=2
pkgdesc='Plex Home Theater'
arch=('i686' 'x86_64')
url='http://www.plexapp.com/'
license=('GPL2')
depends=('boost-libs' 'curl' 'ffmpeg-compat' 'glew' 'libcdio' 'libmad'
         'libmicrohttpd' 'libmpeg2' 'libsamplerate' 'libssh' 'libusb-compat'
         'libvdpau' 'libxrandr' 'lzo' 'sdl_image' 'sdl_mixer' 'smbclient'
         'taglib' 'tinyxml' 'yajl')
makedepends=('boost' 'cmake' 'doxygen' 'git' 'java-environment' 'libcec'
             'libplist' 'libshairport' 'nasm' 'swig' 'unzip' 'zip')
optdepends=('libplist: AirPlay support'
            'libshairport: AirPlay support'
            'libcec: Pulse-Eight USB-CEC adapter support'
            'pulseaudio: PulseAudio support')
#source=("https://github.com/plexinc/plex-home-theater-public/archive/v${_pkgver}.tar.gz"
source=("git+https://github.com/plexinc/plex-home-theater-public.git#commit=${_pkgver##*-}"
        'plex-ffmpeg-compat.patch'
        'plex-libcec-2.2.0.patch'
        'plex-lirc-socket.patch'
        'plexhometheater.sh')
#sha256sums=('f119d11e13ce2ea8a31805a5ea91b65d95f0fc9370d49d8da51bda1e028b564c'
sha256sums=('SKIP'
            '5b370e4dec6998130df46ad6bd5ca6258e3a040dd6c6cc4085c335dc3d6050bd'
            '13032e8da35b0abdd22109f9ee34c63e43e0f9863fe81d4fa503c05ede23a212'
            '630070125ad5239163d89fed5bf7f83abf4a411ac3e24aa2e41f99c5c3c8fb7f'
            'dc6bd394c07a2ececbb2f8c53cb54afd5d78f7c00a0b34acab3e71217da085fb')

prepare() {
  #cd plex-home-theater-public-${_pkgver}
  cd plex-home-theater-public

  patch -Np1 -i ../plex-libcec-2.2.0.patch
  patch -Np1 -i ../plex-lirc-socket.patch
  patch -Np1 -i ../plex-ffmpeg-compat.patch
}

build() {
  #cd plex-home-theater-public-${_pkgver}
  cd plex-home-theater-public

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' \
           -DCMAKE_C_FLAGS="$CMAKE_C_FLAGS -I/usr/include/glib-2.0 -I/usr/lib/glib-2.0/include" \
           -DCMAKE_CXX_FLAGS="$CMAKE_CXX_FLAGS -I/usr/include/glib-2.0 -I/usr/lib/glib-2.0/include" \
           -DENABLE_{AUTOUPDATE,DUMP_SYMBOLS}='FALSE' -DENABLE_PYTHON='TRUE' \
           -DPYTHON_EXEC='/usr/bin/python2' -DUSE_INTERNAL_FFMPEG='FALSE' \
           -DCREATE_BUNDLE='FALSE'
  make
}

package() {
  #cd plex-home-theater-public-${_pkgver}/build
  cd plex-home-theater-public/build

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/usr/{lib/plexhometheater,share/{applications,pixmaps}}
  mv "${pkgdir}"/usr/bin/{system,xbmc-xrandr} "${pkgdir}"/usr/lib/plexhometheater/
  mv "${pkgdir}"/usr/share/XBMC "${pkgdir}"/usr/share/plexhometheater
  rm -rf "${pkgdir}"/usr/bin/*.so
  install -m 755 "${srcdir}"/plexhometheater.sh "${pkgdir}"/usr/bin/
  install -m 644 ../plex/Resources/plexhometheater.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../plex/Resources/plex-icon-256.png "${pkgdir}"/usr/share/pixmaps/plexhometheater.png
}

# vim: ts=2 sw=2 et:
