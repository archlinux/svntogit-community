# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=6.1.7
pkgrel=3
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpeg' 'kcodecs' 'libcue' 'qt5-svg' 'qtdbusextended' 'qtmpris'
         'taglib' 'udisks2-qt5' 'vlc')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
options=('!lto')
source=("https://github.com/linuxdeepin/deepin-music/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        fix-build-on-x86_64-add-fPIC-option.patch)
sha512sums=('f5a0c70e24ec05c1a5e2cde4d98707b21e093825f2584f7395db3dfdab0326d2904aa595f15ad186cda505cc816a89017e15a75533343e957039a81bac6d8def'
            'fd04580c943868769d3ba93bbccdff5893052821c067e217df8bd009d6891c475bffba82d66e66112ebee3b83f0de956b0aef1ccafe6680b82ba4ad724a650b3')

prepare() {
  cd deepin-music-$pkgver
  # https://github.com/linuxdeepin/deepin-music/pull/118
  patch -Np1 -i ../fix-build-on-x86_64-add-fPIC-option.patch
}

build() {
  cd deepin-music-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-music-$pkgver
  DESTDIR="$pkgdir" ninja install
}
