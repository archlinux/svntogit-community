# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=vifm
pkgver=0.10.1
pkgrel=2
pkgdesc="A file manager with curses interface, which provides Vi[m]-like environment"
arch=('x86_64')
url="https://vifm.info/"
license=('GPL')
depends=('ncurses' 'desktop-file-utils')
changelog=$pkgname.changelog
source=(https://prdownloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2{,.asc})
validpgpkeys=('22349CDBE5227D2108A5667799DC5E4DB05F6BE2') # xaizek <xaizek@posteo.net>
sha256sums=('99006f56db05e1bdfb7983e8d5c31c9ac93bf2ac9e0409a577c8ca660fecd03b'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
