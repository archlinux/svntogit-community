# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.6.4
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-imageformats' 'qt5-multimedia'
         'qt5-svg' 'qt5-x11extras' 'startup-notification')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("https://github.com/linuxdeepin/dtkwidget/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1a5046ceaedaee18a6f4aec85085fdfe1d4cf095fa8fd66f076ef3cf607a5435ec87a9c6d0d7950bbfe174fbda743176efebc9def9b5a2e65c9d2451dc412bc5')

build() {
  cd dtkwidget-$pkgver
  cmake . -GNinja \
      -DMKSPECS_INSTALL_DIR=/usr/lib/qt/mkspecs/modules/ \
      -DBUILD_DOCS=ON \
      -DQCH_INSTALL_DESTINATION=/usr/share/doc/qt \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkwidget-$pkgver
  DESTDIR="$pkgdir" ninja install
}
