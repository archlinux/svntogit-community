# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=4.4
pkgrel=2
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('bzip2' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd'
'libmad' 'libx11' 'opusfile' 'qt5-base' 'qt5-svg' 'speex')
makedepends=('alsa-lib' 'capnproto' 'fftw' 'libfishsound' 'libid3tag' 'liblo'
'liblrdf' 'liboggz' 'libpulse' 'libsamplerate' 'libsndfile' 'meson' 'portaudio'
'rubberband' 'serd' 'sord')
provides=('vamp-host')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/sv_v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a76a6e5fc2381a3e7c89762687176fa60a8b731951438094af0ecd50103f73104a4366eacf731015573f8d5e5f436cf497a3e0e041363d5158d4f546bdd1f7b0')
b2sums=('37797b5fdcc397530a8b36bb1727bc56be703738505a117c853616eace41344b9526f98602ec872f4baa157cb1e92044bf7e3ef9079fa24b456453542bcf5763')

build() {
  cd "${pkgname}-${pkgver}"
  arch-meson build
  ninja -C build
}

package() {
  depends+=('libasound.so' 'libcapnp.so' 'libfftw3.so' 'libfftw3f.so'
  'libfishsound.so' 'libid3tag.so' 'libkj.so' 'liblo.so' 'liblrdf.so'
  'liboggz.so' 'libportaudio.so' 'libpulse.so' 'librubberband.so'
  'libsamplerate.so' 'libserd-0.so' 'libsndfile.so' 'libsord-0.so')

  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build
  # samples
  install -vDm 644 samples/* -t "${pkgdir}/usr/share/${pkgname}/samples"
  # templates
  install -vDm 644 templates/* -t "${pkgdir}/usr/share/${pkgname}/templates"
  # docs
  install -vDm 644 {CHANGELOG,CITATION,README{,_OSC}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
