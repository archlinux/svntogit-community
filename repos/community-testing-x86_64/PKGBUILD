# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=adriconf
pkgver=2.4
pkgrel=2
pkgdesc="GUI tool to configure Mesa drivers by setting options and writing them to the standard drirc file"
arch=(x86_64)
url="https://gitlab.freedesktop.org/mesa/adriconf"
license=(GPL3)
depends=(boost-libs gtkmm3 libxml++ pciutils)
makedepends=(boost cmake git)
_commit=bc310a88b251c8a1d4e14f4a6b18f07498b35e2a  # tags/v2.4
source=("git+https://gitlab.freedesktop.org/mesa/adriconf#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_UNIT_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  cd ../$pkgname
  install -Dm644 flatpak/org.freedesktop.adriconf.metainfo.xml "$pkgdir/usr/share/metainfo/org.freedesktop.adriconf.metainfo.xml"
  install -Dm644 flatpak/org.freedesktop.adriconf.desktop "$pkgdir/usr/share/applications/org.freedesktop.adriconf.desktop"
  install -Dm644 flatpak/org.freedesktop.adriconf.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/org.freedesktop.adriconf.png"
}
