# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libmodsecurity
pkgver=3.0.6
pkgrel=2
epoch=1

pkgdesc='ModSecurity v3 library component'
arch=('x86_64')
depends=('yajl' 'curl' 'libxml2' 'pcre' 'geoip' 'libmaxminddb' 'ssdeep' 'luajit')
makedepends=('doxygen')
url="https://github.com/SpiderLabs/ModSecurity/tree/v3/master"
license=('APACHE')
source=("https://github.com/SpiderLabs/ModSecurity/releases/download/v$pkgver/modsecurity-v$pkgver.tar.gz"{,.asc})
sha256sums=('06315d9a32fc830997178f30a69e933c9c2364b263a2f036959bebc4dde59a08'
            'SKIP')
validpgpkeys=('190EFACCA1E9FA466A8ECD9CE6DFB08CE8B11277'  # Felipe Zimmerle da Nobrega Costa <felipe@zimmerle.org>
              'F126692E9BA86B3958E73ED2F2FC4E45883BCBA4') # Martin Vierula (GitHub key) <Martin.Vierula@trustwave.com>

build() {
	cd "$srcdir"/modsecurity-v$pkgver
	./build.sh
	./configure \
		--prefix=/usr \
		--disable-examples
	make
}

package() {
	cd "$srcdir"/modsecurity-v$pkgver
	make DESTDIR="$pkgdir" install
}
