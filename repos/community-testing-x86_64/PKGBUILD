# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Tyler Dence <tyzoid@archlinux32.org>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=nextcloud-client
# Upstream does not provide signed tarballs (yet)
# https://github.com/nextcloud/desktop/issues/236#issuecomment-540538247
pkgver=3.0.1
_commit=0a62628f8a8d92527240435c74c4fb9ffaa46e53
pkgrel=1
pkgdesc='Nextcloud desktop client'
arch=(x86_64)
url='https://nextcloud.com/'
license=(GPL)
depends=(openssl sqlite qtkeychain qt5-svg qt5-webengine xdg-utils libcloudproviders
         qt5-graphicaleffects qt5-quickcontrols2)
makedepends=(doxygen extra-cmake-modules kio python-sphinx qt5-tools git cmocka)
optdepends=(
  'kio: integration with Dolphin'
  'nemo-python: integration with Nemo'
  'python-nautilus: integration with Nautilus'
  'python-caja: integration with Caja'
)
source=("$pkgname::git+https://github.com/nextcloud/desktop.git?signed#commit=$_commit")
validpgpkeys=(
  A26B951528EA1BA1678C7AE5D406C75CEE1A36D6  # one of keys controlled by github.com/camilasan
  42E775EAA3E47F9E0D5CDB0E00819E3BF4177B28  # controlled by github.com/misch7
  17166A5275C5FA6F1A48FD78074BBBCB8DECC9E2  # https://github.com/er-vin.gpg
)
sha256sums=('SKIP')
backup=('etc/Nextcloud/sync-exclude.lst')

prepare() {
  # tmpdir for check()
  mkdir tmpdir

  # Use system GNUInstallDirs.cmake so that we can benefit from
  # https://gitlab.kitware.com/cmake/cmake/merge_requests/3735
  rm -v $pkgname/cmake/modules/GNUInstallDirs.cmake
}

build() {
  cd $pkgname

  # bundled breakpad in libcrashreporter-qt submodule is too old and does not build with glibc >= 2.26
  # Upstream fix: https://github.com/google/breakpad/commit/bddcc58860f522a0d4cbaa7e9d04058caee0db9d
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_SHIBBOLETH=1 \
    -DWITH_CRASHREPORTER=OFF \
    -DUNIT_TESTING=ON

  # TODO: fix installation of PDF and HTML documents
  # WIP at https://github.com/yan12125/desktop/tree/doc-install-path
  make -C build all doc-man
}

check() {
  cd $pkgname/build

  # Tests fail if $TMPDIR is too small; specify an alternative for machines
  # with a small /tmp partition.
  TMPDIR="$srcdir/tmpdir" make test
}

package() {
  cd $pkgname/build

  make DESTDIR="$pkgdir" install
}
