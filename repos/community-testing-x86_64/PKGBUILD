# Maintainer: David Runge <dave@sleepmap.de>
pkgname=jacktrip
pkgver=1.1
pkgrel=4
pkgdesc="Tool to manage and tune JACK settings for optimum performance between networked machines."
arch=('x86_64')
url="https://github.com/jcacerec/jacktrip"
license=('MIT')
groups=('pro-audio')
depends=('jack' 'qt5-base')
makedepends=('doxygen' 'qt5-base')
source=("https://github.com/jcacerec/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4c6b99deb424910348a3a0b8621dba4abfcbdf1a93b3d2a63fe8098113dffc03c472ab536de6c58e9202cd25823af7ebde4ab818e5a74bedcf8b353417dfda91')

prepare() {
  mv -v "${pkgname}-${pkgname}-${pkgver}/${pkgname}" "${pkgname}-${pkgver}"
  mv -v "${pkgname}-${pkgname}-${pkgver}/README.md" "${pkgname}-${pkgver}/README.md"
  cd "${pkgname}-${pkgver}"
  # split out custom license
  sed -n '1,30p' documentation/documentation.cpp > LICENSE 
}

build() {
  cd "${pkgname}-${pkgver}/src"
  qmake-qt5 -spec linux-g++ jacktrip.pro
  make clean
  qmake-qt5 -spec linux-g++ jacktrip.pro
  make release
  cd ..
  doxygen "${pkgname}_doxygen"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm755 src/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  # license
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {CHANGESLOG.txt,README.md,TODO.txt}
  # html
  install -t "${pkgdir}/usr/share/doc/${pkgname}/html" \
    -vDm644 WWW/html/*
}
