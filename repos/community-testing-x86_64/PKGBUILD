# $Id$
# CPAN Name  : Term-ReadLine-Gnu
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: AUR Perl <aurperl@juster.info>
# Generator  : CPANPLUS::Dist::Arch 1.15

pkgname=perl-term-readline-gnu
pkgver=1.35
pkgrel=5
pkgdesc="GNU Readline XS library wrapper"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.7')
url='http://search.cpan.org/dist/Term-ReadLine-Gnu'
source=("http://search.cpan.org/CPAN/authors/id/H/HA/HAYASHI/Term-ReadLine-Gnu-${pkgver}.tar.gz"
        'termcap-bad-ncurses-good.patch')
md5sums=('514619d68bda1618da61b19d04b5cefe'
         'a000706b89792f822b5ec20baa370910')

build() {
   export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/Term-ReadLine-Gnu-$pkgver"
    patch --forward -p1 < "${srcdir}/termcap-bad-ncurses-good.patch"
    /usr/bin/perl Makefile.PL
    make
}

check() {
  cd "${srcdir}/Term-ReadLine-Gnu-$pkgver"
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
}

package() {
  cd "${srcdir}/Term-ReadLine-Gnu-$pkgver"
  make pure_install PERL_INSTALL_ROOT="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

