# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.6.1.1
pkgrel=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
options=('debug')
depends=('dtkcore' 'libqtxdg' 'librsvg' 'qt5-x11extras')
makedepends=('qt5-tools' 'gtest')
source=("https://github.com/linuxdeepin/dtkgui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e2997933ec5679ab0216942e4bde9e1229852d30dec5fada657afa0a0d40559a153612948f0b08aa60cb16d0df37a949a3488a8f313b838c9a424ff426f464d1')

build() {
  cd dtkgui-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd dtkgui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
