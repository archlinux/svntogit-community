# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=5.0.10
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('deepin-file-manager' 'deepin-qt5integration' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('cmake' 'ninja' 'qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('f4b216b24193b7fe0d824bb17edaefe045a79cf1fe5f7f231a1b3baff57d00572d641d7ea189780f2c76cf38f49c96be9c453964962ff1ace69677baeea40c4f')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|sbin|bin|' CMakeLists.txt
}

build() {
  cd deepin-clone-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DLIB_INSTALL_DIR=lib .
  ninja
}

package() {
  cd deepin-clone-$pkgver
  DESTDIR="$pkgdir" ninja install
}
