# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.045.08
pkgrel=15
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc' "linux-lts>=4.14.36" "linux-lts<4.15")
makedepends=("linux-lts-headers>=4.14.36" "linux-lts-headers<4.15")
install=$pkgname.install
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('18161cb72fc872a9aed194514f7b8fb8b255b6fa6ed3d2dd459700eaad575f31')

_extramodules=extramodules-4.14-lts

build() {
	cd "$_pkgname-$pkgver"
	_kernver="$(cat /usr/lib/modules/$_extramodules/version)"
	make -C /usr/lib/modules/$_kernver/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dm644 src/$_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
}
