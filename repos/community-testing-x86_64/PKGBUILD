# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
epoch=1
pkgver=2.3
pkgrel=2
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('x86_64')
url="https://www.poedit.net/"
license=('MIT')
depends=('gtkspell3' 'lucene++' 'wxgtk3')
makedepends=('boost')
source=(https://github.com/vslavik/poedit/releases/download/v$pkgver-oss/poedit-${pkgver}.tar.gz)
sha256sums=('ce6ea0ed5d7e02018aa763513f99aef4e4db8b298f2d272fc39ec3d793613cea')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --libexecdir=/usr/lib/poedit --with-wx-config=/usr/bin/wx-config-gtk3
  make CPPFLAGS+=' -DUCHAR_TYPE=uint16_t'
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
