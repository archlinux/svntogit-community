# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgbase=python-regex
pkgname=('python-regex' 'python2-regex')
pkgver=2019.08.19
pkgrel=3
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python')
makedepends=('python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('587b62d48ca359d2d4f02d486f1f0aa9a20fbaf23a9d4198c4bed72ab2f6c849')
b2sums=('fb8c49856236e119fd125b18d0e82cdca418b6c911780a7301af09fc023e9b2e92b13ece62956b5de38b90d10b17a885ceb75e836e5e416b40d4c7e5092a202f')

build() {
  cd "regex-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "regex-${pkgver}"

  pushd build/lib.linux-${CARCH}-3*/
  python -c 'from regex.test.test_regex import test_main; test_main()'
  popd

  pushd build/lib.linux-${CARCH}-2*/
  python2 -c 'from regex.test.test_regex import test_main; test_main()'
  popd
}

package_python2-regex() {
  depends=('python2')
  pkgdesc="Alternative python regular expression module. (python2 version)"

  cd "regex-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  install -v -m755 -d "${pkgdir}/usr/share/doc/python2-regex"
  install -v -m644 ./docs/Features.html "${pkgdir}/usr/share/doc/python2-regex/"
  install -v -m644 ./docs/Features.rst "${pkgdir}/usr/share/doc/python2-regex/"
  install -v -m644 ./docs/UnicodeProperties.txt "${pkgdir}/usr/share/doc/python2-regex/"

  sed -n '1,/^$/p' regex_2/regex/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-regex() {
  depends=('python')
  pkgdesc="Alternative python regular expression module. (python3 version)"

  cd "regex-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  install -v -m755 -d "${pkgdir}/usr/share/doc/python-regex"
  install -v -m644 ./docs/Features.html "${pkgdir}/usr/share/doc/python-regex/"
  install -v -m644 ./docs/Features.rst "${pkgdir}/usr/share/doc/python-regex/"
  install -v -m644 ./docs/UnicodeProperties.txt "${pkgdir}/usr/share/doc/python-regex/"

  sed -n '1,/^$/p' regex_3/regex/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
