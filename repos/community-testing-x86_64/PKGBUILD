# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gammastep
pkgver=2.0.5
pkgrel=2
pkgdesc="Adjust the color temperature of your screen according to your surroundings."
arch=('x86_64')
url="https://gitlab.com/chinstrap/gammastep"
license=('GPL3')
depends=('geoclue' 'glibc' 'hicolor-icon-theme' 'libdrm' 'libx11' 'libxcb'
'libxxf86vm' 'wayland')
optdepends=('python-gobject: for gammastep-indicator'
            'python-xdg: for gammastep-indicator'
            'gtk3: for gammastep-indicator')
makedepends=('glib2' 'intltool' 'python' 'systemd')
source=("https://gitlab.com/chinstrap/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('659fb417c1c487c75cf824f3e9d6ef26a45191be2afca7a35a41c8637d3e5f27cc89085cd3d9322be1bb0de9e7cfd4da6b1236c053b64d4a670de38a26725451')
b2sums=('72f8bb6d85135d08db09546c6da8d23be940f860b168d97d9ef4d9de53ec63f8c92196fee8c618825e23157624f9e0f80bcab180943b56a71631b7042e76e7dc')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libgio-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {CONTRIBUTING,NEWS,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
