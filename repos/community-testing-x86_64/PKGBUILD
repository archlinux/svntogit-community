# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-redis2
pkgver=0.15
pkgrel=9

_modname="${pkgname#nginx-mod-}"

pkgdesc='Nginx upstream module for the Redis 2.0 protocol'
arch=('x86_64')
depends=('nginx')
makedepends=('nginx-src')
url="https://github.com/openresty/redis2-nginx-module"
license=('BSD')

source=(
	https://github.com/openresty/$_modname-nginx-module/archive/v$pkgver/$_modname-$pkgver.tar.gz
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('d255571bcfb9939b78099df39cb4d42f174d789aec8c8e5e47b93942b0299438')

build() {
  cp -r /usr/src/nginx .

	cd "$srcdir"/nginx
	./configure --with-compat --add-dynamic-module=../$_modname-nginx-module-$pkgver
	make modules
}

package() {
	cd "$srcdir"/nginx/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done

	install -Dm644 "$srcdir"/$_modname-nginx-module-$pkgver/README.markdown \
	               "$pkgdir"/usr/share/licenses/$pkgname/README.markdown
}
