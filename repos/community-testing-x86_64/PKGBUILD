# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hlint
pkgver=2.2.11
_commit=500f2f03ea4f783352b418bfa9dbe1a591171544
pkgrel=44
pkgdesc="Source code suggestions"
url="http://community.haskell.org/~ndm/hlint/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-cmdargs' 'haskell-cpphs'
         'haskell-data-default' 'haskell-extra' 'haskell-file-embed' 'haskell-filepattern'
         'haskell-ghc' 'haskell-ghc-lib-parser-ex' 'haskell-hscolour' 'haskell-refact'
         'haskell-src-exts' 'haskell-src-exts-util' 'haskell-syb' 'haskell-uniplate'
         'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector' 'haskell-yaml')
conflicts=('haskell-hlint')
replaces=('haskell-hlint')
makedepends=('ghc')
source=("https://github.com/ndmitchell/hlint/archive/$_commit/$pkgname-$_commit.tar.gz")
#source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1c6b6eb9145e48fd006b055df00cd11d229df8eea47acd45862994c020cd1f79ad6cc82d48e0cfbaba302dd93d202b2a167e2d6d56fbc61f37aae60e1f4aa1ba')

prepare() {
    mv $pkgname-{$_commit,$pkgver}
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fgpl -fthreaded
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.BSD3"
}
