# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Florian Loitsch <archlinux@florian.loitsch.com>

pkgbase=miniupnpc
pkgname=(miniupnpc python-miniupnpc)
pkgver=2.1.20191224
pkgrel=3
pkgdesc='Small UPnP client library/tool to access Internet Gateway Devices'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
makedepends=('sh' 'glibc' 'lsb-release' 'python' 'python-setuptools')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz{,.sig})
sha512sums=('d362f914ce9177c1bc46f1f3ae59069c61c0c9c1b6ea7e78003d6b46445d3550835ffc541c2649b5fbc997d035357b461148edb3648135f33d0ce98b54961917'
            'SKIP')
b2sums=('85c0b3eb678685bc7192dbee9440ec5f5be80cbac4d6a4e0a6473662c66f05ef512322cd535a142ffe16d3099a86f78ea70645a7eb2979c373e7a486aeab0cd5'
        'SKIP')
validpgpkeys=('BEB7EA42900FF505B7907AE50FF11B67A5C0863C') # miniupnp (miniupnp !) <miniupnp@free.fr>

build() {
  cd ${pkgname}-${pkgver}
  make
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package_miniupnpc() {
  depends=(sh glibc)
  provides=(libminiupnpc.so)

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 man3/miniupnpc.3 -t "${pkgdir}/usr/share/man/man3"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python-miniupnpc() {
  pkgdesc='Python bindings for miniupnpc'
  depends=(python glibc)

  cd ${pkgbase}-${pkgver}
  python setup.py install --optimize=1 --skip-build --root="$pkgdir"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
