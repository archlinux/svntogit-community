# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gammaray
pkgver=2.11.3
pkgrel=3
pkgdesc='A tool for examining the internals of a Qt application and to some extent also manipulate it'
arch=(x86_64)
url='https://www.kdab.com/gammaray/'
license=(GPL)
depends=(syntax-highlighting qt5-tools qt5-svg qt5-3d)
makedepends=(cmake kcoreaddons qt5-script qt5-wayland qt5-webengine qt5-scxml qt5-location qt5-connectivity doxygen glslang)
optdepends=('qt5-wayland: Wayland compositor inspector plugin'
            'qt5-webengine: web inspector plugin'
            'qt5-scxml: state machine viewer plugin'
            'qt5-connectivity: bluetooth plugin'
            'qt5-script: script engine debugger plugin'
            'kcoreaddons: KJob tracker plugin')
source=(https://github.com/KDAB/GammaRay/releases/download/v$pkgver/gammaray-$pkgver.tar.gz{,.asc})
sha256sums=('03d7ca7bd5eb600c9c389d0cf071960330592f1f392a783b7fec5f9eaa5df586'
            'SKIP')
validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # KDAB Products <info@kdab.com>

prepare() {
# Fix plugin install dir
  sed -e 's|plugins/gammaray|lib/qt/plugins/gammaray|' -i $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGAMMARAY_INSTALL_QT_LAYOUT=ON \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt/plugins/gammaray
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
