# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-gevent
pkgver=21.8.0
pkgrel=4
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
depends=('c-ares' 'libev' 'python-greenlet' 'python-setuptools' 'python-zope-event' 'python-zope-interface')
makedepends=('cython')
checkdepends=('lsof' 'python-perf' 'python-objgraph' 'python-dnspython' 'python-requests')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('1ae31913d3a13a5f5080a33686af4031a311059d1bec8791deac87824cecc3825fd1a04ae06d6f52a2e20e82d20f39fc446795169c2e288740ea1981c532385e')

export EMBED=0

build() {
  cd gevent-$pkgver

  CYTHON=cython \
  PYTHON=python \
  python setup.py build
}

check() {
  (
    cd "$srcdir"/gevent-$pkgver
    python -m venv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py install
    python -m gevent.tests
  ) || echo "Tests failed"
}

package() {
  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
