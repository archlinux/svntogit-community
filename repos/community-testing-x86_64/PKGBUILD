# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=embree
pkgver=3.13.2
pkgrel=2
pkgdesc="Collection of high-performance ray tracing kernels"
arch=(x86_64)
url="https://embree.github.io/"
license=(Apache)
depends=(intel-tbb)
makedepends=(cmake ispc freeglut libxmu)
source=(https://github.com/embree/embree/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('dcda827e5b7a606c29d00c1339f1ef00f7fa6867346bc46a2318e8f0a601c6f9')

build() {
    cmake -B build -S ${pkgname}-${pkgver} \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DEMBREE_TUTORIALS=OFF \
      -DEMBREE_MAX_ISA="AVX512SKX" \
      -DEMBREE_RAY_MASK=ON \
      -DEMBREE_FILTER_FUNCTION=ON \
      -DEMBREE_BACKFACE_CULLING=OFF
    # Embree detects actual ISA at runtime
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
}
