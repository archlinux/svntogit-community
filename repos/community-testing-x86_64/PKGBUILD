# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.11.0
pkgrel=2
pkgdesc='An engine for running Transport Tycoon Deluxe.'
arch=('x86_64')
url='https://www.openttd.org'
license=('GPL')
depends=('libpng' 'sdl2' 'icu' 'fontconfig' 'lzo' 'hicolor-icon-theme' 'desktop-file-utils' 'xz' 'fluidsynth')
makedepends=('cmake' 'ninja')
optdepends=('openttd-opengfx: free graphics' 
            'openttd-opensfx: free soundset')
source=("https://proxy.binaries.openttd.org/openttd-releases/${pkgver}/${pkgname}-${pkgver}-source.tar.xz")
sha512sums=('ed3b305a1081552181f291d24a301b360b815d310704d1346fa191970121382fe745e245b04fa13e9560694a81084ae9302cf5800cd94f0fdc93414a1fbccc9d')

build() {
  cmake -G Ninja -B build -S ${pkgname}-${pkgver} \
     -DCMAKE_INSTALL_PREFIX=/usr \
     -DCMAKE_INSTALL_BINDIR=bin \
     -DCMAKE_INSTALL_DATADIR=/usr/share \
     -DGLOBAL_DATA_DIR=/usr/share/openttd

  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}
