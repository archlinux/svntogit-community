# $Id$
# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-package-stash-xs
pkgver=0.28
pkgrel=8
pkgdesc="Faster and more correct implementation of the Package::Stash API"
arch=('x86_64')
url="http://search.cpan.org/dist/Package-Stash-XS"
license=('PerlArtistic' 'GPL')
depends=('perl')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-fatal' 'perl-test-requires')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/D/DO/DOY/Package-Stash-XS-$pkgver.tar.gz)
md5sums=('9664356ec3be02626cbd3081ec246b70')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd Package-Stash-XS-${pkgver}

    /usr/bin/perl Makefile.PL

    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Package-Stash-XS-${pkgver}

    make test
  )
}

package() {
    cd Package-Stash-XS-${pkgver}

    make DESTDIR="$pkgdir"/ install
}
