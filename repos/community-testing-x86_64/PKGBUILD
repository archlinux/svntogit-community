# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=seatd
pkgver=0.6.3
pkgrel=3
pkgdesc="A minimal seat management daemon, and a universal seat management library"
arch=(x86_64)
url="https://sr.ht/~kennylevinsen/seatd/"
license=('MIT')
provides=("libseat.so")
replaces=("libseat")
makedepends=(
    'meson'
    'ninja'
    'scdoc'
    'systemd'
)
depends=(
    'systemd-libs'
)
options=(debug)
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~kennylevinsen/seatd/archive/$pkgver.tar.gz")
sha256sums=('5226850c163b485aebe71da0d3f4941761637e146a5c9393cb40c52617ad84a8')
build() {
    mkdir build
    meson --prefix=/usr \
          --buildtype=plain \
          -Dexamples="disabled" \
          -Dserver="enabled" \
          -Dlibseat-logind="systemd" \
          -Dman-pages="enabled" \
          "seatd-$pkgver" \
          build
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "seatd-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
