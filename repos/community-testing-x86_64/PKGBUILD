# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: alexiobash < me (at) alexiobash (dot) com >

pkgname=wpscan
pkgver=3.7.5
pkgrel=2
epoch=1
pkgdesc='Black box WordPress vulnerability scanner'
url='https://wpscan.org'
arch=('x86_64')
license=('custom:WPScan')
depends=('ruby-bundler' 'libxslt' 'libyaml' 'curl' 'libxml2')
options=('!strip' '!emptydirs')
install=wpscan.install
source=(https://github.com/wpscanteam/wpscan/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('220ce775967b71137b23710939b51ef8caf2e5770d3b6239831fda2bd3fd8be8')
sha512sums=('d5418ce9c41af75705ad0bea6c8726901a117656d399e850b28ab2b82ed7eca0f606dffb2cf28529ebdd72516bf6e2444d961077a1324a05dd516f16024af0d7')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i '/bundler/ s|~>|>=|g' $pkgname.gemspec
  bundle config build.nokogiri --use-system-libraries
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=" -I/usr/include/libxml2"
  bundle install -j"$(nproc)" --path vendor/bundle --without development test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/${pkgname}"
  cp -ra --no-preserve=owner . "${pkgdir}/opt/${pkgname}"

  install -d "${pkgdir}/usr/bin"
  cat > "${pkgdir}/usr/bin/${pkgname}" << EOF
#!/bin/sh
BUNDLE_GEMFILE=/opt/${pkgname}/Gemfile bundle exec ruby /opt/wpscan/bin/wpscan "\$@"
EOF
  chmod 755 "${pkgdir}/usr/bin/${pkgname}"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  find "${pkgdir}" \( -name gem_make.out -or -name mkmf.log \) -delete
}

# vim: ts=2 sw=2 et:
