# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcsh
pkgver=6.19.00
pkgrel=3
pkgdesc='Enhanced version of the Berkeley C shell.'
arch=('i686' 'x86_64')
url='http://www.tcsh.org/Welcome'
license=('BSD')
depends=('ncurses')
backup=('etc/csh.cshrc'
        'etc/csh.login')
install='tcsh.install'
source=("ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        '0001-avoid-gcc-5-optimization-malloc-memset-calloc-Fridol.patch'
        '0002-make-k-volatile-to-prevent-gcc-5-memset-optimization.patch'
        'csh.cshrc'
        'csh.login')
md5sums=('f5f854833578647795bc906dd4bcb5d5'
         '2a28679d1cc9db48720f69ea55e19cdb'
         '8b63b83ae940706cce8db7ecda023441'
         '7ca0fe6d1a1b9a0093f632499d4fb112'
         '4869b9da87c79854e2cc97241f125853')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../0001-avoid-gcc-5-optimization-malloc-memset-calloc-Fridol.patch
  patch -p1 -i ../0002-make-k-volatile-to-prevent-gcc-5-memset-optimization.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --bindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" mandir=/usr/share/man install install.man

  install -Dm0644 "${srcdir}/csh.cshrc" "${pkgdir}/etc/csh.cshrc"
  install -Dm0644 "${srcdir}/csh.login" "${pkgdir}/etc/csh.login"
  install -Dm0644 Copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  ln -s tcsh "${pkgdir}/usr/bin/csh"
  ln -s tcsh.1 "${pkgdir}/usr/share/man/man1/csh.1"
}
