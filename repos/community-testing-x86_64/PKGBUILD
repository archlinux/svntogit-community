# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-cross-cluster-replication-plugin
pkgver=1.2.4.0
_opensearchver=1.2.4
pkgrel=1
pkgdesc="OpenSearch Cross-Cluster Replication Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/replication-plugin"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/cross-cluster-replication/archive/${pkgver}.tar.gz"
)
sha256sums=('e849dc92821f1f2f404e16ca0a9bc65fa48964596b0aad93a71cb711fd0e66fe')

build() {
  cd "cross-cluster-replication-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-cross-cluster-replication"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-cross-cluster-replication"
  unzip "${srcdir}/cross-cluster-replication-${pkgver}/build/distributions/opensearch-cross-cluster-replication-${pkgver}.zip"

  install -Dm644 "${srcdir}/cross-cluster-replication-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
