# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=netwatch
pkgver=1.3.0
pkgrel=5
pkgdesc="monitor network connections"
arch=(i686 x86_64)
url="http://www.slctech.org/~mackay/NETWATCH/netwatch.html"
license=('GPL')
options=(zipman)
depends=(ncurses)
source=(http://www.slctech.org/~mackay/NETWATCH/netwatch-$pkgver-1.tgz)
md5sums=('9168c20c1984e9d126d1a0ec1eb0c1d3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|${CC-cc} |${CC-cc} -O2 |g' configure
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D -m0755 netwatch $pkgdir/usr/bin/netwatch
  install -D -m0755 netresolv $pkgdir/usr/bin/netresolv
  install -D -m0644 netwatch.1 $pkgdir/usr/share/man/man1/netwatch.1
}
