# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgname=python-xapian
_name=xapian-bindings
pkgver=1.4.17
pkgrel=3
epoch=1
pkgdesc="Python bindings for Xapian"
arch=('x86_64')
url="https://xapian.org/docs/bindings/python/"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'python' 'zlib')
makedepends=('util-linux-libs' 'python-sphinx' "xapian-core=$epoch:$pkgver")
source=("https://oligarchy.co.uk/xapian/${pkgver}/${_name}-${pkgver}.tar.xz"{,.asc})
sha512sums=('9cfd9809b8b13d47a7b50950c19b821d4ae902611eed3972d93cccda82d07d5858e37989bb9de31ede082c2574487e0bcd8c1e5f30f1479d47f4c28200ff76e7'
            'SKIP')
b2sums=('8c45322725c341f6e6484e0aa8716798aa5b31e5bbc795d0f5c631022c69e96f472b3fd76609116a196431e5b069a710c3ebbfc989de8fb57d5e2d081008b89c'
        'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${_name}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${_name}-${pkgver}"
  ./configure --prefix=/usr \
              --with-python3
  make
}

check() {
  cd "${_name}-${pkgver}"
  make check
}

package() {
  depends+=('libuuid.so' 'libxapian.so')
  cd "${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${_name}"
}
# vim:set ts=2 sw=2 et:
