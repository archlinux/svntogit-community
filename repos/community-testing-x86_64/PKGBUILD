# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=8.2.1.32
_pkgver=8.2.1
_cudaver=11.3
_majorver=8
pkgrel=1
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>=11')
options=(!strip staticlibs)
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/container-images/cuda/-/tree/master/dist for the appropriate cuda version
# or make an NVIDIA Developer account.
# Alternatively, check https://github.com/pytorch/builder/blob/master/common/install_cuda.sh
source=("https://developer.download.nvidia.com/compute/redist/cudnn/v${_pkgver}/cudnn-${_cudaver}-linux-x64-v${pkgver}.tgz"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('7d88846b69ca523dd2e631c7be7a7deae09c433da94e9c4167dc5e115b2c93b3256cd0e32241e2aa5c2b836d8f1097e207bc7af8bcf4e55a222c8cbf50be831b'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("cudnn-${_cudaver}-linux-x64-v${pkgver}.tgz")

package() {
  mkdir -p "${pkgdir}"/usr/share/licenses/
  tar xf cudnn-${_cudaver}-linux-x64-v${pkgver}.tgz --strip-components 1 -C "${pkgdir}/usr"

  mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  chmod 644 "${pkgdir}"/usr/lib/libcudnn_static_v8.a

  install -Dm644 \
      "${srcdir}"/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      "${pkgdir}"/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf

  mv "${pkgdir}"/usr/NVIDIA_SLA_cuDNN_Support.txt "${pkgdir}"/usr/share/licenses/${pkgname}/NVIDIA_SLA_cuDNN_Support.txt
}

# vim: ts=2 sw=2 ft=sh syn=sh et
