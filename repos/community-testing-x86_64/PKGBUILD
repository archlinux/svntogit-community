# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=5.0.7
pkgrel=26
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("https://github.com/linuxdeepin/deepin-anything/archive/$pkgver/deepin-anything-$pkgver.tar.gz")
sha512sums=('51e8488b4d0652d24afd1b5bd74e88140e2754354bfa85639c668479deca25e8163f3ffcf8bc1c6629d97d1cea24de89c65a76b8c9902745911fd0ed9b12b770')

prepare() {
  cd deepin-anything-$pkgver
  # https://github.com/linuxdeepin/deepin-anything/issues/36
  sed -i '454s/return;/return -1;/' kernelmod/vfs_kretprobes.c
}

build() {
  cd deepin-anything-$pkgver
  make -C kernelmod kdir=/usr/src/linux
}

package() {
  depends=('linux')

  cd deepin-anything-$pkgver
  local extradir=/usr/lib/modules/$(</usr/src/linux/version)/extramodules
  install -Dt "$pkgdir$extradir" -m644 kernelmod/*.ko
  find "$pkgdir" -name '*.ko' -exec strip --strip-debug {} +
  find "$pkgdir" -name '*.ko' -exec xz {} +
}
