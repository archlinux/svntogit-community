# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Bodor Dávid Gábor <david.gabor.bodor@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

_name=scipy
pkgname=python-scipy
pkgver=1.5.4
pkgrel=3
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
arch=('x86_64')
url="https://www.scipy.org/"
license=(BSD)
depends=(python-numpy)
provides=(scipy)
makedepends=(gcc-fortran python-setuptools pybind11)
checkdepends=(python-pytest)
optdepends=('python-pillow: for image saving module')
source=("https://pypi.python.org/packages/source/${_name:0:1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('4a453d5e5689de62e5d38edf40af3f17560bfd63c9c5bd228c18c1f99afa155b')

build() {
  cd scipy-${pkgver}
  python setup.py config_fc build
}

check() {
  cd scipy-${pkgver}
  python setup.py config_fc install \
    --prefix=/usr --root="$srcdir"/test --optimize=1
  export PYTHONPATH="$srcdir"/test/usr/lib/python3.9/site-packages
  cd ..
  python -c "from scipy import test; test('full')"
}

package() {
  cd scipy-$pkgver
  python3 setup.py config_fc install \
    --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
