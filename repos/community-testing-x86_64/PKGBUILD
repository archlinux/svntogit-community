# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.2.0.2
pkgrel=2
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
groups=('deepin')
depends=('deepin-qt5integration' 'deepin-wallpapers' 'kwin')
makedepends=('extra-cmake-modules' 'expac' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        $pkgname-tabbox-chameleon-rename.patch::https://github.com/linuxdeepin/dde-kwin/pull/106.patch
        $pkgname-build-fix.patch::https://github.com/linuxdeepin/dde-kwin/pull/109.patch
        $pkgname-unload-blur.patch
        $pkgname-added-functions-from-their-forked-kwin.patch
        deepin-kwin-qt5.15.patch
        kwin-5.19.patch)
sha512sums=('1d8243a4140f87f73b8bbf9a8b448a8d4bde9653d214382515493c306f47f2866a92b0fed049b26313a96898b18bd0e05a3e1c17eca53a68e82e7669a535ef7b'
            '7ac1a577868b73314274d336287334acc5bf4ac06dde046c93e4c2d3f68a112389c8b0644d1cbc3fde5ba1c01f368676f4f34b4a1b7b6c97a5b73d66507d134d'
            '3e10241050959b51bdd6bf752253af61a2b5899c3df5d84716bd1688d9292ff65d2b7d3aea8f61e896081a9bee013cb3f4c2f42f7915038f7f476120a44cf029'
            '9c5bf86510d0fbba36e71997f55eda1b197b26af7eeb0adb9e06e7e3cc86b32c00cd23dfca872f4043f76a1d9435f916aae73536f10c7b3fe7e79a6e74a309d2'
            '8ac66a3a9a0d6f53c776c5b3c98830cfba6df562aa772f03a20737cbc04ec139513eee484ed42fa96188b26135918540b652ad653da9758162319568d03d185c'
            '8fe73f833e2b24fe9bbaf12220a9d41d5e4808c833d7e7878635a85429e6c474ae79189d75d1d4e77c78bafbe2855db37ded24c85768f9a9415a00a0456c9dce'
            'a33c21cae1d271b9e93df0b2987fb8c3956018406c88cc6a847b7f0e228316020b45f0961b67bf52b70b36da7840920bf729cc552f5ea765110766806756f348')

prepare() {
  cd dde-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' plugins/kwineffects/multitasking/background.cpp
  patch -R -p1 -i ../$pkgname-added-functions-from-their-forked-kwin.patch
  patch -p1 -i ../$pkgname-tabbox-chameleon-rename.patch
  patch -p1 -i ../$pkgname-build-fix.patch
  patch -p1 -i ../$pkgname-unload-blur.patch
  patch -p1 -i ../deepin-kwin-qt5.15.patch # Fix build with Qt 5.15
  patch -p1 -i ../kwin-5.19.patch
}

build() {
  cd dde-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DKWIN_VERSION=$(expac %v kwin | cut -d - -f 1)
  ninja
}

package() {
  cd dde-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/kwin_no_scale
}
