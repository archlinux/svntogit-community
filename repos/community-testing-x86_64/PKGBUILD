# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Giacomo Rizzo <alt@free-os.it>

pkgname=gpsd
pkgver=3.21
pkgrel=3
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('x86_64')
url="http://catb.org/gpsd/"
license=('BSD')
depends=('python' 'libusb' 'bluez-libs')
optdepends=('gtk3: GUI frontends'
            'php: generate a PHP status page for your GPS'
            'php-gd: image support for the PHP status page'
            'python-cairo: GUI frontends'
            'python-gobject: GUI frontends'
            'python-pyserial: GUI frontends'
            'qt5-base: for Qgpsmm')
makedepends=('scons' 'docbook-xsl' 'qt5-base' 'python-gobject' 'python-cairo' 'python-pyserial' 'gtk3')
backup=('etc/default/gpsd')
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.sig}
        $pkgname.conf)
sha256sums=('65504c3af8d3b0cce3c07405b8815d7730d2d2be2da7d28d275f1a9c57c6fe91'
            'SKIP'
            'dcfa71c4c730b486269995468bdcfa8e96bf3c282db1dadea9384d32015d277f')
validpgpkeys=('EED4A0893DCC705DB309E202CCF29C7238522905') # Gary E. Miller <gem@rellim.com>
# validpgpkeys=('41876B2F579463A499843D1DECC8208F8C6C738D') # Eric S. Raymond

build() {
  cd $pkgname-$pkgver

  export LINKFLAGS="${LDFLAGS}"
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  scons prefix=/usr \
        systemd=yes \
        gpsd_group=uucp \
        python_libdir=$_pythonpath \
        sbindir=/usr/bin \
        qt_versioned=5
  scons build
}

package() {
  cd $pkgname-$pkgver

  # Fix path in systemd files
  sed -i 's|local/sbin|bin|' systemd/{gpsd.service,gpsdctl@.service}

  export DESTDIR="${pkgdir}"
  scons install
  install -Dm755 gpsinit -t "${pkgdir}"/usr/bin
  chmod 755 "${pkgdir}"/usr/bin/*

  install -Dm644 "$srcdir/gpsd.conf" "${pkgdir}"/etc/default/gpsd

  install -Dm644 "gpsd.rules" "${pkgdir}"/usr/lib/udev/rules.d/25-gpsd-usb.rules

  install -Dm755 gpsd.hotplug "${pkgdir}"/usr/lib/udev/gpsd.hotplug

  install -Dm644 packaging/X11/xgps.desktop \
    "${pkgdir}"/usr/share/applications/xgps.desktop
  install -Dm644 packaging/X11/xgpsspeed.desktop \
    "${pkgdir}"/usr/share/applications/xgpsspeed.desktop
  install -Dm644 packaging/X11/gpsd-logo.png \
    "${pkgdir}"/usr/share/gpsd/gpsd-logo.png

  install -Dm644 systemd/gpsd.service "${pkgdir}"/usr/lib/systemd/system/gpsd.service
  install -Dm644 systemd/gpsd.socket "${pkgdir}"/usr/lib/systemd/system/gpsd.socket
  install -Dm644 systemd/gpsdctl@.service "${pkgdir}"/usr/lib/systemd/system/gpsdctl@.service

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
