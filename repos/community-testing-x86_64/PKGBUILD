# $Id$
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=mc
pkgver=4.8.14
pkgrel=1
pkgdesc="Midnight Commander is a text based filemanager/shell that emulates Norton Commander"
arch=('i686' 'x86_64')
url="http://www.ibiblio.org/mc/"
license=('GPL')
depends=(
    'e2fsprogs'
    'glib2'
    'gpm'
    'libssh2'
    'slang')
makedepends=('libxt' 'libx11')
optdepends=(
    'cabextract: ucab extfs'
    'cdparanoia: audio extfs'
    'cdrkit: iso9660 extfs'
    'gawk: hp48+ extfs'
    'aspell: spelling corrections'
    'cvs: CVS support'
    'mtools: a+ extfs'
    'perl: needed by several extfs scripts'
    'python2-boto: s3+ extfs'
    'python2-pytz: s3+ extfs'
    'smb: VFS support'
    'unace: uace extfs'
    'unarj: uarj extfs'
    'unrar: urar extfs'
    'zip: uzip extfs'
    'p7zip: support for 7zip archives')
options=('!emptydirs' '!makeflags')
backup=('etc/mc/edit.indent.rc'
        'etc/mc/filehighlight.ini'
        'etc/mc/mcedit.menu'
        'etc/mc/mc.ext'
        'etc/mc/mc.keymap'
        'etc/mc/mc.menu'
        'etc/mc/sfs.ini')
source=("http://www.midnight-commander.org/downloads/${pkgname}-${pkgver}.tar.bz2")

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr --sysconfdir=/etc --enable-vfs-smb \
        --with-x --libexecdir=/usr/lib

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    # Fix FS#15177
    sed 's|op_has_zipinfo = 0|op_has_zipinfo = 1|' \
        -i "${pkgdir}/usr/lib/mc/extfs.d/uzip"

    sed 's#/usr/bin/env python#/usr/bin/python2#' \
        -i "${pkgdir}/usr/lib/mc/extfs.d/s3+"
}

md5sums=('fe1cb75643c2bd4df64bbcf2dab76905')
