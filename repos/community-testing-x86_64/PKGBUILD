# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: William J. Bowman <aur@williamjbowman.com>
# Contributer: Leif Warner <abimelech@gmail.com>

_hkgname=transformers-compat
pkgname=haskell-transformers-compat
pkgver=0.4.0.4
pkgrel=3
pkgdesc="A small compatibility shim exposing the new types from transformers 0.3 to older Haskell platforms."
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc=7.10.3')
options=('staticlibs')
source=(http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
install=${pkgname}.install
sha512sums=('5850fa8ed63684b3dc37897c22d1d32d7412374dcd892e42db7ed975d493fdc8e216630735813ed00203f83dcd17722484a6ec182ccffb6fc8794b4b82b6c951')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup configure -O \
      --enable-library-profiling \
      --enable-shared \
      --prefix=/usr\
      --docdir=/usr/share/doc/${pkgname} \
      --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s /usr/share/doc/${pkgname}/html "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
