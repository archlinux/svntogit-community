# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.249
pkgrel=1
pkgdesc='An extremely fast Python linter, written in Rust'
arch=(x86_64)
url="https://github.com/charliermarsh/$pkgname"
license=(MIT)
depends=(gcc-libs glibc)
makedepends=(cargo)
options=(!lto)
_archive="$pkgname-$pkgver"
source=($url/archive/refs/tags/v$pkgver/$_archive.tar.gz)
sha512sums=('134bc48242a00c233a343322ec6357f77f5389173b43af2d58758e61086a4cd9a2af1579204a828b613b501a91db9ac5217ef3e422bceeb9c50ba985376f6508')
b2sums=('8c3ee2ac9b5cc6c9301b346eebb75283017866a4ebf54a7abbfd43d8db00444b05b1934000470ff38341f9ecfcb6107094de262168039c21fa201156aa4753c7')

prepare() {
  cd "$_archive"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_archive"
  cargo build -p ruff_cli --frozen --release --all-features
}

check() {
  cd "$_archive"
  cargo test -p ruff_cli --frozen --all-features
}

package() {
  cd "$_archive"
  local _target="target/release/$pkgname"
  install -Dm0755 -t "$pkgdir/usr/bin/" "$_target"
  install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
  install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  $_target --generate-shell-completion bash | install -Dm0644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$pkgname.bash"
  $_target --generate-shell-completion fish | install -Dm0644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  $_target --generate-shell-completion zsh  | install -Dm0644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
}
