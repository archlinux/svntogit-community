# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=bluefish
pkgver=2.2.13
pkgrel=2
pkgdesc='A powerful HTML editor for experienced web designers and programmers'
arch=('x86_64')
url='http://bluefish.openoffice.nl/'
license=('GPL3')
depends=('gtk3' 'enchant' 'python')
makedepends=('gucharmap' 'intltool')
optdepends=('gucharmap: for using the charmap plugin')
install=$pkgname.install
source=("https://www.bennewitz.com/$pkgname/stable/source/$pkgname-$pkgver.tar.bz2"{,.sig})
validpgpkeys=(64979277BAFF2D4CB637AC3B291C63A6B78DFBA1) # Olivier Sessink <olivier@bluefish.openoffice.nl>
sha512sums=('959a260f32c7e90ad5b3acc52eaa4a1cc979c7a41d2fa8e3b1f9bd139232c1af940bdc92c84d00b30c53389e25b2b92188cde569bc39bdda5208e1ebec20eb68'
            'SKIP')

prepare() {
  cd $pkgname-$pkgver

  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-spell-check \
    --disable-update-databases \
    --disable-xml-catalog-update

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
