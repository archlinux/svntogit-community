# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Olaf Leidinger <oleid@mescharet.de>
# Contributor: fabien Cellier <fabien.cellier@gmail.com>

pkgname=pocl
pkgver=3.1
pkgrel=2
pkgdesc="Portable OpenCL is an open-source implementation of OpenCL which can be easily adapted for new targets"
arch=('x86_64')
url="http://portablecl.org/"
license=('GPL')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
depends=('clang' 'hwloc' 'opencl-icd-loader')
makedepends=('llvm' 'cmake' 'opencl-headers' 'ocl-icd' 'ninja' 'python')
sha512sums=('40d17e81d715f6897aa1d97fd02834d45227d9d0bd4c70e76d727f9ad4df675c25b7158a862e20e63810182fdad82a3cb1e454668c3a6422a977e59c8325fa0c')

build() {
  cd "$pkgname-$pkgver"
  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DKERNELLIB_HOST_CPU_VARIANTS=distro
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build check
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/ ninja -C build install
}
