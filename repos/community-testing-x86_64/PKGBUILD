# Maintainer: Santiago Torres-Arias <santiago -> a.o >
# Maintainer: Nick Black <dankamongmen@gmail.com>

pkgname=notcurses
pkgver=3.0.8
pkgrel=2
pkgdesc="Blingful TUI/character graphics library"
url="https://nick-black.com/dankwiki/index.php/Notcurses"
license=('Apache')
arch=('x86_64')
# qrcodegen is currently shipped as a static archive on Arch, rather than a
# dynamic library. Should the .so be shipped in the future, promote qrcodegen
# from a makedepends to a true depends, and use that dynamic library.
depends=('ncurses' 'ffmpeg' 'libunistring' 'gpm')
makedepends=('libdeflate' 'cmake' 'pandoc' 'python-pip' 'python-pypandoc' 'python-cffi' 'python-setuptools' 'readline' 'ninja' 'zlib' 'gpm' 'doctest')
source=("https://github.com/dankamongmen/notcurses/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "$pkgname-doctest-2.4.9.patch::https://github.com/dankamongmen/notcurses/commit/7651cde99827.patch")
sha256sums=('56c33ffe2a2bc4d0b6e3ac14bdf620cf41e3293789135f76825057d0166974fd'
            '2443fa429b475d49340d3e6d141a95ef773a8b5507c466d54a1b8b712b016256')

prepare() {
  patch -Np1 -d ${pkgname}-${pkgver} <$pkgname-doctest-2.4.9.patch
  mkdir -p "${pkgname}-${pkgver}/build"
  cd "${pkgname}-${pkgver}/build"
  cmake .. -GNinja -DCMAKE_INSTALL_PREFIX="/usr" -DCMAKE_BUILD_TYPE=RelWithDebInfo -DUSE_QRCODEGEN=off -DUSE_GPM=on
}

build() {
  cd "${pkgname}-${pkgver}/build"
  ninja
}

check() {
  cd "${pkgname}-${pkgver}/build"
  ninja test 
}

package() {
  cd "${pkgname}-${pkgver}/build"
  DESTDIR="$pkgdir" ninja install
  cd ../cffi
  env CFLAGS="-I$pkgdir/usr/include -L../build" python setup.py install --root="$pkgdir" --optimize=1
}
