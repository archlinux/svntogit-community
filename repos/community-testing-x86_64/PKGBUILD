# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime
pkgver=2.11.1
pkgrel=2
pkgdesc='Bible study tool'
arch=('x86_64')
url='http://www.bibletime.info/'
license=('GPL2')
depends=('sword' 'clucene' 'qt5-webengine' 'qt5-svg')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bibletime/bibletime/archive/v$pkgver.tar.gz"
        qt5.11.patch)
sha512sums=('ad718b2c7b6591991ffdcd00fdb9a2bcd7905bc5b8c8cf9828ce60e3a3b1ecbafb6fa06ac5689a1e8e33590439d8bc4cf69f0584bec30c3adb3c32cc14006342'
            'cbe076660075e2227fcc4abd26267cc2e47aa9929cb207ff9dddfefa78e8f822fda4f2389474920a6c1db10441a8775bf25bf320954bcceba20f9091a3324a69')

prepare() {
  mkdir -p build

  cd bibletime-$pkgver
  patch -p1 -i ../qt5.11.patch
}

build() {
  cd build

  cmake ../bibletime-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
