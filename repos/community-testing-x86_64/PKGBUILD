# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Remi Gacogne <rgacogne@archlinux.org>

pkgname=getdns
pkgver=1.5.1
pkgrel=2
pkgdesc="A modern asynchronous DNS API"
arch=('x86_64')
url="https://getdnsapi.net"
license=('BSD')
depends=('libbsd' 'libev' 'libevent' 'libidn2' 'libuv' 'libyaml' 'unbound')
source=("${url}/dist/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('5686e61100599c309ce03535f9899a5a3d94a82cc08d10718e2cd73ad3dc28af' 'SKIP')
validpgpkeys=('DC34EE5DB2417BCC151E5100E5F8F8212F77A498') # Willem Toorop <willem@nlnetlabs.nl>

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-rpath \
        --enable-static=no \
        --with-libev \
        --with-libevent \
        --with-libuv \
        --with-trust-anchor=/etc/trusted-key.key
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
