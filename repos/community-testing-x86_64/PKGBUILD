# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.8
pkgrel=3
_recoilver=5.1.1
pkgdesc='Pixelart-oriented painting program'
arch=(x86_64)
url='http://grafx2.chez.com/'
license=(GPL2)
depends=(fontconfig hicolor-icon-theme lua sdl2_image sdl2_ttf)
makedepends=(git)
options=(!emptydirs)
source=("https://sourceforge.net/projects/recoil/files/recoil/$_recoilver/recoil-$_recoilver.tar.gz"
        "git+https://gitlab.com/GrafX2/grafX2.git#commit=3fa8c98c17b01246c3d39acbeedf4b3db4de6654") # tag: v2.8
b2sums=('0748c0c6f795919af377b3718af482fe4c86b65b0370403d08bb60ad86b8ca78d43fe512410214e3ca8ed2bf46f68ee12c320efc26c3f256024c31c3e3dac205'
        SKIP)

prepare() {
  mv grafX2 $pkgname
  sed -i 's/-liconv//g' $pkgname/src/Makefile

  # update the website URL
  sed -i 's,2\.tk,2.chez.com,' $pkgname/misc/unix/grafx2.appdata.xml

  # TODO: package recoil (retro computer image library) as a separate package
  mkdir -p grafx2/3rdparty/archives
  cp -u recoil-$_recoilver.tar.gz grafx2/3rdparty/archives/recoil-$_recoilver.tar.gz
}

build() {
  make -C $pkgname/src API=sdl2 PREFIX=/usr
}

package() {
  install -d "$pkgdir/usr/share/grafx2/scripts/samples_2.4/picture/"{thomson/lib,others-8bit/lib}
  make -C $pkgname/src API=sdl2 DESTDIR="$pkgdir" PREFIX=/usr install
  mv "$pkgdir/usr/bin/grafx2"{-sdl2,}
  install -Dm644 $pkgname/doc/README.txt "$pkgdir/usr/share/doc/$pkgname/README"
}
