# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=2.1.0
pkgrel=1
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=(x86_64)
url="https://github.com/ocaml/dune"
license=(Apache)
depends=(glibc ocaml ocaml-findlib)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('24afb44e458f819dfde3775b76c3ccf43f3fa5d8ce9a14ee9bf6658c1c116f95659c21a9ea208e5711c844fabc661cc434070bce6c9307154d506b6599ffb57f')

build() {
    cd ${pkgname}-${pkgver}
    make release
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" INSTALL_ARGS="--prefix=/usr --libdir='$(ocamlfind printconf destdir)'" install

    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
}
