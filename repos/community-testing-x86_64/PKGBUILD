# Maintainer:  Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer:  apophys <admin AT kubikmilan DOT sk>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Renzo Carbonara <gnuk0001/gmail.com>

pkgname=lzip
pkgver=1.21
pkgrel=3
pkgdesc="A lossless file compressor based on the LZMA algorithm"
arch=('x86_64')
url="https://www.nongnu.org/lzip/lzip.html"
license=('GPL3')
depends=('gcc-libs')
source=("https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz"
	"https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz.sig")

## upstream provided sha256sum:
## http://lists.nongnu.org/archive/html/lzip-bug/2018-02/msg00006.html
sha256sums=('e48b5039d3164d670791f9c5dbaa832bf2df080cb1fbb4f33aa7b3300b670d8b'
            'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742')  # Antonio Diaz Diaz <antonio@gnu.org>

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr CXXFLAGS="$CXXFLAGS" LDFLAGS="$LDFLAGS"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install{,-man}
}
