# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=python-poppler-qt5
pkgver=21.1.0
pkgrel=3
pkgdesc="Python binding to libpoppler-qt5"
arch=(x86_64)
url="https://github.com/frescobaldi/python-poppler-qt5"
license=(LGPL)
depends=(qt5-base poppler-qt5 python-pyqt5)
makedepends=(pyqt-builder sip)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-pr45.patch::${url}/pull/45.patch)
sha256sums=('5a234c2394990c06fe21d40f39725b349b6478235dd22350987040d3bb3177cc'
            '800b427dd5d88fd47f00a10caaed40a2ea1f3ae0f35adc2640bbe4ce14a3a51e')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../${pkgname}-pr45.patch
}

build() {
  cd ${pkgname}-${pkgver}
  sip-build
}

package() {
  cd ${pkgname}-${pkgver}
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  sip-install --target-dir="${pkgdir}"${site_packages}
}
