# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.18.3
pkgrel=8
pkgdesc='OpenStreetMap editor'
arch=('x86_64')
url='http://merkaartor.be/'
license=('GPL2')
depends=('exiv2' 'gdal' 'gpsd' 'hicolor-icon-theme' 'qt5-svg')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstreetmap/$pkgname/archive/$pkgver.tar.gz"
        merkaartor.appdata.xml
        nowebkit.patch
        merkaartor-exiv2-0.27.1.patch)
sha256sums=('d6b3e4d982b2c7b82331bc5bc8d993d733c443250f02919c3c4dbd6924e6e947'
            '4082cae211a5480501abb95c302608c45363cc3152a70746a3f41f57c4252df6'
            'f976a3bb6bcd0f0ef8723dcc8597f5ae9789c93143e7ef47f24f43c67913129b'
            '4627728ba02a35fc678c05e801e2499f069f4273af8dbb246040e5dc6f61d487')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../nowebkit.patch
  patch -p1 -i ../merkaartor-exiv2-0.27.1.patch # Fix build with exiv2 0.27.1
  sed -i 's/GDAL_VERSION_MAJOR == 2/GDAL_VERSION_MAJOR == 3/' src/ImportExport/ImportExportGdal.h # Fix build with gdal 3.0.0
}

build() {
  cd $pkgname-$pkgver
  lrelease src/src.pro
  qmake Merkaartor.pro PREFIX=/usr GPSDLIB=1 GEOIMAGE=1 USEWEBENGINE=0 \
        QMAKE_CXXFLAGS="$CXXFLAGS -DACCEPT_USE_OF_DEPRECATED_PROJ_API_H=1"
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 ../merkaartor.appdata.xml "$pkgdir/usr/share/metainfo/merkaartor.appdata.xml"
}
