# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.15.0
pkgrel=2
pkgdesc="Compiler for high-performance SIMD programming on the CPU"
arch=(x86_64)
url="https://ispc.github.io/"
license=(BSD)
depends=(ncurses zlib llvm-libs clang)
makedepends=(llvm python lib32-glibc cmake) # level-zero-headers level-zero-loader openmp spirv-llvm-translator vc-intrinsics)
source=(https://github.com/ispc/ispc/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        llvm-11.1.0.patch
        ispc-libclang-cpp.patch)
sha256sums=('2658ff00dc045ac9fcefbf6bd26dffaf723b059a942a27df91bbb61bc503a285'
            'fd2bc96a65656350629918be8c57682d1936ddbe37d410d8c8667538dc992cac'
            'f6a162b8b60e0a0d17526c4e70bf9bc681c0e16cfee2b8cde1d7f9a5a99b8e78')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../llvm-11.1.0.patch # Fix build with LLVM 11.1.0
  patch -p1 -i ../ispc-libclang-cpp.patch # Build with unified libclang-cpp.so
  # Remove tests that requires DUMPS (https://github.com/ispc/ispc/issues/1752)
  sed -i '/debug-phase/d' tests/lit-tests/arg_parsing_errors.ispc
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DISPC_INCLUDE_EXAMPLES=OFF \
    -DISPC_NO_DUMPS=ON #\
#    -DGENX_ENABLED=ON \
#    -DGENX_DEPS_DIR=/usr
  make -C build
}

check() {
  make -C build check-all
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
