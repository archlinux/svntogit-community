# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=stunnel
pkgver=4.39
pkgrel=1
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL"
arch=('i686' 'x86_64')
url="http://www.stunnel.org"
license=('GPL')
depends=('openssl')
install=stunnel.install
options=('!libtool')
source=("ftp://ftp.stunnel.org/stunnel/$pkgname-$pkgver.tar.gz"
        'Makefile.patch' 'stunnel.rc.d')
md5sums=('853739119a8364daea750154af6d7e79'
         'f15398497e10e080c2406d2fc541660c'
         'cb647c71ff4cb1e035b6e515d5f13ebf')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # don't create a certificate...
  patch -p0 < $srcdir/Makefile.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-ipv6 \
    --disable-libwrap
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=$pkgdir install

  for i in ca.pl importCA.sh; do
    install -Dm755 tools/$i $pkgdir/usr/share/stunnel/$i
  done
  install -Dm755 $srcdir/stunnel.rc.d $pkgdir/etc/rc.d/stunnel
  sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
      -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
      -e "s:nobody:stunnel:g" -e "s:nogroup:stunnel:g" \
      -i ${pkgdir}/etc/stunnel/stunnel.conf-sample
  install -Dm644 tools/stunnel.cnf $pkgdir/etc/stunnel/stunnel.cnf
}
