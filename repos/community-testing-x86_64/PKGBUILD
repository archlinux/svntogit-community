# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: James Fryman <jfryman@gmail.com>

pkgname=libdnet
pkgver=1.16.4
pkgrel=1
pkgdesc='A simplified, portable interface to several low-level networking routines'
arch=('x86_64')
checkdepends=('check')
license=('BSD')
url='https://github.com/ofalk/libdnet'
source=("https://github.com/ofalk/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7df1f0a3db9cf03b48cf50ab273fd6a20c1be99eb9344b9663fe3fd9ed9dab65')

prepare() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver

  autoreconf -I config --force --install
}

build() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver

  # fails in build container
  #make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver

  make DESTDIR="$pkgdir/" install

  install -D -m0644 LICENSE "$pkgdir"/usr/share/licenses/libdnet/license
}
