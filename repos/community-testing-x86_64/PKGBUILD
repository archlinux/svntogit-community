# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=adios2
pkgver=2.7.1
pkgrel=4
pkgdesc="The Adaptable Input/Output System version 2"
arch=(x86_64)
url="https://adios2.readthedocs.io/en/latest/"
license=(Apache)
depends=(blosc bzip2 glibc gcc-libs hdf5 libfabric libpng mgard nlohmann-json
         openmpi pugixml pybind11 sz yaml-cpp zeromq zfp zlib)
makedepends=(cmake gcc-fortran gtest python python-mpi4py python-numpy)
source=(https://github.com/ornladios/ADIOS2/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        adios2-gcc11.patch::https://patch-diff.githubusercontent.com/raw/ornladios/ADIOS2/pull/2710.patch)
sha256sums=('c8e237fd51f49d8a62a0660db12b72ea5067512aa7970f3fcf80b70e3f87ca3e'
            'ea10445fc942796568c6196a4bed7d506e8a6bf381b2a01c9bdaae38f1601ea2')

prepare() {
  cd ${pkgname^^}-${pkgver}
  patch -p1 < ../adios2-gcc11.patch
}

build() {
  cmake -B build -S ${pkgname^^}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DADIOS2_USE_EXTERNAL_DEPENDENCIES=ON \
    -DADIOS2_BUILD_EXAMPLES=OFF \
    -DMGARD_INCLUDE_DIR=/usr/include/mgard
  make VERBOSE=1 -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
