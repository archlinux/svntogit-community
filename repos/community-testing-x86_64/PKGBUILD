# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=1.13.1
pkgrel=2
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/mads-hartmann/bash-language-server"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "git")
source=("https://github.com/mads-hartmann/$pkgname/archive/server-$pkgver.tar.gz")
sha512sums=('66530d0484f19162bc03fd7d0e8c1a139a60be6cb603c6931d90d5589a1ccebe17b41ab37caa8000e0573c31041595ac1845e9778034f33005c7a4541dd5ae4d')

build() {
    cd "$srcdir/$pkgname-server-$pkgver"
    yarn
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .

    rm -r node_modules/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
