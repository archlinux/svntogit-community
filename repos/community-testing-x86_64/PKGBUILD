# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-subtitles
pkgver=1.3
pkgrel=5
pkgdesc="Video subtitle editor for GNOME"
arch=('x86_64')
url="http://www.gnomesubtitles.org/"
license=('GPL')
depends=('gst-plugins-base' 'gconf-sharp' 'gtkspell')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('eb1b5954c0bda76da16d6887244a7c0769a80f7cfb3aa8ad838ba087d1d44aa9')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/libenchant.so.1/libenchant-2.so.2/' src/GnomeSubtitles/Execution/gnome-subtitles.exe.config
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make -j1
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
