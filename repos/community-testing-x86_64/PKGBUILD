# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <facter_aur (at) emeraldreverie.orgr>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail.com>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=facter
pkgver=3.1.1
pkgrel=1
pkgdesc="Collect and display system facts"
arch=('i686' 'x86_64')
url="http://puppetlabs.com/facter"
license=('APACHE')
depends=('ruby' 'yaml-cpp' 'boost-libs' 'curl')
makedepends=('boost' 'cmake')
replaces=('cfacter')
source=(http://downloads.puppetlabs.com/$pkgname/$pkgname-$pkgver.tar.gz
        gemspec.in)
md5sums=('2e1e359103b507090e28da1065940a10'
         '8eb61d40e280c90608c11a0e328abe1e')

prepare() {
  cp gemspec.in $pkgname-$pkgver/lib/.gemspec.in
}

build() {
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
