# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.11.1
pkgrel=5
pkgdesc="Lightweight X11 tiled window manager written in Haskell"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.10.3' 'gmp' 'haskell-x11=1.6.1.2' 'sh' 'haskell-mtl=2.2.1' 'haskell-utf8-string' 'haskell-extensible-exceptions=0.1.1.4')
makedepends=('gendesk')
optdepends=('xorg-xmessage: for displaying visual error messages')
install='xmonad.install'
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        'xmonad.svg' 'utf8-string.patch' )
options=('staticlibs')

prepare() {
  patch -Np0 -i $srcdir/utf8-string.patch
}

build() {
  cd "$srcdir"
  ghc-pkg list
  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"

  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup.lhs configure --ghc --enable-shared --enable-split-objs --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir

  #runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE

  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg

  install -D -m644 $srcdir/xmonad.desktop $pkgdir/usr/share/xsessions/xmonad.desktop
}
md5sums=('20792f4e428db24e6c0dbecbd77f69e0'
         '72bfa5e62e4e44fe7fa59b6a7593d993'
         '5ee379d4231096c07d92a31f25f8edab')
