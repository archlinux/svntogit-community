# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=0.1.0
_extramodules=extramodules-ARCH
pkgrel=15
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('linux')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz")
sha512sums=('892828f7c52bb267993507890329e3f3b03550dd5a575a61a70995ce5d9f8cd27f1f1e5b21d4a1ec213ffc430d5236102a9e49b98a0fc0bf2f9042e719d9bfde')

build() {
  cd deepin-anything-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make -C kernelmod kdir=/usr/lib/modules/$_kernver/build
}

package() {
  cd deepin-anything-$pkgver
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 kernelmod/*.ko
  find "$pkgdir" -name '*.ko' -exec xz {} +
}
