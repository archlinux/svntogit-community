# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar
pkgver=0.22.1
pkgrel=1
pkgdesc="A Minimalistic Text Based Status Bar"
url="http://hackage.haskell.org/package/xmobar"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('gmp' 'libxft' 'libxinerama' 'wireless_tools' 'libxrandr' 'libxpm')
makedepends=('ghc=7.8.4-1' 'haskell-x11=1.6.1.2' 'haskell-x11-xft=0.3.1' 'haskell-utf8-string=0.3.8' 'haskell-network-uri'
	     'haskell-stm=2.4.3' 'haskell-parsec=3.1.7' 'haskell-mtl=2.1.3.1' 'haskell-regex-base' 'haskell-regex-compat' 'haskell-http')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/xmobar/$pkgver/xmobar-$pkgver.tar.gz)
md5sums=('b63a1d5ad739e0f2b70ba0366a9854b7')

build() {
    cd ${srcdir}/xmobar-$pkgver

    runhaskell Setup configure --disable-optimization --prefix=/usr --flags="with_utf8 with_xft with_iwlib"
    runhaskell Setup build
}

package() {
    cd ${srcdir}/xmobar-$pkgver
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 license ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

