# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=termshark
pkgver=2.2.0
pkgrel=1
pkgdesc='Terminal UI for tshark, inspired by Wireshark'
url='https://github.com/gcla/termshark'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'wireshark-cli')
makedepends=('go')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5ab2e2ccb7d4ab71a5b364c9677a24725dc5702a1c574f93ec2f4f09aba2322c292728f9ea02eb1d7378e7ea32ee4952dc59fd7b699e70fcaa3007c1f28219db')
b2sums=('f3a6addf3a2358f469a09e30fba506f5f1d05c46e341f297d1079756ef545df4f64e37d3a6d8cf6865b1a64c12ca6f8d986204acaa5b6f3567726590b795fe33')

build() {
  cd ${pkgname}-${pkgver}
  export GO111MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -modcacherw -ldflags=-linkmode=external"
  go build -v ./cmd/...
}

check() {
  cd ${pkgname}-${pkgver}
  go test -v ./...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 termshark -t "${pkgdir}/usr/bin"
  install -Dm 644 CHANGELOG.md README.md docs/{FAQ.md,UserGuide.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
