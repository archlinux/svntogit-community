# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.6.1
pkgrel=2
pkgdesc="A simple sf2 soundfont host/player"
arch=('x86_64')
url="https://www.laborejo.org/fluajho.git/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('fluidsynth' 'glib2' 'jack' 'libsndfile' 'util-linux-libs' 'ncurses'
'nuitka')
optdepends=('non-session-manager: Session management')
provides=('soundfont-synthesizer')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('5534bfb47b0da56127461a0fbbf527ae97bc5c9591ad5599ba86e2116f78c3a5b49fe007c595cf6ba36c3f31819283be38f2a2bfce49207ad042aa04f70f06ef'
            'SKIP')
b2sums=('cf5470e2af6efd6e8832ccf199956fb0c5e5cc141e0909d9106e0a727799e7728d2904763b83fdecaf98bb134c3a2059578ebfcc59baeaeb35cf2e41cc4160a0'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfluidsynth.so' 'libglib-2.0.so' 'libjack.so' 'libncursesw.so'
  'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
