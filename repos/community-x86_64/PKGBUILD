# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-blastem
pkgver=937
pkgrel=1
pkgdesc='Sega Mega Drive core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/blastem-libretro'
license=('GPL3')
groups=('libretro')
depends=('glibc')
makedepends=('git')
source=('libretro-blastem::git+https://github.com/libretro/blastem-libretro.git'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/blastem_libretro.info')
sha256sums=('SKIP'
            '0314c46e4869b5d90e7b8ae627b7dbfcf5ba29963031312c0f28b4eaf8ea0a05')

pkgver() {
  cd libretro-blastem

  git rev-list --count HEAD
}

build() {
  cd libretro-blastem/libretro

  make
}

package() {
  cd libretro-blastem/libretro

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 blastem_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../../blastem_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
