# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>

pkgname=trompeloeil
pkgver=43
pkgrel=1
pkgdesc="Thread-safe header-only mocking framework for C++11/14"
arch=('x86_64')
url="https://github.com/rollbear/trompeloeil"
license=('Boost')
makedepends=(cmake ninja)
checkdepends=(catch2)
source=("$pkgname-$pkgver.tar.gz::https://github.com/rollbear/trompeloeil/archive/v${pkgver}.tar.gz")
sha256sums=('86a0afa2e97347202a0a883ab43da78c1d4bfff0d6cb93205cfc433d0d9eb9eb')
b2sums=('d101c537263b9a884ad62523dfda2ed07d49ec0f87ef6bd53dbf45f16127db358f4e50483e05981f66fd2516600b93b1f0c96cc3264c2609eb9b9d878d546ed3')

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE='Debug' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DTROMPELOEIL_INSTALL_TARGETS=ON \
    -DTROMPELOEIL_INSTALL_DOCS=OFF \
    -Wno-dev
  cmake --build build --verbose --target self_test custom_recursive_mutex
}

check() {
  ./build/self_test || exit -1
  ./build/custom_recursive_mutex || exit -1
}


package() {
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/LICENSE_1_0.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
