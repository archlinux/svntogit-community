# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: GreaterFire <GreaterFire at protonmail dot com>

pkgname=trojan
pkgver=1.15.1
pkgrel=1
pkgdesc="An unidentifiable mechanism that helps you bypass GFW"
arch=('x86_64')
url="https://github.com/trojan-gfw/trojan"
license=('GPL3')
depends=('boost-libs' 'openssl' 'mariadb-libs')
optdepends=('ca-certificates: server certificate verification'
            'mariadb: advanced user management')
makedepends=('cmake' 'boost' 'openssl' 'mariadb-libs')
checkdepends=('openssl' 'python' 'curl' 'netcat')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trojan-gfw/$pkgname/archive/v$pkgver.tar.gz")
backup=('etc/trojan.json'
        'etc/trojan/config.json')
sha512sums=('5e0dfb46116b9de54f5d4e6d0646ab5ba3746143c5ad0f275c894929ba7fc840cacb187e514bbfaa770f0651fc4d3ff9e662f648f515a972ddf72ad0e93d616f')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

check() {
  cd $pkgname-$pkgver
  ctest
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
