# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=usb_modeswitch
pkgver=1.1.3
pkgrel=1
_pkgdata=20100623
pkgdesc="Activating switchable USB devices on Linux."
arch=('i686' 'x86_64')
url="http://www.draisberghof.de/usb_modeswitch/"
license=('GPL')
depends=('libusb' 'tcl')
makedepends=('gcc' 'make')
backup=("etc/$pkgname.conf")
source=("http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-${pkgver}.tar.bz2"
	"http://www.draisberghof.de/usb_modeswitch/usb-modeswitch-data-${_pkgdata}.tar.bz2")
md5sums=('571e6b81873231246693d18a9912f55d'
         '89ff3650f36303265213ee7ab86e2af3')

build() {
  cd $srcdir

  # extracting
  cd $srcdir/usb-modeswitch-$pkgver/
  mkdir -p $pkgdir/lib/udev/usb_modeswitch || return 1
  mkdir -p $pkgdir/etc || return 1
  mkdir -p $pkgdir/usr/share/man/man1 || return 1

  # deleting the precompiled binary
  make clean || return 1
  # recompiling
  make || return 1
  # installing
  make DESTDIR=${pkgdir} install || return 1
  # adjusting the tcl script location
  mv ${pkgdir}/lib/udev/usb_modeswitch/usb_modeswitch.tcl ${pkgdir}/lib/udev/usb_modeswitch.tcl || return 1
  rmdir ${pkgdir}/lib/udev/usb_modeswitch || return 1
  mv ${pkgdir}/lib/udev/usb_modeswitch.tcl ${pkgdir}/lib/udev/usb_modeswitch  || return 1
  # installing config
  cp device_reference.txt $pkgdir/etc/usb_modeswitch.setup || return 1

  # installing data
  cd $srcdir/usb-modeswitch-data-${_pkgdata} || return 1
  make DESTDIR=$pkgdir install || return 1
}
