# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=5.2.1
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'gobject-introspection-runtime' 'libcanberra' 'libinput'
         'libsm' 'libxkbcommon-x11' 'startup-notification' 'zenity')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('356a9fce1f4e9f39a6e0156c5dcab1e4190113a191cf1422d1ea08dbea4bfda4')
b2sums=('9703a2e2c5ef75c7d4da3e201b60033a7ba549d94106df33aa9b89d0029f52c06fb4be00dd4c4d996bb083a790635a8227c8ff729dc43940f70b679e1b46924d')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # thou shalt not -Werror
    sed -i 's/--warn-error//g' src/Makefile.am

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/muffin \
                --localstatedir=/var \
                --disable-gtk-doc \
                --disable-static \
                --disable-schemas-compile \
                --enable-compile-warnings=minimum

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool clutter/libtool cogl/libtool

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    # Remove unused stuff
    make -C src DESTDIR="${pkgdir}" uninstall-binPROGRAMS uninstall-desktopfilesDATA
    make -C src/tools DESTDIR="${pkgdir}" uninstall
    make -C src/compositor/plugins DESTDIR="${pkgdir}" uninstall
    make -C doc/man DESTDIR="${pkgdir}" uninstall
}
