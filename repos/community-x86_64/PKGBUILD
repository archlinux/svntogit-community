# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.45.3
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('i686' 'x86_64')
url="http://crca.ucsd.edu/~msp/software.html"
license=('custom:BSD')
depends=('jack' 'fftw' 'tk')
provides=('puredata')
source=("http://crca.ucsd.edu/~msp/Software/pd-$_ver.src.tar.gz"
        "enable-fftw3.patch")
md5sums=('461a3d0d558a4f45c49943234baa9ca8'
         'bb907907a9b33f6c63312274c8523d40')

prepare() {
  cd pd-$_ver
  patch -p1 -i ../${source[1]}
}

build() {
  cd pd-$_ver
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --enable-fftw \
              --disable-portaudio
  make
}

package() {
  cd pd-$_ver
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/pd/LICENSE"
}
