# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgname=python-regex
pkgver=2021.4.4
pkgrel=1
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python' 'Apache')
depends=('python')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('52ba3d3f9b942c49d7e4bc105bb28551c44065f139a65062ab7912bef10c9afb')
b2sums=('a13a51a883599f726c20cdbf28d5b0f1a53d8f2eec5f4047da0ede2951694507c5065de5a84152510874db087c342d52f19d920a7b72c034325ddc91f5fb75ac')

build() {
  cd "regex-${pkgver}"

  python setup.py build
}

check() {
  cd "regex-${pkgver}"

  cd build/lib.linux-${CARCH}-3*/
  python -m unittest regex/test_regex.py
}

package() {
  cd "regex-${pkgver}"

  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
