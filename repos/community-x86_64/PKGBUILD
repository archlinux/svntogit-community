# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=comonad
pkgname=haskell-comonad
pkgver=5.0.4
pkgrel=12
pkgdesc="Comonads"
url="https://github.com/ekmett/comonad/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-contravariant' 'haskell-distributive' 'haskell-tagged'
         'haskell-transformers-compat')
makedepends=('haskell-cabal-doctest' 'ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('06537fed3fb26a776384c1b8601e82571d6a9ef62a73eb145fe04f7f00eb0a8994b93e5ec36b235545de568e552ca2e77347a25c87ee4237dcddca41f31ec974')

prepare() {
    cd $_hkgname-$pkgver
    sed -i '/semigroups/d' $_hkgname.cabal
    sed -i 's/#if MIN_VERSION_semigroups.*/#if 1/' src/Control/Comonad/Env/Class.hs src/Control/Comonad.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fdistributive -fcontravariant -fcontainers -ftest-doctests
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
