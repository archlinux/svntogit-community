# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libqtxdg
pkgver=1.3.0
pkgrel=3
pkgdesc="Library providing freedesktop.org XDG specs implementations for Qt."
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("libsm" "libxkbcommon-x11" "qt5-base")
makedepends=("cmake")
source=(
	"https://downloads.lxqt.org/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://downloads.lxqt.org/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"850cad7c25412f70ccc997d1f3013a788d35deae42d9c1376556dc779ea92a89"
	"SKIP"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
