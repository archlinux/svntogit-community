# Maintainer: christian rebischke <chris.rebischke@archlinux.org>

pkgname=git-credential-gopass
pkgver=1.12.0
pkgrel=1
pkgdesc="git-credentials helper for gopass"
arch=('x86_64')
url="https://github.com/gopasspw/git-credential-gopass"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/git-credential-gopass/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('b6200930a08bd6ecea04a4b857ecd51fd5e3f0dfceed0d743ba5f097a73726b8a3f34635e06f3051fdcbf98b9a04dac50a462a2f1edf392b6540ff1387e46ca1')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  sed -i 's/install-completion//g' Makefile
}

build(){
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
