# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastnumbers
pkgname=python-fastnumbers
pkgver=5.0.0
pkgrel=1
pkgdesc="Super-fast and clean conversions to numbers"
arch=(x86_64)
url="https://github.com/SethMMorton/fastnumbers"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-hypothesis python-numpy python-pytest python-typing_extensions)
optdepends=(
  'python-numpy: for conversion to numpy arrays'
)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('b39f44330dca6bfcad6ddc2e5009971ce6779a85172742c1355a4e306762cc275726b86bc170ffe67ddce8e910cf3642e42c017a109e5886adb5c8c1a38243c7')
b2sums=('8dc248d5791877d071d632fc714d2cd28335e159aed88eee898a18f742b12a0cb723e3260df96df37552a7c8edf10c150118c920014c8b384290c047d1979541')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
