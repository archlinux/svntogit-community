# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=retry
pkgname=haskell-retry
pkgver=0.9.3.0
pkgrel=13
pkgdesc="Retry combinators for monadic actions that may fail"
url="https://github.com/Soostone/retry"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-random' 'haskell-unliftio-core')
makedepends=('ghc' 'uusi' 'haskell-hedgehog' 'haskell-hunit' 'haskell-tasty' 'haskell-tasty-hunit'
             'haskell-tasty-hedgehog')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1fa6e73af20ef74eca239a042c2573c1c549e0c2572b1ded36372a7f95971a0441a35d4ac0b0727aa2b1afb039fc74824800fd2588014057da264232d15436e8')

prepare() {
    cd $_hkgname-$pkgver
    uusi -d mtl-compat $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-lib-werror
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
