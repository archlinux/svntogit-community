# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.42.0
pkgrel=1
pkgdesc='Window manager of GNOME Flashback'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/Metacity'
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'libxpresent' 'libxres'
         'gsettings-desktop-schemas' 'gtk3' 'startup-notification' 'zenity')
makedepends=('vulkan-headers')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('d89d679dcfed9b5eef18f06e2dcb33a7ab543d53b358fa89ce65593c231eeebc')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
