# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Igor Nemilentsev <trezorg@gmail.com>
# Contributor: Alexander Diana <alexander@rouk.org>

pkgname=python-bcrypt
pkgver=4.0.0
pkgrel=1
pkgdesc="Modern password hashing for your software and your servers"
arch=('x86_64')
url="https://github.com/pyca/bcrypt"
license=('Apache')
depends=('python')
makedepends=('python-setuptools-rust')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pyca/bcrypt/archive/$pkgver.tar.gz")
sha512sums=('d1a6c70193c016b0f861c11304d8aee83994193a6179d999cb969e66ed5263575877147b4fcc409647df393317e0b581f7abf24e1d2e284f04892b8e6add1053')

build() {
  cd bcrypt-$pkgver
  python setup.py build
}

check() {
  cd bcrypt-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310" pytest
}

package() {
  cd bcrypt-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
}
