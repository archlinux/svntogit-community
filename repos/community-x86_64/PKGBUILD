# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=docker-compose
pkgver=1.7.1
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('i686' 'x86_64')
url="https://www.docker.com/"
depends=('python-docopt' 'python-yaml' 'python-requests' 'python-six' 'python-texttable'
         'python-websocket-client' 'python-docker-py' 'python-dockerpty' 'python-setuptools'
         'python-jsonschema' 'python-cached-property')
depends_x86_64+=('docker')
makedepends=('git')
checkdepends=('python-pytest-runner' 'python-mock')
source=("git+https://github.com/docker/compose.git#tag=$pkgver")
license=("BSD")
sha256sums=('SKIP')

prepare() {
  cd compose

  # Remove upper bound on requires
  sed -i 's/==/>=/g' requirements.txt
  sed -i "s/, < .*',$/',/" setup.py
}

build() {
  cd compose

  ./script/build/write-git-sha
  python setup.py build
}

check() {
  # Hack entry points by installing it
  # TODO: need a running docker daemon to test

  cd compose
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  # PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.5/site-packages:$PYTHONPATH" python setup.py ptr
}

package() {
  cd compose

  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 contrib/completion/bash/docker-compose "$pkgdir/usr/share/bash-completion/completions/docker-compose"
}
