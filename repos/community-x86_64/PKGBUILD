# Maintainer: Ivy Foster <iff@archlinux.org>
# Contributor: alive4ever <alive4ever at live.com>
# Contributor: Earnest
pkgname=opendoas
pkgver=6.6.1
pkgrel=2
pkgdesc='Run commands as super user or another user'
arch=(x86_64)
url='https://github.com/Duncaen/OpenDoas'
license=(custom:ISC)
depends=(pam)
install=opendoas.install
source=(
	"https://github.com/Duncaen/OpenDoas/releases/download/v$pkgver/opendoas-$pkgver.tar.gz"
	"https://github.com/Duncaen/OpenDoas/releases/download/v$pkgver/opendoas-$pkgver.tar.gz.sig"
)
backup=(etc/pam.d/doas)
# checksums provided by packager. Take with a grain of salt!
sha256sums=(
	2eb84fd05e377ec1bb924429a143c71ec03c6b9b77b384eabe951836c13955a2
	SKIP
)
validpgpkeys=(C4B6FBA98211F7EFF35DEB73335C1D17EC3D6E35)

build() {
	cd "opendoas-$pkgver"
	./configure --prefix=/usr --with-timestamp
	make
}

package() {
	cd "opendoas-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/opendoas/LICENSE"
}
