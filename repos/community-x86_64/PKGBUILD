# Maintainer: David Runge <dave@sleepmap.de>
pkgname=wolf-shaper
pkgver=0.1.7
pkgrel=2
pkgdesc="A waveshaper plugin with a graph editor"
arch=('x86_64')
url="https://github.com/pdesaulniers/wolf-shaper"
license=('GPL3')
groups=('dssi-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('jack' 'libglvnd')
makedepends=('dssi' 'gendesk' 'lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pdesaulniers/${pkgname}/releases/download/v${pkgver}/${pkgname}-v${pkgver}-source.tar.gz")
sha512sums=('1aaae508f792f574ce29d119eccecff6cb054103bae502816bda9358758f13f666327f52eb9011ef367b758ef876860699a3c05fe229f7a46363fbcb78ff913f')

prepare() {
  mv -v "$pkgname" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  BUILD_VST2=true BUILD_LV2=true BUILD_DSSI=true BUILD_JACK=true make
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 755 "bin/${pkgname}-vst.so" -t "${pkgdir}/usr/lib/vst"
  install -vDm 755 "bin/${pkgname}-dssi.so" -t "${pkgdir}/usr/lib/dssi"
  install -vDm 755 "bin/${pkgname}.lv2/"*.so \
    -t "${pkgdir}/usr/lib/lv2/${pkgname}.lv2"
  install -vDm 644 "bin/${pkgname}.lv2/"*.ttl \
    -t "${pkgdir}/usr/lib/lv2/${pkgname}.lv2"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
