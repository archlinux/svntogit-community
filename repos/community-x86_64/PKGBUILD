# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-visualization-waveform
epoch=1
pkgver=4.4.0
_codename=Matrix
pkgrel=2
pkgdesc="Waveform visualizer for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/visualization.waveform'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-visualization')
makedepends=('cmake' 'glu' 'glm' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/visualization.waveform/archive/$pkgver-$_codename.tar.gz")
sha512sums=('8452b5c9cb57c40fa79c58278ea6fa32e4a6845f0cb1555009aedcb3ce5759330918aa2c10c4fdc7c9fef97e6afff61d6145344ce5fccecbc576f64ac036d0a3')

build() {
    cd "visualization.waveform-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'glu' 'glm')
    cd "visualization.waveform-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

