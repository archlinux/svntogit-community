# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxpanel
pkgver=0.7.0
pkgrel=2
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2' 'alsa-lib' 'menu-cache' 'lxmenu-data' 'libwnck' 'libfm')
makedepends=('intltool' 'docbook-xml' 'docbook-xsl' 'wireless_tools')
optdepends=('wireless_tools: netstat plugin')
source=(http://downloads.sourceforge.net/sourceforge/lxde/lxpanel-$pkgver.tar.xz
        'd4943c80cded6325284be8b58003fe63dd6e8255.patch::http://git.lxde.org/gitweb/?p=lxde/lxpanel.git;a=patch;h=d4943c80cded6325284be8b58003fe63dd6e8255')
sha256sums=('bd010d13b1627335f45be1755f7f260dc439c2cfbce942bc36705fa2cffd0b53'
            '47aefb3889a46c30fbbc8e0f8cfad778c684e4dd535c3ff705d3aafa722e6cae')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../d4943c80cded6325284be8b58003fe63dd6e8255.patch
}

build() {
  cd $pkgname-$pkgver
  CFLAGS+=' -lgmodule-2.0' ./configure \
    --sysconfdir=/etc \
    --prefix=/usr \
    --enable-man
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
