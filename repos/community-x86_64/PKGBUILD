# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http-client
pkgname=haskell-http-client
pkgver=0.7.11
pkgrel=30
pkgdesc="An HTTP client engine"
url="https://github.com/snoyberg/http-client"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base64-bytestring' 'haskell-blaze-builder'
         'haskell-case-insensitive' 'haskell-cookie' 'haskell-http-types' 'haskell-iproute'
         'haskell-mime-types' 'haskell-network' 'haskell-network-uri' 'haskell-random'
         'haskell-streaming-commons')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-discover' 'haskell-monad-control' 'haskell-zlib')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0c01961e66c71636ff2628851151a788aaf941155d50a9f20b01a2e065ca7a8218ab4867b24e7cfb434aaa9b9178f727d950017870540e0bce91fffdcc374852')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid -fnetwork-uri
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
