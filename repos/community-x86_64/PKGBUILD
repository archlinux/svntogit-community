# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.5.7
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools' 'gtest')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("https://github.com/linuxdeepin/dtkwidget/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        fix-qt5.patch)
sha512sums=('b5c8a6ceb38ae56a5e29860eace2fb1f89fd27e00af183552591da9864927a351050e58058ad7360de7713236778aa600f58a055d9345709c383526c6d1c8e0e'
            '3b2564af4cb518e5528ea057799c09e829fe3503ffa6e85fdfe292a43a54f7aa33af357cd3ff7454cc3776f182f1a3c638f02d4a60f944b266cc842fe7a25ac2')

prepare() {
  cd dtkwidget-$pkgver
  patch -p1 -i ../fix-qt5.patch

  # Patch out 5.5 suffix. The version number in dtkcore & friends never matched their tags,
  # and current version of dde-session-shell requires 5.5 explicitly. Let's make qmake and
  # cmake happy while upstream didn't react on this matter.
  find . -name '*.pro' -exec sed -i 's/dtkcore5.5/dtkcore/g;s/dtkgui5.5/dtkgui/g;s/dtkwidget5.5/dtkwidget/g' {} \;
}

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
