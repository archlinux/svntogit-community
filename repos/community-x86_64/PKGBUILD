# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: kleintux <reg-archlinux AT klein DOT tuxli DOT ch> 

_pkgname=FishFight
pkgname=fishfight
pkgver=0.3
pkgrel=2
pkgdesc="A tactical 2D shooter game"
arch=('x86_64')
url="https://fishfight.org/"
license=('MIT' 'Apache')
depends=('pkg-config' 'libx11' 'libxi' 'mesa-libgl' 'alsa-lib')
makedepends=('rust' 'cmake')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/fishfight/FishFight/archive/v$pkgver.tar.gz"
  "$pkgname.sh"
)
sha512sums=('83553106b481d4d738f57651d1f1b58892d6f91465862413a5db5adb25a00fef2eb884948a6f859daa6501bb015138c923348795d0d4419b2aa5cdf4c7fb84f9'
            '73fc2765f9820f64fbce0b16d4d4d9a40563d4070a39815c6f4cdcefaa186c1346094ffcb972287fba487bc3f5dcaa29df38a150a9ad91e4022f252f2824f4fd')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/lib"
  install -Dm 755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  mkdir -p "$pkgdir/opt/$pkgname"
  cp -r "assets" "$pkgdir/opt/$pkgname/"
  find "$pkgdir/opt/$pkgname/assets/sounds/" -type f -exec chmod 444 {} \;

  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 docs/LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
