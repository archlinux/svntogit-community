# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=8.92
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('gcc-libs')
makedepends=('python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('82116c115ad0376c13c722791389a7b18c0e239b6a813e0a867abdcf4c7882ef8fb7f3ff26fcf332f97b32d26758c6cb186bdf3fa8d63e2e7806ac5a37b7ca50'
            '2c836f07fc5b69b169d94cfef615401c836b1abee780f70f151ddf893390adbefe62cd32bd48d75430b3e3c33fd15cf69059f09104ad87ca118b1ae4416a91ce')
b2sums=('e57ebcf5ec19cf9de9094da1cc19635686344153294ce5dee35ca69855f2eb00f4b87f9f43fb98d7c4c8c49af5574740089bad9503902e6d74c478d128d2fc3e'
        '142d9f3281c66b439b297d5a7e7aebe98652325d0f2cabeab74050905725d725e5f1de4c340c9f4349ecc36ca55afe5f849ec2f2d296ed39d7037de1d4aea768')

build() {
  make -C Source/Projects/NonWindows

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  make DESTDIR="$pkgdir" prefix=/usr install -C Source/Projects/NonWindows

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
