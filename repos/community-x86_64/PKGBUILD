# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=hlint
pkgname=(hlint haskell-hlint)
pkgver=1.9.32
pkgrel=3
pkgdesc="Source code suggestions"
url="http://community.haskell.org/~ndm/hlint/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=("ghc=7.10.3" "haskell-ansi-terminal" "haskell-cmdargs" "haskell-cpphs"
             "haskell-extra" "haskell-src-exts" "haskell-hscolour" "haskell-uniplate"
             "haskell-refact")
source=("http://hackage.haskell.org/packages/archive/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('09b135c4811b7a9eae06702fbdc42e0b45fc8c10d091d3d83e9428b64e3e73d7')

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fgpl -fthreaded
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_hlint() {
    depends=('gmp' 'libffi')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_haskell-hlint() {
    pkgdesc="Source code suggestions (docs and libraries)"
    depends=("${makedepends[@]}" 'hlint')
    install="haskell-hlint.install"

    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgbase}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgbase}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.BSD3"
    rm -r "$pkgdir"/usr/{bin,share/${pkgbase}}
}
