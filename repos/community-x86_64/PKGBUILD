# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ponyc
pkgver=0.21.2
pkgrel=1
pkgdesc='An actor model, capabilities, high performance programming language'
url='http://ponylang.org/'
arch=('x86_64')
license=('BSD')
depends=('llvm-libs')
makedepends=('llvm')
checkdepends=('pcre2')
options=('!strip')
optdepends=('pcre2: Needed for the regex package')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ponylang/ponyc/archive/$pkgver.tar.gz"
        https://github.com/winksaville/ponyc/commit/64b64a21b1be70caf4202eb1b1fa22dac1bd6575.patch)
sha512sums=('a669a112933e3da8efb1ac75c6193729678f00aea56f5f33a9788faf08aed2433654945e2fbb727a66f4a8b42d99fb2d98049573cdf71e946149c16bdfb55429'
            '5ff30453ef144d87a32b6c29e024dfbbb252f224b97e4d57df587e796809ca23613f3769d9e66b2f68c1b3d9033108c1c59a8d030ef515edced6d6b3b7ceb6b8')

prepare() {
  cd ponyc-$pkgver
  sed -i 's/-Werror //' Makefile

  patch -p1 -i ../64b64a21b1be70caf4202eb1b1fa22dac1bd6575.patch
}

build() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true

  mkdir docs
  build/release/ponyc packages/stdlib -rexpr -g -o docs
}

check() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true test
}

package() {
  cd ponyc-$pkgver
  # The makefile is very strange and requires almost as much post-correction
  # as to simply do the installation myself.  Even with patches to the
  # makefile to stop it attempting to symlink locations it has no business
  # touching.
  #make config=release prefix=/usr destdir="$pkgdir"/usr/lib/pony/"$pkgver" install

  install -Dm755 build/release/ponyc "$pkgdir"/usr/lib/pony/"$pkgver"/bin/ponyc
  install -Dm644 build/release/libponyrt-pic.a "$pkgdir"/usr/lib/libponyrt-pic.a
  install -Dm644 build/release/libponyc.a "$pkgdir"/usr/lib/libponyc.a
  install -Dm644 src/libponyrt/pony.h "$pkgdir"/usr/include/pony.h

  # Ponyc is designed to read its standard packages from the same directory.
  install -dm755 "$pkgdir"/usr/bin
  ln -sf /usr/lib/pony/"$pkgver"/bin/ponyc "$pkgdir"/usr/bin

  cp -dr --no-preserve=ownership packages "$pkgdir"/usr/lib/pony/"$pkgver"

  # Let's include the examples as documentation is scarce.
  install -dm755 "$pkgdir"/usr/share/doc/pony
  cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

  cp -dr docs/* "$pkgdir"/usr/share/doc/pony

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
