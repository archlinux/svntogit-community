# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tyler Langlois <ty |at| tjll |dot| net>

pkgbase=beats
pkgname=(filebeat packetbeat metricbeat heartbeat)
pkgver=5.4.2
pkgrel=1
pkgdesc='Data shippers for Elasticsearch'
arch=('i686' 'x86_64')
url='https://www.elastic.co/products/beats'
license=('APACHE')
depends=('glibc')
makedepends=('go' 'git' 'libpcap' 'rsync')
optdepends=('elasticsearch: for standalone installation'
            'python: for migration script')
options=('!strip')
source=("https://github.com/elastic/beats/archive/v$pkgver/beats-$pkgver.tar.gz"
        "filebeat.sysusers"
        "filebeat.service"
        "packetbeat.service"
        "metricbeat.service"
        "heartbeat.service"
        "filebeat-tmpfile.conf"
        "packetbeat-tmpfile.conf"
        "metricbeat-tmpfile.conf"
        "heartbeat-tmpfile.conf")
sha256sums=('6a02dffae1b09bc7e2ea673268124bab43c012e8a3b400f53e26c156448f2a99'
            '33feb3690f8b31563cc1e2da557c2aa326501ce9ccd7e0a142036902bfdb05ff'
            '4d5394cfe3299c9a6af5ac34c1ad4bcdc2e79162379cccbf00fb414eb28253c0'
            '9f4e33fc768d3500fa3428dcbfda241c6473a2cae8cc4dbadf8f61ca9f2bbec5'
            '26b123acd96808162a7f827ce6f0e9669140c2665fc28d76583a15996fbf853d'
            '212cb4ab5a02d7078025f7d1f50f48e5caa25debce0852cf68cd8f89ed0a8d09'
            'ec999a29bc19d1b36b84e03cbd7b00b29d3e08c97ca03043b9fd6bfbe04c5e2d'
            '3f3ead4168d5b4953cca883d8a3c4552c26a75f52cedfcf6c7785bfd7bbae3c4'
            'cb927141d3bed94cb5570789dd9fc97713e967580ee354ff393020811f087dca'
            '9629931db8455891f0a6e9f9c91272716f2b47ec88f8a49e601423a16c7cdba5')

prepare() {
    cd beats-$pkgver

    # Avoid installing extraneous configs
    sed '/[- ]win/d ; /[- ]darwin/d ; /[- ]binary/d' -i */Makefile
    # Install the Linux config as default
    sed -r 's#/([a-z]+)-linux.yml#/\1.yml#' -i */Makefile

    # Perform some timestomping to avoid make warnings
    LANG=C _t="$(date -r Makefile +'%Y-%m-%d %k:%M:%S')"
    touch -m -d "$_t" */Makefile

    # Workaround to place extracted release into GOPATH
    mkdir -p "$srcdir"/gopath/src/github.com/elastic
    ln -sf "$srcdir"/beats-$pkgver \
        "$srcdir"/gopath/src/github.com/elastic/beats
}

build() {
    export GOPATH="$srcdir"/gopath
    for beat in ${pkgname[@]}; do
        msg2 "Building $beat..."
        cd "$srcdir"/beats-$pkgver/$beat
        make
        cd "$srcdir"/beats-$pkgver/libbeat/dashboards
        make BEAT=$beat
        mv import_dashboards import_dashboards-$beat
    done

    cd "$srcdir"/beats-$pkgver/filebeat
    make modules
}

_do_package_beat() {
    backup=(etc/$pkgname/$pkgname.yml etc/$pkgname/$pkgname.template{,-es2x,-es6x}.json)

    cd beats-$pkgver/$pkgname
    make install-home HOME_PREFIX="$pkgdir"/usr/share/$pkgname

    install -dm755 "$pkgdir"/var/lib/$pkgname

    install -dm755 "$pkgdir"/etc/$pkgname
    cp $pkgname.{yml,full.yml,template{,-es2x,-es6x}.json} "$pkgdir"/etc/$pkgname

    install -Dm755 $pkgname \
                   "$pkgdir"/usr/bin/$pkgname
    install -Dm644 "$srcdir"/$pkgname.service \
                   "$pkgdir"/usr/lib/systemd/system/$pkgname.service
    install -Dm644 "$srcdir"/$pkgname-tmpfile.conf \
                   "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

    cd ../libbeat
    install -Dm755 dashboards/import_dashboards-$pkgname \
                   "$pkgdir"/usr/share/$pkgname/scripts/import_dashboards
}

package_filebeat() {
    pkgdesc='Collects, pre-processes, and forwards log files to remote sources'

    _do_package_beat

    cd "$srcdir"/beats-$pkgver/$pkgname
    install -Dm644 "$srcdir"/$pkgname.sysusers \
                   "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
}

package_packetbeat() {
    pkgdesc='Network packet analyzer that ships data to Elasticsearch'
    depends=('libpcap')

    _do_package_beat
}

package_metricbeat() {
    pkgdesc='Server monitoring agent that stores metrics in Elasticsearch'
    conflicts=('topbeat')
    replaces=('topbeat')

    _do_package_beat
}

package_heartbeat() {
    pkgdesc='Server monitoring agent that stores metrics in Elasticsearch'
    conflicts=('topbeat')
    replaces=('topbeat')

    _do_package_beat
}
