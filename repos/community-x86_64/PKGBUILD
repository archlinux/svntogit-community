# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-scikit-learn
pkgver=0.23.0
pkgrel=2
pkgdesc="A set of python modules for machine learning and data mining"
arch=('x86_64')
url="http://scikit-learn.sourceforge.net/"
license=('BSD')
depends=('python-scipy' 'python-joblib' 'python-threadpoolctl')
optdepends=('python-matplotlib: plotting capabilities')
makedepends=('python-setuptools' 'cython' 'openmp')
options=(!emptydirs)
source=("scikit-learn-${pkgver}.tar.gz::https://github.com/scikit-learn/scikit-learn/archive/${pkgver}.tar.gz")
sha256sums=('ec3fcbdb27aeb3f91b5fba5f6aa0967074d96ae77472bc5b06e65db4a1f8cd0c')

build() {
  cd "$srcdir"/scikit-learn-$pkgver
  python setup.py build
}

package() {
  cd "$srcdir"/scikit-learn-$pkgver

  python setup.py install --root="$pkgdir"/ --optimize=1

  install -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # See FS#49651
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r doc/tutorial "$pkgdir"/usr/share/doc/$pkgname/tutorial
}
