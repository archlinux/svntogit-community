# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=zlib
pkgname=lib32-$_pkgbasename
pkgver=1.2.5
pkgrel=3
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP (32-bit)'
arch=('x86_64')
license=('custom')
url="http://www.zlib.net/"
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!makeflags')
source=("http://zlib.net/zlib-${pkgver}.tar.gz"
        'zlib-1.2.5-lfs-decls.patch')
md5sums=('c735eab2d659a96e5a594c9e8541ad63'
         '4cb279ea3beab621f3526bf7b7ab99e5')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd ${srcdir}/zlib-$pkgver
    #see http://bugs.archlinux.org/task/19280
    patch -p1 -i ${srcdir}/zlib-1.2.5-lfs-decls.patch
    export CFLAGS="${CFLAGS/-O2/-O3} -fno-tree-vectorize -DUNALIGNED_OK"
    ./configure --prefix=/usr --libdir=/usr/lib32
    make
}

package() {
    cd ${srcdir}/zlib-$pkgver
    make install DESTDIR=${pkgdir}
    rm -rf "${pkgdir}"/usr/{include,share,bin}
}
