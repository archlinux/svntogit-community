# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Marco Scarpetta <marcoscarpetta02@gmail.com>

pkgname=pdfmixtool
pkgver=1.1
pkgrel=2
pkgdesc='Application to split, merge, rotate and mix PDF files'
arch=('x86_64')
url='https://scarpetta.eu/pdfmixtool/'
license=('GPL3')
depends=('hicolor-icon-theme' 'qpdf' 'imagemagick' 'qt6-svg')
makedepends=('cmake' 'ninja' 'qt6-tools')
source=("https://gitlab.com/scarpetta/pdfmixtool/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz"
        qpdf-11.diff)
sha256sums=('c381b4f1c781400990293dafd5ba5dbf3bad1b87a754cbe777f19d5aaef70102'
            '48f2fdcacade0c185180ebd381ba7f28cb7af0565ce9add0b05bf7fd2e6a9c8d')

prepare() {
  # Update URL
  sed -i 's/scarpetta.gitlab.io/scarpetta.eu/' $pkgname-v$pkgver/resources/eu.scarpetta.PDFMixTool.appdata.xml

  # qpdf quick fixes
  # https://qpdf.readthedocs.io/en/stable/release-notes.html
  pushd $pkgname-v$pkgver
  patch -Np1 -i ../qpdf-11.diff
  popd
}

build() {
  cmake -S $pkgname-v$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr' -DQT_VERSION=6
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
