# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=rio-orphans
pkgname=haskell-rio-orphans
pkgver=0.1.1.0
pkgrel=164
pkgdesc="Orphan instances for the RIO type in the rio package"
url="https://github.com/commercialhaskell/rio"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-fast-logger' 'haskell-monad-control'
         'haskell-monad-logger' 'haskell-resourcet' 'haskell-rio' 'haskell-transformers-base')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('2e415ebcc7341cf1c913de54de3218ac487e310c9556d1e1bdb349f4e7b9d041ea729c21e2bfdbcded9a214c84f483e32839a4fec87b2af9b986db5aaa18b42a')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
