# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tamarin-prover-sapic
pkgname=haskell-tamarin-prover-sapic
pkgver=1.6.0
pkgrel=31
pkgdesc="Library for input processes in SAPIC calculus for the tamarin prover"
url="http://www.infsec.ethz.ch/research/software/tamarin"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-dlist' 'haskell-fclabels'
         'haskell-parallel' 'haskell-safe' 'haskell-uniplate' 'haskell-raw-strings-qq'
         'haskell-tamarin-prover-utils' 'haskell-tamarin-prover-term' 'haskell-tamarin-prover-theory')
makedepends=('ghc')
source=("tamarin-prover-$pkgver.tar.gz::https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver.tar.gz")
sha512sums=('7f3569f740d63d715b92a8f073eaecb8b32efe59b910b246977d36bb9e873765440e72feb6e76c7cbafab0495b88cfc666bb1dd12f685627c60d5cbd97b2973e')

prepare() {
    cd tamarin-prover-$pkgver/lib/sapic
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd tamarin-prover-$pkgver/lib/sapic
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd tamarin-prover-$pkgver/lib/sapic
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
