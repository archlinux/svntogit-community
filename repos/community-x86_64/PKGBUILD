# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=0.5.0
pkgrel=2
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('7a0050591e3be3c09fe847ef2ce62df51fb80935fce593e558c7bc5279996ba899cfcc7d68ebd91db3d6b9066824af3c6f5efd63a496f7e330314fc7085366c9')

prepare(){
  export GOPATH="$srcdir"/gopath

  mkdir -p "$GOPATH"/src/github.com/arduino
  ln -rTsf "$pkgname-$pkgver" "$GOPATH"/src/github.com/arduino/arduino-cli
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  export GO111MODULE=on

  go mod vendor
  go mod download
}

build(){
  export GOPATH="$srcdir"/gopath
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  go build \
  	-gcflags "all=-trimpath=$GOPATH" \
  	-asmflags "all=-trimpath=$GOPATH" \
  	-ldflags "-extldflags $LDFLAGS" \
  	-v .
}

package() {
  install -Dm 755 $pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
}

