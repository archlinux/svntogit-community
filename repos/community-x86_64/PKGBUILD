# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=uri-bytestring
pkgname=haskell-uri-bytestring
pkgver=0.3.3.1
pkgrel=127
pkgdesc="Haskell URI parsing as ByteStrings"
url="https://github.com/Soostone/uri-bytestring"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-blaze-builder' 'haskell-th-lift-instances')
makedepends=('ghc' 'haskell-base-compat' 'haskell-hunit' 'haskell-hedgehog' 'haskell-tasty'
             'haskell-tasty-hedgehog' 'haskell-tasty-hunit' 'haskell-safe')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('58a467b94a887c018be118f1775e9c7b8ecdf75d655fc1dd235bc2a49a7e5356b4be5a51cabc3135341193a78065e1b44901745ebe9016fcc6636131a2c28672')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-lib-Werror
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
