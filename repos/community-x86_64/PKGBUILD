# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-bsddb
_path=58954679be79a1c6031a421c81f3c8e283cd700455ad3af1de7da8fabd67
pkgname=('python2-bsddb' 'python-bsddb')
pkgver=6.2.1
pkgrel=2
pkgdesc="Python interface for BerkeleyDB"
license=('MIT')
arch=('i686' 'x86_64')
url="http://www.jcea.es/programacion/pybsddb.htm"
makedepends=('python2-setuptools' 'python-setuptools')
source=(https://pypi.python.org/packages/73/73/${_path}/bsddb3-6.2.1.tar.gz
	LICENSE)
md5sums=('16c7eca6b0494de063dc86fa3cf5ecbf'
         'f412b3fb37739f1c4be64c1e7a5dee67')

prepare() {
  cp -r bsddb3-${pkgver}{,-python2}
  cd bsddb3-${pkgver}-python2
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    Lib{,3}/bsddb/{dbshelve.py,test/test_dbtables.py}
}

build () {
  # Build python 3 module
  cd bsddb3-${pkgver}
  python3 setup.py --berkeley-db=/usr build

  # Build python 2 module
  cd ../bsddb3-${pkgver}-python2
  python2 setup.py --berkeley-db=/usr build
}

package_python2-bsddb() {
  depends=('db' 'python2>=2.7')
  cd bsddb3-${pkgver}-python2

  python2 setup.py --berkeley-db=/usr install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-bsddb() {
  depends=('db' 'python>=3.3')
  cd bsddb3-${pkgver}

  python3 setup.py --berkeley-db=/usr install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
