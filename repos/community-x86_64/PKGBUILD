# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fbida
pkgver=2.14
pkgrel=3
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('x86_64')
url="https://www.kraxel.org/blog/linux/fbida/"
license=('GPL2')
depends=('libexif' 'libepoxy' 'poppler-glib' 'libwebp' 'mesa')
makedepends=('libxpm' 'imagemagick' 'libxpm' 'openmotif')
optdepends=('ghostscript: to use fbgs' 'openmotif: for ida' 'libxpm: for ida')
source=(https://www.kraxel.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        ida-desktop.patch
        fbida-gcc10.patch::"https://gitlab.com/kraxel/fbida/-/commit/1bb8a8aa.patch")
sha1sums=('fe2ec92dc98296e46cd8ddddcd8b3a169cb506a0'
          'SKIP'
          'd06b70f2d581a1a3e9e0909b5af7f3f71ed54717'
          '6d0f3b9784e47a126384b5af70f42d63f4d54b76')
validpgpkeys=('A0328CFFB93A17A79901FE7D4CB6D8EED3E87138')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p0 -i "${srcdir}/ida-desktop.patch"
  patch -p1 -i ../fbida-gcc10.patch # Fix build with GCC 10
  echo 2.13 > VERSION
}

build() {
  cd ${pkgname}-${pkgver}
  make verbose=yes
  convert +set date:create +set date:modify -resize 25% logo.jpg ida.png
}

package() {
  cd ${pkgname}-${pkgver}
  make STRIP="" DESTDIR="${pkgdir}" prefix=/usr install
  install -D -m644 desktop/ida.desktop "${pkgdir}/usr/share/applications/ida.desktop"
  install -D -m644 ida.png "${pkgdir}/usr/share/pixmaps/ida.png"
}
