# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=vty
pkgname=haskell-vty
pkgver=5.33
pkgrel=48
pkgdesc="A simple terminal UI library"
url="https://github.com/jtdaugherty/vty"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-blaze-builder' 'haskell-microlens'
         'haskell-microlens-mtl' 'haskell-microlens-th' 'haskell-hashable' 'haskell-parallel'
         'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hunit' 'haskell-quickcheck' 'haskell-smallcheck'
             'haskell-quickcheck-assertions' 'haskell-test-framework'
             'haskell-test-framework-smallcheck' 'haskell-test-framework-hunit' 'haskell-random'
             'haskell-string-qq')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2d22af15d19a359c4d8b52d15c1a67feb137a058bda0f3cff90630b7705b6ead43ab5c4753fe59bcf9a3d1c3a4a4b755d41d55dba164e014f4271e86267f01aa')

prepare() {
    uusi -u random $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
