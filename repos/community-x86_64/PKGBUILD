# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cristian Maureira <saint@archlinux.cl>
# Contributor: Dr.Egg <rwhite@archlinux.us>

pkgbase=musescore
pkgname=('musescore' 'musescore-docs')
pkgver=1.3
pkgrel=5
pkgdesc="Create, play and print beautiful sheet music"
arch=('i686' 'x86_64')
url="http://musescore.org/"
license=('GPL')
makedepends=('cmake' 'doxygen' 'portaudio' 'qtscriptgenerator' 'texlive-core')
source=("http://downloads.sourceforge.net/mscore/mscore-${pkgver}.tar.bz2"
        'mscore-docs.patch')
sha256sums=('a0b60cc892ac0266c58fc6392be72c0a21c3aa7fd0b6e4f1dddad1c8b36be683'
            'ea4c25e8998c1666103d48fb3bcb5a8d7c9601e971195ef23c2f535c1191585d')

prepare() {
  cd mscore-${pkgver}/mscore

  patch -Np1 -i ../../mscore-docs.patch
  sed 's/COMMAND lupdate/COMMAND lupdate-qt4/g
       s/COMMAND lrelease/COMMAND lrelease-qt4/g
       s/set(BUILD_SCRIPTGEN TRUE)/set(BUILD_SCRIPTGEN FALSE)/' -i CMakeLists.txt
}

build() {
  cd mscore-${pkgver}/mscore

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make lupdate
  make lrelease
  make
}

package_musescore() {
  depends=('portaudio' 'qtscriptgenerator')
  optdepends=('musescore-docs: Documentation')
  install="${pkgname}.install"

  cd mscore-${pkgver}/mscore/build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/mscore-${pkgver}/man
}

package_musescore-docs() {
  pkgdesc="Documentation for MuseScore"

  cd mscore-${pkgver}/mscore

  install -dm 755 "${pkgdir}"/usr/share/doc/${pkgbase}
  install -m 644 manual/*.pdf "${pkgdir}"/usr/share/doc/${pkgbase}/
}

# vim: ts=2 sw=2 et:
