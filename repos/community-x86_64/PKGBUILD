# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
pkgver=0.14.1
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('libgee' 'libwebkit3' 'libgexiv2' 'libraw' 'rest'
	 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'json-glib'
	 'gst-plugins-base' 'libgphoto2')
makedepends=('intltool' 'vala>=0.17.2' 'libunique3')
install=shotwell.install
source=("http://yorba.org/download/shotwell/stable/shotwell-$pkgver.tar.xz"
	"no-libexec.patch")
md5sums=('bb5783f9265e1ce2d4c9f354987c3ab1'
         'b4124e022c98b4382530e6fbcef965e2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 <$srcdir/no-libexec.patch

  ./configure --prefix=/usr \
	--disable-schemas-compile \
	--disable-desktop-update \
	--disable-icon-update
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
