# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-notebook
pkgver=3.1.1
pkgrel=1
pkgdesc="Notebook for UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-notebook"
groups=('ukui')
depends=('gsettings-qt' 'kwindowsystem')
makedepends=('qt5-tools')
source=("https://github.com/ukui/ukui-notebook/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('94f93a8f8bd9fb34fffa7a67a9eb016b0867fb403ac67839c90bde4bb6e04d4fc125f629c34405fad6edd72609e9f9830ea834f88c4624ca2008040ffc929e13')

prepare() {
  cd ukui-notebook-$pkgver
  sed -i 's/-lX11 -lXrandr -lXinerama -lXi -lXcursor/-lX11 -lXi/' src/src.pro
}

build() {
  cd ukui-notebook-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-notebook-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
