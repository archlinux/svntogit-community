# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Eric Gillingham <sysrq@sysrq.tk>

pkgname=libdiscid
pkgver=0.6.3
pkgrel=1
pkgdesc="A Library for creating MusicBrainz DiscIDs"
arch=('x86_64')
url="https://musicbrainz.org/doc/libdiscid"
license=('LGPL')
makedepends=('cmake')
source=("$pkgname-$pkgver"::"https://github.com/metabrainz/libdiscid/archive/refs/tags/v$pkgver.tar.gz")
sha256sums=('df7b19a125e31bb15bb0cebf338fb7a44cc20b76adf5de1798315a2056ff2d5a')

build() {
    cmake -B build -S "$pkgname-$pkgver" \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -Wno-dev
    cmake --build build
}

check() {
    make -C build check
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
