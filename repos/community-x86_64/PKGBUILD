# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jelle van der Waa <jelle at vdwaa dot nl>
# Contributor: Borromini <gotleenucks at g-male dot com>

pkgname=evolution-rss
pkgver=0.3.96
pkgrel=5
pkgdesc='Plugin for Evolution Mail that enables reading of RSS/RDF/ATOM feeds'
arch=('x86_64')
url='https://gnome.eu.org/index.php/Evolution_RSS_Reader_Plugin'
license=('GPL')
depends=('evolution')
makedepends=('intltool')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('af84de0aead5c4b018601227190066e3c1482f6828fdfac0b621670a989787db')

prepare() {
  cd $pkgname-$pkgver

  # Don't check for unused GConf
  sed -i '/GCONF/d' configure.ac

  # Hide desktop file, it's not expected to be shown
  sed -i 's/#NoDisplay=true/NoDisplay=true/' evolution-rss.desktop.in.in

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
