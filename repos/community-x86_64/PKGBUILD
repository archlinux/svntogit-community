# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=caps
pkgver=0.9.24
pkgrel=4
pkgdesc="The LADSPA C* Audio Plugin Suite"
arch=('x86_64')
url="http://quitte.de/dsp/caps.html"
license=('GPL3')
groups=('ladspa-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('ladspa' 'python')
source=("http://quitte.de/dsp/${pkgname}_${pkgver}.tar.bz2"
        "fix-overload.patch")
sha512sums=('051d59c50302e45cec011a573385665e96b3bb0b6f0328e6b1e4f8fa26515d5cb4c2cc8230ea5bd242b1b8f62c86545997da2ab995e953a89d8de715807fc20d'
            'e69dcdfce71a15617b80d770ac2dd590e90977e307aa8f10ee0bbad873df20d4cb37990b61a3ab708edb35454da2168a9e9e09e25b719fd5d2870c86507d6f95')

prepare() {
  cd "${pkgname}-${pkgver}"
  # replacing (now obsolete) use of <pow10f> with <exp10f>
  sed -e 's/<pow10f>/<exp10f>/g' -i dsp/v4f_IIR2.h
  # fix ambiguous overloaded function call
  patch -Np0 -i "${srcdir}/fix-overload.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure.py
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX="$pkgdir/usr" install
  install -vDm 644 {CHANGES,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
