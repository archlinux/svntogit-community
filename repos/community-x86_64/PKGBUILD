# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=3.2.1
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'libcanberra-pulse' 'librsvg' 'nss'
         'pulseaudio-alsa' 'upower' 'libnotify' 'libgnomekbd' 'libwacom'
         'libgudev' 'dbus-glib' 'colord' 'cups' 'polkit' 'libibus' 'lcms2')
makedepends=('intltool' 'docbook-xsl' 'gnome-common' 'xf86-input-wacom' 'autoconf-archive')
options=('!emptydirs')
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
		"http://pkgs.fedoraproject.org/cgit/rpms/cinnamon-settings-daemon.git/plain/csd-background-manager.patch")
sha256sums=('7541b85934d8b632dda0f6ef48dc17f10163501560d95e9f2e23e3d1dfdadea8'
            'a44f8ea45db3431c8abdf6167179c0b6e4c4a0025822c6f207b905b630017366')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver

  # https://github.com/linuxmint/Cinnamon/issues/5569#issuecomment-252537458
  patch -Np1 -i ../csd-background-manager.patch

  ./autogen.sh \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir="/usr/lib/${pkgname}" \
		--disable-static \
		--enable-systemd \
        --enable-polkit

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
