# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=libzen
pkgver=0.4.39
pkgrel=1

pkgdesc="shared library for libmediainfo and mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('gcc-libs')
makedepends=('libtool' 'automake' 'autoconf')

source=(https://mediaarea.net/download/source/libzen/$pkgver/libzen_$pkgver.tar.bz2)

sha256sums=('bd2772ab1ae4e375cdbbbdd114ad8471ebe67bf78c76e3812d66c7d462ed8ae7')

build() {
  cd ZenLib/Project/GNU/Library

  ./autogen.sh
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ZenLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -dm 755 "$pkgdir"/usr/include/ZenLib
  install -m 644 "$srcdir"/ZenLib/Source/ZenLib/*.h "$pkgdir"/usr/include/ZenLib

  for i in HTTP_Client Format/Html Format/Http; do
    install -dm0755 "$pkgdir"/usr/include/ZenLib/$i
    install -m0644 "$srcdir"/ZenLib/Source/ZenLib/$i/*.h "$pkgdir"/usr/include/ZenLib/$i
  done

  install -dm 755 "$pkgdir"/usr/lib/pkgconfig
  install -m 644 "$srcdir"/ZenLib/Project/GNU/Library/libzen.pc "$pkgdir"/usr/lib/pkgconfig

  sed -i -e 's|Version: $|Version: '$pkgver'|g' "$pkgdir"/usr/lib/pkgconfig/libzen.pc

  install -D -m 644 "$srcdir"/ZenLib/License.txt "$pkgdir"/usr/share/licenses/libzen/License.txt
}
