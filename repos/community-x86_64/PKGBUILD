# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-menus
pkgver=1.1.4
pkgrel=4
pkgdesc="Implementation of the freedesktop menu specification for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-menus"
groups=('ukui')
depends=('glib2')
makedepends=('autoconf-archive' 'gobject-introspection' 'intltool' 'mate-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-menus/archive/v$pkgver.tar.gz")
sha512sums=('74ec201ef2a82ca38eff2ed29691980ab7bc1be8df785e778719306a0b665d8d1b805b8575dd581ff030390c0534626797de478c02ee8ef4695caa1aca23e82c')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
