# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hw-hspec-hedgehog
pkgname=haskell-hw-hspec-hedgehog
pkgver=0.1.1.1
pkgrel=9
pkgdesc="Interoperability between hspec and hedgehog"
url="https://github.com/haskell-works/hw-hspec-hedgehog#readme"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hunit' 'haskell-call-stack' 'haskell-hedgehog' 'haskell-hspec')
makedepends=('ghc' 'uusi' 'haskell-doctest' 'haskell-doctest-discover' 'haskell-hspec-discover')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('fa4c101574484cc8f9775fefc558f62e86c0d0c7e4b494b4adfb6d897ac68fc6')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u hspec $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --datasubdir=haskell-hw-hspec-hedgehog --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
