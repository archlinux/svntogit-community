# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Tom K <tomk@runbox.com>
# Contributor: Todd Musall <tmusall@gmail.com>

pkgname=gnubiff
pkgver=2.2.16
pkgrel=4
pkgdesc="Mail notification program that checks for mail and displays headers when new mail has arrived"
arch=('i686' 'x86_64')
url="http://gnubiff.sourceforge.net/"
license=("GPL")
depends=('gtk3' 'popt')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/gnubiff/gnubiff-$pkgver.tar.gz
	mutex.patch)
md5sums=('f908510bf3d9b38f883dc5873b09f99c'
         '76de63fbc4a1487560cfae210fc1d6e4')

prepare() {
  cd $srcdir/gnubiff-$pkgver
  patch -p1 <$srcdir/mutex.patch
}

build() {
  cd $srcdir/gnubiff-$pkgver
  ./configure --prefix=/usr --with-password --with-password-string="65423957eb027b94c9e661611c0e4271" \
	--disable-fam --disable-gnome
  make
}

package() {
  cd $srcdir/gnubiff-$pkgver
  make DESTDIR=$pkgdir install
}
