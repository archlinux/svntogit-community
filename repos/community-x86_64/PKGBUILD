# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=2.90.3
_commit=7b3ec27bf88b066a0f5de6595fbfb3f6e91c7224
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('gtk2' 'libdui' 'qt5-svg' 'deepin-menu' 'deepin-file-manager' 'deepin-daemon')
makedepends=('git')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-dock.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd dde-dock
  sed -i 's,<dde-dock/dockplugin\(.*\)>$,"interfaces/dockplugin\1",' dde-dock-shutdown-plugin/shutdownplugin.h
  sed -i '15iINCLUDEPATH += ../dde-dock/src/' dde-dock-shutdown-plugin/dde-dock-shutdown-plugin.pro
}

build() {
  cd dde-dock
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock
  make INSTALL_ROOT="${pkgdir}" install
}
