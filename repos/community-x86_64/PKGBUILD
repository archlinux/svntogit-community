# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgbase=kid3
pkgname=('kid3' 'kid3-qt')
pkgver=3.0.2
pkgrel=2
arch=('i686' 'x86_64')
url="http://kid3.sourceforge.net/"
license=('GPL')
makedepends=('automoc4' 'cmake' 'docbook-xml' 'docbook-xsl' 'phonon-qt4' 'chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'kdebase-runtime')
install=$pkgbase.install
changelog=$pkgbase.changelog
source=(http://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz)
sha256sums=('31492e01039e78d2e73ba342a2d2c837395597eb48a101366b160c36fcdcfab4')

build() {
  cd ${srcdir}
  [[ ! -d build ]] && mkdir build
  mkdir build-qt

#kid3
  cd build

  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_APPS="CLI;KDE"

  make
  
#kid3-qt
  cd $srcdir/build-qt

  cmake \
    ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_APPS="CLI;Qt"

  make
}

package_kid3() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, KDE version"
depends=('chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'kdebase-runtime')
conflicts=('kid3-qt')

  cd $srcdir/build

  make DESTDIR=${pkgdir} install
}

package_kid3-qt() {
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor, QT version"
depends=('chromaprint' 'id3lib' 'taglib' 'libmp4v2' 'qt4')
conflicts=('kid3')
provides=('kid3')

  cd $srcdir/build-qt

  make DESTDIR=${pkgdir} install
}
