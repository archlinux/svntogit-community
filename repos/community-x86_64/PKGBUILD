# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ListLike
pkgname=haskell-listlike
pkgver=4.5.1
pkgrel=10
pkgdesc="Generic support for list-like structures"
url="https://github.com/sjoerdvisscher/listlike"
license=('custom:BSD3')
arch=('x86_64')
depends=('ghc-libs' 'haskell-dlist' 'haskell-fmlist' 'haskell-text' 'haskell-utf8-string'
         'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4e59eb8cdade42ff63db9f90a7ec524dab674872960bd294da3195c280e0f488a3c0acb3827def8a071e5ecd820bb2bf0d5d68c22b8388adc933accbb0bcc630')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYRIGHT"
}
