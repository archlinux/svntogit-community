# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cxxbridge
pkgver=1.0.84
pkgrel=1
pkgdesc='Safe interop between Rust and C++'
url='https://github.com/dtolnay/cxx'
arch=('x86_64')
license=('Apache' 'MIT')
makedepends=('cargo')
options=(!lto)
source=(https://github.com/dtolnay/cxx/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('3bdae5f93dd512c9e7e5e32fa32283cc4942e4de68127f485ae43b9799a18473224dbeace5bd6dbc95f4e52f4159e7e8a870b99bd094084d6d86b80d5a41aae7'
            '0cf03056b5c95fa8d41faba38560a4aa173e52e7d2e1a669990fd36166bcb7b85e49c2df775140f59d8440f7b31321bc973d05f66eee25912cceec456aa9666d')
b2sums=('d1d805aabd56026ba6908b94b1d4c0687c42968ccd3eddccc5b87f0a0c005391e65f506d1b00d9e601a13d5982513ba19496fc9a180e042caf8f86924da63d6f'
        '5730206472938b3179bf842cc7777906806aa923ab6d92a244f54f442c5dd65504f76fc6722cff36ade2486008033730a5d63ac85570cea98f7eaece54a9394e')

prepare() {
  cd "cxx-${pkgver}"
  cp ../Cargo.lock .
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

updlockfiles() {
  cd "cxx-${pkgver}"
  rm -f Cargo.lock
  cargo update
  cp Cargo.lock "${outdir}/"
}

build() {
  cd "cxx-${pkgver}/gen/cmd"
  cargo build --frozen --release
}

check() {
  cd "cxx-${pkgver}"
  cargo test --frozen
}

package() {
  cd "cxx-${pkgver}"
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 755 include/cxx.h -t "${pkgdir}/usr/share/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
