# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: <kaptoxic _at_ yahoo _dot_ com >
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=maude
pkgver=2.7.1
pkgrel=2
pkgdesc="High-level Specification Language"
arch=('x86_64')
url="http://maude.cs.uiuc.edu"
license=('GPL')
depends=('buddy' 'cvc4' 'libtecla' 'gmp' 'libsigsegv')
makedepends=('flex' 'bison')
source=("http://maude.cs.illinois.edu/w/images/d/d8/Maude-$pkgver.tar.gz"
        "http://maude.cs.illinois.edu/w/images/c/ca/Full-Maude-$pkgver.zip"
        maude.sh maude-cvc4-1.5.patch)
md5sums=('aa31753f742f976940c69aa699c3d0ec'
         'b365fe0fdd161880e95aeb089f166657'
         '0a51738365579574b40a3d32da6f3291'
         '9e759db427d9d6e77d35cc56b7690e0b')

prepare() {
  cd "$srcdir/maude-$pkgver"
  autoreconf -i

  patch -p1 -i ../maude-cvc4-1.5.patch # Fix build with CVC4 1.5
}

build() {
  cd "$srcdir/maude-$pkgver"
  ./configure --prefix=/usr --datadir=/usr/share/maude
  make -j1 all CVC4_LIB="-lcvc4 -lcln"
}

check() {
  cd "$srcdir/maude-$pkgver"
  make check
}

package() {
  cd "$srcdir/maude-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m 444 "$srcdir/full-maude.maude" \
    "$pkgdir/usr/share/maude/full-maude.maude"

  install -D -m 555 "$srcdir/maude.sh" "$pkgdir/etc/profile.d/maude.sh"
}
