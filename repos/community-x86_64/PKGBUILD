# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ipvsadm
pkgver=1.28
pkgrel=1
pkgdesc='The IP Virtual Server administration utility'
arch=('i686' 'x86_64')
url='http://www.linuxvirtualserver.org/software/ipvs.html'
license=('GPL2')
depends=('libnl1' 'popt' 'bash')
backup=("etc/$pkgname.conf")
options=('!makeflags')
install=$pkgname.install
source=("https://kernel.org/pub/linux/utils/kernel/ipvsadm/$pkgname-$pkgver.tar.xz"
        "$pkgname.systemd"
        "$pkgname.service")
# upstream provides sha256sums of release
sha256sums=('90e0e49cad5acb9db1c04ed4cdadf7933152a472c2f694039b9d7a291c37f3ab'
            '4960ea254128347b03665d51817f29cb643cf2ebca13f5f46649257552ad3ceb'
            '25506d07f412cccab6f2f3a2a1900d5eeda27bc58602d14f44910c4e036b5341')

#prepare() {
  #patch -p1 -d $pkgname-$pkgver < 01-fix-not-showing-all-server.patch
#}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  pushd $pkgname-$pkgver
  make \
    BUILD_ROOT="$pkgdir" \
    MANDIR=usr/share/man \
    SBIN="$pkgdir/usr/bin" \
    INIT="$pkgdir/etc/rc.d" \
    install
  popd
  # systemd
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # remove legacy init system
  rm -rf "$pkgdir/etc/rc.d"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
