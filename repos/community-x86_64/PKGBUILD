# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.214
pkgrel=1
pkgdesc="An extremely fast Python linter, written in Rust"
arch=(x86_64)
url="https://github.com/charliermarsh/ruff"
license=(MIT)
depends=(gcc-libs glibc python)
makedepends=(cargo maturin python-installer)
options=(!lto debug)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('bd1cdafa64e82dbb671907dc66b42db3ada63b938125022debe64fc759c4f9fcb0e111e5e6a85da62669699e0da73db16a0d2dc73834bbb9b08dfaa347672768')
b2sums=('2e47f66aa2fac3935aa47c6584b736126b01a4f7039e6c17d9a276b78d98c8fe7c76a5a75991f55c1ddaf1e04ca86ac5470a80f65d8aa77ef05c93318d83941f')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  local maturin_options=(
    --release
    --strip
    --locked
    --all-features
    --target $CARCH-unknown-linux-gnu
  )

  cd $pkgname-$pkgver

  maturin build "${maturin_options[@]}"
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
