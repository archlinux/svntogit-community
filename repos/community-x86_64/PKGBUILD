# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: berberman <hatsue@typed.icu>

pkgname=arch-hs
pkgver=0.6.0.0
pkgrel=4
pkgdesc="Distribute hackage packages to archlinux"
arch=('x86_64')
url="https://github.com/berberman/arch-hs"
license=('MIT')
depends=('ghc-libs' 'pacman' 'haskell-aeson' 'haskell-diff' 'haskell-req' 'haskell-hackage-db'
         'haskell-megaparsec' 'haskell-algebraic-graphs' 'haskell-conduit' 'haskell-tar-conduit'
         'haskell-conduit-extra' 'haskell-split' 'haskell-neat-interpolation' 'haskell-microlens'
         'haskell-microlens-th' 'haskell-polysemy' 'haskell-optparse-applicative'
         'haskell-prettyprinter' 'haskell-prettyprinter-ansi-terminal')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b2dde6a04f704e8c68e3137465ad0b8e19d3154354f17e769c75fe8d5c38d383cc9b1d5ca58c47d6eb35ee09c0a32a4180fbe04f1d0f1a85d8052deccc4d61d8')

_gen_comp(){
  LD_LIBRARY_PATH="$PWD/dist/build" dist/build/arch-hs${1}/arch-hs${1} --bash-completion-script "/usr/bin/arch-hs${1}" > bash${1}
  LD_LIBRARY_PATH="$PWD/dist/build" dist/build/arch-hs${1}/arch-hs${1} --zsh-completion-script  "/usr/bin/arch-hs${1}" > zsh${1}
  LD_LIBRARY_PATH="$PWD/dist/build" dist/build/arch-hs${1}/arch-hs${1} --fish-completion-script "/usr/bin/arch-hs${1}" > fish${1}
}

_install_comp(){
  install -D -m644 bash${1} "$pkgdir/usr/share/bash-completion/completions/arch-hs${1}"
  install -D -m644 zsh${1}  "$pkgdir/usr/share/zsh/site-functions/_arch-hs${1}"
  install -D -m644 bash${1} "$pkgdir/usr/share/fish/vendor_completions.d/arch-hs${1}.fish"
}

build() {
  cd $pkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' -falpm

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
  
  _gen_comp
  _gen_comp "-diff"
  _gen_comp "-submit"
}

package() {
  cd $pkgname-$pkgver
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
  
  _install_comp
  _install_comp "-diff"
  _install_comp "-submit"
} 
