# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.7.0
pkgrel=1
epoch=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base qt5-declarative qt5-networkauth qt5-svg qt5-websockets)
makedepends=(help2man meson jack python-jinja python-yaml)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('c7020afc3a495f0fd9aa4014c8a0b4a6f3bad162ad74184a1448fc65591f0f8ae0cc8ddc5be544445d2b02c37036e254b8741d1b37ff3d1b0f97f02f3b9ae214')
b2sums=('7ae402a98feadb450e7ac36951f4f967bd34b7dd3ce99e23bfe657e74d7a21ac248519ff66fbd1a2e5c719f302b63ec1585dcdc757b86d6608bcc845898d17af')

build() {
  arch-meson -Drtaudio=disabled build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(
    jack libjack.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
