# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fbreader
pkgver=0.99.4
pkgrel=7
pkgdesc="An e-book reader for Linux"
arch=('x86_64')
url="http://www.fbreader.org/"
license=('GPL')
depends=('fribidi' 'bzip2' 'curl' 'qt4' 'libunibreak' 'sqlite')
makedepends=('ccache')
source=(https://www.fbreader.org/files/desktop/fbreader-sources-$pkgver.tgz
	"fbreader-0.99.4.patch::https://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/app-text/fbreader/files/fbreader-0.99.4.patch?revision=1.1"
        fbreader-gcc6.patch)
sha256sums=('3d7c31d5ea314589d2a963290ad16f4d3d631a41e802b8b39f8be0c9f71eb8e9'
            'cb3aa6795c8f7c694ba296f6f6e6a7939648578a289bc84c10f8302c7e72df86'
            'bc67b538c51a5b77468469df6b5e96ce918ee11a5ff4e88e398ad5661d322f9c')

prepare() {
  cd "$srcdir"/fbreader-$pkgver
  patch -p1 <"$srcdir"/fbreader-0.99.4.patch

  patch -p0 -i ../fbreader-gcc6.patch # Fix build with GCC6 (Gentoo)
}

build() {
  export TARGET_ARCH=desktop
  export UI_TYPE=qt4
  export TARGET_STATUS=release
  cd "$srcdir"/fbreader-$pkgver
  make ZLSHARED=no MOC=/usr/bin/moc-qt4
}

package() {
  cd "$srcdir"/fbreader-$pkgver
  make ZLSHARED=no DESTDIR="$pkgdir" install
}
