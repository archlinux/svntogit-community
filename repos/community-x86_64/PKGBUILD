# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jacek Poplawski <jacekpoplawski@gmail.com>

pkgname=rocksndiamonds
pkgver=3.3.1.2
pkgrel=2
pkgdesc="game similiar to Boulderdash with lots of levels"
arch=(i686 x86_64)
url="http://www.artsoft.org/rocksndiamonds/"
license=('GPL')
depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_net' 'smpeg')
makedepends=('unzip' 'p7zip')
source=(http://www.artsoft.org/RELEASES/unix/rocksndiamonds/rocksndiamonds-$pkgver.tar.gz
	rocksndiamonds.desktop)
md5sums=('9fb7d125a314f55c5148c0e47f9ebb42'
         '18a3541db4d71feb0ad3b50ea4006738')

prepare() {
  cd $srcdir/rocksndiamonds-$pkgver
  sed -i 's|inline void InitElementSmallImagesScaledUp|void InitElementSmallImagesScaledUp|g' src/init.c
}

build() {
  cd $srcdir/rocksndiamonds-$pkgver
  make clean
  make RO_GAME_DIR=/usr/share/rocksndiamonds RW_GAME_DIR=/usr/share/rocksndiamonds
}

package() {
  depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_net' 'smpeg' 'rocksndiamonds-data')

  cd $srcdir/rocksndiamonds-$pkgver

  install -d $pkgdir/usr/bin
  install -d $pkgdir/usr/share/man/man1
  cp $srcdir/rocksndiamonds-$pkgver/rocksndiamonds $pkgdir/usr/bin/rocksndiamonds
  cp $srcdir/rocksndiamonds-$pkgver/rocksndiamonds.1 $pkgdir/usr/share/man/man1

  install -Dm0644 $srcdir/rocksndiamonds.desktop $pkgdir/usr/share/applications/rocksndiamonds.desktop
}
