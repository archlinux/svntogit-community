# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: hydro <hydro@freenet.de>

pkgbase=mediainfo
pkgname=(mediainfo mediainfo-gui)
pkgver=21.09
pkgrel=1

pkgdesc="Supplies technical and tag information about a video or audio file"
url="https://mediaarea.net/"
arch=('x86_64')
license=('BSD')

depends=('libmediainfo')
makedepends=('wxgtk3')

source=(https://mediaarea.net/download/source/mediainfo/$pkgver/mediainfo_$pkgver.tar.xz)

sha512sums=('81545c936ec5f17f94246b2cd5343afd105d0b0791f151689fbecdb084778dd4e84da79f47529f16b8383542894819af995ec608588fc6d9f45e9be38a4304db')
b2sums=('8deb996c84ededd390a4fb91f77a4dd0ba60f3a165aefde2fe862720a7a3158888ba56f911533c347703e0a722f05b94edc5f649edef26031b519fc7bd4012cd')

build() {
  cd MediaInfo/Project/GNU/CLI
  ./autogen.sh
  ./configure --prefix=/usr
  make

  cd "$srcdir/MediaInfo/Project/GNU/GUI"
  ./autogen.sh
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package_mediainfo() {
  pkgdesc+=" (CLI interface)"

  cd MediaInfo/Project/GNU/CLI
  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfo/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mediainfo-gui() {
  pkgdesc+=" (GUI interface)"
  depends+=('wxgtk3' 'hicolor-icon-theme')

  cd MediaInfo/Project/GNU/GUI
  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfo/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
