# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=mbedtls
pkgver=2.16.6
pkgrel=1
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('x86_64')
url="https://tls.mbed.org"
license=('Apache')
depends=('glibc' 'sh')
checkdepends=('python')
provides=('polarssl' 'libmbedcrypto.so' 'libmbedtls.so' 'libmbedx509.so')
replaces=('polarssl')
conflicts=('polarssl')
options=('staticlibs')
source=("https://tls.mbed.org/download/mbedtls-$pkgver-apache.tgz")
sha1sums=('3cb5b681597a5bd798d31038c129c0dc911d8a2c')
b2sums=('a72be1d7515ac28c54158aa00c6b3f39e3d35e7a92422e545715934945faa8e0cbc6bde3b5208cc4293066e14e7e14a6dbf629b8585860b0b5e3466328e473ed')

prepare() {
  cd "$pkgname-$pkgver"
  # enable flags for non-embedded systems
  sed -i 's|//\(#define MBEDTLS_THREADING_C\)|\1|' include/mbedtls/config.h
  sed -i 's|//\(#define MBEDTLS_THREADING_PTHREAD\)|\1|' include/mbedtls/config.h

  # FS#49914 (2.3.0-2)
  sed -i 's|<time.h>|"platform.h"|' include/mbedtls/ssl.h
}

build() {
  cd "$pkgname-$pkgver"
  LDFLAGS+=" -I../include " make SHARED=1 no_test
}

check() {
  cd "$pkgname-$pkgver"
  make SHARED=1 PYTHON=python check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/usr" install
}
