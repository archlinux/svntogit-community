# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=libsass
pkgver=3.6.0
pkgrel=1
pkgdesc="C implementation of Sass CSS preprocessor (library)."
arch=("x86_64")
url="http://libsass.org/"
license=("MIT")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('9665e50ee964ca3cc323f26c2b8322677102d26a7a102558ffbc5bef7a4c4ea44ca5096a967e4044dac1404bfa343a37d846f22d1e1adc27592023d5d4ac40c8')


build() {
	cd "$srcdir/$pkgname-$pkgver"

	export LIBSASS_VERSION="$pkgver"
	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir/" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
