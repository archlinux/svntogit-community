# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-libxslt
pkgver=1.82
pkgrel=1
pkgdesc="Interface to the gnome libxslt library"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/XML-LibXSLT"
license=('GPL')
depends=('perl-xml-libxml' 'libxslt')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/S/SH/SHLOMIF/XML-LibXSLT-$pkgver.tar.gz)
md5sums=('91b5432b2c68fcf88b25cfd825b23b14')

build() {
  cd XML-LibXSLT-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-LibXSLT-$pkgver
  make test
}

package() {
  cd XML-LibXSLT-$pkgver
  make DESTDIR="$pkgdir" install
}
