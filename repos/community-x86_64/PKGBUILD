# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Sandy Carter <bwrsandman@gmail.com>

pkgname=openmw
pkgver=0.47.0
pkgrel=10
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt' 'boost-libs')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://github.com/OpenMW/openmw/archive/refs/tags/openmw-0.47.0.tar.gz"
        https://gitlab.com/OpenMW/openmw/-/merge_requests/1870.patch
        1941.patch
        https://gitlab.com/OpenMW/openmw/-/commit/98a7d90ee258ceef9c70b0b2955d0458ec46f048.patch)
sha512sums=('517e650d0054cdba8d38de05ed3975b0fcf8de32fda59c8df7c34b973e5390efa0dd9ec79babdfdaa79dee7aa8a75009776f62fad91b9aa9aa28016c5032652b'
            '99f721bbeba43a0e259498bbf16ae24dba2a8554e5543fce265ef6c3cc544f4b28c6a13ed3aa51b1eea97fc0552fe056b028188f326dca0265096cf29988f504'
            'ca1b8601b4baca6929c928b9349e3b07d21992890d4c50c5ff4d537a5ab1b77f044c520af46e8f21480d37902e6329e5ec4459056ac9d8801ba0278d8391bcd7'
            '37dfc5611523bf158fe2b6c19bdc11ebd0a70837943183b16b1ec36e06df37c7e089586935753c7442720349d5a7e356dc4f11e79666af0c4d5146f8f64d4853')

prepare () {
  cd openmw-openmw-$pkgver
  patch -Np1 -i "$srcdir"/1870.patch
  patch -Np1 -i "$srcdir"/1941.patch
  patch -Np1 -i "$srcdir"/98a7d90ee258ceef9c70b0b2955d0458ec46f048.patch
}

build() {
  cd openmw-openmw-$pkgver

  # We can't use system bullet because openmw needs a version built with double
  # precision but the one we ship in Arch is built with single precision for
  # performance reasons.
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DOPENMW_LTO_BUILD=ON \
    -DDESIRED_QT_VERSION=5 \
    -DOPENMW_USE_SYSTEM_BULLET=OFF \
    -GNinja \
    -Bbuild
  ninja -C build
}

package() {
  cd openmw-openmw-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
