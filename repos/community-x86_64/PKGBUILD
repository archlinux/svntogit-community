# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-visualization-shadertoy
epoch=1
pkgver=19.1.2
_codename=Matrix
pkgrel=2
pkgdesc="Shadertoy visualizer for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/visualization.shadertoy'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-visualization')
makedepends=('cmake' 'p8-platform' 'glm' 'kodi-dev')
options=(!lto debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/visualization.shadertoy/archive/$pkgver-$_codename.tar.gz")
sha512sums=('3dcbdcb147da57c68b2706dae66eb5665a0348d4e7b28e5a1016fb2538d7cc86e572469715a183489e3bdfec750484f7660c4c8e8eb6b39e26e10b084a9f419d')

build() {
    cd "visualization.shadertoy-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'p8-platform' 'glm')
    cd "visualization.shadertoy-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

