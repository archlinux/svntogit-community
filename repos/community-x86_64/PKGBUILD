# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-qt
pkgver=5.0.6
pkgrel=1
pkgdesc="Fcitx5 Qt Library"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-qt"
license=('GPL')
groups=('fcitx5-im')
depends=('fcitx5' 'qt5-base')
makedepends=('extra-cmake-modules' 'ninja' 'qt6-base')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('8a9230c894438abed46ba8c749f0df5724630e94f9af19be8da65cbe75068c4ff92e345431716ed3ed48133f3f59a80f9eccedc84bc3680dbc88c1d77b61a9c5'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib -DENABLE_QT4=0 -DENABLE_QT6=1 .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
