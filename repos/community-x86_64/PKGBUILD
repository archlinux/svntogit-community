# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Kyle Manna <kyle [at] kylemanna [dot] com>

pkgname=lurk
pkgver=0.2.9
pkgrel=2
pkgdesc="A pretty (simple) alternative to strace"
arch=('x86_64')
url="https://github.com/JakWai01/lurk"
license=('AGPL3')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('0dd31847b01e672c28c767f608db48e823fa4e0d15912d05fe4dd7bd241c3a1b4fbeee3f72da07c54c2217cff4a9aeff4fdbb203ea79322783ee8751fa715038')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
