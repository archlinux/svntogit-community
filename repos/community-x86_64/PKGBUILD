# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad
pkgver=1.2.2
pkgrel=1
pkgdesc="A simple and flexible workload orchestrator"
arch=('x86_64')
url="https://www.nomadproject.io"
license=('MPL2')
depends=('iptables')
makedepends=('go' 'git')
optdepends=(
  'docker: for docker driver'
  'java-runtime: for java driver'
  'java-runtime-headless: for java driver - headless'
  'qemu: for qemu driver'
  'nomad-driver-nspawn: for systemd-nspawn driver'
  'nomad-driver-podman: for podman driver'
  'nomad-driver-lxc: for lxc driver'
  'nomad-driver-containerd: for containerd driver'
)
backup=(etc/nomad.d/defaults.hcl)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/nomad/archive/v$pkgver.tar.gz"
  'systemd.service'
  'tmpfiles.conf'
  'defaults.hcl'
)
sha512sums=('4fde02dc6878e9b42a54eac81d7a88e8f6d56f15b6f584e50528ca0605f93a0dd3175e87535981d98902c74020de6b45dc1ab72451c2695fdd9359ffec2c40ed'
            '4fe233272e6872cb9ec0c594944a3c701d93ee59952688a4bc45b126442ecf973d23e50b3c4c9c5c62778d0c555ace2e2781955448239249d39fe21d184c8f1b'
            '398724820ee6f1ffa7860b8dd0013b671ba97ffb001331a10e7795148e3e6ff711a06f2156187c23b71bfccb13dac70828df5a29d59441706753cc17afd1e3d7'
            '9bc9dc93b2683c848e94d5d7a59102ceebd6c4b64f14c85591b9502bc44e3c74589425f1f278ef21407d10c4a9a82abd95e2c748d347d76836bdb741bbff7f7c')
b2sums=('5c40fb4f14793bb7ab2492fe7beff7e7a4156763b7ff2ba6405a607433f50eceae472e5cfd71b3165ec22dc827c322630e2b51ccc80b54d67fb80bc5724deef5'
        '0f931868e9d7465f3be176ab789b7c7b5a4c35713a82e4d3bb94460254011779d814d4708f4f8cc76ba0dd1928ac7eeda42e01ed43be675a5d26d0b75ba986b9'
        '4142a758057382846b48e9bfc443c1218c8fde866544d56c6e773933d67a0824e365280d39864249f355aee6080e0b4b969fd8cc4c5826583821ad1843b07e35'
        'abbf4c69d70e03b318f73ef84ccf258e39b25ac956ec68c82bebe277662ddd1677719c3b693c04a8cc38554c562a127aa2ba4061e20e594440d432d729bab3b3')

prepare() {
	cd "$pkgname-$pkgver"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download
}

build() {
	cd "$pkgname-$pkgver"

  local GO_TAGS="ui release"

  go build -v \
    -buildmode=pie \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags ${LDFLAGS} -X "main.GitCommit=${pkgver}"" \
    -tags "$GO_TAGS" \
    -o build .
}

package() {
  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname-$pkgver/build/$pkgname"

  # configuration
  install -vDm644 -t "$pkgdir/etc/nomad.d" defaults.hcl

  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
