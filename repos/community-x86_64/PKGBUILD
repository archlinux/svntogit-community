# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=iddawc
pkgver=0.9.9
pkgrel=1
pkgdesc='OAuth2 and OIDC client library'
arch=(x86_64)
url=https://github.com/babelouest/iddawc
license=(LGPL2.1)
depends=(
  glibc
  liborcania.so
  librhonabwy.so
  libulfius.so
  libyder.so
)
makedepends=(
  cmake
  doxygen
  git
  graphviz
  ninja
)
provides=(libiddawc.so)
_tag=5d1e130f4791b990e81eb1631b4c9e84478f5767
source=(git+https://github.com/babelouest/iddawc.git#tag=${_tag})
validpgpkeys=(8405B02FCC28EF9744C8F253FE82139440BD22B9) # Nicolas Mora <nicolas@babelouest.org>
b2sums=(SKIP)

pkgver() {
  cd iddawc
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S iddawc -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_IDDAWC_DOCUMENTATION=ON
  ninja -C build doc
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -dm 755 "${pkgdir}"/usr/share/doc/iddawc
  cp -dr --no-preserve=ownership iddawc/doc/html "${pkgdir}"/usr/share/doc/iddawc/
}

# vim: ts=2 sw=2 et:
