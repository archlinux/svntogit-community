# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.12.0
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('imagemagick' 'librsvg' 'libwebp' 'libxslt')
makedepends=('gtk-doc')
license=("LGPL")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.asc})
sha512sums=('39ae005a919bc62d792f19c1685a7a4516a867386af89a4261dede90817262f9b048886904be687d71a612b030eaf07c813c92490f0eebfd58c3d40930f7102e'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
