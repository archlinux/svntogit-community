# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=waybar
pkgver=0.7.1
pkgrel=2
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'fmt'
    'gtkmm3'
    'jsoncpp'
    'libdbusmenu-gtk3'
    'libpulse'
    'libmpdclient'
)
# Sway is needed for building support for workspace and window title modules
makedepends=(
    'meson'
    'spdlog'
    'sway'
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz")
sha256sums=('5100ea9a2ebd2326c791c05d68fc692eaf8c500b21850758205a0a61a0dc77de')

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr build
    ninja -C build
}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
