# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-modsecurity
epoch=1
pkgver=1.0.1
pkgrel=4

pkgdesc='ModSecurity v3 Nginx Connector (module for nginx)'
arch=('x86_64')
depends=('nginx' 'libmodsecurity')
makedepends=('nginx-src')
url="https://github.com/SpiderLabs/ModSecurity-nginx"
license=('GPL3')

source=(
	https://github.com/SpiderLabs/ModSecurity-nginx/releases/download/v$pkgver/modsecurity-nginx-v$pkgver.tar.gz{,.asc}
)
validpgpkeys=('B0F4253373F8F6F510D42178520A9993A1C052F8'  # Maxim Dounin <mdounin@mdounin.ru>
              '190EFACCA1E9FA466A8ECD9CE6DFB08CE8B11277') # Felipe Zimmerle
sha256sums=('def45a8db5bc9da14765eda75363457209a86c89538ccf5bfbd3aa02fa10833c'
            'SKIP')

build() {
	cp -r /usr/src/nginx .

	cd "$srcdir"/nginx
	./configure --with-compat --add-dynamic-module=../modsecurity-nginx-v$pkgver
	make modules
}

package() {
	cd "$srcdir"/nginx/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir/usr/lib/nginx/modules/$mod"
	done
}
