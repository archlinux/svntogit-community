# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=haskell-aeson
_hkgname=aeson
pkgver=1.2.3.0
pkgrel=3
pkgdesc="A JSON parsing and encoding library optimized for ease of use and high performance."
url="https://github.com/bos/aeson"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-compat' 'haskell-dlist' 'haskell-hashable'
         'haskell-scientific' 'haskell-tagged' 'haskell-th-abstraction' 'haskell-time-locale-compat'
         'haskell-unordered-containers' 'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('756c230024010cacb5f8961ccbeda240e8fd04187cbb593605ee8709c6e8d6f20979d72e81fb05b5bcd5d36054a1c0b216c87bba8f2f4f914e80ff39a5a74659')

build() {
  cd "$srcdir/$_hkgname-$pkgver"
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-developer -f-fast -f-bytestring-builder -f-cffi
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "${srcdir}/${_hkgname}-${pkgver}"

  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
