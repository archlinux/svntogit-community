# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-smallcheck
pkgname=haskell-tasty-smallcheck
pkgver=0.8.2
pkgrel=392
pkgdesc="SmallCheck support for the Tasty test framework"
url="https://github.com/feuerbach/tasty"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-optparse-applicative' 'haskell-smallcheck' 'haskell-tagged'
         'haskell-tasty')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('ff816280315e26d20210f0dcbf55e32780aa9ef455d8d00b146d9daeed6afec5b6593cd8a8dc0b7c0bc7ff77da75f065f9c7253494f2c47150e2b7a6aa29fc6a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
