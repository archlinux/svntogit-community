# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>
# Contributor: Glaucous <glakke1 at gmail dot com>

pkgname=apitrace
pkgver=10.0
pkgrel=1
pkgdesc="Graphics API Tracing"
arch=('x86_64')
url="https://github.com/apitrace/apitrace"
license=('custom')
makedepends=('cmake' 'mesa' 'qt5-base')
depends=('python' 'libgl' 'procps-ng')
optdepends=('qt5-base: GUI support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/apitrace/apitrace/archive/${pkgver}.tar.gz")
sha256sums=('2e57d37af39369d7ce78666acb8afb1a49ec758f683a21077ec39eb0befae4ad')

build() {
  cd apitrace-${pkgver}

  cmake . -Bbuild -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_GUI=TRUE
  make -C build
}

package() {
  cd apitrace-${pkgver}

  make -C build DESTDIR="${pkgdir}/" install

  install -m755 -d "${pkgdir}/usr/share/licenses/apitrace"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/apitrace/"
}
