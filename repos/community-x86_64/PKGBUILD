# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.0.3
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'samurai' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('ab44c5c157d5a37b35a88f89ba76254ade3f7dfdfaaa7ea7a09aabb49dce3c8f7d789d5d136b2a629c37483d2098ec957bfd07f930d786475ac1bf3973f4d56f')
b2sums=('78067cc11b0a8e77f8bc0a0a942430dbee31e9b8f1a857b9f10b7c16982c0d5e64267e91d35bbd1d66391d328e35efc536d953eee00b2b4b3c6d8c6c28052601')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/builddir
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir="/usr/lib/${pkgname}" \
          --buildtype=plain \
          -Dinstall_binfiles=false \
          ..

    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

     DESTDIR="${pkgdir}" samu install
}
