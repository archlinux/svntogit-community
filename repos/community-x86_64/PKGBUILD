# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.4.4
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('x86_64')
url="https://github.com/sailfishos/fingerterm"
license=('LGPL')
depends=('qt5-feedback')
makedepends=('nemo-qml-plugin-notifications' 'qt5-tools')
source=("https://github.com/sailfishos/fingerterm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('67c655f09a0a9b7345ef00487d394d354638ecb4b1e764e30c898f84ba32f347ed1ffb336364a63966626858f8a8d03270a443c9b6126a724398fcc82cc2e509')

build() {
  cd fingerterm-$pkgver

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm-$pkgver

  make INSTALL_ROOT="$pkgdir" install
}
