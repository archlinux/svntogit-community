# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Albert Graef <aggraef at gmail.com>
# Contributor: Bernardo Barros <bernardobarros at gmail dot com>
# Contributor: bjoern lindig (bjoern _dot_ lindig _at_ google.com)

pkgname=faust
pkgver=2.5.23
pkgrel=3
pkgdesc="A functional programming language for realtime audio signal processing."
arch=('x86_64')
url="https://faust.grame.fr/"
license=('GPL2')
groups=('pro-audio')
depends=('llvm-libs' 'libsndfile' 'libmicrohttpd')
makedepends=('llvm' 'xxd')
# FIXME: java-environment=8 can not be used with current tooling
optdepends=('clang: needed for sound2reader'
            'jdk8-openjdk: needed for faust2android'
            'python2: needed for faust2md and faust2atomsnippets'
            'ruby: needed for faust2sc and scbuilder')
options=('staticlibs')
source=("https://github.com/grame-cncm/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7ffa165b865cea09b923cb1687e5b48a48186644f9efcf8b532f948368c825f0ce829a8d6ef64d9a59c81bac0597dbd6b05bd1ac40198f5d4e6403eda1e57e69')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix scripts requiring python2
  sed -e '1s/python/&2/' \
    -i "tools/${pkgname}2appls/${pkgname}2"{md,atomsnippets}
}

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr world
}

package() {
  cd "${pkgname}-${pkgver}"
  make install PREFIX=/usr DESTDIR="$pkgdir"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 documentation/{,misc/}*.pdf

  # examples
  install -vd "${pkgdir}/usr/share/${pkgname}/examples"
  cp -vR "examples/"* "${pkgdir}/usr/share/${pkgname}/examples/"

  cd syntax-highlighting
  # atom
  install -vDm 644 "atom/language-${pkgname}/package.json" \
    "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/package.json"
  install -vDm 644 "atom/language-${pkgname}/grammars/${pkgname}.cson" \
    "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/grammars/${pkgname}.cson"
  install -vDm 644 "atom/language-${pkgname}/settings/language-${pkgname}.cson" \
    "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/settings/language-${pkgname}.cson"
  install -t "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/snippets/" \
    -vDm644 "atom/language-${pkgname}/snippets/"*
  install -vDm 644 "atom/language-${pkgname}/process-palette.json.linux" \
    "${pkgdir}/usr/share/doc/${pkgname}/process-palette.json"
  # kate
  install -vDm 644 "${pkgname}.xml" \
    "${pkgdir}/usr/share/apps/katepart/syntax/${pkgname}.xml"
  # gedit
  install -vDm 644 "${pkgname}.lang" \
    "${pkgdir}/usr/share/gtksourceview-2.0/language-specs/${pkgname}.lang"
  install -vDm 644 "${pkgname}.lang" \
    "${pkgdir}/usr/share/gtksourceview-3.0/language-specs/${pkgname}.lang"
  # highlight
  install -vDm 644 dsp.lang "$pkgdir/usr/share/highlight/langDefs/dsp.lang"
  # nano
  install -vDm 644 "${pkgname}.nanorc" "$pkgdir/usr/share/nano/${pkgname}.nanorc"
  # vim
  install -vDm 644 "${pkgname}.vim" \
    "${pkgdir}/usr/share/vim/vimfiles/syntax/${pkgname}.vim"
  # emacs
  install -vDm 644 "${pkgname}-mode.el" \
    "${pkgdir}/usr/share/emacs/site-lisp/${pkgname}-mode.el"
}

