# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Blackleg <blacklg@openaliasbox.org>
# Contributor: Nidhogg
# Contributor: juantascon

pkgname=python-peewee
pkgver=3.16.1
pkgrel=1
pkgdesc="a little orm"
url="https://github.com/coleifer/peewee/"
arch=('x86_64')
license=('MIT')
depends=('python' 'sqlite')
optdepends=('python-psycopg2: for PostgreSQL database support'
            'python-pymysql: for MySQL database support')
makedepends=('python-build' 'python-installer' 'cython' 'python-setuptools' 'python-wheel')
checkdepends=('python-apsw' 'python-flask' 'python-psycopg2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/coleifer/peewee/archive/$pkgver.tar.gz")
sha512sums=('e89f1a2f25c2675403a560d3fa8eade833f9ef8b2fecfef00769a9633e99cb40feff7c4e1d2e9b001d4e6f50a8dae4594ffe22adfbe64a93035c47f673d5557c')

build() {
  cd peewee-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd peewee-$pkgver
  python runtests.py
}

package() {
  cd peewee-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
