# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=2.2.1
pkgrel=3
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'libibus' 'libcanberra-pulse' 'librsvg' 'nss'
         'pulseaudio-alsa' 'upower' 'libnotify' 'libgnomekbd' 'libwacom')
makedepends=('intltool' 'docbook-xsl' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs')
install=cinnamon-settings-daemon.install
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
        "upower_lid.patch")
sha256sums=('89798977ad44fa4cfaff26d93e21b29f1a9a6a68a1c57c30e5414ad2159a849e'
            '4a94ab880a903c88929ecbfc465cb46aadf30ee14930274fa0eefb7a12f4e3b8')

prepare() {
  cd $pkgname-$pkgver

  # Fix close lid action with upower 0.99
  patch -Np1 -i ../upower_lid.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
