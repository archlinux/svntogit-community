# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-dolphin
pkgver=32962
pkgrel=1
pkgdesc='Nintendo GC/Wii core'
arch=(x86_64)
url=https://github.com/libretro/dolphin
license=(GPL2)
groups=(libretro)
depends=(
  bluez-libs
  enet
  fmt
  hidapi
  libevdev
  libpng
  libretro-core-info
  libsystemd
  libx11
  libxi
  libxrandr
  mbedtls
  lzo
  sfml
  systemd-libs
  zlib
)
makedepends=(
  cmake
  git
  mesa
  python
)
provides=(libretro-dolphin)
conflicts=(libretro-dolphin)
_commit=639002c8fa412b2438fa03c1c403c557084339f5
source=(libretro-dolphin::git+https://github.com/libretro/dolphin.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-dolphin

  git rev-list --count HEAD
}

build() {
  cmake -S libretro-dolphin -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_LTO=ON \
    -DENABLE_NOGUI=OFF \
    -DENABLE_QT=OFF \
    -DENABLE_TESTS=OFF \
    -DLIBRETRO=ON \
    -DUSE_SHARED_ENET=ON \
    -Wno-dev
  make -C build
}

package() {
  install -Dm 644 build/dolphin_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
