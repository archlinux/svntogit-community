# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.2.0.7
pkgrel=1
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-wallpapers' 'kwin')
makedepends=('extra-cmake-modules' 'expac' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        $pkgname-tabbox-chameleon-rename.patch::https://github.com/linuxdeepin/dde-kwin/pull/106.patch
        $pkgname-unload-blur.patch
        $pkgname-added-functions-from-their-forked-kwin.patch)
sha512sums=('76807067b5f69e6cad90439d0e0439158edf6a4974d2367f64f1749c88e6161e9238a8903fcd2a777e4a1689cefef3eea4d879a9383181cc93a3a26a3e03250a'
            '7ac1a577868b73314274d336287334acc5bf4ac06dde046c93e4c2d3f68a112389c8b0644d1cbc3fde5ba1c01f368676f4f34b4a1b7b6c97a5b73d66507d134d'
            '9c5bf86510d0fbba36e71997f55eda1b197b26af7eeb0adb9e06e7e3cc86b32c00cd23dfca872f4043f76a1d9435f916aae73536f10c7b3fe7e79a6e74a309d2'
            '48a3bead1c7cde1261f5f710fd055c52f061320999064ba4cc96a81f9b0770ea2a8fba7aff37d20bb7671b219c38bf8424f24fd7742d36b3c6bf171d3874c1f4')

prepare() {
  cd dde-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' plugins/kwineffects/multitasking/background.cpp
  patch -R -p1 -i ../$pkgname-added-functions-from-their-forked-kwin.patch
  patch -p1 -i ../$pkgname-tabbox-chameleon-rename.patch
  patch -p1 -i ../$pkgname-unload-blur.patch

  # https://github.com/linuxdeepin/dde-kwin/issues/127
  sed -i '/m_blurManager->create/d' plugins/kwineffects/blur/blur.cpp
}

build() {
  cd dde-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DKWIN_VERSION=$(expac %v kwin | cut -d - -f 1)
  ninja
}

package() {
  cd dde-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/kwin_no_scale
}
