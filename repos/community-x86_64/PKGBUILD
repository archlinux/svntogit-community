# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=libmysofa
pkgver=1.3
pkgrel=1
pkgdesc='C library to read HRTFs if they are stored in the AES69-2015 SOFA format'
arch=('x86_64')
url='https://github.com/hoene/libmysofa/'
license=('BSD')
depends=('zlib')
makedepends=('cmake' 'cunit')
checkdepends=('nodejs')
source=("https://github.com/hoene/libmysofa/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-libmysofa-fix-missing-stddef-header.patch'::'https://github.com/hoene/libmysofa/commit/7aa1a10277e79642fe900552825b5e37f4fb6425.patch')
sha256sums=('12dcc4108222b2a98170fbdce5e498285e3f0bbed08ff6bc722ae96662f3ed09'
            '1aaea2461beac802afe65d28b8eddccabdf9fc0d3e7adad0600079db0066c04d')

prepare() {
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-libmysofa-fix-missing-stddef-header.patch"
}

build() {
    cmake \
        -B "${pkgname}-${pkgver}/build" \
        -S "${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCODE_COVERAGE:BOOL='OFF' \
        -Wno-dev
    make -C "${pkgname}-${pkgver}/build" all
}

check() {
    make -C "${pkgname}-${pkgver}/build" test
}

package() {
    make -C "${pkgname}-${pkgver}/build" DESTDIR="$pkgdir" install
    install -D -m644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
