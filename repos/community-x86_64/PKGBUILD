# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=gitoxide
pkgver=0.20.0
pkgrel=1
pkgdesc="An idiomatic, lean, fast & safe pure Rust implementation of Git"
url='https://github.com/Byron/gitoxide'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=('libssl.so')
makedepends=('cargo' 'cmake')
options=(!lto)
source=(https://github.com/Byron/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4ef4681b643d1a5b9ca6ae694234441af4c99806fa43a6847a6e4a61df1bb1cd')
b2sums=('51f4bb1f0fed88df094ff2e3a6368bf59383783b4f349534ab1e19361aa92b5d12df6821eec65d7b94967c35cdbdb101071df841a17855979959c0fae1d92b29')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 \
    cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/gix \
    target/release/ein
}

# vim: ts=2 sw=2 et:
