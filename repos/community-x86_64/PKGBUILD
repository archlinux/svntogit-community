# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Verelst <david dot verelst at gmail com>
# Contributor: richli

pkgname=python-shapely
pkgver=1.8.1
pkgrel=1
pkgdesc="Manipulation and analysis of geometric objects in the Cartesian plane"
arch=('x86_64')
url="https://pypi.python.org/pypi/Shapely"
license=('BSD')
depends=('geos' 'python')
optdepends=('python-numpy: for shapely.vectorized submodule')
makedepends=('python-setuptools' 'python-packaging' 'python-numpy' 'cython')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/S/Shapely/Shapely-${pkgver}.tar.gz")
sha512sums=('42fcbf1bfdaf1337e327c1357109fff2fad69bfcc7728cb19cad9a43e94d316299069934af212badbc3cfe2b3694fe43bf6ee589d01d5f7ff57b6c25c1386510')

prepare() {
  cd Shapely-$pkgver
  rm -r _vendor
  sed -i 's/from _vendor.packaging./from packaging./' setup.py
}

build() {
  cd Shapely-$pkgver
  python setup.py build
}

check() {
  # TODO: make the hack less dirty...

  cd Shapely-$pkgver
  mv shapely shapely.bak
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10:$PYTHONPATH" py.test
  mv shapely.bak shapely
}

package() {
  cd Shapely-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
