# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=sniffglue
pkgver=0.8.2
pkgrel=1
pkgdesc='Secure multithreaded packet sniffer'
url='https://github.com/kpcyrd/sniffglue'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libseccomp')
makedepends=('cargo')
backup=('etc/sniffglue.conf')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        sniffglue-sysuser.conf
        sniffglue.conf)
sha256sums=('08b3b3077399d634bfefaad5d9c036b423c484ff06997fcf4223ce37f58de304'
            '531e051488e57ed2f5bf5d8289fa5562aa0ca73783838c11ec13c043dc268d02'
            'a2e6cfa918a4e354b5c4c4c84867f3bb142833fde4acab545e13c8e89864004c')
sha512sums=('5a781e93ac792bcac1c4f355a06fe92a5f51f0796adeb2352a2c0306ad40968e276baed74611e15cffcb3bc2190bff318990bac3be7d957d9cbbd3431753e799'
            '2595eb4856bc7a59c078255bf2aeeaa44a4f6a69b37cc8982e5d70a8ef14cb29b83b4d191ab3baabfbcf209f3effd232c10ec5baabc6ce52990969526d6fda98'
            '66e6b87e832ba8424334d24e3457a214e60e67c731471098329d0ca54c574518f7856ba780bb6bffde53bdefe2c8f789a242138ae67e2fef3e562fa083b362b1')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 "${srcdir}/sniffglue-sysuser.conf" "${pkgdir}/usr/lib/sysusers.d/sniffglue.conf"
  install -Dm 644 "${srcdir}/sniffglue.conf" -t "${pkgdir}/etc"
  install -Dm 644 docs/sniffglue.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
