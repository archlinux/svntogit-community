# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=base-compat-batteries
pkgname=haskell-base-compat-batteries
pkgver=0.10.5
pkgrel=6
pkgdesc="base-compat with extra batteries"
url="https://hackage.haskell.org/package/base-compat-batteries"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat')
makedepends=('ghc'  'haskell-hspec' 'haskell-hspec-discover' 'haskell-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('6f8ebbad8ca5ae54257e9637d36eec48887cfae2e34d0a78339993f63f68419cc86fb031f1d955f129416cf0913e34f243aafe272fcbc5b8b6b43835f898df7f')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
