# $Id$
# Maintainer: 
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: DrZaius <lou at fakeoutdoorsman dot com>
# Contributor: Victor Feight <vrfeight3 at gmail dot com>

pkgname=gsimplecal
pkgver=2.1
pkgrel=3
pkgdesc="Simple and lightweight GTK calendar"
arch=('x86_64')
url="http://dmedvinsky.github.com/$pkgname"
license=('BSD')
depends=('gtk3')
source=($pkgname-$pkgver.tar.gz::https://github.com/dmedvinsky/$pkgname/archive/v$pkgver.tar.gz)
md5sums=('170ac0bdc8663d97dec436e45b195491')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
