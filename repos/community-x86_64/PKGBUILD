# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: arch3y <arch3y[at]archstrike[dot]org>

_gitname=yara-python
pkgbase=python-yara
pkgname=('python-yara' 'python2-yara')
pkgver=3.7.0
_gitcommit=71138553e7cfe7f96e7822898dd8331f3eacef4e
pkgrel=2
pkgdesc='Tool aimed at helping malware researchers to identify and classify malware samples'
url='https://github.com/VirusTotal/yara-python'
arch=('x86_64')
license=('Apache')
makedepends=('git' 'yara' 'glibc'
             'python' 'python-setuptools'
             'python2' 'python2-setuptools')
source=(${pkgbase}::git+"https://github.com/VirusTotal/${_gitname}#commit=${_gitcommit}")
sha512sums=('SKIP')

pkgver() {
  cd ${pkgbase}
  git describe --always | sed 's/^v//;s/-/./g'
}

prepare() {
  cp -a ${pkgbase}{,-py2}
}

build() {
  msg2 'Building python...'
  (cd ${pkgbase}
    python setup.py build --dynamic-linking
  )
  msg2 'Building python2...'
  (cd ${pkgbase}-py2
    python2 setup.py build --dynamic-linking
  )
}

check() {
  msg2 'Checking python...'
  (cd ${pkgbase}
    local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python tests.py
  )
  msg2 'Checking python2...'
  (cd ${pkgbase}-py2
    local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      python2 tests.py
  )
}

package_python-yara() {
  depends=('python' 'yara' 'glibc')
  cd ${pkgbase}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

package_python2-yara() {
  depends=('python2' 'yara' 'glibc')
  cd ${pkgbase}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  ln -s /usr/share/doc/yara/docs "${pkgdir}/usr/share/doc/${pkgname}/docs"
}

# vim: ts=2 sw=2 et:
