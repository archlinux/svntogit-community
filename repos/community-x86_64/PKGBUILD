# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: ajs124 < aur AT ajs124 DOT de >

pkgname=firejail
pkgver=0.9.72
pkgrel=1
pkgdesc='Linux namespaces sandbox program'
arch=('x86_64')
url=https://github.com/netblue30/firejail
license=('GPL2')
depends=('apparmor')
optdepends=('xdg-dbus-proxy: for D-Bus filtering')
backup=('etc/apparmor.d/local/firejail-default'
        'etc/firejail/firecfg.config'
        'etc/firejail/firejail.config'
        'etc/firejail/login.users')
install=firejail.install
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('82e177c48cfc87f62b088b55efc53ff4612b9740aab5ea35cbf2395e83efe7f4'
            'SKIP')
b2sums=('3d57b345476cb62399859622c88f5d6c22842da5894045c09bc7d84229ec2a01c494e4e9393b6fba6c668f73c6b7046f9a014a315baa5bc56d1479b9cad178a7'
        'SKIP')
validpgpkeys=('F951164995F5C4006A73411E2CCB36ADFC5849A7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-apparmor
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
