# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=dopewars
pkgver=1.6.1
pkgrel=1
pkgdesc='Turn based strategy game based on "Drug Wars" from 1984'
url='https://dopewars.sourceforge.io/'
license=(GPL)
arch=(x86_64)
backup=(srv/dopewars/dopewars.sco)
makedepends=(alsa-lib audiofile gendesk git gtk3 sdl_mixer)
optdepends=('alsa-lib: for audio'
            'audiofile: for audio'
            'gtk3: for the graphical interface'
            'sdl_mixer: for audio')
source=(dopewars.service
        dopewars.socket
        "git+https://github.com/benmwebb/dopewars#commit=a9ff2aa7c88021918d6bed86c3b2d531eaef3b7b") # tag: v1.6.1
b2sums=('094e4c569fb399ee8701855583db0acde19d13a1cec4c90e3ac21feee946c4f302563880d6be67433a57b30f9dc540b6d26d06121ecc88799c5c37d934a45ccd'
        '438f7a315eb0af93051c2498b3d81823ce74a121c90a04e16f8d0c8f15d6aa4ddc569203100a38732a3c83a3b8a08c8880b7b9e4a2d23cdcac070e5567767d73'
        'SKIP')

prepare() {
  gendesk -f -n --icon=$pkgname-pill --pkgname "$pkgname" --pkgdesc "$pkgdesc"
}

build() {
  cd $pkgname
  autoreconf -i
  ./configure \
    --localstatedir=/srv/dopewars \
    --mandir=/usr/share/man \
    --prefix=/usr
  make
}

package() {
  make -C $pkgname \
    DESKTOPDIR="$pkgdir/usr/share/applications" \
    DESTDIR="$pkgdir" \
    install
  install -Dm644 dopewars.desktop \
    "$pkgdir/usr/share/applications/dopewars.desktop"
  install -Dm644 dopewars.service \
    "$pkgdir/usr/lib/systemd/system/dopewars.service"
  install -Dm644 dopewars.socket \
    "$pkgdir/usr/lib/systemd/system/dopewars.socket"

  # Fix permissions
  chmod 2755 "$pkgdir/usr/bin/dopewars"
  chown root:games "$pkgdir/usr/bin/dopewars"
  chown -R root:games "$pkgdir/srv/dopewars"
  chmod 0775 "$pkgdir/srv/dopewars"
  chmod 0664 "$pkgdir/srv/dopewars/dopewars.sco"
}
