# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=2.9
pkgrel=3
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoctor' 'cmake' 'aspell' 'guile' 'lua' 'perl' 'python'
             'ruby' 'tcl')
optdepends=('aspell: spellchecker support'
            'guile: support for guile scripts'
            'lua: support for lua scripts'
            'perl: support for perl scripts'
            'python: support for python scripts'
            'ruby: support for ruby scripts'
            'tcl: support for tcl scripts')
source=("https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('c4a5723accb417c0496b5427ee58fbf5d3e842e6d79a7988f8680cd605534cfc9d30f6f4844afe45f5223458f28b8deff1284fff2fb5b7d04cb99a81cde4e13e'
            'SKIP')
b2sums=('8089cd846ca192ec666a50e8071ed57e1c2518b2d5ea98733b8e3fa6c8c684e8ed14253d787b3f9995a8f481a7413444c3ba262dcb6c100283192bcafb502627'
        'SKIP')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8') # WeeChat (signing key) <webmaster@weechat.org>

# cmake does not correctly handle CPPFLAGS, so kludge it in anyway:
# https://gitlab.kitware.com/cmake/cmake/-/issues/12928
CFLAGS+=" ${CPPFLAGS}"

build() {
  mkdir -p build
  cd build
  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON \
        -DENABLE_JAVASCRIPT=OFF \
        -DENABLE_PHP=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
