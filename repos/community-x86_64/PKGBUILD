# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

pkgname=milkytracker
pkgver=1.02.00
pkgrel=4
pkgdesc='Music tracker inspired by Fast Tracker II'
arch=(x86_64)
url='https://milkytracker.titandemo.org/'
license=(GPL3)
depends=(alsa-lib lhasa rtmidi sdl2 zziplib)
makedepends=(cmake jack ninja)
optdepends=('jack: JACK audio support')
# Disable stripping, ref FS#30018
options=(!strip)
source=("$pkgname-$pkgver.tar.gz::https://github.com/milkytracker/MilkyTracker/archive/v$pkgver.tar.gz")
sha256sums=('6bcb6e74ee333e831137435a25c0f2f3da6e1462864deec9e693ef7d23a16023')

build() {
  mkdir -p build
  cd build
  cmake "../MilkyTracker-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -G Ninja
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
  cd "$srcdir/MilkyTracker-$pkgver/resources"
  install -Dm644 pictures/carton.png \
    "$pkgdir/usr/share/pixmaps/milkytracker.png"
  install -Dm644 milkytracker.desktop \
    "$pkgdir/usr/share/applications/milkytracker.desktop"
  # yes, this file extension is wrong
  install -Dm644 milkytracker.appdata \
    "$pkgdir/usr/share/appdata/milkytracker.appdata.xml"
}

# vim: ts=2 sw=2 et:
