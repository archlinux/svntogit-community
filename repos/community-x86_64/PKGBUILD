# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Mikuro Kagamine <mikurok@forgecrushing.com>

pkgname=croc
pkgver=6.4.10
pkgrel=1
pkgdesc='Easily and securely send things from one computer to another.'
arch=('x86_64')
url="https://github.com/schollz/${pkgname}"
license=('MIT')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("${url}/releases/download/v${pkgver}/${pkgname}_${pkgver}_src.tar.gz"
        "${pkgname}.service")
sha256sums=('433e60b130ad0dfb119d6d18a80e38d246b561ea41be44bbac2b5eabc6d1a8a4'
            'cf074568f16c7b86870a89a0a2d8e488e3837223cca071a911085d814d6f2194')

build() {
    cd "${pkgname}-${pkgver}"
    go build -mod vendor -ldflags "-extldflags ${LDFLAGS}" -trimpath -o "${pkgname}"
}

check() {
    cd "${pkgname}-${pkgver}"
    go test -mod vendor ./...
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 -t "${pkgdir}/usr/bin/" "${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/lib/systemd/system/" "${srcdir}/${pkgname}.service"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
