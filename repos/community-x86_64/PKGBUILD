# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=optics-extra
pkgname=haskell-optics-extra
pkgver=0.4.2.1
pkgrel=55
pkgdesc="Extra utilities and instances for optics-core"
url="https://github.com/well-typed/optics"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hashable' 'haskell-indexed-profunctors'
         'haskell-indexed-traversable-instances' 'haskell-optics-core'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('82f481de519cd1e6d086338de001ddcd2185f9ac212a1905cae17d66820b3c77da5660bec32f84e0b6a8d30eba2e25d4d06c4e9a563833f96ce667d4ba24840d')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u vector $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
