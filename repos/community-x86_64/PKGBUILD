# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: mexus <gilaldpellaeon@gmail.com>
pkgname=dua-cli
pkgver=2.10.2
pkgrel=1
pkgdesc="A tool to conveniently learn about the disk usage of directories, fast!"
arch=('x86_64')
url="https://github.com/Byron/dua-cli"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Byron/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('c43caa42c006b79ee8bda5b4e1583762239fd8f66948a9dc796934f623f83c4032ed62e6106f98fbe8b5f250e71dc9d6fb09be901fc3607ed9878c22c794ecbd')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 755 "target/release/dua" "$pkgdir/usr/bin/dua"
}
