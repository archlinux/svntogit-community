# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=21.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=(x86_64)
url="https://www.rosegardenmusic.com/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc lirc hicolor-icon-theme qt5-base zlib)
makedepends=(alsa-lib cmake dssi fftw jack ladspa liblrdf liblo
libsamplerate libsndfile qt5-tools)
checkdepends=(xorg-server-xvfb)
optdepends=('cups: Printing support'
            'evince: Print preview support'
            'lilypond: Notation display support'
            'mupdf: Print preview support'
            'okular: Print preview support'
            'timidity++: Softsynth support'
            'flac: FLAC support'
            'wavpack: Lossless audio codec support')
provides=(dssi-host ladspa-host)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('d11756f57e4f364993af67e4aa10c25ef6a7f5fd4baddd127df50c84c29d31aa1e4a42485c8503e9fca31a8cf8e50c3c5b3bad2a08ac945a3fc8cd1a5874d4be')
b2sums=('d1f3bbd173e1d082c5380be2fe0bd9818a741498add5edc9b24c47b9e37e24b928869c00734bf84dd9c2cb9a79fed15275de35a8e7cd4cbbd8ff6cdcb739c09d')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="${PWD}/test-tmp"
  xvfb-run make VERBOSE=1 test -C build || echo "TestNotationViewSelection still fails: https://sourceforge.net/p/rosegarden/bugs/1585/"
}

package() {
  depends+=(libasound.so libfftw3f.so libjack.so liblrdf.so liblo.so
  libsamplerate.so libsndfile.so)

  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,CHANGELOG,README} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
