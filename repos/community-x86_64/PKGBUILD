# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=uget
pkgver=2.2.2
pkgrel=2
pkgdesc="GTK+ download manager featuring download classification and HTML import"
arch=('x86_64')
url="https://ugetdm.com/"
license=('LGPL')
depends=('libnotify' 'gtk3' 'gstreamer' 'curl')
makedepends=('intltool')
optdepends=('aria2: alternative backend')
source=(https://downloads.sourceforge.net/urlget/$pkgname-$pkgver.tar.gz
        uget-gtk.appdata.xml)
sha256sums=('5cf7f311ef59bd02b71e0ce750dd37a0299ef15f9f6c6e7e60ffd175409abfc2'
            'd6c341ef4c21b1312586e2342725be172507d128246a9ad88b2e8ef142140a5a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../uget-gtk.appdata.xml "$pkgdir/usr/share/metainfo/uget-gtk.appdata.xml"
}
