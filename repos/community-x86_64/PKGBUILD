# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BChoppr
pkgname=bchoppr
pkgver=1.10.10
pkgrel=1
pkgdesc="An audio stream chopping LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BChoppr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('6c4f06579125a69e264ec4d4e6c50f7868e9fb66125e9e79c7b9e636a3026e62a9d02a430e35fde9323e07f354876699b93d883c2c9f0f8080635aa08d353a46')
b2sums=('b4f76598a3be321098daca3f103f914770f6636baf81d437fef8f84f5026b393e58521edca458f2af5e5f9a378ed0e9c84db94a799f1d6bb937662981a6d1eb9')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX='/usr'
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${PWD}/${_name}.lv2/" "https://www.jahnichen.de/plugins/lv2/${_name}"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
