# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.0
pkgrel=2
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('dbus-c++' 'jack' 'libavc1394' 'libconfig' 'libiec61883' 'libxml++' 'python2')
makedepends=('python2-pyqt4' 'scons')
optdepends=('python2-pyqt4: ffado-mixer')
provides=('ffado')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        'python2.patch'
)
sha512sums=('7df37584925cc908cc284dba344609928d39e006f71ae4ede62e4531b2628271ebd0db00407418e943fb569b5b8b919a76c90aaeed03005cefbd3c1c16ee1627'
            '01365c0cd834692e21ba480f596b1eceed619a1b1ef8d77425c96aaf83b3343efa1da681a28c8f99152b5fa18b06370b03a4fc513f0bef822ace3ad35b9d1bb6')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/python2.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  scons PREFIX=/usr \
        DEBUG=no \
        MANDIR=/usr/share/man \
        UDEVDIR=/usr/lib/udev/rules.d
}

package() {
  cd "${pkgname}-${pkgver}"

  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/pixmaps/ffado.png"
}

# vim:set ts=2 sw=2 et:
