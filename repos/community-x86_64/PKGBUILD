# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>

pkgname=lxcfs
pkgver=2.0.0
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(i686 x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'cgmanager>=0.35-1' 'libnih' 'fuse' 'dbus-glib')
makedepends=('help2man')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"
	"lxcfs.service")
md5sums=('a8779fce6bcd4e3e4af6204d5e8ceacb'
         '6b90ba5680fffc0229599b8d0e4489b2')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-pamdir=/usr/lib/security \
        --localstatedir=/var
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -dm0755 $pkgdir/var/lib/lxcfs
    install -Dm0644 $srcdir/lxcfs.service $pkgdir/usr/lib/systemd/system/lxcfs.service
}
