# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=libdom
pkgver=0.1.1
pkgrel=1
pkgdesc='Implementation of the W3C DOM'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libdom/'
license=('MIT')
depends=('libhubbub>=0.3.0' 'libwapcaplet>=0.2.1' 'expat')
makedepends=('netsurf-buildsystem' 'git')
source=("git://git.netsurf-browser.org/libdom.git#tag=release/$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"
  
  export CFLAGS="-Wno-error=unused-but-set-variable"
  make LIBDIR=lib PREFIX=/usr COMPONENT_TYPE="lib-shared"
}

package() {
  cd "$pkgname"
  
  make install LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
