# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.9.15
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
depends=('cups' 'deepin-qt5integration' 'smbclient')
makedepends=('gtest' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/dde-printer/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('98fd0a3581f1a5a9b778d569e3ffb75556baadae3aa8c02ab6b013cd447178727721884576a4e33723ce7ea87830503b3aa3a0e53e3169780e5cc570f173bde0')

prepare() {
  cd dde-printer-$pkgver
  sed -i '$aOnlyShowIn=Deepin' src/Printer/platform/linux/dde-printer.desktop

  # https://github.com/linuxdeepin/dde-printer/issues/4
  sed -i '/#include <strings.h>/a #include <stdexcept>' src/cppcups/cupssnmp.cpp
}

build() {
  cd dde-printer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
