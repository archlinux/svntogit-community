# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=4.228
pkgrel=1
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/projects/verilator/wiki/Intro'
arch=('x86_64')
license=('LGPL')
depends=('perl')
optdepends=('systemc')
# lsb-release is used by configure script to enable usage of c++17
makedepends=('python' 'systemc' 'lsb-release')
source=("https://github.com/verilator/verilator/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8d8c5382c03f3a390a5ce5e60846c69f19bc5b1b0861951fe7633de3378e4affc1b15b1a0f2729c0b0cc8c4c9a8b0e9115a95bc455c0d47b333091afbf06eced')

prepare() {
  cd verilator-$pkgver
  autoconf
}

build() {
  cd verilator-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator-$pkgver
  make test
}

package() {
  cd verilator-$pkgver
  make install DESTDIR="$pkgdir"
}
