# Maintainer: Ainola <ainola@archlinux.org>
# Contributor: Drew DeVault

pkgname=scdoc
pkgver=1.11.1
pkgrel=2
license=('MIT')
pkgdesc='Tool for generating roff manual pages'
arch=('x86_64')
url='https://git.sr.ht/~sircmpwn/scdoc/'
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~sircmpwn/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('cd6ac88fc9db48efaae0df0f6472a18e28f92fc2735fee18c07516fd358e9c0921eae9d944020eacd08285bbf59fbcc1d389a6565cef29fc50ad511ee5e7fa9f')

build() {
    cd "$pkgname-$pkgver"
    # Upstream purposefully does not honor the user's LDFLAGS in order to keep
    # scdoc static. As we gain no benefit to statically linking and, in fact,
    # lose standard Arch practices (such as RELRO and PIE), we must override
    # LDFLAGS
    make PREFIX=/usr LDFLAGS="$LDFLAGS"
}

check() {
    cd "$pkgname-$pkgver"
    make check
}

package() {
    cd "$pkgname-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
