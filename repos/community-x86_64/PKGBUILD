# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: Sebastian Wiesner <lunaryorn googlemail com>
# Contributor: Benjamin Andresen <benny(at)klapmuetz(dot)org>
# Contributor: scj <scj archlinux us>

pkgname=ocaml-findlib
pkgver=1.3.3
pkgrel=1
license=('MIT')
arch=('i686' 'x86_64')
pkgdesc='Objective Caml (OCaml) package manager'
url='http://projects.camlcity.org/projects/findlib.html'
depends=('ocaml')
source=("http://download.camlcity.org/download/findlib-$pkgver.tar.gz")
options=('!strip' 'zipman') # otherwise the bytecode gets broken
md5sums=('a4c22ad5e0d38367a73cf58a25fcbebd')

build() {
  cd "$srcdir/findlib-$pkgver"

  ./configure -config /etc/findlib.conf -sitelib /usr/lib/ocaml -mandir /usr/share/man
  make all opt
}

package () {
  cd "$srcdir/findlib-$pkgver"

  make prefix="$pkgdir" install 

  # add the old site-lib to the path to maintain compatibility with old style packages
  sed -i 's/path=\"\/usr\/lib\/ocaml\"/path="\/usr\/lib\/ocaml:\/usr\/lib\/ocaml\/site-lib"/' \
    "${pkgdir}/etc/findlib.conf"

  install -m755 src/findlib/ocamlfind_opt "$pkgdir/usr/bin/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
