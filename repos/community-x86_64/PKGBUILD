# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tasty-smallcheck
pkgname=haskell-tasty-smallcheck
pkgver=0.8.1
pkgrel=60
pkgdesc="SmallCheck support for the Tasty test framework"
url="http://documentup.com/feuerbach/tasty"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-tasty' 'haskell-smallcheck' 'haskell-tagged')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('26ed0d75559c2da2f4296af6e9b02f8d0a19043b0ef154674eb782703fcd3ad7f4863b7b07dbaae27440078b4b00a9272cd9d5da23089993018e933577be5893')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
