# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=marco
pkgver=1.24.2
pkgrel=1
pkgdesc="A window manager for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libcanberra' 'libgtop' 'mate-desktop' 'zenity' 'libxpresent' 'gettext')
makedepends=('itstool')
groups=('mate')
conflicts=('marco-gtk3')
replaces=('marco-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('498350cb12fc4d9c7c4a0355102c17b50043fbab258d11ad41b7d698b4f042a7')

build() {
	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--enable-startup-notification

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
