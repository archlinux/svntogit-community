# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=reptyr
pkgver=0.7.0
pkgrel=2
pkgdesc='Utility for taking an existing running program and attaching it to a new terminal'
arch=('x86_64')
license=('BSD')
url="https://github.com/nelhage/reptyr"
depends=('glibc')
source=($pkgname-$pkgver.tar.gz::https://github.com/nelhage/$pkgname/tarball/$pkgname-$pkgver)
sha256sums=('9de9f1c79519abe0133effd25afcb6df295c3327fc1e4a3003c7cb076a2c9d47')

prepare() {
  cd "${srcdir}"/nelhage-$pkgname-*

  sed -i 's|etc/bash_completion.d|usr/share/bash-completion/completions|' Makefile
}

build() {
  cd "${srcdir}"/nelhage-$pkgname-*

  make
}

package() {
  cd "${srcdir}"/nelhage-$pkgname-*

  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/reptyr/LICENSE
}
