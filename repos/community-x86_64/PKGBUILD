# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cantonese
pkgver=0.0.0.20220615
_commit=5308836db66fd4bee7e5901ddc81fdbfb7390218
pkgrel=1
pkgdesc="Cantonese input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-cantonese"
license=('CCPL:cc-by')
# dependency for reverse lookup
depends=('rime-cangjie' 'rime-emoji' 'rime-loengfan' 'rime-luna-pinyin' 'rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cantonese/archive/$_commit/rime-cantonese-$_commit.tar.gz")
sha512sums=('49c76adfc1d6e6ca8f9082cd9c3cb4bd5a5c3d6368d0fa0fd063f385333abc3c1cd34ed286c23d4c860307917075813299c00ea872c6e5712c35ba9df2ec6d74')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
  install -Dm644 opencc/* -t "$pkgdir"/usr/share/rime-data/opencc/
}
