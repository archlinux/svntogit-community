# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgname=kid3
pkgver=2.1
pkgrel=4
pkgdesc="An MP3, Ogg/Vorbis and FLAC tag editor for KDE"
arch=('i686' 'x86_64')
url="http://kid3.sourceforge.net/"
license=('GPL')
depends=('chromaprint' 'id3lib' 'taglib' 'kdebase-runtime')
makedepends=('automoc4' 'cmake' 'docbook-xml' 'docbook-xsl')
options=('!makeflags')
install=$pkgname.install
changelog=$pkgname.changelog
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'ffmpeg-0.11.diff')
sha256sums=('8925bde1a773d767ee9695229e65dfd64dc13edf850294560478f2e02ecc6225'
            '76ab3c119817a0e1f26e368afb757488c3865ff634771d4d176e729492991a54')

build() {
  cd ${pkgname}-${pkgver}

  # fix error building with latest ffmpeg
  # http://kid3.git.sourceforge.net/git/gitweb.cgi?p=kid3/kid3;a=commit;h=2a91bff2810281e58948f478e9396c73e5c6154a
  patch -Np1 -i ${srcdir}/ffmpeg-0.11.diff

  cd ${srcdir}

  mkdir build
  cd build

  cmake \
    ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
