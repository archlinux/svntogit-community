# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=2.90.2
_commit=594ba35c81ab24e1dbf241d44205014599cfe4bd
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gtk2' 'gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'deepin-file-manager'
         'startdde' 'deepin-menu' 'deepin-daemon')
makedepends=('git')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("git+https://github.com/linuxdeepin/dde-launcher.git#commit=$_commit")
sha256sums=('SKIP')

build(){
  cd dde-launcher
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-launcher
  make INSTALL_ROOT="${pkgdir}" install
}
