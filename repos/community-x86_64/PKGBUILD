# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=3.1.1
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('x86_64')
url="https://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=("https://xdebug.org/files/$pkgname-${pkgver}.tgz"
	'xdebug.ini')
sha256sums=('9be3ae0fdb4dc4a4c68084626cddc56f12396487e309a8c8dd318f0f900d1a68'
            '7c66883dc2ade69069ef84e30188b25630748aa9c8b0dd123727c00505421205')

build() {
  cd "$srcdir"/$pkgname-${pkgver}
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make INSTALL_ROOT="$pkgdir" install
  install -D -m 644 "$srcdir"/xdebug.ini "$pkgdir"/etc/php/conf.d/xdebug.ini
}
