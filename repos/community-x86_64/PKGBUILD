# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat
pkgver=0.16.0
pkgrel=1
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=(x86_64)
url='https://github.com/sharkdp/bat'
license=(APACHE MIT)
makedepends=(clang cmake git rust)
source=("git+$url#commit=6258dda0f851256c2e1d65cf87e997023a4f997b") # tag: v0.16.0
sha256sums=('SKIP')

build() {
  cargo build \
    --manifest-path "$srcdir/$pkgname/Cargo.toml" \
    --release
}

package() {
  install -Dm755 $pkgname/target/release/$pkgname "$pkgdir/usr/bin/$pkgname"

  # Package licenses
  install -Dm644 $pkgname/LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 $pkgname/LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  # Package the man page
  find $pkgname/target/release -name bat.1 -type f \
    -exec install -Dm644 {} "$pkgdir/usr/share/man/man1/bat.1" \;

  # Package the zsh completion file (not in zsh-completions yet)
  find $pkgname/target/release -name bat.zsh -type f \
    -exec install -Dm644 {} "$pkgdir/usr/share/zsh/site-functions/_bat" \;

  # The fish completion file is already included in fish
}
