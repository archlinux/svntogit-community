# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kernelshark
pkgver=1.3
pkgrel=1
pkgdesc="GUI frontend for trace-cmd based Linux kernel Ftrace captures"
arch=('x86_64')
url="https://kernelshark.org/"
license=('GPL2')
depends=('freeglut' 'gcc-libs' 'glibc' 'glu' 'libglvnd' 'qt5-base' 'trace-cmd')
makedepends=('cmake' 'json-c')
provides=('libkshark.so' 'libkshark-plot.so' 'libkshark-gui.so')
source=("https://git.kernel.org/pub/scm/utils/trace-cmd/trace-cmd.git/snapshot/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('a1f646ef4f250af7fc285c042f364eca0cdd7e6aa7cde111d1428c33e5af58181e2308a543e111614163201c89112b67ffcf9f305e3d2d81cf4398efe8530c1d')
b2sums=('62291ee9ff2eb0070414d4604703aa6ee1f9140b720d4f1df45f937d03222f92ab79b63b58f25b8fb7787572ac77f5ab0cf951f6b44a5b478cf97a3bd0f5f0fd')

prepare() {
  mv -v "${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make prefix='/usr' V=1
  cd "kernel-shark"
  cmake -D_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libjson-c.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" prefix='/usr' VERBOSE=1 -C kernel-shark/build install
}
