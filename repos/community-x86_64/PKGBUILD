# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.22.0
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="http://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses" "glibc-static")
source=($url/downloads/$pkgname-$pkgver.tar.bz2
	config)
install=busybox.install
md5sums=('ac1881d1cdeb0729b22c663feaf1c663'
         'e7c697e37b2120f0e0a354c48180cde4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cp $srcdir/config .config
  sed '1,1i#include <sys/resource.h>' -i include/libbb.h
  # if you want to run menuconfig uncomment the following line:
#  make menuconfig ; return 1
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 busybox $pkgdir/usr/bin/busybox
}
