# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.4.1
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=(
  'sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib' 'lz4' 'xxhash'
  'libxxhash.so' 'file' 'libuv' 'tree-sitter' 'libtree-sitter.so'
  'libzip.so' 'libmagic.so')
makedepends=('meson' 'ninja' 'python' 'python-yaml')
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('565358e11fd4db44ec7f8c25313a1cfc726c38a17afcb699c3fc2af520242fb343ea4a267f0a8e35bdc08e9a765a6a17ec7b8eaba9f9ad9ef5a7dcfe01c7354c')
b2sums=('dcae21320e2533904d9a337d4fb9cc9a450439753106dc5c341e3ca7c788f0a44034340eb0dfe3fad4f1d8a7ebe873720d42d70ce343e8c7b953d4d148a38da2')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v4 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_libuv=true \
    -D local=disabled
  ninja -C build
}

package() {
  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
}

# vim: ts=2 sw=2 et:
