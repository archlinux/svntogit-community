# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=libtracefs
pkgname=(libtracefs libtracefs-docs)
pkgver=1.4.1
pkgrel=1
pkgdesc="Linux kernel trace file system library"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/about/"
license=(GPL2 LGPL2.1)
makedepends=(asciidoc libtraceevent xmlto)
provides=(libtracefs.so)
options=(debug)
source=(
  $pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/snapshot/$pkgname-$pkgver.tar.gz
  $pkgname-1.2.0-docs_makefile.patch
)
sha512sums=('b1e11d7e55d6646daba9eb9eaca5150672a41faa8df2f716dad989b7848ffbd52180b3f3e6df5e80f385f94c55ae2b474b153f60f27ba8eb67e44d125bdecdee'
            'bdfffba9957d889c60baea9c197709bf1a62d30ac4b8fd47d930b8d5b67fa040817b9a835e226dd1201f2ccdb7fcb43a12093c2b559fb6c0edc3e9ba167062c2')
b2sums=('a642018c3c2a2915c70d0795f4ee771776f5160368034108624dbe6d23e8aada31f9edbc730260f029a35e2518fbb50f3a7349f899bd188ada32a5eb64db2816'
        '1340af2c65dc344912715c69827d5849fda83e4bcf136da3ccdbee4e3b508e9b557af0828914dff8d27abebbdbdab975d8800432d5252b6b1eb0da08b0cb4b02')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -d $pkgname-$pkgver -Np 1 -i ../$pkgname-1.2.0-docs_makefile.patch
}

build() {
  make LDFLAGS="$LDFLAGS" -C $pkgname-$pkgver
  make doc -C $pkgname-$pkgver
}

package_libtracefs() {
  depends+=(glibc libtraceevent.so)
  optdepends=('libtracefs-docs: for documentation')

  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/Documentation

  (
    cd "$pkgdir"
    _pick libtracefs-docs usr/share/doc
  )
}

package_libtracefs-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}
