# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=libvolk
pkgver=1.2.2
pkgrel=1
pkgdesc="The Vector-Optimized Library of Kernels from Gnuradio"
arch=('i686' 'x86_64')
url="http://libvolk.org/"
license=('GPL3')
depends=('gcc-libs' 'boost-libs' 'orc' 'python2')
makedepends=('boost' 'cmake' 'python2-cheetah')
source=("http://libvolk.org/releases/volk-$pkgver.tar.gz")
md5sums=('540e2c55e525c04920e0e63d0de530b3')

# doxygen for docs

prepare() {
  cd "$srcdir/volk-$pkgver"
  sed -i -e "s|#![ ]*/usr/bin/env python$|&2|" $(find ./ -name '*.py')
}

build() {
  export PYTHON=python2
  cd "$srcdir/volk-$pkgver"

  mkdir -p build
  cd build
  cmake \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev ../
  make
}

check() {
  cd "$srcdir/volk-$pkgver/build"
  export PYTHON=python2
  if [[ "$CARCH" == "x86_64" ]]; then
    make test
  fi
}

package() {
  cd "$srcdir/volk-$pkgver/build"
  make DESTDIR="$pkgdir" install
}
