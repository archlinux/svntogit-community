# $Id: PKGBUILD 70305 2012-05-04 08:21:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-gevent
pkgname=(python-gevent python2-gevent)
pkgver=1.3.6
pkgrel=1
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
makedepends=('python-greenlet' 'python2-greenlet' 'cython' 'cython2')
checkdepends=('lsof' 'python-perf' 'python2-perf' 'python2-virtualenv' 'python-objgraph'
              'python2-objgraph' 'python-zope-interface' 'python2-zope-interface' 'python-dnspython'
              'python2-dnspython' 'python-requests' 'python2-requests' 'python-zope-event'
              'python2-zope-event' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('bbb05a43051630d7a9db7edfbc52faa5ea003f4d5f0782eefd99b5dcb88e4a1050d209320161a7eff1eba7eca8dc4b6a847cc794c7aa3e25095ea5d4ae99e94f')

prepare() {
  sed -e 's/C.UTF-8/en_US.UTF-8/' -e '/@which/d' -i gevent-$pkgver/Makefile

  cp -a gevent-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/gevent-$pkgver

  EMBED=1 \
  CYTHON=cython \
  PYTHON=python \
  python setup.py build

  cd "$srcdir"/gevent-$pkgver-py2

  EMBED=1 \
  CYTHON=cython2 \
  PYTHON=python2 \
  python2 setup.py build
}

check() {
  (
    cd "$srcdir"/gevent-$pkgver
    pyvenv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py install
    make bench alltest
  ) || warning "Tests failed"

  (
    cd "$srcdir"/gevent-$pkgver-py2
    virtualenv2 "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python2.7/site-packages"
    python setup.py install
    PYTHON=python2 make bench alltest
  ) || warning "Tests failed"
}

package_python-gevent() {
  depends=('python-greenlet')

  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gevent() {
  depends=('python2-greenlet')

  cd gevent-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
