# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=mairix
pkgver=0.24
pkgrel=1
pkgdesc="A program for indexing and searching emails"
arch=('x86_64')
url="http://www.rpcurnow.force9.co.uk/mairix/"
license=('GPL')
depends=(bzip2 zlib)
makedepends=('flex' 'bison')
install=$pkgname.install
source=(https://downloads.sf.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('a0702e079c768b6fbe25687ebcbabe7965eb493d269a105998c7c1c2caef4a57')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/share/
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/
  install -D -m 644 dotmairixrc.eg "$pkgdir"/etc/mairixrc.sample
}
