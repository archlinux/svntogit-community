# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-i18nmessageid
pkgname=('python-zope-i18nmessageid' 'python2-zope-i18nmessageid')
pkgver=4.1.0
pkgrel=2
pkgdesc="Message Identifiers for internationalization"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('e5c603b377e60fc1fc33b8fa1e40ccb0e260e1b69f0302e1f12bd251647c581a09e60c16b4d617a5495164367e431fa2acad337cc4367ca7648cc01930945c8a')

prepare() {
  cp -a zope.i18nmessageid-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py build

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.7:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-i18nmessageid() {
  depends=('python-setuptools')

  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-i18nmessageid() {
  depends=('python2-setuptools')

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
