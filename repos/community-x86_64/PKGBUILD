# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fstrm
pkgver=0.4.0
pkgrel=1
pkgdesc="A C implementation of the Frame Streams data transport protocol"
arch=('x86_64')
url="https://github.com/farsightsec/fstrm"
license=('Apache')
depends=('libevent')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('2c84ff059d5850078d3d85927fd2e8f8a1cbe76bcf91cb125cfbbbd89ec5afc0')

build() {
	cd ${pkgname}-${pkgver}
    autoreconf -vfi
	./configure --prefix=/usr
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install

	install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

