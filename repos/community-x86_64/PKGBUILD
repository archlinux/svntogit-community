# Maintainer: Baptiste Jonglez
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=2.1.2
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=("i686" "x86_64")
url='http://msgpack.org/'
license=("Apache")
depends=("glibc")
checkdepends=("gtest")
makedepends=("cmake")
source=("https://github.com/msgpack/msgpack-c/releases/download/cpp-${pkgver}/msgpack-${pkgver}.tar.gz"
        "test-cflags.patch")
sha256sums=('4f855ac251e927a478aa69e4d3087ec2d5eb62e034e3a7897c1d5d2df97b7863'
            '0e6bd401c3ff6f6c68e8beca682b1ecb1b125092a1b1739757294c3c040ba481')

prepare() {
  cd "msgpack-${pkgver}"
  # Remove -Werror when building tests (useless and makes build fail)
  patch -p0 < ../test-cflags.patch
}

build() {
  cd "msgpack-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DMSGPACK_CXX11=ON \
        -DMSGPACK_BUILD_EXAMPLES=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        .
  make
}

check() {
  cd "msgpack-${pkgver}"
  make test 
}

package() {
  cd "msgpack-${pkgver}"
  make DESTDIR="$pkgdir" install
}
