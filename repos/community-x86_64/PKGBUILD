# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sebastian Faltoni <sebastian.faltoni@gmail.com>

pkgname=dbmail
pkgver=3.0.3pre
pkgrel=1
pkgdesc="Fast and scalable sql based mail services"
arch=('i686' 'x86_64')
depends=('gmime24' 'libzdb' 'mhash' 'libevent')
makedepends=('asciidoc' 'xmlto' 'docbook-xsl' 'docbook-xml' 'postgresql-libs>=8.4.1'
	     'sqlite' 'libmysqlclient' 'libldap>=2.4.18' 'libsieve'
	     'git')
optdepends=('libldap: for LDAP authentication'
	    'libsieve: for dbmail-sieve')
url="http://www.dbmail.org"
license=('GPL')
options=('!libtool' 'zipman')
backup=(etc/xinetd.d/dbmail-imapd
	etc/xinetd.d/dbmail-pop3d
	etc/xinetd.d/dbmail-lmtpd
	etc/xinetd.d/dbmail-timsieved)
#source=(http://www.dbmail.org/download/3.0/dbmail-${pkgver/_/-}.tar.gz
source=("$pkgname-$pkgver::git://git.subdir.eu/paul/dbmail#branch=dbmail_3_0"
	dbmail.tmpfiles
	dbmail-imapd.service
	dbmail-lmtpd.service
	dbmail-pop3d.service
	dbmail-timsieved.service
	dbmail-imapd.xinetd
	dbmail-lmtpd.xinetd
	dbmail-pop3d.xinetd
	dbmail-timsieved.xinetd)
md5sums=('SKIP'
         'c4b5793c5422b62a675d4c66ff7e9300'
         '5a6297cb03c8d0b424f978ea1d7402de'
         '070db88538af9833f003f4cb516d337b'
         '422f0399f97a780b7cab84443e8f429a'
         '15c7d367d4242aebac5f87649a2250aa'
         '890de13361afbdf4fed12d6d7eb53e66'
         '961593658cd596297d03d25eb9c9e98f'
         '4cb764894abd3914802e90602bf90a0c'
         'e78dc86355f9aaf24590bc7c6611162f')

build() {
  cd $srcdir/dbmail-${pkgver/_/-}/
  [ -f Makefile ] || ./configure --prefix=/usr --sbindir=/usr/bin \
    --with-ldap --with-sieve
  make
}

package() {
  cd $srcdir/dbmail-${pkgver/_/-}/
  make DESTDIR=$pkgdir install
  (cd man && make && make install DESTDIR=$pkgdir)

  mkdir $pkgdir/etc
  install -Dm644 dbmail.conf $pkgdir/etc/dbmail.conf.sample
  mkdir $pkgdir/usr/share/dbmail
  cp -r sql/* $pkgdir/usr/share/dbmail/
  cp dbmail.schema $pkgdir/usr/share/dbmail/

  for i in dbmail-imapd dbmail-lmtpd dbmail-pop3d dbmail-timsieved; do
    install -Dm0644 $srcdir/$i.service $pkgdir/usr/lib/systemd/system/$i.service
    install -Dm0644 $srcdir/$i.xinetd $pkgdir/etc/xinetd.d/$i
  done

  install -Dm0644 $srcdir/dbmail.tmpfiles $pkgdir/usr/lib/tmpfiles.d/dbmail.conf
}
