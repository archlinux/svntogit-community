# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-scrypt
pkgver=0.8.18
pkgrel=3
pkgdesc='Bindings for the scrypt key derivation function library'
url='https://github.com/holgern/py-scrypt'
arch=('x86_64')
license=('BSD')
depends=('python' 'glibc' 'openssl')
makedepends=('python-setuptools')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0f64ab5f279b26b7c5d8135e891834efc2abe112d5d514cc98e6a743ff4b44e9cb60cdc39ae344fd2db4c83e09a50c3fd253ecd9b34ddf2b9a4bbacc149e715e')
b2sums=('e69d83a6307b63f00757449bea2a82329c2bd0c1356e6dbdf1dae2d2f9bc335db10cdf09e34a8527c8472912e8450e0811ae1911d2b023dd4e5a4cc4ec17c9f5')

build() {
  cd py-scrypt-${pkgver}
  python setup.py build
}

check() {
  cd py-scrypt-${pkgver}
  python setup.py test
}

package() {
  cd py-scrypt-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  rm -r "${pkgdir}"/usr/lib/python*/site-packages/scrypt/tests
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
