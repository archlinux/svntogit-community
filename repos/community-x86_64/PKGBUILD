# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.13.1
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=(x86_64)
url="https://amsynth.github.io/"
license=(GPL2)
groups=(dssi-plugins lv2-plugins pro-audio vst-plugins)
depends=(cairo gcc-libs glibc)
makedepends=(
  atk
  autoconf-archive
  dssi
  fontconfig
  freetype2
  gdk-pixbuf2
  glib2
  gtk2
  harfbuzz
  intltool
  jack
  ladspa
  liblo
  lv2
  pandoc
)
checkdepends=(lv2lint)
optdepends=(
  'alsa-lib: for standalone application'
  'dssi-host: for DSSI plugin'
  'jack: for standalone application'
  'liblo: for standalone application and DSSI plugin'
  'new-session-manager: for session management with the standalone application'
  'lv2-host: for LV2 plugin'
  'vst-host: for VST plugin'
)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/amsynth/amsynth/archive/release-$pkgver.tar.gz
)
sha512sums=('5c7b736335cd84afac7deafa662901631b0256923d5d124bee6891504783310e8b139ff8a80dcb6de4edac7e1d1fadce66cae2e745fe79b06014fe589cea9212')
b2sums=('40d8b9395b61b9cbe6566ddb8bb239ab449f7d721e3ad26bce366d81163d8fb16dd09e54bddcd296d5d08a07d0caa90533432853c89548c7d6c82f2a6dff9de9')

prepare() {
  cd $pkgname-release-$pkgver
  autoreconf -fiv
  intltoolize -f
}

build() {
  local configure_options=(
    --prefix=/usr
    --with-alsa
    --with-jack
    --with-dssi
    --with-lv2
    --with-vst
    --with-pandoc
  )

  cd $pkgname-release-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-release-$pkgver
  cp .libs/${pkgname}_lv2*.so data/$pkgname.lv2/
  lv2lint -Mpack -I data/$pkgname.lv2 "http://code.google.com/p/amsynth/amsynth"
}

package() {
  depends+=(
    atk libatk-1.0.so
    fontconfig libfontconfig.so
    freetype2 libfreetype.so
    gdk-pixbuf2 libgdk_pixbuf-2.0.so
    gtk2 libgdk-x11-2.0.so libgtk-x11-2.0.so
    glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
    harfbuzz libharfbuzz.so
    pango libpangocairo-1.0.so libpangoft2-1.0.so libpango-1.0.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-release-$pkgver
  install -vDm 644 $pkgname-release-$pkgver/{AUTHORS,NEWS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
