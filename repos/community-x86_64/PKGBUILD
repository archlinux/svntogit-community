# Maintainer:  Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=wezterm
pkgver=20211205.192649.672c1cc1
_gitcommit=672c1cc135f7d366f5bd2bd79a428b2f8b2282d1
pkgrel=1
pkgdesc="A GPU-accelerated cross-platform terminal emulator and multiplexer"
arch=('x86_64')
url="https://github.com/wez/wezterm"
license=('MIT')
depends=(
  'fontconfig'
  'hicolor-icon-theme'
  'wayland'
  'libx11'
  'libxkbcommon-x11'
  'xcb-util-keysyms'
  'xcb-util-wm'
  'xcb-util-image'
  'openssl'
)
makedepends=('rust' 'cargo' 'cmake' 'git' 'pkgconf' 'python')
source=(
  "${pkgname}::git+$url#commit=$_gitcommit"
  "${pkgname}-freetype2::git+https://github.com/wez/freetype2.git"
  "${pkgname}-zlib::git+https://github.com/madler/zlib.git"
  "${pkgname}-harfbuzz::git+https://github.com/harfbuzz/harfbuzz.git"
  "${pkgname}-libpng::git+https://github.com/glennrp/libpng.git"
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "$pkgname"
  git submodule init

  git config submodule."harfbuzz/harfbuzz".url "${srcdir}/${pkgname}"-harfbuzz
  git config submodule."freetype/libpng".url "${srcdir}/${pkgname}"-libpng
  git config submodule."deps/freetype/zlib".url "${srcdir}/${pkgname}"-zlib
  git config submodule."freetype2".url "${srcdir}/${pkgname}"-freetype2

  git submodule update --init --recursive
  cargo fetch --locked
}

build() {
  cd "$pkgname"
	cargo build --frozen --release
}

check() {
  cd "$pkgname"
  cargo test --frozen -- --skip "e2e::sftp"
}

package() {
	cd "$pkgname"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-gui" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/$pkgname-mux-server" -t "$pkgdir/usr/bin"
  install -Dm 755 "target/release/strip-ansi-escapes" -t "$pkgdir/usr/bin"
  install -Dm 644 "assets/icon/terminal.png" "$pkgdir/usr/share/icons/hicolor/128x128/apps/org.wezfurlong.$pkgname.png"
  install -Dm 644 "assets/$pkgname.desktop" "$pkgdir/usr/share/applications/org.wezfurlong.$pkgname.desktop"
  install -Dm 644 "assets/$pkgname.appdata.xml" "$pkgdir/usr/share/metainfo/org.wezfurlong.$pkgname.appdata.xml"
  install -Dm 644 assets/shell-integration/* -t "$pkgdir/etc/profile.d"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
