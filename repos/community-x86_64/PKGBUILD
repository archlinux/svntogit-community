# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# AUR Maintainer: Gabriel Rauter <rauter.gabriel@gmail.com>

arch=('x86_64')
_pkgname=KDiskMark
_singleapplication_commit=f1e1508
pkgname=kdiskmark
pkgver=3.1.3
pkgrel=1
pkgdesc="HDD and SSD benchmark tool with a very friendly graphical user interface"
url="https://github.com/JonMagon/${_pkgname}"
license=(GPL3)
depends=(qt5-base fio libaio hicolor-icon-theme kauth glibc gcc-libs polkit-qt5)
makedepends=(git cmake extra-cmake-modules qt5-tools)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/JonMagon/${_pkgname}/archive/${pkgver}.tar.gz"
        "git+https://github.com/itay-grudev/SingleApplication.git#commit=${_singleapplication_commit}")
sha256sums=('c929dcd459663c57a6a817c32b861dba4b9c45255b424ace613e325b7317ffa3'
            'SKIP')


prepare() {
    cd "${_pkgname}-${pkgver}"
    rmdir "src/singleapplication"
    mv "$srcdir/SingleApplication" "src/singleapplication"
}

build() {
    cmake -B build -S "${_pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE='Release' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_LIBEXECDIR='lib' \
        -Wno-dev
    make -C build
}

package() {
  DESTDIR="$pkgdir" make -C build install
}
