# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.3.6
pkgrel=1

pkgdesc="User interface library focused on simplicity and minimalism"
url="https://pwmt.org/projects/girara"
arch=('x86_64')
license=('custom')

provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')

depends=('gtk3' 'glib2' 'json-c' 'pango')
makedepends=('meson' 'ninja' 'doxygen' 'check')

source=(https://pwmt.org/projects/girara/download/girara-$pkgver.tar.xz)
source=(girara-$pkgver.tar.gz::https://github.com/pwmt/girara/archive/$pkgver.tar.gz)

sha512sums=('29b73d085669ef0d5cbbcdc8169bfdc2b08bffc9a98ec95ea41c61b2a7b9823e6c8c0f4e8cb543ed517f4b8c3742b164273e2c7c48506b7a60d7f395e73c1391')

build() {
  cd girara-$pkgver
  arch-meson build

  cd build
  ninja
}

check() {
  cd girara-$pkgver/build

  ninja test
}

package() {
  cd girara-$pkgver/build
  DESTDIR="$pkgdir/" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
