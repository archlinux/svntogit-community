# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgbase=python-pymongo
pkgname=('python-pymongo' 'python2-pymongo')
pkgver=3.10.0
pkgrel=1
pkgdesc='Python module for using MongoDB'
arch=('x86_64')
license=('APACHE')
url='https://pypi.python.org/pypi/pymongo/'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python2-gevent' 'python2-monotonic' 'pifpaf')
checkdepends_x86_64=('mongodb')
source=("https://pypi.io/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha512sums=('43fb571af90583eb29bf0f52a0ae12b6499fc4c3913d57655d3b58ba58c2e16c510a86136beaf8fd9c9d43fb498aa562c0816340de6d1e716eb323450100e2db')

prepare() {
  cp -a "pymongo-$pkgver" "pymongo2-$pkgver"
}

build() {
  cd "$srcdir"/pymongo-$pkgver
  python setup.py build

  cd "$srcdir"/pymongo2-$pkgver
  python2 setup.py build
}

check_x86_64() {
  export DB_PORT=29000

  cd "$srcdir"/pymongo-$pkgver
  pifpaf run mongodb python setup.py test

  cd "$srcdir"/pymongo2-$pkgver
  pifpaf run mongodb python2 setup.py test
}

package_python2-pymongo() {
  depends=('python2')
  optdepends=('python2-monotonic: enable support for a monotonic clock')

  cd "pymongo2-$pkgver"
  python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
}

package_python-pymongo() {
  depends=('python')

  cd "pymongo-$pkgver"
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}

# vim:set ts=2 sw=2 et:
