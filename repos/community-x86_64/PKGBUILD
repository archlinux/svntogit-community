# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Wellington <wellingtonwallace@gmail.com>
# Contributor: robertfoster

pkgname=pulseeffects
pkgver=5.0.3
pkgrel=1
pkgdesc='Audio Effects for Pulseaudio Applications over Pipewire'
arch=('x86_64')
url='https://github.com/wwmm/pulseeffects'
license=('GPL3')
depends=('boost-libs' 'calf' 'glibmm' 'gst-plugin-gtk' 'gst-plugin-pipewire'
         'gst-plugins-bad' 'gst-plugins-base' 'gstreamer' 'gtk3'
         'gtkmm3' 'libebur128' 'libsamplerate' 'libsigc++' 'libsndfile' 'lilv'
         'lsp-plugins' 'pipewire' 'pipewire-pulse' 'yelp' 'zita-convolver' 'rnnoise')
makedepends=('appstream-glib' 'boost' 'itstool' 'meson' 'rubberband' 'zam-plugins')
optdepends=('zam-plugins: maximizer'
            'rubberband: pitch shifting')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e9a969b7661e8daca84f5406dafba59a3cbf036f3b8a8893320574ed56465537454d2c854a7053c7f425728342776e9156ebcae8fa2b07bc5bfca04d04356dfd')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson ..

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
