# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gtkspell3
pkgver=3.0.9
pkgrel=3
url="http://gtkspell.sourceforge.net/"
pkgdesc="Provides word-processor-style highlighting and replacement of misspelled words in a GtkTextView widget"
arch=('x86_64')
license=('GPL')
depends=('gtk3' 'enchant')
makedepends=('intltool' 'gobject-introspection' 'vala' 'python2')
source=(https://downloads.sourceforge.net/gtkspell/$pkgname-$pkgver.tar.xz
        enchant-2.patch)
sha256sums=('a4f4a4a2789f7499563e26d96b22d8085222ebe278da47d026b2de782b8b4d26'
            '17355439e8540e02591e398bc8fd2abfce708157cc116bfd93feab214613cbd3')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../enchant-2.patch
  sed -e 's/enchant/enchant-2/g' -i configure.ac
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
