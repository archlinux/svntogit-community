# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gregory Haynes <greg@greghaynes.net>

pkgname=libinfinity
pkgver=0.6.7
pkgrel=2
pkgdesc="An implementation of the Infininote protocol written in GObject-based C"
arch=('i686' 'x86_64')
url="http://gobby.github.io/"
license=('GPL')
depends=('gnutls>=1.7.2' 'gsasl>=0.2.21' 'glib2>=2.16' 'libxml2>=2.0')
makedepends=('gtk-doc' 'pkgconfig' 'gtk2')
optdepends=('avahi: zeroconf support'
            'gtk2: gtk support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gobby/libinfinity/archive/${pkgver}.tar.gz")
md5sums=('eb01f715b12d0698ccbc0b03c0ce079c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  [ -x configure ] || ./autogen.sh
  ./configure --prefix=/usr --localstatedir=/var
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf $pkgdir/var
}
