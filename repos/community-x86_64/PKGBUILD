# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=2.0.2
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=('sqlite' 'postgresql-libs' 'libmariadbclient')
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('efeb4e70cffd154563d5fd8928749b1820ea8c6ea07b504306f7c9ab145c22b3f86f04acfa2c90338179506e4829ec0e2f5456192ef0f19c7b647d523868462f'
            '3700bcaa0b9e19cf1f6f5199b558bc0a877f124d9fce3476fdb7c765ecebac93b893f1eb47e5e3181ed0f0b9a7eb17760f34792712952086c4e7479abbb8074c')
b2sums=('aa1fa24d18dedbd51dcac48f0c285b83b8a737c73b6093590d5a9bced0cfcb6b93a7ae9d16c5813983264b3e97e3e94c60f5ceb1b565a3043b5dc5a8d41d1eaf'
        'efb492f013aac1863ce0afa418973470d8917e5b607b4f25884bfc2401ad7018e820ba181ebd1a90cb979989d3683b737734bf5497b3498cf96fe779f4038107')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
