# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: RedTide <redtid3@gmail.com>

pkgname=sfizz
pkgver=0.5.1
pkgrel=2
pkgdesc="SFZ library and LV2 plugin"
arch=('x86_64')
url="https://github.com/sfztools/sfizz"
license=('BSD')
groups=('lv2-plugins' 'pro-audio' 'vst3-plugins')
# TODO: devendor abseil-cpp
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'xcb-util' 'xcb-util-cursor'
'libxkbcommon' 'libxkbcommon-x11' 'ttf-roboto' 'zenity')
makedepends=('cmake' 'jack' 'freetype2' 'libsndfile' 'lv2' 'xcb-util-keysyms')
checkdepends=('lv2lint')
optdepends=(
  'jack: for sfizz_jack'
  'lv2-host: for the LV2 plugin'
  'vst3-host: for the VST3 plugin'
)
provides=('libsfizz.so' 'soundfont-synthesizer')
source=("https://github.com/sfztools/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz")
sha512sums=('e25072be304775aa7f0173b6cac4284ddc192f7bc40b7ea6335e3cef7d982845aefc67c1f905560c036564a67e4049f81198e1b316cc17aa3df5b9f4095b3c2a')
b2sums=('02125f290eaeb88fbe9eb89b8d49209fc160840c37302c2ce3ea0a0f8e04d956e731747f069bc0767fcfd8b6178ab8dabfa376f1e885f482929cf5028a2c47d4')

prepare() {
  cd "$pkgname-$pkgver"
  # devendor lv2
  rm -rfv lv2/lv2
  sed -e 's|"lv2/core/lv2.h"|<lv2/core/lv2.h>|g' \
      -i lv2/external/ardour/ardour/lv2_extensions.h
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE='Release' \
        -ENABLE_LTO=ON \
        -DSFIZZ_TESTS=ON \
        -DSFIZZ_VST=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  ./build/tests/${pkgname}_tests
  lv2lint -Mpack -I "build/${pkgname}.lv2" "http://sfztools.github.io/sfizz"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # devendor ttf-roboto
  ln -svf /usr/share/fonts/TTF/Roboto-Regular.ttf "${pkgdir}/usr/lib/vst3/${pkgname}.vst3/Contents/Resources/Fonts/"
}
