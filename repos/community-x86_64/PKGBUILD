# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Johannes Pfau <johannespfau at gmail dot com>

pkgname=notify-osd
pkgver=0.9.34
pkgrel=2
pkgdesc="Canonical's on-screen-display notification agent, implementing the freedesktop.org Desktop Notifications Specification with semi-transparent click-through bubbles"
arch=('i686' 'x86_64')
url="https://launchpad.net/notify-osd"
license=('GPL')
depends=('libwnck3' 'dbus-glib' 'dconf' 'gsettings-desktop-schemas')
makedepends=('libnotify')
provides=('notification-daemon')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/precise/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('18d9a95467daac570789fb7893011595')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Remove readme file from non-standard location
  rm "$pkgdir/usr/share/notify-osd/icons/hicolor/scalable/status/README"
}
