# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=2.3.0
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi' 'libxkbcommon')
makedepends=('rust' 'cmake' 'llvm11')
checkdepends=('lld' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/${pkgver}.tar.gz")
sha512sums=('78257162078073000e71b99d0a1d466c7702800d2116f9f4446794dbec960695bb06d4dd2117aed914f06de0d44286b404395003e9908e7b2c9ab20d8793646c')
options=('staticlibs' '!lto')

WASMER_INSTALL_PREFIX=/usr
ENABLE_LLVM=1

build() {
  cd "$pkgname-$pkgver"
  make all
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/usr make install
}

# vim:set ts=2 sw=2 et:
