# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=edit-distance-vector
pkgname=haskell-edit-distance-vector
pkgver=1.0.0.4
pkgrel=286
pkgdesc="Calculate edit distances and edit scripts between vectors"
url="https://github.com/thsutton/edit-distance-vector"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-vector')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-instances')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8d7ef7c6ca971aec5efc2e90adff2584c1e4f996df3ad4f6111f790fd88fcce1814111eb3a3bbe07d8455b03ade98f11e6343c4746e895be2ba9b82a86a33e6d')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
