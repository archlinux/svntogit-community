# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=gigedit
pkgver=0.2.0
pkgrel=3
pkgdesc="Gigasampler instrument editor"
arch=('i686' 'x86_64')
url="http://www.linuxsampler.org/"
license=('GPL')
depends=('linuxsampler' 'gtkmm') # linuxsampler required for plugin
makedepends=('intltool')
options=('!makeflags') # code chokes on parallel build
source=("http://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.gz")
md5sums=('eadfc82118460206e8f83c104f9d2a3a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix linking
  sed -i -e 's/@LIBS@/@LIBS@ -lsigc-2.0/g' src/gigedit/Makefile.in
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
