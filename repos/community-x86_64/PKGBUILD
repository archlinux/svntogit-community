# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-shell
pkgver=5.3.0.39
pkgrel=1
pkgdesc='Deepin desktop-environment - session-shell module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-shell"
license=('GPL3')
# qt5-tools: for qdbus executable
depends=('deepin-daemon' 'deepin-qt5integration' 'deepin-wallpapers' 'gsettings-qt' 'liblightdm-qt5'
         'qt5-tools' 'startdde')
makedepends=('cmake' 'ninja')
conflicts=('deepin-session-ui<5')
provides=('lightdm-deepin-greeter')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-shell/archive/$pkgver.tar.gz"
        $pkgname-wrapper-execution.patch::https://github.com/linuxdeepin/dde-session-shell/commit/5b5aa3aeb9ac8fc378083eeb327eac8f784cba66.patch
        $pkgname-wrapper-typo.patch::https://github.com/linuxdeepin/dde-session-shell/commit/fe0f13fca67c955a0a5c7f0b17178c22bc9a7b51.patch)
sha512sums=('ef5fbb8fba8d1306cea1c2adf4b2d23df2ea78d11c178200e03932ffc52f9652edd3d6929f166fdbec3daa130878d0ae7343f28490a6c3b67466d824ef1a1cb2'
            '78180766be987b9619161571bbea3e598b3f47433a892af2d5a2387ea8321160feef38f0b9450d8bc8c18c0cae515b00e1a07e21f57538f39009abd0d75411b3'
            '482707b46af2165ce44730b394f2d29e7abcb011fff5793cda204e7e7feaaf0c94ec94ceaebed934a206815dea0757520821b60df9b8f2c12dea93b1d047109c')

prepare() {
  cd dde-session-shell-$pkgver
  patch -p1 -i ../$pkgname-wrapper-execution.patch
  patch -p1 -i ../$pkgname-wrapper-typo.patch
  # We don't have common-auth on Arch
  sed -i 's/common-auth/system-login/' src/libdde-auth/authagent.cpp
}

build() {
  cd dde-session-shell-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dde-session-shell-$pkgver
  DESTDIR="$pkgdir" ninja install

  chmod +x "$pkgdir"/usr/bin/deepin-greeter
}
