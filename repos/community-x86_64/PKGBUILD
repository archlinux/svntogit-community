# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=1.1
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('i686' 'x86_64')
url="https://github.com/manateelazycat/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-tool-kit' 'byzanz' 'ffmpeg')
makedepends=('git' 'qt5-tools')
groups=('deepin-extras')
source=("git+https://github.com/manateelazycat/deepin-screen-recorder.git#tag=$pkgver")
sha256sums=('SKIP')

build(){
  cd deepin-screen-recorder
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder
  make INSTALL_ROOT="${pkgdir}" install
}
