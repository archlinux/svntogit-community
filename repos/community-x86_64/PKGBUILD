# $Id: PKGBUILD 371260 2018-08-13 09:13:06Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Cravix <dr.neemous@gmail.com>
# Contributor: AlexanderR <rvacheva@nxt.ru>
# Contributor: zhn <zhangn1985@gmail.com>

pkgbase=renpy
pkgname=(
  renpy
  renpy-demos
)
pkgver=7.4.6
pkgrel=1
pkgdesc="The Ren'Py Visual Novel Engine"
arch=(x86_64)
url=https://www.renpy.org
license=(MIT)
depends=(
  ffmpeg
  glew
  python2-future
  python2-pygame-sdl2
)
makedepends=(
  cython2
  python2-setuptools
)
optdepends=(
  'atom: Default editor'
  'code: Another editor'
)
options=(!emptydirs)
source=(
  https://www.renpy.org/dl/${pkgver}/renpy-${pkgver}-source.tar.bz2
  renpy
  renpy.desktop
  renpy.png
  renpy-system-fribidi.patch
)
b2sums=('fa5c1bac041874ef6f60272f7adfbe4d683b142b30930ce18e7665998aff6c8df01f8e153b5956469e112db60c99e15ba87f86c9b00db6c6532093a8d6b156c9'
        '1b6ebdbca74d03c86a3b90d8c749fbb433d4427bf2f5e38dc976819a61a911015d059995463a3be81661080a28bae8042c24a552c5b5a3ef0efb6acd83df1053'
        'b5bec9d19a25ae849d0e86e483040b320be9048b55ba002cc83d4ee018e99ff7549ba26a60afe6522c2efe2abb724f2679750e25f8d7c161847ad1babeccf006'
        'd0b22e3f970f28096a6ea50019d708a568cfca489acc3c5b1bbce78a0b5afc35d43712b5348ab88b643543ce4f627c31a83eacaab293bede55de2eeabfea2238'
        '1e1c8b1a8d670c7ec1587b13140972d463ba6a442f895e73ba9df45f36c52babe86783180b0d265e4bb93df44ad31ed5c0e9e859d656d253ad086b8fdb784281')

prepare() {
  cd renpy-${pkgver}-source

  sed 's/python/python2/' -i atom/Atom.edit.py launcher/game/tkaskdir.py

  patch -Np1 -i ../renpy-system-fribidi.patch
}

build() {
  cd renpy-${pkgver}-source

  export RENPY_CYTHON=cython2

  python2 module/setup.py build
}

package_renpy() {
  optdepends=(
    'renpy-demos: Tutorial and The Question demos'
    'tk: Set projects directory'
  )

  cd renpy-${pkgver}-source

  python2 module/setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1 --skip-build

  install -dm 755 "${pkgdir}"/usr/share/{renpy,doc}

  cp -dr --no-preserve=ownership atom doc gui launcher renpy renpy.py "${pkgdir}"/usr/share/renpy/
  ln -s /usr/share/renpy/doc "${pkgdir}"/usr/share/doc/renpy

  install -Dm 755 ../renpy -t "${pkgdir}"/usr/bin/
  install -Dm 644 ../renpy.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 ../renpy.png -t "${pkgdir}"/usr/share/pixmaps/

  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/renpy/
}

package_renpy-demos() {
  depends=(renpy)

  cd renpy-${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/share/renpy

  cp -dr --no-preserve=ownership the_question tutorial "${pkgdir}"/usr/share/renpy/

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s renpy "${pkgdir}"/usr/share/licenses/renpy-demos
}

# vim: ts=2 sw=2 et:
