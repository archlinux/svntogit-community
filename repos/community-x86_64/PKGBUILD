# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=libaxc
pkgver=0.3.4
pkgrel=2
pkgdesc='Client lib for libsignal-c aka libaxolotl'
arch=('x86_64')
url='https://github.com/gkdr/axc'
license=('GPL3')
depends=('libsignal-protocol-c' 'sqlite' 'libgcrypt' 'glib2')
provides=('libaxc.so')
source=("https://github.com/gkdr/axc/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('78fc122eefbc5c6173bdad5cf31734cf95b750c09bddede5b2656a88bede3d6c25fb22af5447681c58eb1f296ee326480e22aacb843d951f1bd7a6933155432a')

build() {
  cd ${pkgname##lib}-${pkgver}
  make PREFIX="/usr"
}

package() {
  cd ${pkgname##lib}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  chmod a+x "${pkgdir}"/usr/lib/libaxc.so.${pkgver}
  ln -s libaxc.so.${pkgver} "${pkgdir}"/usr/lib/libaxc.so
  ln -s libaxc.so.${pkgver} "${pkgdir}"/usr/lib/libaxc.so.${pkgver%%.*}
}
