# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=python-pymongo
pkgver=4.0
pkgrel=1
pkgdesc='Python module for using MongoDB'
arch=('x86_64')
license=('Apache')
url='https://pypi.python.org/pypi/pymongo/'
depends=('python')
makedepends=('python-setuptools')
optdepends=('python-pymongocrypt: for [encryption]'
            'python-pyopenssl: for [ocsp]'
            'python-requests: for [ocsp]'
            'python-service-identity: for [ocsp]'
            'python-snappy: for [snappy]'
            'python-zstandard: for [zstd]'
            'python-pymongo-auth-aws: for [aws]'
            'python-dnspython: for [srv]')
source=("https://pypi.io/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha512sums=('457c138a2e6277c87654a20fe9f07ce4803ab3fba619b15132b315bbbcdc7065920373058192e7449065faeeff324ae3b748df75605fc8ab1fad26bc57f7773c')

build() {
  cd pymongo-$pkgver
  python setup.py build
}

package() {
  cd pymongo-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}

# vim:set ts=2 sw=2 et:
