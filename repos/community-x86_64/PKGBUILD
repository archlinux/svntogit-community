# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.24.8
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'libgit2' 'libssh' 'openssl' 'zlib')
makedepends=('cargo' 'pkg-config')
options=('!lto')
source=(https://github.com/crate-ci/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('cd5fe61dba0f70d34af5838b07cb7666d1d70e50c0a823340a5140bd3ebbaaa31a5e14f37f8c1a01b451d72854ee70e708c3551e1ca33312b82e93a466ca6185')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  OPENSSL_NO_VENDOR=1 \
  LIBSSH2_SYS_USE_PKG_CONFIG=1 \
    cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
