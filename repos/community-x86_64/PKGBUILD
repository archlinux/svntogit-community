# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.12.2
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://wiki.gnome.org/Projects/gexiv2'
arch=(x86_64)
license=(GPL2)
depends=(exiv2 glib2)
makedepends=(git gobject-introspection meson python-gobject vala)
source=("git+https://gitlab.gnome.org/GNOME/gexiv2.git?signed#tag=gexiv2-$pkgver")
sha256sums=('SKIP')
validpgpkeys=('AC9CD4E32D7C7F6357BA8ADD10F6E970175D29E1') # Jens Georg <mail@jensge.org>

build() {
  arch-meson gexiv2 build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
