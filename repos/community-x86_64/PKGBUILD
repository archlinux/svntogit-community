# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgname=python-xapian
_name=xapian-bindings
pkgver=1.4.20
pkgrel=1
epoch=1
pkgdesc="Python bindings for Xapian"
arch=('x86_64')
url="https://xapian.org/docs/bindings/python/"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'python' 'zlib')
makedepends=('util-linux-libs' 'python-sphinx' "xapian-core=$epoch:$pkgver")
source=("https://oligarchy.co.uk/xapian/${pkgver}/${_name}-${pkgver}.tar.xz"{,.asc})
sha512sums=('bdc876b8e1b946d27673122de6d298112c82fa41e19090a2bcbe0954905ddf66171d36e6753a1e8d46578b128edd729e2fd252311d60ff40a2aec9a4d57796ed'
            'SKIP')
b2sums=('3ab6a9be536d10b0dd369c1c638acbdd7fdf855f6dc8726db9c3791a8369cd83196214771b923aa538e5dda320d2b417f5f0944181b5691cfe5d8e7ddc97dd96'
        'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${_name}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${_name}-${pkgver}"
  ./configure --prefix=/usr \
              --with-python3
  make
}

check() {
  cd "${_name}-${pkgver}"
  make check
}

package() {
  depends+=('libuuid.so' 'libxapian.so')
  cd "${_name}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${_name}"
}
# vim:set ts=2 sw=2 et:
