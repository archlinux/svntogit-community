# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Birks <david@birks.dev>
# Contributor: Mike Williamson <mike at korora dot ca>

pkgname=eksctl
pkgver=0.124.0
pkgrel=1
pkgdesc='Command line tool for creating clusters on Amazon EKS'
url='https://github.com/weaveworks/eksctl'
arch=('x86_64')
license=('Apache')
depends=('kubectl' 'glibc')
makedepends=('go')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('c966c7ee19384b77d48bea3956fd7364dbce18ec8acd582bad4feb06b32fe463')
b2sums=('27536909087c5910801f7716fe291cffe2926233458571b8bededde5fcf04e803d430cd73a029409042feb69002171635fd44de555b67d39629eea1fdb0966a6')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X github.com/weaveworks/eksctl/pkg/version.gitTag=${pkgver}" \
    ./cmd/eksctl
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "$pkgdir/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  # completions
  "${pkgdir}/usr/bin/${pkgname}" completion bash | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/fish/completions/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
