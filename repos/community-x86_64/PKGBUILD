# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=2.90.7
pkgrel=1
pkgdesc="An enhanced gettext po file editor for the GNOME"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gtranslator/"
license=('GPL')
depends=('desktop-file-utils' 'gdl' 'gettext' 'gtksourceview3' 'gtkspell' 'iso-codes' 'libgda4' 'libpeas' 'libunique' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-utils' 'gucharmap' 'json-glib')
optdepends=('gtkspell: spell check'
            'gucharmap: for charmap plugin'
            'json-glib: for open-tran plugin')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/2.90/$pkgname-$pkgver.tar.bz2)
sha256sums=('55845ad665f5e55265dd017d1c96e41a72c4c5691eb218ba9db6985b66afa556')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
