# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=3.8.0
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
makedepends=('autoconf-archive' 'intltool' 'python' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "0001-Remove-vestigial-manpage.patch::${url}/pull/229.patch")
sha256sums=('f707403c0cc8b558e706b15044aad7ce87321ca688cb22a96626b644a6532239'
            '6c85ec72dd83b1301ba8f01e0de5f8f6001198de3502c87a4da908b12e6cee24')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # https://github.com/linuxmint/cinnamon-settings-daemon/pull/229
    patch -p1 < ../0001-Remove-vestigial-manpage.patch
    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libexecdir="/usr/lib/${pkgname}" \
                --enable-systemd \
                --enable-polkit

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="$pkgdir" install
}
