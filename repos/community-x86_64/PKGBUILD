# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=git-warp-time
pkgver=0.4.8
pkgrel=1
pkgdesc='reset timestamps of Git repository files to the time of the last modifying commit'
arch=(x86_64)
url="https://github.com/alerque/$pkgname"
license=(GPL3)
depends=(libgit2
         libgit2.so)
makedepends=(cargo
             git
             jq)
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.xz")
sha256sums=('14d542118c84506a93d7172d8de2ad17388862c10e11165efd62fc1c0e06eebe')

prepare() {
	cd "$_archive"
	sed Makefile.am -i \
		-e 's/cargo \(build\|install\|test\)/cargo --offline \1/'
	autoreconf
	cargo fetch --locked
}

build() {
	cd "$_archive"
	./configure --prefix /usr
	make
}

check() {
	cd "$_archive"
	make check
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
