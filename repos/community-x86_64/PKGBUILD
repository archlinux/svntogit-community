# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel-indicator-power
pkgver=2.1.4
pkgrel=1
pkgdesc='Wingpanel Power Indicator'
arch=(x86_64)
url=https://github.com/elementary/wingpanel-indicator-power
license=(GPL3)
groups=(pantheon)
depends=(
  bamf
  glib2
  gtk3
  libgee
  libgranite.so
  libgtop
  libudev.so
  libwingpanel-2.0.so
)
makedepends=(
  git
  intltool
  meson
  vala
  wingpanel
)
source=(git+https://github.com/elementary/wingpanel-indicator-power.git#tag=a2a344eabd31e17e27b929cef6bc4432f312c82e)
sha256sums=(SKIP)

pkgver() {
  cd wingpanel-indicator-power

  git describe --tags
}

build() {
  arch-meson wingpanel-indicator-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
