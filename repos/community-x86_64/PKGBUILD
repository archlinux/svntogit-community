# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=flickcurl
pkgver=1.26
pkgrel=2
pkgdesc="C library for the Flickr API"
arch=(i686 x86_64)
url="http://librdf.org/flickcurl/"
license=('GPL')
depends=('raptor' 'curl')
source=(http://download.dajobe.org/flickcurl/flickcurl-$pkgver.tar.gz
	https://github.com/dajobe/flickcurl/commit/a5cc2a5d2fc7074f50fbaa772232b6e0fea7ce89.patch)
md5sums=('7013a36656400dac398748a374c9104f'
         '2d925b978c266f0a9a3d5571eee2d7d0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <$srcdir/a5cc2a5d2fc7074f50fbaa772232b6e0fea7ce89.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|#include <curl/types.h>||' src/flickcurl_internal.h
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
