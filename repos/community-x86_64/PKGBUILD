# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Adrian Sausenthaler <aur@sausenthaler.de>

pkgname=openpgl
pkgver=0.4.1beta
_pkgver=0.4.1-beta
pkgrel=1
pkgdesc="Intel Open Path Guiding Library"
arch=('x86_64')
url="https://github.com/openpathguidinglibrary/openpgl"
license=('Apache')
depends=('embree')
makedepends=('cmake' 'make' 'gcc' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenPathGuidingLibrary/openpgl/archive/refs/tags/v${_pkgver}.tar.gz")
sha512sums=('c6d46ede9484e56830e4e133eefe94f449b42e2d7c0c1eeb17d6ae3441f011ff291d1f1d1ffbf863ce5f416ab788552f0dfde7e17773fe60dc2139b935b29766')

build() {
  cd openpgl-$_pkgver
  cmake \
    -Bbuild \
    -GNinja \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DOPENPGL_BUILD_STATIC=OFF \
	-DCMAKE_BUILD_TYPE=Release

  ninja -C build
}

package() {
  cd openpgl-$_pkgver
  DESTDIR="$pkgdir" ninja -C build install
}
