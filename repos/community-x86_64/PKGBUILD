# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=esqueleto
pkgname=haskell-esqueleto
pkgver=2.4.3
pkgrel=52
pkgdesc="Type-safe EDSL for SQL queries on persistent backends."
url="https://github.com/prowdsponsor/esqueleto"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-blaze-html" "haskell-conduit" "haskell-monad-logger"
         "haskell-persistent" "haskell-resourcet" "haskell-tagged" "haskell-text"
         "haskell-unordered-containers")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        persistent-2.5.patch)
sha256sums=('bf555cfb40519ed1573f7bb90c65f693b9639dfa93fc2222230d3ded6e897434'
            '101534ae4db348978b427a5a5b06a51765a680c4c8ae038ce283214fcd44f9e4')

prepare() {
    # https://github.com/prowdsponsor/esqueleto/pull/139
    (cd $_hkgname-$pkgver && patch -p1 -i ../persistent-2.5.patch)

    sed -e 's/base                 >= 4.5     && < 4.9/base                 >= 4.5     \&\& < 4.10/' \
        -e 's/, persistent           >= 2.5     && < 2.6/, persistent           >= 2.5     \&\& < 2.7/' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-mysql -f-postgresql
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
