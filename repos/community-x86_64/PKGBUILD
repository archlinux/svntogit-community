# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=asyncpg
pkgname=python-$_pkgname
pkgver=0.26.0
pkgrel=1
pkgdesc='A fast PostgreSQL Database Client Library for Python/asyncio'
arch=('x86_64')
url='https://github.com/MagicStack/asyncpg'
license=('Apache')
depends=('python')
makedepends=('git' 'python-setuptools' 'cython')
makedepends=('git' 'python-build' 'python-installer' 'python-wheel' 'cython'
             'python-setuptools' 'python-setuptools-scm' 'python-setuptools-scm-git-archive')
checkdepends=('python-pytest-runner' 'python-uvloop' 'postgresql')
source=("git+$url.git#tag=v$pkgver"
        'git+https://github.com/MagicStack/py-pgproto.git')
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd $_pkgname

  git submodule init
  git config submodule.asyncpg/pgproto.url "$srcdir"/py-pgproto
  git submodule update

  sed -i "s|'Cython==.*'|'Cython'|" setup.py
}

build() {
  cd $_pkgname

  python -m build -nw
}

check() {
  cd $_pkgname

  python -m venv --system-site-packages test
  cd test/bin
  ./python -m installer ../../dist/*.whl

  ./python -m pytest ../../tests
}

package() {
  cd $_pkgname

  python -m installer -d "$pkgdir" dist/*.whl

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
