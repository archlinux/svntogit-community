# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libvips
pkgver=8.12.1
pkgrel=1
pkgdesc="A fast image processing library with low memory needs"
arch=('x86_64')
license=('LGPL')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'imagemagick' 'libexif' 'libgsf' 'libheif' 'libimagequant' 'librsvg'
         'libwebp' 'libxml2' 'openexr' 'orc' 'pango' 'poppler-glib' 'libcgif')
makedepends=('gobject-introspection')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.gz")
sha512sums=('fc7ae2c9212a82bc693fea28f6719b3b2422bf777fc9a6be349075f099804015a2850fca2b6e370881689bf5a1a2ebbf6fc5a01b48dbc48ffb22a11a07169f7c')

build() {
  cd vips-$pkgver
  ./configure --prefix=/usr
# Fix overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd vips-$pkgver
  make DESTDIR="$pkgdir" install
}
