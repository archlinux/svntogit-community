# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: mightyjaym <jmambrosino@gmail.com>
# Contributor: moostik <mooostik_at_gmail.com>

pkgname=audex
pkgver=0.79+84+g25d4ca1
pkgrel=1
pkgdesc="A CDDA extraction tool with a ftp upload function for KDE"
arch=('x86_64')
url="http://kde-apps.org/content/show.php?content=77125"
license=('GPL')
depends=('libkcddb' 'hicolor-icon-theme' 'kcmutils')
makedepends=('extra-cmake-modules' 'git')
optdepends=('vorbis-tools: OGG encoder'
	'faac: MP4/M4A/AAC encoder'
	'flac: FLAC encoder'
	'lame: MP3 encoder')
_commit=25d4ca113e27d2bce16fa4ef36f969fe5f129061  # master
source=("git://git.kde.org/audex.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g' -e 's/v//'
}

build() {
  cd ${pkgname}

  mkdir build
  cd build

  cmake ../  \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${pkgname}/build

  make DESTDIR=${pkgdir} install
}
