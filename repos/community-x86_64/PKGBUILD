# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: <kaptoxic _at_ yahoo _dot_ com >
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=maude
pkgver=3.2.1
pkgrel=1
pkgdesc="High-level Specification Language"
arch=('x86_64')
url="http://maude.cs.illinois.edu"
license=('GPL')
depends=('buddy' 'cvc4' 'libtecla' 'gmp' 'libsigsegv' 'yices')
makedepends=('flex' 'bison')
source=("https://github.com/SRI-CSL/Maude/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "http://maude.cs.illinois.edu/w/images/b/bc/Full-Maude-$pkgver.zip"
        maude.sh)
sha512sums=('7bcde5a51696af887b83fae84e7dead1138543fba13b33addd1d5f890fe3e463b2b5db95f7ab6af168600edb624ce3dbf6a4da05440796a13a4738698c424791'
            '6816538606d91d404a953666e842749375b9d8caa2a45402fae32417b9443335a651052c0dd217cc7ba1f738191bbe8a74d951ddc307b0e59d0eff0efad3fe6f'
            'f62e1709d035e996c9f9a887840492527415f157990cc2d52355d8a7fe9382a757c06bed95f97e298f8eea0dd2a846bf7efb8fb84987784a4a797820807be12e')

prepare() {
  cd Maude-$pkgver

  # Always enabled in CVC4 1.8: https://github.com/CVC4/CVC4/pull/4519
  sed -i '/rewrite-divk/d' src/Mixfix/cvc4_Bindings.cc

  autoreconf -i

  mkdir -p build
}

build() {
  cd Maude-$pkgver/build
  ../configure --with-cvc4=yes --with-yices2=yes --prefix=/usr --datadir=/usr/share/maude
  make -j1 all CVC4_LIB="-lcvc4 -lcln"
}

check() {
  cd Maude-$pkgver/build
  # https://github.com/SRI-CSL/Maude/issues/2
  make check && exit 1 || echo "Tests failed as expected"
}

package() {
  cd Maude-$pkgver/build
  make DESTDIR="$pkgdir/" install
  install -D -m 444 "$srcdir/full-maude.maude" \
    "$pkgdir/usr/share/maude/full-maude.maude"

  install -D -m 555 "$srcdir/maude.sh" "$pkgdir/etc/profile.d/maude.sh"
}
