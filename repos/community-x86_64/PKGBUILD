# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Robert R Miller <robertrmiller@outlook.com>
# Contributor: June Tate-Gans <june@theonelab.com>

pkgname=libfixposix
pkgver=0.4.3
pkgrel=1
pkgdesc='Thin wrapper over POSIX syscalls'
arch=('x86_64')
url='https://github.com/sionescu/libfixposix'
license=('Boost')
depends=('glibc')
makedepends=('git')
_commit='3ca833ea258fdd6c36b5f95207162ec02e763ce1'
source=("libfixposix::git+https://github.com/sionescu/libfixposix#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  autoreconf -fiv
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install
}
