# Maintainer: Campbell Jones <serebit at archlinux dot org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.7.2
pkgrel=1
pkgdesc='A familiar, modern desktop environment'
arch=('x86_64')
url='https://github.com/BuddiesOfBudgie/budgie-desktop'
license=('GPL2' 'LGPL')
groups=('budgie')
depends=('budgie-screensaver' 'budgie-control-center' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard'
         'libgee' 'libpeas' 'libwnck3' 'mutter' 'accountsservice' 'ibus' 'zenity')
makedepends=('gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('budgie-desktop-view: Desktop icons'
            'budgie-backgrounds: Default background set'
            'network-manager-applet: Network management in the panel'
            'switcheroo-control: Open apps with the secondary GPU')
source=("https://github.com/BuddiesOfBudgie/$pkgname/releases/download/v$pkgver/$pkgname-v$pkgver.tar.xz"{,.asc}
        "30_org.archlinux.budgie-desktop.gschema.override")
validpgpkeys=('0E0D97562A4EC8BD8E329DCDAA7A2325E04B609B') # Joshua Strobl <me at joshuastrobl dot com>
sha256sums=('cce9ebe21b4bba4886bb2a227d739b7340c35085f08c132d3d8ae6b6621ba7b4'
            'SKIP'
            '8bf90523787a7acf907adff557e0f578d8516f76c4c63aefe1395250f497324a')

build() {
    arch-meson "$pkgname-$pkgver" build -Duse-old-zenity=true
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"
    install -Dm 644 30_org.archlinux.budgie-desktop.gschema.override -t "${pkgdir}/usr/share/glib-2.0/schemas"
}
