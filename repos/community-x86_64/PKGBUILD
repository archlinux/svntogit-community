# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=filezilla
pkgver=3.62.2
pkgrel=2
pkgdesc='Fast and reliable FTP, FTPS and SFTP client'
url='https://filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=(
  'dbus' 'xdg-utils' 'wxwidgets-gtk3' 'libidn' 'hicolor-icon-theme' 'sqlite'
  'gnutls' 'libfilezilla'
  libfilezilla.so libdbus-1.so libgtk-3.so libgdk-3.so libnettle.so
)
source=(https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2
        wxwidgets-3.2.patch)
sha256sums=('a76709635ca0ea474f691f6c60b191334af3079ef192c07c346504baab738c12'
            'd1b55c763222b625b24e97e2982a0eed5fa16a18909a7a3f86f06b246c2bf3d8')
sha512sums=('a7dec07f6b6fe6fe1a54481e67e9ad2cb057bf84db6e6128f5e853f2edac3c34583a84f25c206f382f8bac6fa4ab48ea54a2a2f7efe8cc6c9d18cb1d3eba0255'
            '0f92bd0180830fdb54cfb2eff2b6ebb3413aaace2b901300ca2f192b5abffea28f511eb26dea4eb8fb2ff8b951d15b4c39bef77cad59542b9f3632bbddd79183')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../wxwidgets-3.2.patch # Allow building with wxWidgets 3.2
  autoreconf -vif
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
