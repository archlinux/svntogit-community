# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: gilbus

pkgname=swayidle
pkgver=1.7
pkgrel=2
license=('MIT')
pkgdesc="Idle management daemon for Wayland"
makedepends=(
    'meson'
    'scdoc'
    'wayland-protocols'
)
depends=(
    'wayland'
    'systemd'
)
arch=('x86_64')
url="https://github.com/swaywm/swayidle"
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/swaywm/swayidle/archive/$pkgver.tar.gz"
    "https://github.com/swaywm/swayidle/releases/download/$pkgver/$pkgname-$pkgver.tar.gz.sig"
)
sha256sums=('06062dd07d4ef625bb2420ab5b4de395b0e12e71c631a516bfa972e1c3acb503'
            'SKIP')
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48'  # Simon Ser
              '9DDA3B9FA5D58DD5392C78E652CB6609B22DA89A') # Drew DeVault

build() {
    meson "$pkgname-$pkgver" build \
        --prefix /usr \
        --buildtype=plain \
        -Dlogind=enabled \
        -Dman-pages=enabled \
        -Dlogind-provider=systemd
    ninja -C build
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 "$pkgname-$pkgver/README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}

