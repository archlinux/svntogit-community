# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=mailutils
pkgver=2.2
pkgrel=3
pkgdesc="MUA command line tool (mailx)"
arch=(i686 x86_64)
url="http://www.gnu.org/software/mailutils/"
license=('GPL')
depends=('libldap' 'pam' 'gnutls' 'guile')
makedepends=('python2' 'emacs')
optdepends=('python2')
conflicts=('libsieve')
install=mailutils.install
options=(zipman !emptydirs)
source=(http://ftp.gnu.org/gnu/mailutils/mailutils-$pkgver.tar.bz2)
md5sums=('834d2f616b922856127ecfe6c2de7d1a')

build() {
  cd "$srcdir/mailutils-$pkgver"

  py2=`pacman -Q python2 | cut -d\  -f2 | cut -d. -f1-2`
  py3=`pacman -Q python | cut -d\  -f2 | cut -d. -f1-2`

  export PYTHON=/usr/bin/python2
  export PYTHON_CONFIG=/usr/bin/python2-config
  export CFLAGS="$CFLAGS `pkg-config --cflags python-$py2`"
  export LDFLAGS="$LDFLAGS `pkg-config --libs python-$py2`"

  [ -f Makefile ] || ./configure --prefix=/usr --with-gdbm --with-gnutls \
	--libexecdir=/usr/lib/$pkgname --without-fribidi \
	--without-tcp-wrappers
#  [ -z "$py3" ] || find -type f -name Makefile -exec sed -i "s|python$py3|python$py2|" {} \;
  make -j1
}

package() {
  cd "$srcdir/mailutils-$pkgver"
  make -j1 DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/info/dir
  mv $pkgdir/usr/bin/mail $pkgdir/usr/bin/gnu-mail
  mv $pkgdir/usr/share/man/man1/mail.1 $pkgdir/usr/share/man/man1/gnu-mail.1
  chown -R root:root $pkgdir
}
