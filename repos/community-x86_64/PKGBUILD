# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=zita-ajbridge
pkgver=0.8.4
pkgrel=2
pkgdesc="Jack client to use additional ALSA devices."
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-ajbridge-doc/quickguide.html"
license=(GPL3)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  jack
  zita-alsa-pcmi
  zita-resampler
)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2)
sha512sums=('591c75324fdb51d705b15b7cb93ff0ff098d354277262468faba2c45741c24e0749ae9fa9169d206a4d589c7fac33d05ed33cb6ea6d9d808b3f640c7e938f244')
b2sums=('e4f0923129fd8cc38234efc0449bd5d408676cbf00e1225d563b4fb2e41b3b27541cc2be6ac6eed61b1294379b51e0f2d929553ff2053b544f62e87f3679fb10')

build() {
  make -C $pkgname-$pkgver/source
}

package() {
  depends+=(
    jack libjack.so
    zita-alsa-pcmi libzita-alsa-pcmi.so
    zita-resampler libzita-resampler.so
  )

  make PREFIX=/usr DESTDIR="$pkgdir" install -C $pkgname-$pkgver/source
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
