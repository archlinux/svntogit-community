# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-desktop
pkgver=6.4.0
pkgrel=1
pkgdesc='Switchboard Desktop Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-pantheon-shell
license=(GPL3)
groups=(pantheon-unstable)
depends=(
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  libgee
  libgexiv2
  libgranite.so
  libhandy
  libswitchboard-2.0.so
  plank
  tumbler
)
makedepends=(
  git
  meson
  vala
)
_tag=de674847df3277918a295890408c30ab98fd6b7c
source=(switchboard-plug-desktop::git+https://github.com/elementary/switchboard-plug-pantheon-shell.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-desktop
  git describe --tags
}

prepare() {
  sed 's|/usr/share/backgrounds|/usr/share/backgrounds/elementary|' -i switchboard-plug-desktop/src/Views/Wallpaper.vala
}

build() {
  arch-meson switchboard-plug-desktop build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
