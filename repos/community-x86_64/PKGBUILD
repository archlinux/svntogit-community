# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cipher-aes128
pkgname=haskell-cipher-aes128
pkgver=0.7.0.5
pkgrel=9
pkgdesc="AES and common modes using AES-NI when available."
url="https://github.com/TomMD/cipher-aes128"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-crypto-api' 'haskell-tagged' 'haskell-cereal')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('8479dec68d6fa35e8d615bcc8c6336459fa537f1962b7af87e63b0c38d544ed26e6d30e980f90bb3585513bd618e01330d3e008e77404dae8d2e27954d7a535a')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
