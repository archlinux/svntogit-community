# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=granite7
pkgver=7.1.0
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=(x86_64)
url=https://github.com/elementary/granite
license=(LGPL3)
groups=(pantheon)
depends=(
  cairo
  gdk-pixbuf2
  glib2
  gtk4
  libgee
  pango
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
provides=(libgranite-7.so)
_tag=d79db0b7132eca09319c3481befdf8bc241445a9
source=(git+https://github.com/elementary/granite.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd granite
  git describe --tags
}

build() {
  arch-meson granite build \
    -D b_pie=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
