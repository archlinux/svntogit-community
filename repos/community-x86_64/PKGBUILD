# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=budgie-extras
pkgver=1.1.0
_quickchar=3b8897b1fb901530ba703e4717f9719e0236a0d2
_app_menu=1e717efa679bd1e789326b4963a532ac11903667
_net_applet=4a965dfb0395d621774be3db81d98c73b4998b6e
pkgrel=2
pkgdesc='Additional Budgie Desktop enhancements for the user experience'
arch=('x86_64')
license=('GPL3')
url='https://github.com/UbuntuBudgie/budgie-extras'
depends=('budgie-desktop' 'wmctrl' 'xdotool' 'xprintidle' 'python' 'python-gobject' 'python-cairo' 'libgee'
         'zenity' 'vorbis-tools' 'python-psutil' 'sound-theme-freedesktop' 'imagemagick' 'python-pillow'
         'python-svgwrite' 'python-cairosvg' 'python-pyudev' 'python-requests' 'libpeas' 'json-glib'
         'libkeybinder3' 'python-pyperclip' 'python-xlib' 'xorg-xinput' 'granite' 'appstream' 'zeitgeist'
         'libnma' 'libhandy0' 'xorg-xprop')
makedepends=('gobject-introspection' 'meson' 'ninja' 'vala' 'intltool' 'git')
source=("git+$url.git#tag=v$pkgver"
        "git+https://github.com/UbuntuBudgie/QuickChar.git#commit=$_quickchar"
        "git+https://github.com/UbuntuBudgie/applications-menu.git#commit=$_app_menu"
        "git+https://github.com/UbuntuBudgie/budgie-network-applet.git#commit=$_net_applet")
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git config submodule.budgie-quickchar/quickchar.url "$srcdir"/QuickChar
  git config submodule.budgie-applications-menu/applications-menu.url "$srcdir"/applications-menu
  git config submodule.budgie-network-manager/budgie-network-applet.url "$srcdir"/budgie-network-applet
  git submodule update
}

build() {
  mkdir -p $pkgname/build
  cd $pkgname/build

  arch-meson .. \
  	-D b_pie=false

  ninja
}

package() {
  cd $pkgname/build

  DESTDIR="$pkgdir" ninja install
}

