# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=wiredtiger
pkgver=2.7.0
pkgrel=2
pkgdesc="High performance, scalable, production quality, NoSQL, Open Source extensible platform for data management"
arch=('x86_64')
url="http://source.wiredtiger.com/"
license=('GPL')
depends=('snappy' 'lz4' 'zlib' 'bzip2')
makedepends=('git')
provides=('wiredtiger')
conflicts=('wiredtiger')
source=("git://github.com/wiredtiger/wiredtiger.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "$srcdir/wiredtiger"
  
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-leveldb \
              --enable-bzip2 \
              --enable-lz4 \
              --with-builtins=snappy,zlib
  make
}

check() {
  cd "$srcdir/wiredtiger"
  make test
}

package() {
  cd "$srcdir/wiredtiger"
  make DESTDIR="$pkgdir/" install
}
