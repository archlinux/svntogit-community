# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wsaccel
pkgver=0.6.3
pkgrel=4
pkgdesc='Accelerator for ws4py and AutobahnPython'
arch=('x86_64')
url='https://github.com/methane/wsaccel'
license=('Apache')
depends=('python')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-pytest')
source=("https://github.com/methane/wsaccel/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0fe97c7fa94cec1b9cf019507f55ce633a9b4bda86329937cddcfa25ba94755010813688cda77ee4fea1c50918e45cd50770bb27e7583e630c46e84d27c155da')

build() {
  cd wsaccel-$pkgver
  python setup.py build
}

check() {
  cd wsaccel-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-x86_64-3.10" pytest
}

package() {
  cd wsaccel-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
