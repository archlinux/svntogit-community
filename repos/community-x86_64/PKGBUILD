# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wails
pkgver=2.4.0
pkgrel=1
pkgdesc="Create desktop apps using Go and Web Technologies"
arch=(x86_64)
url="https://github.com/wailsapp/wails"
license=(MIT)
depends=(glibc)
makedepends=(go webkit2gtk gtk3)
source=(https://github.com/wailsapp/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('b2e9bfa3941af9f0e5683e65273ef97fd76d1e144af61d76ba688dc194cbf5ea40ef1608686641b14b39f21b344c1ed50e187d3068747af878c43b246e7eced2')
b2sums=('02538cc3bbbb10c2cf43c8e38489c4df8f13f7dbf723d24f0e3c41f84d3053047a653274051003c2022ce4cd0a18bef6f3cbe685e2bb045552e155f5fe87a5e3')

prepare() {
  mkdir -vp $pkgname-$pkgver/build
}

build() {
  cd $pkgname-$pkgver/v2
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o build/ ./cmd/wails
}

check() {
  cd $pkgname-$pkgver
  go test
}

package() {
  install -vDm 755 $pkgname-$pkgver/v2/build/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,CONTRIBUTORS,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
