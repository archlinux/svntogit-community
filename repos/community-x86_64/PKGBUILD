# Maintainer: Anatol Pomozov

pkgname=osquery
pkgver=5.3.0
pkgrel=2
pkgdesc='SQL powered operating system instrumentation, monitoring, and analytics'
arch=(x86_64)
url='https://osquery.io'
license=(Apache GPL2)
depends=(zlib)
makedepends=(cmake ninja clang python gcc-libs git libunwind)
options=(!strip)
# following commit represents osquery $pkgver with some Arch-specific patches
_commit=279553bf33ebe62c98923be19826fe4bd29cced2
source=(git+https://github.com/anatol/osquery.git#commit=$_commit
        ebpf-common.patch
        libaudit.patch)
sha256sums=('SKIP'
            '88cba2eebd12079bf3a4a8c5ba4ed34b1b5ccd9f9ac3fa518f1591396176666d'
            '96218ef5b7d6d6deb3a7b4b3dfed8068b7e4d10acd5b19372b9882f89d4478a8')

prepare() {
  cd osquery

  git submodule update --init --recursive

  (cd $srcdir/osquery/libraries/cmake/source/libaudit/src && patch -p1 < $srcdir/libaudit.patch)
  (cd $srcdir/osquery/libraries/cmake/source/ebpfpub/src/ && git submodule update --init && cd libraries/ebpf-common/src/ && patch -p1 < $srcdir/ebpf-common.patch)
}

build() {
  cd osquery

  CC=clang CXX=clang++ cmake \
      -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DOSQUERY_VERSION=$pkgver \
      -G Ninja -S . -B build

  ninja -C build
}

package() {
  cd osquery

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 tools/deployment/osquery.example.conf "$pkgdir/etc/osquery/osquery.conf"
  install -Dm644 tools/deployment/linux_packaging/osqueryd.sysconfig "$pkgdir/etc/sysconfig/osqueryd"
  install -Dm644 tools/deployment/linux_packaging/rpm/osqueryd.service "$pkgdir/usr/lib/systemd/system/osqueryd.service"
  rm -r $pkgdir/control
}
