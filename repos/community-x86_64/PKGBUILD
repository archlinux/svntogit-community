# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libstrophe
pkgver=0.8.8
pkgrel=2
epoch=1
pkgdesc='Simple, lightweight C library for writing XMPP clients'
url='http://strophe.im/libstrophe/'
arch=('i686' 'x86_64')
license=('MIT' 'GPL3')
depends=('openssl' 'expat')
makedepends=('git')
checkdepends=('check')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/strophe/libstrophe/archive/${pkgver}.tar.gz)
sha512sums=('fe1e9ee0ea93271862ca8bd3cd5cd66d0bd060398f3b11a242158872501ab517f4f436e474dd5603d73433333d4aaf0957d68b7fe69578d7d99541aaa63a38a3')

prepare() {
  cd ${pkgname}-${pkgver}
  ./bootstrap.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  make -C ${pkgname}-${pkgver} check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
}

# vim: ts=2 sw=2 et:
