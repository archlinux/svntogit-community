# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.11.1
pkgrel=2
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2.so' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('cython' 'python-build' 'python-cffi' 'python-installer' 'python-setuptools' 'git' 'python-six' 'python-wheel')
checkdepends=('python-pytest-runner')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
        # "pseudo-1.11.2.patch::$url/compare/v1.11.1...c8d6d27.patch")
sha256sums=('c352cf8712094b6fa997a24d76e906d13f02ad5f54f66ce2fb3fdc3890150546')
            # 'f8e59174431acf418a26655200fb41fb739430be2d77f7cec1abbcad12a67502')

prepare() {
	cd "pygit2-$pkgver"
	# Disable tests that do stuff online
	sed -e '/has_network/s/True/False/' -i test/utils.py
	# Bypass hard limit on libgit2-1.5.x
	sed -i -e '/LIBGIT2_VER_MINOR/s/5/6/g' src/types.h
	# apply patches from master branch to 1.11.2 commit (tag yanked)
	# patch -p1 < ../pseudo-1.11.2.patch
}

build() {
	cd "pygit2-$pkgver"
	python -m build -wn
}

check() {
	cd "pygit2-$pkgver"
	local _pyver=cpython-$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
	PYTHONPATH="$PWD/build/lib.linux-$CARCH-$_pyver" pytest
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python -m installer -d "${pkgdir}" dist/*.whl
}
