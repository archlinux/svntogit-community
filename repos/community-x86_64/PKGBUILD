# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.9.14
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
depends=('cups' 'deepin-qt5integration' 'smbclient')
makedepends=('gtest' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/dde-printer/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f8f1c71efbd1484512b16e83aa4161cdc0e1acb4c72afc2c822c328766c2c7bf3a661c997b1893e4f7819ef13ab2e50924347822992f038b30d9f898b7c4adf1')

prepare() {
  cd dde-printer-$pkgver
  sed -i '$aOnlyShowIn=Deepin' src/Printer/platform/linux/dde-printer.desktop

  # https://github.com/linuxdeepin/dde-printer/issues/4
  sed -i '/#include <strings.h>/a #include <stdexcept>' src/cppcups/cupssnmp.cpp
}

build() {
  cd dde-printer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
