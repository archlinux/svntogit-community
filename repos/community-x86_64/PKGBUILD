# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-setproctitle
pkgver=1.2
pkgrel=9
pkgdesc="Allows a python process to change its process title"
license=("BSD")
url="https://github.com/dvarrazzo/py-setproctitle"
depends=('python')
checkdepends=('python-pytest' 'procps-ng')
arch=('x86_64')
source=(https://files.pythonhosted.org/packages/source/s/setproctitle/setproctitle-$pkgver.tar.gz)
sha256sums=('9b4e48722dd96cbd66d5bf2eab930fff8546cd551dd8d774c8a319448bd381a6')

build() {
    cd "$srcdir"/setproctitle-$pkgver
    python setup.py build
}

check() {
    local _pyver=$(python -c "import sys; print('{0}.{1}'.format(*sys.version_info[:2]))")
    cd "$srcdir"/setproctitle-$pkgver/build/lib.linux-$CARCH-$_pyver
    python -m pytest ../../tests
}

package_python-setproctitle() {
    cd "$srcdir"/setproctitle-$pkgver
    python setup.py install --skip-build -O1 --root="$pkgdir"
    install -m0644 -D COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
