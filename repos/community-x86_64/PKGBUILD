# $Id: PKGBUILD 101980 2013-12-03 09:48:38Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=libvirt-python
pkgname=(libvirt-python2 libvirt-python)
pkgver=3.6.0
pkgrel=1
pkgdesc="libvirt python binding"
arch=('i686' 'x86_64')
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL')
makedepends=('python' 'python2' 'libvirt')
options=('emptydirs')
install="libvirt-python.install"
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
#source=("https://pypi.python.org/packages/4d/22/b415c2b6907fcb1d0b9b81f6a1c585f7ee9fef44b906ac479f60a002cf0b/libvirt-python-$pkgver.tar.gz") #{,.asc}
source=("http://libvirt.org/sources/python/libvirt-python-$pkgver.tar.gz")
sha256sums=('ecb9cbc8b1f33b245b04e3a937d8483584ab7a18258b217be96661343a4b1ad0')

package_libvirt-python2() {
  depends=('python2' 'libvirt')
  replaces=('libvirt-python<=3.2.0-1')
  conflicts=('libvirt-python<=3.2.0-1')

  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py clean
  python2 setup.py install --root="$pkgdir"
}

package_libvirt-python() {
  depends=('python' 'libvirt')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py clean
  python setup.py install --root="$pkgdir"
}
