# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python-gevent
pkgver=22.10.1
pkgrel=1
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
depends=('c-ares' 'libev' 'python-greenlet' 'python-setuptools' 'python-zope-event' 'python-zope-interface')
makedepends=('cython')
checkdepends=('lsof' 'python-perf' 'python-objgraph' 'python-dnspython' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('7732e1fe7b51940081c43b587e2cad577e1493a3de5cbe601d605dce3fbd7811eee5b1f27408c91041bf51844588f888aa565b931f8f2a3f04d20bb095815139')

export GEVENTSETUP_EMBED=0

build() {
  cd gevent-$pkgver
  python setup.py build
}

check() {
  (
    cd gevent-$pkgver
    python -m venv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.10/site-packages"
    python setup.py install
    python -m gevent.tests
  ) || echo "Tests failed"
}

package() {
  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
