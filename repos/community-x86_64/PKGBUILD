# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Markus Martin <markus@archwyrm.net>

pkgname=alure
pkgver=1.2
pkgrel=8
pkgdesc='Utility library to help manage common tasks with OpenAL applications.'
arch=('x86_64')
url='https://kcat.tomasu.net/alure.html'
license=('MIT')
depends=('openal')
makedepends=('cmake' 'libsndfile' 'libvorbis' 'flac' 'mpg123' 'dumb' 'fluidsynth')
optdepends=('libsndfile: for uncompressed audio support'
            'libvorbis: for OGG Vorbis support'
            'flac: for FLAC support'
            'mpg123: for MPEG support'
            'dumb: for IT, XM, S3M and MOD support'
            'fluidsynth: for SoundFont 2 support')
source=("https://kcat.tomasu.net/alure-releases/${pkgname}-${pkgver}.tar.bz2"
        build.patch
        dumb-2.patch)
md5sums=('3088aba074ad02d95ea51e705053b9f5'
         '6a8dfd62ccae920d393b4202736aba46'
         'f6569e1fee4015a9e0a68df37798c8aa')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../build.patch
  patch -p1 -i ../dumb-2.patch # Fix build with dumb 2 (Gentoo)
}

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
# Fix build with fluidsynth 2.4
  find -name build.make | xargs sed -e 's|\;| |g' -i
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
