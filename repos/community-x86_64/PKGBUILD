# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=reapack
pkgver=1.2.4.2
pkgrel=3
pkgdesc='Package manager for the Reaper DAW'
arch=('x86_64')
url='https://reapack.com'
license=('GPL3' 'LGPL3')
depends=(
  'reaper'
  'libxml2'
  'curl'
  'sqlite'
)
makedepends=(
  'git'
  'cmake'
  'boost'
  'openssl'
  'zlib'
  'ruby'
  'php'
  'catch2'
)
install='reapack.install'
_commit='daa3346cee146d5eeca806452d26b70e623d5aa7'
source=(
  "$pkgname::git+https://github.com/cfillion/reapack#commit=$_commit"
  'github.com-justinfrankel-wdl::git+https://github.com/justinfrankel/WDL'
  'github.com-justinfrankel-reaper-sdk::git+https://github.com/justinfrankel/reaper-sdk'
)
b2sums=('SKIP'
        'SKIP'
        'SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # prepare git submodule(s)
  git submodule init
  git config submodule.vendor/WDL.url "$srcdir/github.com-justinfrankel-wdl"
  git config submodule.vendor/reaper-sdk.url "$srcdir/github.com-justinfrankel-reaper-sdk"
  git -c protocol.file.allow=always submodule update

  # FTBFS due to catch2 v3 not being in the repos yet
  # so we have to use an older version
  git revert --no-commit d551e255fe79ea1e463fe757554a55bf5adf5058

  # FTBFS: FS#77178
  git cherry-pick --no-commit f2806c95ba16549e969581429d1fc51616c68796
}

build() {
  cd "$pkgname"

  cmake \
    -B build \
    -DCMAKE_BUILD_TYPE=Release

  cmake --build build
}

check() {
  make -C "$pkgname/build" test
}

package() {
  cd "$pkgname"

  # plugin
  install -vDm755 -t "$pkgdir/usr/lib/$pkgname" "build/reaper_reapack-$CARCH.so"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
