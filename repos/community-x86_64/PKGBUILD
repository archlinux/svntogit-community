# Maintainer: Alad Wenter <alad@archlinux.org>
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Lorenzo Nizzi Grifi Gargiolli <lorenzo.nizzi.grifi@gmail.com>
# Contributor: marc0s <marc0s@fsfe.org>

pkgname=pcsc-tools
pkgver=1.5.8
pkgrel=1
pkgdesc='PC/SC Architecture smartcard tools'
arch=('x86_64')
url='http://ludovic.rousseau.free.fr/softwares/pcsc-tools/'
license=('GPL')
depends=('pcsclite' 'pcsc-perl' 'glib-perl')
makedepends=('pkg-config')
optdepends=('perl-gtk3: gscriptor')
validpgpkeys=('F5E11B9FFE911146F41D953D78A1B4DFE8F9C57E') # Ludovic Rousseau <rousseau@debian.org>
source=("http://ludovic.rousseau.free.fr/softwares/${pkgname}/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('d7e1f6f46923be27d98ce4986e6b1d96cc1b04ae120f8dc1d5d96cca5c39564905bfbf3b42c99659bb4439f917c40fa6c946f3e40584595485a68209559c2016'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
