# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Hannes Graeuler <hgraeule @ uos dot de>

# this is needed only for building the imagequant-sys crate
_libver=4.1.0

pkgname=pngquant
pkgver=3.0.1
pkgrel=1
pkgdesc="Command-line utility to quantize PNGs down to 8-bit paletted PNGs"
arch=('x86_64')
url="https://pngquant.org/"
license=('BSD')
depends=('libpng' 'lcms2' 'libimagequant')
makedepends=('cargo')
source=("https://github.com/kornelski/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "https://github.com/ImageOptim/libimagequant/archive/$_libver/libimagequant-$_libver.tar.gz")
sha256sums=('488e4587c27c7515427b231530cc070a6b123727137b32c501658cff3f8a3451'
            '7673521655cdf0ec16b99fd0c7c8b1bd542b6f3a8469fa2effe34b0f53e9fa92')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    rm -rf lib
    ln -s ../libimagequant-$_libver lib
    cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    export CARGO_TARGET_DIR=target
    cargo build --release
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    export RUSTUP_TOOLCHAIN=stable
    cargo test
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -Dm644 target/release/pngquant "$pkgdir/usr/bin/pngquant"
    install -Dm644 pngquant.1 "$pkgdir/usr/share/man/man1/pngquant.1"
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
