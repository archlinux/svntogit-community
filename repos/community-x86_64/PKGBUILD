# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Eric Anderson <ejona86 at gmail.com>
# Contributor: Pierre DOUCET <pierre at equinoxefr.org>
pkgname=openscad
pkgver=2014.03
pkgrel=6
pkgdesc="The programmers solid 3D CAD modeller"
url="http://openscad.org/"
arch=('i686' 'x86_64')
license=('GPL2')
install=openscad.install
changelog=CHANGELOG
depends=('qt4' 'cgal' 'opencsg' 'boost-libs' 'shared-mime-info')
makedepends=('eigen' 'boost' 'imagemagick')
source=("http://files.openscad.org/openscad-$pkgver.src.tar.gz")
md5sums=('38bebd02bce365fe39e695a07411ab73')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    rm -f libraries/MCAD/*.py
    # internal version will use the build date, override that
    qmake-qt4 PREFIX="/usr" VERSION="$pkgver"
    make
    convert "icons/$pkgname.png" -resize 128x128\> "icons/$pkgname-128.png"
}

# it has tests
# tests need cmake, python, imagemagick
# see doc/testing.txt about enabling
# needs to display stuff, run image diffs

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make INSTALL_ROOT="$pkgdir" install
    install -Dm644 "icons/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
    install -Dm644 "icons/$pkgname-128.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
    install -Dm644 "icons/$pkgname.xml" "$pkgdir/usr/share/mime/packages/$pkgname.xml"
}
