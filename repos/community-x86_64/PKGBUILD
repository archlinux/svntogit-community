# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
_pkgdir=github-hub-
pkgver=2.2.0
pkgrel=1
pkgdesc="cli interface for Github"
arch=('x86_64' 'i686')
url="https://hub.github.com"
depends=('git')
makedepends=('go')
license=('MIT')
source=("$pkgname-$pkgver.tar.gz::https://github.com/github/hub/tarball/v$pkgver")
sha256sums=('cd1fb034c7793fd0f4fe1419eb253602accf0f9c051544112cc5d6fa6b449f26')

build() {
  cd "$srcdir/$_pkgdir"*

  ./script/build
  gzip --best -c man/hub.1> hub.1.gz

}

package() {
  cd "$srcdir/$_pkgdir"*

  install -Dm755 "$srcdir/$_pkgdir"*"/$pkgname" "$pkgdir/usr/bin/$pkgname"

  install -Dm644 "$srcdir/$_pkgdir"*"/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/$_pkgdir"*"/etc/hub.bash_completion.sh" "$pkgdir/usr/share/bash-completion/completions/hub"
  install -Dm644 "$srcdir/$_pkgdir"*"/etc/hub.zsh_completion" "$pkgdir/usr/share/zsh/site-functions/_hub"

  install -Dm644 "$srcdir/$_pkgdir"*"/man/$pkgname.1" "$pkgdir/usr/share/man/man1/$pkgname.1"

}
