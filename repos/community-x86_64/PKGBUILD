# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=vty
pkgname=haskell-vty
pkgver=5.25.1
pkgrel=31
pkgdesc="A simple terminal UI library"
url="https://github.com/jtdaugherty/vty"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-microlens' 'haskell-microlens-mtl'
         'haskell-microlens-th' 'haskell-hashable' 'haskell-parallel'
         'haskell-utf8-string' 'haskell-vector')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-smallcheck'
             'haskell-quickcheck-assertions' 'haskell-test-framework'
             'haskell-test-framework-smallcheck' 'haskell-test-framework-hunit' 'haskell-random'
             'haskell-string-qq')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('cc1ce5801dc02e86523b5cc40989d871e516476f5560fd0b3bb7fc815bedbc66f14fe8dd9f832e0ecf10c0f68a11195311833b9080cb5b9447ea95c2113da489')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *0.4/<0.5/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    # https://github.com/jtdaugherty/vty/issues/132
    # https://github.com/haskell/cabal/issues/4918
    cd $_hkgname-$pkgver
    # runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
