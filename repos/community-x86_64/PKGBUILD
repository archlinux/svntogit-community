# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=8.43
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('gcc-libs')
makedepends=('python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('e0bb3c8641729db81b844172ebdebae6a038bdfa732b73f0100dd2871fd58ffeb4e36b591c23c87e225016bc1d18d8b2996ad636d7ddd8d2bd86e9fdfafd15c4'
            'ae842550b52e6dd7281acf254c9b4457a69a33bf7e8229780df990915048c44a91172b59c47e5f2b5e51dfae9f0dbc1916161d89689badfc59378c22a2a2347b')
b2sums=('036b6500ee5f554b200ff3671425280152a5906c4d554ebca531c7fc3cb34cbbb85e3b45c52261291af36ae78f86ee3d922b9d64c468218f8106f1aba8b5009c'
        'eb2ff25d3114cc18005816a8a8a3809fd28ecb9be0823d9ce6f1bf58120830d4623fa87ba71a38d7f25d354daf7575f819385bacb679f307e5373d4c4ea8973e')

build() {
  make -C Source/Projects/NonWindows

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  make DESTDIR="$pkgdir" prefix=/usr install -C Source/Projects/NonWindows

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
