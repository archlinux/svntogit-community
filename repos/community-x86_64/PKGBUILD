# Maintainer:
# Contributor: Robert Tari <robert at tari dot in>

pkgname=libayatana-appindicator
pkgver=0.5.92
pkgrel=1
pkgdesc='Ayatana Application Indicators shared library'
arch=(x86_64)
url='https://github.com/AyatanaIndicators/libayatana-appindicator'
license=(LGPL2.1 LGPL3)
makedepends=(cmake gobject-introspection vala)
depends=(libdbusmenu-gtk3 libayatana-indicator)
source=(https://github.com/AyatanaIndicators/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('adedcf160dae7547971d475b42062cab278d54ec075537e6958ffdbf2d996857')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_BINDINGS_MONO=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
