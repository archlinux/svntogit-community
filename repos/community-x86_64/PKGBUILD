# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-reports-plugin'
_pluginname='dashboards-reports'
pkgver=2.6.0.0
_dashboardsver=2.6.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Reports Plugin'
url='https://opensearch.org/docs/latest/dashboards/reporting'
arch=('x86_64')
license=('Apache')
depends=("opensearch-dashboards=${_dashboardsver}" 'coffeescript')
makedepends=('yarn' 'python' 'git')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/dashboards-reporting.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" "OpenSearch-Dashboards/package.json"
}

build() {
  mv "dashboards-reporting" "OpenSearch-Dashboards/plugins/${_pluginname}"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn build --skip-archive
}

# TODO: Fix tests. Seem to include integration tests.
#check() {
#  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
#  yarn test
#}

package() {
  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}"

  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "build/opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
