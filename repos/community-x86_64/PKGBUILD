# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=libfdk-aac
pkgver=0.1.3
pkgrel=2
pkgdesc='Fraunhofer FDK AAC codec library'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/opencore-amr/'
license=('custom')
depends=('glibc')
source=("http://downloads.sourceforge.net/opencore-amr/${pkgname#lib}-${pkgver}.tar.gz")
sha256sums=('a9cb872802cfb7a2aff7a549c114c547d1e518dd60c2f85942229cc20c0d0c8d')

build() {
  cd ${pkgname#lib}-${pkgver}

  ./configure --prefix='/usr' --disable-static
  make
}

package () {
  cd ${pkgname#lib}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m 644 NOTICE "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
