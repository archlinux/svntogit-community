# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

# Upstream tests have multiple issues including race conditions, using external
# binaries, differing results based on unrelated system directories, etc.
# See https://github.com/nushell/nushell/issues/7951
# See https://github.com/nushell/nushell/issues/8404
BUILDENV+=(!check)

pkgname=nushell
pkgver=0.76.0
_commit='bc38a6a795f438ab1465dd987adf92efd9530529'
pkgrel=1
pkgdesc='A new type of shell'
arch=('x86_64')
url='https://www.nushell.sh'
license=('MIT')
depends=('openssl' 'libxcb' 'curl' 'bzip2')
makedepends=('git' 'cargo')
options=('!lto')
install=nushell.install
source=("$pkgname::git+https://github.com/nushell/nushell.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags
}

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --release --frozen --workspace --features=extra,dataframe
}

check() {
  cd "$pkgname"

  cargo test --frozen --workspace --features=extra,dataframe
}

package() {
  cd "$pkgname"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -name "nu*" \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
