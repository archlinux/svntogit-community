# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgovirt
pkgver=0.3.6
pkgrel=1
pkgdesc="GObject-based library to access oVirt REST API"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/libgovirt"
license=(LGPL)
depends=(rest)
makedepends=(git gobject-introspection)
_commit=74852b8d76b0bbdb17d0e9fc3378afb5bf95ca6c  # tags/v0.3.6^0
source=("git+https://gitlab.gnome.org/GNOME/libgovirt.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/^v//' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
