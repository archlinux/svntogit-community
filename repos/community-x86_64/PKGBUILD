# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=icecast
pkgver=2.4.3
pkgrel=2
pkgdesc='Streaming audio over the Internet'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.icecast.org/'
depends=('libxslt' 'libvorbis' 'curl' 'speex' 'libtheora' 'libkate')
backup=('etc/icecast.xml'
        'etc/logrotate.d/icecast')
source=("http://downloads.us.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'icecast.logrotate'
        'start-by-nobody.patch'
        'icecast.service')
md5sums=('83d7d34dbe22f0f8fc119d2c9839efc0'
         '59c6552bcb1dd9fb542af8670dfabd3c'
         'd8e929d2214123a1954da4383bf16583'
         '0753c15f01dc14852e5d70925fc1f6a0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/start-by-nobody.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install logrotate config (taken from Fedora)
  install -Dm644 "${srcdir}/icecast.logrotate" "${pkgdir}/etc/logrotate.d/icecast"

  # create log directory
  install -d -g99 -o99 "${pkgdir}/var/log/icecast"

  # install systemd unit
  install -Dm0644 "${srcdir}/icecast.service" "${pkgdir}/usr/lib/systemd/system/icecast.service"
}
