# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=54
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=(x86_64)
url="https://dinisnoise.org/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libglvnd sdl tcl)
makedepends=(boost glu rtaudio rtmidi)
options=(debug)
source=(
  https://archive.org/download/dinisnoise_source_code/$pkgname-$pkgver.tar.gz
  $pkgname-54-devendor_rtaudio_rtmidi.patch
)
sha512sums=('8831e6d838ba06373b13574701929f96a508f2899618509b3e79f23cb0aa11c34b347fa00b4fa765a7668c7db54733be2c9146b8e783d3fe07e3ea4ce2707bd8'
            '151613648d4da638835a1d5eceb3c6db78aea801e8f2bd385120a4b7bdfaadb707849a3ec9745b7e1e5f2a600ab13fe6fc3b2abe78c2783461deffb291787f1d')
b2sums=('2a7540d9a413c0f83b2cfc7373b1c0fdd40012ae725f162810e4d3165e3bc669661860fd653eaed8c7f2eeed7f015ff50ce6082ce8de04bd035bf395ca018d6a'
        'a80851871f075971139a1eb82144fa05aaad4e7892eb80c5a045d12f59dd8a25f76fabd6eaa435409c5db2ea5e40615117a9016c50160594590fd434af9a87b0')

prepare() {
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-54-devendor_rtaudio_rtmidi.patch

  cd $pkgname-$pkgver
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  export CXXFLAGS+=" -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS+=" -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS+=" $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=(librtaudio.so librtmidi.so)

  make install DESTDIR="$pkgdir" -C $pkgname-$pkgver
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
