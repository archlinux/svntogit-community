# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Hermann Zahnweh (eigengrau)
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: foalsrock <foalsrock at gmail dot-com>
# Contributor: jebaum <jebaum at ucla dot edu>

pkgname=fzf
pkgver=0.16.5
pkgrel=1
pkgdesc="Command-line fuzzy finder"
arch=("i686" "x86_64")
url="https://github.com/junegunn/fzf"
license=("MIT")
makedepends=("git" "go")
optdepends=("fish: fish keybindings"
	"tmux: fzf-tmux script for launching fzf in a tmux pane"
	"vim: plugin"
	"zsh: zsh keybindings")
source=("$pkgname-$pkgver.tar.gz::https://github.com/junegunn/fzf/archive/$pkgver.tar.gz"
	"git+https://github.com/junegunn/$pkgname.wiki")
sha256sums=('7add1b5290af779fc1c6a3e306d696fb0e833d4efb5b60d94f9c424ac1b6f9fe'
            'SKIP')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	msg2 'Building...'
	GOPATH="$srcdir" TMPDIR=/tmp go get -u github.com/junegunn/fzf/...
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	## Doc
	install -dm755 "$pkgdir/usr/share/doc/fzf/wiki"
	install -m644 "$srcdir/fzf.wiki"/* "$pkgdir/usr/share/doc/fzf/wiki"
	install -m644 README.md install uninstall "$pkgdir/usr/share/doc/fzf"

	## Man page
	install -Dm644 "man/man1/fzf.1" "$pkgdir/usr/share/man/man1/fzf.1"

	## License
	install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/fzf/LICENSE"

	## Binaries
	install -dm755 "$pkgdir/usr/bin"
	install -m755 "$srcdir/bin/fzf" "bin/fzf-tmux" "$pkgdir/usr/bin/"

	## Completion and keybindings
	install -dm755 "$pkgdir/usr/share/fzf"
	install -m644 shell/*.bash shell/*.zsh "$pkgdir/usr/share/fzf"

	## Fish keybindings
	install -Dm644 "shell/key-bindings.fish" "$pkgdir/usr/share/fish/functions/fzf_key_bindings.fish"

	## Vim plugin
	install -Dm644 plugin/fzf.vim "$pkgdir"/usr/share/vim/vimfiles/plugin/fzf.vim
}
