# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=2.3
pkgrel=3
pkgdesc='Lightweight NTP client and server'
arch=('i686' 'x86_64')
url="http://chrony.tuxfamily.org/"
options=(strip emptydirs)
license=('GPL')
depends=('nss' 'readline' 'libcap' 'libedit')
backup=('etc/chrony.conf')
source=(http://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        service)
install=${pkgname}.install
sha256sums=('d3fd820fa63badf54ee0e48a649b94ea739df3aac5efa104bd90f89795aa2485'
            'dbd30435d5f51a92ecef779efa0eabaf56d09a35a18ddd30644d5418beada212')
sha256sums=('4776fa8e80d698723e9a88eb882170951f6c45860545d84ae9f9d8b9bbd73796'
            'dbd30435d5f51a92ecef779efa0eabaf56d09a35a18ddd30644d5418beada212')
sha256sums=('58bffb523012fb0fa87cc0d94d6e36de9689fe9556519cbd0d1ba254af92ccb2'
            'dbd30435d5f51a92ecef779efa0eabaf56d09a35a18ddd30644d5418beada212')

prepare() {
  cd $pkgname-$pkgver
  sed -i \
    -e 's|pool.ntp.org|3.arch.pool.ntp.org|g' \
    -e 's|server foo.example.net|server 0.arch.pool.ntp.org iburst|g' \
    -e 's|server bar.example.net|server 1.arch.pool.ntp.org iburst|g' \
    -e 's|server baz.example.net|server 2.arch.pool.ntp.org iburst|g' \
    examples/chrony.conf.example*
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-docs

  install -Dm0644 "$srcdir/$pkgname-$pkgver/examples/chrony.conf.example3" "$pkgdir/etc/chrony.conf"

  echo -e "\n# Enable kernel RTC sync\nrtcsync" >> "$pkgdir/etc/chrony.conf"

  sed -i '/^! rtconutc$/ s/^! //' "$pkgdir/etc/chrony.conf"

  mv "$pkgdir/usr/sbin/chronyd" "$pkgdir/usr/bin"
  rmdir "$pkgdir/usr/sbin"

  install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/chrony.service"

  cp -a examples $pkgdir/usr/share/doc/chrony/examples

  chown 183:183 "$pkgdir/var/lib/chrony"
}
