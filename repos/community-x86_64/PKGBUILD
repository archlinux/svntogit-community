# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_name=llvmlite
pkgbase="python-${_name}"
pkgname=("${pkgbase}")
pkgver=0.40.0rc1
pkgrel=1
pkgdesc='A Lightweight LLVM Python Binding for Writing JIT Compilers'
arch=('x86_64')
depends=('gcc-libs' 'glibc' 'llvm14-libs' 'python')
makedepends=('cmake' 'git' 'llvm14' 'python-build' 'python-installer' 'python-setuptools-scm' 'python-wheel')
checkdepends=('python-pytest')
url="https://github.com/numba/${_name}"
license=('BSD')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('f87877f4703bbc73b2c1a872a5487f4720031b9ad7bc8e2bf3dc5fe616db6b15')
b2sums=('02804284bbbdb0ea0664748eebef5e2edd9f6f848916d36bec4024bbe09c0add35c388ddd39a85db0f8f79bd3086aa45d17e40ab3e4f99b7248c8a6527440069')

build() {
    cd "${_name}-$pkgver"
    export PATH="/usr/lib/llvm14/bin:$PATH"
    python -m build --wheel --no-isolation
}

check() {
    cd "${_name}-$pkgver"
    pytest -vv $_name/tests
}

package() {
    cd "${_name}-$pkgver"

    python -m installer --destdir="$pkgdir" dist/*.whl

    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
