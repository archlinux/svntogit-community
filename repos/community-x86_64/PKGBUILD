# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.24.0
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/sunng87/cargo-release'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('gcc-libs' 'libgit2' 'libssh' 'openssl' 'zlib')
makedepends=('cargo' 'pkg-config')
options=('!lto')
source=(https://github.com/sunng87/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('a502603b5875f17507bf9ad31e2d85cdd6c9f18b5f044e152afeb4fb4851acb8afd6707fdd4aa9b46bc16bf0df68611c6c83e2ca8f873482a91a893a44049641')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/"vendored-libgit2"//' Cargo.toml
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
