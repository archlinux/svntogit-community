# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-text-charwidth
pkgver=0.04
pkgrel=12
pkgdesc="Perl/CPAN Module Text::CharWidth"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/Text-CharWidth"
license=("GPL" "PerlArtistic")
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/K/KU/KUBOTA/Text-CharWidth-${pkgver}.tar.gz")
md5sums=('37a723df0580c0758c0ee67b37336c15')

build() {
  cd Text-CharWidth-${pkgver}

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

package() {
  cd Text-CharWidth-${pkgver}

  make install DESTDIR=${pkgdir}

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
