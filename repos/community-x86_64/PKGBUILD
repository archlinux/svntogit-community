# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cassava-megaparsec
pkgname=haskell-cassava-megaparsec
pkgver=2.0.0
pkgrel=12
pkgdesc="Megaparsec parser of CSV files that plays nicely with Cassava"
url="https://github.com/stackbuilders/cassava-megaparsec"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-cassava' 'haskell-megaparsec' 'haskell-unordered-containers'
         'haskell-vector')
makedepends=('ghc' 'haskell-hspec' 'haskell-hspec-megaparsec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('5d33e84549a1ea04a794f8fe2f6052a9cc82c0444b3608a2f98005aa0437b9c944a9d0b4096f31ed30dd2aaa15428cbd8671dc303cd4ea8c2418343893801863')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.md"
}
