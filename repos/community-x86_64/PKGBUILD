# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
pkgname=ctwm
pkgver=4.0.3
pkgrel=1
pkgdesc="Claude's Tab Window Manager for the X Window System"
arch=(x86_64)
url='https://ctwm.org/'
license=('custom:MIT' 'custom:X')
depends=(libjpeg-turbo libxmu libxpm)
makedepends=(cmake ctags doxygen gendesk graphviz ninja)
source=("https://ctwm.org/dist/$pkgname-$pkgver.tar.xz")
sha256sums=('2e4b475a0168cb799723fd693b359e188179e51ce476ab453689db8fcdfc800c')

prepare() {
  gendesk -f -n --pkgname $pkgname --pkgdesc "$pkgdesc" --name CTWM
  gendesk -f -n -wm --pkgname ${pkgname}_kdm --exec $pkgname --name CTWM
}

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname.desktop \
    "$pkgdir"/usr/share/xsessions/$pkgname.desktop
  install -Dm644 ${pkgname}_kdm.desktop \
    "$pkgdir"/usr/share/apps/kdm/sessions/$pkgname.desktop
  install -Dm644 "$pkgname-$pkgver/COPYRIGHT" \
    "$pkgdir/usr/share/licenses/ctwm/COPYRIGHT"
}
