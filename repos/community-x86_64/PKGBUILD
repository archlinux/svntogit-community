# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2021-06-14
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://rust-analyzer.github.io/
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
makedepends=('git')
source=("git+https://github.com/rust-analyzer/rust-analyzer.git#tag=$_pkgver")
b2sums=('SKIP')

build() {
  cd $pkgname
  cargo build --release --locked
}

package() {
  cd $pkgname
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
