# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Albert Graef <aggraef at gmail.com>
# Contributor: Bernardo Barros <bernardobarros at gmail dot com>
# Contributor: bjoern lindig (bjoern _dot_ lindig _at_ google.com)

pkgname=faust
pkgver=2.5.23
pkgrel=5
pkgdesc="A functional programming language for realtime audio signal processing."
arch=('x86_64')
url="https://faust.grame.fr/"
license=('GPL2')
groups=('pro-audio')
depends=('llvm7-libs' 'libsndfile' 'libmicrohttpd')
makedepends=('llvm7' 'xxd')
optdepends=('clang: needed for sound2reader'
            'java-environment=8: needed for faust2android'
            'python2: needed for faust2md and faust2atomsnippets'
            'ruby: needed for faust2sc and scbuilder')
options=('staticlibs')
source=("https://github.com/grame-cncm/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'llvm7.patch')
sha512sums=('7ffa165b865cea09b923cb1687e5b48a48186644f9efcf8b532f948368c825f0ce829a8d6ef64d9a59c81bac0597dbd6b05bd1ac40198f5d4e6403eda1e57e69'
            'db5465db46ee5cf1f20bfba7d54e373f02e7a23848a9af7bd613e6b1589fac47f437582b6934b21469ba082a6f8c54d8528a33c48a5a747a20b2fa0bf7479337')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix scripts requiring python2
  sed -e '1s/python/&2/' \
    -i "tools/${pkgname}2appls/${pkgname}2"{md,atomsnippets}
  # fix for llvm7 (derived partially from upstream fix of HEAD):
  # https://github.com/grame-cncm/faust/issues/220
  patch -Np1 -i ../llvm7.patch
  # RELRO for sound2faust and sound2file
  sed -e "/\$(LIBS)/ s/$/ $LDFLAGS/g" \
      -i tools/sound2faust/Makefile
}

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr world
}

package() {
  cd "${pkgname}-${pkgver}"
  make install PREFIX=/usr DESTDIR="$pkgdir"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 documentation/{,misc/}*.pdf

  # examples
  install -vd "${pkgdir}/usr/share/${pkgname}/examples"
  cp -vR "examples/"* "${pkgdir}/usr/share/${pkgname}/examples/"

  cd syntax-highlighting
  # atom
  install -vDm 644 "atom/language-${pkgname}/package.json" \
    -t "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/"
  install -vDm 644 "atom/language-${pkgname}/grammars/${pkgname}.cson" \
    -t "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/grammars/"
  install -vDm 644 "atom/language-${pkgname}/settings/language-${pkgname}.cson" \
    -t "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/settings/"
  install -vDm 644 "atom/language-${pkgname}/snippets/"* \
    -t "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/snippets/"
  install -vDm 644 "atom/language-${pkgname}/process-palette.json.linux" \
    "${pkgdir}/usr/share/doc/${pkgname}/process-palette.json"
  # kate
  install -vDm 644 "${pkgname}.xml" \
    -t "${pkgdir}/usr/share/apps/katepart/syntax/"
  # gedit
  install -vDm 644 "${pkgname}.lang" \
    -t "${pkgdir}/usr/share/gtksourceview-2.0/language-specs/"
  install -vDm 644 "${pkgname}.lang" \
    -t "${pkgdir}/usr/share/gtksourceview-3.0/language-specs/"
  # highlight
  install -vDm 644 dsp.lang -t "$pkgdir/usr/share/highlight/langDefs/"
  # nano
  install -vDm 644 "${pkgname}.nanorc" -t "$pkgdir/usr/share/nano/"
  # vim
  install -vDm 644 "${pkgname}.vim" \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax/"
  # emacs
  install -vDm 644 "${pkgname}-mode.el" \
    -t "${pkgdir}/usr/share/emacs/site-lisp/"
}

