# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=suil
pkgname=(suil suil-docs)
pkgver=0.10.16
pkgrel=3
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=(x86_64)
url="https://drobilla.net/software/suil.html"
license=(ISC)
makedepends=(doxygen gtk2 gtk3 lv2 meson python-sphinx python-sphinx-lv2-theme qt5-base qt5-x11extras)
options=(debug)
source=(
  https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig}
  $pkgbase-0.10.16-fix_gtk_linking.patch
)
sha512sums=('9b1dac07b017934a93f70d514ec477785683e7ed5949b62a2c4ad139c1e84265399f54d3140dcf0e542d20a0919286aacb52103c91e2f6fc7ebd707db2d98ac4'
            'SKIP'
            '79fbfe748714c430026d499bf9e7f46cc686e52dcb325d9a9c04206ad0324f7100453a54f7b5ca9eb2562aa0f36215666077346775ef3db8dbe88c361dc2fedf')
b2sums=('614217013b39215f4b6d9414451a55fbca42dd038c6bc961d3769660707bf2da45432c97628edef8dacb8630437bcb304d10616ced2fd9cf9a6a4d094f196f6c'
        'SKIP'
        'e9d70fc61783acaf40670ce37ab68c57e244dd570ca1e2b9949f301eb31ad0203530f7e5230bc9904d58cde54bd86413052cd0fe2a24f3f68b70004cebf04f34')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  # fix issues with opening lv2 plugins multiple times: https://gitlab.com/lv2/suil/-/issues/7
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgbase-0.10.16-fix_gtk_linking.patch
}

build() {
  arch-meson $pkgname-$pkgver build -D cocoa=disabled
  meson compile -C build
}

check() {
  meson test -C build
}

package_suil() {
  depends=(gcc-libs glibc)
  optdepends=(
    'glib2: support for wrapping X11 and Qt5 in GTK3 and GTK2'
    'gtk2: support for wrapping X11 and Qt5 in GTK2 and GTK2 in Qt5'
    'gtk3: support for wrapping X11 and Qt5 in GTK3'
    'libx11: support for wrapping X11 directly and in GTK3 and GTK2'
    'qt5-base: support for wrapping X11 in Qt5 and Qt5 in GTK3 and GTK2'
    'qt5-x11extras: support for wrapping X11 in Qt5'
  )
  provides=(libsuil-0.so)

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,PACKAGING,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_suil-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/$pkgbase"{-0,}
  find "$pkgdir" -type f -iname "*.buildinfo" -delete
}
