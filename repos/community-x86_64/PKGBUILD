# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: kagan <juanynie@mi.madritel.es>
# Contributor: Hans-Kristian Arntzen <maister@archlinux.us>

pkgname=mednafen
pkgver=0.9.33.3
pkgrel=1
pkgdesc="A command-line multi-system gaming emulator"
url="http://mednafen.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libcdio' 'libsamplerate' 'sdl_net' 'zlib')
makedepends=('mesa' 'glu')
replaces=('mednafen-wip')
install=$pkgname.install
source=(http://downloads.sourceforge.net/mednafen/$pkgname-$pkgver.tar.bz2)
md5sums=('004b0854b7d47916160b0f4c4a0ead60')

build() {
  cd "$srcdir"/$pkgname

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir"/$pkgname

  make DESTDIR="$pkgdir"/ install
}
