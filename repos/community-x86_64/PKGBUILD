# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=test-framework
pkgname=haskell-test-framework
pkgver=0.8.2.0
pkgrel=598
pkgdesc="Framework for running and organising tests, with HUnit and QuickCheck support"
url="https://batterseapower.github.io/test-framework/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-ansi-wl-pprint' 'haskell-hostname'
         'haskell-old-locale' 'haskell-random' 'haskell-regex-posix' 'haskell-xml')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-libxml')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d8e7ea274819a1b49b9f938df527d22c808f456d25b84f03173ed57dddd39c230fb037a57ce90c38f571801ee38e86bed6826df1c93f184e4a803c72ce5f41e4')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/semigroups/d' -e 's/< *0.6/<1/;s/< *0.9/<1/;s/< *1.9/<2/;s/< *1.2/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    # --enable-tests \  Doesn't work with new QuickCheck
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
