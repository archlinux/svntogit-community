# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-selection-range-plugin
pkgname=haskell-hls-selection-range-plugin
pkgver=1.0.0.0
pkgrel=12
pkgdesc="HLS Plugin to support smart selection range"
url="https://hackage.haskell.org/package/hls-selection-range-plugin"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-extra' 'haskell-ghcide' 'haskell-hls-plugin-api'
         'haskell-lsp' 'haskell-semigroupoids')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils' 'haskell-lens' 'haskell-lsp-test')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('c04e4a53c77369f6ae0bdea9d9deca84b99b4903cc9eddbb7a28636fcfefcfdd')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
  uusi -u ghcide -u hls-plugin-api -u hls-test-utils $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --datasubdir=haskell-hls-selection-range-plugin --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
