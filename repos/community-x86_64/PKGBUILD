# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libiodbc
pkgver=3.52.12
pkgrel=2
pkgdesc='Independent Open DataBase Connectivity for Linux'
arch=('x86_64')
url='http://www.iodbc.org/dataspace/iodbc/wiki/iODBC/'
license=('LGPL')
makedepends=('chrpath')
source=("https://downloads.sourceforge.net/iodbc/${pkgname}-${pkgver}.tar.gz")
sha256sums=('51c5ff3a7d9a54202486cb77a3514e0e379a135beefcd5d12b96d1901f9dfb62')

build() {
	cd ${pkgname}-${pkgver}

	./configure --prefix=/usr \
		--disable-static \
		--includedir=/usr/include/libiodbc \
		--disable-gui \
		--disable-libodbc
	make
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	chrpath -d ${pkgdir}/usr/bin/iodbctest{,w}
	rm -rf ${pkgdir}/usr/share/libiodbc/
}
