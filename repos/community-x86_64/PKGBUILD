# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Simo Leone <neotuli@gmail.com>

pkgname=t1utils
pkgver=1.41
pkgrel=2
pkgdesc="A collection of simple Type 1 font manipulation programs"
arch=('x86_64')
url="http://www.lcdf.org/~eddietwo/type/#t1utils"
license=("custom")
depends=('glibc')
source=("https://www.lcdf.org/~eddietwo/type/$pkgname-$pkgver.tar.gz"
        "memmem-decl.patch::https://github.com/kohler/t1utils/commit/3f1ddda424353f0f926dd28efa47b0ac61556ce8.patch"
        "LICENSE")
sha512sums=('b532d3af38a73903a512eb1297ba7510747dcbe8362acc4a0f20f2a3c786d82d8f5b22997ed3867114f33153a34cdd71c4c8e6a093db3cecc3825b6e4725a00b'
            '573a15133e8227100820039aaf5dea12d038674dd486510a38647e9f500c9de2c16034d99c8b26444f6c7253fe788833aa6dbd3f46be389bc1ca1b031deb51c8'
            '82dbc206391a9f9116038be74360455c960ddb76e35fdeb123044f74c74014223c3b35d7f1894ae7224c7a9e091acc1ff00df2a43d38a856b4f75988fb4dbd3e')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#57999 and https://github.com/kohler/t1utils/issues/8
  patch -p1 -i "${srcdir}/memmem-decl.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

