# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Miles McLean <mills00013@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Brad Pitcher <bradpitcher@gmail.com>
# Contributor: Moritz Lipp <mlq@pwmt.org>

pkgname=networkmanager-l2tp
pkgver=1.8.6
_pppver=2.4.8
pkgrel=1
pkgdesc='L2TP support for NetworkManager'
arch=(x86_64)
url='https://github.com/nm-l2tp/NetworkManager-l2tp'
license=(GPL2)
depends=(libnm libsecret nss openssl "ppp=$_pppver" xl2tpd)
makedepends=(intltool libnma python)
optdepends=('libreswan: IPSec support (recommended)'
            'strongswan: IPSec support')
source=("$pkgname-$pkgver.tar.gz"::"$url/archive/$pkgver.tar.gz")
sha256sums=('1455b2bc0dd802522e79a176c654d1a19482b6a26d7c3a9c0fce2ee2dbb892e2')

prepare() {
  ln -sf NetworkManager-l2tp-$pkgver $pkgname
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --localstatedir=/var \
    --libexecdir=/usr/lib/NetworkManager \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" install
}
