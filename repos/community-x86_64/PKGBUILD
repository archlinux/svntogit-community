# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=selene
pkgname=selene-linter
pkgver=0.24.0
pkgrel=2
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
options=('!lto' 'debug')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
b2sums=('11d020b1844be9c04fadf0041f4c44e8079d5be45161dc73a7b5f6e9b4010cdc28b4aafe4b5c89d8796370ffd453ad2a2009048d4edaac61cb81a4fa2051b21d')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $_name-$pkgver
  cargo test --locked --offline
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
}

# vim:set ts=2 sw=2 et:
