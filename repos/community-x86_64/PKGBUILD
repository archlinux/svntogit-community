# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=http3
pkgname=haskell-http3
pkgver=0.0.1
pkgrel=2
pkgdesc="HTTP/3 library"
url="https://github.com/kazu-yamamoto/http3"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-case-insensitive' 'haskell-http-types' 'haskell-http2'
         'haskell-network' 'haskell-network-byte-order' 'haskell-quic' 'haskell-sockaddr'
         'haskell-time-manager' 'haskell-unliftio')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-attoparsec' 'haskell-base16-bytestring'
             'haskell-conduit' 'haskell-conduit-extra' 'haskell-cryptonite' 'haskell-doctest'
             'haskell-hspec' 'haskell-hspec-discover' 'haskell-iproute' 'haskell-tls')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('28414a069cb10b0d3a47e6ba07e897da3c953cc2c93562246af795fb256b395c')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -f-devel

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
