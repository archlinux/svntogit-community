# Maintainer:

pkgname=aspell-pt
_pkgverPT=20161001
_pkgverBR=20090702
pkgver=$_pkgverPT
pkgrel=2
pkgdesc="Portuguese and Brazilian Portuguese dictionary for aspell"
arch=('x86_64')
url="http://aspell.net/"
license=('GPL2')
depends=('aspell')
conflicts=('aspell-pt_br')
replaces=('aspell-pt_br')
source=(https://ftp.gnu.org/gnu/aspell/dict/pt_BR/aspell6-pt_BR-$_pkgverBR-0.tar.bz2
        https://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-$_pkgverPT.tar.bz2
        https://natura.di.uminho.pt/download/sources/Dictionaries/aspell6/aspell6.pt-preao-$_pkgverPT.tar.bz2)
sha1sums=('add1db9a6a908dccaad13a7fd85c3b202299ff26'
          '932704e6dece67b0b6f79ac2665de44ef44ae452'
          'bc2ee9ef81fff88ef35820e5dca4feeaf92a6da7')

build() {
  cd aspell6-pt_BR-$_pkgverBR-0
  ./configure
  make

  cd ../aspell6-pt_PT-$_pkgverPT-0
  ./configure
  make

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  ./configure
  make
}

package() {
  cd aspell6-pt_BR-$_pkgverBR-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  cd ../aspell6-pt_PT-preao-$_pkgverPT-0
  make DESTDIR="$pkgdir" install

  echo "add pt_BR.multi" > "$pkgdir"/usr/lib/aspell-0.60/brasileiro.alias
}
