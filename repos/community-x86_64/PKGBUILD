# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.25.0
pkgrel=1
pkgdesc='DDE window manager forked from KWin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-kwin"
license=('GPL3')
depends=(qt5-declarative qt5-x11extras kconfig kconfigwidgets kcoreaddons kcrash kdbusaddons
         kglobalaccel ki18n kiconthemes kidletime knotifications kpackage plasma-framework
         kwidgetsaddons kwindowsystem kcompletion kcmutils knewstuff kservice ktextwidgets kxmlgui
         krunner kdecoration kscreenlocker dwayland xcb-util-cursor xorg-xwayland libpipewire breeze
         lcms2 libqaccessibilityclient qt5-quickcontrols python)
makedepends=(extra-cmake-modules kdoctools ninja qt5-tools wayland-protocols
             deepin-wayland-protocols)
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-kwin/archive/$pkgver.tar.gz")
sha512sums=('c9160483ae76ca8027801550a596097ad40a57104998c93e74fb8ba2e909cf9e1518b1fcb5c2d13507a6ccfa68174a634413e6259b7980f7954c04bc84e000c4')

prepare() {
  cd deepin-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/effects/multitaskview/multitaskview.cpp
}

build() {
  cd deepin-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBEXECDIR=lib
  ninja
}

package() {
  cd deepin-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install
}
