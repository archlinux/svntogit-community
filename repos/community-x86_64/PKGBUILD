# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=16
pkgrel=1
pkgdesc="Jack desktop mixer application"
arch=('x86_64')
url="https://github.com/jack-mixer/jack_mixer"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'python-cairo'
'python-gobject' 'python-xdg')
makedepends=('cython' 'glib2' 'jack2' 'meson' 'python-docutils')
optdepends=('new-session-manager: for session management')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jack-mixer/${pkgname}/archive/release-${pkgver//./-}.tar.gz")
sha512sums=('4145c034c3e2a224ead598f3859761b192fddcd02f222593ebed15e409ef82615840a0090a60984aeb20a313e9e3a956d93fdc65bc07f0063f8ec9cd8559e58b')
b2sums=('62bd2f49a2e19dc5e55bc5373d9d3a3c243d21ac8a7b2b8acc6186f36ad66877cc51ba595de8040669191f8512789a52b5c7d6da7a53bf8104614c9daf62946f')

prepare() {
  mv -v "$pkgname-release-${pkgver//./-}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 *.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
