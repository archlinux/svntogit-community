# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Frank Tetzel <frank.tetzel@mailbox.tu-dresden.de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes
pkgver=1.51
pkgrel=23
pkgdesc='Super Nintendo emulator'
url='https://www.zsnes.com/'
arch=(x86_64)
license=(GPL)
options=(!buildflags)
makedepends=(gendesk mesa nasm)
depends=(lib32-libpng lib32-opengl-driver lib32-sdl)
optdepends=('lib32-alsa-lib: sound support for ALSA'
            'lib32-libpulse: sound support for Pulse Audio')
source=("https://downloads.sourceforge.net/project/zsnes/zsnes/ZSNES%20v$pkgver/zsnes${pkgver/.}src.tar.bz2"
        zsnes.patch
        zsnes-1.51-libpng15.patch
        zsnes-1.51-gamepad.patch
        zsnes.png)
sha512sums=('b10373cf81b201a33dc60c728431d4f69d36de31fcf6f137b623ea206a2998f722dcd6132daec1d0bf14a05ae52f3ee9ecee4334517039be554db85c9f7b8274'
            'a89bd37a800627f80e5385be83dde0db7da2570d1479a4d265c2493033bc57e46d220ee496eff9353e7c61b4b5e321b7db8199334227bfb9b7f65b25fec52012'
            '4191987cad2f8dd7d7fd1d4f8f0c60de07059f18a60cb9bb94f1e5c35c06122032de9887762ebcf2d255f24c3e90aa5b4f6a7540a8e4dbdc4ce5f7d3b3e3b532'
            '1eb97498441a112b8abb5db52e8608a81556eca9443e3bfe86a3ba9b479bf4bfcba30e7d43becda9c9888f8af8667530d56c7d38446430d9b2dd12675c7a5854'
            '50a3392de384f3279ec2c8aa0d7e4cedca4b0ff0b8dc6b1b265a84d8e5d91589b23fa52ae0f083c93e638ee9a785b2f18636de6146145dc09d13174bef2511f6')
_dir=${pkgname}_${pkgver/./_}

prepare() {
  cd $_dir
  patch -p1 -i ../zsnes.patch
  patch -p0 -i ../zsnes-1.51-libpng15.patch
  patch -p1 -i ../zsnes-1.51-gamepad.patch
}

build() {
  cd $_dir/src
  export CC="gcc $CFLAGS -m32 -fcommon -w"
  export CXX="g++ $CXXFLAGS -m32 -std=c++14 -w"
  ./configure \
    --enable-release \
    --prefix=/usr \
    force_arch=$CARCH \
    x_libraries=/usr/lib
  make
}

package() {
  gendesk "$startdir/PKGBUILD"
  install -Dm644 zsnes.desktop "$pkgdir/usr/share/applications/zsnes.desktop"
  install -Dm644 zsnes.png "$pkgdir/usr/share/pixmaps/zsnes.png"
  make -C $_dir/src DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share"
  mv "$pkgdir/usr/man" "$pkgdir/usr/share"
}
