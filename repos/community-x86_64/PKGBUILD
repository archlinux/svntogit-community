# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor:  3ED <kas1987@o2.pl>

pkgname=mcabber
pkgver=0.10.1
pkgrel=4
pkgdesc="A small Jabber console client, includes features: SSL, PGP, MUC, UTF8"
arch=('i686' 'x86_64')
url="http://www.lilotux.net/~mikael/mcabber/"
license=('GPL')
depends=('ncurses' 'glib2' 'loudmouth' 'gpgme' 'libotr3' 'aspell')
makedepends=('pkg-config')
options=('!libtool')
source=(http://www.lilotux.net/~mikael/mcabber/files/$pkgname-${pkgver/_/-}.tar.bz2)
md5sums=('fe96beab30f535d5d6270fd1719659b4')

build() {
  cd "${srcdir}"/$pkgname-${pkgver/_/-}

  sed -i 's|lotr|lotr3|' configure
  sed -i 's|libotr/version.h|libotr3/version.h|' configure
  sed -i 's|#include <libotr|#include <libotr3|' mcabber/otr.h

  ./configure --prefix=/usr --enable-hgcset \
      --mandir=/usr/share/man \
      --enable-aspell --enable-otr
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver/_/-}

  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/usr/share/mcabber/example
  msg "Copying mcabberrc.example to usr/share/mcabber/example/mcabberrc"
  cp -a "${srcdir}"/$pkgname-${pkgver/_/-}/mcabberrc.example \
	"${pkgdir}"/usr/share/mcabber/example/mcabberrc
}

