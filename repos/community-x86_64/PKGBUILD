# Maintainer: SÃ©bastien Luttringer
# Contributor: Patrik Cyvoct <patrik at ptrk dot io>
# Contributor: Mikkel Oscar Lyderik <mikkeloscar at gmail dot com>

pkgname=scaleway-cli
pkgver=2.5.6
pkgrel=2
pkgdesc='Command line interface for Scaleway Cloud'
arch=('x86_64')
url='https://www.scaleway.com/en/cli/'
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("git+https://github.com/scaleway/scaleway-cli.git#tag=v$pkgver"
        'https://github.com/scaleway/scaleway-cli/commit/d244b629192fc62a3e54e673474e34baef7285ec.patch')
sha256sums=('SKIP'
            '0316f937b36e8fc48d1a250d75528705d647e7888e46257158998772c2767764')

prepare() {
  cd $pkgname
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
  :
}

build() {
  cd $pkgname
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  go build \
    -buildmode=pie \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X main.Version=$pkgver-$pkgrel -X main.BuildDate=$(date -u '+%Y-%m-%dT%I:%M:%S%p') -X main.DisableVersionCheck=true"  \
    -o scw ./cmd/scw/main.go
  PATH=. scw autocomplete script shell=zsh > zshcomp
  PATH=. scw autocomplete script shell=bash > bashcomp
}

check() {
  cd $pkgname
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  go test ./... \
    -buildmode=pie \
    -trimpath \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X main.Version=$pkgver-$pkgrel -X main.BuildDate=$(date -u '+%Y-%m-%dT%I:%M:%S%p')"
}

package() {
  cd $pkgname
  install -Dm755 scw "$pkgdir"/usr/bin/scw
  install -Dm644 bashcomp "$pkgdir"/usr/share/bash-completion/completions/scw
  install -Dm644 zshcomp "$pkgdir"/usr/share/zsh/site-functions/_scw
}

# vim:set ts=2 sw=2 ft=PKGBUILD et:
