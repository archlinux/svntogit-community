# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.4.3
pkgrel=2
pkgdesc='Professional vector animation program (CLI renderer only)'
arch=('x86_64')
url='https://www.synfig.org/'
license=('GPL3')
depends=('boost-libs' 'fftw' 'imagemagick' 'libjpeg-turbo' 'libmng' 'libxml++2.6' 'mlt' 'openexr'
         'pango')
makedepends=('boost' 'etl' 'intltool')
source=("https://downloads.sourceforge.net/synfig/$pkgname-$pkgver.tar.gz"
         https://github.com/synfig/synfig/commit/54ba18f2.patch)
sha256sums=('0c8d02ff6c77e468021a4497be34323d38246208d199ab1c3bbf65c9794752e7'
            'd64bb9ef67e003323f9f4ddab5dff46371516b1e090896aab9b001b0139bbb0e')

prepare() {
  cd $pkgname-$pkgver
  patch -p2 -i ../54ba18f2.patch # Support MLT 7
  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
