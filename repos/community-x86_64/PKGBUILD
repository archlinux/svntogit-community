# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=fontobene-qt5
pkgver=0.1.0
pkgrel=2
pkgdesc='A header-only library to parse FontoBene stroke fonts with C++/Qt5'
arch=('x86_64')
url='https://github.com/fontobene/fontobene-qt5'
license=('MIT' 'Apache')
depends=('qt5-base')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        "$pkgname.pc")
sha512sums=('33f1d2a4245f54749d357f07ef8bc5ec6cbc1b66a2190dcf958b6e8175b69c415811835738e330f6801db852086792fce4378f256ad2154257ce37bed8bf108b'
            'c107b1060e6ac75ce96cff23b40018f65f54f7f30025586eed7e3a9f067c241c43d8abb603186ec96103af63c36aac0e8330f8fe65aeb4ace679209412c08244')

prepare() {
  sed -i "s|%PKGVER%|$pkgver|g" $pkgname.pc
}

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  qmake -r ../$pkgname.pro \
      PREFIX=/usr \
      QMAKE_CFLAGS_RELEASE="$CFLAGS" \
      QMAKE_CXXFLAGS_RELEASE="$CXXFLAGS"

  make
}

check() {
  cd $pkgname-$pkgver/build

  make check
}

package() {
  cd $pkgname-$pkgver/build

  make INSTALL_ROOT="$pkgdir" install

  install -Dm 644 "$srcdir"/$pkgname.pc "$pkgdir"/usr/lib/pkgconfig/$pkgname.pc

  install -Dm 644 ../LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
  install -Dm 644 ../LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

