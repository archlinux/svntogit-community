# Maintainer: Caleb Maclennan <caleb@alerque.com
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cocogitto
_bin=cog
pkgver=5.3.1
pkgrel=1
pkgdesc='Set of CLI tools for the conventional commit and semver specifications'
arch=(x86_64)
url="https://github.com/$pkgname/$pkgname"
license=(MIT)
depends=(git
         gcc-libs
         libgit2
         libgit2.so
         zlib)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('ac6847ce55ba284184d0792afb53c6579da415600bc1b01c180dd87ad34597d0')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
	mkdir {completions,man}
}

build() {
	cd "$_archive"
	cargo build --frozen --release
	local compgen="target/release/$_bin generate-completions"
	$compgen bash > "completions/$_bin"
	$compgen fish > "completions/$_bin.fish"
	$compgen zsh  > "completions/_$_bin"
	local mangen="target/release/$_bin generate-manpage"
	$mangen cog > "man/$_bin.1"
	for cmd in 'check' 'commit' 'edit' 'log' 'verify' \
		'changelog' 'bump' 'init' 'install-hook' \
		'generate-completions'; do
		$mangen $cmd > "man/$_bin-$cmd.1"
	done
}

check() {
	cd "$_archive"
	# Test suite is not atomic, relies on user environment such as git user configs
	# cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/bash-completion/completions/" "completions/$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "completions/$_bin.fish"
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "completions/_$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	find man/ -type f -exec install -Dm0644 -t "$pkgdir/usr/share/man/man1" {} \;
}
