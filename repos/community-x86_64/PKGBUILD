# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=4.27.0
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2fly/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2fly/v2ray-core/archive/v$pkgver.tar.gz"
        'v2ray.service')
sha512sums=('9fa588e324d11afc1e574da1e43628fb39b8823b8c4ceae79ec13d6747b9f647b704bf5b85fac0d0992ff1552eac88990bff7f0702f778cf52dde6253d5438ac'
            '507cb9033ced1ae7886ea186461edb906e2ec7125aa9465c02f7c49e631604b718952d0ddfc1f90636c87a3ab6accd080f65f54c8e969e889eb4f79c23cdfcd9')

build() {
  cd "v2ray-core-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o v2ray ./main
  go build -o v2ctl ./infra/control/main
}

check() {
  cd "v2ray-core-$pkgver"
  go test -p 1 -tags json -v -timeout 30m v2ray.com/core/...
}

package() {
  cd v2ray-core-$pkgver
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  sed -i -e 's|/usr/bin/v2ray/v2ray|/usr/lib/v2ray/v2ray|' \
         "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 v2ray -t "$pkgdir"/usr/lib/v2ray/
  install -Dm755 v2ctl -t "$pkgdir"/usr/lib/v2ray/
  install -dm755 "$pkgdir"/usr/bin
  ln -s ../lib/v2ray/v2ray "$pkgdir"/usr/bin/v2ray
  ln -s ../lib/v2ray/v2ctl "$pkgdir"/usr/bin/v2ctl

  install -Dm644 "$srcdir"/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
}
