# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openfec
pkgver=1.4.2.7
pkgrel=1
pkgdesc="Open Forward Erasure Codecs"
arch=(x86_64)
url="http://openfec.org/"
license=(custom:CeCILL-C)
depends=(glibc)
makedepends=(cmake)
provides=(libopenfec.so)
# NOTE: we are using roc-streaming's version, because the original upstream is effectively dead:
# https://github.com/roc-streaming/openfec
source=(https://github.com/roc-streaming/openfec/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('70de4fb4e7b2c104e88c5fdec44def2018cdbe987d28e0324652158320b6e39ac8508958d8da16854f136dc182bc44453a77dbdec1568b0068d30f060e34d22b')
b2sums=('b09147a0abbd85935f179da066cd26f6f0d7f9b3e4149253fb58a2e57ca3bf97176eb492eea3581b5767809b477164e7177b9e298f644afa92fd00f8f14a3c15')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DBUILD_SHARED_LIBS=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{LICENCE,Licence}* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
