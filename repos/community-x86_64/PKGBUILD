# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=clxclient
pkgver=3.9.0
pkgrel=4
pkgdesc="C++ wrapper library around the X Window System API"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('LGPL')
depends=('libxft')
makedepends=('clthreads')
source=("${url}downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('4bbd621ceeebf3dfc36f0a3d02c63ba1c0e818e691e5c9681e737710824f53a58c682fb2475a7a2bba4f2dfd520166b782977320ceb73de39448cf512e26c842')

prepare() {
  cd "$pkgname-$pkgver"
  # patch call to ldconfig to be not global (upstream fix requested)
  sed -i 's/ldconfig/& -N $(PREFIX)\/$(LIBDIR)/' Makefile
}

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" LIBDIR=lib
}

package(){
  cd "${pkgname}-${pkgver}"
  # install of include dir due to broken Makefile (upstream fix requested)
  install -d "${pkgdir}/usr/include"
  make PREFIX="${pkgdir}/usr" LIBDIR=lib install
}

