# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: David Moore <davidm@sjsoft.com>

pkgname=python-simplejson
pkgver=3.17.3
pkgrel=1
pkgdesc='Simple, fast, extensible JSON encoder/decoder for Python'
license=('MIT')
arch=('x86_64')
url='https://github.com/simplejson/simplejson'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simplejson/simplejson/archive/v$pkgver.tar.gz")
sha512sums=('eb8bae7cb14b9f3cdcc0fe8ce7fc9edde09099c054d6335d0afc26cfaa16657d580b095fd8879bb48856be82050824015e9636942dafe95c888ee2db43adfb97')

build() {
  cd "$srcdir"/simplejson-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/simplejson-$pkgver
  python setup.py pytest
}

package_python-simplejson() {
  cd simplejson-$pkgver
  python setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir"/simplejson-$pkgver/LICENSE.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
