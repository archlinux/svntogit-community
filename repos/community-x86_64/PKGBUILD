# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.0.16
pkgrel=1
pkgdesc="A fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'openssl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('7fb7233fcb770ec0893d35244173a656d2316655aa85226c99d1fad5387dace77c5d0deb0d0a47a11c27012c9b6ebdfe90fd881b1d05a7d34eaf26ab2fbe7b96')
b2sums=('d5157daafa8d3e8863054daae151506cdb83417b7e63ad7a08f1c7204297f173989a6622305c4f9f19027b50d77cd920d11ad88d5b4dd90ea6bcc4216c3f685b')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
