# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-security
pkgname=('python-zope-security' 'python2-zope-security')
pkgver=4.2.2
pkgrel=2
pkgdesc="Zope Security Framework"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.security"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-component' 'python2-zope-component'
             'python-zope-i18nmessageid' 'python2-zope-i18nmessageid' 'python-zope-location'
             'python2-zope-location' 'python-zope-proxy' 'python2-zope-proxy'
             'python-zope-schema' 'python2-zope-schema')
checkdepends=('python-btrees' 'python2-btrees' 'python-nose' 'python2-nose'
              'python-zope-configuration' 'python2-zope-configuration' 'python-zope-testing'
              'python2-zope-testing' 'python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.security/archive/$pkgver.tar.gz")
sha512sums=('33e84e7a0015c305bf07a1593e6eba64a27b878dd122a5058681d030c83ac711002689a25561afe4ab93d914890749118d57e63e8510b9e1c9ad48965350a9f3')

prepare() {
  cp -a zope.security-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.security-$pkgver
  python setup.py build

  cd "$srcdir"/zope.security-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.security-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.security-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-security() {
  depends=('python-setuptools' 'python-zope-component' 'python-zope-i18nmessageid'
           'python-zope-interface' 'python-zope-location' 'python-zope-proxy' 'python-zope-schema')

  cd zope.security-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-security() {
  depends=('python2-setuptools' 'python2-zope-component' 'python2-zope-i18nmessageid'
           'python2-zope-interface' 'python2-zope-location' 'python2-zope-proxy'
           'python2-zope-schema')

  cd zope.security-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
