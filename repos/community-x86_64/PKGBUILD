# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=pgadmin4
pkgver=4.2
pkgrel=1
pkgdesc='Comprehensive design and management interface for PostgreSQL'
url='https://www.pgadmin.org/'
arch=('x86_64')
license=('custom')
depends=('qt5-base' 'postgresql-libs' 'hicolor-icon-theme'
         'python' 'python-blinker' 'python-extras' 'python-fixtures'
         'python-flask' 'python-flask-login' 'python-flask-migrate'
         'python-flask-sqlalchemy' 'python-flask-wtf' 'python-html5lib'
         'python-passlib' 'python-mimeparse' 'python-pytz' 'python-simplejson'
         'python-six' 'python-speaklater' 'python-sqlparse' 'python-wtforms'
         'python-psutil' 'python-jinja' 'python-paramiko' 'python-psycopg2'
         'python-cryptography' 'python-sqlalchemy' 'python-testtools'
         'python-webencodings' 'python-werkzeug' 'python-dateutil'
         'python-pyrsistent' 'python-pbr' 'python-flask-gravatar'
         'python-flask-mail' 'python-flask-principal' 'python-flask-paranoid'
         'python-htmlmin' 'python-flask-htmlmin' 'python-sshtunnel'
         'python-flask-security')
makedepends=('python-setuptools' 'python-sphinx' 'imagemagick')
source=(https://ftp.postgresql.org/pub/pgadmin/pgadmin4/v${pkgver}/source/${pkgname}-${pkgver}.tar.gz{,.asc}
        pgAdmin4.desktop
        config_distro.py
        config_local.py
        pgadmin4-python-de-vendor-venv-paths.patch)
validpgpkeys=('E8697E2EEF76C02D3A6332778881B2A8210976F2') # Package Manager (Package Signing Key) <packages@pgadmin.org>
sha512sums=('d3d242904c65f0af871890954c24005b4849b88599cde9a7cb82e077ff5db5a8c5f015c4af0b135d68f02daad42492af71373831c2212a36672af8d7621c3b2a'
            'SKIP'
            'b19dda3331585010c759099eb09f4db288ce4cd3d36882b56748e1e3756dc7bee2899d7438d496280498ec6a60f6e1ba90309d49fc599403f1fdc7e8817b6645'
            '16d00dc2095904a6b12da7039458f632873829ad98d4d7653eac5804032ba92097ccae4488d56467d0ea9bd64e2654a3dead73eb7924c947ff1737ff6e3b4745'
            'cf83e1357eefb8bdf1542850d66d8007d620e4050b5715dc83f4a921d36ce9ce47d0d13c5d85f2b0ff8318d2877eec2f63b931bd47417a81a538327af927da3e'
            '70c8e589421ab50cf81753fbcfb5df65669b0feeefa3a57ee574b3785424fd107af720fc7007e52876798a46fae88f2c02dbf156aa096397afa0f85ac04fdb76')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 < ../pgadmin4-python-de-vendor-venv-paths.patch

  local PYTHONVERSION="$(python -c 'import sys; print(f"{sys.version_info.major}.{sys.version_info.minor}")')"
  sed -E "s|/usr/pgadmin4/web|/usr/lib/pgadmin4/web|g;
          s|/usr/pgadmin4/lib/python[0-9\\.]+|/usr/lib/python${PYTHONVERSION}|g" \
      -i runtime/ConfigWindow.ui
  sed "s|##PYTHONVERSION##|${PYTHONVERSION}|g" -i runtime/Server.cpp

  sed -E -i requirements.txt \
    -e '/blinker>?=/d' \
    -e '/extras>?=/d' \
    -e '/fixtures>?=/d' \
    -e '/Flask>?=/d' \
    -e '/Flask-Login>?=/d' \
    -e '/Flask-Migrate>?=/d' \
    -e '/Flask-SQLAlchemy>?=/d' \
    -e '/Flask-WTF>?=/d' \
    -e '/html5lib>?=/d' \
    -e '/pycrypto>?=/d' \
    -e '/passlib>?=/d' \
    -e '/python-mimeparse>?=/d' \
    -e '/pytz>?=/d' \
    -e '/simplejson>?=/d' \
    -e '/six>?=/d' \
    -e '/speaklater>?=/d' \
    -e '/sqlparse>?=/d' \
    -e '/WTForms>?=/d' \
    -e '/psutil>?=/d' \
    -e '/psycopg2>?=/d' \
    -e '/python-dateutil>?=/d' \
    -e '/SQLAlchemy>?=/d' \
    -e '/pyrsistent>?=/d' \
    -e '/pbr>?=/d' \
    -e '/linecache2>?=/d' \
    -e '/Flask-Gravatar>?=/d' \
    -e '/Flask-Mail>?=/d' \
    -e '/Flask-Principal>?=/d' \
    -e '/Flask-Paranoid>?=/d' \
    -e '/htmlmin>?=/d' \
    -e '/Flask-Security>?=/d' \
    -e '/Flask-HTMLmin>?=/d' \
    -e '/sshtunnel>?=/d'
  cat requirements.txt
}

build() {
  export LANG=en_US.UTF-8
  export LC_ALL=en_US.UTF-8

  cd ${pkgname}-${pkgver}
  convert +set date:create +set date:modify runtime/pgAdmin4.{ico,png}
  make docs

  cd runtime
  qmake CONFIG+=release
  make
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 runtime/pgAdmin4 "${pkgdir}/usr/lib/pgadmin4/runtime/pgAdmin4"
  cp -a docs web "${pkgdir}/usr/lib/pgadmin4"
  install -Dm 644 "${srcdir}"/config_{distro,local}.py -t "${pkgdir}/usr/lib/pgadmin4/web"

  install -Dm 644 runtime/pgAdmin4-0.png "${pkgdir}/usr/share/icons/hicolor/256x256/apps/pgAdmin4.png"
  install -Dm 644 runtime/pgAdmin4-1.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/pgAdmin4.png"
  install -Dm 644 runtime/pgAdmin4-2.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/pgAdmin4.png"
  install -Dm 644 runtime/pgAdmin4-3.png "${pkgdir}/usr/share/icons/hicolor/16x16/apps/pgAdmin4.png"
  install -Dm 644 "${srcdir}/pgAdmin4.desktop" -t "${pkgdir}/usr/share/applications"

  install -D /dev/stdin "${pkgdir}/usr/bin/pgadmin4" <<END
#!/bin/sh
cd /usr/lib/pgadmin4
exec runtime/pgAdmin4 "\$@"
END
  install -D /dev/stdin "${pkgdir}/usr/bin/pgadmin4-server" <<END
#!/bin/sh
cd /usr/lib/pgadmin4
python web/pgAdmin4.py "\$@"
END

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
