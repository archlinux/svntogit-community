# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=llvmlite
pkgbase="python-${_pkgname}"
pkgname=("${pkgbase}")
pkgver=0.38.0
pkgrel=1
pkgdesc='A Lightweight LLVM Python Binding for Writing JIT Compilers'
arch=('x86_64')
makedepends=('cmake' 'llvm11' 'git' 'python-setuptools' 'python-setuptools-scm')
depends=('python' 'llvm11-libs')
url="https://github.com/numba/${_pkgname}"
license=('BSD')

source=("${_pkgname}-${pkgver}::git+${url}")
sha256sums=('SKIP')

build() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py test
}

package() {
    cd "${srcdir}/${_pkgname}-${pkgver}"

    python setup.py install --root="${pkgdir}/" --optimize=1

    install -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
