# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=calc
pkgver=2.13.0.1
pkgrel=2
pkgdesc="Arbitrary precision console calculator"
arch=('x86_64')
url="http://www.isthe.com/chongo/tech/comp/calc/"
license=('GPL')
depends=('readline')
makedepends=('chrpath')
#source=("http://switch.dl.sf.net/sourceforge/calc/calc-$pkgver.tar.bz2")
source=("http://www.isthe.com/chongo/src/calc/calc-$pkgver.tar.bz2")
sha256sums=('6ae538f57785c5701a70112ccf007ab5553abd332ae2deaadaf564f401c734ad')

prepare() {
  cd "$srcdir/calc-$pkgver"
  sed -i 's/${CC} ${LIBCALC_SHLIB}/${CC} ${LDFLAGS} ${LIBCALC_SHLIB}/' Makefile
  sed -i 's/${CC} ${LIBCUSTCALC_SHLIB}/${CC} ${LDFLAGS} ${LIBCUSTCALC_SHLIB}/' custom/Makefile
}

build() {
  cd "$srcdir/calc-$pkgver"
  make USE_READLINE="-DUSE_READLINE" READLINE_LIB="-lreadline" READLINE_EXTRAS="-lhistory -lncurses" -j1 \
    EXTRA_CFLAGS="$CPPFLAGS $CFLAGS" EXTRA_LDFLAGS="$LDFLAGS" ARCH_CFLAGS='' LD_SHARE='' DEBUG=''
}

package() {
  cd "$srcdir/calc-$pkgver"
  make T="$pkgdir" install
  mv "$pkgdir/usr/bin/cscript" "$pkgdir/usr/share/calc/"
  chrpath -d "$pkgdir/usr/bin/calc"
}
