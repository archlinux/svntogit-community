# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=4.5.1
pkgrel=3
pkgdesc="A viewer and analyser of music audio files."
arch=(x86_64)
url="https://www.sonicvisualiser.org/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libmad libx11
opusfile qt5-base qt5-svg)
makedepends=(alsa-lib bzip2 capnproto fftw libfishsound libglvnd libid3tag
liblo liblrdf liboggz libpulse libsamplerate libsndfile meson portaudio
rubberband serd sord speex)
provides=(vamp-host)
source=(https://github.com/$pkgname/$pkgname/releases/download/sv_v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('f65903b1255836fc870e91d16efcc0297db97ed08cbc6c980c876c843ec7c99af504f2e5f706d4e7ffd8aa7c4e04164c71c56da8e2184d8774b5358c425ab72b')
b2sums=('61b23a2db603476635bbf241c955dde5f0375286c6266f3669b841d3d77c2f51c6c600ebc0f4b8c2cc1129af725cbc7246aefabc5defb7bbc286edc2175aa0ee')

build() {
  arch-meson build $pkgname-$pkgver
  ninja -C build
}

package() {
  depends+=(
    alsa-lib libasound.so
    bzip2 libbz2.so
    capnproto libcapnp.so libkj.so
    fftw libfftw3.so libfftw3f.so
    libfishsound libfishsound.so
    libglvnd libGL.so
    libid3tag libid3tag.so
    liblo liblo.so
    liblrdf liblrdf.so
    liboggz liboggz.so
    libpulse libpulse.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
    portaudio libportaudio.so
    rubberband librubberband.so
    serd libserd-0.so
    sord libsord-0.so
    speex libspeex.so
  )

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/samples/* -t "$pkgdir/usr/share/$pkgname/samples"
  install -vDm 644 $pkgname-$pkgver/templates/* -t "$pkgdir/usr/share/$pkgname/templates"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,CITATION,README{,_OSC}.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
