# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=bespokesynth
pkgver=1.1.0
_commit=bd41884b9032b585c6271b6d91f8942d3fac4e2b  # v1.1.0
pkgrel=4
pkgdesc="A software modular synth"
arch=(x86_64)
url="https://www.bespokesynth.com/"
license=(GPL3)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libglvnd libpng python zlib)
makedepends=(alsa-lib cmake flac git jack juce libusb libvorbis libx11
libxcursor libxinerama pybind11 tuning-library xorg-xrandr)
optdepends=(
  'jack: to use with JACK'
  'python-jedi: for code completion'
)
provides=(vst3-host)
source=(
  "$pkgname::git+https://github.com/${pkgname}/${pkgname}#commit=${_commit}"
  "MTS-ESP::git+https://github.com/ODDSound/MTS-ESP"
)
sha512sums=('SKIP'
            'SKIP')
b2sums=('SKIP'
        'SKIP')

prepare() {
  cd "$pkgname"
  git cherry-pick -n d134b0aa015c9876f9bba9a68ecd910cd71b0ba1  # remove 3dxware
  git cherry-pick -n 5b44c1ba7228e8b3cb5076099b087dfa14fa56a9  # devendor jsoncpp
  git cherry-pick -n f4490398600e33054be6e4e05298d9d60d4724f7  # devendor pybind11
  git cherry-pick -n f900e132f30fc6a266467f815183f20c417925b6  # devendor juce
  git cherry-pick -n 7e98b949c1be23d0cae3bf80b479ae3a933fe8d5  # devendor tuning-library

  git submodule init
  git config submodule.libs/oddsound-mts/MTS-ESP.url "${srcdir}/MTS-ESP"
  git submodule deinit libs/JUCE
  git submodule deinit libs/pybind11
  git submodule deinit libs/tuning-library
  git submodule deinit libs/json/jsoncpp
  git submodule update

}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DBESPOKE_SYSTEM_PYBIND11=ON \
        -DBESPOKE_SYSTEM_JSONCPP=ON \
        -DBESPOKE_SYSTEM_TUNING_LIBRARY=ON \
        -DBESPOKE_SYSTEM_JUCE=ON \
        -DBESPOKE_DEVENDORED_SYSTEM_JUCE=ON \
        -Wno-dev \
        -B build \
        -S "$pkgname"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libFLAC.so libfreetype.so libjpeg.so libjsoncpp.so
  libogg.so libusb-1.0.so libvorbis.so libvorbisenc.so libvorbisfile.so)

  make DESTDIR="$pkgdir/" install -C build
}
