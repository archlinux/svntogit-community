# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gammastep
pkgver=2.0.8
pkgrel=1
pkgdesc="Adjust the color temperature of your screen according to your surroundings."
arch=(x86_64)
url="https://gitlab.com/chinstrap/gammastep"
license=(GPL3)
depends=(geoclue glibc hicolor-icon-theme libdrm libx11 libxcb
libxxf86vm wayland)
makedepends=(glib2 intltool python systemd)
optdepends=('python-gobject: for gammastep-indicator'
            'python-xdg: for gammastep-indicator'
            'gtk3: for gammastep-indicator')
source=("https://gitlab.com/chinstrap/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('c43816931c44f6fe4f9840849ec0e8a857612fe4ab03785fa14d96a41c6b4235b09bf2c892dedcc9437db36f1f876454ae9b25b1496ae998706ef23eb7628361')
b2sums=('1085c71a9bc9f096aae1fa11cd72820636f8dd9a972ffe51b99c00d2051236a455d55c16dcbc6801d7bbd2b1f5f22d410451252158f48ae06581585bd38d7255')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=(libgio-2.0.so libgobject-2.0.so libglib-2.0.so)

  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {CONTRIBUTING,NEWS,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
