# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2017.0.0
pkgrel=1
pkgdesc='Panorama photo stitcher'
url='http://hugin.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'swig')
depends=('wxgtk3' 'boost-libs' 'enblend-enfuse' 'exiv2' 'libpano13' 'glew'
         'python' 'lensfun' 'lapack' 'make' 'sqlite' 'perl-image-exiftool')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/r/_r}.tar.bz2")
sha256sums=('86646ea27f32b959d38e7d36270703214b1ae961e379dd64b14d027139cd4609')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	install -d build
	cd build
	cmake .. \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LAPACK=yes \
		-DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	cd build
	make DESTDIR="${pkgdir}" install
}
