# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=cadaver
pkgver=0.23.3
pkgrel=9
pkgdesc="Command-line WebDAV client for Unix"
arch=('i686' 'x86_64')
url="http://www.webdav.org/cadaver"
license=('GPL')
depends=('neon')
validpgpkeys=('6D0C74985535060B23300BFE47F6969D0E44CF0C')
source=(http://www.webdav.org/cadaver/cadaver-$pkgver.tar.gz{,.asc}
        disable-sslv2.patch)
md5sums=('502ecd601e467f8b16056d2acca39a6f'
         'SKIP'
         'cbcf9db91efe751c523d6835d2abee43')

prepare() {
  cd "$srcdir"/cadaver-$pkgver
  patch -Np1 -i ../disable-sslv2.patch
}

build() {
  cd "$srcdir"/cadaver-$pkgver
  ./configure --prefix=/usr --with-ssl
  make
}

package() {
  cd "$srcdir"/cadaver-$pkgver
  make prefix="$pkgdir"/usr install
}
