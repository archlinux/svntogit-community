# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

_name=mirall
pkgname=owncloud-client
pkgver=1.7.1
pkgrel=3
pkgdesc='ownCloud client based on mirall'
arch=('i686' 'x86_64')
url='http://owncloud.org/'
license=('GPL2')
depends=('qtkeychain-qt4' 'qtwebkit' 'neon')
makedepends=('cmake' 'python-sphinx')
optdepends=('python2-nautilus: integration with Nautilus')
install=owncloud-client.install
backup=('etc/ownCloud/sync-exclude.lst')
source=("https://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2"
        "https://download.owncloud.com/desktop/stable/${_name}-${pkgver}.tar.bz2.asc")
md5sums=('5355f5dee2beb2d2dc39c8ad77511c0b'
         'SKIP')
validpgpkeys=('F8A819CEB7B10C78D032F028C2B0EE195647594B')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../${_name}-${pkgver} \
        -DBUILD_WITH_QT4=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc/$pkgname \
        
  make
  make doc-man
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
