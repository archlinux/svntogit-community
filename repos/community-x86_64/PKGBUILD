# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fabien Dubosson <fabien.dubosson@gmail.com>
# Contributor: Konstantin Gizdov <arch@kge.com>

pkgname=xxhash
pkgver=0.7.4
pkgrel=1
pkgdesc='Extremely fast non-cryptographic hash algorithm'
arch=(x86_64)
url=https://cyan4973.github.io/xxHash/
license=(
  GPL2
  BSD
)
makedepends=(git)
provides=(libxxhash.so)
_tag=173e50be0509c6fb6c1bb74d95049ef61d7fdced
source=(git+https://github.com/Cyan4973/xxHash.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd xxHash

  git describe --tags | sed 's/^v//'
}

build() {
  make PREFIX=/usr -C xxHash
}

package() {
  make PREFIX=/usr DESTDIR="${pkgdir}" -C xxHash install
  install -Dm 644 xxHash/LICENSE -t "${pkgdir}"/usr/share/licenses/xxhash
}

# vim: ts=2 sw=2 et:
