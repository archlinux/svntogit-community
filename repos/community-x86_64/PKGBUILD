# $Id$
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=ibus
pkgver=1.4.1
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('dbus-python>=0.84.0' 'gconf' 'dconf' 'pyxdg' \
         'iso-codes' 'librsvg' 'python-notify' 'hicolor-icon-theme')
makedepends=('intltool' 'gobject-introspection')
optdepends=('notification-daemon')
options=('!libtool')
install=ibus.install
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('88ef7dda720cec2de92b8673832a702e')

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=python2
  sed -i 's|--pkg=ibus-1.0||' src/Makefile.in
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
    --enable-gtk3

# python2 fix
  for file in setup/ibus-setup.in ui/gtk/ibus-ui-gtk.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  make
}

package() {
  cd ${pkgname}-${pkgver}
  export PYTHON=python2
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/etc/xdg/autostart"
  ln -s /usr/share/applications/ibus.desktop "${pkgdir}/etc/xdg/autostart/ibus.desktop"
  find "${pkgdir}" -type f -exec sed -i 's_exec /usr/bin/python_exec /usr/bin/python2_' {} \;
}
