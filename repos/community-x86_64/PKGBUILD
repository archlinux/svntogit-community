# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Michael Barbour <barbour.michael.0@gmail.com>
# Contributor: sekret

pkgname=dsp
pkgver=1.8
pkgrel=2
pkgdesc="An audio processing program with an interactive mode"
arch=('x86_64')
url="https://github.com/bmc0/${pkgname}"
license=('ISC')
groups=('lasdpa-plugins')
makedepends=('ladspa')
depends=(
  'libmad' 'libtool'
  #'ffmpeg' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
  'ffmpeg4.4'
  'alsa-lib' 'libasound.so'
  'fftw' 'libfftw3.so'
  'libao' 'libao.so'
  'libpulse' 'libpulse.so' 'libpulse-simple.so'
  'libsndfile' 'libsndfile.so'
  'zita-convolver' 'libzita-convolver.so'
)
optdepends=(
  'ladspa-host: For the LADSPA plugin.'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bmc0/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('b2ac432b830ffe9ddd8e3c45c781b3816cfe8d523921045b6acd108de2f1a689')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
