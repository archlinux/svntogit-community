# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=darcs
pkgver=2.12.0
pkgrel=9
pkgdesc="A distributed, interactive, smart revision control system"
url="http://darcs.net"
license=("GPL")
arch=('i686' 'x86_64')
depends=('curl')
makedepends=("ghc=8.0.1" "haskell-async" "haskell-attoparsec" "haskell-base16-bytestring"
             "haskell-cryptohash" "haskell-data-ordlist" "haskell-fgl" "haskell-graphviz"
             "haskell-hashable" "haskell-html" "haskell-http" "haskell-mmap" "haskell-mtl"
             "haskell-network" "haskell-network-uri" "haskell-old-time" "haskell-parsec"
             "haskell-sandi" "haskell-random" "haskell-regex-applicative"
             "haskell-regex-compat-tdfa" "haskell-tar" "haskell-text"
             "haskell-transformers-compat" "haskell-unix-compat" "haskell-utf8-string"
             "haskell-vector" "haskell-zip-archive" "haskell-zlib")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        sandi-0.4.patch)
sha256sums=('17318d1b49ca4b1aa00a4bffc2ab30a448e7440ce1945eed9bf382d77582308d'
            'f144bf4ce7ee9f718e086267a936e2b113f9eb08c23688cbedb40276538562d3')

prepare() {
    sed -i 's/sandi        >= 0.2 && < 0.4,/sandi        >= 0.2 \&\& < 0.5,/' $pkgname-$pkgver/$pkgname.cabal
    (cd $pkgname-$pkgver && patch -p1 -i ../sandi-0.4.patch)
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        -fcurl -fpkgconfig -fhttp -f-static -fterminfo -fthreaded -fexecutable \
        -f-rts -foptimize -f-warn-as-error \
        -f-libiconv -fuse-time-1point5 -fnetwork-uri
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    # Remove library and doc (license only)
    rm -r "$pkgdir"/usr/{lib,share/doc}
    chmod 644 "${pkgdir}/usr/share/man/man1/darcs.1"

    install -Dm644 contrib/darcs_completion "${pkgdir}/usr/share/bash-completion/completions/darcs"
    install -Dm644 contrib/_darcs.zsh "${pkgdir}/usr/share/zsh/site-functions/_darcs"
}
