# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=wl-pprint-terminfo
pkgname=haskell-wl-pprint-terminfo
pkgver=3.7.1.4
pkgrel=369
pkgdesc="A color pretty printer with terminfo support"
url="https://github.com/ekmett/wl-pprint-terminfo/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-wl-pprint-extras')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('1f2a7bd195732fe297c49c50b59812190d5698efa7c2e9b2fbcfbbae71523c25dfcc2a0f098aebe22453cd867bc40480100c8231a6f43a7bd46202f3dcd924b3')

prepare() {
    sed -e '/nats/d' -e '/semigroups/d' -e 's/< *0.6/<1/' -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -fCursed
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
