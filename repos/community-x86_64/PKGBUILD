# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=alembic
pkgver=1.7.13
pkgrel=1
pkgdesc="A open framework for storing and sharing scene data"
arch=('x86_64')
url="http://www.alembic.io/"
license=('BSD')
depends=('openexr' 'boost-libs' 'hdf5')
makedepends=('cmake' 'boost')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('1dcaf1e42f098e6052ec93aa0af99d3ab99d40d570aaa71cb980092be68e440982b05d0230a4543efa01dbd4e82fe617c70e0b3b2548bf9836a553f41f8232b1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
