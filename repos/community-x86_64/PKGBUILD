# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=nsd
pkgver=4.1.19
pkgrel=1
pkgdesc='Authoritative only, high performance and simple DNS server'
url='https://www.nlnetlabs.nl/nsd/'
license=('BSD')
arch=('x86_64')
depends=('openssl' 'libevent')
makedepends=('flex')
validpgpkeys=('EDFAA3F2CA4E6EB05681AF8E9F6F1C2D7E045F8D')
source=("https://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'tmpfiles.d'
        'service')
sha256sums=('b0782cb9b57416888d488b6460b071cd85ecb5f99381865b3a7f93dddf9e02c5'
            'SKIP'
            '8e72816177069d6b82306c4b61ca4d0eefd2a77a31ea79c50635eea5aecf8c5b'
            '5ec7616df32c29ddec25a6ec772db5053e234410cf1042f2945d0f554b1f9b65')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/ \
		--sbindir=/usr/bin \
		--datarootdir=/usr/share \
		--with-pidfile=/run/nsd/nsd.pid \
		--enable-ratelimit \
		--enable-relro-now \
		--enable-pie \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/nsd.conf
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/nsd.service
	rmdir "${pkgdir}"/{tmp,run/{nsd,}}

	rm doc/differences.pdf
	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	install -m644 doc/* "${pkgdir}"/usr/share/doc/"${pkgname}"
	ln -s ../../doc/"${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
