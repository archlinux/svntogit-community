# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=type-errors
pkgname=haskell-type-errors
pkgver=0.2.0.0
pkgrel=43
pkgdesc="Tools for writing better type errors"
url="https://github.com/isovector/type-errors"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-first-class-families' 'haskell-syb' 'haskell-th-abstraction')
makedepends=('ghc' 'haskell-doctest')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('694ce594462234cbef53efae630e302d3653fb8afc68f1bf6bb4f21a407a5f0637b8fab27f8f62538cadd4ff2c51049ae18d60932eaf7359cbf60ca6b959011e')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *0.17/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
