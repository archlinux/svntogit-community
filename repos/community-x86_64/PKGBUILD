# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=xtrabackup
pkgver=2.2.8
pkgrel=1
pkgdesc='Non-blocking backup tool for MySQL'
arch=('i686' 'x86_64')
url='http://www.percona.com/software/percona-xtrabackup/'
license=('GPL')
depends=('libaio' 'libgcrypt' 'openssl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib')
source=(http://www.percona.com/downloads/XtraBackup/LATEST/source/tarball/percona-xtrabackup-$pkgver.tar.gz)
sha256sums=('4b98d5ae65fe64ed3523da1831ac3c679cdf1b4d25033c16baa58ee12401818e')

build() {
  cd percona-xtrabackup-$pkgver
  cmake -DBUILD_CONFIG=xtrabackup_release -DCMAKE_INSTALL_PREFIX=/usr
  make

  cd storage/innobase/xtrabackup/doc
  make man
}

package() {
  cd percona-xtrabackup-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/xtrabackup-test

  cd storage/innobase/xtrabackup/doc/build/man
  for file in *.1; do
    install -Dm0644 $file "$pkgdir"/usr/share/man/man1/$file
  done
}
