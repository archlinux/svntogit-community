# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=nsd
pkgver=4.1.4
pkgrel=2
pkgdesc='Authoritative only, high performance and simple DNS server'
url='http://www.nlnetlabs.nl/nsd/'
license=('BSD')
arch=('i686' 'x86_64')
depends=('openssl' 'libevent')
makedepends=('flex')
source=("http://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'hints.patch'
        'tmpfiles.d'
        'service')
sha256sums=('d6e0e9cd1f637a3b4bfa48dba47b1564408eb531c57f6a3dda1a2d529df800f0'
            '11cd5d6e5dc9bf3cda9024d1c0dfab8eb3ba00c3aa8e03dd078a580db4151dcb'
            '8e72816177069d6b82306c4b61ca4d0eefd2a77a31ea79c50635eea5aecf8c5b'
            '5ec7616df32c29ddec25a6ec772db5053e234410cf1042f2945d0f554b1f9b65')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p0 -i ../hints.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/ \
		--sbindir=/usr/bin \
		--datarootdir=/usr/share \
		--with-pidfile=/run/nsd/nsd.pid \
		--enable-ratelimit \
		--enable-relro-now \
		--enable-pie \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/nsd.conf
	install -Dm644 ../service "${pkgdir}"/usr/lib/systemd/system/nsd.service
	rmdir "${pkgdir}"/{tmp,run/{nsd,}}

	rm doc/differences.pdf
	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	install -m644 doc/* "${pkgdir}"/usr/share/doc/"${pkgname}"
	ln -s ../../doc/"${pkgname}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}
