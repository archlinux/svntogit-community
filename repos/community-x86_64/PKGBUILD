# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.8.6
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(x86_64)
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
         'hicolor-icon-theme' 'shared-mime-info')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('16921f10c2898881301ecd732181a076eced1d920592c8e18f6562fa2e3c718cc6ad94645f5a93731cda21228f06c12e4f53f0b7602337d2e374c215977b4b0a')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"
}

# vim:set ts=2 sw=2 et:
