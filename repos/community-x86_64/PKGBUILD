# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.6
pkgrel=2
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/iikira/BaiduPCS-Go"
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("$pkgname-$pkgver.tar.gz::https://github.com/iikira/BaiduPCS-Go/archive/v$pkgver.tar.gz")
sha512sums=('62082f0a1bedfb495d9b096ae06effacbf917cc721ff537522fb95b15e32300927a6cbaccd94f4c2881953af8eb69782d43ac35b195c647b3b67b7c664e3f3d0')

prepare() {
  cd BaiduPCS-Go-$pkgver

  mkdir -p .gopath/src/github.com/iikira
  ln -sf "$PWD" .gopath/src/github.com/iikira/BaiduPCS-Go
  export GOPATH="$PWD/.gopath:/usr/share/gocode"

  go get github.com/iikira/args github.com/olekukonko/tablewriter github.com/peterh/liner \
         github.com/urfave/cli
}

build() {
  cd BaiduPCS-Go-$pkgver
  go build -ldflags "-extldflags ${LDFLAGS}" -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
