# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=1.24
pkgrel=1
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache)
depends=(android-tools ffmpeg sdl2)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('33a0249ee9e9579b3bf8e0a4f94427b0c44b2a3aaf953c527a072d4f69ab89a680107b234803415741ba003ea6a8a207d33ce0d40341560fd6d85cda3807f7a6'
        '332f05198bd64392ed870add260249d7e549ceb963e707ee6a249c08197c2c6a52bc427be0dfa99a479d61926d1ccea1471f5a448816b4b157c933d7a17cd75f')

build() {
  mkdir -p build
  arch-meson build $pkgname-$pkgver -D b_lto=true -D b_ndebug=true \
    -D prebuilt_server="../$pkgname-server-v$pkgver" --buildtype release
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-$pkgver/LICENSE -t \
    "$pkgdir/usr/share/licenses/$pkgname"
}
