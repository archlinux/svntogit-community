# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: 3ED <krzysztof1987@gmail.com>

pkgname=caja-actions
pkgver=1.8.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="An extension for Caja which allows arbitrary programs to be launched via context menu (GTK2 version)"
url="https://github.com/raveit65/caja-actions"
license=('GPL')
depends=('caja' 'libgtop')
makedepends=('mate-common' 'python' 'yelp-tools')
conflicts=('caja-actions-gtk3')
replaces=('caja-actions-gtk3')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/raveit65/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('e20b89dc33ad36b5ee0a684c0ca02a0fb7756447cac59932fff40d949f791817')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname}

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
