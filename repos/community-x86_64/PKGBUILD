# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgbase=python-pymongo
pkgname=('python-pymongo' 'python2-pymongo')
pkgver=3.7.2
pkgrel=1
pkgdesc='Python module for using MongoDB'
arch=('x86_64')
license=('APACHE')
url='http://pypi.python.org/pypi/pymongo/'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python2-gevent' 'python2-monotonic' 'pifpaf')
checkdepends_x86_64=('mongodb')
source=("https://pypi.io/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha512sums=('4d1dfe03d94f381f27cfb5c8dd4f75ecbab90567fdc4dd594b880031e68825eda6285c8b261a4c9d76fb7ccc92c91e27ae2f696525f7732b1f0084794042a33d')

prepare() {
  cp -a "pymongo-$pkgver" "pymongo2-$pkgver"
}

build() {
  cd "$srcdir"/pymongo-$pkgver
  python setup.py build

  cd "$srcdir"/pymongo2-$pkgver
  python2 setup.py build
}

check_x86_64() {
  export DB_PORT=29000

  cd "$srcdir"/pymongo-$pkgver
  pifpaf run mongodb python setup.py test

  cd "$srcdir"/pymongo2-$pkgver
  pifpaf run mongodb python2 setup.py test
}

package_python2-pymongo() {
  depends=('python2')
  optdepends=('python2-monotonic: enable support for a monotonic clock')

  cd "pymongo2-$pkgver"
  python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
}

package_python-pymongo() {
  depends=('python')

  cd "pymongo-$pkgver"
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}

# vim:set ts=2 sw=2 et:
