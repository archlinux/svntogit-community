# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.4.3
pkgrel=1
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'glibmm' 'hicolor-icon-theme' 'libavc1394'
'libiec61883' 'libxml++' 'python-dbus')
makedepends=('alsa-lib' 'dbus-c++' 'jack' 'libconfig' 'python-pyqt5' 'scons')
optdepends=('python-pyqt5: ffado-mixer')
provides=('libffado.so')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz")
sha512sums=('d5d3f6b5c446002f3b62d00a215532c208fcc86bedb444bb20872b6b064d48330c6c1ba923bacc4f538ac8e2c73be4dfe0e27f80ea196d5789493d980dfde609')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix icon name, so it's coherent with XDG desktop file name
  sed -e 's/hi64-apps-ffado/ffado-mixer/g' \
      -i support/xdg/ffado.org-ffadomixer.desktop
}

build() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "${pkgname}-${pkgver}"
  scons PREFIX=/usr \
        DEBUG=no \
        CUSTOM_ENV=True \
        MANDIR='/usr/share/man' \
        PYTHON_INTERPRETER='/usr/bin/python3' \
        PYPKGDIR="/usr/lib/python${python_version}/site-packages" \
        UDEVDIR='/usr/lib/udev/rules.d'
}

package() {
  depends+=('libasound.so' 'libconfig++.so' 'libdbus-c++-1.so' )
  cd "${pkgname}-${pkgver}"
  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # XDG
  install -vDm 644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffado-mixer.desktop"
  install -vDm 644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/icons/hicolor/64x64/apps/ffado-mixer.png"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
