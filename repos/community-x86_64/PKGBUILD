# $Id: PKGBUILD 371260 2018-08-13 09:13:06Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Cravix <dr.neemous@gmail.com>
# Contributor: AlexanderR <rvacheva@nxt.ru>
# Contributor: zhn <zhangn1985@gmail.com>

pkgbase=renpy
pkgname=(
  renpy
  renpy-demos
)
pkgver=7.3.2
pkgrel=1
pkgdesc="The Ren'Py Visual Novel Engine"
arch=(x86_64)
url=https://www.renpy.org
license=(MIT)
depends=(
  ffmpeg
  glew
  python2-pygame-sdl2
)
makedepends=(cython2)
optdepends=('atom: Default editor')
source=(
  https://www.renpy.org/dl/${pkgver}/renpy-${pkgver}-source.tar.bz2
  renpy
  renpy.desktop
  renpy.png
  renpy-system-fribidi.patch
)
sha256sums=('952822ebd542b4f00187364ccfdf2eb0ac5b4c9a540abed52a556f1b914efac4'
            'e10630ec0b5d0e479f20b15a4f5ceec8e6992dd8cbc7d3cd5c487da380858a28'
            'fccde3461617a098a78d938d9db782d403eda410a84ab52825a597498ab95834'
            '611edc07a40ccb8e04e8858847fc1d2a066d29c2ed54e5b357880a0605818dc5'
            'c82d57331b8146ac3f76579f95e7cdca559f84919f96b35e9b19c25a5e2128b5')

prepare() {
  cd renpy-${pkgver}-source

  sed 's/python/python2/' -i atom/Atom.edit.py launcher/game/tkaskdir.py

  patch -Np1 -i ../renpy-system-fribidi.patch
}

build() {
  cd renpy-${pkgver}-source

  export RENPY_CYTHON=cython2

  python2 module/setup.py build
}

package_renpy() {
  optdepends=(
    'renpy-demos: Tutorial and The Question demos'
    'tk: Set projects directory'
  )

  cd renpy-${pkgver}-source

  python2 module/setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1 --skip-build

  install -dm 755 "${pkgdir}"/usr/share/{renpy,doc}

  cp -dr --no-preserve=ownership atom doc gui launcher renpy renpy.py "${pkgdir}"/usr/share/renpy/
  ln -s /usr/share/renpy/doc "${pkgdir}"/usr/share/doc/renpy

  install -Dm 755 ../renpy -t "${pkgdir}"/usr/bin/
  install -Dm 644 ../renpy.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 644 ../renpy.png -t "${pkgdir}"/usr/share/pixmaps/

  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/renpy/
}

package_renpy-demos() {
  depends=(renpy)

  cd renpy-${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/share/renpy

  cp -dr --no-preserve=ownership the_question tutorial "${pkgdir}"/usr/share/renpy/

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s renpy "${pkgdir}"/usr/share/licenses/renpy-demos
}

# vim: ts=2 sw=2 et:
