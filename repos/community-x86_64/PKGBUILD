# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>

pkgname=granite
pkgver=0.2.3
pkgrel=1
pkgdesc='An extension of GTK+ libraries'
arch=('i686' 'x86_64')
url='https://launchpad.net/granite'
license=('LGPL3')
depends=('gtk3' 'libgee06')
makedepends=('cmake' 'gobject-introspection' 'vala')
install="${pkgname}.install"
source=("${url}/${pkgver%\.?}/${pkgver}/+download/${pkgname}-${pkgver}.tgz")
sha256sums=('e0a448e647ee4b6dcff543b1d8695e9cb7e630e38c31ba741cf6b19eddae71f1')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
