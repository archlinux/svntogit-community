# $Id$
# Maintainer: Dan Printzell <arch@vild.io>
# Contributor: Mihails Strasunse <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=('dmd' 'dmd-docs' 'libphobos')
pkgdesc="D programming language compiler and standard library"
groups=('dlang' 'dlang-dmd')
pkgbase=dmd
pkgver=2.077.0
pkgrel=1
epoch=1
arch=('x86_64')
url="http://www.dlang.org"
makedepends=('git' 'dmd')
source=("git+https://github.com/dlang/dmd.git#tag=v$pkgver"
        "git+https://github.com/dlang/druntime.git#tag=v$pkgver"
        "git+https://github.com/dlang/phobos.git#tag=v$pkgver"
        "http://downloads.dlang.org/releases/2.x/$pkgver/dmd.$pkgver.linux.tar.xz"
        "http://downloads.dlang.org/releases/2.x/$pkgver/dmd.$pkgver.linux.tar.xz.sig"
        "dmd.conf"
        "dmd-doc.desktop")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '54471d7c5fdb73dd38cd8ab1d8ac4cb6b7aca0d0d18e7a9f38dcafcb242732f4'
            'SKIP'
            '3d639e89528fed1da90006f4dfb2b0fdc41308da5a96d953381ff4ccf257c035'
            '19c9cd3670bdef7fbde88de36c459d940e6b7bacc2a5e48aefbfd1b2c1895e07')
validpgpkeys=('AFC7DB45693D62BB472BF27BAB8FE924C2F7E724') # Martin Nowak <code@dawg.eu>
noextract=("dmd.$pkgver.linux.tar.xz")

prepare() {
    # We only want to extract the docs & samples, not the prebuild executables
    tar xfJ "dmd.$pkgver.linux.tar.xz" dmd2/html
}

build() {
    cd "$srcdir"/dmd
    make -f posix.mak RELEASE=1 PIC=1

    cd "$srcdir"/druntime
    make -f posix.mak DMD="$srcdir"/dmd/src/dmd RELEASE=1 PIC=1

    cd "$srcdir"/phobos
    make -f posix.mak DMD="$srcdir"/dmd/src/dmd RELEASE=1 PIC=1
}

package_dmd() {
    pkgdesc="The D programming language reference compiler"
    backup=('etc/dmd.conf')
    depends=('gcc' 'libphobos')
    optdepends=(
        'dtools: collection of useful utilities for development in D'
        'gcc-multilib: to cross-compile 32-bit applications'
        'dmd-docs: documentation and sample code for D'
    )
    provides=("d-compiler=$pkgver")
    license=('Boost')

    install -Dm755 dmd/src/dmd "$pkgdir"/usr/bin/dmd

    mkdir -p "$pkgdir"/etc
    install -Dm644 dmd.conf "$pkgdir"/etc/dmd.conf

    mkdir -p "$pkgdir"/usr/share/man/man1
    mkdir -p "$pkgdir"/usr/share/man/man5
    cp -r dmd/docs/man/man1/* "$pkgdir"/usr/share/man/man1/
    cp -r dmd/docs/man/man5/* "$pkgdir"/usr/share/man/man5/

    install -Dm644 dmd/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    find "$pkgdir"/usr -type f | xargs chmod 0644
    chmod 755 "$pkgdir"/usr/bin/*
}

package_dmd-docs() {
    pkgdesc="Documentation and sample code for D programming language"
    depends=('dmd')
    license=('Boost')

    mkdir -p "$pkgdir"/usr/share/applications
    install -Dm644 dmd-doc.desktop "$pkgdir"/usr/share/applications/dmd-doc.desktop

    mkdir -p "$pkgdir"/usr/share/d/samples/
    cp -r dmd/samples/* "$pkgdir"/usr/share/d/samples/

    mkdir -p "$pkgdir"/usr/share/d/html
    cp -r dmd2/html/* "$pkgdir"/usr/share/d/html/

    install -Dm644 dmd/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_libphobos() {
    pkgdesc="The Phobos standard library for D programming language"
    options=('staticlibs')
    depends=('gcc-libs')
    conflicts=('libphobos-devel')
    provides=("d-runtime=$pkgver" "d-stdlib=$pkgver" "libphobos-devel=$pkgver")
    replaces=('libphobos-devel')
    license=('Boost')

    mkdir -p "$pkgdir"/usr/lib
    install -Dm644 phobos/generated/linux/release/*/libphobos2.{a,so} "$pkgdir"/usr/lib/
    install -Dm644 phobos/LICENSE_1_0.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    mkdir -p "$pkgdir"/usr/include/dlang/dmd
    cp -r phobos/{*.d,etc,std} "$pkgdir"/usr/include/dlang/dmd
    cp -r druntime/import/* "$pkgdir"/usr/include/dlang/dmd/

    find "$pkgdir"/usr -type f | xargs chmod 0644

    install -Dm644 druntime/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-druntime
}
