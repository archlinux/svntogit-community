# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.3.7
pkgrel=2
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
optdepends=('redshift: automatic color temperature support')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz"
        disable-sys-update.patch)
sha512sums=('a8b1c3ca918ace88577e924ff66335d1ce7ddb2de55ce4abb160816a7cab8721188fa9a57bc97ddb1fc617d4a3f7df79b2f10a4ac2286eed0b3c98337b8b7777'
            'e968606156bea0c86c2c2baa4b936c785942e8c3a0de74aaa73b19ad505e3242d442c3276bbaed393e73cd88598daf6cc5356cea687f984b3966f01264f7028c')

prepare() {
  cd dde-control-center-$pkgver
  patch -p1 -i ../disable-sys-update.patch
}

build() {
  cd dde-control-center-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
