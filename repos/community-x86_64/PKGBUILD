# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

#
# cd deps ; ls -1 | while read g; do (test -d $g && cd $g && sh ../t.sh); done >qwe
#
# U=`grep 'url =' .git/config | cut -f2 -d= | tr -d ' '`
# C=`git rev-parse HEAD`
# N=`basename $(pwd)`
# echo -e "\t\"$N::$U#commit=$C\""
#

pkgname=ejabberd
pkgver=17.03
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"cache_tab::git://github.com/processone/cache_tab#commit=89659f4e3c8c67fca351f709e1ea7b0f6327948d"
	"elixir::git://github.com/elixir-lang/elixir#commit=3dbfb92860fb24f83c92e1f1b67f48554fb211d6"
	"epam::git://github.com/processone/epam#commit=488759ff2bbfdbfb9e2a253857c049bc8a357da0"
	"eredis::git://github.com/wooga/eredis#commit=cbc013f516e464706493c01662e5e9dd82d1db01"
	"esip::git://github.com/processone/esip#commit=c4f971f93f6141f716bf0e8f4cc2e2acdba16aaa"
	"ezlib::git://github.com/processone/ezlib#commit=e42ce61e60dd312ac60ef299d7f6f4522af577e5"
	"fast_tls::git://github.com/processone/fast_tls#commit=2ae628f6e0d1acb09e683b1a5978909ade8f3038"
	"fast_xml::git://github.com/processone/fast_xml#commit=dca6aaf422e3d82cc616d4f22187ce2bdb5af3c9"
	"fast_yaml::git://github.com/processone/fast_yaml#commit=d5cc9701cf0bd15de129453c82ef433bd752a27c"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=212299233c7e7eb63a97be2777e1c05ebaa58dbe"
	"hamcrest::git://github.com/basho/hamcrest-erlang.git#commit=98bc7aa19ea081478c816824aa05fc5a48acae66"
	"iconv::git://github.com/processone/iconv#commit=4886399bd973c8544821f5b70482ef54168155b8"
	"jiffy::git://github.com/davisp/jiffy#commit=1febce3ca86c5ca5d5a3618ed3d5f125bb99e4c5"
	"lager::git://github.com/basho/lager#commit=8187757388c9adc915379caaab36a2f2ca26e944"
	"luerl::git://github.com/rvirding/luerl#commit=f7b2cc0ab6fa4245ebeda0169fc994aff0628bf9"
	"meck::git://github.com/eproxus/meck#commit=70d6a33ce7407029dc59e22a5b3c1c61c1348b23"
	"moka::git://github.com/processone/moka.git#commit=3eed3a6dd7dedb70a6cd18f86c7561a18626eb3b"
	"p1_mysql::git://github.com/processone/p1_mysql#commit=45ac9ffc243c0822c6f58ca2da04ef55351a0e42"
	"p1_oauth2::git://github.com/processone/p1_oauth2#commit=34f9b20fd68134a4646130bdcf1abf320f815a00"
	"p1_pgsql::git://github.com/processone/p1_pgsql#commit=8040c9a33104a3fe214e2fde353ef2d96f9feffe"
	"p1_utils::git://github.com/processone/p1_utils#commit=0860e5f913ccf6ded432450c942927cca5ccdbea"
	"protobuffs::git://github.com/basho/erlang_protobuffs.git#commit=0d22220a8e56adab129836d6ff582a9bca103a0c"
	"rebar_elixir_plugin::git://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"riakc::git://github.com/basho/riak-erlang-client#commit=261df630afe4a998e099a8ab209a2dc7419f85de"
	"riak_pb::git://github.com/basho/riak_pb#commit=9ca2395f9d6464b82bab466bbdca410cb85a0ac7"
	"samerlib::git://github.com/processone/samerlib#commit=fbbba035b1548ac4e681df00d61bf609645333a0"
	"sqlite3::git://github.com/processone/erlang-sqlite3#commit=6ee3570060fbbf793a0bc873831e8eabb6c39851"
	"stringprep::git://github.com/processone/stringprep#commit=7643cf198b45a176d7e8258e42e5a756453fcd75"
	"stun::git://github.com/processone/stun#commit=35fa0ca65188550d96ee30ce680a83f74c21ab21"
	"xmpp::git://github.com/processone/xmpp#commit=a7f2ebfab89f015133c69f41754477f322c082b3"
	"$pkgname.logrotate"
	"ejabberd.service"
	"sysuser.conf")
sha256sums=('afa6dd0eefe70a0acdcfa18a7a4cc5a427d30945f9d3a04c8241f3331c77778f'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '31780cac78736d285e46f445f8c8463a70f2aeb2683280c259129db11832ddd2'
            '94582b1df76f6d86abfcc78bf4c4ac79276761c75483dd40a45b7ff2b19dc8a8'
            '0599a3d20153d620e9ac4fdb5bd7370854136235382a2fed4110b1950cf07d20')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in cache_tab elixir eredis esip ezlib fast_tls fast_xml fast_yaml \
    goldrush hamcrest iconv jiffy lager luerl meck moka p1_mysql p1_oauth2 \
    epam p1_pgsql p1_utils protobuffs rebar_elixir_plugin riakc riak_pb \
    samerlib sqlite3 stringprep stun xmpp; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv "$srcdir"/$i "$srcdir/$pkgname-$pkgver/deps"
  done

  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
  sed -i 's#R15|R16|17|18#R15|R16|17|18|19#' deps/riakc/rebar.config
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm04750 deps/epam/priv/bin/epam "$pkgdir"/usr/lib/ejabberd-$pkgver/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm0644 "$srcdir"/ejabberd.service "$pkgdir"/usr/lib/systemd/system/ejabberd.service
  install -Dm644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf
  install -Dm644 tools/ejabberdctl.bc "$pkgdir"/usr/share/bash-completion/completions/ejabberdctl

  # workaround
  ln -s mod_configure.beam "$pkgdir"/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f "$pkgdir"/usr/bin/{elixir,iex,mix}

  # permissions
  chown -R 17:17 "$pkgdir"/var/{lib,log}/ejabberd
  chown root:17 \
    "$pkgdir"/etc/ejabberd/ejabberd.yml \
    "$pkgdir"/etc/ejabberd/ejabberdctl.cfg \
    "$pkgdir"/etc/ejabberd \
    "$pkgdir"/usr/lib/ejabberd-$pkgver/priv/bin/epam
}

# vim:set ts=2 sw=2 et:
