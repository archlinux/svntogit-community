# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:thefatprecious <marty@loomar.com>

pkgname=tvtime
pkgver=1.0.7
pkgrel=1
pkgdesc="A high quality television application for use with video capture cards"
arch=('i686' 'x86_64')
url='http://linuxtv.org/'
license=('GPL' 'LGPL')
depends=('gcc-libs' 'libpng' 'libxml2' 'freetype2' 'libxtst' 'libxinerama' 'libxv'
	 'libsm' 'libxxf86vm' 'alsa-lib')
makedepends=('libxt' 'v4l-utils' 'git')
source=("git://linuxtv.org/tvtime.git#tag=v1.0.7")
md5sums=('SKIP')

prepare() {
  cd $srcdir/$pkgname
  echo "StartupWMClass=tvtime" >>docs/tvtime.desktop
}

build() {
  cd $srcdir/$pkgname
  ./autogen.sh --prefix=/usr --mandir=/usr/share/man --localstatedir=/var --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname
  make DESTDIR=$pkgdir install
  install -D -m644 docs/html/default.tvtime.xml $pkgdir/usr/share/tvtime/default.tvtime.xml
}
