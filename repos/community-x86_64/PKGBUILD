# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-recipes
pkgver=2.0.4
pkgrel=4
pkgdesc='Recipe management application for GNOME'
arch=('x86_64')
url='https://wiki.gnome.org/Apps/Recipes'
license=('GPL3')
groups=('gnome-extra')
depends=('cairo' 'dconf' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'gnome-autoar'
         'gnome-online-accounts' 'gspell' 'gtk3' 'hicolor-icon-theme' 'json-glib' 'libcanberra'
         'libsoup' 'pango' 'rest')
makedepends=('itstool' 'meson')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('b30e96985f66fe138a17189c77af44d34d0b4c859b304ebdb52033bc2cd3ffed')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
