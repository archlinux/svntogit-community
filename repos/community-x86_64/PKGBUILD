# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Peter Mattern <pmattern at arcor dot de>

pkgname=feathernotes
pkgver=0.10.0
pkgrel=1
pkgdesc='Lightweight Qt hierarchical notes-manager'
arch=('x86_64')
url='https://github.com/tsujan/FeatherNotes'
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'qt5-svg' 'qt5-translations' 'qt5-x11extras')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("https://github.com/tsujan/FeatherNotes/releases/download/V$pkgver/FeatherNotes-$pkgver.tar.xz"{,.asc})
sha256sums=('f30e1aadad7d388ceb9e7ba1160337feecb5b618faaf17638d78a9e571afa6d5'
            'SKIP')
validpgpkeys=('19DFDF3A579BD509DBB572D8BE793007AD22DF7E')

build() {
  cmake -S FeatherNotes-$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
