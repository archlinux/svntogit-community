# $Id$
# Contributor: SÃ©bastien "Seblu" Luttringer <seblu@arhlinux.org>

pkgname=go-md2man
pkgver=1.0.2
pkgrel=1
pkgdesc='A markdown to manpage generator'
arch=('x86_64' 'i686')
url='https://github.com/cpuguy83/go-md2man'
license=('MIT')
makedepends=('git' 'go')
source=("$pkgname.tar.gz::https://github.com/cpuguy83/go-md2man/archive/v$pkgver.tar.gz")
sha256sums=('bbb936fbaba460200d0fe97181ba5a64c511d8aa9a647f76587f283a8cc6398f')

build() {
  export GOPATH=$(readlink -f .)
  local repo=src/github.com/cpuguy83
  mkdir -p "$repo"
  ln -sf "../../../go-md2man-$pkgver" "$repo/go-md2man"
  cd "$repo/go-md2man"
  go get -v ./...
  "$srcdir/bin/go-md2man" -in=go-md2man.1.md -out=go-md2man.1
}

package() {
  install -Dm755 "$srcdir/bin/go-md2man" "$pkgdir/usr/bin/go-md2man"
  install -Dm755 "$srcdir/go-md2man-$pkgver/go-md2man.1" "$pkgdir/usr/share/man/man1/go-md2man.1"
}

# vim:set ts=2 sw=2 et:
