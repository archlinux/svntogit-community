# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.8.4
pkgrel=2
pkgdesc="A fast AppStream metadata generator"
arch=(x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg optipng glibd)
makedepends=(ldc meson ninja mustache-d curl gir-to-d gobject-introspection docbook-xsl)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ximion/appstream-generator/archive/v$pkgver.tar.gz"
        "FixCompilationDueToBrokenGeneratedCode.patch::https://github.com/ximion/appstream-generator/commit/d5dad3b174eea7e9fbe0ed6e7b55a366b8eb203d.patch")
sha256sums=('49bebcc959f02d72c31f26dad84e3acb3d1a12f64f18fe0a58db23e4d66f97b5'
            '26341b31ddb7478affaba1dbcf5581ba1ae3a3d11d9770efa8d57fcc0bcc26ea')

prepare() {
  # Fix compilation due to broken generated GdkPixbuf code
  # https://github.com/ximion/appstream-generator/pull/90
  patch -d $pkgname-$pkgver -p1 -i "$srcdir"/FixCompilationDueToBrokenGeneratedCode.patch
}

build() {
  meson build $pkgname-$pkgver -Ddownload-js=false --prefix=/usr
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja install -C build
}
