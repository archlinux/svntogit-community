# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Robert Knauer <robert@privatdemail.net>

pkgbase=luadbi
pkgname=(lua51-dbi lua52-dbi lua-dbi)
pkgver=0.7.1
pkgrel=3
pkgdesc="Database interface library for Lua"
arch=('x86_64')
url="https://github.com/mwild1/luadbi"
license=('MIT')
makedepends=('sqlite3' 'mariadb-libs' 'postgresql' 'lua' 'lua52' 'lua51')
optdepends=('sqlite3: sqlite3 Support'
            'postgresql-libs: postgresql Support'
            'mariadb-libs: mysql Support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mwild1/luadbi/archive/v$pkgver.tar.gz")
sha1sums=('992c322b09e8a109c45aae938c14303a6b28720c')

prepare() {
  cp -r "$srcdir"/luadbi-$pkgver "$srcdir"/luadbi51-$pkgver
  cp -r "$srcdir"/luadbi-$pkgver "$srcdir"/luadbi52-$pkgver
}

build() {
  cd "$srcdir"/luadbi51-$pkgver
  make free LUA_V=5.1

  cd "$srcdir"/luadbi52-$pkgver
  make free LUA_V=5.2

  cd "$srcdir"/luadbi-$pkgver
  make free LUA_V=5.3
}

package_lua51-dbi() {
  depends=('lua51')
  cd "$srcdir"/luadbi51-$pkgver
  make install_free DESTDIR="$pkgdir" LUA_V=5.1
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_lua52-dbi() {
  depends=('lua52')
  cd "$srcdir"/luadbi52-$pkgver
  make install_free DESTDIR="$pkgdir" LUA_V=5.2
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_lua-dbi() {
  depends=('lua')
  cd "$srcdir"/luadbi-$pkgver
  make install_free DESTDIR="$pkgdir" LUA_V=5.3
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
