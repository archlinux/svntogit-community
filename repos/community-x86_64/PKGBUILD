# $Id$$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffms2
pkgver=2.19.1.r120.9203af5
pkgrel=1
pkgdesc='A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access'
arch=('i686' 'x86_64')
url='https://github.com/FFMS/ffms2'
license=('GPL')
depends=('ffmpeg')
makedepends=('git')
provides=('vapoursynth-plugin-ffms2')
source=("git+https://github.com/FFMS/ffms2.git#commit=9203af5")
sha256sums=('SKIP')

build() {
  cd ${pkgname}

  ./autogen.sh --prefix='/usr' --enable-shared --disable-static --enable-avresample
  make
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/vapoursynth
  ln -s ../libffms2.so "${pkgdir}"/usr/lib/vapoursynth/
}

# vim: ts=2 sw=2 et:
