# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Tilman Blumenbach <tilman (at) ax86 (dot) net>

pkgname=netperf
pkgver=2.6.0
pkgrel=1
pkgdesc='Benchmarking tool for many different types of networking'
url='http://www.netperf.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('glibc' 'libsmbios' 'lksctp-tools')
install=$pkgname.install
source=(
  "ftp://ftp.netperf.org/netperf/$pkgname-$pkgver.tar.bz2"
  "$pkgname.rc"
  "$pkgname.conf"
  "$pkgname.service")
md5sums=('9654ffdfd4c4f2c93ce3733cd9ed9236'
         '4f11bf24fcd7a44c17bbe27cdc7dae01'
         '14836578ba49bbfa9e928feeca8346f1'
         '9a8b47101fb2b0bf8fb9bbfb73ba6486')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --enable-demo \
    --enable-unixdomain \
    --enable-dccp \
    --enable-sctp
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # install license
  install -D -m 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  # install initscripts & systemd files
  install -D -m 755 ../$pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 ../$pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  install -D -m 644 ../$pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 ft=sh et:
