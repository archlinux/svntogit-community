# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Carlos Aznar√°n <caznaranl@uni.pe>

pkgbase=qxlsx
pkgname=($pkgbase-qt5 $pkgbase-qt6)
pkgver=1.4.5
pkgrel=1
pkgdesc='Excel file reader/writer library using Qt'
arch=(x86_64)
url='https://github.com/QtExcel/QXlsx'
license=(MIT)
makedepends=(cmake qt5-base qt6-base)
source=(https://github.com/QtExcel/QXlsx/archive/v$pkgver/$pkgbase-$pkgver.tar.gz
        coinstallability.patch)
sha256sums=('1b546b28863cb86889da586c1d702f1627b9e6a946acf13d52b1f3dd65c6df8f'
            'b48f9a144791adfd6c3f1a8cbcb2eea2094d84accfa2237444171c2b01ed37c8')

prepare() {
  patch -d QXlsx-$pkgver -p1 < coinstallability.patch # Fixx Qt5/6 coinstallability
}

build() {
  cmake -B build5 -S QXlsx-$pkgver/QXlsx \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=5 \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build5

  cmake -B build6 -S QXlsx-$pkgver/QXlsx \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_VERSION_MAJOR=6 \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build6
}

package_qxlsx-qt5() {
  depends=(qt5-base)

  DESTDIR="$pkgdir" cmake --install build5
  install -Dm644 QXlsx-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_qxlsx-qt6() {
  depends=(qt6-base)

  DESTDIR="$pkgdir" cmake --install build6
  install -Dm644 QXlsx-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname 
}
