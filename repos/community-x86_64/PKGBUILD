# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Guillem Rieu <guillemr@gmx.net>

pkgname=ccgo
pkgver=0.3.6.5
pkgrel=2
pkgdesc='Play the game of Go with gnugo or against other players on IGS'
arch=('x86_64' 'i686')
url='http://ccdw.org/~cjj/prog/ccgo/'
license=('GPL')
depends=('gtkmm' 'gconfmm')
source=("http://ccdw.org/~cjj/prog/$pkgname/src/$pkgname-$pkgver.tar.gz")
sha256sums=('d12be7fb9170d650ae78197983fac05af21ddbf47f7b1a2a14de9aa832ed602c')

prepare() {
  cd "$pkgname-$pkgver"

  find -name '*.hh' -exec sed -i '/sigc++\/object.h/d' {} +
  find -name '*.cc' -exec sed -i 's/(bind(/(sigc::bind(/' {} +
}

build() {
  cd "$pkgname-$pkgver"

  CXXFLAGS+=' -std=c++11 -fpermissive'
  ./configure --prefix=/usr
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: http://ccdw.org/~cjj/prog/ccgo/ChangeLog
# vim:set ts=2 sw=2 et:
