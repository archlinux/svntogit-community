# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=libime
pkgver=0.0.0.20200830
_commit=41029847d07b382b6d002ba8d467e8b333dcc1d1
_kenlmcommit=01c49fe86714276f77be9278d00906fc994256c1
_lm_sc_ver=20140820
_dict_ver=20200715
pkgrel=1
pkgdesc="A library to support generic input method implementation"
arch=('x86_64')
url="https://github.com/fcitx/libime"
license=('GPL')
depends=('boost-libs' 'fcitx5')
makedepends=('boost' 'extra-cmake-modules' 'ninja' 'python')
source=($pkgname-$_commit.tar.gz::https://github.com/fcitx/libime/archive/$_commit.tar.gz
        kenlm-$_kenlmcommit.tar.gz::https://github.com/kpu/kenlm/archive/$_kenlmcommit.tar.gz
        https://download.fcitx-im.org/data/lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
        https://download.fcitx-im.org/data/dict.utf8-$_dict_ver.tar.xz
        https://download.fcitx-im.org/data/table.tar.gz)
noextract=(lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
           dict.utf8-$_dict_ver.tar.xz
           table.tar.gz)
sha512sums=('514015dad8c96f1c62c96d583f4786245f626f62034e55fd0a1e1ba38929c0ff9bd339f9171bd9c7b14f462004a73400d2bd7acb2b61bf2bbf61ab93ccab4491'
            '461c2f45a7aa5685f418b7b456bff9c00b44b0bfdd347c36a5f9f1fb9a39539d27d0d66b078ed2847d8765e07157c241e24d61314cc08b90bdfbab99a800369e'
            'ea3ede62eb993c4d21ca7cf339635f4686493a0cefd57df4a31f00a001e67502a13ff9612ff667b941b82b47de00be9f3c9689677d04195a7ef8716bac087519'
            '75ad207842937369e79c3c3616165424c4e71fbce7ba2b15c7c2f1dcb898950e6d9babf39ed5bde01c21dd025906c988a80989863175daa8d1315a9be4402130'
            '99e1d69c679355fcf3e117a1ef39b131cbd3af841387d2335757247d840b90ff6b7323e16fca98cf7a043033faf250d2e9c6d47b96845d18c278a0cf6f30fec9')

prepare() {
  cd $pkgname-$_commit/src/libime/core
  rmdir kenlm
  ln -s "$srcdir"/kenlm-$_kenlmcommit ./kenlm

  cd "$srcdir"/$pkgname-$_commit/data
  ln -s "$srcdir"/lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
  ln -s "$srcdir"/dict.utf8-$_dict_ver.tar.xz
  ln -s "$srcdir"/table.tar.gz
}

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$_commit
  ninja test
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
