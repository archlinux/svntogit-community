# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=iwd
pkgver=0.22
pkgrel=1
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('glibc')
makedepends=('python-docutils')
source=("https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar"{.xz,.sign})
sha512sums=('4fa9f8688a33663aad754a371e9c1c71161f394fc0d7c4a32ce3afe7e00efdca8f55f378a71ab32002bbe73538b1a092bc0f24c19912672d70d6b02fcc824fe0'
            'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')
changelog=ChangeLog
install=iwd.install

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/iwd \
	--enable-wired \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
	for f in doc/*; do
		install -Dm644 "${f}" "${pkgdir}/usr/share/iwd/doc/$(basename ${f})"
	done
}

# vim: set ts=2 sw=2 ft=sh noet:
