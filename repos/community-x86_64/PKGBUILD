# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Elrondo46 TuxnVape <elrond94@hotmail.com>
# Contributor: Ivan Fonseca <ivanfon@riseup.net>
# Contributor: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Giacomo <giacomogiorgianni at gmail dot com>

pkgname=vokoscreen
_pkgname=vokoscreenNG
pkgver=3.0.8
pkgrel=1
pkgdesc='Easy to use screencast creator'
arch=(x86_64)
url='https://linuxecke.volkoh.de/vokoscreen/vokoscreen.html'
license=(GPL2)
depends=(gst-plugins-good qt5-multimedia qt5-x11extras)
optdepends=('gst-plugins-bad: record audio into AAC format'
            'gst-plugins-ugly: record screen into H.264 format')
makedepends=(qt5-tools)
source=(https://github.com/vkohaupt/$_pkgname/archive/$pkgver/$_pkgname-$pkgver.tar.gz
        0001-Add-better-integration-for-Linux.patch)
sha256sums=('bb694195fe05f9ebcdb2bd4d912c0b2a1c73b9ab864d010ca4d2be661c9dad10'
            'f0b04c72883b87862b1a57aa908cc1a2b39d1638419f548ee49a6a803377637e')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../0001-Add-better-integration-for-Linux.patch
}

build() {
  cd $_pkgname-$pkgver
  qmake PREFIX=/usr src/vokoscreenNG.pro
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
