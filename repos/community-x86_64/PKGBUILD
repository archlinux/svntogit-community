# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=ptex
pkgver=2.3.2
_commit_sha=1b8bc985a71143317ae9e4969fa08e164da7c2e5
pkgrel=1
pkgdesc="Per-Face Texture Mapping for Production Rendering"
arch=(x86_64)
url="http://ptex.us/"
license=('BSD')
depends=(zlib)
makedepends=(git doxygen cmake)
source=("https://github.com/wdas/ptex/archive/v${pkgver}.tar.gz")
sha512sums=('dbc557dc5e1761204ee3483af9bf4ff1504cbd7955e0405dc27a51f7182e2445e41db086b2792c2491aa2cbaddc74e523170a4b3d25e44d332123d5b7081f4b9')

prepare() {
	mkdir "$pkgname-$pkgver"/build
}

build() {
	cd "$pkgname-$pkgver"/build

	cmake .. \
        -DPTEX_SHA=$_commit_sha \
        -DPTEX_VER=$pkgver \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr

    make
}

check() {
	cd "$pkgname-$pkgver"/build
    make test V=1
}

package() {
	cd "$pkgname-$pkgver"/build

    # install -d ${pkgdir}/usr/share
    make DESTDIR="$pkgdir" install

    # cp -r install/{bin,include,lib} ${pkgdir}/usr
    # cp -r install/doc ${pkgdir}/usr/share/

    install -Dm644 ../src/doc/License.txt ${pkgdir}/usr/share/licenses/${pkgname}/License.txt
}
