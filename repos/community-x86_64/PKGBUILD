# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Luis Martinez <luis dot martinez at disroot dot org>
# Contributor: Daniel Ekl√∂f <daniel at ekloef dot se>

pkgname=fnott
pkgver=1.2.0
pkgrel=2
pkgdesc='Keyboard driven and lightweight Wayland notification daemon'
url="https://codeberg.org/dnkl/$pkgname"
arch=(x86_64)
license=(MIT)
depends=(dbus
         fcft
         wlroots)
makedepends=(meson
             tllist
             scdoc
             wayland-protocols)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('d6cfb7efcfad5e0aa95c5fd9632c638a53a0debfc17128e70922a1ba9f841470')

build() {
	cd "$pkgname"
	arch-meson . build
	meson compile -C build
}

package() {
	cd "$pkgname"
	meson install -C build --destdir "$pkgdir"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
