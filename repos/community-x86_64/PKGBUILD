# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Peter Reschenhofer <peter.reschenhofer@gmail.com>

_pkgauthor=rfjakob
pkgname=gocryptfs
pkgver=1.7
pkgrel=3
pkgdesc='Encrypted overlay filesystem written in Go.'
arch=('x86_64')
url="https://github.com/${_pkgauthor}/${pkgname}"
license=('MIT')
depends=('gcc-libs' 'openssl' 'fuse')
makedepends=('go-pie' 'pandoc' 'man-db')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz.asc"
        "build_flags.patch"
        "makefile.patch")
sha256sums=('2d1a2cfd072d554a28ee6e6807474b00ac710fb1aaf7aa81f3d8e94e80f6a703'
            'SKIP'
            'f43c3d2cc8c7f0338dbd553d0189eda269d7ceee6a3539dd6096c18e7344e567'
            '2821a72db020d4d5b3aeb0ce6584ec1edcda2b0948c155bc95ead634e760d37e')
validpgpkeys=('FFF3E01444FED7C316A3545A895F5BC123A02740')

prepare() {
    mkdir -p "${srcdir}/gopath/src/github.com/${_pkgauthor}"
    ln -rTsf "${srcdir}/${pkgname}_v${pkgver}_src-deps" "${srcdir}/gopath/src/github.com/${_pkgauthor}/${pkgname}"

    cd "${srcdir}/gopath/src/github.com/${_pkgauthor}/${pkgname}"
    patch -Np1 -i "${srcdir}/build_flags.patch"
    patch -Np1 -i "${srcdir}/makefile.patch"
}

build() {
    export GOPATH="${srcdir}/gopath"
    cd "${srcdir}/gopath/src/github.com/${_pkgauthor}/${pkgname}"
    make build
}

package() {
    cd "${srcdir}/gopath/src/github.com/${_pkgauthor}/${pkgname}"
    make DESTDIR="${pkgdir}" install
}
