# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=kresus
pkgver=0.17.4
pkgrel=3
pkgdesc="Self-hosted personal finance manager"
arch=(x86_64)
url="https://kresus.org"
license=(MIT)
backup=('etc/webapps/kresus/config.ini')
depends=(python nodejs-lts-fermium woob)
makedepends=(yarn npm libpng)
optdepends=('python-pdfminer: La Banque Postale support')
source=(https://framagit.org/kresusapp/kresus/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        config.ini
        ${pkgname}.service
        ${pkgname}.sysusers
        ${pkgname}.tmpfiles)
sha256sums=('bf08050b9f704c5727f2e6a8410f2a2914f589372a2de539c429fd1fec0e7613'
            '2a1de56c469b9a8e899614e6cb6ff8d6f205b5df8d30141230f1d0bc2bf15f40'
            'f57bac585629200877e03d75631b174cafa7d8fd42ca815db9aecc9e166d200c'
            'd9d30f5470c7165e4917487b69d7ab82e463da4e1355056e1035ee501d3f1adc'
            'ba8ad7d9eb5d2b47fde5f6a3ab98596e5c679141b78d76d54b44830604b67632')

prepare() {
    cd ${pkgname}-${pkgver}
    yarn install --no-fund
}

build() {
    cd ${pkgname}-${pkgver}
    export CFLAGS+=" -ffat-lto-objects"
    yarn run build:prod --offline
}

package() {
    cd ${pkgname}-${pkgver}
    export CFLAGS+=" -ffat-lto-objects"
    DESTDIR="${pkgdir}" npm -g install kresus --no-audit --no-fund

    # Remove useless stuff
    rm -r "${pkgdir}"/usr/lib/node_modules/kresus/support
    find "${pkgdir}"/usr/lib/node_modules/kresus/ -type d \( -name 'test*' -o -name '.github' \) -exec rm -rf {} \+
    find "${pkgdir}"/usr/lib/node_modules/kresus/ -type f \( -name '*\.md' -o -name 'LICENSE*' -o -name 'license' -o -name '\.*' \) -delete
    # No -o -name 'package.json' because they seem required
    # node-expat artifacts
    rm -r "${pkgdir}"/usr/lib/node_modules/kresus/node_modules/node-expat/{binding.gyp,build/{Makefile,Release/{.deps,obj.target},binding.Makefile,config.gypi,deps,node_expat.target.mk},deps/,node-expat.cc}

    # Fix npm crazyness
    #chmod -R go-w "${pkgdir}"/usr
    chown -R root:root "${pkgdir}"/usr

    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    cd ..
    install -Dm600 config.ini -t "${pkgdir}"/etc/webapps/kresus/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
