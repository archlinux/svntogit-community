# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: farid abdelnour <farid at atelier-labs.org>
# Contributor: Gustavo Alvarez <sl1pkn07 at gmail.org>

pkgname=twolame
pkgver=0.4.0
pkgrel=3
pkgdesc="An optimized MPEG Audio Layer 2 (MP2) encoder"
arch=(x86_64)
url="http://www.twolame.org/"
license=(LGPL2.1)
depends=(
  glibc
  libsndfile
)
provides=(libtwolame.so)
source=(https://github.com/njh/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('cc594bc8d2322922280f915a3c0aa52540cca0350d6498bc96f3f60fd6e53f951e775ea015a44bdb29ec883b46b31a0e5483f6a5c188b02e30008289273c7d03')
b2sums=('f61a49aed06dcd2a60744dd758b480670f14f0eece0586b62a0b21fe2d9ff8280f8f7324b7daf88e85675c6200a1c68815ddd6bafa308b503a52df4552031bb1')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
}
