# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=50.2
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'sdl' 'tcl')
makedepends=('boost' 'glu' 'rtaudio' 'rtmidi')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-50.2-devendor_rtaudio_rtmidi.patch"
)
sha512sums=('e15511e1e1c766fe0473d34ad8f8b938cf4465313b0fc9f04336f394d6255939c533ed648d8932621a0536c56e918cebc6c08f1ca9d450ec307d7bd8def95c60'
            'a3e27c9935857358aef0137a6346c2bb543209a6711b25d7d7b00cc508ff57f1a2b851cac47e3e32119f3b8f6edd5986fe56a5da105389a114fd38471adc2218')
b2sums=('cc29960202d942eaae71f16332aae85b8b1990d8a3fe08c561690b08c8bf9c1c6ff082969b34e6361080fdf182a6478afffee6cfd80e6705fc5c07e880ae8c0f'
        'ef94a41625f3730726863daae2438d05ed2fb47f38cc80fe5088a53b0a2e3f1c1fdea21d0f16d6e0d7f07520bc8f5f598fcda29143acd7996892527065853a6f')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-50.2-devendor_rtaudio_rtmidi.patch"
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('librtaudio.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
