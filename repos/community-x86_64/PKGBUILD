# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=sz
pkgver=2.1.11
pkgrel=1
pkgdesc="Modular parametrizable lossy compressor framework for scientific data"
arch=(x86_64)
url="https://szcompressor.org/"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(cmake gcc-fortran hdf5 netcdf python python-numpy swig)
source=(https://github.com/szcompressor/SZ/releases/download/v${pkgver}/sz-${pkgver}.tar.gz)
sha256sums=('85b8ef99344a3317ba9ee63ca4b9d99a51d1832d4d8880e01c7c56b3a69cacc9')

build() {
  cmake -B build -S SZ-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_FORTRAN=ON \
    -DBUILD_HDF5_FILTER=ON \
    -DBUILD_NETCDF_READER=ON \
    -DBUILD_PYTHON_WRAPPER=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 SZ-${pkgver}/copyright-and-BSD-license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
  rm "${pkgdir}"/usr/bin/test*
}
