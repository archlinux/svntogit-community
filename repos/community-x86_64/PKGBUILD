# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname=gnome-packagekit
pkgver=43.0
pkgrel=1
pkgdesc='Collection of graphical tools for PackageKit to be used in the GNOME desktop'
arch=('x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'libnotify' 'packagekit')
makedepends=('meson' 'intltool' 'itstool' 'docbook-xsl' 'libcanberra')
source=("https://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('cda455a652a923b2ea2f70316bd0fdd8296f7b62eef3fafd9ce50c8e66c5f195')

build() {
	arch-meson "${pkgname}-${pkgver}" build

	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}

