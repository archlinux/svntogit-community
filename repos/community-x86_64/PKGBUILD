# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=iwd
pkgver=0.16
pkgrel=4
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('glibc')
source=("https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar"{.xz,.sign}
        'iwd.tmpfiles'
				'skip-associate-event.patch')
sha512sums=('33b10516fb203eb92651e18ebff8448874452fad9425470bf4306b4d75a512d386561ed4fcd251c54175ac6953e29f6574175e4e9b18c88e55009f670f29ddb1'
            'SKIP'
            '03a3a0fba53706ef46bb12e75ad0bc30969044662df5bdcebc608c57b6ab9c5b3b6944e7941549db3172577c7d86c42e39a06e285459663ae48032fd3ab697d4'
            'ff718504b871c2e66fc433d0da24e8f9e1c78d3dbb18d76693fba671109a878e737f9e3d40f3b7e736b53d758c931083db86c9f5ba54391e715cf847b01bbaf3')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	# patch for #62245: will be fixed in 0.17
	patch -p1 < "${srcdir}/skip-associate-event.patch"
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/iwd \
	--enable-wired \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
	install -Dm644 "${srcdir}/iwd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/iwd.conf"
}

# vim: set ts=2 sw=2 ft=sh noet:
