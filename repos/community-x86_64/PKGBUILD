# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Filipe Laíns <lains@archlinux.org>
# Contributor: Marq Schneider <queueRAM@gmail.com>
# Contributor: Nick Østergaard
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgname=kicad
pkgver=6.0.11
pkgrel=1
pkgdesc='Electronic schematic and printed circuit board (PCB) design tools'
arch=('x86_64')
url='http://kicad.org/'
license=('GPL')
depends=(
  'boost-libs'
  'curl'
  'glew'
  'glm'
  'ngspice'
  'opencascade'
  'python'
  'python-wxpython'
  'wxwidgets-gtk3'
)
makedepends=(
  'git'
  'cmake'
  'mesa'
  'boost'
  'swig'
)
optdepends=(
  'kicad-library: for footprints, symbols and templates'
  'kicad-library-3d: for 3D models of components'
)
_commit='cdf04d55821ea53f821678025b0fd85e9552fbac'
source=(
  "$pkgname::git+https://gitlab.com/kicad/code/kicad.git#commit=$_commit"
  'fix-archive-permissions.patch'
  'fix-version-string.patch'
)
sha512sums=('SKIP'
            'e655d37f743e131e60ea8143b78c195ea5233b1336a0cd73c3388bf27848240622843e31c895467fcb8af5d74e351676b353c524c835b9dde49e1e6bc85e0dce'
            '229559222b96f7ff4334e69142934b1ac74cfdb01b76dd16f1d56fcf70dfe06fa928de4bb9ffb46e023f4a3918bceb04fa39758e2673f51354304ebd2ef6d8b0')
b2sums=('SKIP'
        'fd1312d6adc0a9d00734a95a76fd0ed82824cfae0e626bfa2e0a24a710dd7a9f30d8d9d8b80a278d94aab8dd6d06bfe5edb41f03bb3bb8d4c3b5e9f066d49a84'
        'bad6ad3b0278e24717cf8b1c5a30989cb9a329aa7e0fd36c9b2197c2031783c4873b1e0fb5ad9827c15cf93271fb910afddac2c853153e9f7f98bd703725732b')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed "s/^v//"
}

prepare() {
  cd "$pkgname"

  patch -p1 -i ../fix-archive-permissions.patch
  patch -p1 -i ../fix-version-string.patch
}

build() {
  cmake \
    -S "$pkgname" \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D KICAD_USE_OCC=ON \
    -D KICAD_USE_EGL=ON \
    -D KICAD_SCRIPTING_WXPYTHON=ON \
    -D KICAD_BUILD_I18N=ON \
    -D KICAD_I18N_UNIX_STRICT_PATH=ON \
    -D KICAD_BUILD_QA_TESTS=OFF \
    -W no-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  strip "$STRIP_SHARED" "${pkgdir}${site_packages}/_pcbnew.so"
}

# vim:set ts=2 sw=2 et:
