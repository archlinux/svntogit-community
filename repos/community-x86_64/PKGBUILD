# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pycares
pkgver=4.1.2
pkgrel=3
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('c-ares' 'python-cffi')
makedepends=('python-setuptools')
#checkdepends=('python-idna')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/p/pycares/pycares-$pkgver.tar.gz")
sha256sums=('03490be0e7b51a0c8073f877bec347eff31003f64f57d9518d419d9369452837')
b2sums=('0996030aa19aac737a7d1e98561bc4b99d6e99c97e989a823b4e4222de895e8fcf49bbfa76e39cfe59a39394c64f8f60e3da1b157cd3a62d4e2f1e8e71607f59')

build() {
  cd pycares-$pkgver
  PYCARES_USE_SYSTEM_LIB=1 python setup.py build
}

# Test failures: test_custom_resolvconf, test_getaddrinfo{,2..4},
#                gethostbyaddr{,6}, gethostbyname{,_small_timeout},
#                test_getnameinfo
#check() {
#  cd pycares-$pkgver
#  mkdir -p temp
#  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
#  python setup.py install --root=temp --optimize=1 --skip-build
#  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" python tests/tests.py
#}

package() {
  cd pycares-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
