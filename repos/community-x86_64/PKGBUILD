# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=feed
pkgname=haskell-feed
pkgver=1.0.0.0
pkgrel=108
pkgdesc="Interfacing with RSS (v 0.9x, 2.x, 1.0) + Atom feeds."
url="https://github.com/bergmark/feed"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-old-locale' 'haskell-old-time' 'haskell-safe'
         'haskell-time-locale-compat' 'haskell-utf8-string' 'haskell-xml-types'
         'haskell-xml-conduit')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('dfda1387e2af46fa703634ae3988c243c59bf0c00eefe5ed193c51d5c18131221fa20cfb83404e7c227a2962e4d621186579f7c2d9353d19c01d9277de13ae4d')

prepare() {
    sed -e 's/, xml-conduit >= 1.3 && < 1.6/, xml-conduit >= 1.3/' \
        -e 's/<.*4.11/<5/' -e 's/==.*0.9./==0.10./' \
        -i $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
