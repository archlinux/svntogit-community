# Maintainer: Anatol Pomozov
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Philipp Schrader <philipp.schrader+arch@gmail.com>

pkgname=uboot-tools
pkgver=2019.01
pkgrel=1
pkgdesc='U-Boot bootloader utility tools'
arch=(x86_64)
url='https://www.denx.de/wiki/U-Boot/WebHome'
license=(GPL)
depends=(openssl)
source=(ftp://ftp.denx.de/pub/u-boot/u-boot-$pkgver.tar.bz2{,.sig})
validpgpkeys=('E872DB409C1A687EFBE8633687F9F635D31D7652')
sha1sums=('3c4d22eea02032488e1a4cc5da202bb2469cf6fa'
          'SKIP')

build() {
  cd u-boot-$pkgver
  make defconfig
  make tools-all
}

package() {
  install -m 755 -d "$pkgdir"/usr/bin
  install -m 755 -t "$pkgdir"/usr/bin/ u-boot-$pkgver/tools/{mk{,env}image,env/fw_printenv,img2srec,dumpimage,netconsole,jtagconsole,ncb}
  ln -s fw_printenv "$pkgdir"/usr/bin/fw_setenv
  install -m 644 -D u-boot-$pkgver/doc/mkimage.1 "$pkgdir"/usr/share/man/man1/mkimage.1
}
