# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrián Pérez de Castro <aperez@igalia.com>

pkgname=lowdown
pkgdesc="A simple Markdown translator"
pkgver=0.10.0
_pkgver="VERSION_${pkgver//./_}"
pkgrel=2
license=('ISC')
url="https://kristaps.bsd.lv/lowdown"
arch=('x86_64')
depends=('glibc' 'libmd')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/kristapsdz/lowdown/archive/$_pkgver.tar.gz"
  'shared.patch'
)
sha512sums=('7454e618607628ec0a1649f44f5ec64f8778ecaa151f6aad4984935e297abfe8e84ffc321d3c93cec5d336ff14b6bf9a0ff9054e7363cc58ba708a5b60db9048'
            'ade69503093b22ea392a2a76f0979a89c8f0676532a823250791f33f3bad06dea5d12f314b9c35fed8bea9e031334d591f5a301078cc87ec4cebc759b86bc650')
b2sums=('93d87e3aca2466398a625750b928062fbafd26311899e52ddd8264218675e78d30714c920f3331619fcd510a92cc6046401c946f5bd1465d6fd46f26937318ac'
        '746230be62fcc61e2e4850ef1b5989b2b03dbbbdce52edd790e1c5571977172149bf6d25c9be7d80d267bb2f783096d0cbecc92f5957444935b90d633b445351')

prepare() {
  cd "$pkgname-$_pkgver"

  # convert to shared library
  # NOTE: https://github.com/kristapsdz/lowdown/commit/e33536ae131e31adf77d3258c0cdd3da10868534
  # NOTE: ensure -lmd is passed to .so after next version
  patch --forward --strip=1 --input="$srcdir/shared.patch"
}

build () {
  cd "$pkgname-$_pkgver"

	./configure PREFIX=/usr MANDIR=/usr/share/man

  # ensure LDFLAGS is passed correctly
  sed -i "s/^LDFLAGS.*/LDFLAGS = $LDFLAGS/" Makefile.configure

	make
}

check () {
	make -C "$pkgname-$_pkgver" regress
}

package () {
  cd "$pkgname-$_pkgver"

  # package
	make DESTDIR="$pkgdir" install

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
