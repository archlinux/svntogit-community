# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libimagequant
pkgver=2.12.2
pkgrel=1
pkgdesc="Library for high-quality conversion of RGBA images to 8-bit indexed-color (palette) images"
arch=('i686' 'x86_64')
url="https://pngquant.org/lib/"
license=('BSD')
depends=('gcc-libs')
source=("https://github.com/ImageOptim/libimagequant/archive/$pkgver/libimagequant-$pkgver.tar.gz")
sha256sums=('23ccecb4898ec17474914cfd2fbc4684425f7fd249117f2f1e3f3ba0bf8159e6')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    sed -r 's/^install:.*/install:/;/install.*STATICLIB/d' -i Makefile
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-openmp
    make shared imagequant.pc
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
