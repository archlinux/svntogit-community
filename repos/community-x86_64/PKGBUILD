# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.29.0
pkgrel=2
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qca-qt5' 'qt5-script' 'qt5-webengine' 'qt5-xmlpatterns' 'libofx'
         'grantlee' 'knewstuff' 'krunner' 'knotifyconfig' 'kparts'
         'sqlcipher' 'python' 'hicolor-icon-theme')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt5-tools' 'kdesignerplugin')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('e7c62be4ca4909a0f7d411d305231f36688ed56e65ba87859ace67554e55e37c'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  cmake . -DSKG_WEBENGINE=ON \
          -DSKG_WEBKIT=OFF \
          -DSKG_BUILD_TEST=OFF
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
