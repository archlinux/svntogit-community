# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tzdata
pkgname=haskell-tzdata
pkgver=0.1.20190911.0
pkgrel=14
pkgdesc="Haskell package that distributes the standard time zone database"
url="https://github.com/nilcons/haskell-tzdata"
license=('Apache')
arch=('x86_64')
depends=('ghc-libs' 'haskell-vector')
makedepends=('ghc' 'haskell-test-framework' 'haskell-test-framework-hunit'
             'haskell-test-framework-th')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('f4c4fec7a8fb7c4706ee73ad19f3b96b7299b524dffcdf012c1236c866755c1e6a08434e22f0a58aae89d3b9c79bcd72a4c58fdfa7cad25db3786fe30db252fa')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
