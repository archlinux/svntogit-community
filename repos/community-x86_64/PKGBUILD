# Maintainer: Ivy Foster <iff@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Alex Szczuczko <alex at szc dot ca>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Imanol Celaya <ilcra1989@gmail.com>

pkgname=bmake
pkgver=20230126
pkgrel=1
pkgdesc='Portable version of the NetBSD make build tool'
arch=(x86_64)
url='https://www.crufty.net/help/sjg/bmake.html'
license=(BSD)
# upstream recommends using python for improved meta2deps script
depends=(python)
source=(
	"https://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz"
	"https://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz.asc"
)
sha256sums=('864f7218582cf790ec73b20b7105425cb9ffa335222541772f0313306b6a0bc4'
            'SKIP')
validpgpkeys=(7E228507C26D8DC164F531BFBA54C8AF755A2A99)

build() {
	cd bmake
	./boot-strap --prefix=/usr op=build
}

# op=build also runs unit tests; thus no check()

package() {
	cd bmake

	./boot-strap --install-destdir="$pkgdir" --prefix=/usr op=install

	rm -rf "$pkgdir/usr/share/man/cat1"
	install -Dm644 bmake.1 "$pkgdir/usr/share/man/man1/bmake.1"
	install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	chmod 755 "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man1"
}
