# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: hydro <hydro@freenet.de>

pkgname=mediainfo
pkgver=17.12
pkgrel=1

pkgdesc="supplies technical and tag information about a video or audio file"
url="http://mediaarea.net"
arch=('x86_64')
license=('GPL')

depends=('libmediainfo')
makedepends=('libtool' 'automake' 'autoconf')

source=(https://mediaarea.net/download/source/mediainfo/$pkgver/mediainfo_$pkgver.tar.xz)

sha256sums=('ce14ae3b15fb36e9cd21c73ac1b4c58bcc7d9f52b1fd110a7d68b0e12d70addf')

build() {
  cd MediaInfo/Project/GNU/CLI

  sh ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd MediaInfo/Project/GNU/CLI
  make DESTDIR="$pkgdir" install
}
