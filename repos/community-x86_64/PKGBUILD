# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: <david@pako.st>
# Contributor: Alexander Rødseth <rodseth@gmail.com>

pkgname=chicken
pkgver=4.8.0.6
pkgrel=1
pkgdesc='Compiler for R5RS scheme, featuring handy extensions'
arch=('x86_64' 'i686')
url='http://call-cc.org/'
license=('BSD')
depends=('gcc')
makedepends=('inetutils' 'chicken')
options=('staticlibs')
source=("http://code.call-cc.org/releases/${pkgver%.*}/$pkgname-$pkgver.tar.gz")
sha256sums=('74bc45a09b8f1fc41942e6aabf55ac75a5a9b2b2a9756eed2aca82fabe86f7f9')

build() {
  make -C "$pkgname-$pkgver" -j1 PLATFORM=linux PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make -j1 PLATFORM=linux PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
