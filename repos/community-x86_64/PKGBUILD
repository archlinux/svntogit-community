# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=hiredis
pkgver=0.13.1
pkgrel=1
pkgdesc='Minimalistic C client library for Redis'
arch=('x86_64' 'i686')
url="https://github.com/redis/hiredis/"
license=('BSD')
depends=('glibc')
checkdepends=('redis')
source=(https://github.com/redis/$pkgname/archive/v$pkgver/hiredis-$pkgver.tar.gz)
sha256sums=('8865105e15331156a74b64aafbfd3f8c784a8375e003a55512dcca3d82168487')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make PREFIX=/usr
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  sed -r 's|echo \\|echo -e \\|' -i Makefile
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
