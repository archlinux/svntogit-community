# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=kmon
pkgver=1.5.5
pkgrel=1
pkgdesc="Linux kernel manager and activity monitor"
arch=('x86_64')
url="https://github.com/orhun/kmon"
license=('GPL3')
depends=('kmod' 'libxcb')
makedepends=('rust' 'python' 'git')
source=("git+${url}.git#tag=v${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('C4B2D24CF87CD188C79D00BB485B7C52E9EC0DC6') # kmon releases <kmonlinux@protonmail.com>

prepare() {
    cd "${srcdir}"/${pkgname}

    cargo fetch --locked
}

build() {
    cd "${srcdir}"/${pkgname}

    cargo build --release --frozen
}

check() {
    cd "${srcdir}"/${pkgname}

    cargo test --frozen
}

package() {
    cd "${srcdir}"/${pkgname}

    install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
    install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}
    install -Dm644 man/${pkgname}.8 -t "${pkgdir}"/usr/share/man/man8
}
