# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor:  KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.70.0
pkgrel=1
pkgdesc='A new type of shell'
arch=('x86_64')
url='https://www.nushell.sh'
license=('MIT')
depends=('openssl' 'libxcb' 'libx11' 'curl' 'bzip2')
makedepends=('git' 'rust')
options=('!lto')
install=nushell.install
_commit='9ef65dcd692b502d7476b1787247fae8638c2f0c'
source=("$pkgname::git+https://github.com/nushell/nushell.git#commit=$_commit")
b2sums=('SKIP')

# NOTE: as of 0.40.0, upstream uses upx to reduce binary size, but this strips
# RELRO + PIE, so we do not enable this

pkgver() {
  cd "$pkgname"
  git describe --tags
}

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"
  cargo build --release --frozen --workspace --features=extra
}

check() {
  cd "$pkgname"
  cargo test --frozen --workspace --features=extra
}

package() {
  cd "$pkgname"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -name "nu*" \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
