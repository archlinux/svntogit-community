# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-panel
pkgver=0.9.0
pkgrel=2
pkgdesc="The LXQt desktop panel"
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=("libxcomposite" "lxmenu-data" "lxqt-common" "lxqt-globalkeys"
         "liblxqt-mount" "menu-cache")
optdepends=("kguiaddons: Keyboard indicator plugin"
            "libpulse: Volume control plugin"
            "libstatgrab: CPU monitor and Network monitor plugins"
            "libsysstat: System Statistics plugin"
            "lm_sensors: Sensors plugin")
makedepends=("cmake" "kguiaddons" "libpulse" "libstatgrab" "libsysstat"
             "lm_sensors" "qt5-tools")
source=("http://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=("96b2c2f1b7de7605ac79f094936e6fcf8f0034342e11d84624a033f2450f6bf1")


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
