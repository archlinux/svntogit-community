# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Sherlock Holo <sherlockya(at)gmail.com>
# Contributor: Ariel AxionL <i [at] axionl [dot] me>

pkgname=dns-over-https
pkgver=2.2.2
pkgrel=1
pkgdesc="Client and server software to query DNS over HTTPS, using Google DNS-over-HTTPS protocol"
url="https://github.com/m13253/dns-over-https"
arch=('x86_64')
license=('MIT')
backup=('etc/dns-over-https/doh-client.conf'
        'etc/dns-over-https/doh-server.conf')
provides=('dns-over-https-client' 'dns-over-https-server')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/m13253/dns-over-https/archive/v$pkgver.tar.gz")
sha256sums=('78785dae932f68a318edda2bea8f688bf1a399d75f8a40dfefc262dfe3de9eb3')

prepare(){
  cd "$pkgname-$pkgver"
  sed -i 's/\/local//g' systemd/doh-{client,server}.service
}

build() {
  cd "$pkgname-$pkgver"
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -v -o client ./doh-client
  go build -v -o server ./doh-server
}

check(){
  cd "$pkgname-$pkgver"
  go test -v ./...
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 client "$pkgdir"/usr/bin/doh-client
  install -Dm644 doh-client/doh-client.conf "$pkgdir"/etc/dns-over-https/doh-client.conf
  install -Dm644 systemd/doh-client.service "$pkgdir"/usr/lib/systemd/system/doh-client.service
  install -Dm755 NetworkManager/dispatcher.d/doh-client "$pkgdir"/etc/NetworkManager/dispatcher.d/doh-client

  install -Dm755 server "$pkgdir"/usr/bin/doh-server
  install -Dm644 doh-server/doh-server.conf "$pkgdir"/etc/dns-over-https/doh-server.conf
  install -Dm644 systemd/doh-server.service "$pkgdir"/usr/lib/systemd/system/doh-server.service
  install -Dm755 NetworkManager/dispatcher.d/doh-server "$pkgdir"/etc/NetworkManager/dispatcher.d/doh-server

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
