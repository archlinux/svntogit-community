# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
# PKGBUILD base; Simper < simonpersson1 AT gmail DOT com >

pkgname=kup
pkgver=0.9.0
pkgrel=1
pkgdesc='A backup software for helping people to keep up-to-date backups'
arch=(x86_64)
url='https://apps.kde.org/kup/'
license=(GPL2)
depends=(kidletime kinit libgit2 hicolor-icon-theme)
makedepends=(extra-cmake-modules plasma-framework)
optdepends=('rsync: support for synced backups')
source=(https://download.kde.org/stable/kup/kup-$pkgver.tar.xz)
sha256sums=('90e188cf7abd3ddf47129307a8d9acee5c85d762ef9ed846ccdfe371d0aba835')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
