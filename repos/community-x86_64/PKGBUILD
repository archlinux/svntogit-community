# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tyler Langlois <ty |at| tjll |dot| net>

pkgbase=beats
pkgname=(filebeat packetbeat metricbeat heartbeat-elastic auditbeat journalbeat)
pkgver=7.5.2
pkgrel=1
pkgdesc='Data shippers for Elasticsearch'
arch=('x86_64')
url='https://www.elastic.co/products/beats'
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git' 'libpcap' 'rsync' 'python2-virtualenv' 'audit')
optdepends=('elasticsearch: for standalone installation'
            'python: for migration script')
options=('!makeflags')
source=("https://github.com/elastic/beats/archive/v$pkgver/beats-$pkgver.tar.gz"
        "filebeat.service"
        "packetbeat.service"
        "metricbeat.service"
        "heartbeat.service"
        "auditbeat.service"
        "journalbeat.service"
        "tmpfile.conf"
        "magefile_text_file_busy.patch")
sha256sums=('fcfecd4fca374843efac346e5db40272ab306e2c971e074efb71ada1c17f0e12'
            '7256be466b4255cf3ec9e3600998ec006e762508345a08db16e004a1f15083dc'
            '9f4e33fc768d3500fa3428dcbfda241c6473a2cae8cc4dbadf8f61ca9f2bbec5'
            '7d83a068e8c4da3de3f42bf21b840a86900e9cf02f5ba0851501a1620e5fa29d'
            '878b28a891c9683da4b3d8b6914212e3de0f5e832633c54c6263b3099eb96ba2'
            '5ee738b911022494bc9f8422a4fa7ab7862bd635ca7b08fa3bc8d789ef2785bf'
            'c3633d95314d11dd4e153197f22f308c239ced9240291a9aeaec4f6721911e01'
            'f29361772f23ec42bb15b5e0ca7e93f17fe00d31cf08a9e2c0d0ab27f2277bbc'
            '6fdef755fe03d1c83aa9dfe0cba505b4a9d3e4134cb6d3f9c62e9795a3b7c85a')

prepare() {
    mkdir -p "$srcdir/bin"
    ln -s /usr/bin/python2 "$srcdir/bin/python"
    ln -s /usr/bin/pip2 "$srcdir/bin/pip"
    ln -s /usr/bin/virtualenv2 "$srcdir/bin/virtualenv"

    export GOPATH="$srcdir"/gopath
    mkdir -p "$GOPATH"
    go get golang.org/x/sys/cpu

    cd "$srcdir"/beats-$pkgver
    patch -p1 -i "$srcdir"/magefile_text_file_busy.patch

    # Perform some timestomping to avoid make warnings
    LANG=C _t="$(date -r Makefile +'%Y-%m-%d %k:%M:%S')"
    touch -m -d "$_t" */Makefile

    # Use version instead of commit id
    sed -ri "s/^COMMIT_ID=.*/COMMIT_ID=$pkgver/" libbeat/scripts/Makefile

    # Workaround to place extracted release into GOPATH
    mkdir -p "$GOPATH"/src/github.com/elastic
    ln -sf ../../../../beats-$pkgver "$GOPATH"/src/github.com/elastic/beats

    # Patch go flags, remove -i for trimpath and add extldflags
    sed -E 's|-i (-ldflags ")|-v -trimpath \1-extldflags='"${LDFLAGS}"' |' -i libbeat/scripts/Makefile
}

build() {
    export GOPATH="$srcdir"/gopath
    export PATH="$GOPATH/bin:$srcdir/bin:$PATH"

    cd "$GOPATH"/src/github.com/elastic/beats
    git init # git root required by one of the build scripts

    cd libbeat
    make update

    for beat in ${pkgname[@]}; do
        beat="${beat%-elastic}"
        echo "-> Building $beat..."
        cd ../$beat
        make $beat
        make update
    done
}

_do_package_beat() {
    _pkgname="${pkgname%-elastic}"

    backup=(etc/$_pkgname/$_pkgname.yml)

    install -dm755 "$pkgdir"/{etc,usr/share,usr/share/licenses,var/lib}/$_pkgname
    install -dm755 "$pkgdir"/usr/lib/tmpfiles.d

    cd "$srcdir"/beats-$pkgver
    install -Dm644 NOTICE.txt "$pkgdir"/usr/share/licenses/$pkgname/NOTICE.txt

    cd $_pkgname

    cp $_pkgname.{yml,reference.yml} "$pkgdir"/etc/$_pkgname
    install -Dm644 fields.yml "$pkgdir"/etc/$_pkgname/fields.yml

    install -Dm755 $_pkgname \
                   "$pkgdir"/usr/bin/$_pkgname
    install -Dm644 "$srcdir"/$_pkgname.service \
                   "$pkgdir"/usr/lib/systemd/system/$_pkgname.service

    sed "s/BEATNAME/$_pkgname/g" "$srcdir"/tmpfile.conf > "$pkgdir"/usr/lib/tmpfiles.d/$_pkgname.conf

    if [ -d build/kibana ]; then
        cp -R build/kibana "$pkgdir"/usr/share/$_pkgname/
    elif [ -d _meta/kibana.generated ]; then
        cp -R _meta/kibana.generated "$pkgdir"/usr/share/$_pkgname/kibana
    fi
}

package_filebeat() {
    pkgdesc='Sends log files to Logstash or directly to Elasticsearch'

    _do_package_beat
    cp -R build/package/modules.d "$pkgdir"/etc/$_pkgname/
    cp -R build/package/module "$pkgdir"/usr/share/$_pkgname/
}

package_packetbeat() {
    pkgdesc='Analyzes network traffic and sends the data to Elasticsearch'
    depends=('libpcap')

    _do_package_beat
}

package_metricbeat() {
    pkgdesc='Server monitoring agent that sends metrics to Elasticsearch'
    conflicts=('topbeat')
    replaces=('topbeat')

    _do_package_beat
    cp -R modules.d "$pkgdir"/etc/$_pkgname/
}

package_heartbeat-elastic() {
    pkgdesc='Ping remote services for availability and log results to Elasticsearch or send to Logstash'
    conflicts=('heartbeat')
    replaces=('heartbeat')

    _do_package_beat
    cp -R monitors.d "$pkgdir"/etc/$_pkgname/
}

package_auditbeat() {
    pkgdesc='Audit the activities of users and processes on your system'
    depends=('audit')
    install='auditbeat.install'

    _do_package_beat
    install -D module/auditd/_meta/audit.rules.d/sample-rules-linux-64bit.conf "$pkgdir"/etc/$_pkgname/audit.rules.d/sample-rules.conf.disabled
}

package_journalbeat() {
    pkgdesc='Data collector to ship systemd journal entries to Elasticsearch or Logstash'

    _do_package_beat
}

# vim: ts=4 sw=4 et:
