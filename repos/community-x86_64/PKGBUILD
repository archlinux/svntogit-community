# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=fortune-mod
pkgver=3.6.1
pkgrel=1
pkgdesc="The Fortune Cookie Program from BSD games"
arch=('x86_64')
url="https://www.shlomifish.org/open-source/projects/fortune-mod/"
license=('BSD')
depends=('recode')
makedepends=('cmake' 'rinutils')
source=("https://github.com/shlomif/fortune-mod/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz"
        'not-a-game.patch')
b2sums=('0ff684ee8aaf5eaabd75f187a3bff533ab4ee6e66c47c57fc700d2671006c8dff42f732fde78b40516f8ee907a52fc1965fb0ff257b12563d645f97c5ebc0031'
        '3af374f348d42bf649199a79afc18a8f0dace50d2b4c8aa090a1a54ccffcb65bf069d26f0d1d417a395c5a3574f7376c5ae1ec8b97dd2385d6b4b944643b1215')


prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../not-a-game.patch
}

build() {
  cd "$pkgname-$pkgver"
  cmake -B build -S . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DNO_OFFENSIVE=OFF \
        -DLOCALDIR=/usr/share/fortune/ \
        -DLOCALODIR=/usr/share/fortune/off/ \
        -DCOOKIEDIR=/usr/share/fortune/ \
        -DOCOOKIEDIR=/usr/share/fortune/off/
  make -C build
}

package() {
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir" \
       FORTDIR="$pkgdir/usr/bin" \
       COOKIEDIR="$pkgdir/usr/share/fortune" \
       BINDIR="$pkgdir/usr/bin" \
       BINMANDIR="$pkgdir/usr/share/man/man1" \
       FORTMANDIR="$pkgdir/usr/share/man/man6" install
  rm "$pkgdir/usr/share/fortune/"{,off/}*.u8
  install -Dm644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
