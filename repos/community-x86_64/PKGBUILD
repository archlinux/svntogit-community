# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrey Vihrov <andrey.vihrov@gmail.com>
# Contributor: Frank Vanderham <twelve.eighty@gmail.com>
# Contributor: Jacob McSwain <jacob.a.mcswain@gmail.com>

pkgname=broadcom-wl-dkms
pkgver=6.30.223.271
pkgrel=13
pkgdesc='Broadcom 802.11 Linux STA wireless driver'
arch=('x86_64' 'i686')
url="https://www.broadcom.com/support/download-search/?pf=Wireless+LAN+Infrastructure"
license=('custom')
depends=('dkms')
conflicts=('broadcom-wl')
install=broadcom-wl-dkms.install
source=('broadcom-wl-dkms.conf'
        'dkms.conf.in'
        '001-null-pointer-fix.patch'
        '002-rdtscl.patch'
        '003-linux47.patch'
        '004-linux48.patch'
        '005-debian-fix-kernel-warnings.patch'
        '006-linux411.patch'
        '007-linux412.patch')
source_i686=("https://docs.broadcom.com/docs-and-downloads/docs/linux_sta/hybrid-v35-nodebug-pcoem-${pkgver//./_}.tar.gz")
source_x86_64=("https://docs.broadcom.com/docs-and-downloads/docs/linux_sta/hybrid-v35_64-nodebug-pcoem-${pkgver//./_}.tar.gz")
sha256sums=('b97bc588420d1542f73279e71975ccb5d81d75e534e7b5717e01d6e6adf6a283'
            '06b20686c0dce5d7c5ca8930ac896ed9444be6638051d8aace117462fabd628a'
            '32e505a651fdb9fd5e4870a9d6de21dd703dead768c2b3340a2ca46671a5852f'
            '4ea03f102248beb8963ad00bd3e36e67519a90fa39244db065e74038c98360dd'
            '30ce1d5e8bf78aee487d0f3ac76756e1060777f70ed1a9cf95215c3a52cfbe2e'
            '09d709df0c764118ca43117f5c096163d9669a28170da8476d4b8211bd225d2e'
            '2306a59f9e7413f35a0669346dcd05ef86fa37c23b566dceb0c6dbee67e4d299'
            '5bc12cb57712e6a944dff1c90de50135c2508085d8497ab99284ccccdb35c32b'
            '207559303f411de84821fbf16a94ba1ccf1e57495628fda98b9501c47cb64614')
sha256sums_x86_64=('5f79774d5beec8f7636b59c0fb07a03108eef1e3fd3245638b20858c714144be')
sha256sums_i686=('4f8b70b293ac8cc5c70e571ad5d1878d0f29d133a46fe7869868d9c19b5058cd')

prepare() {
  sed -i -e '/BRCM_WLAN_IFNAME/s/eth/wlan/' src/wl/sys/wl_linux.c
  sed -i -e "/EXTRA_LDFLAGS/s|\$(src)/lib|/usr/lib/${pkgname}|" Makefile
  sed -e "s/@PACKAGE_VERSION@/${pkgver}/" dkms.conf.in > dkms.conf
}

package() {
  local dest="$pkgdir/usr/src/${pkgname/-dkms/}-$pkgver"
  mkdir -p "$dest"
  cp -a src Makefile dkms.conf "$dest"
  install -Dm644 -t "$dest/patches" *.patch
  install -Dm644 lib/wlc_hybrid.o_shipped "${pkgdir}/usr/lib/${pkgname}/wlc_hybrid.o_shipped"
  install -Dm644 broadcom-wl-dkms.conf "${pkgdir}/usr/lib/modprobe.d/broadcom-wl-dkms.conf"
  local ldir="$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 lib/LICENSE.txt "${ldir}/LICENSE.shipped"
  sed -n -e '/Copyright/,/SOFTWARE\./{s/^ \* //;p}' src/wl/sys/wl_linux.c > "${ldir}/LICENSE.module"
}

# vim: ts=2 sw=2 et:
