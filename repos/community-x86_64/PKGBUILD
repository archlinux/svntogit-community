# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

pkgname=marco
pkgver=1.8.2
pkgrel=4
pkgdesc="A window manager for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'libcanberra' 'libgtop' 'libice' 'libsm' 'libxt' 'mate-desktop'
         'mate-dialogs' 'startup-notification')
makedepends=('mate-common' 'perl-xml-parser' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
replaces=('mate-window-manager')
provides=('mate-window-manager')
conflicts=('mate-window-manager')
options=('!emptydirs')
groups=('mate')
source=("http://pub.mate-desktop.org/releases/1.8/${pkgname}-${pkgver}.tar.xz"
        'do_not_add_shadows_to_ARGB_windows.diff'
        'update_keybindings_with_new_name.diff'
        'fix_maximise.diff')
sha1sums=('0b361403ad4603d6b93f1b7b684c8c5b905ddf3c'
          'a2af3d1d6ea6a3b19bf9aa290036b2c7181a3587'
          'f09d9871ef920e7848458bca853589f482f340b3'
          '956f921e8f8fbdd77de4b2eec1b663b7b165107e')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    # Adapted from the commit below to correct drop down menu rendering with GTK 3.14.
    # - https://git.gnome.org/browse/metacity/commit/?id=a6b29b2d2f6a7787c59cfffdc2bed1b5b5b99244
    patch -Np1 -i "${srcdir}/do_not_add_shadows_to_ARGB_windows.diff"
    # Update keybindings files with new name
    # - https://github.com/mate-desktop/marco/commit/c2cbcaa90d88d3d74dc7b79cae17964f4243a51d
    patch -Np1 -i "${srcdir}/update_keybindings_with_new_name.diff"
    # Fix maximise
    # https://github.com/infirit/marco/commit/7fe362e60b79bd2d74a3362c792ec5a2411a4ffe
    patch -Np1 -i "${srcdir}/fix_maximise.diff"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-startup-notification \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
