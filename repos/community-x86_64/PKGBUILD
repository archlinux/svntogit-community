# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=cbor2
pkgname=python-cbor2
pkgver=5.4.3
pkgrel=1
pkgdesc="Pure Python CBOR (de)serializer with extensive tag support"
arch=(x86_64)
url="https://github.com/agronholm/cbor2"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools-scm python-toml python-wheel)
checkdepends=(python-pytest)
options=(debug)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('62b863c5ee6ced4032afe948f3c1484f375550995d3b8498145237fe28e546c2')
b2sums=('0303ed49a1c468a3aa13e12a4bebef17479f106bc3f0a92e35c6029ff1bd86aea6a77f557ac0c8acb5e055ec0b56c82e5c95e9417d00715a1fb244bd94d0f60d')

prepare() {
  cd "${_name}-$pkgver"
  sed -i -E '/setuptools/s/ >= 61//' pyproject.toml setup.py
}

build() {
  cd "${_name}-$pkgver"
  python -m build -wn
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v -c /dev/null tests
}

package() {
  cd "${_name}-$pkgver"
  python -m installer -d "${pkgdir}" dist/*.whl
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
