# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=rhit
pkgver=1.7.1
pkgrel=1
pkgdesc="A nginx log explorer"
arch=('x86_64')
url="https://github.com/Canop/rhit"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('69b5546a0730ee1415cdfcff08955b0224205c0d78a3d9cf09bd1ae0a93976648cb3d87f66cc4cd27603a5ca80a47ea62bce28e6a3f24385c5382eceb47c0a41')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
