# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.2.4
pkgrel=4
pkgdesc='Lexical analyser generator for Haskell'
arch=('x86_64')
url='https://hackage.haskell.org/package/alex'
license=('custom:BSD3')
depends=('ghc-libs')
makedepends=('happy' 'ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e9e99caef4bc985aa37bd42488121f9b83c4f3026c9745ad12f50b5896e9f7c07f48a753c647c30ff16be804529d2478f8d90fa38e352f1d8861e610651c871a')

build() {
  cd "$pkgname-$pkgver"

  runghc Setup.hs configure --prefix=/usr --datasubdir="$pkgname" --enable-executable-dynamic
  runghc Setup.hs build
}

package() {
  cd "$pkgname-$pkgver"

  runghc Setup.hs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"

  # Doc only contains the license
  rm -rf "$pkgdir/usr/share/doc"
}

# getver: hackage.haskell.org/package/alex/changelog
# vim: ts=2 sw=2 et:
