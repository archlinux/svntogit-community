# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Florian Pritz <f-p@gmx.at>
# Contributor: Xyne <xyne at archlinux dot us>
# Contributor: thiagoc
# Contributor: Chris <chris at christopherjones dot us>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: Fabiano Furtado <fusca14 at gmail dot com>

pkgname=fwknop
pkgver=2.6.8
pkgrel=4
pkgdesc='FireWall KNock OPerator: Single Packet Authorization and Port Knocking'
url='https://www.cipherdyne.org/fwknop'
arch=('i686' 'x86_64')
license=('GPL2')
backup=(etc/${pkgname}/access.conf
        etc/${pkgname}/fwknopd.conf)
depends=('gpgme' 'iptables' 'libpcap' 'wget')
options=('!libtool')
source=(https://cipherdyne.org/fwknop/download/fwknop-${pkgver}.tar.bz2{,.asc}
        fwknopd.service)
sha512sums=('a9652d65a425a816cdd8677a1a2dac030e545b8ec38b6e7189870a7bf354e3a779d111bc1af8f032ec2fe3abb08aa5c7eec345f9f8094bc555c933541dc0d26b'
            'SKIP'
            'a171682a054495bb10315e98a300ddbe4adf1f653b719d3344ecb6cfb8ccd15043cffbbd28304c4f15569a12ddcd9733409448d596f0c706e48d4fda2c877a6a')
validpgpkeys=('4D6644A9DA036904BDA2CB90E6C9E3350D3E7410') # Michael Rash (Signing key for cipherdyne.org projects) <mbr@cipherdyne.org>

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 "${srcdir}/fwknopd.service" "${pkgdir}/usr/lib/systemd/system/fwknopd.service"
}

# vim: ts=2 sw=2 et:
