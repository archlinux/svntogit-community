# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-wxpython
pkgver=4.0.1
pkgrel=2
pkgdesc="Phoenix wxWidgets GUI toolkit for Python"
arch=('x86_64')
license=('custom:wxWindows')
url="https://www.wxpython.org"
depends=('wxgtk3' 'python-setuptools' 'python-six')
makedepends=('mesa' 'glu' 'webkit2gtk')
source=("https://github.com/wxWidgets/Phoenix/releases/download/wxPython-${pkgver}/wxPython-${pkgver}.tar.gz")
sha256sums=('f8f2ac1a75368b9b103259addc77f4e3dfe729c6d70aa1fd0b7e9c5b6075c710')

build() {
    cd wxPython-${pkgver}
    python3 setup.py build
}

package() {
    cd wxPython-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
    find "$pkgdir/usr/lib" -type f | xargs chmod 644
}
