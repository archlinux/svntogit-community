# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer

pkgname=perl-file-rsyncp
pkgver=0.74
pkgrel=3
pkgdesc='Perl interface to rsync file list encoding and decoding'
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs' '!makeflags')
url='http://search.cpan.org/dist/File-RsyncP'
source=("http://search.cpan.org/CPAN/authors/id/C/CB/CBARRATT/File-RsyncP-$pkgver.tar.gz")
md5sums=('17b73204db0389d882bf5563a41e4a78')

build() {
  cd File-RsyncP-$pkgver
  USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-RsyncP-$pkgver
  make test
}

package() {
  cd File-RsyncP-$pkgver
  make install DESTDIR="$pkgdir/"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

# vim:set ts=2 sw=2 et:
