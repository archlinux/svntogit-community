# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=clthreads
pkgver=2.4.0
pkgrel=4
pkgdesc="C++ wrapper library around the POSIX threads API"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('LGPL')
depends=('gcc-libs')
source=("${url}downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('c307d9611e0530469b36367f05721e6676d7bb186be97d38b851e66d9f670663ceceedf065823b27ac5864725d52cc2f462fd8d96f1398a202e30d82910ec58b')

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  # mandatory fix for broken Makefile behavior (upstream fix is requested)
  install -d "${pkgdir}/usr/include"
  make PREFIX="${pkgdir}/usr" LIBDIR=lib install
}
