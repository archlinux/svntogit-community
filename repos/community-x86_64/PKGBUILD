# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:thefatprecious <marty@loomar.com>

pkgname=tvtime
pkgver=1.0.10
pkgrel=1
pkgdesc="A high quality television application for use with video capture cards"
arch=('i686' 'x86_64')
url='http://linuxtv.org/'
license=('GPL' 'LGPL')
depends=('gcc-libs' 'libpng' 'libxml2' 'freetype2' 'libxtst' 'libxinerama' 'libxv'
	 'libsm' 'libxxf86vm' 'alsa-lib')
makedepends=('libxt' 'v4l-utils')
source=("http://linuxtv.org/downloads/tvtime/tvtime-$pkgver.tar.gz")
md5sums=('97a09d1723c073eb2b8bea48a148a994')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  echo "StartupWMClass=tvtime" >>docs/tvtime.desktop
}

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --localstatedir=/var --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 docs/html/default.tvtime.xml $pkgdir/usr/share/tvtime/default.tvtime.xml
}
