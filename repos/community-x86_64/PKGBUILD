# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich tobias [funnychar] archlinux org

pkgbase=aubio
pkgname=('aubio' 'python-aubio')
pkgver=0.4.9
pkgrel=6
pkgdesc="A tool for extracting annotations from audio signals"
arch=('x86_64')
url="https://aubio.org/"
license=('GPL3')
makedepends=('doxygen' 'ffmpeg' 'fftw' 'jack' 'python-numpy'
'python-setuptools' 'python-sphinx' 'txt2man' 'waf')
checkdepends=('python-pytest')
source=("https://${pkgbase}.org/pub/${pkgbase}-${pkgver}.tar.bz2")
sha512sums=('0cb81bb4b15051db3f3f4d160d500af56fdfb237e0a74e3f366f53c2870030aa0a7cee8469a611a9694c36b8866d3d42ffb48241c999de08f3fee43e6d903130')

prepare() {
 cd "${pkgname}-${pkgver}"
 rm -rv waflib
}

build() {
  cd "${pkgbase}-${pkgver}"
  waf configure --prefix=/usr \
                --alltests \
                --enable-fftw3 \
                --libdir=/usr/lib
  waf build
  python setup.py build
}

check() {
  local _py3_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  local PYTHON3_PATH="${srcdir}/${pkgbase}-${pkgver}/build/lib.linux-$CARCH-${_py3_ver}/:${PYTHONPATH}"
  cd "${pkgbase}-${pkgver}"
  PYTHONPATH="${PYTHON3_PATH}" \
  LD_LIBRARY_PATH="${PWD}/build/src" \
  py.test python/tests
}

package_aubio() {
  pkgdesc="A tool for extracting annotations from audio signals"
  depends=('fftw' 'glibc' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
  'libswresample.so' 'libjack.so' 'libsamplerate' 'libsndfile')
  optdepends=('python2-aubio: Python 2.x interface'
              'python-aubio: Python 3.x interface')
  provides=('libaubio.so')
  groups=('pro-audio')
  cd "${pkgname}-${pkgver}"
  waf --destdir="${pkgdir}" install
  # move doc, as waf configure doesn't honor the htmldir and docdir settings
  mv -v "${pkgdir}/usr/share/doc/lib${pkgname}-doc" \
    "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 {AUTHORS,ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

package_python-aubio() {
  pkgdesc="Python 3.x bindings for aubio"
  depends=('glibc' 'libaubio.so' 'python-numpy' 'python-setuptools')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
