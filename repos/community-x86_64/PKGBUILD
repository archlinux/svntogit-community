# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Matej Grabovsky <matej.grabovsky at gmail>
# Contributor: Kohei Suzuki <eagletmt@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>
# Contributor: Tej Chajed <tchajed at mit dot edu>

pkgname=camlp5
pkgver=7.08
_relname=rel708
pkgrel=1
arch=('x86_64')
pkgdesc='A preprocessor-pretty-printer of OCaml.'
url='https://camlp5.github.io/'
license=('BSD')
depends=('ocaml')
makedepends=('ocaml-compiler-libs')
source=("camlp5-${_relname}.tar.gz::https://github.com/camlp5/camlp5/archive/$_relname.tar.gz")
options=('staticlibs')
sha256sums=('46e67d8e36e5e4558c414f0b569532a33d3a12d7120ffdc474a6b3da1bfff163')

build() {
  cd "${srcdir}/camlp5-${_relname}"

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict

  make world.opt
}

package() {
  cd "${srcdir}/camlp5-${_relname}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 etc/META "${pkgdir}/usr/lib/ocaml/camlp5"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/camlp5/LICENSE"
}

