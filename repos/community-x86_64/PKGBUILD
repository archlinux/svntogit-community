# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

pkgname=rubberband
pkgver=3.1.0
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=(x86_64)
url="https://www.breakfastquay.com/rubberband/"
license=(GPL2)
depends=(gcc-libs glibc)
makedepends=(fftw java-environment ladspa meson libsamplerate libsndfile
vamp-plugin-sdk)
optdepends=(
  'ladspa-host: for LADSPA plugin'
  'vamp-host: for VAMP plugin'
  'vamp-plugin-sdk: for VAMP plugin'
)
provides=(librubberband.so librubberband-jni.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/breakfastquay/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('0c29fb471c968f3038e4b45c056646c94523d86a8701cefed3792b14aeca4aab52ef2e67e46fdc2c169f82710cb2b5f51e53202733a3710eee8d4c94e52b87c2')
b2sums=('73534d870624339dcf6823d6711e7a897de9501a61e54200b296529628a8b116c83a381befd295549b4c84fb3b7841f6ab5ede621f1645952f2d14b556e33afc')

build() {
  local java_major=$(java --version 2>/dev/null |grep 'openjdk'| cut -d ' ' -f2| cut -d '.' -f1)
  local meson_options=(
    -Dfft=fftw
    -Dresampler=libsamplerate
    -Dextra_include_dirs="/usr/lib/jvm/java-$java_major-openjdk/include,/usr/lib/jvm/java-$java_major-openjdk/include/linux"
  )
  export JAVA_HOME="/usr/lib/jvm/java-$java_major-openjdk"

  arch-meson build $pkgname-$pkgver "${meson_options[@]}"
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  depends+=(libfftw3.so libsamplerate.so libsndfile.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
