# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Florian Maunier <fmauneko@dissidence.ovh>
# Contributor: jtmb <packaging at technologicalwizardry dot com>

pkgbase=mono-msbuild
pkgname=(
  mono-msbuild
  mono-msbuild-sdkresolver
)
pkgver=16.5.xamarinxplat.2020.01.10.05.36
pkgrel=1
pkgdesc='Xamarin implementation of the Microsoft build system'
arch=(x86_64)
url=https://github.com/mono/linux-packaging-msbuild
license=(MIT)
depends=(mono)
makedepends=(
  bash
  git
  dotnet-sdk
  unzip
)
source=(
  mono-msbuild::git+https://github.com/mono/linux-packaging-msbuild#tag=c274e3391d1ffa81f3f6a5f72fc25700aef3848f
  mono-msbuild-dotnetbits-case.patch
  mono-msbuild-license-case.patch
  mono-msbuild-no-hostfxr.patch
  mono-msbuild-use-bash.patch
)
sha256sums=(
  SKIP
  f2784ac003dc84c40e8a9532ff954e6d6c29778b3e80cad7f1f91d86a9b43811
  3a12a9c33ad5938e8af24d2985241053602f4efc94a4818a00a17da32ce4aba5
  041f536fbe5a36ba5bf7164460f07bd2becf4ecf2935594bf92676198b7becc2
  a13ecb4125c673372d87a3b7d957fc8716a3c3e74cd08e9e354b5dcf170ed453
)

pkgver() {
  cd mono-msbuild

  git describe --tags | sed 's/.*\///; s/+/./'
}

prepare() {
  cd mono-msbuild

  patch -Np1 -i ../mono-msbuild-dotnetbits-case.patch
  patch -Np1 -i ../mono-msbuild-license-case.patch
  patch -Np1 -i ../mono-msbuild-no-hostfxr.patch
  patch -Np1 -i ../mono-msbuild-use-bash.patch
}

build() {
  cd mono-msbuild

  export DOTNET_MSBUILD_SDK_RESOLVER_CLI_DIR=/opt/dotnet
  ./eng/cibuild_bootstrapped_msbuild.sh --host_type mono --configuration Release --skip_tests /p:DisableNerdbankVersioning=true
  ./stage1/mono-msbuild/msbuild mono/build/install.proj /p:MonoInstallPrefix="${srcdir}"/target/usr /p:Configuration=Release-MONO /p:IgnoreDiffFailure=true
  sed "s@${srcdir}/target@@g" -i "${srcdir}"/target/usr/bin/msbuild
  find "${srcdir}"/target/usr/lib/mono/ -name Microsoft.DiaSymReader.Native.*dll -delete
  find "${srcdir}"/target/usr/lib/mono/ -name *.dylib -delete
  find "${srcdir}"/target/usr/lib/mono/ -name *.so -delete
}

package_mono-msbuild() {
  conflicts=(msbuild)
  provides=(msbuild)

  cp -dr --no-preserve=ownership target/usr "${pkgdir}"/
  rm -rf "${pkgdir}"/usr/lib/mono/msbuild/Current/bin/SdkResolvers/Microsoft.DotNet.MSBuildSdkResolver
}

package_mono-msbuild-sdkresolver() {
  depends=(
    mono-msbuild
    dotnet-host
  )
  conflicts=(msbuild-sdkresolver)
  provides=(msbuild-sdkresolver)

  install -dm 755 "${pkgdir}"/usr/lib/mono/msbuild/Current/bin/SdkResolvers
  cp -dr --no-preserve=ownership target/usr/lib/mono/msbuild/Current/bin/SdkResolvers/Microsoft.DotNet.MSBuildSdkResolver "${pkgdir}"/usr/lib/mono/msbuild/Current/bin/SdkResolvers/
}

# vim: ts=2 sw=2 et:
