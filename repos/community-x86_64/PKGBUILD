# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.79.0
_commit=a1b72611215dbfca257351003204a80c83859e05
pkgrel=1
pkgdesc='A new type of shell'
arch=('x86_64')
url='https://www.nushell.sh'
license=('MIT')
depends=(
  'glibc'
  'gcc-libs'
  'libcrypto.so'
  'libssl.so'
  'zlib'
)
makedepends=('cargo' 'git')
install=nushell.install
source=("git+https://github.com/nushell/nushell.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd "$pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

pkgver() {
  cd "$pkgname"
  git describe --tags
}

_pkgver() {
  curl -fs https://api.github.com/repos/$pkgname/$pkgname/git/ref/tags/$pkgver |
    jq -r .object.sha
}

build() {
  cd "$pkgname"
  CFLAGS+=" -ffat-lto-objects"
  cargo build --release --frozen --workspace --features=extra,dataframe
}

check() {
  cd "$pkgname"
  cargo test --frozen --workspace --features=extra,dataframe
}

package() {
  cd "$pkgname"
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -name "nu*" \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
