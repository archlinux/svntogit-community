# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Jan Fader <jan.fader@web.de>
pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.12
pkgrel=2
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(qt lua51 boost)
source=(http://www.andre-simon.de/zip/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('4b3f01e86a6b3fa25f3c4905a9cd63c7'
         'b784cd3f264a0f281da36f26c0c7f2c2')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  sed -i 's#pkg-config --cflags lua#pkg-config --cflags lua5.1#' src/makefile
  sed -i 's#pkg-config --libs lua#pkg-config --libs lua5.1#' src/makefile
  sed -i 's#lua#lua5.1#g' src/gui-qt/highlight.pro

  make
  make gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua')
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (QT version)"
  depends=('qt' 'highlight')
  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" install-gui
}

# vim:set ts=2 sw=2 et:
