# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Konstantin Gizdov < arch at kge dot pw >

pkgname=unuran
pkgver=1.8.1
pkgrel=6
pkgdesc='A C library for generating non-uniform pseudorandom variates.'
arch=('x86_64')
url='http://statmath.wu.ac.at/unuran/'
license=('GPL2')
depends=('gsl')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::http://statmath.wu.ac.at/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('c270ae96857857dbac6450043df865e0517f52856ddbe5202fd35583b13c5193')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --with-urng-gsl --enable-shared
  make "$MAKEFLAGS"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
