# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=sqlitebrowser
pkgver=2.0b1
_pkgrealver=200_b1
pkgrel=4
pkgdesc="SQLite Database browser is a light GUI editor for SQLite databases, built on top of QT"
arch=('i686' 'x86_64')
url="http://sqlitebrowser.sf.net"
license=('GPL')
depends=('qt4' 'sqlite')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/${pkgname}_${_pkgrealver}_src.tar.gz)
md5sums=('5ca3d41cca454235b93ce72cd585dfe3')

prepare() {
  cd $srcdir/trunk/$pkgname
  sed -i 's|getline|xgetline|' sqlitebrowser/sqlbrowser_util.c
  sed -i 's|: name( 0 ) { }|: name( ) { }|' sqlitebrowser/sqlitedb.h
  sed -i '/sqlite_source\/sqlite3.h/d' sqlitebrowser/sqlbrowser_util.c # Remove two useless #include
  sed -i 's/"sqlite_source\/sqlite3.h"/<sqlite3.h>/' sqlitebrowser/sqlbrowser_util.h
  sed -i -e '1i LIBS += -lsqlite3' -e '/sqlite_source/d' sqlitebrowser/sqlitebrowser.pro
}

build() {
  cd $srcdir/trunk/$pkgname
  qmake-qt4
  make
}

package() {
  cd $srcdir/trunk/$pkgname
  mkdir -p $pkgdir/usr/bin
  cp $pkgname/$pkgname $pkgdir/usr/bin/
}
