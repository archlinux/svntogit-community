# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vico
pkgver=1.2.2
pkgrel=3
pkgdesc="Minimalistic Midi sequencer intended to be used in parallel with other software"
arch=(x86_64)
url="https://www.laborejo.org/vico/"
license=(GPL3)
groups=(pro-audio)
depends=(
  glibc
  hicolor-icon-theme
  python
  python-pyqt5
  ttf-dejavu
)
makedepends=(
  glib2
  jack
  libsndfile
  util-linux-libs
)
optdepends=('new-session-manager: for session management')
source=(https://www.laborejo.org/downloads/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('826ff38db210b768053dda8991c4c22196f6e2fd871eea687492e314133f8f4062421809de097e43ced0ff2b68bdfc43bcd1f5ceb350a1df27001d1ce344721e'
            'SKIP')
b2sums=('6bbbc58ae969f87242f118296149afe9e4cbf92ff76df7f81fb503f1958895057541a391005683eff70122e70510afb0fed11947b59744e1cc37fd67935d8e7d'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  depends+=(
    glib2 libglib-2.0.so
    jack libjack.so
    libsndfile libsndfile.so
    util-linux-libs libuuid.so
  )

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
}
