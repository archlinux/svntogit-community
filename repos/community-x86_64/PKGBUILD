# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hasql-notifications
pkgname=haskell-hasql-notifications
pkgver=0.2.0.2
pkgrel=22
pkgdesc="LISTEN/NOTIFY support for Hasql"
url="https://github.com/diogob/hasql-notifications"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-hasql' 'haskell-hasql-pool' 'haskell-postgresql-libpq')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'pifpaf' 'postgresql')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('b23974e745733546a49b29e1b1b90b44b9209c88cc3c97fd38357b5c8ecfb0c4')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  eval $(pifpaf run postgresql --host 127.0.0.1)
  createuser -s postgres
  createdb hasql_notifications_test
  runhaskell Setup test --show-details=direct
  pifpaf_stop
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
