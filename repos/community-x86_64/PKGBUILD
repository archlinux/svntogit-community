# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Bartlomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jason Pierce <pierce.json@gmail.com>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>
# Contributor: Simon Perry <aur@sanxion.net>

pkgbase=dropbear
pkgname=(dropbear scp)
pkgver=2017.75
pkgrel=4
pkgdesc='Lightweight SSH server'
arch=('x86_64' 'i686')
url='https://matt.ucc.asn.au/dropbear/dropbear.html'
license=('MIT')
validpgpkeys=('F7347EF2EE2E07A267628CA944931494F29C6773')
source=("https://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2"{,.asc}
        "$pkgname.service")
options=('emptydirs')
sha256sums=('6cbc1dcb1c9709d226dff669e5604172a18cf5dbf9a201474d5618ae4465098c'
            'SKIP'
            '8890a35b6cb14745c1b22f80d3a8532de2a853c78a30eac72b954012e286504a')

prepare() {
  sed -i 's,usr/libexec/sftp,usr/lib/ssh/sftp,;/#define DROPBEAR_SMALL_CODE/d' \
    "$pkgname-$pkgver/options.h"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin
  make PROGRAMS="dbclient dropbear dropbearconvert dropbearkey scp" SCPPROGRESS=1
}

package_dropbear() {
  install -d "$pkgdir/etc/$pkgname"
  make -C "$pkgbase-$pkgver" install DESTDIR="$pkgdir"
  install -Dm644 "$pkgname.service" \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 "$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_scp() {
  pkgdesc='Lightweight SCP executable'
  conflicts=('openssl')

  make -C "$pkgbase-$pkgver" install PROGRAMS="scp" DESTDIR="$pkgdir"
  install -Dm644 "$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
