# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=fwupd-efi
pkgver=1.1
pkgrel=1
pkgdesc='EFI Application used by uefi-capsule plugin in fwupd'
arch=('x86_64')
url='https://github.com/fwupd/fwupd'
license=('LGPL')
makedepends=('meson' 'gnu-efi')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('5c01f1ccaf1d232e91d0947937af7bd9a32b1c020a99707d2f5f694b3b4e07f1'
            'SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

build() {
  arch-meson ${pkgname}-${pkgver} build  \
    -D efi_sbat_distro_id='arch' \
    -D efi_sbat_distro_summary='Arch Linux' \
    -D efi_sbat_distro_pkgname=${pkgname} \
    -D efi_sbat_distro_version=${pkgver} \
    -D efi_sbat_distro_url="https://archlinux.org/packages/community/x86_64/${pkgname}/"
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
