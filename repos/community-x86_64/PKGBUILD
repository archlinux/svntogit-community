# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=hedgewars
pkgver=0.9.18
pkgrel=5
pkgdesc="Free Worms-like turn based strategy game"
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL' 'custom')
depends=('qt' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'lua51')
makedepends=('fpc' 'cmake'
	     # haskell specific
	     'ghc=7.6.2-1'
	     'haskell-network=2.4.1.0-1'
             'haskell-bytestring-show=0.3.5.4-3'
	     'haskell-utf8-string=0.3.7-4'
             'haskell-dataenc=0.14.0.5-1'
	     'haskell-hslogger=1.2.1-3'
             'haskell-vector=0.10.0.1-2')
source=(http://download.gna.org/hedgewars/hedgewars-src-$pkgver-3.tar.bz2
	hedgewars.desktop
	hedgewars.png)
md5sums=('02465736fdcf48feea1cacaa0aa5960e'
         '48c66a3204d4fa0a69e9aa46f7c31e0c'
         'eeb14d50df39063549ac5eca9dbc65d1')

build() {
  cd $pkgname-src-$pkgver
  sed -i 's|instance NFData B.ByteString||' gameServer/Actions.hs
#  sed -i 's|lua51 lua5.1 lua-5.1|lua lua51 lua5.1 lua-5.1|g' cmake_modules/FindLua.cmake
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DDATA_INSTALL_DIR=/usr/share \
	-DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
	-DQT_MOC_EXECUTABLE=/usr/bin/moc \
	-DWITH_SERVER=1 .
  make
}

package() {
  cd $pkgname-src-$pkgver

  make DESTDIR=$pkgdir install

  install -D -m644 Fonts_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/Fonts_LICENSE.txt
  install -D -m644 $srcdir/hedgewars.png $pkgdir/usr/share/pixmaps/hedgewars.png
  install -D -m644 $srcdir/hedgewars.desktop $pkgdir/usr/share/applications/hedgewars.desktop
}
