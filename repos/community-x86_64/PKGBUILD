# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: thacrazze <thacrazze|googlemail|com>

pkgname=clipgrab
pkgver=3.5.5
pkgrel=2
pkgdesc='A video downloader and converter for YouTube, Veoh, DailyMotion, MyVideo, ...'
arch=(i686 x86_64)
url='http://clipgrab.de/en'
license=(GPL3)
depends=(qtwebkit)
optdepends=('ffmpeg: for the conversion functionality')
source=(http://download.clipgrab.de/$pkgname-$pkgver.tar.gz
        $pkgname.desktop)
md5sums=('a8fe8f0bc80f69b77d677bc44df2354a'
         '9a141791f6952917c441050c3dac81ce')

build() {
  cd $pkgname-$pkgver

  qmake-qt4 clipgrab.pro
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir/$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"
}
