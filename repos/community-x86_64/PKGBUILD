# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=soplex
pkgver=6.0.3
pkgrel=2
pkgdesc='Sequential object-oriented simPlex'
arch=(x86_64)
url='https://www.scipopt.org/'
license=(Apache)
depends=(zlib mpfr)
makedepends=(cmake boost)
options=(!lto)
source=(https://github.com/scipopt/$pkgname/archive/release-${pkgver//./}/$pkgname-$pkgver.tar.gz)
sha256sums=('2bdf9adc9ac6ad48f98056679b7b852e626ac4aaaf277e7d4ce17794ed1097be')

build() {
  cmake -B build -S $pkgname-release-${pkgver//./} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DMPFR=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
