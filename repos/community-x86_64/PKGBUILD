# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-clevercsv
pkgver=0.7.1
pkgrel=1
pkgdesc="A Python package for handling messy CSV files"
url="https://github.com/alan-turing-institute/CleverCSV"
license=('MIT')
arch=('x86_64')
depends=('python-chardet' 'python-regex')
makedepends=('python-setuptools')
optdepends=('python-pandas' 'python-tabview' 'python-cleo' 'python-clikit' 'python-cchardet')
checkdepends=('python-pytest-runner' 'python-pandas' 'python-tabview' 'python-cleo' 'python-clikit'
              'python-cchardet' 'python-termcolor')
source=("https://github.com/alan-turing-institute/CleverCSV/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4ca03f63974804ce9c0431ed9f7bbc85d52003f83cbfbc70be0cf23f807c90e7e0f5fd863f010066ae5c5fddd4935287e1c6059e2c1267627ba83b1893d79c7f')

build() {
  cd CleverCSV-$pkgver
  python setup.py build
}

check() {
  cd CleverCSV-$pkgver
  PYTHONPATH="$PWD"/build/lib.linux-$CARCH-3.10 python setup.py pytest
}

package() {
  cd CleverCSV-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
