# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnsbmp
pkgver=0.1.1
pkgrel=1
pkgdesc='Decoding library for BMP and ICO image file formats'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libnsbmp/'
license=('MIT')
depends=('glibc')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('584046809d3213e5917cd26eafb03068ca94274229f595f0e0520b15fa454f98')

build() {
  cd "$pkgname-$pkgver"

  make LIBDIR=lib PREFIX=/usr COMPONENT_TYPE="lib-shared" 
}

package() {
  cd "$pkgname-$pkgver"

  make install LIBDIR=lib PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"   
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
