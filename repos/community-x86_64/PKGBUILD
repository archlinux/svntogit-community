# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.18.0
pkgrel=2
pkgdesc="Compiler for high-performance SIMD programming on the CPU"
arch=(x86_64)
url="https://ispc.github.io/"
license=(BSD)
depends=(ncurses zlib llvm-libs clang spirv-llvm-translator)
makedepends=(llvm python lib32-glibc cmake level-zero-headers level-zero-loader openmp vc-intrinsics)
optdepends=(
    'intel-compute-runtime: GPU support'
    'level-zero-loader: GPU support'
    'openmp: GPU support'
)
_gtestcommit=6a7ed316a5cdc07b6d26362c90770787513822d4
source=(https://github.com/ispc/ispc/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        googletest-${_gtestcommit}.zip::https://github.com/google/googletest/archive/${_gtestcommit}.zip
        ispc-libclang-cpp.patch)
sha256sums=('81f2cc23b555c815faf53429e9eee37d1f2f16873ae7074e382ede94721ee042'
            '718cc64f6f563399a228fc439b6ea9cb58d062e66e5340b92ca0f26adecfaac5'
            '51ae0943cbce2b3f29549c5fb262e8620ad5e171283a83255e15dac20c86bf9f')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../ispc-libclang-cpp.patch # Build with unified libclang-cpp.so
  # Verbose output on tests failures
  sed -i 's|COMMAND ${LIT_COMMAND} ${LIT_ARGS}|COMMAND ${LIT_COMMAND} ${LIT_ARGS} "--verbose"|' tests/CMakeLists.txt
  # Remove tests that requires DUMPS (https://github.com/ispc/ispc/issues/1752)
  sed -i '/debug-phase/d' tests/lit-tests/arg_parsing_errors.ispc
  # Add Google Test
  rmdir ispcrt/tests/vendor/google/googletest
  mv ../googletest-${_gtestcommit} ispcrt/tests/vendor/google/googletest
  # Broken check?
  sed -i 's|NOT EXISTS "${PROJECT_SOURCE_DIR}/ispcrt/tests/vendor/google/googletest/CMakeLists.txt"|FALSE|' ispcrt/tests/CMakeLists.txt
  # https://github.com/google/googletest/issues/3427
  sed -i 's/-Werror//' ispcrt/tests/vendor/google/googletest/googletest/cmake/internal_utils.cmake
}

build() {
  # Remove -Wp,-D_GLIBCXX_ASSERTIONS from CXXFLAGS as it leads to failures (https://github.com/ispc/ispc/issues/2283)
  export CXXFLAGS="$CFLAGS"
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DISPC_INCLUDE_EXAMPLES=OFF \
    -DISPC_NO_DUMPS=ON \
    -DXE_ENABLED=ON \
    -DXE_DEPS_DIR=/usr
  make -C build
}

check() {
  # https://github.com/ispc/ispc/issues/2325
  make -C build check-all || echo "Tests failed"
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
  # Remove uneeded files
  rm -r "${pkgdir}"/build
  rm "${pkgdir}"/usr/lib/libispcrt_static.a
}
