# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Mario Finelli <mario at finel dot li>

pkgname=go-yq
pkgver=4.25.2
pkgrel=1
pkgdesc='Portable command-line YAML processor'
arch=('x86_64')
url=https://github.com/mikefarah/yq
license=('MIT')
depends=('glibc')
makedepends=('go')
conflicts=('yq')
source=("$url/archive/v$pkgver/yq-$pkgver.tar.gz")
b2sums=('78e6ff09058d073205c17ba3dca7ba1504891a76a9fccde53178183c6b69903696e270205768ad6043d6f24686c61290e5a3f9ed3de1d0744f76e75f16eeb81a')

prepare() {
  cd yq-$pkgver
  go mod vendor
}

build() {
  cd yq-$pkgver
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"
  go build .
}

check() {
  cd yq-$pkgver
  go test ./...
}

package() {
  cd yq-$pkgver
  install -Dt "$pkgdir"/usr/bin yq
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md

  ./yq shell-completion bash | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/bash-completion/completions/yq
  ./yq shell-completion zsh | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/zsh/site-functions/_yq
  ./yq shell-completion fish | install -Dm644 /dev/stdin \
    "$pkgdir"/usr/share/fish/vendor_completions.d/yq.fish
}

# vim:set ts=2 sw=2 et:
