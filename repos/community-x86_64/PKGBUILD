# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.9.5
pkgrel=3
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=(
  'curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme'
  'python' 'gtk2' 'autoconf-archive' 'libsignal-protocol-c' 'libgcrypt'
  'libx11' 'libxss' 'libxcrypt' 'glibc' 'freetype2' 'readline' 'libassuan'
  'harfbuzz' 'pango' 'gdk-pixbuf2' 'atk' 'cairo' 'libgpg-error')
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('ad9fef30eb82b61f6735917a1467757f3e4bcd31c213b5721b4e4b2ec483d253')
sha512sums=('4f6e1c9f05d61600de47a8d23d17477a0ebf62bfecddc7ed3bc33321edb9b3456d73858549198d469ab977f13b3aaa250f155629d05c45ee312c49e4f976735d')
b2sums=('a0ee06c7b1ec58f4dd73931936f46b69213320009b2f0c06e7ac3e75469289edafc58468c62f30ae1d8a7177e5f386089c1111dc423d856f33372f1516e02280')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons-and-clipboard \
      --without-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
  echo "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons-and-clipboard \
      --with-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=(
    'curl' 'libcurl.so' 'ncurses' 'libncursesw.so' 'expat' 'glib2' 'libotr'
    'libmesode' 'libnotify' 'libnotify.so' 'libxcrypt' 'libcrypt.so'
    'gpgme' 'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
    'glibc' 'readline' 'libreadline.so' 'libassuan' 'libgpg-error'
  )
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc+=' (gtk support)'
  depends=(
    'curl' 'libcurl.so' 'ncurses' 'libncursesw.so' 'expat' 'glib2' 'libotr'
    'libmesode' 'libnotify' 'libnotify.so' 'libxcrypt' 'libcrypt.so'
    'gpgme' 'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
    'glibc' 'readline' 'libreadline.so' 'libassuan' 'libgpg-error'

    'gtk2' 'libxss' 'libx11' 'pango' 'libpango-1.0.so' 'libpangoft2-1.0.so'
    'freetype2' 'libfreetype.so' 'harfbuzz' 'libharfbuzz.so' 'gdk-pixbuf2'
    'atk' 'libatk-1.0.so' 'cairo'
  )
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
