# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: rudy.matela

pkgname=z3
pkgver=4.6.0
pkgrel=1
pkgdesc="A high-performance theorem prover"
arch=('x86_64')
url="https://github.com/Z3Prover/z3"
license=('MIT')
depends=('gcc-libs')
makedepends=('python2') # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib')
optdepends=('python2-setuptools: bindings for python2')
source=("https://github.com/Z3Prover/z3/archive/z3-$pkgver.tar.gz")
sha512sums=('92a87292db17ceb4a90e45bce4f6237fe0437a7a5e147b4b63a2cb793e2d59fc04a6aa579ddb904280eac08b4a85c600df092d4c2fa99239199514b9c0363243')

build() {
  cd "$srcdir/$pkgname-$pkgname-$pkgver"
  python2 scripts/mk_make.py --python # --dotnet --java --ml
  cd build
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgname-$pkgver/build
  make install DESTDIR="$pkgdir"
  install -Dm644 ../LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
