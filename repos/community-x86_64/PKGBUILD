# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=jose
pkgname=haskell-jose
pkgver=0.9
pkgrel=2
pkgdesc="JSON Object Signing and Encryption (JOSE) and JSON Web Token (JWT) library"
url="https://github.com/frasertweedale/hs-jose"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-aeson' 'haskell-base64-bytestring'
         'haskell-concise' 'haskell-cryptonite' 'haskell-lens' 'haskell-memory' 'haskell-monad-time'
         'haskell-network-uri' 'haskell-quickcheck-instances' 'haskell-x509')
makedepends=('ghc' 'haskell-hspec' 'haskell-pem' 'haskell-tasty' 'haskell-tasty-hspec'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('74f240ac86d6d4ffffa8b0dbbb9968a24e1cf9639ea3e06325bc96c705da422101d0ed601b64566586e3f34249488a53d83e390b9858cf184b3243ed6be8e7b3')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
