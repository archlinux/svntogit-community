# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# Contributor: devmotion <nospam-archlinux.org@devmotion.de>
# Contributor: Valentin Churavy <v.churavy@gmail.com>

pkgbase=julia
pkgname=(julia julia-docs)
epoch=2
pkgver=0.6.3
pkgrel=2
arch=('x86_64')
pkgdesc='High-level, high-performance, dynamic programming language'
url='https://julialang.org/'
license=('MIT')
depends=('fftw' 'hicolor-icon-theme' 'libgit2' 'libunwind' 'libutf8proc>=2' 'mpfr' 'pcre2' 'suitesparse')
makedepends=('chrpath' 'cmake' 'gcc-fortran' 'gmp' 'gtk-update-icon-cache' 'python2')
options=('!emptydirs' 'staticlibs')
source=("https://github.com/JuliaLang/$pkgbase/releases/download/v$pkgver/$pkgbase-$pkgver-full.tar.gz"{,.asc}
        'julia-libunwind-version.patch'
        'julia-makefile.patch')
sha256sums=('2b5b0d84e49ca88fdec6d54a64eba4ce77f5c5cb8f34d307f3ba0ec04a89913a'
            'SKIP'
            '16009ce454f58463464027cbaf6186ccfab84c37660a52fb1ec3a0f514df64f9'
            '342b43532fbace3508688eee879113160e01cbbda29610b53f2f34796bc0312a')
validpgpkeys=('3673DF529D9049477F76B37566E3C7DC03D6E495') # Julia (Binary signing key) <buildbot@julialang.org>

prepare() {
  cd "$pkgbase"
  patch -p1 -i ../julia-libunwind-version.patch
  patch -p0 -i ../julia-makefile.patch # make 'make install' really just install
}

build() {
  #
  # See FS#57387 for why USE_SYSTEM_LLVM=0 is used, for now
  # See FS#58221 for why USE_SYSTEM_ARPACK=0 is used, for now
  #
  # patchelf is not even used unless $(private_libdir_rel) != $(build_private_libdir_rel)
  # but we USE_SYSTEM_PATCHELF=1 to prevent building it. This is why it is not in makedepends.
  #
  export CFLAGS="$CFLAGS -w"
  export CXXFLAGS="$CXXFLAGS -w"
  make -C "$pkgbase" \
    prefix=/usr \
    sysconfdir=/etc \
    MARCH=x86-64 \
    JULIA_BUILD_MODE=release \
    USE_BLAS64=0 \
    USE_INTEL_MKL=0 \
    USE_LLVM_SHLIB=1 \
    USE_SYSTEM_ARPACK=0 \
    USE_SYSTEM_BLAS=0 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_LAPACK=0 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_LLVM=0 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_SUITESPARSE=1 \
    USE_SYSTEM_UTF8PROC=1
}

package_julia() {
  backup=('etc/julia/juliarc.jl')
  optdepends=('gnuplot: If using the Gaston Package from julia')

  export CFLAGS="$CFLAGS -w"
  export CXXFLAGS="$CXXFLAGS -w"
  make -C "$pkgbase" \
    DESTDIR="$pkgdir" \
    prefix=/usr \
    sysconfdir=/etc \
    MARCH=x86-64 \
    USE_BLAS64=0 \
    USE_INTEL_MKL=0 \
    USE_LLVM_SHLIB=1 \
    USE_SYSTEM_ARPACK=0 \
    USE_SYSTEM_BLAS=0 \
    USE_SYSTEM_DSFMT=0 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_LAPACK=0 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_LLVM=0 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_SUITESPARSE=1 \
    USE_SYSTEM_UTF8PROC=1 \
    install

  # Remove duplicate man-page from julia/doc
  rm -rf "$pkgdir/usr/share/julia/doc/man"

  # FS#58211 && https://github.com/JuliaLang/julia/issues/26830
  chrpath -r '$ORIGIN/julia' "$pkgdir/usr/lib/libjulia.so.$pkgver"
  # points to /usr/lib
  chrpath -d "$pkgdir/usr/bin/julia"

  # Documentation and examples are in the julia-docs package
  rm -rf "$pkgdir/usr/share/"{doc,julia/doc,julia/examples}

  # License
  install -Dm644 "$pkgbase/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

package_julia-docs() {
  pkgdesc='Documentation and examples for Julia'
  depends=('julia')

  cd "$pkgbase"

  install -d "$pkgdir/usr/share/doc"
  cp -r doc "$pkgdir/usr/share/doc/$pkgbase"
  rm -rf "$pkgdir"/usr/share/doc/julia/man/
  cp -r examples "$pkgdir/usr/share/doc/$pkgbase/examples"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# getver: julialang.org/downloads
# vim: ts=2 sw=2 et:
