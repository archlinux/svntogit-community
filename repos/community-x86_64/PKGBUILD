# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=hasql
pkgname=haskell-hasql
pkgver=1.3.0.3
pkgrel=43
pkgdesc="An efficient PostgreSQL driver and a flexible mapping API"
url="https://github.com/nikita-volkov/hasql"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-prelude' 'haskell-bytestring-strict-builder'
         'haskell-contravariant' 'haskell-contravariant-extras' 'haskell-data-default-class'
         'haskell-dlist' 'haskell-hashable' 'haskell-hashtables' 'haskell-loch-th'
         'haskell-placeholders' 'haskell-postgresql-binary' 'haskell-postgresql-libpq'
         'haskell-profunctors' 'haskell-text-builder' 'haskell-vector')
makedepends=('ghc' 'haskell-bug' 'haskell-tasty' 'haskell-tasty-quickcheck' 'haskell-tasty-hunit'
             'haskell-quickcheck-instances' 'haskell-quickcheck' 'haskell-rerebase')
checkdepends=('postgresql' 'pifpaf')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('fb951f3c7f09b7bc9ba62afc01c25dea7b839d3da21065bb4bba923a96ecd9b53074650084db846e00f3bb27de638e7dd776b081346919b4e92c5d1aac405336')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<.*0.6/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver

    eval $(pifpaf run postgresql --host 127.0.0.1 --port 5432)
    createuser -s postgres
    runhaskell Setup test
    pifpaf_stop
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
