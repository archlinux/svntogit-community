# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dbus-factory
pkgver=2.92.2
_commit=bb43480b94f666f1eb4b139d7e9710e13ee5aee6
pkgrel=1
pkgdesc='QML DBus factory for DDE'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dbus-factory"
license=('GPL3')
depends=('qt5-declarative')
makedepends=('git' 'deepin-dbus-generator')
source=("git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_commit"
        fix-arch.patch)
sha256sums=('SKIP'
            '0195e1258534ef14d8ec63cde5389f8cfad39d5308ab8ddf73966018a34bd300')

prepare() {
  cd dbus-factory
  patch -p1 -i ../fix-arch.patch
}

build(){
  cd dbus-factory
  make build-qml
}

package() {
  cd dbus-factory
  make DESTDIR="${pkgdir}" install-qml
}
