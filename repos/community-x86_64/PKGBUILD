# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=lilypond
pkgver=2.19.82
pkgrel=1
pkgdesc="An automated music engraving system"
arch=('x86_64')
url="http://lilypond.org"
license=('GPL')
depends=('guile1.8' 'python2' 'texlive-core' 'ghostscript' 'pango' 'fontconfig')
makedepends=('flex' 'bison' 'gettext' 'mftrace' 'texinfo' 'fontforge' 't1utils'
             'gsfonts' 'texi2html' 'dblatex' 'texlive-langcyrillic' 'imagemagick'
             'zip' 'rsync' 'netpbm' 'tex-gyre-fonts')
options=('emptydirs')
source=("http://lilypond.org/downloads/sources/v2.19/$pkgname-$pkgver.tar.gz"
	lilyfontsize.patch
	https://arch.p5n.pp.ru/~sergej/dl/2017/gsfonts/c059013l.{pfb,pfm,afm}
	https://arch.p5n.pp.ru/~sergej/dl/2017/gsfonts/c059016l.{pfb,pfm,afm}
	https://arch.p5n.pp.ru/~sergej/dl/2017/gsfonts/c059033l.{pfb,pfm,afm}
	https://arch.p5n.pp.ru/~sergej/dl/2017/gsfonts/c059036l.{pfb,pfm,afm})
sha256sums=('a5679cddb3e415828642f75027997596b814922aadb1fa633c8f973238ae291b'
            '17b86b7a0b09b73cb5cf8751464571cf6a785c0b1a23db425cc828855a9d8ae6'
            '34218d1c2710553f528cf8296b37eff98119b174aa48e454dc41ad31f855232c'
            'aaf8da7af542c6de7b8a0954a3dbc282dd61fc7c80fd0cf1df31da3aad0aa576'
            'e7775af4c475190d3f9c66be652622e204e5b7dff74ed142010c3b160f2c2d31'
            '1c8b6234cfb14703a33fdb8181c8c0c64d09180edddd37dc1594c0473425d994'
            'b0aa88a7a87079240a5b89bd45781deacc35bc72ce2ec2b548a8800ec6983605'
            'cb2e0ff8b76afd97c801da65fd3331adb51a3e7fcb69ae1751e7eebd07bec791'
            '2def00a5fb56aeb9e67bb1536eff45e19a898a63510a0104d12e1cd579c7bbc9'
            '0c2753b0e7e107d15b323883d1bef7466466d10f3d2ac9005902e249117f880d'
            '3c8744efa776a819fe04f0f87c9f7f2be6cb8dde8cbfbc122aa73f0d8e5046f1'
            '9a118f78b168a990dbff4788f0fcac33f876d771c06f61682c5c29165d8673be'
            '569645cadb0aaec15786bb67c5f0801154b71d092d138e4f407265a0130752ee'
            '6abb4172ca88db4a1a4cf006cee147f35988153d47e0072b25eb8f54b3be247a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  sed -i 's|GUILE_CFLAGS=.*|GUILE_CFLAGS="`pkg-config --cflags guile-1.8`"|' configure
  sed -i 's|GUILE_LDFLAGS=.*|GUILE_LDFLAGS="`pkg-config --libs guile-1.8`"|' configure

  rm -rf lily/out/ python/out/
  patch -p1 -i "$srcdir/lilyfontsize.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export PYTHON=/usr/bin/python2
  export GUILE=/usr/bin/guile1.8
  export GUILE_CONFIG=/usr/bin/guile-config1.8
  export LDFLAGS="$LDFLAGS -lpthread"
  ./configure \
    --prefix=/usr \
    --with-ncsb-dir="$srcdir"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" vimdir="/usr/share/vim/vimfiles" install
}

