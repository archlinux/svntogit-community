# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gcolor3
pkgver=2.4.0
pkgrel=1
pkgdesc="Simple color chooser written in GTK3"
arch=(x86_64)
url="https://www.hjdskes.nl/projects/gcolor3/"
license=(GPL2)
depends=(gtk3 libportal)
makedepends=(git meson)
_commit=7053f9e6a890175fc212df625e02ffbdce418146
source=("git+https://gitlab.gnome.org/World/gcolor3.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
