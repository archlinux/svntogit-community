# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Giacomo Rizzo <alt@free-os.it>

pkgname=gpsd
pkgver=3.19
pkgrel=3
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('x86_64')
url="http://catb.org/gpsd/"
license=('BSD')
depends=('python' 'libusb' 'bluez-libs')
optdepends=('gtk3: GUI frontends'
            'php: generate a PHP status page for your GPS'
            'php-gd: image support for the PHP status page'
            'python-cairo: GUI frontends'
            'python-gobject: GUI frontends'
            'qt5-base: for Qgpsmm')
makedepends=('scons' 'docbook-xsl' 'qt5-base' 'python-gobject' 'python-cairo')
backup=('etc/default/gpsd')
source=(https://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.sig}
        $pkgname.conf fix-python-libdir.patch)
sha256sums=('27dd24d45b2ac69baab7933da2bf6ae5fb0be90130f67e753c110a3477155f39'
            'SKIP'
            'dcfa71c4c730b486269995468bdcfa8e96bf3c282db1dadea9384d32015d277f'
            '631cf609db1f4b26b94d324a9ca16624487781f0b8736c5c59d8c45acc5d7322')
validpgpkeys=('EED4A0893DCC705DB309E202CCF29C7238522905') # Gary E. Miller <gem@rellim.com>
# validpgpkeys=('41876B2F579463A499843D1DECC8208F8C6C738D') # Eric S. Raymond

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-python-libdir.patch # Fix python install dir
}

build() {
  cd $pkgname-$pkgver

  export LINKFLAGS="${LDFLAGS}"
  scons prefix=/usr \
        systemd=yes \
        gpsd_group=uucp \
        sbindir=/usr/bin \
        qt_versioned=5
  scons build
}

package() {
  cd $pkgname-$pkgver

  # Fix path in systemd files
  sed -i 's|local/sbin|bin|' systemd/{gpsd.service,gpsdctl@.service}

  export DESTDIR="${pkgdir}"
  scons install
  install -Dm755 gpsinit -t "${pkgdir}"/usr/bin
  chmod 755 "${pkgdir}"/usr/bin/*

  install -Dm644 "$srcdir/gpsd.conf" "${pkgdir}"/etc/default/gpsd

  install -Dm644 "gpsd.rules" "${pkgdir}"/usr/lib/udev/rules.d/25-gpsd-usb.rules

  install -Dm755 gpsd.hotplug "${pkgdir}"/usr/lib/udev/gpsd.hotplug

  install -Dm644 packaging/X11/xgps.desktop \
    "${pkgdir}"/usr/share/applications/xgps.desktop
  install -Dm644 packaging/X11/xgpsspeed.desktop \
    "${pkgdir}"/usr/share/applications/xgpsspeed.desktop
  install -Dm644 packaging/X11/gpsd-logo.png \
    "${pkgdir}"/usr/share/gpsd/gpsd-logo.png

  install -Dm644 systemd/gpsd.service "${pkgdir}"/usr/lib/systemd/system/gpsd.service
  install -Dm644 systemd/gpsd.socket "${pkgdir}"/usr/lib/systemd/system/gpsd.socket
  install -Dm644 systemd/gpsdctl@.service "${pkgdir}"/usr/lib/systemd/system/gpsdctl@.service

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
