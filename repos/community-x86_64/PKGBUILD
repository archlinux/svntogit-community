# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.5.1
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=('x86_64')
url="https://qsampler.sourceforge.io/"
license=('GPL')
depends=('hicolor-icon-theme' 'linuxsampler' 'liblscp' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('845aa9ac851fa257bfad6af81d1114bc6040b84d8a69da8e87fc7de9137f2151e509334f8355fcaaf672ad4c97d2840c51f83285cf5ab9b6ee95e95a3ed48e87')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}

# vim:set ts=2 sw=2 et:

