# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=8.3.0.98
_pkgver=8.3.0
_cudaver=11.5
_majorver=8
pkgrel=1
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>='"${_cudaver}")
options=(!strip staticlibs)
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/container-images/cuda/-/tree/master/dist for the appropriate cuda version
# or make an NVIDIA Developer account.
# Alternatively, check https://github.com/pytorch/builder/blob/master/common/install_cuda.sh
# or https://developer.download.nvidia.com/compute/cuda/repos/ubuntu2004/x86_64/
source=("https://developer.download.nvidia.com/compute/redist/cudnn/v${_pkgver}/cudnn-${_cudaver}-linux-x64-v${pkgver}.tgz"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('46af7d47b40a2c0ee8d5b893514755f803b5a0c7426b8d88b258e71ae6312cb553cba7dd68f63e2060849c70713330d52f93f56121f663213c16030d39b2fc1d'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("cudnn-${_cudaver}-linux-x64-v${pkgver}.tgz")

package() {
  mkdir -p "${pkgdir}"/usr/share/licenses/
  tar xf cudnn-${_cudaver}-linux-x64-v${pkgver}.tgz --strip-components 1 -C "${pkgdir}/usr"

  mv "${pkgdir}"/usr/lib64 "${pkgdir}"/usr/lib
  find "${pkgdir}/usr/lib" -type f \( -iname "*.a" -or -iname "*.a.*" \) -print0 | while read -rd $'\0' _so_file; do
    # make sure static libraries have proper permissions
    chmod 644 "${_so_file}"
  done

  install -Dm644 \
      "${srcdir}"/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      "${pkgdir}"/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf

  mv "${pkgdir}"/usr/NVIDIA_SLA_cuDNN_Support.txt "${pkgdir}"/usr/share/licenses/${pkgname}/NVIDIA_SLA_cuDNN_Support.txt
}

# vim: ts=2 sw=2 ft=sh syn=sh et
