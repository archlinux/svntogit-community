# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sauyon Lee <sauyonl@sauyon.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: korjjj <korjjj+aur@gmail.com>

pkgname=syncthing
pkgver=0.12.23
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing"
arch=('i686' 'x86_64')
url="http://syncthing.net/"
license=('MPL')
depends=('glibc')
makedepends=('git' 'go' 'godep' 'inetutils' 'sed')
install=$pkgname.install
source=($pkgname-$pkgver::git+https://github.com/syncthing/syncthing.git#tag=v${pkgver})
sha1sums=('SKIP')

prepare() {
  cd "${srcdir}"
  install -d "src/github.com/syncthing"
  mv "${pkgname}-${pkgver}" "src/github.com/syncthing/${pkgname}"
}

build() {
  export GOPATH="${srcdir}" GOROOT_FINAL="/usr/bin"
  cd "${srcdir}/src/github.com/syncthing/${pkgname}"
  if [ ${CARCH}" == "i686" ] ; then
      go run build.go -no-upgrade -goarch 386 build
  elif [ ${CARCH}" == "x86_64" ] ; then
      go run build.go -no-upgrade -goarch amd64 build
  else
      go run build.go -no-upgrade build
  fi
}

check() {
  export GOPATH="${srcdir}" GOROOT_FINAL="/usr/bin"
  cd "${srcdir}/src/github.com/syncthing/${pkgname}"
  if [ "${CARCH}" == "x86_64" ] ; then
    go run build.go -no-upgrade test
  fi
}

package() {
  cd "${srcdir}/src/github.com/syncthing/${pkgname}"
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 "etc/linux-systemd/system/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
  install -Dm644 "etc/linux-systemd/user/${pkgname}.service" "${pkgdir}/usr/lib/systemd/user/${pkgname}.service"

# license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

# man pages
  cd "${srcdir}/src/github.com/syncthing/${pkgname}/man"
  for file in $(find . -name '*.1' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man1/$file
  done
  for file in $(find . -name '*.5' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man5/$file
  done
  for file in $(find . -name '*.7' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man7/$file
  done
}
