# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.2.7
pkgrel=1
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('procps-ng' 'hwdata' 'karchive' 'kauth' 'qt5-charts' 'qt5-quickcontrols2' 'qt5-base>=5.9' 'botan>=2.2.0' 'qt5-svg' 'qt5-xmlpatterns' 'qt5-base')
makedepends=('cmake>=3.3' 'qt5-tools' 'karchive' 'gcc>=8' 'extra-cmake-modules' 'ninja')
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha512sums=('6ff88352353539dd5a59abd3f2c6ad37592ed34f256dc29d8c95cad3377fc38524d12fa6fa2087f44f7932287076bd37b4802e87bbc0fc114944ba02f72a0178')

build() {
  cd "${pkgname}-v${pkgver}"
  cmake \
	-Bbuild \
	-GNinja \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-Wno-dev
  cmake --build build --verbose
}

check () {
  cd "${pkgname}-v${pkgver}"
  ninja -C build test
}

package() {
  cd "${pkgname}-v${pkgver}"
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
