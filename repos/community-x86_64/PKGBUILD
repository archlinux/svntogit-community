# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.dot.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=proj
pkgver=6.3.0
pkgrel=1
pkgdesc='Cartographic Projections library'
arch=('x86_64')
url="https://trac.osgeo.org/proj/"
license=('MIT')
makedepends=('autoconf' 'sqlite')
changelog=$pkgname.changelog
source=(https://github.com/OSGeo/PROJ/releases/download/$pkgver/$pkgname-$pkgver.tar.gz
        https://download.osgeo.org/proj/proj-datumgrid-1.8.zip)
sha256sums=('68ce9ba0005d442c2c1d238a3b9bc6654c358159b4af467b91e8d5b407c79c77'
            'b9838ae7e5f27ee732fb0bfed618f85b36e8bb56d7afb287d506338e9f33861e')

build(){
  cd "${srcdir}"/$pkgname-$pkgver

  autoreconf -if
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  bsdtar --no-same-owner -xzvf "${srcdir}"/proj-datumgrid-1.8.zip -C "${pkgdir}"/usr/share/$pkgname
}
