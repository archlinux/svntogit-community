# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.5.1
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
url='http://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'webkitgtk3' 'libesmtp' 'libnotify' 'gpgme' 'gtksourceview3' 'gtkspell3' 'gnome-icon-theme' 'desktop-file-utils')
makedepends=('perl-xml-parser' 'gnome-doc-utils' 'intltool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://pawsa.fedorapeople.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('52ce445dca86eb42e2e402a5b76616a1a522b89acbb631215079022ef80a7a10')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-ssl \
        --with-gpgme=gpgme-config \
        --with-gss \
        --with-ldap \
        --with-gtksourceview \
        --with-gtkspell \
        --with-rubrica \
        --with-sqlite \
        --without-nm \
        --without-gnome \
        --with-html-widget=webkit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
