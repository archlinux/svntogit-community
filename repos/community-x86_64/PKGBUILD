# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
pkgname=usd
pkgver=21.05
pkgrel=6
pkgdesc="3D VFX pipeline interchange file format"
arch=('x86_64')
url="https://graphics.pixar.com/usd/docs/index.html"
license=('Apache')
depends=('boost-libs' 'glew' 'opensubdiv' 'python' 'pyside2' 'python-opengl' 'ptex')
makedepends=('boost' 'cmake' 'intel-tbb' 'ninja' 'qt5-base')
provides=("usd=${pkgver}")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PixarAnimationStudios/USD/archive/v${pkgver}.tar.gz"
        'demangle-fix.patch'
        'gcc11.patch'
        'pyside2-uic')
sha256sums=('165d0e1e45385076121268eba0da2a0ba4a7e5c2b8eab9ab26fb2fcd81a1a478'
            '99ea5fba92842d0215e5188662a066e0cc714ed4dea9c8663cb6239f6c1afbd0'
            '663352c8932a0b48230087284f4f5c540876f6a5adab3d4d1a7ee7b3a4ad6462'
            'bf2d902eddd24d4c3498f05a89f0d867df8a6cbd921214131c14a28152e4ffbe')

prepare() {
  cd USD-${pkgver}
  patch -Np1 -i ../demangle-fix.patch
  patch -Np1 -i ../gcc11.patch
}

build() {
  cd USD-${pkgver}
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPXR_USE_PYTHON_3=ON \
    -DPXR_BUILD_IMAGING=ON \
    -DPXR_BUILD_TESTS=OFF \
    -DPXR_BUILD_MONOLITHIC=ON \
    -DPYSIDE_BIN_DIR="$srcdir"
  ninja -C build
}

package() {
  cd USD-${pkgver}
  DESTDIR="$pkgdir" ninja -C build install

  mv "$pkgdir"/usr/lib/python "$pkgdir"/usr/lib/python3.9
}

# vim:set ts=2 sw=2 et:
