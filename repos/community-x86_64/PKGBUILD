# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-audit
pkgver=0.14.1
pkgrel=1
pkgdesc="Audit Cargo.lock for crates with security vulnerabilities"
url="https://github.com/RustSec/cargo-audit"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
source=("https://github.com/RustSec/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('bdf1b12a298f585195fbbe75cb01c8746a0bbb734409d0a9130c197cb179f638')
b2sums=('7307dba5fd1c9e54cce278b0403efde920ced5a0c2575d8359ce27a0908485fed5776baa4ab7d790a4e46204542e52b120b0240e894f29262f3b77ba4a420cdf')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
