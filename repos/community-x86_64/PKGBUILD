# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-proxy
pkgver=4.6.1
pkgrel=1
pkgdesc="Generic Transparent Proxies"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
checkdepends=('python-zope-security' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.proxy/archive/$pkgver.tar.gz")
sha512sums=('b4c776c393ac86f6606520f0588c6d5bbe0dca6675afe6aca1c05897a5c17866bd0142115eb1ccbc2cdde1285eba8500e2dd0f3b1be1e4291dd43ae665389c2a')

build() {
  cd "$srcdir"/zope.proxy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.proxy-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
