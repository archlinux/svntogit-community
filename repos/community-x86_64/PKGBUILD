# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=xdg-desktop-portal-hyprland
pkgver=0.1.0
_protocolver=b8f55e02a328c47ed373133c52483bbfa20a1b75
pkgrel=1
pkgdesc='xdg-desktop-portal backend for hyprland'
url="https://github.com/hyprwm/$pkgname"
arch=(x86_64)
license=(MIT)
provides=(xdg-desktop-portal-impl)
depends=(libinih
         pipewire
         qt6-base
         qt6-wayland
         xdg-desktop-portal)
makedepends=(cmake
             meson
             scdoc
             wayland
             wayland-protocols)
optdepends=('grim: required for the screenshot portal to function'
            'slurp: support for interactive mode for the screenshot portal')
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/v$pkgver.tar.gz"
        "https://github.com/hyprwm/hyprland-protocols/archive/$_protocolver.tar.gz")
sha256sums=('43b40bfaf94c01706759202a770b13f07469f4a8aef2cc14d4b2d2a758582edf'
            '5340ca9a11bfc1a839b5acb23a3c93739cf6d82708e975330b687b0f1bd763f1')

prepare() {
    cd "$_archive/subprojects"
    rm -rf hyprland-protocols
    ln -sfT "$srcdir/hyprland-protocols-$_protocolver" hyprland-protocols
}

build() {
    cd "$_archive"
    arch-meson -Dsd-bus-provider=libsystemd build
    ninja -C build
    make -C hyprland-share-picker all
}

package() {
    cd "$_archive"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm0755 -t "$pkgdir/usr/bin/" hyprland-share-picker/build/hyprland-share-picker
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
