# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=bash-language-server
pkgver=3.0.3
pkgrel=1
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/bash-lsp/$pkgname"
license=("MIT")
depends=("nodejs")
makedepends=("yarn" "typescript" "git")
optdepends=('bash-completion: for CLI args completion'
            'shellcheck: to enable linting')
source=("$pkgname-$pkgver.tar.gz::$url/archive/server-$pkgver.tar.gz")
sha512sums=('edcf168f10e4a7add68fdcc3b859f13cd28a2be2a77fc27352a67e41bca4fdfd53055278b02bcd17d1a6a31f8bca7248c97455487cfb781f46dcd708fcbe6076')

build() {
    cd "$pkgname-server-$pkgver"
    yarn
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$srcdir/$pkgname-server-$pkgver/LICENSE"

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
