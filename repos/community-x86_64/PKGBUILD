# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=medusa
pkgver=2.2
pkgrel=4
pkgdesc='Speedy, massively parallel and modular login brute-forcer for network'
url='http://www.foofus.net/jmk/medusa/medusa.html'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openssl')
optdepends=(
  'openssl: HTTP, MSSQL, SMBNT and SSL-based connection support'
  'libssh2: SSH support'
  'postgresql-libs: PostgreSQL support'
  'afpfs-ng: AFP support'
  'subversion: Subversion support'
  'freerdp: RDP support'
)
makedepends=('pkg-config' 'libssh2' 'libpqxx' 'afpfs-ng' 'subversion' 'freerdp')
source=(${pkgname}-${pkgver}.tar.gz::http://www.foofus.net/jmk/tools/${pkgname}-${pkgver}.tar.gz)
sha512sums=('4117c95e9a93c1cdf275c33f86046811b7100bfc4430464d66051e736dcf105a0733855d896ee0ea85c02a6dd3d3c060c8a84bb795f9870ec589fd9a87232690')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf --force --install
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    CFLAGS="${CFLAGS} $(pkg-config --cflags freerdp2)"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 misc/zsh/_medusa "${pkgdir}/usr/share/zsh/site-functions/_medusa"
  install -Dm 644 doc/*.html README NEWS ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
