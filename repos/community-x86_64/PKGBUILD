# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=agda-stdlib
pkgver=1.7.1
_agdaver=2.6.2
pkgrel=1
pkgdesc="Agda standard libraries"
url="https://github.com/agda/agda-stdlib"
license=("MIT")
arch=('x86_64')
depends=("agda=$_agdaver")
makedepends=('ghc' 'haskell-filemanip')
options=('staticlibs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agda/agda-stdlib/archive/v$pkgver.tar.gz"
        standard-library.agda-lib.in)
sha512sums=('fd0fc0b6a97dd4a51bd0208691800046ecdd200d9e57c83b0c3dae720c2e687942fee7c28be0cfdbdc0e54cefd9c2f1d1a683961bb368a48e5a02bf1bd36866a'
            '92fad2f9b941af4506bb0d5d9d533d09823df89f7530ee5d787b741816ba07e68f25b96d6653ae6ed949ec23484c1fba47a63cc6401311462a55b5e06cfc032c')

prepare() {
    cd $pkgname-$pkgver
    rm standard-library.agda-lib
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla
    runhaskell Setup build $MAKEFLAGS

    dist/build/GenerateEverything/GenerateEverything

    agda -i. -isrc Everything.agda
}

package() {
    install -Dm644 standard-library.agda-lib.in "$pkgdir"/usr/share/agda/lib/standard-library.agda-lib

    cd $pkgname-$pkgver
    install -dm755 "$pkgdir"/usr/share/agda/lib/stdlib
    cp -pr src/* "$pkgdir"/usr/share/agda/lib/stdlib/

    # Symlink to avoid writing to this place when agda-lib is present
    install -dm755 "$pkgdir"/usr/share/agda/lib/_build/$_agdaver/agda
    ln -s ../../../stdlib "$pkgdir"/usr/share/agda/lib/_build/$_agdaver/agda/

    install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
