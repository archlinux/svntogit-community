# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=config-ini
pkgname=haskell-config-ini
pkgver=0.2.4.0
pkgrel=124
pkgdesc="A library for simple INI-based configuration files."
url="https://github.com/aisamanra/config-ini"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-unordered-containers' 'haskell-megaparsec')
makedepends=('ghc' 'haskell-ini' 'haskell-hedgehog' 'haskell-doctest' 'haskell-microlens')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('76af8bf132d545425d9db5579df2e37fa08cad479adf5f586fb3becd0712e2329eae019db1796cd24c6fda80aa958e45450ebae74a5bcbdfad9337ae44696a5e')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<8/<9/' $_hkgname.cabal
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --disable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fenable-doctests
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/aisamanra/config-ini/issues/22
    # runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
