# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: mkoloberdin <my@aprisobal.by>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Brian De Wolf <arch@bldewolf.com>

pkgname=mlton
pkgver=20170725
pkgrel=2
pkgdesc='Whole-program, optimizing Standard ML compiler'
arch=('x86_64' 'i686')
url='http://mlton.org/'
license=('BSD' 'MIT' 'LGPL')
depends=('gmp')
makedepends=('git' 'setconf' 'mlton')
makedepends_x86_64=('smlnj')
options=('staticlibs')
source=("git+https://github.com/MLton/mlton#commit=159190284e1268fded189104705939fe3b691b12")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  sed 's/_BSD_SOURCE/_DEFAULT_SOURCE/g' -i runtime/cenv.h
  find . -name Makefile -type f -exec setconf {} CFLAGS "$CFLAGS -fPIC" \;
}

build() {
  export CFLAGS="$CFLAGS -fPIC"
  export COMPILE_ARGS="-codegen c -cc-opt '-fPIC'" 

  if [ $arch = 'x86_64' ]; then
    # Bootstrap with SML-NJ on x86_64
    make -C "$pkgname" -j1 smlnj-mlton all-no-docs
  else
    make -C "$pkgname" -j1 all-no-docs
  fi
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install-no-docs
  for f in "$pkgname"/doc/license/*-LICENSE; do
    install -Dm644 "$f" "$pkgdir/usr/share/licenses/$pkgname/$f"
  done
}

# vim: ts=2 sw=2 et:
