# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=cockatrice
_pkgname=Cockatrice
pkgver=2.8.0
_date=2021-01-26
_tag=$_date-Release-$pkgver
pkgrel=9
pkgdesc='A cross-platform virtual tabletop for multiplayer card games'
arch=('x86_64')
url='https://cockatrice.github.io'
license=('GPL2')
depends=('qt5-base' 'protobuf' 'qt5-tools' 'qt5-svg' 'qt5-multimedia' 'qt5-websockets')
optdepends=('mariadb-libs: MySQL/MariaDB connection')
makedepends=('cmake' 'git' 'zlib' 'mariadb-libs')
checkdepends=('gtest' 'valgrind')
optdepends=('zlib: Load zipped files in Oracle')
provides=('cockatrice-client' 'cockatrice-server')
conflicts=('cockatrice-client' 'cockatrice-server')
source=("git+https://github.com/Cockatrice/Cockatrice.git#tag=$_tag")
sha512sums=('SKIP')

build() {
  mkdir -p $_pkgname/build
  cd $_pkgname/build

  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DWITH_ORACLE=ON \
  	-DWITH_CLIENT=ON \
  	-DWITH_SERVER=ON \
  	-DTEST=ON

  make
}

check() {
  cd $_pkgname/build

  make test
}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}
