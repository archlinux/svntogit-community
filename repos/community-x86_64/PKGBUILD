# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_hkgname=dlist-instances
pkgname=haskell-dlist-instances
pkgver=0.1.1.1
pkgrel=10
pkgdesc='Difference lists instances'
arch=(x86_64)
url="https://hackage.haskell.org/package/$_hkgname"
license=(MIT)
depends=(ghc-libs haskell-dlist)
makedepends=(ghc)
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('9a66f9f3d73466731c1b1a3f208c564f0fd8fe990e81dd274a86441e94b48c40d778535a9d346e9ff8160b23c19efa73cc836b4dcb3a6cb0e81817348bc5041a')

prepare() {
  cd $_hkgname-$pkgver
  sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
      --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
      --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver
  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
