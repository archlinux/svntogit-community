# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org> 
# Contributor: John D Jones III <jnbek1972 -_AT_- g m a i l -_Dot_- com>

pkgname=perl-sub-identify
pkgver=0.14
pkgrel=5
pkgdesc="Retrieve names of code references"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-test-pod')
url='http://search.mcpan.org/dist/Sub-Identify'
source=("http://search.mcpan.org/CPAN/authors/id/R/RG/RGARCIA/Sub-Identify-$pkgver.tar.gz")
sha512sums=('f69f542c84c5c3ca1f1c7f5f58fd2cf19279a65229f43117c31f24ab0e49d5f329bef2bc00f22252fd2e52b4e17f16b279dac438920668e046e59f2e22e52c14')

build() {
  cd "$srcdir/Sub-Identify-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Sub-Identify-$pkgver"
  make test
}

package() {
  cd "$srcdir/Sub-Identify-$pkgver"
  make DESTDIR="$pkgdir"/ install
}

# vim:set ts=2 sw=2 et:
