# $Id$
# Maintainer: Maxime Gauduin <alucryd@Ã¦rchlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-scummvm
pkgver=71580
pkgrel=1
pkgdesc='ScummVM core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/scummvm'
license=('GPL')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git')
source=('libretro-scummvm::git+https://github.com/libretro/scummvm.git'
        'https://raw.github.com/libretro/libretro-super/master/dist/info/scummvm_libretro.info')
sha256sums=('SKIP'
            '7a2bed9546dd67cbc7efa3d7e556758dc4e49cf01a4082f3587acc48e3b9d4bb')

pkgver() {
  cd libretro-scummvm

  git rev-list --count HEAD
}

build() {
  cd libretro-scummvm/backends/platform/libretro/build

  make
}

package() {
  cd libretro-scummvm/backends/platform/libretro/build

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 scummvm_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 "${srcdir}"/scummvm_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
