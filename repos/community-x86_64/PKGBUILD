# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>

pkgname=guake
pkgver=0.4.4
pkgrel=1
pkgdesc='Drop-down terminal for GNOME'
arch=('i686' 'x86_64')
url="http://guake.org/"
license=('GPL')
depends=('python-notify' 'vte' 'python2-gconf' 'python2-dbus' 'python2-xdg' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://guake.org/files/$pkgname-$pkgver.tar.gz
        guake-fix-notification.patch)
md5sums=('532adada29b8f0bb79dc15904aa6b70c'
         '0b57227e6869dd84dd4fb0794ed0d082')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|schemadir = $(sysconfdir)/gconf/schemas|schemadir = $(GCONF_SCHEMA_FILE_DIR)|' data/Makefile.in
  sed -i 's|/usr/bin/env python|/usr/bin/python2|' src/{guake{,-prefs},prefs.py}

  # http://guake.org/ticket/416
  patch -Np1 -i "$srcdir/guake-fix-notification.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
