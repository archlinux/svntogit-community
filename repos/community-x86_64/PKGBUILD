# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=5.2
pkgrel=3
pkgdesc="KDE Photo Album, picture collection manager"
url="https://www.kphotoalbum.org/"
arch=('x86_64')
license=('GPL')
depends=('libkipi' 'libkdcraw' 'libkgeomap' 'exiv2')
makedepends=('extra-cmake-modules' 'python' 'plasma-framework' 'phonon-qt5' 'kdoctools' 'kparts' 'libkexiv2')
optdepends=()
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        kphotoalbum-qt-5.10.patch)
sha256sums=('9ad02080b21787d5824d0c4bb49be2a00822098adca7937e9c7497440d3b0b89'
            '67efc8339a9754f28f5a438510b3f2b114dd9cc07ecc34e75c0e1aa72f1a6b80')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../kphotoalbum-qt-5.10.patch # Fix crash with Qt 5.10
}

build() {
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
