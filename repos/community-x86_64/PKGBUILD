# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: SÃ©bastien Luttringer
# Contributor: kevku <kevku@msn.com>

pkgname=opensc
pkgver=0.17.0
pkgrel=2
pkgdesc='Tools and libraries for smart cards'
arch=('x86_64')
url='https://github.com/OpenSC/OpenSC/wiki'
license=('LGPL')
backup=('etc/opensc.conf')
makedepends=('docbook-xsl')
depends=('pcsclite' 'libltdl')
options=('!emptydirs')
source=("https://github.com/OpenSC/OpenSC/releases/download/$pkgver/opensc-$pkgver.tar.gz"
        bash-completion-path.patch)
sha256sums=('be73c6816867ab4721e6a9ae7dba8e890c5f169f0a2cbb4bf354e0f30a948300'
            'bd0991dc8d68b21704104aba4d3470f4f4f50bf36547569ed032b48fd3aed811')

prepare() {
  cd $pkgname-$pkgver
  patch -Np0 -i "$srcdir"/bash-completion-path.patch
}

build() {
  cd $pkgname-$pkgver
  _sheetdir=(/usr/share/xml/docbook/xsl-stylesheets-*)
  ./bootstrap
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-man \
    --enable-doc \
    --enable-readline \
    --enable-openssl \
    --enable-pcsc \
    --enable-zlib \
    --enable-sm \
    --with-xsl-stylesheetsdir="$_sheetdir"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 etc/opensc.conf "$pkgdir/etc/opensc.conf"
}

# vim:set ts=2 sw=2 et:
