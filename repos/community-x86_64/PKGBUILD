# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

# NOTE: Gitlab isn't always compatible with modern Ruby versions. In that case, check the
# commit log for an old fix on how to tell it to use older versions of Ruby. I'm afraid we'll
# need this again at some point in the future.
pkgname=gitlab-gitaly
pkgver=1.80.0
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=(glibc ruby libxml2 libxslt libssh2)
options=(!buildflags)
makedepends=(go ruby-bundler cmake)
backup=("etc/gitlab-gitaly/config.toml")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitaly/repository/archive?sha=v${pkgver}"
        "gitlab-gitaly.service")
sha512sums=('16d2483702186e7000fe359dd03454f9805b380e9842ea78b17ca9990f69321c8f5cd31bbec17efdf2d0130b68480c8a3bb38a87b4a53a3821120b2c2ee1118d'
            'b330c0f42592322ad2131079ca554a13a364007182dded8c556198caff2c9ec642acf5bb7dfecb05de5a3d89bffec6588b6d05c8c7c5c771a46df3d296deed28')

_homedir="/var/lib/gitlab"

prepare() {
  cd "gitaly-"*

  sed -i "s|dir = \"/home/git/gitaly/ruby\"|dir = \"/usr/share/webapps/gitlab-gitaly/ruby\"|" config.toml.example
  sed -i "s|dir = \"/home/git/gitlab-shell\"|dir = \"/usr/share/webapps/gitlab-shell\"|" config.toml.example
  sed -i "s|bin_dir = \"/home/git/gitaly\"|bin_dir = \"/usr/bin\"|" config.toml.example

  # https://github.com/bundler/bundler/issues/6882
  sed -e '/BUNDLED WITH/,+1d' -i ruby/Gemfile.lock
}

build() {
  cd "gitaly-"*

  bundle config force_ruby_platform true # build from sources as some prebuilt gems are not available for newer ruby
  make BUILD_TAGS="tracer_static tracer_static_jaeger"
}

package() {
  cd "gitaly-"*

  make PREFIX=/usr DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}/etc/${pkgname}"
  sed \
    -e "s|^socket_path =.*|socket_path = \"${_homedir}/sockets/gitlab-gitaly.socket\"|" \
    -e "s|^path =.*|path = \"${_homedir}/repositories\"|" \
    config.toml.example > "${pkgdir}/etc/${pkgname}/config.toml"

  mkdir -p ${pkgdir}/usr/share/webapps/gitlab-gitaly
  cp -r ruby ${pkgdir}/usr/share/webapps/gitlab-gitaly/ruby

  install -Dm644 config.toml.example "${pkgdir}/usr/share/${pkgname}/config.toml.example"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-gitaly.service" "${pkgdir}/usr/lib/systemd/system/gitlab-gitaly.service"
}

# vim:set ts=2 sw=2 et:
