# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: John D Jones III <jnbek1972 -_AT_- g m a i l -_Dot_- com>
# Generator  : CPANPLUS::Dist::Arch 1.28

pkgname=perl-net-dns-sec
pkgver=1.07
pkgrel=1
pkgdesc="DNSSEC extensions to Net::DNS"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
depends=('perl-crypt-openssl-bignum' 'perl-crypt-openssl-dsa' 'perl-crypt-openssl-rsa' 'perl-digest-bubblebabble' 'perl-mime-base32' 'perl-net-dns' 'perl')
checkdepends=('perl-test-pod')
url='http://search.mcpan.org/dist/Net-DNS-SEC'
options=('!emptydirs')
source=("http://search.mcpan.org/CPAN/authors/id/N/NL/NLNETLABS/Net-DNS-SEC-$pkgver.tar.gz")
sha512sums=('4061b90a7fa447b456db52c33e347b8d0749320cd5544b2dcbbfc047597a223a86b7da998a751c894e5bca6ee693945149fa19d778b399e3a970211a10e22197')
_distdir="Net-DNS-SEC-$pkgver"

build() {
  cd "$srcdir/$_distdir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_distdir"
  make test
}

package() {
  cd "$srcdir/$_distdir"
  make DESTDIR="$pkgdir/" install

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
        _perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
        _perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
        depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

# vim:set ts=2 sw=2 et:
