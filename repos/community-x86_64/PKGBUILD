# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-terra-pinyin
pkgver=0.0.0.20200914
_commit=3fc0721e30f9b5dcae9116e393d565332fa10f1b
pkgrel=1
pkgdesc="Terra pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-terra-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-terra-pinyin/archive/$_commit/rime-terra-pinyin-$_commit.tar.gz")
sha512sums=('386de27cf8a577e6a614c5b8dbee25760871a617407d2aabd4cba6cafb1c52d05d585449523a6116cfcd60aaca26af303c91ce7de4d7469ee72623851cac18a0')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
