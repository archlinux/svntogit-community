# Maintainer: David Runge <dave@sleepmap.de>
pkgname=moony.lv2
pkgver=0.30.0
pkgrel=1
pkgdesc="Easily add realtime programmable logic glue in LV2 plugin graphs"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/moony/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11')
makedepends=('lv2' 'meson' 'sord')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('ed9828546bb6f09b97d83649553b09451b0a072430dd3082bf523611c7b6a16a7ff955b17a9811fa41a904755b796edff061cb66249087123cd90c1a2a84fbbb')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build || msg "lv2_validate currently known to fail on the plugin"
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
