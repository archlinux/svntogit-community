# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=blosc
pkgver=1.21.2
pkgrel=1
pkgdesc='A blocking, shuffling and loss-less compression library'
arch=('x86_64')
url='https://www.blosc.org'
license=('BSD')
depends=('snappy' 'zstd')
makedepends=('cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Blosc/c-blosc/archive/v${pkgver}.tar.gz")
sha512sums=('f976c40834c1c3a0ec5af8dfb36b0b674430053a902b2e4dcac68eec9118f022a7a90aa73447e4d8234ce819088cf19ee574c1a1ca39c2e4f9164d79a95066b6')
b2sums=('fa1702150a8b64bb9b4eb4de963a42c89be8f62f0b53804cb81345abb6be150b4fdbf2c2a8d4f2379301c65de07564608f1e25ee1d9b045747c486ef08740815')

build() {
  cmake \
    -B build \
    -S "c-blosc-$pkgver" \
    -D CMAKE_INSTALL_PREFIX="/usr" \
    -D BUILD_STATIC="OFF" \
    -D DEACTIVATE_SNAPPY="OFF" \
    -D PREFER_EXTERNAL_LZ4="ON" \
    -D PREFER_EXTERNAL_ZLIB="ON" \
    -D PREFER_EXTERNAL_ZSTD="ON"

  cmake --build build
}

check() {
  cd build

  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 \
    "c-blosc-$pkgver/LICENSES/BLOSC.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
