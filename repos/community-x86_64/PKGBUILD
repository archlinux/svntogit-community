# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
pkgver=1.8.10
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('python2' 'systemd')
makedepends=('pkg-config')
options=('!libtool' '!docs')
source=("https://alioth.debian.org/frs/download.php/file/3963/pcsc-lite-${pkgver}.tar.bz2")
md5sums=('7268cc5ef8fc01b785c7cea87adb1ade')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  sed -i -e "s:python:python2:g" src/spy/pcsc-spy

  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --enable-libudev \
              --sysconfdir=/etc \
              --enable-ipcdir=/run/pcscd \
              --enable-usbdropdir=/usr/lib/pcsc/drivers \
              --with-systemdsystemunitdir=/usr/lib/systemd/system

  make
}

package() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
