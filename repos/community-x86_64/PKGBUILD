# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=ko
pkgver=0.11.2
pkgrel=2
pkgdesc="Build and deploy Go applications on Kubernetes"
arch=('x86_64')
url="https://github.com/google/ko"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/google/ko/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('544b19bf4e4fd02cf18a5162bfc447126b55e879e1d0999aa6d0f785696bcde755ef5a7f56cccc5eeba541d7940c4bd2d636b95f0a94d6896bf5be07527cc42e')
b2sums=('6704e07046cef9389417ca7a80d817a777160306e5e21a2f816ef7547ffec1fc36db20a3746b4bddd73c79beee7e1e155fac63d7975a764a8963b7b40d89c9ab')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-w -s -linkmode=external" .
  chmod +x ./ko
}

check() {
  cd "${pkgname}-${pkgver}"
  # currently disabled
  #go test -v -x ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dsm755 ./ko "${pkgdir}/usr/bin/ko"
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions/"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d/"
  ./ko completion > "${pkgdir}/usr/share/bash-completion/completions/ko"
  ./ko completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_ko"
  ./ko completion completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/ko.fish"
}
