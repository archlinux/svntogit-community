# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Sven Kauber <celeon@gmail.com>

pkgname=siege
pkgver=4.1.5
pkgrel=3
arch=('x86_64')
license=('GPL')
pkgdesc="An http regression testing and benchmarking utility"
url="https://www.joedog.org/siege-home/"
depends=('openssl')
optdepends=('perl: siege2csv.pl')
backup=(etc/siegerc etc/urls.txt)
source=("http://download.joedog.org/siege/siege-${pkgver}.tar.gz")
md5sums=('84504c5107480ea4b4d952c910e1e32d')
sha256sums=('076df9fcdb7f63c46d6f661acc2ccc8405937ae9cae490ab8a9d78a9d2e7b8cb')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  install -d -m755 "$pkgdir/etc"
  install -d -m755 "$pkgdir/usr/share"
  make DESTDIR="$pkgdir" install
  sed -i 's/^# file =$/file = \/etc\/urls.txt/' "$pkgdir/etc/siegerc"
}
