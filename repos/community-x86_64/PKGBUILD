# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.9.0
pkgrel=1
pkgdesc='A fast AppStream metadata generator'
arch=(x86_64)
url='https://github.com/ximion/appstream-generator'
license=(GPL3)
depends=(appstream libarchive librsvg optipng glibd lmdb)
makedepends=(ldc meson ninja mustache-d curl gir-to-d gobject-introspection docbook-xsl)
source=(https://github.com/ximion/appstream-generator/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('7964924feb09348444cb8159fd2c22415f5df61d451d9f769f18d5b606d2cf52')

build() {
  export LDFLAGS="$(echo -ne $LDFLAGS | sed -e 's/-flto=auto/-flto=full/')"
  arch-meson $pkgname-$pkgver build -D download-js=false
  meson compile -C build
}

package() {
  meson install -C build --destdir="$pkgdir"
}
