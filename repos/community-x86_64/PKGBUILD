# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-genesis-plus-gx
pkgver=1559
pkgrel=1
pkgdesc='Sega MS/GG/MD/CD core'
arch=(x86_64)
url=https://github.com/libretro/Genesis-Plus-GX
license=(BSD LGPL2.1)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
source=(libretro-genesis-plus-gx::git+https://github.com/libretro/Genesis-Plus-GX.git#commit=92b35285bb07b7ab82f0d2aae12a7ab8f17107e7)
sha256sums=(SKIP)

pkgver() {
  cd libretro-genesis-plus-gx

  git rev-list --count HEAD
}

build() {
  make -C libretro-genesis-plus-gx -f Makefile.libretro
}

package() {
  install -Dm 644 libretro-genesis-plus-gx/genesis_plus_gx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-genesis-plus-gx/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx/
}

# vim: ts=2 sw=2 et:
