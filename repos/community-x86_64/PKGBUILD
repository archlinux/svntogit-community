# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=lsp
pkgname=haskell-lsp
pkgver=1.2.0.1
pkgrel=141
pkgdesc="Haskell library for the Microsoft Language Server Protocol"
url="https://github.com/haskell/lsp"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-async' 'haskell-attoparsec' 'haskell-data-default'
         'haskell-dependent-map' 'haskell-hashable' 'haskell-hslogger'
         'haskell-lens' 'haskell-lsp-types' 'haskell-network-uri' 'haskell-random'
         'haskell-scientific' 'haskell-sorted-list' 'haskell-unliftio-core'
         'haskell-unordered-containers' 'haskell-uuid' 'haskell-unliftio')
makedepends=('ghc' 'uusi' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-discover'
             'haskell-quickcheck-instances' 'haskell-rope-utf16-splay')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        lsp-aeson-2.patch)
sha512sums=('77c79d06cec9bf5fab707c9c1907ca8428ba15f763cad16c5a4b18fb4ee80a1b04770c2b9579d0f4ba31b16f71c2296501c2bd2d3da2475974b42e3c7daf3838'
            '99fe0e6948f850731980f25138a7a8bdc649b8f1beb852756dc58f3208b66b8e46b3f472467a3d6140eab13a5734a2274cd0373e54facf93c862ae4021adb7b8')

prepare() {
  cd $_hkgname-$pkgver
  patch -p2 -i ../lsp-aeson-2.patch
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fdemo

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
