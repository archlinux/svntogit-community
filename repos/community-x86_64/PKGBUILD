# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=microlens-th
pkgname=haskell-microlens-th
pkgver=0.4.3.13
pkgrel=1
pkgdesc="Automatic generation of record lenses for microlens"
url="https://github.com/stevenfontanella/microlens"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-microlens' 'haskell-th-abstraction')
makedepends=('ghc' 'haskell-tagged')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('81648369f235a4225c972bca21e9a8fa419454e739591f56cd560c018561827257b1679c4c222820834db06dcc5d287b4f1bf1068d61f4c6f158b09a85ddce9a')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -finlining
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
