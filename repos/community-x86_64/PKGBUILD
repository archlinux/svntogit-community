# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-brotlipy
pkgver=0.7.0
pkgrel=6
pkgdesc='Python binding to the Brotli library'
arch=('x86_64')
license=('MIT')
url='https://github.com/python-hyper/brotlipy'
depends=('brotli06' 'python-cffi')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis' 'brotli06-testdata')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-hyper/brotlipy/archive/v$pkgver.tar.gz"
        shared-brotli.patch
        brotlipy-disable-hypothesis-timeout.patch::https://github.com/python-hyper/brotlipy/pull/155.patch)
sha512sums=('bec8183cf3cc8ba9e594d1a86a8a33d88a6ac8e9c6a6287c837bda3726183ff1b486f2c49da4e489b5b3cb51b57dd5816413b59419920c2ced48435221f1beaf'
            'fc472dfb857d223f1b40edb8182a34c22ed1a8ff02953de934a0cf3b223e072699b100e63bf3ba1848b312743dc302b781b71fcafb3117223439f65f97e334de'
            '9ddc62dcbca5e33ea861f096a364e032622147c277e8a49d94ccf03ef706801b5c5d3d38da93ad39d01be87eafcde958903ac089028c190619a3f78786a11eee')

prepare() {
  patch -d brotlipy-$pkgver -p1 -i ../shared-brotli.patch
  patch -d brotlipy-$pkgver -p1 -i ../brotlipy-disable-hypothesis-timeout.patch
  sed -i 's|^TEST_DATA_DIR = .*|TEST_DATA_DIR = "/usr/share/brotli06/testdata"|' brotlipy-$pkgver/test/conftest.py

  export USE_SHARED_BROTLI=1
  export CFLAGS="-I/usr/include/brotli06 -L/usr/lib/brotli06"
}

build() {
  cd brotlipy-$pkgver
  python setup.py build
}

check() {
  cd brotlipy-$pkgver
  python setup.py pytest
}

package() {
  cd brotlipy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
