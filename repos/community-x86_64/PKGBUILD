# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Hannes Graeuler <hgraeule @ uos dot de>

pkgname=pngquant
pkgver=2.17.0
pkgrel=1
pkgdesc="Command-line utility to quantize PNGs down to 8-bit paletted PNGs"
arch=('x86_64')
url="https://pngquant.org/"
license=('BSD')
depends=('libpng' 'lcms2' 'libimagequant')
source=("https://github.com/kornelski/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('bc1baa43c814b4416bb63d7b2168d4e5395cfc69a00f8997a595361caa507887')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-openmp --with-libimagequant=/usr/lib
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    install -dm755 "$pkgdir/usr/bin"
    make DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
