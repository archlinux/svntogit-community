# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>

pkgname=('wireshark-cli' 'wireshark-common' 'wireshark-gtk' 'wireshark-qt')
pkgbase=wireshark
pkgver=2.6.3
pkgrel=1
arch=('x86_64')
license=('GPL2')
makedepends=('c-ares' 'geoip' 'qt5-tools' 'qt5-multimedia' 'gtk3' 'krb5' 'libpcap' 'bash' 'libssh'
             'gnutls' 'portaudio' 'lua52' 'python' 'desktop-file-utils' 'hicolor-icon-theme')
options=('!emptydirs')
url='https://www.wireshark.org/'
source=(https://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.xz
        wireshark.sysusers
        do_not_use_svn_version.patch)
sha256sums=('d158a8a626dc0997a826cf12b5316a3d393fb9f93d84cc86e75b212f0044a3ec'
            'df07748152443c7d727bd51cd57af950c345b7141986b4f0e476cd6aa3623ac4'
            '3981bb46f592e319f9c679c5e215ae0af828044a3b64d49d2cf0305ecacf4691')

prepare() {
  cd ${pkgbase}-${pkgver}
  # Avoid ugly "svn rev unknown from unknown" version string
  patch -p1 < "${srcdir}/do_not_use_svn_version.patch"
}

build() {
  cd ${pkgbase}-${pkgver}
  ./autogen.sh
  ./configure \
      --prefix=/usr \
      --with-qt=5 \
      --with-gtk=3 \
      --with-pcap \
      --with-zlib \
      --with-lua \
      --with-portaudio \
      --with-ssl \
      --with-krb5 \
      --with-c-ares \
      --with-geoip
  make all
}

package_wireshark-cli() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - CLI version'
  depends=('c-ares' 'geoip' 'krb5' 'libgcrypt' 'libcap' 'libpcap' 'gnutls' 'glib2' 'lua52' 'libssh' 'libxml2' 'libnghttp2')
  install=wireshark.install
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" uninstall-local

  # wireshark uid group is 150
  install -Dm644 "${srcdir}/wireshark.sysusers" "${pkgdir}/usr/lib/sysusers.d/wireshark.conf"
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "${pkgdir}/usr/bin/wireshark" "${pkgdir}/usr/bin/wireshark-gtk"

  # headers
  install -Dm 644 *.h -t "${pkgdir}/usr/include/${pkgbase}"
  for d in epan epan/crypt epan/dfilter epan/dissectors epan/ftypes epan/wmem wiretap wsutil; do
    install -Dm 644 ${d}/*.h -t "${pkgdir}/usr/include/${pkgbase}/${d}"
  done
}

package_wireshark-common() {
  pkgdesc='Common files used by wireshark-gtk and wireshark-qt'
  depends=('hicolor-icon-theme' 'shared-mime-info' 'xdg-utils')

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install-data-local
  rm "${pkgdir}"/usr/share/applications/wireshark{,-gtk}.desktop
}

package_wireshark-gtk() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - GTK frontend'
  depends=('desktop-file-utils' 'gtk3' 'portaudio' 'wireshark-cli' 'wireshark-common')
  replaces=(wireshark)
  conflicts=(wireshark)

  cd ${pkgbase}-${pkgver}
  install -Dm 755 .libs/wireshark-gtk "${pkgdir}/usr/bin/wireshark-gtk"
  install -Dm 644 wireshark-gtk.desktop -t "${pkgdir}/usr/share/applications"
}

package_wireshark-qt() {
  pkgdesc='a free network protocol analyzer for Unix/Linux and Windows - Qt frontend'
  depends=('desktop-file-utils' 'qt5-multimedia' 'wireshark-cli' 'wireshark-common')

  cd ${pkgbase}-${pkgver}
  install -Dm 755 .libs/wireshark "${pkgdir}/usr/bin/wireshark"
  install -Dm 644 wireshark.desktop -t "${pkgdir}/usr/share/applications"
}

# vim: ts=2 sw=2 et:
