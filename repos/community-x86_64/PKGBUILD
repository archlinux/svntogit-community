# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=sqlitebrowser
_gitcommit=ceb3730083e0bb1fb80ab4285e3b75c365f0ab98
pkgver=3.12.1
pkgrel=1
pkgdesc='SQLite Database browser is a light GUI editor for SQLite databases, built on top of Qt'
url='https://sqlitebrowser.org/'
arch=('x86_64')
license=('GPL3')
depends=('qt5-base' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('git' 'cmake' 'ninja' 'qt5-tools')
source=("git+https://github.com/sqlitebrowser/sqlitebrowser#tag=${_gitcommit}?signed")
sha256sums=('SKIP')
validpgpkeys=('05E463FDB5802C3D00257F9C79EAD0AB3BD7DD2C') # Justin Clift (GPG key for signing git commits) <justin@postgresql.org>

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${pkgname}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_TESTING=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

check() {
  cd ${pkgname}/build
  ctest -V
}

package() {
  cd ${pkgname}
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
