# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ciano
pkgver=0.2.2
pkgrel=3
pkgdesc="Multimedia file converter focused on simplicity"
arch=(x86_64)
url="https://robertsanseries.github.io/ciano/"
license=(GPL3)
depends=(ffmpeg granite imagemagick)
makedepends=(meson vala)
source=("https://github.com/robertsanseries/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "0001-Handle-GIF-with-ImageMagick.patch"
        "0001-Create-output-directory-if-not-exist.patch"
        "0001-Use-standard-icon-names.patch"
        "0001-Add-support-for-conversion-and-selection-of-OPUS-aud.patch"
        "0001-Add-support-for-conversion-and-selection-of-WEBP-ima.patch")
sha256sums=('49e648ff0d6f122057e6f15f7d3a3a451b005e296781d4f4d9816c8f0fcaa387'
            '8b44b7df58f82d0ff38572b773724b6f42e085fc1a1aa098776c794eb117b49d'
            'f682329bc9cbed2d0605b31e1cc4915c9a7e188bb398fed88001e892f44c50bb'
            'fb99403b4f6707454eef3f8552cded3e48e230dd4a118c7ada6e78e1a52fa3a4'
            '0eb72a2e4ea2f83e25043396acdf5a090f554985ab4902e63363c0a1d30b4d36'
            '4759a78664c2e406d41cdbf0b2187171a1383dd9dd6e3711bb969be9deea36b4')

prepare() {
  cd $pkgname-$pkgver

  # Handle GIF with ImageMagick
  patch -Np1 -i ../0001-Handle-GIF-with-ImageMagick.patch

  # Create output directory if not exist
  patch -Np1 -i ../0001-Create-output-directory-if-not-exist.patch

  # Use standard icon names
  patch -Np1 -i ../0001-Use-standard-icon-names.patch

  # Add support for more formats
  patch -Np1 -i ../0001-Add-support-for-conversion-and-selection-of-OPUS-aud.patch
  patch -Np1 -i ../0001-Add-support-for-conversion-and-selection-of-WEBP-ima.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
