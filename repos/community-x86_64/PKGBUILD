# Maintainer: David Runge <dave@sleepmap.de>

_name=igbinary
pkgname=php-igbinary
pkgver=3.0.1
pkgrel=3
pkgdesc="A drop in replacement for the standard php serializer"
arch=('x86_64')
url="https://github.com/igbinary/igbinary"
license=('BSD')
depends=('php')
backup=("etc/php/conf.d/${_name}.ini")
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_name}/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('4930670ecdc1796fa73184e8f12bf570f7b1a43cb422cede056ccd1ca28fc950b00a9418519c59a9c07e346a89153b6b105e873fcfccb1365d43d4abf98804da')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # disable the extension by default
  sed 's/extension/;extension/g' -i "${_name}.php.ini"
  phpize
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-igbinary
  make
}

check() {
  cd "$pkgname-$pkgver"
  NO_INTERACTION=1 make -k test
}

package() {
  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
