# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=osv-scanner
pkgver=1.0.2
pkgrel=1
pkgdesc="Vulnerability scanner written in Go which uses the data provided by https://osv.dev"
url='https://github.com/google/osv-scanner'
arch=('x86_64')
license=('Apache-2.0')
makedepends=('go')
source=(https://github.com/google/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c1f5d1a6ab6cb82a280b777212ac72a74f68475963a63e30650e2daf71afbef9')
b2sums=('3b8d8ad581c3540aa979e2fdaa773db3dd530477085e0a12775b21b7f58ab10dbd57aedc13b933ad57b25edb7012b11332a41f5c0b17f17c2b7bb861ea5df7f5')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/osv-scanner
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    osv-scanner
}

# vim: ts=2 sw=2 et:
