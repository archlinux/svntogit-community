# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=5.1.0.13
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory' 'deepin-network-utils' 'libdbusmenu-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz"
         deepin-dock-qt5.15.patch)
sha512sums=('c60734a0380b1c7bec0fecc293b36e7bf1e2ef3c3ba6527f19c16b14402d7a0cb7c9ce6a6b527f8f890ad0d7d3b9809ded0fc401cdf03f8f54c1044469c77e8e'
            '1fb3a6490e152d081f5fdff655e503dac602cb29b231df252edd664bf8b5ffcc589efd7d96b7b61e53f620670b110dcd5e1952fe5b88fe65fbf8acf8ce8812fd')

prepare() {
  patch -d dde-dock-$pkgver -p1 -i ../deepin-dock-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd dde-dock-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DDOCK_TRAY_USE_NATIVE_POPUP=YES
  ninja
}

package() {
  cd dde-dock-$pkgver
  DESTDIR="$pkgdir" ninja install
}
