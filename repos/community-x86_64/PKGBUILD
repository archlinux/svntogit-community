# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

pkgbase=python-statsmodels
pkgname=('python2-statsmodels' 'python-statsmodels')
pkgver=0.9.0
pkgrel=2
pkgdesc="Statistical computations and models for use with SciPy"
arch=('x86_64')
url="http://statsmodels.sourceforge.net/"
license=('BSD')
makedepends=('python2-setuptools' 'python2-scipy' 'python2-pandas' 'python2-patsy'
             'python-setuptools' 'python-scipy' 'python-pandas' 'python-patsy'
	     'cython' 'cython2' 'python-tempita' 'python2-tempita')
source=("https://github.com/statsmodels/statsmodels/archive/v${pkgver}.tar.gz")
md5sums=('8cb06154cef733d1e03b1b5d763a2ae1')

prepare() {
  cd "$srcdir"
  cp -a statsmodels-${pkgver} statsmodels-py2-${pkgver}
  cd statsmodels-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/statsmodels-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/statsmodels-${pkgver}
  python setup.py build
}

package_python2-statsmodels() {
  depends=('python2-scipy' 'python2-pandas' 'python2-patsy')
  optdepends=('python2-nose: needed for integrated test suite'
              'python2-matplotlib: needed for plotting functions')

  cd "$srcdir"/statsmodels-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-statsmodels() {
  depends=('python-scipy' 'python-pandas' 'python-patsy')
  optdepends=('python-nose: needed for integrated test suite'
              'python-matplotlib: needed for plotting functions')

  cd "$srcdir"/statsmodels-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

