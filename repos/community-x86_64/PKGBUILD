# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=wimlib
pkgver=1.14.1
pkgrel=1
pkgdesc='A library and program to extract, create, and modify WIM files'
arch=('x86_64')
url='https://wimlib.net'
license=('custom')
depends=('fuse3' 'ntfs-3g')
checkdepends=('attr')
optdepends=(
  'cdrkit: for making ISO image of Windows PE'
  'mtools: for making disk image of Windows PE' 
  'syslinux: for making disk image of Windows PE'
  'cabextract: for extracting Windows PE from the WAIK'
)
provides=('libwim.so')
source=("https://wimlib.net/downloads/$pkgname-$pkgver.tar.gz")
sha512sums=('9f92a8ce2d386918d66a1a3e52d4fbffeeb9f8b9345b25012fd0bd07039a00be84fd83d83a9bb25bcefab09597a81e9392522be835dd6693dd00050f8624af00')
b2sums=('56e44928229c9c4fda662fa64fc4021bd361c2c959f0786764b84fa4cc9d7f7b0d5440edb4d4079657cc7bf450ac9c755f554c7da7a8b2b7e96d69ddbeb4f22f')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --disable-static \
    --with-libcrypto \
    --with-fuse \
    --with-ntfs-3g \
    --prefix=/usr

  make
}

check() {
  cd "$pkgname-$pkgver"

  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # documentation
  install -Dm644 {NEWS,README}.md -t "$pkgdir/usr/share/doc/$pkgname"

  # licenses
  install -Dm644 COPYING* -t "$pkgdir/usr/share/licenses/$pkgname"
}
