# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=skylighting
pkgname=haskell-skylighting 
pkgver=0.1.1.5
pkgrel=22
pkgdesc="Syntax highlighting library"
url="https://github.com/jgm/skylighting"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-aeson' 'haskell-blaze-html' 'haskell-case-insensitive' 'haskell-hxt'
         'haskell-mtl' 'haskell-pretty-show' 'haskell-regex-pcre' 'haskell-safe' 'haskell-text'
         'haskell-utf8-string')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        system-pcre.patch)
md5sums=('7681c510ab20d79a32a0a54479a2325c'
         '5971978a26d8133df62b803bd67a70b6')

prepare() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    patch -p1 -i ../system-pcre.patch
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fsystem-pcre
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
