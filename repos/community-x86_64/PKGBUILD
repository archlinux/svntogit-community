# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=hlint
pkgname=(hlint haskell-hlint)
pkgver=1.9.41
pkgrel=2
pkgdesc="Source code suggestions"
url="http://community.haskell.org/~ndm/hlint/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=("ghc=8.0.1" "haskell-ansi-terminal" "haskell-cmdargs" "haskell-cpphs"
             "haskell-extra" "haskell-src-exts" "haskell-hscolour" "haskell-uniplate"
             "haskell-refact")
source=("https://hackage.haskell.org/packages/archive/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('2d9299f7952af44b2f06a67af917859fd51e1056c7d405f0930769ea1e093fb4')

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fgpl -fthreaded
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_hlint() {
    depends=('gmp' 'libffi')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_haskell-hlint() {
    pkgdesc="Source code suggestions (docs and libraries)"
    depends=("${makedepends[@]}" 'hlint')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgbase}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgbase}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.BSD3"
    rm -r "$pkgdir"/usr/{bin,share/${pkgbase}}
}
