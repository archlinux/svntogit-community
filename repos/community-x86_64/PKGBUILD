# Maintainer: Anatol Pomozov

pkgname=aws-c-io
pkgver=0.13.18
pkgrel=1
pkgdesc='AWS SDK module to handle all IO and TLS work for application protocols'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-io'
license=(Apache)
depends=(aws-c-common aws-c-cal s2n-tls)
makedepends=(cmake)
source=(aws-c-io-$pkgver.zip::https://github.com/awslabs/aws-c-io/archive/v$pkgver.zip)
sha256sums=('cbe5c2dc73003dbde057fc592b76b7d86cd4ae279681ea71a59ffb434fa6e194')

build() {
  cd aws-c-io-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-io-$pkgver
  cmake --build build --target test --verbose
}

package() {
  cd aws-c-io-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
