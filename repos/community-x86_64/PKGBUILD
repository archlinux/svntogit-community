# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgbase=Kvantum
pkgname=kvantum
pkgver=1.0.1
pkgrel=1
pkgdesc="SVG-based theme engine for Qt5/6 (including config tool and extra themes)"
arch=(x86_64)
url="https://github.com/tsujan/Kvantum"
license=(GPL)
provides=(kvantum-qt5)
conflicts=(kvantum-qt5)
replaces=(kvantum-qt5)
depends=(qt5-base qt6-base qt5-svg qt6-svg qt5-x11extras libx11 libxext hicolor-icon-theme kwindowsystem)
makedepends=(cmake qt5-tools qt6-tools)
source=(${url}/releases/download/V${pkgver}/${_pkgbase}-${pkgver}.tar.xz{,.asc})
sha256sums=('f7e4530f86544531313fab10267d934b7fa2d5a946b7dd26b2181001f0f8cae0'
            'SKIP')
validpgpkeys=(19DFDF3A579BD509DBB572D8BE793007AD22DF7E) # Pedram Pourang <tsujan2000@gmail.com>

prepare() {
  # Fix Qt6 build with Qt5 installed
  sed 's|Qt6 Qt5|Qt6|' -i ${_pkgbase}-${pkgver}/${_pkgbase}/style/CMakeLists.txt
}

build() {
  cmake -B build5 -S ${_pkgbase}-${pkgver}/${_pkgbase} -DCMAKE_INSTALL_PREFIX=/usr
  make -C build5
  cmake -B build6 -S ${_pkgbase}-${pkgver}/${_pkgbase} -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_QT5=OFF
  make -C build6
}

package() {
  make -C build5 DESTDIR="${pkgdir}" install
  make -C build6 DESTDIR="${pkgdir}" install
}
