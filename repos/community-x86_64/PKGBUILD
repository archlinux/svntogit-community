# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.9.8.1
pkgrel=1
pkgdesc="Batch image converter and resizer"
arch=('x86_64')
url="http://converseen.fasterland.net/"
license=('GPL3')
depends=('ghostscript' 'hicolor-icon-theme' 'libheif' 'imagemagick' 'libraw' 'librsvg' 'libwebp' 'libwmf' 'openexr' 'qt5-base')
makedepends=('cmake' 'qt5-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('43621963aceb1f68ca961417cd45d5bc64ba911efb02bae7ceb1e09974d70b9d')

prepare() {
  mkdir build
  cd $pkgname
  convert res/converseen.png +set date:create +set date:modify -resize 256x256 -alpha on res/converseen.png
  sed -i 's|/share/pixmaps|/share/icons/hicolor/256x256/apps|' CMakeLists.txt

  # Don't use legacy path for AppStream metainfo file
  # https://github.com/Faster3ck/Converseen/pull/61
  sed -i 's|/usr/share/appdata/|/usr/share/metainfo/|' CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname \
          -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
