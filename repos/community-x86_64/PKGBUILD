# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Jurica BradariÄ‡ <jbradaric@gmail.com>

pkgname=libluv
pkgver=1.34.1
pkgrel=1
pkgdesc='Bare libuv bindings for lua'
arch=('x86_64')
url='https://github.com/luvit/luv'
license=('apache')
provides=('libluv')
depends=('luajit' 'libuv')
makedepends=('cmake' 'luajit' 'libuv' 'ninja')
source=("https://github.com/luvit/luv/releases/download/${pkgver}-0/luv-${pkgver}-0.tar.gz"
        https://github.com/luvit/luv/raw/df03f2ac6d4e7269cb0c88be3ef44cf1f0de9b24/libluv.pc.in)
sha256sums=('353483a96602d620aa4e67424e2256cec9b89dc6ca3df421c12a65ef5dce93b9'
            'be2a4909c724e09a50de42b1caa3c82c1b1afee8b80abf20c6944f1df1c7fd0e')

prepare() {
  # They forgot to ship this file in this release. Hopefully they'll fix it in the next release.
  cp "$srcdir"/libluv.pc.in "$srcdir"/luv-${pkgver}-0/
}

build() {
    mkdir "${srcdir}/build"
    cd "${srcdir}/build"
    cmake -GNinja \
      -DWITH_SHARED_LIBUV=ON \
      -DLUA_BUILD_TYPE=System \
      -DBUILD_MODULE=OFF \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      "${srcdir}/luv-${pkgver}-0"
    ninja
}

package() {
    cd "${srcdir}/build"
    DESTDIR="${pkgdir}" ninja install
}

# vim:set ts=2 sw=2 et:
