# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.12.4
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=(x86_64)
url="https://amsynth.github.io/"
license=(GPL2)
groups=(dssi-plugins lv2-plugins pro-audio vst-plugins)
depends=(cairo gcc-libs glibc)
makedepends=(atk autoconf-archive dssi fontconfig freetype2 gdk-pixbuf2 glib2
gtk2 harfbuzz intltool jack ladspa liblo lv2 pandoc)
checkdepends=(lv2lint)
optdepends=('alsa-lib: for standalone application'
            'dssi-host: for DSSI plugin'
            'jack: for standalone application'
            'liblo: for standalone application and DSSI plugin'
            'new-session-manager: for session management with the standalone application'
            'lv2-host: for LV2 plugin'
            'vst-host: for VST plugin')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/amsynth/amsynth/archive/release-${pkgver}.tar.gz"
)
sha512sums=('7dab6f161887dd48bbe81c6e2d62ab3fd13c860c526e636a2053a785074a08a5b9060fc75b865d6facf75d6281af0f4cff4c4ba178f6271117a139d7e19b26d4')
b2sums=('ee9937830f847ef0ee92509b0f64ea2293a2a145d46ad1a465794488ede60cc95c78acae2a890f5e6614ef61b3cd7d9523b0f7f089da072dba7c77cb5a2b3b6f')

prepare() {
  mv -v "${pkgname}-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -fiv
  intltoolize -f
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack \
              --with-dssi \
              --with-lv2 \
              --with-vst \
              --with-pandoc
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  cp .libs/${pkgname}_lv2*.so data/${pkgname}.lv2/
  lv2lint -Mpack -I "data/${pkgname}.lv2" "http://code.google.com/p/amsynth/amsynth"
}

package() {
  depends+=(libatk-1.0.so libfontconfig.so libfreetype.so libgdk_pixbuf-2.0.so
  libgdk-x11-2.0.so libgio-2.0.so libglib-2.0.so libgobject-2.0.so
  libgtk-x11-2.0.so libharfbuzz.so libpangocairo-1.0.so libpangoft2-1.0.so
  libpango-1.0.so)

  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
