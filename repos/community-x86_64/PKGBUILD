# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: flu

pkgname=sqlcipher
pkgver=4.1.0
pkgrel=1
pkgdesc="SQLite extension that provides transparent 256-bit AES encryption of database files"
arch=('x86_64')
url="https://www.zetetic.net/sqlcipher/"
license=('BSD')
makedepends=('openssl' 'sqlite' 'tcl')
source=(https://www.zetetic.net/$pkgname/verify/$pkgver/$pkgname-v$pkgver.zip
        https://www.zetetic.net/$pkgname/verify/$pkgver/$pkgname-v$pkgver.zip.sig)
validpgpkeys=('D83F5F9EB811D6E6B4A0D9C5D1FA3A2A97ED25C2') # Zetetic LLC <support@zetetic.net>
sha256sums=('d65b337b7eb69db1abbbb66341e8bde54cf7b2b7da7b7f369e5f876d6fb7e6a1'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-v$pkgver

  ./configure --prefix=/usr --enable-tempstore=yes \
                CFLAGS="$CFLAGS -DSQLITE_HAS_CODEC" LDFLAGS="-lcrypto"
  make
}

package() {
  cd "${srcdir}"/$pkgname-v$pkgver

  make DESTDIR="${pkgdir}" install

#license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
