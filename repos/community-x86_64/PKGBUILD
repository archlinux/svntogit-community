# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Kogan <michael dot kogan at gmx dot net>

pkgname=perl-net-dropbox-api
pkgver=1.9
pkgrel=6
pkgdesc="Net::Dropbox::API - Perl dropbox API interface"
arch=('x86_64')
url="http://search.cpan.org/~norbu/Net-Dropbox-API/lib/Net/Dropbox/API.pm"
license=('GPL' 'PerlArtistic')
depends=('perl-common-sense' 'perl-data-random' 'perl-http-message' 'perl-json' 'perl-libwww' 'perl-mouse' 'perl-net-oauth' 'perl-uri' 'perl-lwp-protocol-https')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
source=("http://search.cpan.org/CPAN/authors/id/N/NO/NORBU/Net-Dropbox-API-${pkgver}.tar.gz")
sha1sums=('c1c5ba827492e171cb65124b65ece4da2442ea84')

build() {
  cd "$srcdir/Net-Dropbox-API-${pkgver}"
  PERL_USE_UNSAFE_INC=1 \
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Net-Dropbox-API-${pkgver}"
  make test
}

package() {
  cd "$srcdir/Net-Dropbox-API-${pkgver}"
  make DESTDIR="$pkgdir" install
}
