# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=liblockfile
pkgver=1.16_1.1
_basever=1.16
pkgrel=1
pkgdesc="a library with NFS-safe locking functions"
arch=('x86_64')
license=('GPL')
url="https://packages.debian.org/unstable/libs/liblockfile1"
depends=('glibc')
makedepends=()
source=(http://ftp.debian.org/debian/pool/main/libl/$pkgname/${pkgname}_${_basever}.orig.tar.gz
	http://ftp.debian.org/debian/pool/main/libl/$pkgname/${pkgname}_${pkgver/_/-}.debian.tar.xz)
sha256sums=('cf6e3828ced3fcc7e5ed36b7dabdf9e0e3ba55a973dab8ed212fb86afc901c69'
            '73043ba68ccded64e677da413972d1e71b3c4400bb87a9d3738469c6730e7cfe')

build() {
  cd "$srcdir"/$pkgname-${_basever}
  for p in `cat ../debian/patches/series` ; do
    patch -p1 < ../debian/patches/$p
  done
  LDCONFIG=/usr/bin/true ./configure --enable-shared --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir"/$pkgname-${_basever}
  mkdir -p "$pkgdir"/usr/{lib,bin,include,share/man/man1,share/man/man3}
  make install prefix="$pkgdir"/usr mandir="$pkgdir"/usr/share/man
}
