# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Felix Hanley <felix@seconddrawer.com.au>
# Contributor: SÅ‚awomir Kowalski <suawekk@gmail.com>

pkgname=percona-toolkit
pkgver=3.5.1
pkgrel=1
pkgdesc="Collection of advanced command-line tools to perform a variety of MySQL and system tasks."
url="https://www.percona.com/software/database-tools/percona-toolkit"
arch=('x86_64')
license=('GPL')
depends=('perl-term-readkey')
makedepends=('perl-dbd-mysql' 'perl-dbi' 'go' 'git')
optdepends=(
    'perl-dbd-mysql: MySQL connections'
    'perl-dbi: MySQL connections'
)
options=('!emptydirs' 'purge' '!lto')
source=("https://github.com/percona/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('73c48be4282e83791d7d63ff478dc357a6173dd6a5994bd2d002fe6135ae3587')

prepare() {
    mkdir -p "$srcdir"/src/github.com/percona
    cd "$srcdir"/src/github.com/percona
    mv "$srcdir"/$pkgname-$pkgver $pkgname
    cd $pkgname

    sed -ri src/go{,/*}/Makefile \
        -e "s#\\$\\(shell git describe --abbrev=0\\)#v$pkgver#" \
        -e "s#\\$\\(shell git rev-(parse|list).* HEAD\\)#v$pkgver#" \
        -e "s#\\$\\(shell git rev-parse --show-toplevel\\)#$srcdir/src/github.com/percona/$pkgname#" \
        -e "s#\\$\\(shell basename \`git rev-parse --show-toplevel\`\\)#$pkgname#" \
        -e "s#\\bLDFLAGS\\b#PTLDFLAGS#" \
        -e 's#^PTLDFLAGS="#PTLDFLAGS="-extldflags \\"$(LDFLAGS)\\" #'

    # disable go dep
    sed '/dep ensure/d' -i src/go/Makefile
}

build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    export GOPATH="$srcdir"
    export PATH="$GOPATH/bin:$PATH"

    cd "$srcdir"/src/github.com/percona/$pkgname
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps MODULEBUILDRC=/dev/null
    perl Makefile.PL INSTALLDIRS=vendor
    make

    cd src/go
    make linux-amd64
}

package() {
    cd "$srcdir"/src/github.com/percona/$pkgname
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    make install DESTDIR="$pkgdir"
    rm -rf "$pkgdir"/usr/lib

    cd bin
    for bin in *; do
        if [ ! -f "$pkgdir/usr/bin/vendor_perl/$bin" ]; then
            install -Dm755 "$bin" "$pkgdir/usr/bin/$bin"
        fi
    done
}
