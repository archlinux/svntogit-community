# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.20.0
pkgrel=1
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qca' 'qt5-script' 'qt5-webengine' 'libofx'
         'grantlee' 'kross' 'knewstuff' 'krunner' 'knotifyconfig'
         'sqlcipher' 'python' 'hicolor-icon-theme')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt5-tools' 'patch' 'kdesignerplugin')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('3c58c54ad048b03608fde0b794de0cd3a83d0b5bd43c1d033c5dc8739f755365'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_LIBDIR=/usr/lib \
          -DSKG_WEBENGINE=ON \
          -DSKG_BUILD_TEST=OFF
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
