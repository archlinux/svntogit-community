# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=xchm
pkgver=1.35
pkgrel=1
pkgdesc='Viewer for CHM files (GUI front-end to chmlib)'
arch=('x86_64')
url='https://github.com/rzvncj/xCHM'
license=('GPL2')
depends=('chmlib' 'wxwidgets-gtk3')
source=("https://github.com/rzvncj/xCHM/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('719ca190e6dbc27d0ea9c7515df02da1d15e3edb4b5a341710fe61665b6589a7')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
