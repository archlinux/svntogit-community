# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=packer
replaces=('packer-io')
pkgver=1.7.8
pkgrel=1
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL')
makedepends=('git' 'go' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('6ea9fbf0f45fad3387a0462b1b4f788e5d1c5bebfb9f7e1cc82726a97e4a42ce5397e829bcc0c3441e10c0230ab4cd1d749f8e9d4839307f97bd72e699db708b')
changelog=CHANGELOG.md

prepare() {
	export GOPATH="${srcdir}"
	export PATH="$PATH:$GOPATH/bin"
	mkdir -p "src/github.com/hashicorp"
	mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
	sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}

build() {
  cd "src/github.com/hashicorp/${pkgname}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o packer-binary
}

# Currently broken
# check() {
#   cd "src/github.com/hashicorp/${pkgname}"
#   go test ./...
# }

package() {
	cd "src/github.com/hashicorp/${pkgname}"
	install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
	install -Dm644 contrib/zsh-completion/_packer "${pkgdir}"/usr/share/zsh/site-functions/_packer
}

# vim:set et sw=2 ts=2 tw=79:
