# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgname=dolphin-emu
pkgver=5.0rc
pkgrel=1
epoch=1
pkgdesc='A Gamecube / Wii / Triforce emulator'
arch=('x86_64')
url='http://dolphin-emu.org'
license=('GPL2')

makedepends=('cmake')
depends=('bluez-libs' 'enet' 'ffmpeg' 'libao' 'libevdev' 'miniupnpc'
         'portaudio' 'sfml' 'soundtouch' 'xdg-utils' 'wxgtk')
optdepends=('pulseaudio: PulseAudio backend')

options=('!emptydirs')

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dolphin-emu/dolphin/archive/5.0-rc.tar.gz")
md5sums=('4b1fa4bbf917bdebf39e710ca214b873')

prepare() {
  cd dolphin-5.0-rc

  # Compatibility with miniupnpc 1.9.20150730, which requires a new ttl argument.
  sed 's/upnpDiscover(2000, nullptr, nullptr, 0, 0, \&upnperror)/upnpDiscover(2000, nullptr, nullptr, 0, 0, 2, \&upnperror)/' -i Source/Core/Core/NetPlayServer.cpp

}

build() {
  cd dolphin-5.0-rc

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_CXX_FLAGS='-fno-pie' \
    -DENABLE_LTO='TRUE' \
    -DENABLE_SDL='TRUE' \
    -DUSE_SHARED_ENET='TRUE'
  make
}

package() {
  cd dolphin-5.0-rc/build

  make DESTDIR="${pkgdir}" install
}
