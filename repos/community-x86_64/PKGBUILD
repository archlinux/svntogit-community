# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=parsers
pkgname=haskell-parsers
pkgver=0.12.8
pkgrel=1
pkgdesc="Parsing combinators"
url="https://github.com/ekmett/parsers/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base-orphans' 'haskell-charset' 'haskell-mtl'
         'haskell-parsec' 'haskell-scientific' 'haskell-semigroups' 'haskell-text'
         'haskell-unordered-containers')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-instances')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e38e2df58dad939c4402c3db5c33f656f71f3528d1b04223d0f19b340fa187c94145b19fc5a642fbfdc402b2d71fe3eaebc46d865b51a541a8b70cdad4d585e8')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
