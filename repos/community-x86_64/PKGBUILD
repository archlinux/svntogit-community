# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: poplarch <poplarch@gmail.com>

pkgname=fcitx-libpinyin
pkgver=0.3.1
pkgrel=2
pkgdesc="Fcitx Wrapper for libpinyin, Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx-libpinyin"
license=('GPL')
depends=('fcitx' 'libpinyin' 'fcitx-qt4' 'qtwebkit')
makedepends=('cmake' 'intltool' 'wget')
source=("http://download.fcitx-im.org/$pkgname/$pkgname-${pkgver}_dict.tar.xz")
install=$pkgname.install

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  rm -rf build
  mkdir build
  cd build

  msg "Starting make..."

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}

sha512sums=('3b7501a75dab2f5b83541c71ccac93709f3354f390f322d7b95e4b94e0c4dc6963522759ab52eb77203d787d6d14d3ce1c838dd92ecd0db041a3b7a25008eaa4')
