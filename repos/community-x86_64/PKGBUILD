# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=apk-tools
pkgver=2.12.9
pkgrel=1
pkgdesc="Alpine Package Keeper - package manager for alpine"
url="https://gitlab.alpinelinux.org/alpine/apk-tools"
arch=('x86_64')
license=("GPL2")
depends=(zlib openssl)
makedepends=(scdoc lua53 lua53-zlib)
source=("https://gitlab.alpinelinux.org/alpine/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
b2sums=('71b5a68df30989405993b3791248a18b5fc4a9f247f26be428a98b53cf630170ba7429cb37ef24da7d92d43d93ce7237e87a0316005ab6f03c41eb8f67e06ebc')

prepare() {
  cd "${pkgname}-v${pkgver}"
  sed -i -e 's:-Werror::' Make.rules
}

build() {
  cd "${pkgname}-v${pkgver}"
  make SBINDIR=/usr/bin LIBDIR=/usr/lib
}

check() {
  cd "${pkgname}-v${pkgver}"
  make test
}

package() {
  cd "${pkgname}-v${pkgver}"
  make install DESTDIR="${pkgdir}" SBINDIR=/usr/bin LIBDIR=/usr/lib
}

# vim: ts=2 sw=2 et:
