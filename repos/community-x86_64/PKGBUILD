# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapp
pkgver=2.4.1
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(libdbusmenu-gtk3 libgnomekbd)
optdepends=('python: for mate-desktop status applet')
makedepends=(meson samurai gobject-introspection python-gobject vala)
provides=(xapps)
conflicts=(xapps)
replaces=(xapps)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('919bfd9633d8a3a10d06f711feecf7de90d6c4c600d30c19e1550374471439c1')
b2sums=('2b64e4eb12edda67957768c3f708a10c694d45bf57f0a18e1496c19e770819997961ff7c32ce550d52cb378c9cf8d9f4c9ee1fd70605c7aec079d8a156fd7596')

build() {
  arch-meson ${pkgname}-${pkgver} build
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install

  # rm useless scripts
  rm -r "${pkgdir}"/usr/bin

  # byte-compile python modules since meson does not implement autotoolsâ€™ py-compile.
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m compileall -o 0 -o 1 -o 2 --hardlink-dupes -s "${pkgdir}" "${pkgdir}"${site_packages}
}
