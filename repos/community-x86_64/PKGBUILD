# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=spire
pkgname=("spire-agent" "spire-server")
pkgver=1.1.1
pkgrel=1
pkgdesc="SPIFFE Runtime Environment"
url='https://github.com/spiffe/spire'
arch=("x86_64")
license=("Apache")
makedepends=("go")
depends=("glibc")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/spiffe/spire/archive/v${pkgver}.tar.gz")
sha512sums=('c66ff8ab0a3dfe041e0d94dabd58145bc88bc26ce1bb3a7d339b47387d7f741117fdf2149e9c8b4951ccb2604b430a56569621542972a32a3cea487dc56de8bb')
b2sums=('4762b791efdff227c3810415fe4d80db1a185b98b0a53fc7fb4b2ff2b32e3de8bf2634480e184601b0374b7eccd9df1c4eb41e09263462e4ed01c0d4b61e0761')

build() {
  cd "${pkgbase}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  cd cmd/spire-agent/
  go build -o spire-agent .
  cd ../../cmd/spire-server
  go build -o spire-server .
}

check() {
  cd "${pkgbase}-${pkgver}"
  # test is blocking for some reason..
  # go test -v ./...
}

package_spire-agent() {
  pkgdesc="SPIFFE runtime environment (agent)"
  provides=("spire-agent")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-agent/spire-agent "${pkgdir}/usr/bin/spire-agent"
}

package_spire-server() {
  pkgdesc="SPIFFE runtime environment (server)"
  provides=("spire-server")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-server/spire-server "${pkgdir}/usr/bin/spire-server"
}
