# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com
# Contributor: Philipp A. <flying-sheep@web.de>

pkgname=python-unicodedata2
_pyname=${pkgname#python-}
pkgver=15.0.0
pkgrel=1
pkgdesc="unicodedata backport/updates to Python 3"
arch=('x86_64')
url="https://github.com/fonttools/$_pyname"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest'
              'python-pytest-xdist'
              'python-pytest-randomly')
source=("$url/archive/$pkgver/$_pyname-$pkgver.tar.gz")
sha256sums=('3793953ec7321eb9a01e398cac230aed557b9c7706e6b569b34ae6c442b8a2aa')

build() {
	cd "$_pyname-$pkgver"
	python setup.py build
}

check() {
	cd "$_pyname-$pkgver"
	python setup.py test
}

package() {
	cd "$_pyname-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
