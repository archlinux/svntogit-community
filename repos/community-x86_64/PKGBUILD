# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=2.2.0
pkgrel=1
pkgdesc='Minimalistic plugin API for video effects'
arch=('x86_64')
url='https://frei0r.dyne.org/'
license=('GPL2')
depends=('cairo' 'gavl')
makedepends=('cmake' 'doxygen' 'ninja' 'opencv')
optdepends=('opencv: for facebl0r and facedetect plugins')
#source=("https://files.dyne.org/frei0r/$pkgname-$pkgver.tar.gz"{,.asc})
source=(https://github.com/dyne/frei0r/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
validpgpkeys=('6113D89CA825C5CEDD02C87273B35DA54ACB7D10') # Denis Roio (Jaromil)
sha256sums=('51bbcbebb40be75c75dba7df211b0ef1bd0463b02b80b7d34e93efeaceb98d3b')

build() {
  cmake -S frei0r-$pkgver -B build -G Ninja -DCMAKE_INSTALL_PREFIX='/usr'
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
