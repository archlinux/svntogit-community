# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Changaco <changaco ατ changaco δοτ net>

pkgname=slowhttptest
pkgver=1.9.0
pkgrel=1
pkgdesc='Highly configurable tool that simulates some Application Layer Denial of Service (DoS) attacks'
url='https://github.com/shekyan/slowhttptest'
arch=('x86_64')
license=('Apache')
depends=('glibc' 'openssl' 'gcc-libs')
source=(https://github.com/shekyan/slowhttptest/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ee32b0832a1cf30e09a77c0d3d0d7c710fadae8fc08944a0b7fafb38d9080a749a5c6eb1f5f755ee13169a333d19780d8e435d1e1ab64c58c060c80b41e3b6a2')
b2sums=('04dd6fe19e13d5bcf95f8cef5ab6cd77819d92c3a4a19abad4206fcb0ee290d286592ba7a8c3cbc818de61ff43a4fe44ebf423f15257321cc1d3a482421b5176')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
