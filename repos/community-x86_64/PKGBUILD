# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.6.3
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('LGPL3')
provides=('deepin-qt5dxcb-plugin')
conflicts=('deepin-qt5dxcb-plugin')
replaces=('deepin-qt5dxcb-plugin')
depends=('cairo' 'deepin-wayland' 'kwayland' 'qt5-x11extras')
makedepends=('expac' 'qt5-xcb-private-headers' 'libglvnd' 'libxcb')
groups=('deepin')
source=("https://github.com/linuxdeepin/qt5platform-plugins/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4da8bca97af45f0d5194c24218d39e24ada0dd1c91d74b19b011485edc4b1a336e27412f25ca31bc730c0aeec37447dd5e77c4f735eb1e93b9e4abb4ff95d79d')

prepare() {
  cd qt5platform-plugins-$pkgver

  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev

  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' xcb/linux.pri
  sed -i '/wayland/d' qt5platform-plugins.pro
}

build() {
  cd qt5platform-plugins-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5platform-plugins-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
