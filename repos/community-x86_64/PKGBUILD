# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=wails
pkgver=2.2.0
pkgrel=1
pkgdesc="Create desktop apps using Go and Web Technologies"
arch=(x86_64)
url="https://github.com/wailsapp/wails"
license=(MIT)
depends=(glibc)
makedepends=(go webkit2gtk gtk3)
source=($pkgname-$pkgver.tar.gz::https://github.com/wailsapp/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('f42d5f5ca14572697c3cb1e634cf09b4b81b5bbbc89e280b4962d7ab0daa524f8907323b70bfb6dc05c3e31527a8486a98b3d8d85141d1c861f014597a2eb789')
b2sums=('7ddd8cb4e746b841cad22eb847ee5a14a8aee75776813d3c6c2ed13bf247b5ca04bdc511658250a41ea0368196d65f7697a4e1ae8681ab55787e1735777920ad')

prepare() {
  mkdir -vp $pkgname-$pkgver/build
}

build() {
  cd $pkgname-$pkgver/v2
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o build/ ./cmd/wails
}

check() {
  cd $pkgname-$pkgver
  go test
}

package() {
  install -vDm 755 $pkgname-$pkgver/v2/build/$pkgname -t "$pkgdir/usr/bin/"
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,CONTRIBUTORS,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
