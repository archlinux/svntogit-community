# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ejabberd
pkgver=20.07
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64')
url="https://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite' 'gd')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
optdepends=('gsfonts: for captcha'
	    'imagemagick: for captcha')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"base64url::git://github.com/dvv/base64url.git#commit=f2c64ed8b9bebc536fad37ad97243452b674b837"
	"cache_tab::git://github.com/processone/cache_tab#commit=3278ec3bbf60168c01b48babd264a5dd6350e224"
	"eimp::git://github.com/processone/eimp#commit=bd9f2cf0e966ab601ea06238abac46e6e77f6581"
	"elixir::git://github.com/elixir-lang/elixir#commit=867da6f876c48279823a41a33030c95ef37379b2"
	"epam::git://github.com/processone/epam#commit=66056f1482acd9970df564d256a4944ea5f3a472"
	"eredis::git://github.com/wooga/eredis#commit=cbc013f516e464706493c01662e5e9dd82d1db01"
	"esip::git://github.com/processone/esip#commit=fe0ff038f093bea4fca3e7b8453656f57f2333eb"
	"ezlib::git://github.com/processone/ezlib#commit=1927d3cf624ecd4ed6d56c1f43f1eccd2734cecc"
	"fast_tls::git://github.com/processone/fast_tls#commit=1521dd401f3e3be55a8be099c1c399e7fdddf176"
	"fast_xml::git://github.com/processone/fast_xml#commit=7fce39a0a82ae4b7afeeecd9b074f1180c6700a1"
	"fast_yaml::git://github.com/processone/fast_yaml#commit=5c8e4a0511117617a3b9ff36c3385cbce40f3fb4"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=8f1b715d36b650ec1e1f5612c00e28af6ab0de82"
	"idna::git://github.com/benoitc/erlang-idna#commit=6cff72747821110169ecfac871b0c69e5064afff"
	"jiffy::git://github.com/davisp/jiffy#commit=6dadc0cd22b06e2b1972ee8aba5708c478418926"
	"jose::git://github.com/potatosalad/erlang-jose#commit=5ad99d03c5381ec68a9576c81ed4e9312fea73e0"
	"lager::git://github.com/erlang-lager/lager#commit=4ffdf0dbef53304bc0e958a00776bf0650de8c8b"
	"luerl::git://github.com/rvirding/luerl#commit=01fa43a1a5d68becfe07236a9a71505039474108"
	"mqtree::git://github.com/processone/mqtree#commit=3fb6b43fb8f72b6b824c03619ba1d882fbb83251"
	"p1_acme::git://github.com/processone/p1_acme.git#commit=83559538bb2036593d8aa9bc36ddcf7af187903a"
	"p1_mysql::git://github.com/processone/p1_mysql#commit=da86adc5b5418b37a2e9e3fd9b9721d9a5a05d80"
	"p1_oauth2::git://github.com/processone/p1_oauth2#commit=5fea359d9c0a4a0603a5aff048985a5c84382c86"
	"p1_pgsql::git://github.com/processone/p1_pgsql#commit=04d4a6fb814e4116782c0454318bb98ac99e1ee5"
	"p1_utils::git://github.com/processone/p1_utils#commit=aaf2b25d9dc273da7ec29fe4146610f5ef6396af"
	"pkix::git://github.com/processone/pkix#commit=c4e400bed13fbbaf5136fc578ebcd108d06b15b9"
	"rebar_elixir_plugin::git://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"sqlite3::git://github.com/processone/erlang-sqlite3#commit=5d2356244783016a5def4205fa370ee3772e9fa8"
	"stringprep::git://github.com/processone/stringprep#commit=a7b25c6eeed2136d1ffb396ea732408c37c61ac3"
	"stun::git://github.com/processone/stun#commit=1cf72b52061ac329d0ef4e40a6d10f751ec8bb55"
	"unicode_util_compat::git://github.com/benoitc/unicode_util_compat.git#commit=38d7bc105f51159e8ea3279c40121db9db1e652f"
	"xmpp::git://github.com/processone/xmpp#commit=0029f3e5021e7152373187888473c9a2e5ca07a4"
	"yconf::git://github.com/processone/yconf#commit=7bca9c9c0e0c02da26b36293d80faa27c54b2ced"
	"$pkgname.logrotate"
	"sysuser.conf")
sha256sums=('7fd1142906a5f7d0345dc3d1363f94c62cc7d62088f5b2f3bd70dff4dca3bb41'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '31780cac78736d285e46f445f8c8463a70f2aeb2683280c259129db11832ddd2'
            '68de8fd4df6e4f0e21a241dcd2b7075bdff495876646dd726c7054bf5780e3c6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in \
    base64url cache_tab eimp elixir epam eredis esip ezlib fast_tls fast_xml fast_yaml goldrush idna jiffy jose lager luerl mqtree p1_acme p1_mysql p1_oauth2 p1_pgsql p1_utils pkix rebar_elixir_plugin sqlite3 stringprep stun unicode_util_compat xmpp yconf \
    ; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv "$srcdir"/$i "$srcdir/$pkgname-$pkgver/deps"
  done
  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  sed -i \
    -e "s|$pkgdir||g" \
    -e "s|Group=.*|Group=jabber|g" \
    -e "s|User=.*|User=jabber|g" \
    ejabberd.service
  install -Dm0644 ejabberd.service "$pkgdir"/usr/lib/systemd/system/ejabberd.service

  install -Dm04750 deps/epam/priv/bin/epam "$pkgdir"/usr/lib/ejabberd-$pkgver/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf
  install -Dm644 tools/ejabberdctl.bc "$pkgdir"/usr/share/bash-completion/completions/ejabberdctl

  # workaround
  ln -s mod_configure.beam "$pkgdir"/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f "$pkgdir"/usr/bin/{elixir,iex,mix}

  echo -e "\n\n# home dir workaround\nHOME=/var/lib/ejabberd" >>"$pkgdir"/etc/ejabberd/ejabberdctl.cfg

  # /usr/lib/ejabberd symlink follows to ejabberd version
  ln -s ejabberd-$pkgver "$pkgdir"/usr/lib/ejabberd

  # permissions
  chown -R 17:17 "$pkgdir"/var/{lib,log}/ejabberd
  chown root:17 \
    "$pkgdir"/etc/ejabberd/ejabberd.yml \
    "$pkgdir"/etc/ejabberd/ejabberdctl.cfg \
    "$pkgdir"/etc/ejabberd \
    "$pkgdir"/usr/lib/ejabberd/priv/bin/epam

  # fix eimp
  chmod a+x "$pkgdir"/usr/lib/eimp-*/priv/bin/eimp
}

# vim:set ts=2 sw=2 et:
