# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.18.0
pkgrel=2
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('x86_64')
license=('MIT')
depends=('gcc-libs' 'libgit2>=1.2.0' 'libgit2.so' 'libxcb' 'zlib')
makedepends=('cargo' 'python') # xcb crate needs python
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7c8d5829fc8e555d02c576ba0325f0a68114c1fd2eadf17166a3bda866f539be')
b2sums=('3a55e9850141fe554e704265d77652e38ed404f2065f5d0b550e452c372897442b709931343d0ba3588f270d117cfd168bfc8bb4ae8ce56c3113efaf80d1f9db')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
