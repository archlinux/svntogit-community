# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gammu
pkgver=1.39.0
pkgrel=2
pkgdesc="GNU All Mobile Management Utilities"
arch=('x86_64')
url="http://wammu.eu/gammu/"
license=('GPL')
depends=('glib2' 'bluez-libs' 'curl' 'libdbi' 'libmariadbclient' 'postgresql-libs' 'libusb' 'libgudev')
makedepends=('cmake' 'doxygen' 'python2')
checkdepends=('libdbi-drivers')
optdepends=('dialog: support for the gammu-config script' 'python2: for python bindings')
source=(https://dl.cihar.com/$pkgname/releases/$pkgname-$pkgver.tar.xz)
sha256sums=('66d1d991d7a993fdf254d4c425f0fdd38c9cca15b1735936695a486067a6a9f8')

prepare() {
  cd $pkgname-$pkgver
# bash completion dir change
  sed -i 's,COMPLETIONSDIR "/etc/bash_completion.d",COMPLETIONSDIR "/usr/share/bash-completion/completions",' contrib/CMakeLists.txt
}

build() {
  cd $pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX="/usr" -DLIB_SUFFIX=""
  make
}

check() {
  cd $pkgname-$pkgver

  # tests can be flaky on slower hardware due to timing
  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
