# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

pkgname=python-plyvel
pkgver=1.4.0
pkgrel=1
pkgdesc="A fast and feature-rich Python interface to LevelDB"
arch=('x86_64')
license=('BSD')
url="https://github.com/wbolster/plyvel"
depends=('leveldb' 'python')
makedepends=('cython' 'python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/p/plyvel/plyvel-$pkgver.tar.gz")
sha512sums=('6532007bb103f2b0121c41ae0a23fa67dfd2c27b1472b43265f6fb5928e4f28514744887f190fb087c8640cba2e62923a1ffe14f59a70b123dc9ad7798e329d5')

prepare() {
  # Shipped egg-info has wrong permission etc
  cd plyvel-$pkgver
  make clean
}

build() {
  cd plyvel-$pkgver
  make cython
  python setup.py build
}

check() {
  cd plyvel-$pkgver
  python setup.py pytest
}

package() {
  cd plyvel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
