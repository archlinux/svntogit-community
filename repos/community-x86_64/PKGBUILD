# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=sassc
pkgver=3.4.2
pkgrel=1
pkgdesc="C implementation of Sass CSS preprocessor."
arch=("i686" "x86_64")
url="http://libsass.org/"
license=("MIT")
depends=("libsass")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha256sums=("ad805f2d404d17cf2980c8079a7413cd58d2f2085120167997b85420a722e079")


build() {
	cd "$srcdir/$pkgname-$pkgver"

	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir/" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
