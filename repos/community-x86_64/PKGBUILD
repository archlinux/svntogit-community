# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Beno√Æt Allard <benoit dot allard at gmx dot de>
# Contributor: killruana <killruana@gmail.com>

pkgname=yosys
pkgver=0.9
pkgrel=14
pkgdesc='A framework for RTL synthesis'
arch=('x86_64')
url='http://www.clifford.at/yosys/'
license=('custom:ISC')
depends=('abc' 'bash' 'boost-libs' 'tcl' 'libffi' 'python' 'protobuf')
makedepends=('boost')
checkdepends=('iverilog')
optdepends=('graphviz: Schematics display support'
            'xdot: Display netlists')
source=("https://github.com/cliffordwolf/yosys/archive/$pkgname-$pkgver.tar.gz"
        gcc11.patch)
sha512sums=('6ae0f822ca0e3ec749773dae89e948488b698aaa149e73f04604dd899ac403a3073645f0e27382801f5f1a9483d87ef4066fde5dc797181eff06684de7a5b138'
            '5e087b9b11544adfb48eee6242634f47c60280748c627b27fa806490906047ee1ce34f174398dac6544cc83845823d76bfe94004157304d32ec40ca425a24386')

_make() {
    make \
        PREFIX="/usr" \
        CONFIG=gcc \
        ENABLE_TCL=1 \
        ENABLE_ABC=1 \
        ENABLE_GLOB=1 \
        ENABLE_PLUGINS=1 \
        ENABLE_LIBYOSYS=1 \
        ENABLE_PROTOBUF=1 \
        ENABLE_ZLIB=1 \
        ENABLE_PYOSYS=1 \
        ABCEXTERNAL=abc \
        BOOST_PYTHON_LIB="-lpython3.9 -lboost_python39" $@
}

prepare() {
    cd $pkgname-$pkgname-$pkgver
    patch -Np1 -i ../gcc11.patch
}

build() {
    cd $pkgname-$pkgname-$pkgver
    _make
}

check() {
    cd $pkgname-$pkgname-$pkgver
    _make test
}

package() {
    cd $pkgname-$pkgname-$pkgver
    _make DESTDIR="$pkgdir" PYTHON_PREFIX="$pkgdir/usr" install

    install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
