# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

_langs=(afr amh ara asm aze aze_cyrl bel ben bod bos bre bul cat ceb ces
    chi_sim chi_tra chr cos cym dan dan_frak deu deu_frak div dzo ell eng enm epo
    equ est eus fao fas fil fin fra frk frm fry gla gle glg grc guj hat heb
    hin hrv hun hye iku ind isl ita ita_old jav jpn kan kat kat_old kaz khm
    kir kmr kor kor_vert lao lat lav lit ltz mal mar mkd mlt mon mri msa
    mya nep nld nor oci ori osd pan pol por pus que ron rus san sin slk
    slk_frak slv snd spa spa_old sqi srp srp_latn sun swa swe syr tam tat
    tel tgk tgl tha tir ton tur uig ukr urd uzb uzb_cyrl vie yid yor)

pkgname=tesseract
pkgver=5.0.1
pkgrel=2
pkgdesc="An OCR program"
arch=(x86_64)
url="https://github.com/tesseract-ocr/tesseract"
license=("APACHE")
depends=(libpng libtiff libjpeg-turbo zlib giflib gcc-libs curl libarchive leptonica)
makedepends=(icu cairo pango asciidoc)
install=tesseract.install
optdepends=('icu' 'cairo' 'pango'
            "${_langs[@]/#/tesseract-data-}")
provides=(libtesseract.so)
source=($pkgname-$pkgver.tar.gz::https://github.com/tesseract-ocr/tesseract/archive/$pkgver.tar.gz
        https://github.com/tesseract-ocr/tessdata/raw/bf82613055ebc6e63d9e3b438a5c234bfd638c93/osd.traineddata)
sha256sums=('b5b0e561650ed67feb1e9de38d4746121d302ae4c876c95b99b8b6f9f89d5c58'
            '9cf5d576fcc47564f11265841e5ca839001e7e6f38ff7f7aacf46d15a96b00ff')

build() {
  cd "$srcdir"/$pkgname-${pkgver}
  [ -x configure ] || ./autogen.sh
  [ -f Makefile ] || ./configure --prefix=/usr
  make
  make training
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" training-install
  mkdir -p "$pkgdir"/usr/share/tessdata
  install -Dm0644 "$srcdir"/osd.traineddata "$pkgdir"/usr/share/tessdata/osd.traineddata
}
