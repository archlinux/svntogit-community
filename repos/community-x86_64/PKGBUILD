# Maintainer: Christian Heusel <gromit@archlinux.org>
# Contributor: Ã©clairevoyant
# Contributor: nullgemm <nullgemm@mailbox.org>

pkgname=ly
pkgver=0.5.3
pkgrel=4
_tag=4094d2182a58795059cd3aa755f7b2975e1da861
pkgdesc="TUI display manager"
arch=(i686 x86_64 aarch64)
url="https://github.com/fairyglade/ly"
license=('custom:WTFPL')
depends=(pam libxcb)
makedepends=(git)
optdepends=('xorg-xauth: for X server sessions')
backup=(etc/$pkgname/{config.ini,wsetup.sh,xsetup.sh})
source=("git+$url.git#tag=$_tag"
        "git+https://github.com/nullgemm/argoat.git"
        "git+https://github.com/nullgemm/configator.git"
        "git+https://github.com/nullgemm/dragonfail.git"
        "git+https://github.com/nullgemm/termbox_next.git")
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP')

prepare() {
    cd "$pkgname"
    git submodule init

    git config submodule.sub/argoat.url "$srcdir/argoat"
    git config submodule.sub/configator.url "$srcdir/configator"
    git config submodule.sub/dragonfail.url "$srcdir/dragonfail"
    git config submodule.sub/termbox_next.url "$srcdir/termbox_next"

    git -c protocol.file.allow=always submodule update
}

build() {
    make -C "$pkgname"
}

package() {
    cd "$pkgname"
    # we install the binary as ly-dm because of the conflict with python-ly
    make DESTDIR="$pkgdir" NAME=ly-dm install
    sed -i "s;/usr/bin/ly;/usr/bin/ly-dm;g" "$pkgdir/usr/lib/systemd/system/ly.service"
    install -Dm644 license.md "$pkgdir/usr/share/licenses/$pkgname/WTFPL"
}
