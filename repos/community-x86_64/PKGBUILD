# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: yk <yk at archlinuxcn dot org>

pkgname=shadowsocks-qt5
pkgver=3.0.0
pkgrel=1
pkgdesc="A fast, reliable and cross-platform GUI fronted for Shadowsocks."
arch=("x86_64")
url="https://github.com/shadowsocks/shadowsocks-qt5"
license=("GPL")
depends=('qrencode' 'libqtshadowsocks' 'zbar')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/shadowsocks-qt5/archive/v$pkgver.tar.gz")
sha512sums=('0e51d5c64ca9ce77e5e2df38852b7e48fbeffec5c0977df9720d335e66b9d333917fa7278c4387204c667094a3a6830baf28603288aa3caa3e76c695379489a9')

prepare() {
  cd shadowsocks-qt5-$pkgver

  # Disable appindicator (was needed for Qt < 5.4.2)
  sed -i '/APPINDICATOR/d' CMakeLists.txt
}

build() {
  cd shadowsocks-qt5-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd shadowsocks-qt5-$pkgver
  make DESTDIR="$pkgdir" install
}
