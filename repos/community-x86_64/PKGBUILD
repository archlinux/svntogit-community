# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='psych'
pkgname="ruby-${_gemname}"
pkgver=4.0.4
pkgrel=2
pkgdesc="A YAML parser and emitter"
arch=('x86_64')
url="https://github.com/ruby/psych"
license=('MIT')
depends=('ruby' 'ruby-stringio')
makedepends=('ruby-bundler' 'ruby-rake' 'ruby-rake-compiler')
checkdepends=('ruby-test-unit')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c84c1048e31fdf45b546ec826e4abe5cce84b3906c4feda403ca2699b2a76952a24ce3bcdf84446d97d73b21dc95f5550525726291b62902e065645a68b45b47')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  rake build
}

check() {
  cd "${_gemname}-${pkgver}"

  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"
  local _platform="$(gem env platform | cut -d':' -f2)"
  local _extension_api_version="$(ruby -e'puts Gem.extension_api_version')"

  gem install --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "pkg/${_gemname}-${pkgver}.gem"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem" \
      ${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/psych/*.o \
      "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/psych/Makefile" \
      "${pkgdir}/${_gemdir}/extensions/${_platform}/${_extension_api_version}/${_gemname}-${pkgver}/gem_make.out"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 CONTRIBUTING.md README.md --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
