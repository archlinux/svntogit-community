# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=criterion
pkgname=haskell-criterion
pkgver=1.4.1.0
pkgrel=22
pkgdesc="Robust, reliable performance measurement and analysis"
url="http://www.serpentine.com/criterion"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-wl-pprint' 'haskell-base-compat-batteries'
         'haskell-cassava' 'haskell-code-page' 'haskell-exceptions' 'haskell-glob' 'haskell-js-flot'
         'haskell-js-jquery' 'haskell-microstache' 'haskell-mwc-random'
         'haskell-optparse-applicative' 'haskell-statistics' 'haskell-transformers-compat'
         'haskell-vector' 'haskell-vector-algorithms')
makedepends=('ghc' 'haskell-base-compat' 'haskell-hunit' 'haskell-quickcheck' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b3ea94b71789ae459f86d1161c6180a39a1b0cc3dd522115280d434bd1f970cd3c7e7395e23d893ce0724c7f04c47cf4e5e481eebbec885558d5887b7c0bff42')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/semigroups/d' -e 's/optparse-applicative >= 0.13,/optparse-applicative >= 0.13/' \
           -e 's/parsec >= 3.1.0/,parsec >= 3.1.0/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-fast -f-embed-data-files
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
