# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.27.1
pkgrel=3
pkgdesc="Curses-style terminal interface for the hledger accounting system"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-ansi-terminal' 'haskell-async'
         'haskell-breakpoint' 'haskell-cmdargs' 'haskell-data-default' 'haskell-doclayout'
         'haskell-extra' 'haskell-fsnotify' 'haskell-microlens' 'haskell-microlens-platform'
         'haskell-megaparsec' 'haskell-safe' 'haskell-split' 'haskell-text-zipper'
         'haskell-vector' 'haskell-brick' 'haskell-vty')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3044ce1a94d4741827924800714410abc7128bf780e180da670d13ce2fecb5e76959f5d4fea49f9d2eeab5ba48dbef9cb2843252e4373bd1de13e440f4e7fb4d')

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir=$pkgname --enable-tests \
            -fthreaded
    runhaskell Setup build $MAKEFLAGS
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE

    install -Dm644 hledger-ui.1 -t "$pkgdir"/usr/share/man/man1/
}
