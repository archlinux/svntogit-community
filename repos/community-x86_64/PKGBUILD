# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lv2lint
pkgver=0.8.0
pkgrel=1
pkgdesc="Check whether a given LV2 plugin is up to the specification"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/lv2lint/"
license=('Artistic2.0')
depends=('glibc' 'libelf')
makedepends=('curl' 'lilv' 'lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('3531f5cb6534b6fe79584a52c5fec0520a7bec5af95b48401c6bfe75409b9317ad9cc0ff873a849de9897bd35e1a076f664cd885443126e2c21d9a58b95d6891'
            'SKIP')
b2sums=('d56b3de13b6eae1f54403add02f30e2fa82edb2ccaef2f967268c78f3614dc9901a75ac444d38df53722f2ae2ea95db46b9635a3e6cc4d4226df245abbb41655'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        -Donline-tests=true \
        -Delf-tests=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  depends+=('libcurl.so' 'liblilv-0.so')
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir/" ninja -C build install
  install -vDm 644 {ChangeLog,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
