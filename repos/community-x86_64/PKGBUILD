# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ejabberd
pkgver=22.10
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64')
url="https://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite' 'gd')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
optdepends=('gsfonts: for captcha'
	    'imagemagick: for captcha')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"base64url::git+https://github.com/dvv/base64url#commit=bf1360685da704c8453aba2268c4698c959d2ba4"
	"cache_tab::git+https://github.com/processone/cache_tab#commit=6da609074b855b904ab72e201c60f1a1e2c00b3f"
	"eimp::git+https://github.com/processone/eimp#commit=ec616de3762ce8498856b4fdadf8ba36045c6773"
	"ejabberd_po::git+https://github.com/processone/ejabberd-po#commit=7a977ef321ad305f848920d42a257140c86f3537"
	"elixir::git+https://github.com/elixir-lang/elixir#commit=867da6f876c48279823a41a33030c95ef37379b2"
	"epam::git+https://github.com/processone/epam#commit=485aedb105c13c3c6763e5e5b86b9845cd4bec4a"
	"eredis::git+https://github.com/wooga/eredis#commit=9ad91f149310a7d002cb966f62b7e2c3330abb04"
	"esip::git+https://github.com/processone/esip#commit=d7a72805dc4b88b2a350ed7693324311c25f0df8"
	"ezlib::git+https://github.com/processone/ezlib#commit=af37eb27aa0614db8b89110a4b56806b7f3544c4"
	"fast_tls::git+https://github.com/processone/fast_tls#commit=5aa1e02d1e91155a7b0027abde1af94147c54b82"
	"fast_xml::git+https://github.com/processone/fast_xml#commit=26d3247e7b7ff94a3bdcd30bad1193e2d86f511b"
	"fast_yaml::git+https://github.com/processone/fast_yaml#commit=86c66abc4d3b0dcfdf6bd4f0c1a4b3f9609da02d"
	"idna::git+https://github.com/benoitc/erlang-idna#commit=6cff72747821110169ecfac871b0c69e5064afff"
	"jiffy::git+https://github.com/davisp/jiffy#commit=9ea1b35b6e60ba21dfd4adbd18e7916a831fd7d4"
	"jose::git+https://github.com/potatosalad/erlang-jose#commit=090a2ed054304ecc012d6c2d9d10d2a294d835b1"
	"luerl::git+https://github.com/rvirding/luerl#commit=836d08ad3287d73d9c91cb11a590de15a1761027"
	"mqtree::git+https://github.com/processone/mqtree#commit=b87975f2c46ea44f3ed0dd484ae248aeea0c5d1e"
	"p1_acme::git+https://github.com/processone/p1_acme#commit=ff64d95e403bb5eb1670af328359d3c4cc8410d0"
	"p1_mysql::git+https://github.com/processone/p1_mysql#commit=d07fbce20e51af5bfe3313224c615214aa7456e1"
	"p1_oauth2::git+https://github.com/processone/p1_oauth2#commit=e1fbe216c9e0c1f85b44bf9750af608fcc2848d2"
	"p1_pgsql::git+https://github.com/processone/p1_pgsql#commit=ac8a9f4b7d716ceac9fa22c4fa2862ce37f02cd8"
	"p1_utils::git+https://github.com/processone/p1_utils#commit=ba816751674ab7df46978345ec4c0c84c0b44b0d"
	"pkix::git+https://github.com/processone/pkix#commit=9655992dc39e91fc8f5e237eea95d74ec9c01891"
	"rebar_elixir_plugin::git+https://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"sqlite3::git+https://github.com/processone/erlang-sqlite3#commit=a68c21ff012142326584c7512a6084a1b61619cc"
	"stringprep::git+https://github.com/processone/stringprep#commit=11fe965934be988f5c6c9ded9affa870f3e5057a"
	"stun::git+https://github.com/processone/stun#commit=79dfaa0ff46ea71b448533e5da8d377c147c3a62"
	"unicode_util_compat::git+https://github.com/benoitc/unicode_util_compat.git#commit=38d7bc105f51159e8ea3279c40121db9db1e652f"
	"xmpp::git+https://github.com/processone/xmpp#commit=6265bf55c989a8eda9c6f6666555557faea72403"
	"yconf::git+https://github.com/processone/yconf#commit=b57afc24d6bb32b21bb70f792d5c4b49b4c68092"
	"$pkgname.logrotate"
	"sysuser.conf")
sha256sums=('8539707da7e75785444edf402b5ff60fc3f956806c341b2565702dcc77900402'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '31780cac78736d285e46f445f8c8463a70f2aeb2683280c259129db11832ddd2'
            '68de8fd4df6e4f0e21a241dcd2b7075bdff495876646dd726c7054bf5780e3c6')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in \
    base64url cache_tab eimp ejabberd_po elixir epam eredis esip ezlib fast_tls fast_xml fast_yaml idna jiffy jose luerl mqtree p1_acme p1_mysql p1_oauth2 p1_pgsql p1_utils pkix rebar_elixir_plugin sqlite3 stringprep stun unicode_util_compat xmpp yconf \
    ; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv "$srcdir"/$i "$srcdir/$pkgname-$pkgver/deps"
  done
  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  sed -i \
    -e "s|$pkgdir||g" \
    -e "s|Group=.*|Group=jabber|g" \
    -e "s|User=.*|User=jabber|g" \
    ejabberd.service
  sed -i -e "s|$pkgdir||g" "$pkgdir"/usr/bin/ejabberdctl

  install -Dm0644 ejabberd.service "$pkgdir"/usr/lib/systemd/system/ejabberd.service

  install -Dm04750 deps/epam/priv/bin/epam "$pkgdir"/usr/lib/ejabberd-$pkgver/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf
  install -Dm644 tools/ejabberdctl.bc "$pkgdir"/usr/share/bash-completion/completions/ejabberdctl

  # workaround
  ln -s mod_configure.beam "$pkgdir"/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f "$pkgdir"/usr/bin/{elixir,iex,mix}

  echo -e "\n\n# home dir workaround\nHOME=/var/lib/ejabberd" >>"$pkgdir"/etc/ejabberd/ejabberdctl.cfg

  # /usr/lib/ejabberd symlink follows to ejabberd version
  ln -s ejabberd-$pkgver "$pkgdir"/usr/lib/ejabberd

  # permissions
  chown -R 17:17 "$pkgdir"/var/{lib,log}/ejabberd
  chown root:17 \
    "$pkgdir"/etc/ejabberd/ejabberd.yml \
    "$pkgdir"/etc/ejabberd/ejabberdctl.cfg \
    "$pkgdir"/etc/ejabberd \
    "$pkgdir"/usr/lib/ejabberd/priv/bin/epam

  # fix eimp
  chmod a+x "$pkgdir"/usr/lib/eimp-*/priv/bin/eimp
}

# vim:set ts=2 sw=2 et:
