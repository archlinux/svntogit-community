# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>

pkgname=libdeflate
pkgver=1.11
pkgrel=1
pkgdesc='Heavily optimized library for DEFLATE/zlib/gzip compression and decompression'
arch=(x86_64)
url=https://github.com/ebiggers/libdeflate
license=(MIT)
depends=(glibc)
makedepends=(git)
provides=(libdeflate.so)
_tag=b01537448e8eaf0803e38bdba5acef1d1c8effba
source=(git+https://github.com/ebiggers/libdeflate.git#tag=${_tag})
b2sums=(SKIP)

build() {
  make PREFIX=/usr -C libdeflate
}

pkgver() {
  cd libdeflate
  git describe --tags | sed 's/^v//'
}

check() {
  make PREFIX=/usr check -C libdeflate
}

package() {
  make PREFIX=/usr DESTDIR="${pkgdir}" install -C libdeflate
  install -Dm 644 libdeflate/COPYING -t "${pkgdir}"/usr/share/licenses/libdeflate/
}

# vim ts=2 sw=2 et:
