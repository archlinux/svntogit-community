# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-btrees
pkgver=4.10.1
pkgrel=1
pkgdesc='A fancy and practical functional tools'
arch=('x86_64')
license=('ZPL')
url='https://github.com/zopefoundation/btrees'
depends=('python-persistent' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-transaction' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('91ba0572d49b675303d9881b8e2d2026494b0d6d32c4e167c6d859e07471a8bdd07d52d05f478211f6c6bb8993c7d6d7ce0aa949177b7a558275f4d672e08519')

build() {
  cd BTrees-$pkgver
  python setup.py build
}

check() {
  cd BTrees-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-cpython-310" python -m zope.testrunner --test-path=src
}

package() {
  cd BTrees-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
