# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=opencl-clang
pkgname=intel-${_srcname}
pkgver=10.0.0.1
pkgrel=1
pkgdesc="Wrapper library around clang that can compile OpenCL C kernels to SPIR-V modules"
arch=(x86_64)
url="https://github.com/intel/opencl-clang/"
license=(custom)
depends=(clang llvm-libs spirv-llvm-translator)
makedepends=(cmake llvm git)
source=("${url}/archive/v${pkgver/.1/-1}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('815005b6fb7fd8fd2f08b9036035d3aa9543f5ce8464e41ef2f2ed08a816f507')

build() {
    cmake -B build -S ${_srcname}-${pkgver/.1/-1} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLLVMSPIRV_INCLUDED_IN_LLVM=OFF \
        -DSPIRV_TRANSLATOR_DIR=/usr \
        -DLLVM_NO_DEAD_STRIP=ON \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 ${_srcname}-${pkgver/.1/-1}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
