# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=roc-toolkit
pkgver=0.2.2
pkgrel=1
pkgdesc="Real-time audio streaming over the network"
arch=(x86_64)
url="https://github.com/roc-streaming/roc-toolkit/"
license=(MPL2 custom:CC0)
depends=(glibc libunwind libuv)
makedepends=(alsa-lib cpputest gengetopt libpulse openfec ragel scons sox speexdsp)
optdepends=(
  'libpulse: for roc-conv, roc-receive and roc-send'
  'sox: for roc-conv, roc-receive and roc-send'
)
provides=(libroc.so)
source=(
  $url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz
  # remove unnecessary library dependencies from pkg-config integration: https://github.com/roc-streaming/roc-toolkit/pull/507
  $pkgname-0.2.1-pkgconfig_deps.patch
)
sha512sums=('b39a88811c3d7c8dfbaeb73f22d24715240c0e7ce0ae6b9148980a520efc0cf4939e1d461f05c7f00b15282756826c145a5560b2cbce8373ecc13b4ad93c47ee'
            '59ab91620a1c1a013f5f3da51d78d0e56df10fb5c3e21e156c0a86929652f7f9921798b2b8ada1ab8056109a9c30712e9dfa00f41697b3ca6d81e5ff92d0dfdf')
b2sums=('3f90b771e360bf1a6f2fe67fa5586076a16adb8ddf991190082de308fbef6a4d174f9072a83693195fbd08d9ab6b8d5e91916f7e8c9e77147119da80a8ed7792'
        '7cff5118b8773f3266d345f410308e110bd9c4615b230ce6586def53ea0028c6c417ba66bde61be472ac5ae7988eae25ffb858ce9d2b3431794c155d2149752d')

prepare() {
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.2.1-pkgconfig_deps.patch
}

build() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons "${scons_options[@]}"
}

check() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
    --enable-tests
    --enable-examples
  )

  cd $pkgname-$pkgver
  scons test "${scons_options[@]}"
}

package() {
  local scons_options=(
    --prefix=/usr
    --libdir=/usr/lib
  )
  depends+=(
    openfec libopenfec.so
    speexdsp libspeexdsp.so
  )

  cd $pkgname-$pkgver
  scons DESTDIR="$pkgdir/" "${scons_options[@]}" install

  install -vDm 644 3rdparty/hedley/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/hedley.COPYING"
}
