# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=megaglest
pkgver=3.13.0
pkgrel=5
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world"
arch=('x86_64')
url="https://megaglest.org"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl2' 'libvorbis' 'openal' 'libgl' 'lua51' 'icu' 'ftgl' 'glew'
         'libircclient' 'miniupnpc' 'wxgtk3' 'glu' 'libx11' 'libminiupnpc.so')
makedepends=('ftjam' 'cmake' 'mesa')
source=(https://github.com/MegaGlest/megaglest-source/releases/download/${pkgver}/megaglest-source-${pkgver}.tar.xz{,.asc})
sha256sums=('09fef67bc6878b1afbb0b403fec9fcf7dc270d500fdc62ed98e5f210e398ccb9'
            'SKIP')
validpgpkeys=('0DA321A44B30EF477E9A6DB8DE45EB9E71EEB59B')

build() {
  cd megaglest-${pkgver}

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-lX11 ${CXXFLAGS}" \
    -DWANT_GIT_STAMP=0 \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make VERBOSE=1
}

package() {
  cd megaglest-${pkgver}/build

  make DESTDIR="$pkgdir" install
}

# vim: sw=2:ts=2 et:
