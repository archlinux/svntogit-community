# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=entr
pkgver=4.9
pkgrel=1
pkgdesc='Run arbitrary commands when files change'
arch=('x86_64')
url='http://eradman.com/entrproject'
license=('MIT')
validpgpkeys=('519151D83E83D40A232B4D615C418B8631BC7C26' '5C418B8631BC7C26') # Eric Radman <ericshane@eradman.com>
source=("$url/code/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('b05595be5ae1c6fbd39ea886331b9b77563460d03eae8889c7ad0cc156a44c6f50477dc3448eefe715e124a79ab08b9baf7c53a9059dcf137e5782bf7b499751'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  export PREFIX='/usr'

  ./configure

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
