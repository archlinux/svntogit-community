# $Id$
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.7
pkgrel=3
pkgdesc='Lightweight and fast web browser'
arch=('x86_64')
url='http://www.netsurf-browser.org/'
license=('MIT' 'GPL2')
depends=('curl' 'desktop-file-utils' 'duktape>=2.2.0' 'gtk3' 'lcms'
         'libcss>=0.7.0' 'libdom>=0.3.2' 'libmng' 'libnsbmp>=0.1.4'
         'libnsgif>=0.1.4' 'libnsutils>=0.0.3' 'librsvg' 'libutf8proc>=2.1.1')
makedepends=('check' 'git' 'inetutils' 'netsurf-buildsystem' 'nsgenbind>=0.5'
             'perl-html-parser' 're2c' 'setconf')
# invalid HTTPS cert
source=("http://download.netsurf-browser.org/netsurf/releases/source-full/netsurf-all-$pkgver.tar.gz"
        'netsurf.sh')
sha256sums=('0b3e78f04728238e4867f2d6505522e777ebafcd0ed02e1e50860885e00a5879'
            '1f1b495af685e611fe4d121c6d48ed63d6dfe0c69bd5637c41cd71b42429fa85')

prepare() {
  cd "$pkgname-all-$pkgver/$pkgname"

  # Fix compilation issues
  sed -i 's:libutf8proc/::;s:UTF8PROC_CCC_VIRAMA:UTF8PROC_BIDI_CLASS_EN:' \
    utils/idna.c

  # Use "netsurf" as the name of the executable in /usr/bin
  setconf frontends/gtk/res/netsurf-gtk.desktop 'Exec=netsurf %u'

  # Set build configuration in Makefile.defaults
  for opt in \
    NETSURF_UA_FORMAT_STRING='"NetSurf/%d.%d (%s; Arch Linux)"' \
    NETSURF_USE_DUKTAPE=YES
  do
    setconf Makefile.defaults "$opt"
  done
}

build() {
  CFLAGS="$CFLAGS -w -Os -funroll-loops" make -C "$pkgname-all-$pkgver/$pkgname" \
    NETSURF_GTK_MAJOR=3 \
    TARGET=gtk \
    LIBDIR=lib \
    INCLUDEDIR=include \
    PREFIX=/usr
}

package() {
  make -C "$pkgname-all-$pkgver/$pkgname" \
    TARGET=gtk \
    NETSURF_GTK_MAJOR=3 \
    LIBDIR=lib \
    INCLUDEDIR=include \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    install

  # Launch script
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"

  cd "$pkgname-all-$pkgver/$pkgname"

  # Desktop icon and shortcut
  install -Dm644 frontends/gtk/res/$pkgname.xpm \
    "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 frontends/gtk/res/$pkgname-gtk.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
