# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname=libkkc
pkgver=0.3.4
pkgrel=1
pkgdesc="Japanese Kana Kanji conversion library"
arch=('i686' 'x86_64')
url="https://bitbucket.org/libkkc"
license=('GPL')
depends=('marisa' 'json-glib' 'libgee')
makedepends=('vala' 'gobject-introspection' 'intltool' 'python2-marisa')
optdepends=('sh: to use kkc-package-data')
source=(https://bitbucket.org/libkkc/libkkc/downloads/${pkgname}-${pkgver}.tar.gz)
sha512sums=('ec6afc488e8e176b7c180254a21097ba4de2091c1959f4088c01c915c641a7eca28bf709e65c2aeed9f2f327090125ae4b48d1b71a9e12cf7667947769537759')

build() {
  cd "${pkgname}-${pkgver}"

  PYTHON=/usr/bin/python2 \
  ./configure --prefix=/usr --disable-static --disable-silent-rules
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m 644 README data/rules/README.rules COPYING \
    "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"  
}

