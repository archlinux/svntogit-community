# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=universe-reverse-instances
pkgname=haskell-universe-reverse-instances
pkgver=1.1.1
pkgrel=126
pkgdesc="Instances of standard classes that are made possible by enumerations"
url="https://github.com/dmwit/universe"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-universe-base')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('c7f3d10a7b9be605bdc7e51b236240dce781e7674ded90e40998e71ac1765bc8b2c55271063c0e05ed033ea454a264da7fb1ff828283df504fc86ca52db0fda7')

prepare() {
  cd $_hkgname-$pkgver
  uusi -u universe-base $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver    

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
