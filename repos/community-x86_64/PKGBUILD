# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=5.6.3
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
depends=(accountsservice cinnamon-desktop libgnomekbd
         python-cairo python-gobject python-setproctitle python-xapp xapp xdotool)
optdepends=('cinnamon-translations: i18n')
makedepends=(gobject-introspection meson samurai)
backup=(etc/pam.d/cinnamon-screensaver)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('632bc9b518e59be7ce32c22ccfa32da060ea9edabe1683f91f2bf3c716fdcd7ca95fe4bda595ccc99821405bfbd987df2a416f229de56b3139f8d9975453ad39')
b2sums=('50728147fcb5849998113d5f19393e89f2cb4c5610359b81b42853fc2378814a52a8ed4f829cdd4e546a705a594da7b958ab25f69f996ee3d2204a64b7fdba90')

build() {
  arch-meson --libexecdir=lib/cinnamon-screensaver build ${pkgname}-${pkgver}
  samu -C build
}

package() {
  DESTDIR="${pkgdir}" samu -C build install
}
