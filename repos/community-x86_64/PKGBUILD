# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libinsane
pkgver=1.0.10
pkgrel=1
pkgdesc='Library to access scanners'
arch=('x86_64')
url='https://gitlab.gnome.org/World/OpenPaperwork/libinsane'
license=('LGPL3')
depends=('sane')
makedepends=('gobject-introspection' 'meson' 'vala')
checkdepends=('cunit')
source=("https://gitlab.gnome.org/World/OpenPaperwork/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('a9b8daaef03eb3bc4a8e638131e506ff3c2f99f715170da572e7f5ce7ba76467')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
