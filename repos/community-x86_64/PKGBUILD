# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: X0rg

pkgname=libcpuid
pkgver=0.5.1
pkgrel=2
pkgdesc="A small C library for x86 CPU detection and feature extraction"
arch=('x86_64')
url="http://libcpuid.sourceforge.net"
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'ninja' 'doxygen' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/anrieff/libcpuid/archive/v$pkgver.tar.gz")
sha512sums=('d725bc82e5aa2db69cea7b3590afbe11d6028cf4802fde8d498b4f1cc6d823e73d71b89a453d81bf996d97b36a71f6c00d0f6e8292b72f2be8a64b5d59c01861')
b2sums=('3471c11bbe4b0eaac7b00ac20a27556a95991f1b678269ae6f142a0fe2db066ebb756722ae2e76f95158742c93e29a35da6e735226d32b746034acae3ec2a5b7')

build() {
	cmake -S "$pkgname-$pkgver" -B build -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
	cmake --build build
}

check() {
  cd build
  ninja test-old
}

package() {
	DESTDIR="$pkgdir" cmake --install build

  # license
	install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/COPYING"
}
