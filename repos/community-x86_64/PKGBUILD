# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=libfm
pkgver=1.1.0
pkgrel=4
pkgdesc='Library for file management'
url='http://pcmanfm.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL')
groups=('lxde')
options=('!libtool')
install=libfm.install
depends=('gtk2' 'menu-cache' 'shared-mime-info' 'udisks' 'desktop-file-utils')
source=(http://downloads.sourceforge.net/pcmanfm/$pkgname-$pkgver.tar.gz)
md5sums=('a5bc8b8291cf810c659bfb3af378b5de')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-udisks \
    --with-gnu-ld 
  make LDFLAGS+=' -lgobject-2.0 -lglib-2.0'
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # Temporary fix to FS#32361
  rm $pkgdir/usr/include/libfm
  mv $pkgdir/usr/include/libfm-1.0/ $pkgdir/usr/include/libfm 
}
