# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org> 
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.4.5
pkgrel=1
pkgdesc="A tiling window manager initialy based on a dwm code rewriting."
arch=('i686' 'x86_64')
url="http://awesome.naquadah.org/"
source=($url/download/$pkgname-$pkgver.tar.bz2
	'awesome.desktop')
install=awesome.install
license=('GPL2')
depends=('pango' 'imlib2' 'lua' 'libev' 'dbus' 'libxdg-basedir' 'startup-notification')
makedepends=('docbook-xsl' 'asciidoc' 'xmlto' 'doxygen' 'gperf' 'cmake' 'luadoc' 'imagemagick')
options=('!libtool' 'docs')
sha1sums=('b069bc1cb865da1404999c0f3aa4fc6ff9e179e4'
          '896f3d70a03e51ef9d4d6121ce85bba9177b2fcb')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make CMAKE_ARGS=" -DPREFIX=/usr -DSYSCONFDIR=/etc \
	-DCMAKE_BUILD_TYPE=RELEASE" || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install || return 1

 # install desktop file so you can start awesome from your login manager
 install -Dm644 ${srcdir}/awesome.desktop \
        ${pkgdir}/usr/share/xsessions/awesome.desktop
}
 
