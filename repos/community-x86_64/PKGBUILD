# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: chiku <chirantan.mitra@gmail.com>

pkgname=ucblogo
pkgver=6.2.2
pkgrel=2
pkgdesc='Interpreter for the Logo programming language'
url='https://www.cs.berkeley.edu/~bh/logo.html'
arch=(x86_64)
license=(GPL)
makedepends=(git texinfo texlive-bin texlive-core)
depends=(libice libsm libx11 ncurses wxgtk3)
source=(configure.patch
        "$pkgname::git+https://github.com/jrincayc/ucblogo-code#commit=0ba6ad1629f94a89f98e2bf884c1388c6ee78b6d") # tag: version_6.2.2
b2sums=('609a8a2304d6867a83a581dfa8065c05c6847c0983a29fce39efdfc18cfecfeca54989937f472c4339b2bc7fa4bce51951083d2d48b33614bf751dc67abd0e61'
        'SKIP')

prepare() {
  cd $pkgname
  patch -p1 -i ../configure.patch
  autoreconf -i
}

build() {
  cd $pkgname
  ./configure \
    --enable-docs \
    --prefix=/usr \
    --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  make -C $pkgname \
    DESTDIR="$pkgdir" \
    INFODIR="$pkgdir/usr/share/info" \
    install
}
