# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>

pkgname=i3status
pkgver=2.12
pkgrel=1
pkgdesc='Generates status bar to use with i3bar, dzen2 or xmobar'
arch=('x86_64')
url='https://i3wm.org/i3status/'
license=('BSD')
groups=('i3')
depends=('wireless_tools' 'confuse' 'yajl' 'libpulse' 'libnl')
makedepends=('asciidoc')
backup=('etc/i3status.conf')
install=i3status.install
options=('docs')
source=("https://i3wm.org/i3status/$pkgname-$pkgver.tar.bz2"
        "https://i3wm.org/i3status/$pkgname-$pkgver.tar.bz2.asc")
md5sums=('ed839f1626781aaca236b46693fab975'
         'SKIP')
validpgpkeys=('424E14D703E7C6D43D9D6F364E7160ED4AC8EE1D') # Michael Stapelberg

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  make clean
}

# vim:set ts=2 sw=2 et:

