# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dhall
pkgver=1.41.2
pkgrel=42
pkgdesc='A configuration language guaranteed to terminate'
url='https://dhall-lang.org'
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-ansi-terminal'
         'haskell-atomic-write' 'haskell-base16-bytestring' 'haskell-case-insensitive'
         'haskell-cborg' 'haskell-cborg-json' 'haskell-contravariant' 'haskell-cryptohash-sha256'
         'haskell-cryptonite' 'haskell-data-fix' 'haskell-diff' 'haskell-dotgen' 'haskell-either'
         'haskell-half' 'haskell-hashable' 'haskell-http-client' 'haskell-http-client-tls'
         'haskell-http-types' 'haskell-indexed-traversable' 'haskell-lens-family-core'
         'haskell-megaparsec' 'haskell-mmorph' 'haskell-network-uri' 'haskell-optparse-applicative'
         'haskell-parsers' 'haskell-parser-combinators' 'haskell-prettyprinter'
         'haskell-prettyprinter-ansi-terminal' 'haskell-pretty-simple' 'haskell-profunctors'
         'haskell-repline' 'haskell-serialise' 'haskell-scientific' 'haskell-text-manipulate'
         'haskell-text-short' 'haskell-th-lift-instances' 'haskell-unordered-containers'
         'haskell-uri-encode' 'haskell-vector')
makedepends=('ghc' 'haskell-doctest' 'haskell-foldl' 'haskell-generic-random' 'haskell-mockery'
             'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-special-values'
             'haskell-spoon' 'haskell-system-filepath' 'haskell-tasty'
             'haskell-tasty-expected-failure' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-tasty-silver' 'haskell-temporary' 'haskell-turtle')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d2d1b0f513f02f8eb9bc25d0e01a1e749c09eb4089a0982b1ea20f18ff598932b62c898e764167f74718dc71aabe5760ce2e0abe0fe550cb7f1b3ac773219f9a')

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
