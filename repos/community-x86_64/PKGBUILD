# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.8.4
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="https://www.indilib.org/index.php?title=Main_Page"
license=(GPL2)
arch=(x86_64)
depends=(libnova cfitsio libusb libjpeg gsl libtheora fftw)
makedepends=(cmake boost qt5-base)
#source=("https://github.com/indilib/indi/releases/download/v$pkgver/libindi_$pkgver.tar.gz")
source=($pkgname-$pkgver.tar.gz::"https://github.com/indilib/indi/archive/v$pkgver.tar.gz")
sha256sums=('6e3b26fe6753ff5d97e69b4671d3f19d4c4a0f4e04ecac4a77d1ff834d59049f')

prepare() {
  mkdir -p build

  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i indi-$pkgver/drivers/*/*.rules
}

build() {
  cd build
  cmake ../indi-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DINDI_BUILD_QT5_CLIENT=ON \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
