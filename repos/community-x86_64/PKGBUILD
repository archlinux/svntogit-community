# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: eagletmt <eagletmt@gmail.com>
# Contributor: kusanaginoturugi <kusanaginoturugi@gmail.com>

pkgname=libskk
pkgver=1.0.1
pkgrel=3
pkgdesc='GObject-based library to deal with Japanese kana-to-kanji conversion method'
arch=('i686' 'x86_64')
url='https://github.com/ueno/libskk'
license=('GPL3')
depends=('libgee' 'json-glib')
makedepends=('intltool' 'git' 'gnome-common' 'gobject-introspection' 'vala')
source=(git+https://github.com/ueno/libskk.git#tag=$pkgver
        https://github.com/ueno/libskk/commit/024a258817ac98d28375ae4c7b4839b346919ce4.patch)
md5sums=('SKIP'
         'e784c8973f86e388b2d2fad8e4263166')

prepare() {
  cd $pkgname
  patch -p1 -i ../024a258817ac98d28375ae4c7b4839b346919ce4.patch
}

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
