# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-proxy
pkgver=4.5.0
pkgrel=3
pkgdesc="Generic Transparent Proxies"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
checkdepends=('python-zope-security' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.proxy/archive/$pkgver.tar.gz")
sha512sums=('68a0debe9c83dc7c0b05803cc2c9a24cfd2442c433e5404377c098844f1f285520a95d1319e05bf256f4b2d352784febdfb5ca96f8d78680f1abb1aefbb5e02d')

build() {
  cd "$srcdir"/zope.proxy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.proxy-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
