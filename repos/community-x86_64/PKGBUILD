# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Darkpelz <lukeh@outlook.my>
# Contributor: TheDarkBug <adrianoliviero23@gmail.com>

pkgname=uwufetch
pkgver=1.7
pkgrel=3
pkgdesc="A meme system info tool for Linux, based on nyan/uwu trend on r/linuxmasterrace."
arch=('x86_64')
url="https://github.com/TheDarkBug/$pkgname"
license=('GPL3')
depends=('glibc')
makedepends=("gcc")
optdepends=("viu: Display distro logos as images"
            "lshw: Better GPU detection")
source=("$pkgname-$pkgver.tar.gz::https://github.com/TheDarkBug/uwufetch/archive/$pkgver.tar.gz")
sha256sums=("6fa341b68d4c8c4bf4ff12d88ea0a81d35328992530d3747ba6b186095f09554")

build() {
    cd $pkgname-$pkgver
    make CC="cc $CFLAGS $LDFLAGS" build
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
