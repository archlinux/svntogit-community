# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-screensaver-pyro
epoch=1
pkgver=3.3.0
_codename=Matrix
pkgrel=2
pkgdesc="Pyro screensaver for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/screensaver.pyro'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-screensaver')
makedepends=('git' 'cmake' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/screensaver.pyro/archive/$pkgver-$_codename.tar.gz")
sha512sums=('e12ad6ba0d49f1e479da7d448646ff1d0bf5521d59c86b0652e03ee932e7de160d67d96d8d55a79830a9c04271f5d9a2f4f45cac98a1146265eb204e0b2d8826')

build() {
    cd "screensaver.pyro-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi')
    cd "screensaver.pyro-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

