# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgname=python-apsw
pkgver=3.38.5
pkgrel=1
pkgdesc="Python wrapper for SQLite"
arch=('x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('python' 'sqlite')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
sha512sums=('58331ca2db993f87316a55075ee9c440d4bd6e985e8ca11079391bd9cc2a907271ffe5c21d66840e336cd9e6568f6134c8b8fa5d28bbc7c77a1dddd48e8b99b7')
b2sums=('145e38d4057be498f85a655ad7a437ec19a895769f58505b4b99093be30e67498faf9572494009db8e20e6700fa5e514605af131d045a4e8bb41587b828f5a88')

build() {
  cd apsw-${pkgver}-r1

  python setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw-${pkgver}-r1

  gcc ${CFLAGS} ${CPPFLAGS} ${LDFLAGS} -fPIC -shared -o testextension.sqlext src/testextension.c
  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3*)
  PYTHONPATH="${pythonpaths[0]}" python setup.py test
}

package() {
  cd "$srcdir"/apsw-${pkgver}-r1

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
