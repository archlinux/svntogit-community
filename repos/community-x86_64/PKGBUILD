# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
pkgver=0.7
pkgrel=1
pkgdesc="A BibTeX editor for KDE"
arch=('x86_64')
url='https://userbase.kde.org/KBibTeX'
license=('GPL')
depends=('kdebase-runtime' 'poppler-qt4')
optdepends=('okular: Document preview')
makedepends=('cmake' 'automoc4' 'qca-qt4')
source=("https://download.kde.org/stable/KBibTeX/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('5d3acdd7cd6da9cb8db23e46cd02b8a6648e516330b169d09a6b6ffe226f9a96'
            'SKIP')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
