# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Keith Hughitt <khughitt@umd.edu>

# TODO: move python-xarray from AUR to community (with dependencies)
# TODO: package python-s3fs
# TODO: package pandas-gbq
# TODO: Feather Format support
# TODO: Apache Parquet support

pkgbase=python-pandas
pkgname=('python2-pandas' 'python-pandas')
pkgver=0.23.4
pkgrel=1
pkgdesc="Cross-section and time series data analysis toolkit"
arch=('x86_64')
url="http://pandas.pydata.org/"
license=('BSD')
makedepends=('python-setuptools' 'python-dateutil' 'python-numpy' 'python-pytz'
             'python2-setuptools' 'python2-dateutil' 'python2-numpy' 'python2-pytz'
             'cython' 'cython2')
source=("https://github.com/pydata/pandas/archive/v${pkgver}.tar.gz")
md5sums=('6ce319605a73db4ebd39021718f382c6')

prepare() {
  cd "$srcdir"
  cp -a pandas-${pkgver} pandas-py2-${pkgver}
  cd pandas-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/pandas-py2-${pkgver}
  python2 setup.py build_ext --inplace
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/pandas-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package_python2-pandas() {
  depends=('python2-numpy' 'python2-dateutil' 'python2-pytz' 'python2-setuptools')
  optdepends=('python2-pandas-datareader: pandas.io.data replacement (recommended)'
              'python2-numexpr: needed for accelerating certain numerical operations (recommended)'
              'python2-bottleneck: needed for accelerating certain types of nan evaluations (recommended)'
              'python2-scipy: needed for miscellaneous statistical functions'
              'python2-pytables: needed for HDF5-based storage'
              'python2-sqlalchemy: needed for SQL database support'
              'python2-matplotlib: needed for plotting'
              'python2-openpyxl: needed for Excel XLSX input/output'
              'python2-xlrd: needed for Excel XLS input'
              'python2-xlwt: needed for Excel XLS output'
              'python2-xlsxwriter: alternative Excel XLSX output'
              'python2-jinja: needed for conditional HTML formatting'
              'python2-blosc: for msgpack compression using blosc'
              'python2-pyqt5: needed for read_clipboard function (only one needed)'
              'pygtk: needed for read_clipboard function (only one needed)'
              'xsel: needed for read_clipboard function (only one needed)'
              'xclip: needed for read_clipboard function (only one needed)'
              'python2-backports.lzma: needed for xz compressed DataFrames in CSV'
              'python2-beautifulsoup4: needed for read_html function'
              'python2-lxml: needed for read_html function (and/or python2-html5lib)'
              'python2-html5lib: needed for read_hmlt function (and/or python2-lxml)')

  cd "$srcdir"/pandas-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-pandas() {
  depends=('python-numpy' 'python-dateutil' 'python-pytz' 'python-setuptools')
  optdepends=('python-pandas-datareader: pandas.io.data replacement (recommended)'
              'python-numexpr: needed for accelerating certain numerical operations (recommended)'
              'python-bottleneck: needed for accelerating certain types of nan evaluations (recommended)'
              'python-scipy: needed for miscellaneous statistical functions'
              'python-pytables: needed for HDF5-based storage'
              'python-sqlalchemy: needed for SQL database support'
              'python-matplotlib: needed for plotting'
              'python-openpyxl: needed for Excel XLSX input/output'
              'python-xlrd: needed for Excel XLS input'
              'python-xlwt: needed for Excel XLS output'
              'python-xlsxwriter: alternative Excel XLSX output'
              'python-jinja: needed for conditional HTML formatting'
              'python-blosc: for msgpack compression using blosc'
              'python-pyqt5: needed for read_clipboard function (only one needed)'
              'pyside2: needed for read_clipboard function (only one needed)'
              'xsel: needed for read_clipboard function (only one needed)'
              'xclip: needed for read_clipboard function (only one needed)'
              'python-beautifulsoup4: needed for read_html function'
              'python-lxml: needed for read_html function (and/or python-html5lib)'
              'python-html5lib: needed for read_hmlt function (and/or python-lxml)')

  cd "$srcdir"/pandas-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

