# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-rencode
pkgname=(python-rencode python2-rencode)
pkgver=1.0.5
pkgrel=2
pkgdesc="A Module similar to bencode from the BitTorrent project"
url="https://github.com/aresch/rencode"
license=('GPL')
arch=('x86_64')
makedepends=('cython' 'cython2')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/aresch/rencode/archive/v$pkgver.tar.gz")
sha512sums=('809d7070b746fe77419e7b4d2eed274e1e0b3b4f15bcddb466806478a90f89965b0b456443b390cce6d1ca9df409367f336dd659f4481cd442e3cc459bd63c18')

prepare() {
  cp -a rencode-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/rencode-$pkgver
  python setup.py build

  cd "$srcdir"/rencode-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/rencode-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.7" py.test

  cd "$srcdir"/rencode-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7" py.test2
}

package_python-rencode() {
  depends=('python')

  cd rencode-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-rencode() {
  depends=('python2')

  cd rencode-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
