# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=tensorboard
pkgver=1.12.2
pkgrel=2
pkgdesc="TensorFlow's Visualization Toolkit"
url='https://github.com/tensorflow/tensorboard'
arch=('x86_64')
license=('APACHE')
depends=('python' 'python-werkzeug' 'python-numpy' 'python-bleach' 'python-wheel' 'python-markdown' 'python-tensorflow-serving-api'
         'python-html5lib' 'python-protobuf' 'python-setuptools' 'python-tensorflow' 'python-grpcio')
makedepends=('bazel' 'tree')
source=("https://github.com/tensorflow/tensorboard/archive/${pkgver}.tar.gz"
        # Temp fix for https://github.com/tensorflow/tensorflow/issues/24124
        https://github.com/bazelbuild/bazel/releases/download/0.19.2/bazel-0.19.2-linux-x86_64)
sha512sums=('7fc10ae20f95730fbf35c1bb274c23bea96379344aff8ecacdfaaa2018295e20872f862e8d3870fde6f3b64ba27c1bf505769e8fbe0453886a92e704127a09e0'
            'bd87b7c8c517ff2c4783b7b09c4609e9979d79985ff8ed1d564a8f2f49ad8588bfb72eb5d20b3ecc6c7e7e20b64e79e3913c4705b14d7a330ffbc2a0e09f10c4')

build() {
  cd "$pkgname-$pkgver"

  chmod +x "${srcdir}"/bazel-0.19.2-linux-x86_64
  "${srcdir}"/bazel-0.19.2-linux-x86_64 build tensorboard:tensorboard
  "${srcdir}"/bazel-0.19.2-linux-x86_64 build //tensorboard/pip_package:build_pip_package

  mkdir pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/external pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/tensorboard pypackage
  cp tensorboard/pip_package/{README.rst,MANIFEST.in} pypackage
  cp README.md pypackage
  cp tensorboard/pip_package/setup.py pypackage
  cd pypackage
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd pypackage
  python setup.py install --root="$pkgdir" --optimize=1

  rm -r "$pkgdir"/usr/lib/python3.7/site-packages/external/

  _requires="$pkgdir"/usr/lib/python3.7/site-packages/tensorboard-${pkgver}-py3.7.egg-info/requires.txt
  # This is ugly: We need to patch it to use current bleach and html5lib
  # because old ones are not even compatible with Python 3.6 and we don't have
  # those in Arch of course.
  sed -i 's/bleach==.*/bleach/' ${_requires}
  sed -i 's/html5lib==.*/html5lib/' ${_requires}
  # Other ugly hack: workaround for issues 650
  # https://github.com/tensorflow/tensorboard/issues/650
  sed -i '/^futures/d' ${_requires}
}

# vim:set ts=2 sw=2 et:
