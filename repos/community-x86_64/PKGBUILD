# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=Allure
pkgname=allure
pkgver=0.11.0.0
pkgrel=61
pkgdesc="Near-future Sci-Fi roguelike and tactical squad combat game"
url="http://allureofthestars.com"
license=("AGPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-lambdahack' 'haskell-async' 'haskell-enummapset' 'haskell-file-embed'
         'haskell-hsini' 'haskell-optparse-applicative' 'haskell-primitive' 'haskell-splitmix'
         'haskell-th-lift-instances')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('6125cc585e2a5f28c88855c3c328385c1f21bed093d7606478f1b2af0cb2b6d6')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --datasubdir=allure --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -frelease -fsupportnodejs -fwith_costly_optimizations -f-with_expensive_assertions

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 COPYLEFT -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/COPYLEFT
}
