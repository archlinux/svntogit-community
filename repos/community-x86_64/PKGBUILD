# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=libhubbub
pkgver=0.3.5
pkgrel=1
pkgdesc='HTML5 compliant parsing library'
arch=('x86_64')
url='http://www.netsurf-browser.org/projects/hubbub/' # no https available
license=('MIT')
depends=('libparserutils>=0.2.4')
makedepends=('netsurf-buildsystem>=1.7')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz") # no https available
sha256sums=('aa82e36be124e582e307929eee81a42ffaf4355b2986934917271d65d20cd88f')

build() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE='lib-shared'
}

package() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE='lib-shared' \
    install

  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
