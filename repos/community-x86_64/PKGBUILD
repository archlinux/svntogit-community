# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=lapce
pkgver=0.1.2
pkgrel=1
pkgdesc='Lightning-fast and Powerful Code Editor'
arch=(x86_64)
url=https://lapce.dev
_url="https://github.com/$pkgname/$pkgname"
license=(Apache)
depends=(expat
         fontconfig
         freetype2
         gcc-libs
         gtk3
         libxcb
         libxkbcommon)
makedepends=(cargo
             python
             cmake)
options=('!lto')
_archive="$pkgname-$pkgver"
source=("$_url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('041cc835da6ee73425d150b8d95f136e107e9a2aaced30aaacd981700bee8a6e')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	cargo build --frozen --profile release-lto --all-features
}

package() {
	cd "$_archive"
    install -Dm0755 -t "$pkgdir/usr/bin/" "target/release-lto/$pkgname"{,-proxy}
	local lname=dev.lapce.lapce
	install -Dm0644 -t "$pkgdir/usr/share/applications/" extra/linux/$lname.desktop
	install -Dm0644 -t "$pkgdir/usr/share/metainfo/" extra/linux/$lname.metainfo.xml
	install -Dm0644 extra/images/logo.png "$pkgdir/usr/share/pixmaps/$lname.png"
}
