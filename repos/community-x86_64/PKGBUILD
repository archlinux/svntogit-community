# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

pkgname=rawtherapee
pkgver=4.2
pkgrel=2
epoch=1
pkgdesc="Raw photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL3')
depends=('fftw' 'gtkmm' 'libcanberra' 'libiptcdata' 'lcms2' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('gtk-engines: workaround theme bug under GNOME')
makedepends=('cmake' 'mercurial')
install=rawtherapee.install
source=("http://rawtherapee.com/shared/source/rawtherapee-${pkgver}.tar.xz")
md5sums=('e6510ed56fdc35aa712b4c0f54c52ac0')
#source=("rawtherapee-$pkgver::hg+https://code.google.com/p/rawtherapee/#tag=$pkgver")
#md5sums=('SKIP')

build() {
  mkdir "$srcdir/$pkgname-build"
  cd "$srcdir/$pkgname-build"

  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/$pkgname-build"
  make DESTDIR="$pkgdir/" install
}
