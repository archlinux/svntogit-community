# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=python-blosc
pkgver=1.11.0
pkgrel=1
pkgdesc='Python wrapper for the extremely fast Blosc compression library'
arch=('x86_64')
url='http://python-blosc.blosc.org/'
license=('BSD')
depends=('python' 'blosc')
makedepends=('python-scikit-build' 'ninja')
checkdepends=('python-numpy' 'python-psutil')
optdepends=('python-numpy: for tests and benchmarks')
source=("https://github.com/Blosc/python-blosc/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('94c6f470b9f6e8d1fec0e44709e84582cd595291dddcd07ea174665a6fc12e029c6f0c781c8e506e95d4f9ee9fc27cbbfb5bc2789f341df3e84f342626dcfdb0')
b2sums=('6bc5dda4e40ceb9770dd5b6e6db1fced42e257475b220bcb530c8b291bf132f61013ece5934a7d47778fdac716fcea0e6d57b7543b1cc76ccd95201cad072bdd')

build() {
  cd "${pkgname}-${pkgver}"

  export INCLUDE_SNAPPY=1 # Disabled by default for compatibility with non-C++ systems
  python setup.py build \
    --build-type none \
    -DUSE_SYSTEM_BLOSC=ON \
    -DCMAKE_C_FLAGS_INIT=-DNDEBUG
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  local python_version_truncated=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')

  cd "${pkgname}-${pkgver}/_skbuild/linux-${CARCH}-${python_version}/setuptools/lib.linux-${CARCH}-cpython-${python_version_truncated}"

  PYTHONPATH="${PWD}:$PYTHONPATH" python -m blosc.test
}

package() {
  cd "${pkgname}-${pkgver}"

  python setup.py --skip-cmake install --skip-build --root="${pkgdir}" --optimize=1

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}
