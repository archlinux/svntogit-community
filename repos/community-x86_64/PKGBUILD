# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Steven Honeyman <stevenhoneyman at gmail com>

pkgname=flite
pkgver=2.1
pkgrel=1
pkgdesc="A lightweight speech synthesis engine"
arch=(x86_64)
url="http://cmuflite.org"
license=(custom)
depends=(alsa-lib)
makedepends=(chrpath)
source=(http://festvox.org/flite/packed/flite-$pkgver/flite-$pkgver-release.tar.bz2 flite-ldflags.patch)
sha256sums=('c73c3f6a2ea764977d6eaf0a287722d1e2066b4697088c552e342c790f3d2b85'
            'ff43e11241c9aea26483865c672c20421d12c688ae8b59b39471bafb52c1463e')

prepare() {
  cd $pkgname-$pkgver-release
  sed '/^#VOXES.*$/d; s/+//g; s/cmu_indic_lex/&\nVOXES = cmu_us_kal16 cmu_us_slt/' config/android.lv >config/archlinux.lv
  sed -i '/$(INSTALL) -m 755 $(BINDIR)\/flite_time $(DESTDIR)$(INSTALLBINDIR)/d' main/Makefile

# Fix linking (Gentoo)
  patch -p1 -i ../flite-ldflags.patch
}

build() {
  cd $pkgname-$pkgver-release
  ./configure --prefix=/usr --enable-shared \
			    --with-audio=alsa \
			    --with-vox=cmu_us_kal16 \
			    --with-langvox=archlinux
  make
}

package() {
  cd $pkgname-$pkgver-release
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

# Fix rpath
  chrpath -d "$pkgdir"/usr/bin/*
}
