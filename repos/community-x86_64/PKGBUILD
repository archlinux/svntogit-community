# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=netCDF4
_mpi=openmpi
pkgname=python-${_pkg,,}-${_mpi}
pkgver=1.6.2
pkgrel=1
pkgdesc="Python/NumPy interface to the netCDF C library (${_mpi} version)"
arch=(x86_64)
url="https://unidata.github.io/${_pkg,,}-python"
license=(MIT)
depends=(python-numpy python-cftime python-mpi4py "netcdf-${_mpi}")
makedepends=(cython python-setuptools)
checkdepends=(openssh)
conflicts=(python-netcdf4)
provides=(python-netcdf4)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('0382b02ff6a288419f6ffec85dec40f451f41b8755547154c575ddd9f0f4ae53')

build() {
  cd ${_pkg}-${pkgver}
  USE_NCCONFIG=1 python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}/test
  export OMPI_MCA_opal_warn_on_missing_libcuda=0
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  NO_NET=1 PYTHONPATH="../build/lib.linux-${CARCH}-cpython-${python_version/./}" python -B ./run_all.py
}

package() {
  cd ${_pkg}-${pkgver}
  USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
