# Maintainer: Frederik Schwan <freswa at archlinux dot org>

pkgname=libomemo
pkgver=0.7.1
pkgrel=2
pkgdesc='Implementation of OMEMO (XEP-0384) in C'
arch=('x86_64')
url='https://github.com/gkdr/libomemo'
license=('GPL3')
depends=('sqlite' 'libgcrypt' 'mxml' 'glib2')
provides=('libomemo.so')
source=("https://github.com/gkdr/libomemo/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('a3909650e399075f62026bd048a667a276a6cd7197ac18ddf9e3c12f57e5fd9cb2f5532ba69d0836af87e1415a5ffdc1746cf90a596dcf255265a64e921f039d')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX="/usr"
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  chmod a+x "${pkgdir}"/usr/lib/libomemo.so.${pkgver}
  ln -sf libomemo.so.${pkgver} "${pkgdir}"/usr/lib/libomemo.so
  ln -sf libomemo.so.${pkgver} "${pkgdir}"/usr/lib/libomemo.so.${pkgver%%.*}
}
