# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=gwenhywfar
pkgver=4.18.0
_dnrel=206
pkgrel=3
pkgdesc="OS abstraction functions for various projects"
arch=(x86_64)
url="http://www.aquamaniac.de"
license=("LGPL")
depends=('openssl' 'gnutls' 'libgcrypt' 'libice' 'libsm')
makedepends=('gtk2' 'qt5-base')
optdepends=('gtk2: for the GTK2 UI' 'qt5-base: for the Qt5 UI')
source=(gwenhywfar-$pkgver.tar.gz::"http://www2.aquamaniac.de/sites/download/download.php?package=01&release=${_dnrel}&file=01&dummy=gwenhywfar-$pkgver.tar.gz"
  $pkgname-$pkgver.tar.gz.asc::"https://www.aquamaniac.de/sites/download/download.php?package=01&release=${_dnrel}&file=02&dummy=gwenhywfar-$pkgver.tar.gz.asc")
sha256sums=('6915bba42d8b7f0213cee186a944296e5e5e97cdbde5b539a924261af03086ca'
            'SKIP')
validpgpkeys=(42400AF5EB2A17F0A69BB551E9899D784A977416) # AqBanking Package Key <packages@aqbanking.de>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --enable-ssl --enable-system-certs --with-guis="gtk2 qt5"
  sed -i 's|gnutls_transport_set_lowat||g' src/sio/syncio_tls.c
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
