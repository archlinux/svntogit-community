# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pyname=cvxpy
pkgname=python-$_pyname
pkgver=1.3.1
pkgrel=1
pkgdesc='A Python-embedded modeling language for convex optimization problems'
url='https://www.cvxpy.org/'
license=(Apache)
arch=(x86_64)
depends=(python-scipy python-ecos python-scs python-osqp)
makedepends=(python-build python-installer python-setuptools python-wheel)
optdepends=('python-cylp: CBC solver'
            'python-cvxopt: GLPK solver'
            'python-pyscipopt: SCIP solver')
checkdepends=(python-pytest python-ecos python-scs python-osqp)
source=(https://github.com/cvxpy/cvxpy/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('4a29003de4828ecbd87044e550fea448b410fc7ce485f1e0fa3eb9cd61ed8777')

prepare() {
  sed -e 's|,<1.24||' -i $_pyname-$pkgver/pyproject.toml # Drop version constraints
}

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m pytest -v -k 'not test_scipy_mi_lp_2'
}

package() {
    cd $_pyname-$pkgver
    python -m installer --destdir="$pkgdir" dist/*.whl
}
