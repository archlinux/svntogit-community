# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Orivej Desh <smpuj@bk.ru>

_dir=2691
pkgname=vamp-plugin-sdk
pkgver=2.10.0
pkgrel=2
pkgdesc="The Vamp audio analysis plug-in system"
arch=(x86_64)
url="https://www.vamp-plugins.org/"
license=(BSD)
groups=(pro-audio)
depends=(
  gcc-libs
  glibc
)
makedepends=(libsndfile)
optdepends=('libsndfile: for vamp-simple-host')
provides=(
  libvamp-hostsdk.so
  libvamp-sdk.so
)
source=(https://code.soundsoftware.ac.uk/attachments/download/$_dir/$pkgname-$pkgver.tar.gz)
sha512sums=('153b7f2fa01b77c65ad393ca0689742d66421017fd5931d216caa0fcf6909355fff74706fabbc062a3a04588a619c9b515a1dae00f21a57afd97902a355c48ed')
b2sums=('50ad1c69d497b17c03439d94be7b3f70071de0c34c02c597506290fbd0e0cd2632f0a543d59468923a72ac995b7e44ecdace7c30e1896b2d08b8b98ec9d38245')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  # license
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  # docs
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
