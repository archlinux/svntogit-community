# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Marcel Korpel <marcel[dot]korpel[at]gmail>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=ctemplate
pkgver=2.3
pkgrel=5
pkgdesc='A library implementing a simple but powerful template language for C++'
arch=('x86_64')
url='https://github.com/olafvdspek/ctemplate'
license=('BSD')
makedepends=('python2')
depends=('gcc-libs' 'perl')
source=("https://github.com/OlafvdSpek/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('99e5cb6d3f8407d5b1ffef96b1d59ce3981cda3492814e5ef820684ebb782556')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

	# use python2
	sed -i '1s/python/&2/' \
		src/htmlparser/fsm_config.py \
		src/htmlparser/generate_fsm.py
}

build() {
	cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
