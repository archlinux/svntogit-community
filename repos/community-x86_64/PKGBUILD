# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=spire
pkgname=("spire-agent" "spire-server")
pkgver=1.6.3
pkgrel=1
pkgdesc="SPIFFE Runtime Environment"
url='https://github.com/spiffe/spire'
arch=("x86_64")
license=("Apache")
makedepends=("go")
depends=("glibc")
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/spiffe/spire/archive/v${pkgver}.tar.gz")
sha512sums=('68140c66fd2410a299b637f6fbac1573756c505baa9bad6f685f5a729fbfdd0efc18db4c1e9569c3d6cff36a4bd60337301543c7c76b76e89511df8e0db74e3c')
b2sums=('514cefd45718479f68a429517bd347856437ab5419041ca5fa53eac579d1e42faa01847866cf36968ddaf753048bef5e8939ceb3a9f0032fcc83fe917f0c2012')

build() {
  cd "${pkgbase}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  cd cmd/spire-agent/
  go build -o spire-agent .
  cd ../../cmd/spire-server
  go build -o spire-server .
}

check() {
  cd "${pkgbase}-${pkgver}"
  # test is blocking for some reason..
  # go test -v ./...
}

package_spire-agent() {
  pkgdesc="SPIFFE runtime environment (agent)"
  provides=("spire-agent")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-agent/spire-agent "${pkgdir}/usr/bin/spire-agent"
}

package_spire-server() {
  pkgdesc="SPIFFE runtime environment (server)"
  provides=("spire-server")
  depends=("glibc")
  cd "${pkgbase}-${pkgver}"
  install -Dsm755 cmd/spire-server/spire-server "${pkgdir}/usr/bin/spire-server"
}
