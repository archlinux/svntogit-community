# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=passenger
pkgname=(passenger mod_passenger nginx-mod-passenger)
pkgver=5.3.4
pkgrel=1
_nginxver=1.14.0
pkgdesc="Fast and robust web server and application server for Ruby, Python and Node.js"
arch=('x86_64')
url="https://www.phusionpassenger.com"
_watch="https://github.com/phusion/passenger/releases"
license=('MIT')
makedepends=('ruby-rake' 'ruby-rack' 'curl' 'apache')
options=('!emptydirs' 'staticlibs')
validpgpkeys=('D5F0851426939232F437AB722AC745A50A212A8C'
              '16378A33A6EF16762922526E561F9B9CAC40B2F7'
              'B0F4253373F8F6F510D42178520A9993A1C052F8') # Maxim Dounin <mdounin@mdounin.ru>
source=(https://s3.amazonaws.com/phusion-passenger/releases/passenger-$pkgver.tar.gz{,.asc}
        https://nginx.org/download/nginx-$_nginxver.tar.gz{,.asc})
sha256sums=('516a9f6d8df1cdb3b1beb6b5463c9454c5816ef732d1948c4482315664cfc944'
            'SKIP'
            '5d15becbf69aba1fe33f8d416d97edd95ea8919ea9ac519eff9bafebb6022cb5'
            'SKIP')

build(){
  cd "$srcdir"/nginx-$_nginxver
  ./configure --with-compat --add-dynamic-module=../$pkgbase-$pkgver/src/nginx_module
  make modules

  cd "$srcdir"/$pkgbase-$pkgver
  rake nginx CACHING=false
  ./bin/passenger-install-apache2-module -a
}

package_passenger() {
  cd "$srcdir"/passenger-$pkgver
  depends=('ruby' 'ruby-rack' 'curl' 'libnsl')

  mkdir -p "$pkgdir"/usr/lib/passenger/
  cp -R * "$pkgdir"/usr/lib/passenger/

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mod_passenger() {
  pkgdesc="Passenger apache module"
  depends=('passenger' 'apache')
  install=passenger.install

  cd "$srcdir"/passenger-$pkgver

  mkdir -p "$pkgdir"/usr/lib/httpd/modules
  ln -sf /usr/lib/passenger/buildout/apache2/mod_passenger.so "$pkgdir"/usr/lib/httpd/modules/mod_passenger.so

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_nginx-mod-passenger() {
  pkgdesc="Passenger nginx module"
  depends=('passenger' "nginx=$_nginxver")

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$srcdir"/nginx-$_nginxver/objs
  for mod in ngx_*.so; do
    install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
  done

  install -Dm644 "$srcdir"/$pkgbase-$pkgver/LICENSE \
                 "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
