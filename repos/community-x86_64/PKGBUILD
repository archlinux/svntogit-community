# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=python-pytables
pkgver=3.6.1
pkgrel=10
pkgdesc="A package for managing hierarchical datasets and designed to efficiently and easily cope with extremely large amounts of data"
arch=(x86_64)
url="https://www.pytables.org"
license=('BSD')
depends=(blosc lzo hdf5 python-numexpr)
makedepends=(python-setuptools cython)
source=(https://github.com/PyTables/PyTables/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        pytables-tostring-tobytes.patch::https://patch-diff.githubusercontent.com/raw/PyTables/PyTables/pull/810.patch
        pytables-numpy.object.patch::https://github.com/PyTables/PyTables/pull/862/commits/e8d622305ba30762a0ffa3f5b1fcf105a278f255.patch)
sha256sums=('4cea86bab5bcb5423a07c7951b8c65e24b674e0dcec0e448d434829eff5f18d0'
            '40f1c5be09d02f1d7fc6f43264019249ee62b516b249f74abcc75296424f553c'
            'aea8022888052ab3c8081fd9c2fd157f7bc6687a3a7c380cbfd178804ba57bb9')

prepare() {
  cd PyTables-${pkgver}
  patch -p1 < ../pytables-tostring-tobytes.patch
  patch -p1 < ../pytables-numpy.object.patch
  sed -i "s|Atom.from_sctype('Float64')|Atom.from_sctype('float64')|" tables/atom.py
}

build() {
  cd PyTables-${pkgver}
  python setup.py build --blosc=/usr --lzo=/usr --hdf5=/usr --bzip2=/usr
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd PyTables-${pkgver}/build/lib.linux-${CARCH}-${python_version}
  # Currently 12 errors in VLArray https://github.com/PyTables/PyTables/issues/845 
  PYTHONPATH="." python -m tables.tests.test_all || echo "Tests failed"
}

package() {
  cd PyTables-${pkgver}
  python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
