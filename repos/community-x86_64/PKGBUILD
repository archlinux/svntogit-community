# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: TDY <tdy@gmx.com>

pkgname=parole
pkgver=0.5.0
pkgrel=1
pkgdesc="A modern media player based on the GStreamer framework"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/parole/"
license=('GPL')
depends=('gstreamer0.10-base>=0.10.11' 'hicolor-icon-theme' 'libnotify>=0.4.1'
         'libxfce4ui>=4.7.0' 'taglib>=1.4' 'desktop-file-utils')
makedepends=('pkgconfig>=0.9.0' 'xfce4-dev-tools')
optdepends=('xulrunner>=1.9.1: browser plugin')
conflicts=('parole-media-player-git')
options=('!libtool')
install=parole.install
#source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
source=(http://git.xfce.org/apps/parole/snapshot/parole-$pkgver.tar.bz2)
md5sums=('03eb2df373adaae56959a548553ef72e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|AM_CONFIG_HEADER|AC_CONFIG_HEADER|g' configure.ac.in

  [ -x configure ] || ./autogen.sh

  ./configure --prefix=/usr --libexecdir=/usr/lib --enable-taglib --enable-libnotify
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
