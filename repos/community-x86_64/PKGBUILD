# $Id$
# Maintainer: Johannes Löthberg <johannes@kyrisis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Christopher Reimer <mail+aur@c-reimer.de>

pkgname=cargo
pkgver=0.19.0
pkgrel=1

pkgdesc='Rust package manager'
url='http://crates.io/'
arch=('x86_64' 'i686')
license=('APACHE' 'MIT' 'custom')

depends=('curl' 'rust')
makedepends=('git' 'python' 'cmake' 'cargo')
optdepends=('gcc: for compiling C source code with gcc'
            'clang: for compiling C source code with clang')

options=('!emptydirs')

source=("git+https://github.com/rust-lang/cargo.git#tag=$pkgver"
        "git+https://github.com/rust-lang/rust-installer.git")

md5sums=('SKIP'
         'SKIP')

prepare() {
  cd cargo

  git submodule init
  git config submodule.src/rust-installer.url "$srcdir"/rust-installer
  git submodule update

  sed 's^share/doc^share/licenses^g' -i Makefile.in
}

build() {
  cd cargo

  ./configure \
    --prefix=/usr \
    --release-channel=stable \
    --enable-optimize
  make
}

package() {
  cd cargo

  make DESTDIR="$pkgdir" install

  # Remove files that contains references to $srcdir or $pkgdir,
  # or that conflicts with the rust package.
  rm -f "$pkgdir/usr/lib/rustlib/"{install.log,manifest-cargo,uninstall.sh}

  install -d "$pkgdir/usr/share/bash-completion/completions"
  mv "$pkgdir/usr/etc/bash_completion.d/cargo" \
    "$pkgdir/usr/share/bash-completion/completions/cargo"

  for f in LICENSE-APACHE LICENSE-MIT LICENSE-THIRD-PARTY; do
    install -Dm644 "$f" "$pkgdir/usr/share/licenses/$pkgname/$f"
  done
}

# vim:set ts=2 sw=2 et:
