# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=dsdcc
pkgver=1.9.3
pkgrel=1
pkgdesc='Digital Speech Decoder (DSD) rewritten as a C++ library'
arch=('x86_64')
url='https://github.com/f4exb/dsdcc'
license=('GPL3')
depends=('mbelib' 'serialdv')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('c62baa022526053d99bcc64ed4a5dcdaa5c82fb82357639d4eb4c1d99cf704050faa55a6de155eed44b76c0f0c37fd65c8a038ee03dd074251f06aa0c5620c01')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|LIBSERIAL_NAMES|LIBSERIALDV_NAMES|g' cmake/Modules/FindSerialDV.cmake
}

build() {
  mkdir -p "$srcdir"/$pkgname-$pkgver/build
  cd "$srcdir"/$pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLIBSERIALDV_INCLUDE_DIR=/usr/include/serialdv \
    -DUSE_MBELIB=ON

  make

  # Build tests
  cd "$srcdir"/$pkgname-$pkgver/testfec

  make
}

check() {
  cd $pkgname-$pkgver/testfec

  ./qr
  ./golay20
  ./golay23
  ./golay24
  ./hamming7
  ./hamming12
  ./hamming15
  ./hamming16
  ./viterbi
  ./viterbi35
  ./crc
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}

