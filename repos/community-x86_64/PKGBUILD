# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=yggdrasil
pkgver=0.3.13
pkgrel=1
pkgdesc="An experiment in scalable routing as an encrypted IPv6 overlay network"
arch=('x86_64')
url="https://github.com/yggdrasil-network/yggdrasil-go"
license=('LGPL3')
depends=('glibc')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/yggdrasil-network/yggdrasil-go/archive/v${pkgver}.tar.gz"
        'yggdrasil.sysusers')
sha512sums=('d3a4d4de62bbcb910a4de179e571b1ce11345560728e2b9b1fe30df12aee04703de832b5d6065d52c247fd5e2e79a2a9a5696d948622ffb2cd8bd861825c7dd5'
            'b78d1f5efeeba184588ba7bdb2249d976aec160daa59742e032983da1aedad062d15c7c97cba3eba69412a0f7904ee123d98b58f859892d71188c25624295c32')
b2sums=('93cf134cdf5ef1467ab897b5c85f38bef1de3e5759ad9d66fa139f708a3139c3dabca52f6d0bac58635015cc4618016cb6ed20ad49c4ce56b7df27511834b520'
        '3cf5bfb3c09908f841b39f7206bb472e50e60f10d1b87ab6def97a7212e27627f1e795bbaffd964d2d27a80fbbd1a3be63d63c59793ec1967c02f48b8462c08b')

build() {
  cd "${pkgname}-go-${pkgver}"
  PKGNAME="${pkgname}" PKGVER="${pkgver}" \
    CGO_LDFLAGS="${LDFLAGS}" \
    GOFLAGS="-trimpath" \
    ./build
}

package() {
  cd "${pkgname}-go-${pkgver}"
  install -Dm755 "yggdrasil" "${pkgdir}/usr/bin/yggdrasil"
  install -Dm755 "yggdrasilctl" "${pkgdir}/usr/bin/yggdrasilctl"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 contrib/systemd/yggdrasil.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm644 "${srcdir}/yggdrasil.sysusers" "${pkgdir}/usr/lib/sysusers.d/yggdrasil.conf"
}

# vim: ts=2 sw=2 et:
