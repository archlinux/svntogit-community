# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.0.4
_golibver=0.4.9
_deepinapiver=3.0.7
_dbusfactoryver=3.0.3
_girgeneratorver=0.9.3
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('gcc-libs>=5.3.0-3' 'deepin-daemon' 'deepin-wm-switcher')
makedepends=('cmake' 'coffee-script' 'deepin-dbus-generator' 'deepin-gir-generator' 'gcc-go' 'python2' 'webkitgtk' 'git')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/startdde.git#tag=$pkgver"
        "git+https://cr.deepin.io/go-lib.git#tag=$_golibver"
        "git+https://cr.deepin.io/dde/dde-api.git#tag=$_deepinapiver"
        "git+https://cr.deepin.io/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://cr.deepin.io/go-gir-generator.git#tag=$_girgeneratorver")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i "s|\"\${GOLDFLAGS}\"|\"\${GOLDFLAGS} $CFLAGS $LDFLAGS\"|" startdde/Makefile
  sed -i 's| generator$||' go-gir-generator/makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/go-gir-generator"
  make GENERATOR=gir-generator build
  make copyfile
  cp -r out/src/gir "$GOPATH/src"/

  cd "$srcdir/startdde"
  make USE_GCCGO=1
}

package() {
  cd startdde
  make DESTDIR="${pkgdir}" install
}
