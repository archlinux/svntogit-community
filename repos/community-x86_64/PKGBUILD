# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongo-c-driver
pkgver=1.20.0
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='https://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libsasl' 'icu' 'snappy' 'zlib' 'zstd')
makedepends=('cmake')
provides=('libbson' 'libmongoc')
conflicts=('libbson' 'libmongoc')
replaces=('libbson' 'libmongoc')
source=("https://github.com/mongodb/mongo-c-driver/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('acaf432d28cf8f507e893c9b0c1561035a73187e03630666703a8cfdc55d731347af8f37ea4a961149be1ebd806c2fd92a752fa443ce46b8aa8d8436c9f1ed15')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../mongo-c-driver-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_STATIC=OFF -DENABLE_TESTS=OFF
    # Test profiles are not found even with ENABLE_TESTS=ON
  make
}

check() {
  cd build
  # No test profile found
  # pifpaf run mongodb --port 27017 make test || echo "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
