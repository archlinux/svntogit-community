# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Lev Lybin <lev.lybin@gmail.com>
# Contributor: Vadym Abramchuk <abramm@gmail.com>
# Contributor: John Gerritse <reaphsharc@gmail.com>

pkgname=gearmand
pkgver=1.1.11
pkgrel=1
pkgdesc="Distributed job queue server."
arch=('i686' 'x86_64')
depends=('libevent' 'boost-libs' 'libmariadbclient' 'postgresql-libs' 'sqlite')
makedepends=('boost' 'gperf' 'python-sphinx')
conflicts=('gearman')
replaces=('gearman')
provides=("gearman=$pkgver")
url="http://gearman.org/"
license=('CUSTOM')
install=$pkgname.install
backup=('etc/gearmand.conf')
source=(https://launchpad.net/$pkgname/1.2/$pkgver/+download/$pkgname-$pkgver.tar.gz
        gearmand.service
        gearmand.conf)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export LDFLAGS="$LDFLAGS -lboost_system"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/run \
    --disable-static \
    --enable-ssl
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cd "$srcdir"
  install -Dm644 gearmand.service "$pkgdir/usr/lib/systemd/system/gearmand.service"
  install -Dm644 gearmand.conf "$pkgdir/etc/gearmand.conf"
}

sha256sums=('a175cbdeabe8102796418a516532adad58abd5e355365c78b6a234e544655825'
            'dee38ad4e614969047253b8c4996b8cdf33c981b0df215b6627356fd4cbafa33'
            '6c14fdcd94105338e13bdd59dbed4f9b77430a517b2c125ac2e6d3bc43bdf506')
