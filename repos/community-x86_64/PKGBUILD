# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-scummvm
pkgver=93193
pkgrel=1
pkgdesc='ScummVM core'
arch=(x86_64)
url=https://github.com/libretro/scummvm
license=(GPL)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit=6df2bdf73cc9596dbc0ef395d78e48a93e0854be
source=(libretro-scummvm::git+https://github.com/libretro/scummvm.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-scummvm

  git rev-list --count HEAD
}

build() {
  make -C libretro-scummvm/backends/platform/libretro/build
}

package() {
  install -Dm 644 libretro-scummvm/backends/platform/libretro/build/scummvm_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
