# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=nothunks
pkgname=haskell-nothunks
pkgver=0.1.3
pkgrel=44
pkgdesc="Examine values for unexpected thunks"
url="https://github.com/input-output-hk/nothunks"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-hedgehog' 'haskell-random' 'haskell-tasty' 'haskell-tasty-hedgehog')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        hedgehog-1.1.1.patch)
sha256sums=('c191e9c3d86ca006bf8d85adfd936ff997759120b0fcfaf4960a56d8bd850e53'
            'd6342d09e8447b9b595587128704fe08e93ae1539244c3bd5172db5bcf8678bc')

prepare() {
  cd $_hkgname-$pkgver
  patch -p1 -i ../hedgehog-1.1.1.patch
  gen-setup
  uusi -u tasty-hedgehog -u vector $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fbytestring -ftext -fvector

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
