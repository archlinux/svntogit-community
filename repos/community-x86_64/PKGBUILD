# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=2.91.0
pkgrel=2
pkgdesc='Go-lang bingdings for dde-daemon'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill' 'gcc-go' 'poppler-glib' 'metacity')
makedepends=('git' 'deepin-dbus-generator' 'bzr')
source=("git+https://github.com/linuxdeepin/dde-api.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/go-lib.git"
        "git+https://github.com/linuxdeepin/dbus-factory.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/lib
  cp -a go-lib/* build/src/pkg.deepin.io/lib

  cd "$srcdir/dde-api"
  sed -i '/LDFLAGS/ s/)/ gtk+-3.0 librsvg-2.0 libmetacity-private)/' Makefile

  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2
}

build(){
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/dde-api"
  make USE_GCCGO=1
}

package() {
  cd dde-api
  make DESTDIR="${pkgdir}" install-binary
}
