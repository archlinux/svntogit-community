# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>

pkgname=guake
pkgver=0.4.4
pkgrel=4
pkgdesc='Drop-down terminal for GNOME'
arch=('i686' 'x86_64')
url="http://guake.org/"
license=('GPL')
depends=('python2-notify' 'vte' 'python2-gconf' 'python2-dbus' 'python2-xdg')
makedepends=('intltool' 'gnome-common')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/Guake/$pkgname/archive/$pkgver.tar.gz
        guake-fix-notification.patch
        fix-compiositing.patch)
md5sums=('06b424ae32c7e63a8256922f1e8c51b6'
         '0b57227e6869dd84dd4fb0794ed0d082'
         '742729d81a9e22b59bd4ec0af64804d3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix GConf schema dir
  sed -i 's|schemadir = $(sysconfdir)/gconf/schemas|schemadir = $(GCONF_SCHEMA_FILE_DIR)|' data/Makefile.am

  # Python2 fix
  sed -i 's|/usr/bin/env python|/usr/bin/python2|' src/{guake{,-prefs},prefs.py}

  # Runtime fix when no notification server available
  patch -Np1 -i ../guake-fix-notification.patch

  # Handle enabling/disabling compositing while Guake is running
  patch -Np1 -i ../fix-compiositing.patch

  # Fix build
  sed -i '/AM_INIT_AUTOMAKE/ s/-Werror//' configure.ac
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --disable-schemas-install \
               --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
               PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
