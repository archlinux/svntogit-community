# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher O'Neill <milkytracker@chrisoneill.co.uk>
# Contributor: Maks Verver <maksverver@geocities.com>

pkgname=milkytracker
pkgver=1.01.00
pkgrel=1
pkgdesc='Fast Tracker II inspired music tracker'
arch=('x86_64' 'i686')
url='http://milkytracker.titandemo.org/'
license=('GPL3')
depends=('sdl2' 'alsa-lib' 'zlib' 'gcc-libs')
makedepends=('jack-audio-connection-kit' 'gendesk' 'cmake' 'ninja' 'sdl2')
optdepends=('jack-audio-connection-kit: JACK audio support')
options=('docs' '!strip')
source=("https://github.com/milkytracker/MilkyTracker/archive/v$pkgver.tar.gz")
sha256sums=('9b07c910c3c037ae57a76a4d571511a50276a563a0d2463e1d88256b756b76b7')

prepare() {
  gendesk -f -n \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --name 'MilkyTracker' \
    --categories 'AudioVideo;Audio;AudioVideoEditing;'
}

build() {
  mkdir -p build
  cd build
  cmake ../"MilkyTracker-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -G Ninja
  ninja
}

package() {
  cd build

  DESTDIR="$pkgdir" ninja install

  cd "$srcdir/MilkyTracker-$pkgver"
  install -d "$pkgdir/usr/share/applications"
  install -Dm644 resources/pictures/carton.png \
    "$pkgdir/usr/share/pixmaps/milkytracker.png"
  install -m644 "$srcdir/milkytracker.desktop" \
    "$pkgdir/usr/share/applications"
}

# vim: ts=2 sw=2 et:
