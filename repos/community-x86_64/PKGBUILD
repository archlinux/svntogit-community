# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Ben <ben@benmazer.net>

pkgname=fox
epoch=1
pkgver=1.6.54
pkgrel=1
pkgdesc="Free Objects for X: GUI Toolkit for C++"
arch=('i686' 'x86_64')
url="http://www.fox-toolkit.org/"
license=('LGPL' 'custom')
depends=('bzip2' 'libxcursor' 'libxft' 'libxrandr' 'libxi' 'libpng' 'libtiff' 'glu')
makedepends=('mesa' 'gendesk' 'imagemagick')
optdepends=('perl')
source=(http://ftp.fox-toolkit.org/pub/fox-$pkgver.tar.gz)
sha256sums=('960f16a8a69d41468f841039e83c2f58f3cb32622fc283a69f20381abb355219')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver}
  gendesk -n -f --pkgname=adie --pkgdesc="Fast and convenient programming text editor" --categories="Utility;TextEditor"
  gendesk -n -f --pkgname=calculator --name="FOX Calculator" --pkgdesc="Simple desktop calculator" --categories="Utility;Calculator"
  gendesk -n -f --pkgname=ControlPanel --name="FOX Desktop Setup" --pkgdesc="Setup desktop appearance for FOX applications" --categories="Settings;DesktopSettings"
  gendesk -n -f --pkgname=PathFinder --pkgdesc="File browser" --categories="System;FileTools;FileManager"
}

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  convert adie/big_gif.gif adie.png
  convert calculator/bigcalc.gif calculator.png
  convert pathfinder/file_gif.gif PathFinder.png

  export CPPFLAGS="$CPPFLAGS -I/usr/include/freetype2"
  ./configure   --prefix=/usr \
                --enable-release \
                --with-xft=yes \
                --with-opengl=yes \
                --with-xim \
                --with-xshm \
                --with-shape \
                --with-xcursor \
                --with-xrender \
                --with-xrandr \
                --with-xfixes \
                --with-xinput
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  pushd tests
  make ControlPanel
  install -m755 .libs/ControlPanel "$pkgdir"/usr/bin/
  popd

  install -Dm644 LICENSE_ADDENDUM "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -dm755 "$pkgdir"/usr/share/{applications,pixmaps}
  install -m644 *.png "$pkgdir"/usr/share/pixmaps/
  install -m644 *.desktop "$pkgdir"/usr/share/applications/
}
