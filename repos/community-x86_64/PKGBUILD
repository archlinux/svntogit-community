# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

shopt -s extglob

pkgbase=ghc
pkgname=(ghc-libs ghc ghc-static)
pkgver=8.10.3
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('x86_64')
url='https://www.haskell.org/ghc/'
license=('custom')
makedepends=('ghc-static' 'perl' 'libxslt' 'docbook-xsl' 'python-sphinx' 'haskell-hscolour'
             'texlive-bin' 'texlive-latexextra' 'ttf-dejavu' 'alex' 'happy' 'time')
source=("https://downloads.haskell.org/~ghc/$pkgver/$pkgbase-${pkgver}-src.tar.xz"
        ghc-rebuild-doc-index.hook ghc-register.hook ghc-unregister.hook)
noextract=("$pkgbase-${pkgver}-src.tar.xz")
sha512sums=('6f3248e728d3677dfddf2f02609167c67a194750418f2a5e34ed68f1135960761c5a5768255bfc8b2ef5640395609424878375184d2dde8e2d69021a3baf9ec8'
            'd69e5222d1169c4224a2b69a13e57fdd574cb1b5932b15f4bc6c7d269a9658dd87acb1be81f52fbcf3cb64f96978b9943d10cee2c21bff0565aaa93a5d35fcae'
            '5f659651d8e562a4dcaae0f821d272d6e9c648b645b1d6ab1af61e4dd690dc5a4b9c6846753b7f935963f001bb1ae1f40cd77731b71ef5a8dbc079a360aa3f8f'
            '3bdbd05c4a2c4fce4adf6802ff99b1088bdfad63da9ebfc470af9e271c3dd796f86fba1cf319d8f4078054d85c6d9e6a01f79994559f24cc77ee1a25724af2e6')

prepare() {
  # Need to extract this tarball with a UTF-8 locale instead of a chroot's "C"
  # locale; otherwise we get:
  #   bsdtar: Pathname can't be converted from UTF-8 to current locale.
  LANG=en_US.UTF-8 bsdtar xf $pkgbase-${pkgver}-src.tar.xz

  cd ghc-$pkgver

  cp mk/build.mk{.sample,}
  sed -i '1iBuildFlavour = perf' mk/build.mk
}

build() {
  cd ghc-$pkgver

  ./configure \
    --prefix=/usr \
    --docdir=/usr/share/doc/ghc \
    --with-system-libffi \
    --with-ffi-includes=$(pkg-config --variable=includedir libffi)
  make
}

package_ghc-static() {
  pkgdesc='The Glasgow Haskell Compiler - Static Libraries and Documentation'
  depends=('ghc')

  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  mv "$pkgdir"/usr/lib/ghc-$pkgver/package.conf.d "$srcdir"/static-package.conf.d
  find "$pkgdir"/usr/lib ! \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -type f -delete
  find "$pkgdir"/usr/lib -type d -empty -delete
  mv "$srcdir"/static-package.conf.d "$pkgdir"/usr/lib/ghc-$pkgver/
  rm -r "$pkgdir"/usr/bin "$pkgdir"/usr/share/man

  install -Dm644 "$srcdir"/ghc-rebuild-doc-index.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-rebuild-doc-index.hook
}

package_ghc() {
  pkgdesc='The Glasgow Haskell Compiler'
  depends=('gcc' 'ghc-libs')
  provides=('haskell-haddock=2.24.0'
            'haskell-hp2ps=0.1'
            'haskell-hpc-bin=0.68'
            'haskell-hsc2hs=0.68.7')
  replaces=('haskell-haddock'
            'haskell-hp2ps'
            'haskell-hpc-bin'
            'haskell-hsc2hs')

  provides+=("haskell-ghc=$pkgver")
  replaces+=("haskell-ghc")

  cd ghc-$pkgver
  make DESTDIR="$pkgdir" -j1 install

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc-pkg is in ghc-libs
  rm "$pkgdir"/usr/lib/ghc-$pkgver/bin/ghc-pkg*
  rm "$pkgdir"/usr/bin/ghc-pkg*
  (cd "$pkgdir"/usr/lib/ghc-$pkgver; rm -r !(bin|ghc-$pkgver))

  # docs moved to ghc-static
  rm -r "$pkgdir"/usr/share/doc

  install -Dm644 utils/completion/ghc.bash \
    "$pkgdir/usr/share/bash-completion/completions/ghc"
}

package_ghc-libs() {
  pkgdesc='The Glasgow Haskell Compiler - Dynamic Libraries'
  install='ghc.install'
  depends=('gmp' 'libffi' 'perl')
  provides=('haskell-array=0.5.4.0'
            'haskell-base=4.14.1.0'
            'haskell-binary=0.8.8.0'
            'haskell-bytestring=0.10.12.0'
            'haskell-cabal=3.2.1.0'
            'haskell-containers=0.6.2.1'
            'haskell-deepseq=1.4.4.0'
            'haskell-directory=1.3.6.0'
            'haskell-exceptions=0.10.4'
            'haskell-filepath=1.4.2.1'
            'haskell-ghc-boot=8.10.3'
            'haskell-ghc-boot-th=8.10.3'
            'haskell-ghc-compact=0.1.0.0'
            'haskell-ghc-heap=8.10.3'
            'haskell-ghc-prim=0.6.1'
            'haskell-haskeline=0.8.0.1'
            'haskell-hpc=0.6.1.0'
            'haskell-integer-gmp=1.0.3.0'
            'haskell-libiserv=8.10.3'
            'haskell-mtl=2.2.2'
            'haskell-parsec=3.1.14.0'
            'haskell-pretty=1.1.3.6'
            'haskell-process=1.6.9.0'
            'haskell-stm=2.5.0.0'
            'haskell-template-haskell=2.16.0.0'
            'haskell-terminfo=0.4.1.4'
            'haskell-text=1.2.4.1'
            'haskell-time=1.9.3'
            'haskell-transformers=0.5.6.2'
            'haskell-unix=2.7.2.2'
            'haskell-xhtml=3000.2.2.1'
            'haskell-ghc-pkg=6.9')
  replaces=('haskell-array'
            'haskell-base'
            'haskell-binary'
            'haskell-bytestring'
            'haskell-cabal'
            'haskell-containers'
            'haskell-deepseq'
            'haskell-directory'
            'haskell-exceptions'
            'haskell-filepath'
            'haskell-ghc-boot'
            'haskell-ghc-boot-th'
            'haskell-ghc-compact'
            'haskell-ghc-heap'
            'haskell-ghc-prim'
            'haskell-haskeline'
            'haskell-hpc'
            'haskell-integer-gmp'
            'haskell-libiserv'
            'haskell-mtl'
            'haskell-parsec'
            'haskell-pretty'
            'haskell-process'
            'haskell-stm'
            'haskell-template-haskell'
            'haskell-terminfo'
            'haskell-text'
            'haskell-time'
            'haskell-transformers'
            'haskell-unix'
            'haskell-xhtml'
            'haskell-ghc-pkg')

  provides+=("haskell-ghci=$pkgver")
  conflicts+=('haskell-ghci')

  cd ghc-$pkgver

  make DESTDIR="$pkgdir" -j1 install

  # Remove static libs
  find "$pkgdir"/usr/lib \( -name "*.a" -o -name "*.o" -o -name "*.p_o" -o -name "*.p_hi" -o -name "*.hi" \) -delete

  # ghc library and other exes are in the ghc package
  rm -r "$pkgdir"/usr/lib/ghc-$pkgver/ghc-$pkgver
  (cd "$pkgdir"/usr/lib/ghc-$pkgver/bin; rm !(ghc-pkg*))
  (cd "$pkgdir"/usr/bin; rm !(ghc-pkg*))

  # docs moved to ghc-static
  rm -r "$pkgdir"/usr/share/{man,doc}

  install -Dm644 "$srcdir"/ghc-register.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-register.hook
  install -Dm644 "$srcdir"/ghc-unregister.hook "$pkgdir"/usr/share/libalpm/hooks/ghc-unregister.hook

  install -dm755 "$pkgdir"/usr/share/haskell/{register,unregister}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
