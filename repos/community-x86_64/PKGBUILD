# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent B. <vb@luminar.eu.org>

pkgname=opam
pkgver=2.0.0
pkgrel=3
pkgdesc='OCaml package manager'
arch=('x86_64')
url='https://opam.ocaml.org/'
license=('GPL')
depends=('bubblewrap' 'ocaml-compiler-libs' 'unzip')
optdepends=('darcs: For downloading packages with darcs'
            'git: For downloading packages with git'
            'mercurial: For downloading packages with mercurial'
            'rsync: For downloading packages with rsync')
source=("https://github.com/ocaml/opam/releases/download/$pkgver/opam-full-$pkgver.tar.gz")
sha256sums=('9dad4fcb4f53878c9daa6285d8456ccc671e21bfa71544d1f926fb8a63bfed25')

build() {
  cd "opam-full-$pkgver"

  ./configure --prefix=/usr
  make -j1 lib-ext all OCAMLC="ocamlc -unsafe-string" OCAMLOPT="ocamlopt -unsafe-string"
}

package() {
  make -C "opam-full-$pkgver" DESTDIR="$pkgdir" install
}

# getver: opam.ocaml.org
# vim: ts=2 sw=2 et:
