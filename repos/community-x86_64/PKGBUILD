# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libjcat
pkgver=0.1.12
pkgrel=1
pkgdesc="Library for reading and writing Jcat files"
arch=(x86_64)
url="https://github.com/hughsie/libjcat"
license=(LGPL)
depends=(gobject-introspection-runtime json-glib gnutls gpgme)
makedepends=(meson gobject-introspection gtk-doc vala help2man)
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('318d7e370b3cff4b03f2a4be541e6a309eb40de22c48ccbc0661659dfa7b0c92'
            'SKIP')
validpgpkeys=(163EB50119225DB3DF8F49EA17ACBA8DFA970E17) # Richard Hughes <richard@hughsie.com>

build() {
  arch-meson ${pkgname}-${pkgver} build -D gtkdoc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -r "${pkgdir}"/usr/{lib,share}/installed-tests/
}
