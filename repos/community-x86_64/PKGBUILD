# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=deriving-compat
pkgname=haskell-deriving-compat
pkgver=0.5.9
pkgrel=18
pkgdesc="Backports of GHC deriving extensions"
url="https://github.com/haskell-compat/deriving-compat"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-abstraction' 'haskell-transformers-compat')
makedepends=('ghc' 'haskell-base-compat' 'haskell-base-orphans' 'haskell-hspec' 'haskell-quickcheck'
             'haskell-tagged' 'haskell-void')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('4f1442867894d0b4899e7e649c404be13160cc9bda3d6f24d38d391e3057913279d6b815ef26bc95c4a6ac752304480627d051d3aa826af10a668e309ae7a105')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
