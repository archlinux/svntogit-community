# $Id$
# Maintainer: Jonathan Steel <jsteel at aur.archlinux.org>

pkgname=cmatrix
pkgver=1.2a
pkgrel=7
pkgdesc="Let you see the matrix code in your console"
arch=('i686' 'x86_64')
url="http://www.asty.org/cmatrix/"
license=('GPL')
depends=('ncurses' 'fontconfig')
install=$pkgname.install
source=(http://www.asty.org/cmatrix/dist/$pkgname-$pkgver.tar.gz)
md5sums=('ebfb5733104a258173a9ccf2669968a1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  CPPFLAGS="-D_FORTIFY_SOURCE=0" ./configure \
    --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  
  install -Dm644 mtx.pcf "$pkgdir/usr/share/fonts/misc/mtx.pcf"
  install -Dm644 matrix.fnt "$pkgdir/usr/share/kbd/consolefonts/matrix.fnt"
  install -Dm644 matrix.psf.gz "$pkgdir/usr/share/kbd/consolefonts/matrix.psf.gz"
}
