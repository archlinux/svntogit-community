# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=haskell-hpack
_hkgname=hpack
pkgver=0.33.0
pkgrel=61
pkgdesc="A modern format for Haskell packages"
url="https://github.com/sol/hpack#readme"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-bifunctors' 'haskell-cryptonite' 'haskell-glob'
         'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-types'
         'haskell-infer-license' 'haskell-scientific' 'haskell-unordered-containers'
         'haskell-vector' 'haskell-yaml')
makedepends=('ghc' 'haskell-hunit' 'haskell-quickcheck' 'haskell-hspec' 'haskell-interpolate'
             'haskell-mockery' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-${pkgver}.tar.gz")
sha512sums=('8d70bea1c18e9a26fe137f6e20fec4af28dff6ebc50981d2f57ad9a8e0b9574ff158adb5ba16597b6a5f0e4d843c09fee57dd82407dc1383b1e07453b64d010b')

build() {
  cd $_hkgname-$pkgver
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test || warning "https://github.com/sol/hpack/issues/358"
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
