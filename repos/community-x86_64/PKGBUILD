# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Alexander Bruegmann <mail[at]abruegmann[dot]eu>

pkgname=cargo-generate-rpm
pkgver=0.10.0
pkgrel=1
pkgdesc='Cargo helper command to generate a binary RPM package'
arch=('x86_64')
url="https://github.com/cat-in-136/cargo-generate-rpm"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname-$pkgver-cargo-lock.patch::$url/commit/5444a35749e48e84d4cf94028ab2a4b6aa1576fb.patch"
        "$pkgname-$pkgver-tests.patch::$url/commit/a3f8157391e019e0b8f031ba1c99656e4a49e25c.patch")
sha256sums=('119f8e1df94ae098c17301b5094e19866c8deabe05bd95370b98e339b0e02c83'
            'a1047fde815821541f5e2f398b5f5e933c4536d037c97b916fc07b762f3377a1'
            'd58372aa079be634ce42446da1b51082bbc413c85305b92e9cdb2c220de2ccc6')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "../$pkgname-$pkgver-cargo-lock.patch"
  patch -Np1 -i "../$pkgname-$pkgver-tests.patch"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}


check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
