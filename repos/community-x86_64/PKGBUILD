# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=6.0.0
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=(x86_64)
url=https://github.com/elementary/terminal
license=(GPL3)
depends=(
  glib2
  gtk3
  libgranite.so
  libhandy-1.so
  pango
  vte3
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
groups=(pantheon)
_tag=06fb5ad548c8f6048f771a76290ebdf4b6324773
source=(pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-terminal

  git describe --tags
}

build() {
  arch-meson pantheon-terminal build \
    -Dubuntu-bionic-patched-vte=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
