# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.11.8
pkgrel=1
pkgdesc='COIN-OR collection of utility classes'
arch=(x86_64)
url='https://github.com/coin-or/CoinUtils'
license=(EPL)
depends=(zlib bzip2 glpk lapack coin-or-data-sample)
makedepends=(gcc-fortran doxygen)
groups=(coin-or)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/CoinUtils/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('202e347d1c1d2ccf5355e3c2874a4dc16500226c180b00d6677f464d80be337e')

build() {
  cd CoinUtils-releases-$pkgver

  ./configure --prefix=/usr --with-blas-lib='-lblas' --with-lapack-lib='-llapack' --with-glpk-lib='-lglpk' --enable-dependency-linking
  make
}

check() {
  cd CoinUtils-releases-$pkgver
  make test
}

package() {
  cd CoinUtils-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
