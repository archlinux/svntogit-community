# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=opentimelineio
pkgver=0.15
pkgrel=1
pkgdesc='Open Source API and interchange format for editorial timeline information'
url='http://opentimeline.io/'
license=(Apache)
arch=(x86_64)
depends=(python-aaf2)
makedepends=(cmake python-setuptools pybind11 git imath)
source=(git+https://github.com/PixarAnimationStudios/OpenTimelineIO#tag=v$pkgver)
sha256sums=('SKIP')

prepare() {
  cd OpenTimelineIO
  git submodule update --init
  sed -e '/deps/d' -i CMakeLists.txt # Unbundle pybind11
  sed -e '1 i\find_package(pybind11)' -i src/py-opentimelineio/opentime-bindings/CMakeLists.txt \
                                      -i src/py-opentimelineio/opentimelineio-bindings/CMakeLists.txt
}

build() {
  cmake -B build -S OpenTimelineIO \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOTIO_FIND_IMATH=ON
  cmake --build build

# Workaround messy build system
  export CXXFLAGS+=" -I/usr/include/Imath"
  cd OpenTimelineIO
  python setup.py build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd OpenTimelineIO
  DESTDIR="$pkgdir" python setup.py install --root="$pkgdir" --optimize=1
  rm -fr "$pkgdir"/build
}
