# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
# Contributor: champus
# Contributor: madeye
# Contributor: Ranguvar
# Contributor: ninja_pt

pkgname=hwinfo
pkgver=21.0
_subver=1.2
pkgrel=1
pkgdesc='Hardware detection tool from openSUSE'
arch=('x86_64' 'i686')
url='http://download.opensuse.org/source/factory/repo/oss/suse/src/'
license=('GPL2')
depends=('perl' 'libx86emu')
makedepends=('sysfsutils' 'rpmextract' 'flex')
options=('!emptydirs')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/$pkgname-$pkgver-$_subver.src.rpm"
        'custom_ioctl.patch')
sha256sums=('4126875621a935a450cf616b69a1f922047f241cedc3ac02ece54cd78b23a9fb'
            '6e83b50aa34f31db5fe6de938402e7c4dda6ffa9ffe1346c076e4779018fcf1d')

prepare() {
  rpmextract.sh "$pkgname-$pkgver-$_subver.src.rpm"
  tar jxf $pkgname-$pkgver.tar.bz2
  cd "$pkgname-$pkgver"
  patch -p0 -i ../custom_ioctl.patch
}

build() {
  make -C "$pkgname-$pkgver" -j1 \
    CFLAGS+="-fPIC -I$srcdir/$pkgname-$pkgver/src/hd" \
    LIBDIR=/usr/lib
}

package() {
  make -C "$pkgname-$pkgver" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}

# vim:set ts=2 sw=2 et:
