# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>
# Contributor: Stephen Gregoratto <dev@sgregoratto.me>

pkgname=fdm
pkgver=2.1
pkgrel=1
pkgdesc='Program to fetch mail and deliver it in various ways'
url='https://github.com/nicm/fdm'
license=('BSD')
arch=('x86_64')
depends=('openssl' 'pcre' 'tdb' 'zlib')
source=("https://github.com/nicm/fdm/releases/download/${pkgver}/fdm-${pkgver}.tar.gz")
sha256sums=('858df2e6ef0836d940e8b8cc4fec333770fa9c97ba0f2485a9e63ed18b2cadff')

options=('!makeflags')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-pcre
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	sed '3,17p;d' fdm.c > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -d "${pkgdir}/usr/share/doc/${pkgname}"
	cp -a examples MANUAL "${pkgdir}/usr/share/doc/${pkgname}"
}
