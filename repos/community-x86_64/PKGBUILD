# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: wizzomafizzo <wizzomafizzo@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Benjamin Andresen <benny AT klapmuetz DOT org>
# Contributor: Douglas Thrift <douglas@douglasthrift.net>

pkgname=rlwrap
pkgver=0.45.2
pkgrel=1
pkgdesc="Adds readline-style editing and history to programs."
arch=('x86_64')
url="https://github.com/hanslub42/rlwrap"
license=('GPL')
depends=('glibc' 'readline' 'ncurses' 'perl')
optdepends=('python: filters')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('77664bc17c4e6148037b8ae64c025b07c3feae91dfeff59f1846ce38e5f808a41005286daa81fd0ed01e0c789762e377cc48e6abdf124a7d4654f66b2b6c6cc4')
b2sums=('38e54ab87b0530e49cb736072825441f638aa4ed4b22b0f30c7243f5f8ac7b439e3a75784d6a4eed051bf367d15ac3b576109663ddf3fc082171a73c5af7c11e')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
