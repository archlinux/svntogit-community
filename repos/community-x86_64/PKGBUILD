# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehg@gmail.com>
# Contributor: champus, madeye, ranguvar and ninja_pt

pkgname=hwinfo
pkgver=21.68
pkgrel=2
pkgdesc='Hardware detection tool from openSUSE'
arch=(x86_64)
url='https://github.com/openSUSE/hwinfo'
license=(GPL2)
depends=(libx86emu perl)
makedepends=(flex git sysfsutils)
options=(!emptydirs)
source=("$pkgname-$pkgver.tgz::https://github.com/openSUSE/$pkgname/archive/$pkgver.tar.gz"
        custom_ioctl.patch)
sha256sums=('066c3cd8802d81b5a55a3a6ba55095eba613e817c60ebae8be3465e7e702c216'
            '6e83b50aa34f31db5fe6de938402e7c4dda6ffa9ffe1346c076e4779018fcf1d')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../custom_ioctl.patch
  echo 'touch changelog' > git2log
}

build() {
  make -C $pkgname-$pkgver -j1 \
    HWINFO_VERSION="$pkgver" \
    CFLAGS+="-fPIC -I$srcdir/$pkgname-$pkgver/src/hd" \
    LIBDIR=/usr/lib
}

package() {
  make -C $pkgname-$pkgver LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
}

# getver: github.com/openSUSE/hwinfo/releases
# vim: ts=2 sw=2 et:
