# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=1.0
epoch=1
pkgrel=3
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('x86_64')
url="https://widelands.org/"
license=('GPL')
depends=('sdl2_mixer' 'sdl2_image' 'sdl2_net' 'sdl2_ttf' 'sdl2_gfx' 'lua' 'glew' 'python' 'dbus')
makedepends=('cmake' 'boost' 'mesa' 'ninja' 'git')
replaces=('widelands-data')
source=("$pkgname-$pkgver.tar.gz::https://github.com/widelands/widelands/archive/refs/tags/v${pkgver}.tar.gz"
        $pkgname.desktop
        $pkgname.png)
sha512sums=('1092750944a8669682097dfbd2b578cdb8fab377f32cb81ce8d85b9d9b2e8ae8c4c73500c0a89be649b360fd7e086e19595eba37cecb8d64679e46f7da13e46e'
            'ef7e54ffba11a2fe54d2cafc0a12fcbe5028e861b9b980f549317b159c8532ee62423af8724b05cfd344017097e022215fa750f4ca9afffa57703d434017d727'
            'f7db9d7584ec8431f2f1857baddeb32cd3ae182343e7f0369e61fb444514bd9c2e6a3931ae34f24cc994840dffbbe4451f33845798f3d582f41b31f451b261ac')

build() {
  cd $pkgname-$pkgver

  cmake \
      -GNinja \
      -Bbuild \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DWL_INSTALL_BASEDIR=/usr \
      -DWL_INSTALL_DATADIR=/usr/share/widelands
  ninja -C build
}

package() {
  cd $pkgname-$pkgver

  DESTDIR="$pkgdir" ninja -C build install

  # Fix wrong locations of some installed files (upstream bug)
  # See also FS#72240
  mkdir -p "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{widelands,wl_map_object_info,wl_map_info,wl_create_spritesheet} "$pkgdir"/usr/bin
  mv "$pkgdir"/usr/{VERSION,ChangeLog,CREDITS} "$pkgdir"/usr/share/widelands
  mv "$pkgdir"/share/* "$pkgdir"/usr/share/

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/COPYING "$pkgdir"/usr/share/licenses/$pkgname

  install -Dm644 "$srcdir"/$pkgname.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  install -Dm644 "$srcdir"/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop
}
