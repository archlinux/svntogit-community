# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=3.1.8.1
pkgrel=3
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpeg' 'taglib' 'libcue' 'qt5-svg' 'gst-plugins-bad')
makedepends=('python' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-music/archive/$pkgver.tar.gz")
sha512sums=('f597f4118839264f1f131bbdd24954de50869f886901d6b5e3c524b1d2ad71ac63476fdb8cf17966b19693da5a482550cfcc55872edaa03dbf62e2c6f1134887')

prepare() {
  sed -i 's/-0-2//g' deepin-music-$pkgver/music-player/music-player.pro
}

build() {
  cd deepin-music-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-music-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
