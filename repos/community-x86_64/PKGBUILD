# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=lxtask
pkgver=0.1.5
pkgrel=1
pkgdesc='Task manager of the LXDE Desktop'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.xz
        fix-integer-overflow.patch)
md5sums=('c8b1e9df8cbe41c07ebc3830d4386324'
         'd505170197a070a11b67798ebb5519ac')

prepare() {
  cd $pkgname-$pkgver

  # Fix integer overflow on shorting by memory
  patch -Np1 -i ../fix-integer-overflow.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
