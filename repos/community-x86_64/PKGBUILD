# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=5.9.17
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-dock' 'dtkwm' 'procps-ng' 'libcap')
makedepends=('cmake' 'deepin-gettext-tools' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz"
        $pkgname-disable-wayland.patch::https://github.com/linuxdeepin/deepin-system-monitor/pull/63.patch)
sha512sums=('50ab9734677a69c97cd82b67b470c10e827dd98a7925c1c8a5b08167de18b4d311ad99da32685eb7a94e4d5e5124c5e727e26083b683bf729d20a36176557c30'
            '2cd45c9e4d16d3f8ffd5fe712ac893ba634c38a99495434c8f587ad84be76c703c5e3209fe39863c1481d78d2b3df10102f4f271e88396f1da611c2e8067caad')

prepare() {
  mkdir -p build
  patch -d deepin-system-monitor-$pkgver -p1 -i ../$pkgname-disable-wayland.patch
}

build() {
  cd build
  cmake ../deepin-system-monitor-$pkgver -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DWAYLAND_SESSION_SUPPORT=OFF
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
