# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-typelits-natnormalise
pkgname=haskell-ghc-typelits-natnormalise
pkgver=0.7.7
pkgrel=10
pkgdesc="GHC typechecker plugin for types of kind GHC.TypeLits.Nat"
url="https://github.com/clash-lang/ghc-typelits-natnormalise"
license=('custom:BSD2')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc' 'haskell-ghc-tcplugins-extra')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('337a1f092d3ece8cacfe147d081f3e24e2ba0f706a705e04515a4666e760ee3c013b599152fce3ffeaf981b08f883e3b98ff2eb18250f0961209fc54aa9f5f1c')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
