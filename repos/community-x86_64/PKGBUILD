# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Samuel Littley <aur@toastwaffle.com>
# Contributor: rnons <remotenonsense at gmail dot com>

_pkg=ruamel.yaml.clib
pkgname=python-$_pkg
pkgver=0.2.2
pkgrel=1
pkgdesc="C version of reader, parser and emitter for ruamel.yaml derived from libyaml"
arch=('x86_64')
url="https://sourceforge.net/projects/ruamel-yaml-clib/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(https://files.pythonhosted.org/packages/source/r/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha512sums=('5bbab2a23c65b1416480758cf0d0a75dcf8ddece0f8df603ce90f2b1c54356bb6876ac113d53e01c4869e3c9fb22b13ea29e66dc31a09144d4426c3e0f3b9399')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkg}-${pkgver}
  # https://bitbucket.org/ruamel/yaml/issues/114/setuppy-requirement-for-pip-prevents
  RUAMEL_NO_PIP_INSTALL_CHECK=1 python setup.py install --skip-build --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
