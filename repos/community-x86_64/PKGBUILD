# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=csound-plugins
pkgver=1.0.2
pkgrel=6
pkgdesc="Extra opcodes for Csound"
arch=(x86_64)
url="https://github.com/csound/plugins"
license=('GPL')
groups=('pro-audio')
depends=(
	'csound'
	'hdf5'
	'lame'
	'libfaust.so'
	'libfltk.so'
	'libfluidsynth.so'
	'libjack.so'
	'libwebsockets.so'
	'libwiiuse.so'
	'python'
	'stk'
)
makedepends=(
	'cmake'
	'eigen'
	'git'
	'gmm'
	'libpng'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/csound/plugins/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('8c2f0625ad1d38400030f414b92d82cfdec5c04b7dc178852f3e1935abf75d30')

build() {
	mkdir build
	cmake -B build -S "plugins-$pkgver" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX='/usr' \
		-Wno-dev
	make -C build
}

package() {
	make -C build DESTDIR="$pkgdir/" install
}
