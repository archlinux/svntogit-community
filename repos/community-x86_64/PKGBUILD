# Maintainer: Anatol Pomozov

pkgname=aws-c-http
pkgver=0.6.10
pkgrel=1
pkgdesc='C99 implementation of the HTTP/1.1 and HTTP/2 specifications'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-http'
license=(Apache)
depends=(aws-c-common aws-c-compression aws-c-io)
makedepends=(cmake)
source=(aws-c-http-$pkgver.zip::https://github.com/awslabs/aws-c-http/archive/v$pkgver.zip)
sha256sums=('c66f2300075524bdfb60aec3a157ebfba9f474ad43f5889f91845aaedce69699')

build() {
  cd aws-c-http-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-http-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-http-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
