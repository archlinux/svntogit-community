# Maintainer: Christian Heusel <christian@heusel.eu>
# Contributor: Sanjay Pavan <withercubes@protonmail.com>

pkgname=nsxiv
pkgver=31
pkgrel=4
pkgdesc='Neo (or New or Not) Simple (or Small or Suckless) X Image Viewer'
arch=('x86_64')
license=('GPL2')
url='https://nsxiv.codeberg.page/'

depends=('imlib2' 'libx11'                 # core dependencies
         'libxft' 'fontconfig' 'freetype2' # status bar
         'libexif'                         # display exif images
         'hicolor-icon-theme')             # make icon

source=("$pkgname-$pkgver.tar.gz"::"https://codeberg.org/nsxiv/nsxiv/archive/v$pkgver.tar.gz")
sha256sums=('035fbb3fb3ffec45555afd718947ec8a7d7dfac3c5abc7ba6863cc075720d7f2')

prepare() {
  cd "$pkgname"
  [ ! -f config.h ] && cp config.def.h config.h
  # make install currently also runs make all
  # see https://codeberg.org/nsxiv/nsxiv-record/issues/237
  sed -i -e '/^install: / s|: all|:|' Makefile
}

build() {
  make -C "$pkgname" HAVE_LIBGIF=0 HAVE_LIBWEBP=0 HAVE_INOTIFY=1 HAVE_LIBFONT=1 HAVE_LIBEXIF=1
}

package() {
  make -C "$pkgname" PREFIX=/usr DESTDIR="$pkgdir" install-all
}
