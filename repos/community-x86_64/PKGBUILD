# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.8.4
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("https://github.com/qjfoidnh/BaiduPCS-Go/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b8d5d7a91f3df620f241515c2703f6d41b64959f4fb29abf9daf89b6c1338dd00729561f597275a86b28b29d6c978bc446293b4aebef30f5eceb015e96c625db')

build() {
  cd BaiduPCS-Go-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
