# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.11.1
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=(x86_64)
url="https://github.com/agraef/pd-lua"
license=(GPL2)
groups=(pd-externals pro-audio)
depends=(gcc-libs glibc lua pd)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/agraef/$pkgname/archive/$pkgver.tar.gz)
sha512sums=('70cc6c1f9049f4c663df6f4e781be3fa4b1d745cb694c666d6c19e6de8bf06a79c1b20c446bb930df859408718d74c78567e844f284d6d88a2dfe93c9a82f871')
b2sums=('fa0eeae26504acc4134023c2aeeaef8091b4b2405a8ba867b1cebb095572a8cbd250f625db2ae1c471d47f2c62efc53bfafaf0cac707455b1c6838dc0ac8f62a')

build() {
  make LDFLAGS="$LDFLAGS" CFLAGS="$CFLAGS" -C $pkgname-$pkgver
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" PDLIBDIR=/usr/lib/pd/extra prefix=/usr install
  install -vDm 644 README -t "$pkgdir/usr/share/doc/$pkgname/"
}
