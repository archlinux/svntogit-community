# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=ulfius
pkgver=2.7.11
pkgrel=1
pkgdesc='HTTP Framework for REST Applications in C'
arch=(x86_64)
url=https://github.com/babelouest/ulfius
license=(LGPL2.1)
depends=(
  glibc
  gnutls
  jansson
  libcurl.so
  libmicrohttpd.so
  liborcania.so
  libyder.so
  zlib
)
makedepends=(
  cmake
  doxygen
  git
  graphviz
  ninja
)
provides=(libulfius.so)
_tag=7fb370f0985d5cf2916ff7ebaf8d6346e6aa46fd
source=(git+https://github.com/babelouest/ulfius.git#tag=${_tag})
validpgpkeys=(8405B02FCC28EF9744C8F253FE82139440BD22B9) # Nicolas Mora <nicolas@babelouest.org>
b2sums=(SKIP)

pkgver() {
  cd ulfius
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S ulfius -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_ULFIUS_DOCUMENTATION=ON
  ninja -C build doc
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  cp -dr --no-preserve=ownership ulfius/doc/html "${pkgdir}"/usr/share/doc/ulfius/
}

# vim: ts=2 sw=2 et:
