# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-clp
pkgver=1.16.11
pkgrel=1
pkgdesc="COIN-OR linear programming solver"
arch=(i686 x86_64)
url="https://projects.coin-or.org/Clp"
license=(EPL)
groups=(coin-or)
depends=(coin-or-osi suitesparse)
source=("http://www.coin-or.org/download/source/Clp/Clp-${pkgver}.tgz")
sha256sums=('b525451423a9a09a043e6a13d9436e13e3ee7a7049f558ad41a110742fa65f39')

build() {
  cd Clp-$pkgver/Clp
  COIN_SKIP_PROJECTS="Sample" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" \
              --with-cholmod-lib='-lcholmod' --with-cholmod-incdir=/usr/include
  make
}

check() {
  cd Clp-$pkgver/Clp
# make test
}

package() {
  cd Clp-$pkgver/Clp
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
