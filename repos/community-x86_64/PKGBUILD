# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.8.7
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64)
url="https://github.com/ijl/orjson"
license=(Apache MIT)
depends=(python)
makedepends=(maturin python-installer rust)
checkdepends=(python-arrow python-pendulum python-psutil python-pytest
python-pytz python-xxhash)
source=($pkgname-$pkgver.tar.gz::https://github.com/ijl/$_name/archive/$pkgver.tar.gz)
sha512sums=('3210b5ec5d88ebab14515fe3e66fb12260eed9ecb69472effc3ec6f45d2abc671fd7b46430ddb0b4864b17dbba2f1efd6b0238c693fce53e305e7ae3251158ab')
b2sums=('00a9e26334a2fa6af05a88de033c7d338075b3d4db0254598ad763fa3baf484eff9caf52e3f26e412b6a496cb4ded1a270b72a652bffe442be54e0b385cca6cb')

build() {
  cd $_name-$pkgver
  maturin build --release --strip
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir target/wheels/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 LICENSE-* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
