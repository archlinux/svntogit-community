# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Daniel Wallace <daniel.wallace@gatech.edu>

pkgname=plex-home-theater
pkgver=1.1.4
pkgrel=1
pkgdesc='Plex Home Theater'
arch=('i686' 'x86_64')
url='http://www.plexapp.com/'
license=('GPL2')
depends=('boost-libs' 'curl' 'fontconfig' 'glew' 'lame' 'libass' 'libcdio'
         'libjpeg-turbo' 'libmad' 'libmicrohttpd' 'libmodplug' 'libmpeg2'
         'libsamplerate' 'libssh' 'libusb-compat' 'libva' 'libxrandr' 'lzo2'
         'mesa' 'rtmpdump' 'sdl_image' 'sdl_mixer' 'smbclient' 'taglib'
         'tinyxml' 'yajl')
makedepends=('boost' 'cmake' 'doxygen' 'ftgl' 'java-runtime' 'libcec'
             'libplist' 'libshairport' 'nasm' 'swig' 'unzip' 'zip')
optdepends=('libplist: AirPlay support'
            'libshairport: AirPlay support'
            'libcec: Pulse-Eight USB-CEC adapter support'
            'pulseaudio: PulseAudio support')
install="${pkgname}.install"
source=("https://github.com/plexinc/plex-home-theater-public/archive/pht-v${pkgver}.tar.gz"
        'plexhometheater.service'
        'plexhometheater.sh')
sha256sums=('1f3be5bb9a09b05e624a0ca010c673436b2f1cdcf8dff7c94a030235af340fbb'
            'bca8cd5aa13f469f93dd4e9868c0b5050395ca32f28b19852e5830441b67e1be'
            'c551d8f50342dcfede5a11e1bae2c18b25d44b89a5a29863e5809d45ef365702')

build() {
  cd plex-home-theater-public-pht-v${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/opt/plexhometheater' \
           -DCMAKE_C_FLAGS="$CMAKE_C_FLAGS -I/usr/include/glib-2.0 -I/usr/lib/glib-2.0/include" \
           -DCMAKE_CXX_FLAGS="$CMAKE_CXX_FLAGS -I/usr/include/glib-2.0 -I/usr/lib/glib-2.0/include" \
           -DENABLE_{AUTOUPDATE,DUMP_SYMBOLS}='FALSE' -DENABLE_PYTHON='TRUE' \
           -DPYTHON_EXEC='/usr/bin/python2' -DUSE_INTERNAL_FFMPEG='TRUE' \
           -DCREATE_BUNDLE='FALSE'
  make
}

package() {
  cd plex-home-theater-public-pht-v${pkgver}/build

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,pixmaps}}
  install -m 755 "${srcdir}"/plexhometheater.sh "${pkgdir}"/usr/bin/
  install -m 644 ../plex/Resources/plexhometheater.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 ../plex/Resources/plex-icon-256.png "${pkgdir}"/usr/share/pixmaps/plexhometheater.png

  # Systemd
  install -dm 755 "${pkgdir}"/usr/lib/systemd/system
  install -m 644 ../../plexhometheater.service "${pkgdir}"/usr/lib/systemd/system/

  # Plex user home
  install -dm 700 "${pkgdir}"/var/lib/plex
  chown 421:421 "${pkgdir}"/var/lib/plex
}

# vim: ts=2 sw=2 et:
