# $Id$
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=0.7.6
pkgrel=3
pkgdesc="Notification daemon for the desktop notifications framework"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org/"
depends=(gtk3 libcanberra)
makedepends=(intltool)
source=(http://download.gnome.org/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.xz
        fix-boolean-hint.patch
        fix-gsource-usage.patch
        fix-position.patch)
sha256sums=('64d0ce6fb12c94c3b73b701502c804c408cb5a94580bcae5ac37607639f7a0b3'
            'ba4e4fe42d82034cd12cb10d1c3c9c6f4318b5d9e10e7829ec0478b6ce801282'
            '2c36bb0c77f27345f18240c7ba39aff7e559fd5155c54d83bc18e21ca13ce9ff'
            'a4e5ed7cdf42a137b1672b8128eb7a754ceece57243a02e0dc4dd977911beea2')

prepare() {
    cd ${pkgname}-${pkgver}

    # Avoid crashes for boolean hints
    # https://bugzilla.gnome.org/show_bug.cgi?id=665166
    patch -Np1 -i ../fix-boolean-hint.patch

    # Fix GSource usage with recent GLib
    # https://bugzilla.gnome.org/show_bug.cgi?id=728157
    patch -Np1 -i ../fix-gsource-usage.patch

    # Fix positioning notifications with long text
    # https://bugzilla.gnome.org/show_bug.cgi?id=687724
    patch -Np1 -i ../fix-position.patch
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/notification-daemon-1.0 \
        --localstatedir=/var --disable-static
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
}
