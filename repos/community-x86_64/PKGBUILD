# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname="psi-plus"
pkgver=1.2.164
pkgrel=1
pkgdesc="Psi+ is a powerful Jabber client (Qt, C++) designed for the Jabber power users (built with Qt 5.x)"
url="http://psi-plus.com"
license=('GPL2')
arch=('x86_64')
depends=('qt5-base' 'qt5-webengine' 'qt5-multimedia' 'qt5-x11extras' 'qca-qt5'
	 'libidn' 'libxss' 'qt5-svg' 'hunspell' 'qtkeychain')
makedepends=('git' 'patch' 'cmake')
conflicts=('psi-plus-qt5-git' 'psi-plus-webkit-qt5-git' 'psi-plus-webkit-git' 'psi-plus-git')
source=("git://github.com/psi-plus/psi-plus-snapshots#tag=$pkgver"
	"no-qca-qt4.patch::https://aur.archlinux.org/cgit/aur.git/plain/no-qca-qt4.patch?h=psi-plus-git&id=bc3ee431ca26d6294810636a514c50985540bd23"
	'join.patch')
sha256sums=('SKIP'
            'a6ea9a62100b6990b8a6c8590a6d0e9df095b99b6527bb5490a7fd3f6929ac23'
            'ea25a58c7efe25979b8d257598608187380e8f35ace25e96cab9c357dcdbc974')

prepare() {
  cd psi-plus-snapshots
  patch -p1 <"$srcdir"/join.patch
  patch -p1 <"$srcdir"/no-qca-qt4.patch
}

build() {
  cd psi-plus-snapshots
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd psi-plus-snapshots/build

  make DESTDIR="$pkgdir" install

#  install -dm755 "$pkgdir/usr/include/psi-plus/plugins"
#  install -m644 src/plugins/include/*.h "$pkgdir/usr/include/psi-plus/plugins"
}
