# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Oliver Jaksch <arch-aur@com-in.de>

pkgname=libretro-mame2016
pkgver=44430
pkgrel=1
arch=(x86_64)
pkgdesc='MAME 2016 Arcade core'
url=https://github.com/libretro/mame2016-libretro.git
license=(custom)
groups=(libretro)
depends=(
  alsa-lib
  glibc
  libretro-core-info
  libgl
  zlib
)
makedepends=(
  git
  libglvnd
  python
)
source=(libretro-mame2016::git+https://github.com/libretro/mame2016-libretro.git)
b2sums=(SKIP)

pkgver() {
  cd libretro-mame2016
  git rev-list --count HEAD
}

build() {
  cd libretro-mame2016
  make -f Makefile.libretro \
    PTR64=1 \
    PYTHON_EXECUTABLE=python
}

package() {
  install -Dm 644 libretro-mame2016/mame2016_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-mame2016/docs/LICENSE -t "${pkgdir}"/usr/share/licenses/libretro-mame2016/
}

# vim: ts=2 sw=2 et:
