# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
pkgname=libmatemixer
pkgver=${_ver}.1
pkgrel=1
pkgdesc="Mixer library for MATE Desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('alsa-lib' 'libpulse')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('37c5b081e80db36210834e123643e2f7a12f176d')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
