# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gammu
pkgver=1.41.0
pkgrel=3
pkgdesc="GNU All Mobile Management Utilities"
arch=('x86_64')
url="https://wammu.eu/gammu/"
license=('GPL')
depends=('glib2' 'bluez-libs' 'curl' 'libdbi' 'mariadb-libs' 'postgresql-libs' 'libusb' 'libgudev')
makedepends=('cmake' 'doxygen' 'python2' 'systemd')
checkdepends=('libdbi-drivers')
optdepends=('dialog: support for the gammu-config script' 'python2: for python bindings')
source=(https://dl.cihar.com/$pkgname/releases/$pkgname-$pkgver.tar.xz)
sha256sums=('8426b69b07b259de301f41163fab5587935e27b94cc5eefab9277773b3e4df8f')

prepare() {
  cd $pkgname-$pkgver
# bash completion dir change
  sed -i 's,COMPLETIONSDIR "/etc/bash_completion.d",COMPLETIONSDIR "/usr/share/bash-completion/completions",' contrib/CMakeLists.txt
}

build() {
  cd $pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX="/usr" -DLIB_SUFFIX=""
  make
}

check() {
  cd $pkgname-$pkgver

  # tests can be flaky on slower hardware due to timing
  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
