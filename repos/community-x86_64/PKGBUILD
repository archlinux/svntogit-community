# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=quickcheck-instances
pkgname=haskell-quickcheck-instances
pkgver=0.3.22
pkgrel=27
pkgdesc="Common quickcheck instances"
url="https://github.com/phadej/qc-instances"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-base-compat' 'haskell-case-insensitive'
         'haskell-hashable' 'haskell-old-time' 'haskell-scientific' 'haskell-splitmix'
         'haskell-tagged' 'haskell-time-compat' 'haskell-transformers-compat'
         'haskell-unordered-containers' 'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('cd36e6833752fbc5501a4fe8459381fe3c07f73b706a438effee06bd1b5e17ecb12b1347fdacb65556e339de58ca8ac32480dc1339cb43b1416c4aeb4f714f8c')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/< *0.11/<1/' -e 's/< *4.13/<5/' -e 's/< *2.14/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-bytestring-builder
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
