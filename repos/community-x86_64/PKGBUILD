# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=cargo-watch
pkgver=8.2.0
pkgrel=1
pkgdesc="Utility for Cargo to compile projects when sources change"
url='https://github.com/passcod/cargo-watch'
arch=('x86_64')
license=('CC0-1.0')
depends=('cargo')
makedepends=('ruby-ronn')
source=("https://github.com/passcod/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5fb4ca920aef1da4bb1d0ed0a7a2d7a29f0fd7a82225c2c0cc2c597f52d8bb9d')
b2sums=('95a0840642d9dfb2f1ebb8814495f9760733cc69ff585c2e6c028e7ce878499ccf988b8f170af992df70585a489f67b9004859360f78f9f9548003d2c569f42f')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  cargo build --frozen --release
  ronn --roff cargo-watch.1.ronn
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm 644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_cargo-watch"
  install -Dm 644 cargo-watch.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
