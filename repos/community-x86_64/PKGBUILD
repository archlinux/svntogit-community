# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-citeproc
pkgver=0.10.5.1
pkgrel=9
pkgdesc="Supports using pandoc with citeproc"
url="https://hackage.haskell.org/package/$pkgname"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-aeson" "haskell-aeson-pretty" "haskell-attoparsec" "haskell-data-default"
         "haskell-hs-bibutils" "haskell-mtl" "haskell-old-locale" "pandoc" "haskell-pandoc-types"
         "haskell-parsec" "haskell-rfc5051" "haskell-setenv" "haskell-split" "haskell-syb"
         "haskell-tagsoup" "haskell-temporary" "haskell-text" "haskell-text-icu"
         "haskell-unordered-containers" "haskell-vector" "haskell-xml-conduit" "haskell-yaml")
conflicts=('haskell-pandoc-citeproc')
replaces=('haskell-pandoc-citeproc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/${pkgver}/$pkgname-${pkgver}.tar.gz")
sha512sums=('12bdce94a022189a84da4f6b6c2b427dc4f5aef3199c443a7fb56bb65c1fffce469d7bff61addba9cd909b129b0fdac7fd885f53e6de746fd7e8b7fcd32a7780')

build() {
    cd "${srcdir}/$pkgname-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/$pkgname" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-debug -f-test_citeproc -funicode_collation -f-embed_data_files -fbibutils
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/$pkgname-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/$pkgname.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/$pkgname.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/$pkgname/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/$pkgname"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/$pkgname/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
