# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=38.4
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/DejaDup"
license=('GPL')
depends=('duplicity' 'gnome-online-accounts' 'gvfs' 'python2-gobject')
makedepends=('appstream-glib' 'gobject-introspection' 'intltool' 'itstool' 'meson' 'nautilus' 'vala' git)
optdepends=('gnome-keyring: save passwords'
            'nautilus: backup extension'
            'python2-boto: Amazon S3 and Google Cloud Storage backend')
_commit=017e0b81677c6d36ea7d4d91e58a1d1c8de4d08d  # tags/38.4
source=("git+https://gitlab.gnome.org/World/deja-dup.git#commit=$_commit")
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
