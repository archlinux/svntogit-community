# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=20220128
pkgrel=1
_commit=c43a34f
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=('x86_64')
url='https://www.chiark.greenend.org.uk/~sgtatham/puzzles/'
license=(MIT)
depends=(gtk3)
makedepends=(cmake imagemagick)
source=(https://www.chiark.greenend.org.uk/~sgtatham/puzzles/puzzles-$pkgver.$_commit.tar.gz)
sha256sums=('4b1855c1b209f7534a73a49319668b071cec273db5b14e274b4909403a09d9b7')

build() {
  cmake -B build -S puzzles-$pkgver.$_commit \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DNAME_PREFIX=puzzles-
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 puzzles-$pkgver.$_commit/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
