# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=netavark
_commit=0b5ab2e8648c801ff55a745ea0e32b7b9ab7ccb4  # refs/tags/v1.5.0
pkgver=1.5.0
pkgrel=1
pkgdesc="Container network stack"
arch=(x86_64)
url="https://github.com/containers/netavark"
license=(Apache)
depends=(gcc-libs glibc)
makedepends=(cargo git libgit2 mandown protobuf)
optdepends=('aardvark-dns: for authorative DNS server')
provides=(container-network-stack=2)
source=(git+https://github.com/containers/netavark.git#tag=$_commit)
sha256sums=('SKIP')
# NOTE: pinning commit until upstream clarifies commitment to chain of trust:
# https://github.com/containers/netavark/issues/231
# validpgpkeys=('74FE091D25519980B2D84447160386BECB6F0643')  # Brent Baude <bbaude@redhat.com>

prepare() {
  cd $pkgname
  # fix issue with yanked dependency: https://github.com/containers/netavark/issues/521
  git cherry-pick -n 3a1ff2447d8cef1eeab0413bbbbb1f1fb8280a17
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cd $pkgname
  # generate man page directly as docs target is broken: https://github.com/containers/netavark/issues/524
  mandown docs/$pkgname.1.md > $pkgname.1
  cargo build --frozen --release --all-features
}

check() {
  export RUSTUP_TOOLCHAIN=stable

  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  install -vDm 644 $pkgname.1 -t "$pkgdir/usr/share/man/man1/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
