# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.10.9
pkgrel=1
pkgdesc='Movie player based on mpv'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration'  'ffmpegthumbnailer' 'libxtst' 'mpv' 'qtdbusextended' 'qtmpris')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
options=('!lto')
source=("https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c1c06ee1d7046bc67891982ede4d2a48eb3e8e9ba71a6a9cec054361a62ab0272c72cbf40d06ecafe4fe7bfe8cc6d9edc8dfe20be1d53a020d746be4925b7fb6')

prepare() {
  cd deepin-movie-reborn-$pkgver
  sed -i '/avformat.h>/i #include <libavcodec/avcodec.h>' src/libdmr/playlist_model.cpp
}

build() {
  cd deepin-movie-reborn-$pkgver
  export CXXFLAGS+=' -fpermissive'
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release -DVERSION=$pkgver .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
