# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=darkhttpd
pkgver=1.12
pkgrel=1
pkgdesc="A small and secure static webserver"
arch=('i686' 'x86_64')
url="http://dmr.ath.cx/net/darkhttpd/"
license=('BSD')
depends=()
backup=('etc/conf.d/mimetypes')
source=("$url/$pkgname-$pkgver.tar.bz2"
        'darkhttpd.service')
sha256sums=('afad244add43c53010b36ada6ccdf1a854885cb37d6aeff103ad0aa4aed96c51'
            '40f037a74bf8d28e779dcf7c0d447bcc74a629ffe6abb470e193f047567aed11')
sha256sums=('a50417b622b32b5f421b3132cb94ebeff04f02c5fb87fba2e31147d23de50505'
            '40f037a74bf8d28e779dcf7c0d447bcc74a629ffe6abb470e193f047567aed11')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install darkhttpd
  install -Dm755 "$srcdir/$pkgname-$pkgver/darkhttpd" "$pkgdir/usr/bin/darkhttpd"

  # install systemd files
  install -Dm644 "$srcdir/darkhttpd.service" "$pkgdir/usr/lib/systemd/system/darkhttpd.service"
  install -dm0755 $pkgdir/etc/conf.d/
  touch $pkgdir/etc/conf.d/mimetypes

  # install license
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n 18 darkhttpd.c > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
