# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=mc
pkgver=4.8.28
pkgrel=2
pkgdesc='A file manager that emulates Norton Commander'
arch=('x86_64')
url='https://midnight-commander.org/'
license=('GPL')
depends=(
  'e2fsprogs'
  'glib2'
  'gpm'
  'libssh2'
  'slang'
  'which'
)
makedepends=(
  'aspell'
  'libxt'
  'libx11'
  'unzip'
)
optdepends=(
  'aspell: spelling corrections'
  'cabextract: ucab extfs'
  'cdparanoia: audio extfs'
  'cdrkit: iso9660 extfs'
  'cvs: CVS support'
  'gawk: hp48+ extfs'
  'mtools: a+ extfs'
  'p7zip: support for 7zip archives'
  'perl: needed by several extfs scripts'
  'python: to access uc1541 or s3 storage'
  'python-boto: s3+ extfs'
  'python-pytz: s3+ extfs'
  'samba: VFS support'
  'unace: uace extfs'
  'unarj: uarj extfs'
  'unrar: urar extfs'
  'unzip: open zip archives'
  'zip: uzip extfs'
)
backup=(
  'etc/mc/edit.indent.rc'
  'etc/mc/filehighlight.ini'
  'etc/mc/mc.ext'
  'etc/mc/mc.keymap'
  'etc/mc/mc.menu'
  'etc/mc/mcedit.menu'
  'etc/mc/sfs.ini'
)
options=('!emptydirs')
source=(
  "http://ftp.midnight-commander.org/${pkgname}-${pkgver}.tar.xz"
  'mc-python3.patch'
)
sha512sums=('ca3b3a57281e9387cb52bf1fcfc3f39a4d5f438aafc256dc492c192678824f67c983a852c759664efc5bc6d0de471c9b3929834f8301f6302ca1ebeee513fdfd'
            '516c25cdc482afba6b0fa78737e4e10815acdc4d528a908e118025e44f44be0bb86b90b5044d4675e835d8ff64be551b5d48943c2c2ddd9716c34b39b1e627bc')
b2sums=('f4218e6c166df85b7b3736f0fd1414c9507c72074f06001f12b114ce6c6fbbf3688ab850aa70f5063eb3920736f878e113eb39a5d261ccfc3ffa9dab473a8436'
        'b0e20d89c7c1ba25f76f564481ed7e2b9f8614b460dc170bb3d1998411c3d7945dee5200e9a2db81f706a8bdbffffb72665a0c43353b520c02fcf5fee4d251db')

prepare() {
  cd "$pkgname-$pkgver"

  # port to Python 3
  # ref: https://github.com/MidnightCommander/mc/pull/149
  patch -p1 -i "$srcdir/mc-python3.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-vfs-smb \
    --enable-aspell \
    --with-screen=slang \
    --with-x

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # FS#50889: Replace mc.keymap symlink with target file to fix backup mechanism.
  rm "$pkgdir/etc/mc/mc.keymap"
  cp "$pkgdir"/etc/mc/mc{.default,}.keymap
}
