# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <greg.sutcliffe@gmail.com>>

pkgname=ruby-shadow
pkgver=2.5.0
pkgrel=7
pkgdesc="Shadow password module"
arch=('x86_64')
url="https://github.com/apalmblad/ruby-shadow"
license=('CPL')
depends=('ruby')
source=($pkgname-$pkgver.tar.gz::https://github.com/apalmblad/$pkgname/archive/$pkgver.tar.gz
        ruby-shadow-2.5.0-cflags.patch)
sha512sums=('66cf0405230c0cbc723f17bf016d84b5054be4db5b32bca55ede876b5725c0f8de849c537c6020f38053825f55177bebacd0fdbb20d49110ea8cb2d5b75df9da'
            '7dfcb925a21a02d42fb595c4dc847e6fe8b952b0bf0a4a31a27ae0b0296bf361ddb6cc5eef745c8bbc93990967f3ea39dfcb448be72d9b835a589f9d28b3eacc')

prepare() {
  cd $pkgname-$pkgver

  # Respect CFLAGS and fix build with Ruby 3.0.0
  # https://github.com/apalmblad/ruby-shadow/issues/26
  patch -Np0 -i ../ruby-shadow-2.5.0-cflags.patch
}

build() {
  cd $pkgname-$pkgver

  ruby extconf.rb
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir"/ sitedir='$(rubylibprefix)/vendor_ruby' \
    sitehdrdir='$(rubyhdrdir)/vendor_ruby' install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
