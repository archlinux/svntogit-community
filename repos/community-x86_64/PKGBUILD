# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>

_pkgname=coveragepy
pkgname=python-coverage
pkgver=6.2
pkgrel=1
pkgdesc="A tool for measuring code coverage of Python programs"
arch=('x86_64')
url="https://nedbatchelder.com/code/coverage/"
license=('Apache')
depends=('python')
optdepends=('python-tomli: for pyproject.toml support')
makedepends=('python-setuptools')
checkdepends=('python-flaky' 'python-hypothesis' 'python-mock'
              'python-pycontracts' 'python-pytest' 'python-pytest-xdist' 'python-tomli'
              'python-unittest-mixins' 'python-virtualenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nedbat/coveragepy/archive/$pkgver.tar.gz")
sha512sums=('fed10100131e8c53f4389de16d419412453e92ac8ecf0384b531d10c184247fe0da666369f9b04b3d0214c905091428e23fd0d0de82d39f9a77cb07a643d1ffe')
b2sums=('4b62c5f28606867129f7674acabe3ecab5a68d15055efe08c797693922b1e98feffec382e8b799e22f422753581930253d1765c96c81e846ccdfbcad1590d50d')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  (
    virtualenv "test_dir" --system-site-packages
    . "test_dir/bin/activate"
    export PYTHONPATH="/usr/lib/python$python_version/site-packages:$PYTHONPATH"
    python setup.py --quiet clean develop
    python igor.py zip_mods install_egg remove_extension
    python igor.py test_with_tracer py
    python setup.py --quiet build_ext --inplace
    python igor.py test_with_tracer c
  ) || echo "Known failing tests."
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="$pkgdir"
}
