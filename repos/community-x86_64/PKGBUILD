# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=camlp4
pkgver=4.11
pkgrel=1
pkgdesc='Caml preprocessor and pretty-printer'
arch=('x86_64')
license=('GPL2')
url='https://github.com/ocaml/camlp4/releases'
depends=('ocaml>=4.11.0')
makedepends=('git' 'ocamlbuild' 'ocaml-compiler-libs')
options=('!makeflags' '!emptydirs' 'staticlibs')
source=("https://github.com/ocaml/camlp4/archive/$pkgver.tar.gz")
sha256sums=('52a21e1a011c8697516a91455c19a3b27dedd85c510a32c470f944ccb9331498')

build() {
  cd "${srcdir}/$pkgname-${pkgver}"
  ./configure
  make all camlp4/META
}

package() {
  cd "${srcdir}/$pkgname-${pkgver}"
  make \
    BINDIR="$pkgdir/usr/bin" \
    LIBDIR="$pkgdir/usr/lib/ocaml" \
    PKGDIR="$pkgdir/usr/lib/ocaml" \
    install install-META
}

# vim:set ts=2 sw=2 et:
