# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=pplpy
pkgname=python-pplpy
pkgver=0.8.4
pkgrel=7
pkgdesc="Python wrapper to the C++ Parma Polyhedra Library (PPL)"
arch=(x86_64)
url="https://gitlab.com/videlec/pplpy/"
license=(LGPL2.1)
depends=(python-cysignals python-gmpy2 ppl)
makedepends=(cython python-sphinx)
conflicts=(pplpy-doc)
provides=(pplpy-doc)
replaces=(pplpy-doc)
source=("https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz")
sha256sums=('b962914d5814edab320d00c11ca396b70c4ce1e67db7a6d59c4b0a3772a16836')

build() {
  cd $_pipname-$pkgver
  python setup.py build

  _pythonver=`python -c "from sysconfig import get_python_version; print(get_python_version())"`
  PYTHONPATH="$PWD/build/lib.linux-x86_64-$_pythonver" make -C docs html
}

package() {
  cd $_pipname-$pkgver 
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  mkdir -p "$pkgdir"/usr/share/doc/pplpy
  cp -r docs/build/html/* "$pkgdir"/usr/share/doc/pplpy
}
