# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=adios2
pkgver=2.8.3
pkgrel=2
pkgdesc="The Adaptable Input/Output System version 2"
arch=(x86_64)
url="https://adios2.readthedocs.io/en/latest/"
license=(Apache)
depends=(blosc bzip2 glibc gcc-libs hdf5 libfabric libpng nlohmann-json
         openmpi pugixml pybind11 sz yaml-cpp zeromq zfp zlib)
# with mgard does not build currently, not time to investigate
makedepends=(cmake gcc-fortran gtest python python-mpi4py python-numpy)
source=(https://github.com/ornladios/ADIOS2/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4906ab1899721c41dd918dddb039ba2848a1fb0cf84f3a563a1179b9d6ee0d9f')

build() {
  cmake -B build -S ${pkgname^^}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DADIOS2_USE_EXTERNAL_DEPENDENCIES=ON \
    -DADIOS2_BUILD_EXAMPLES=OFF #\
    #-DMGARD_INCLUDE_DIR=/usr/include/mgard
  make VERBOSE=1 -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
