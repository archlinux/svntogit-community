# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=iwd
pkgver=0.20
pkgrel=1
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('glibc')
source=("https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar"{.xz,.sign}
        'iwd.tmpfiles')
sha512sums=('acfe77e96b4658c6efbfe1bab460593e2e15c9d54244deb1dd6e6bc0ec5bab7c0760257dda7b1218338b103daa9b5a422d48d7f5ae50a631bc46669bf14f8fcd'
            'SKIP'
            '03a3a0fba53706ef46bb12e75ad0bc30969044662df5bdcebc608c57b6ab9c5b3b6944e7941549db3172577c7d86c42e39a06e285459663ae48032fd3ab697d4')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')
changelog=ChangeLog

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/iwd \
	--enable-wired \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
	install -Dm644 "${srcdir}/iwd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/iwd.conf"
}

# vim: set ts=2 sw=2 ft=sh noet:
