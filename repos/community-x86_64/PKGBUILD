# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=microlens-aeson
pkgname=haskell-microlens-aeson
pkgver=2.2.0.1
pkgrel=8
pkgdesc="Law-abiding lenses for Aeson, using microlens"
url="http://github.com/fosskers/microlens-aeson/"
license=('MIT')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-aeson' 'haskell-attoparsec' 'haskell-microlens' 'haskell-scientific'
         'haskell-text' 'haskell-unordered-containers' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('a62398c2d8b3e55d8e054d437b110094cb73cb9ba22fe96bbe9c272c039b9991858a64ca92c407c73ec6ef2ff46664ed1d98b3417126ab5ef89ba368d0aaec46')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
