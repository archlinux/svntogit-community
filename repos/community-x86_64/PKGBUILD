# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=40.0
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Boxes"
license=(LGPL)
groups=(gnome)
depends=(cdrtools edk2-ovmf gtksourceview4 gtk-vnc libarchive libgudev libhandy
         libosinfo libsecret libvirt-glib mtools qemu spice-gtk tracker3 webkit2gtk)
makedepends=(git gobject-introspection yelp-tools meson spice-protocol vala)
_commit=f6bc86b9fe73b391114e27f3fe607b1e916904c6  # tags/40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit"
        "git+https://gitlab.gnome.org/felipeborges/libovf-glib.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libovf-glib "$srcdir/libovf-glib"
  git submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
