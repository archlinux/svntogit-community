# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=pointed
pkgname=haskell-pointed
pkgver=5
pkgrel=47
pkgdesc="Pointed and copointed data"
url="https://github.com/ekmett/pointed/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-comonad" "haskell-data-default-class" "haskell-hashable"
         "haskell-kan-extensions" "haskell-semigroupoids" "haskell-semigroups" "haskell-stm"
         "haskell-tagged" "haskell-transformers-compat" "haskell-unordered-containers")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('940a0c30845cb39238a0056f12da4debb488bec9bfac11a384320c79102e92dbd949526188eca46a2e543dfe612f5ee335955413e4f81eb1c1c9bc321805b598')

prepare() {
    sed -i 's/data-default-class >= 0.0.1 && < 0.1/data-default-class >= 0.0.1 \&\& < 0.2/' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcomonad -fcontainers -fkan-extensions -fsemigroupoids -fsemigroups \
            -fstm -ftagged -ftransformers -funordered-containers
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
