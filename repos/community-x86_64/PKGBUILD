# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: ccorn

pkgname=git-delta
_pkgname=delta
pkgver=0.14.0
pkgrel=1
pkgdesc='Syntax-highlighting pager for git and diff output'
arch=('x86_64')
url='https://github.com/dandavison/delta'
license=('MIT')
depends=('libgit2' 'libgit2.so')
makedepends=('rust' 'mdbook')
checkdepends=('git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('5eba901e3b5d06543d30dad49e92ad8ca9ad02d9757d7cfb4edf8ce460c2549bca07dd073dd44443a3befebe32d7d854b19e72dabf4efdced2eac109eedc614e')
b2sums=('bb0fa08aa0e05474242518872f1db3f8ebc569dd7cdc399dc1d7eb1837b721e0944df814b8fa4e4669ab24fd9512f82ad50d9faa3ae1129bfff92474b8a811b2')

prepare() {
  cd "$_pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"

  # https://archlinux.org/todo/lto-fat-objects/
  CFLAGS+=' -ffat-lto-objects'

  # binary
  cargo build --frozen --release --all-features

  # html manual
  cd manual
  mdbook build --dest-dir html
}

check() {
  cd "$_pkgname-$pkgver"
  # some tests hang for 60+ seconds, find out why
  # took my machine ~ 470 seconds
  cargo test --frozen --all-features
}

package() {
  cd "$_pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/delta

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # shell auto-completions
  install -vDm644 etc/completion/completion.bash \
    "$pkgdir/usr/share/bash-completion/completions/$_pkgname"
  install -vDm644 etc/completion/completion.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$_pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # html manual
  cp -r manual/html "$pkgdir/usr/share/doc/$pkgname"
}
