# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=2.7.0
pkgrel=3
pkgdesc='The Pantheon Photos Manager'
arch=(x86_64)
url=https://github.com/elementary/photos
license=(GPL3)
depends=(
  cairo
  gdk-pixbuf2
  geocode-glib
  glib2
  gst-plugins-base-libs
  gstreamer
  gtk3
  json-glib
  libexif
  libgee
  libgexiv2
  libgphoto2
  libgranite.so
  libgudev-1.0.so
  libraw
  libsoup
  libxml2
  pango
  rest
  sqlite
)
makedepends=(
  git
  intltool
  meson
  vala
  webkit2gtk
)
optdepends=('webkit2gtk: Publishing plugins')
groups=(pantheon)
_tag=84e161733d921a4a8a520bfc8a2654d69928c942
source=(pantheon-photos::git+https://github.com/elementary/photos.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd pantheon-photos
  git cherry-pick -n 27e529fc96da828982563e2e19a6f0cef883a29e
}

pkgver() {
  cd pantheon-photos
  git describe --tags
}

build() {
  arch-meson pantheon-photos build \
    -Dlibunity=false
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
