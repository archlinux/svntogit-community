# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.9.13
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
depends=('cups' 'deepin-qt5integration' 'smbclient')
makedepends=('gtest' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/dde-printer/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8a24b9206c21dd9e64b25bd97aeb405ce698ae51f2eee93d988821bfb84a0a09ec6016ba472aa408051d529ad1c7d36e2daa17526719fc67d0cbcb42bee47f25')

prepare() {
  cd dde-printer-$pkgver
  sed -i '$aOnlyShowIn=Deepin' src/Printer/platform/linux/dde-printer.desktop

  # https://github.com/linuxdeepin/dde-printer/issues/4
  sed -i '/#include <strings.h>/a #include <stdexcept>' src/cppcups/cupssnmp.cpp
}

build() {
  cd dde-printer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
