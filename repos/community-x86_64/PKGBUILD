# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=alembic
pkgver=1.8.1
pkgrel=1
pkgdesc="An open framework for storing and sharing scene data"
arch=('x86_64')
url="http://www.alembic.io/"
license=('BSD')
depends=('imath' 'boost-libs' 'hdf5')
makedepends=('cmake' 'boost' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('895493f36c895aef0675e1300ee3bbbf1b8819b4978b842c48810078e51219423500f74014eafe357a318a755ffa8c5c17c2c806b4216bab6cd334908bf684a4')

build() {
  cd "${pkgname}-${pkgver}"

  cmake . \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=ON

  ninja -C build
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  DESTDIR="${pkgdir}/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
