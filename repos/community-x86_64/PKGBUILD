# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=97.0
pkgrel=3
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'java-environment' 'mesa')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
install=spring.install
source=(http://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma
        optional.patch
        glew1.11.patch)
md5sums=('cf37518379c2a3a8c3c9cd689ee9b034'
         'd7904319b17c32299b44594ccb6ff4ea'
         'd4ec0dc8e3bce4138de4415cc445c77e')

build() {
  bsdtar -xf ${pkgname}_${pkgver}_src.tar.lzma
  
  cd spring_${pkgver}

  sed -i "/ory(test)/d" tools/unitsync/CMakeLists.txt
  patch -Np1 < $srcdir/optional.patch
  patch -Np1 < $srcdir/glew1.11.patch

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd spring_${pkgver}

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.spring' > "$pkgdir/etc/spring/datadir"
}

# vim sw=2:ts=2 et:
