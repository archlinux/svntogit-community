# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>

pkgname=gnome-tour
pkgver=44.0
pkgrel=1
pkgdesc='Guided tour and greeter for GNOME'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/gnome-tour'
license=('GPL3')
depends=(
  'gtk4'
  'libadwaita'
)
makedepends=(
  'appstream-glib'
  'meson'
  'rust'
)
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('06de7677dd1c590d0ea33a032c9ccf4c37c62bc56215b823c879e42ee630c2b6')

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build

  # Fails when run under fakeroot, so can't be in package()
  meson install -C build --destdir "$srcdir/fakeinstall"
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  cp -a fakeinstall/* "$pkgdir"
}
