# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=brial
pkgver=1.2.8
pkgrel=1
pkgdesc="Library for polynomials over boolean rings"
arch=(x86_64)
url="https://github.com/BRiAl/BRiAl"
license=(GPL)
depends=(m4ri)
makedepends=(boost python)
optdepends=('python: Python bindings')
source=("https://github.com/BRiAl/BRiAl/releases/download/$pkgver/brial-$pkgver.tar.bz2")
sha256sums=('deb95fc1a99b6f9324f1278fcb676a605b77d59f24683d6af87f573cb46d0a4f')

build() {
  cd $pkgname-$pkgver 

  ./configure --prefix=/usr --enable-shared
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  make

  cd sage-brial
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  cd sage-brial
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
