# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=tamarin-prover
pkgver=1.2.3
pkgrel=33
pkgdesc="The Tamarin prover for security protocol analysis"
url="http://tamarin-prover.github.io"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'graphviz' 'maude' 'haskell-hunit' 'haskell-binary-orphans'
         'haskell-blaze-builder' 'haskell-blaze-html' 'haskell-cmdargs' 'haskell-conduit'
         'haskell-fclabels' 'haskell-file-embed' 'haskell-gitrev' 'haskell-http-types'
         'haskell-lifted-base' 'haskell-mtl' 'haskell-parsec' 'haskell-resourcet' 'haskell-safe'
         'haskell-shakespeare' 'haskell-text' 'haskell-threads' 'haskell-wai' 'haskell-warp'
         'haskell-yesod-core' 'haskell-yesod-static' 'haskell-tamarin-prover-utils'
         'haskell-tamarin-prover-term' 'haskell-tamarin-prover-theory')
optdepends=('ocaml: for sapic support')
makedepends=('ghc' 'ocaml')
source=("tamarin-prover-$pkgver.tar.gz::https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver.tar.gz")
sha512sums=('3ec371f0abf1a391bbc2be0b3e3940b5f634515e443a6535ab3b8a5c6514004bbada35838b63e2c1e5edcbbdb230eae5be03dd8464f8516d3301bf02aad3d5ff')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i '/cp sapic/d' plugins/sapic/Makefile
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-executable-dynamic --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -fthreaded -ftest-coverage -f-build-tests
    runhaskell Setup build

    cd plugins/sapic
    make -j1
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    install -Dm644 etc/filetype.vim "$pkgdir"/usr/share/vim/vimfiles/filetype.vim
    install -Dm644 etc/spthy.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/spthy.vim
    install -Dm644 etc/sapic.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/sapic.vim

    cd plugins/sapic
    install -Dm755 sapic "$pkgdir"/usr/bin/sapic
}
