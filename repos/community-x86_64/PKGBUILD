# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=19.1
pkgrel=1
pkgdesc="Download from Usenet using .nzb files"
arch=('i686' 'x86_64')
url="http://nzbget.net/"
license=('GPL')
depends=('libxml2')
optdepends=('python: to run included scripts'
            'unrar: unpacking archives'
            'p7zip: unpacking archives')
changelog=$pkgname.changelog
source=(https://github.com/nzbget/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver-src.tar.gz)
sha256sums=('06df42356ac2d63bbc9f7861abe9c3216df56fa06802e09e8a50b05f4ad95ce6')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix='/usr' --sbindir='/usr/bin' --enable-parcheck --with-tlslib=OpenSSL
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/$pkgname
  install -m644 -t "${pkgdir}"/usr/share/$pkgname ChangeLog README
}
