# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=docker-machine
pkgver=0.13.0
pkgrel=2
pkgdesc='Machine management for a container-centric world'
arch=('i686' 'x86_64')
url='https://github.com/docker/machine'
license=('Apache')
depends=('glibc')
optdepends=('net-tools: for VirtualBox support')
makedepends=('go-pie')
optdepends=('net-tools: required for VirtualBox driver')
source=("$pkgname-$pkgver.tar.gz::https://github.com/docker/machine/archive/v$pkgver.tar.gz")
sha512sums=('c31b3505e475f5e55310cc3b8805db8b94522703956a640954c3898c584fc3a07830c346535855cbb2fb10016dd0e7ef72e7174e919a3651f05e18b652132121')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src
  mv machine-$pkgver/vendor/* build/src/
  mkdir -p build/src/github.com/docker
  ln -s "$srcdir"/machine-$pkgver build/src/github.com/docker/machine
}

build() {
  cd machine-$pkgver
  go build -o bin/docker-machine cmd/machine.go # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd machine-$pkgver
  install -Dm755 bin/docker-machine "$pkgdir"/usr/bin/docker-machine
  install -Dm644 contrib/completion/bash/docker-machine.bash "$pkgdir"/usr/share/bash-completion/completions/docker-machine
  install -Dm644 contrib/completion/zsh/_docker-machine "$pkgdir"/usr/share/zsh/site-functions/_docker-machine
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
