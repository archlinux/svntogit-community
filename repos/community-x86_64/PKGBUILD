# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=git-annex
pkgver=6.20170520
pkgrel=34
pkgdesc="Manage files with git, without checking their contents into git"
url="http://git-annex.branchable.com/"
license=("AGPL3")
arch=('i686' 'x86_64')
depends=('git' 'rsync' 'ghc' "haskell-aeson" "haskell-async" "haskell-aws" "haskell-blaze-builder"
         "haskell-bloomfilter" "haskell-byteable" "haskell-case-insensitive" "haskell-clientsession"
         "haskell-concurrent-output" "haskell-conduit" "haskell-crypto-api" "haskell-cryptonite"
         "haskell-data-default" "haskell-dav" "haskell-dbus" "haskell-disk-free-space"
         "haskell-dlist" "haskell-dns" "haskell-edit-distance" "haskell-esqueleto"
         "haskell-exceptions" "haskell-fdo-notify" "haskell-feed" "haskell-hinotify"
         "haskell-hslogger" "haskell-http-client" "haskell-http-conduit" "haskell-http-types"
         "haskell-ifelse" "haskell-magic" "haskell-memory" "haskell-monad-control"
         "haskell-monad-logger" "haskell-mountpoints" "haskell-mtl" "haskell-network"
         "haskell-network-info" "haskell-network-multicast" "haskell-network-uri"
         "haskell-old-locale" "haskell-optparse-applicative" "haskell-path-pieces"
         "haskell-persistent" "haskell-persistent-sqlite" "haskell-persistent-template"
         "haskell-quickcheck" "haskell-random" "haskell-regex-tdfa" "haskell-resourcet"
         "haskell-safesemaphore" "haskell-sandi" "haskell-securemem" "haskell-shakespeare"
         "haskell-socks" "haskell-split" "haskell-stm" "haskell-stm-chans" "haskell-tasty"
         "haskell-tasty-hunit" "haskell-tasty-quickcheck" "haskell-tasty-rerun" "haskell-text"
         "haskell-torrent" "haskell-unix-compat" "haskell-unordered-containers"
         "haskell-utf8-string" "haskell-uuid" "haskell-wai" "haskell-wai-extra" "haskell-warp"
         "haskell-warp-tls" "haskell-yesod" "haskell-yesod-core" "haskell-yesod-default"
         "haskell-yesod-form" "haskell-yesod-static")
makedepends=("chrpath")
source=("git+https://git.joeyh.name/git/git-annex.git#tag=$pkgver"
        quickcheck-2.10.patch)
sha512sums=('SKIP'
            'a987bb49072fa27bdbb061bffa9174e75c1e7cfa1e139945465262557c1bd31832373e2684d9ff83b682dcb6b5cfdf2f40e86a8cedf118fb768f9c5827318f4c')

prepare() {
  cd git-annex
  patch -p1 -i ../quickcheck-2.10.patch
}

build() {
  cd git-annex
  
  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --docdir="/usr/share/doc/$pkgname" \
    -fnetwork-uri -fconcurrentoutput -ftorrentparser \
    -ftestsuite -f-androidsplice -f-android -fproduction -fpairing -fwebapp \
    -fassistant -fwebdav -fs3 -f-benchmark -fdbus -fmagicmime
  runhaskell Setup build
}

package() {
  cd git-annex
  runhaskell Setup copy --destdir="$pkgdir"
  make GHC="ghc -dynamic" BUILDER=true DESTDIR="$pkgdir" install-misc

  rm "$pkgdir"/usr/share/doc/git-annex/COPYRIGHT
  rmdir "$pkgdir"/usr/share/doc/git-annex "$pkgdir"/usr/share/doc
}
