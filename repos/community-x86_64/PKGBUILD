# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=libtracefs
pkgname=(libtracefs libtracefs-docs)
pkgver=1.3.1
pkgrel=1
pkgdesc="Linux kernel trace file system library"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/about/"
license=(GPL2 LGPL2.1)
makedepends=(asciidoc libtraceevent xmlto)
provides=(libtracefs.so)
options=(debug)
source=(
  $pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/libs/libtrace/libtracefs.git/snapshot/$pkgname-$pkgver.tar.gz
  $pkgname-1.2.0-docs_makefile.patch
)
sha512sums=('1d8423ff1033a4ab601dd8e9fa4a801c27425234505070733641c8c9a4dd382cc6563d3fde89958f18e0bc95e2cbbdf65e69a2935fe208d5de9fd3ac26374ec1'
            'bdfffba9957d889c60baea9c197709bf1a62d30ac4b8fd47d930b8d5b67fa040817b9a835e226dd1201f2ccdb7fcb43a12093c2b559fb6c0edc3e9ba167062c2')
b2sums=('d9801c9f50513a8b2d2de57f768aec69c3e47b4c41ba1e887b28465d3a0a6bf10e5d2c60f81615615176bdb65e10ef7e2b7dee5cbb214705cc69e55bb923231c'
        '1340af2c65dc344912715c69827d5849fda83e4bcf136da3ccdbee4e3b508e9b557af0828914dff8d27abebbdbdab975d8800432d5252b6b1eb0da08b0cb4b02')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

prepare() {
  patch -d $pkgname-$pkgver -Np 1 -i ../$pkgname-1.2.0-docs_makefile.patch
}

build() {
  make LDFLAGS="$LDFLAGS" -C $pkgname-$pkgver
  make doc -C $pkgname-$pkgver
}

package_libtracefs() {
  depends+=(glibc libtraceevent.so)
  optdepends=('libtracefs-docs: for documentation')

  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C $pkgname-$pkgver/Documentation

  (
    cd "$pkgdir"
    _pick libtracefs-docs usr/share/doc
  )
}

package_libtracefs-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
}
