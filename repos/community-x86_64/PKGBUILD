# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.17
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=('x86_64')
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'liblo' 'libsndfile' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'non-session-manager: for session management using the standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('62116bde79af56dacae69c31f67a1bd6fa95ec0e1f6cb9dadf281ad24157c427fc1c517c0ed20d7ce28bd3b4b13106e43e0a4bd242830966b69bdd44dbe7eec8')
b2sums=('6698aafa816ac2d431d41c7475ab42fc145bdbbdae357842e9b654460ebdbb2662adbfb71dea96854db386bdbd9a1a379afbc2f9bbb8a29f6b06ee421b30479f')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
