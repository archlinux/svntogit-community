# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Agda
pkgname=agda
pkgver=2.5.2
pkgrel=16
pkgdesc="A dependently typed functional programming language and proof assistant"
url="http://wiki.portal.chalmers.se/agda/"
license=("custom")
arch=('i686' 'x86_64')
depends=('ghc' "alex" "happy" "haskell-boxes" "haskell-cpphs" "haskell-data-hash"
         "haskell-edisonapi" "haskell-edisoncore" "haskell-edit-distance" "haskell-equivalence"
         "haskell-geniplate-mirror" "haskell-gitrev" "haskell-hashable" "haskell-hashtables"
         "haskell-ieee754" "haskell-monadplus" "haskell-mtl" "haskell-murmur-hash"
         "haskell-parallel" "haskell-regex-tdfa" "haskell-strict" "haskell-text"
         "haskell-transformers-compat" "haskell-unordered-containers" "haskell-zlib")
optdepends=('agda-stdlib: for standard library')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('d812cec3bf7f03c4b27248572475c7e060154102771a8434cc11ba89f5691439')

prepare() {
    sed -i 's/, ieee754 >= 0.7.8 && < 0.8/, ieee754 >= 0.7.8 \&\& < 0.9/' ${_hkgname}-${pkgver}/${_hkgname}.cabal

    # TODO: Find a better way!
    # Build it twice to compile the agdai file.
    cp -a ${_hkgname}-${pkgver}{,-tmp}
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcpphs -f-uhc -f-debug
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh

    cd "${srcdir}/${_hkgname}-${pkgver}-tmp"
    runhaskell Setup configure --prefix="$PWD/target" --enable-executable-dynamic --datasubdir="$pkgname"
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    runhaskell Setup copy

    LD_PRELOAD=$(ls dist/build/libHSAgda-*-ghc*.so) dist/build/agda/agda target/share/agda/lib/prim/Agda/Primitive.agda
    for _file in target/share/agda/lib/prim/Agda/Builtin/*.agda; do
        LD_PRELOAD=$(ls dist/build/libHSAgda-*-ghc*.so) dist/build/agda/agda "$_file"
    done
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete

    install -m644 "${srcdir}/${_hkgname}-${pkgver}-tmp"/target/share/agda/lib/prim/Agda/Primitive.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Primitive.agdai
    install -m644 "${srcdir}/${_hkgname}-${pkgver}-tmp"/target/share/agda/lib/prim/Agda/Builtin/*.agdai "$pkgdir"/usr/share/agda/lib/prim/Agda/Builtin/
}
