# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=incremental-parser
pkgname=haskell-incremental-parser
pkgver=0.5
pkgrel=5
pkgdesc="Generic parser library capable of providing partial results from partial input."
url="https://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-input-parsers' 'haskell-monoid-subclasses' 'haskell-parsers'
         'haskell-rank2classes')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-checkers' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('4adee2c72c009db5a379ae684dadb2c3b202f6f347ec592fb4189d27c6f927750c23f365a19f3df0f63c7554ff5eb9aaf0320a089f8a9942b3f3ba684b931152')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.txt"
}
