# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Fernandez Ludovic <lfernandez dot dev at gmail dot com>

pkgname=lego
pkgver=4.7.0
pkgrel=1
pkgdesc='Lets Encrypt client and ACME library'
url='https://go-acme.github.io/lego'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
_commit='364439a7c69e8be82b0a07db921e9c04d8e41d9b'
source=("$pkgname::git+https://github.com/go-acme/lego.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download
}

build() {
  cd "$pkgname"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags $LDFLAGS \
    -X main.version=v$pkgver" \
    -o build \
    ./cmd/...
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/lego

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
