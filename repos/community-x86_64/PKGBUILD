# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-luna-pinyin
pkgver=0.0.0.20210627
_commit=623adb022b094d540218b287c2e601509eee3347
pkgrel=1
pkgdesc="Luna pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-luna-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-luna-pinyin/archive/$_commit/rime-luna-pinyin-$_commit.tar.gz")
sha512sums=('7e072dae64453eec537677ab4333051bb7eec94695da1762813a8cfe45de47fff3007b806c51c9208875f52aa2764c2db71edddcae7d5e16c95491727cf8d34f')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
