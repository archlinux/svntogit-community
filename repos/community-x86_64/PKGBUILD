# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.3.6
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('i686' 'x86_64')
url="https://git.merproject.org/mer-core/fingerterm"
license=('LGPL')
depends=('qt5-feedback')
makedepends=('nemo-qml-plugin-notifications')
source=("$pkgname-$pkgver.tar.bz2::https://git.merproject.org/mer-core/fingerterm/repository/archive.tar.bz2?ref=$pkgver")
sha512sums=('34a09091a657198498e596001829e3db28a55640ffd0a868270a6a92c01c4060d12701529e3064028d262c57685425e319261944b3be932ae356b0098e509cf1')

prepare() {
  rm fingerterm-$pkgver.tar.bz2
  mv fingerterm-* fingerterm-$pkgver
}

build() {
  cd fingerterm-$pkgver

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm-$pkgver

  make INSTALL_ROOT="${pkgdir}" install
}
