# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.6.4
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('fa93e71b093542ff13ad846481e0d07f0aa8081b17cb93131d5c6ef3de1aa7f6b10eda2221b52ffe319c3368a26b5250b9c00bacf803b45fb8d4bb525312cc0e')
b2sums=('a470e5ad07ddf4c51608e8e65abf5ae756bc5991347a358e6a60897a34261710146b30e90c548aeb9aba267f04d6e2a4bc55f08e1cb3ae68ace86e02a5aff9d9')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter

  for i in bash fish zsh; do
    ./kube-linter completion ${i} > ./kube-linter.${i}
  done
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 kube-linter -t "${pkgdir}/usr/bin"
  install -Dm644 kube-linter.bash "${pkgdir}"/usr/share/bash-completion/completions/kube-linter
  install -Dm644 kube-linter.fish "${pkgdir}"/usr/share/fish/completions/kube-linter.fish
  install -Dm644 kube-linter.zsh "${pkgdir}"/usr/share/zsh/site-functions/_kube-linter
}

# vim: ts=2 sw=2 et:
