# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-2-1
_patchver=3
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=1
pkgdesc='Computer Algebra System for polynomial computations'
arch=(x86_64)
url='https://www.singular.uni-kl.de/'
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen)
source=(ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz
#source=(ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz)
         doc-$pkgver.tbz2::ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/doc.tbz2)
sha256sums=('14d073b252d6b4f3fce02cebed612d6a09e0eba2bd58384d19b53528c628d71b'
            '0e3860de6d39204cf623fa6486175f560e862002f433a93b9a1bda4acbac1d8e')
options=(!zipman)

build() {
  cd singular-${_majver//-/.}
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --docdir=/usr/share/doc \
    --enable-bigintm-module \
    --enable-bigintm-module \
    --enable-Order-module \
    --enable-python-module \
    --enable-gfanlib-module \
    --enable-polymake-module
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd singular-${_majver//-/.}
  make check
}
  
package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  install -Dm644 ../singular.idx -t "$pkgdir"/usr/share/singular
}
