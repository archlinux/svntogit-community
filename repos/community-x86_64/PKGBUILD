# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.4.2
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11' 'qt5-declarative' 'qt5-quickcontrols2' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python3')
optdepends=('python3: Python plugin')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('1fddd8c8d67e4eaf116c82e276b0941e6dae7c529f2132e49da77d7e215fc7106d5fd3ef55d118f799153b57340effcf2481a131a436be7f00f61c4fc4bd45d2')

build() {
  cd $pkgname-$pkgver
  qmake -r CONFIG+="tiled_zstd" SYSTEMD_ZSTD=yes INSTALL_HEADERS=yes RPATH=no PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make install INSTALL_ROOT="$pkgdir"
}
