# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.9.4
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=('x86_64')
url="https://qsampler.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libgig' 'libgig.so' 'liblscp'
'liblscp.so' 'linuxsampler' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0e43a335c503bd76d41bfe88967b199bcd8c34a44a65293c1460daa342ca1e35f236984af74e30404637e9f6c8428b4cc52fc27501af7d52e1c9e30f1a80d35e')
b2sums=('83f0eb7102d6dbba994d2efeee59ebc46ce2b07d40b9dcfe97ed3059fefd449fa90167718ef65ef41f04709ef9c985c86e2b35a0572b47a948172cf5b1a5dc23')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
