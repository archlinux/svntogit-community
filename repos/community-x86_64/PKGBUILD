# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=3.8.1
pkgrel=1
pkgdesc="The Cinnamon Session Handler"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('cinnamon-desktop' 'dbus-glib' 'libsm' 'libcanberra' 'xapps')
makedepends=('meson' 'xtrans')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${url}/commit/be5e7380ae69a2620ecf488250ce7e48d51c6338.patch")
sha256sums=('c5ddf1ee820a2d76f0515905b85a998e80fcbc586c1fd41ad2652aa18e1755fe'
            '5035cad8dec129b1a0635c560a359a7e6ec35c81ba98a7a314c842243b6d1ad1')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # fix more improperly defined paths from the meson port.
    patch -p1 < ../be5e7380ae69a2620ecf488250ce7e48d51c6338.patch
}

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/${pkgname} \
          --buildtype=plain \
          -Dwith-gconf=false \
          -Dwith-docbook=false \
          ..
    ninja
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" ninja install
}
