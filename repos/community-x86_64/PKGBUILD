# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=4.216
pkgrel=1
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/projects/verilator/wiki/Intro'
arch=('x86_64')
license=('LGPL')
depends=('perl')
optdepends=('systemc')
makedepends=('python' 'systemc')
source=("https://github.com/verilator/verilator/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e90dd57bfb4128feb51aa94bf590f84fb645e2560e5f928f7ff8999bd7c7c77d941cfd988ae8b05c1e61d6c9d54f857e122e307528f2a42700807ece3fc64856')

prepare() {
  cd verilator-$pkgver
  sed -i 's/#_MY_CXX_CHECK_SET(CFG_CXXFLAGS_STD_NEWEST,-std=c++17)/_MY_CXX_CHECK_SET(CFG_CXXFLAGS_STD_NEWEST,-std=c++17)/' configure.ac
  autoconf
}

build() {
  cd verilator-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator-$pkgver
  make test
}

package() {
  cd verilator-$pkgver
  make install DESTDIR="$pkgdir"
}
