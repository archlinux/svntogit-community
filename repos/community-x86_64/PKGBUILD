# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tools
pkgver=0.9.2
pkgrel=1
pkgdesc="CLI tool to inspect, manage and use OpenPGP cards"
arch=(x86_64)
url="https://gitlab.com/openpgp-card/openpgp-card"
license=(Apache)
depends=(ccid gcc-libs glibc gmp)
makedepends=(bzip2 cargo clang nettle pcsclite)
source=($url/-/archive/tools/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('8d77dd26c5011153168e0c725dda81b28005b7c9a8d6221f1c4f5cee581a93b75932967832d279b146d4a711c7b351dafece52c413e67798a5659cc839bfaca3')
b2sums=('91c1a838c4fd037513972c1e450cbadffc9e10862b9c07ca9253b756071082609fd903a36b043a83b545a231b4124604e2f4685933d68a384ea006765b785bc1')

prepare() {
  cd $pkgname-v$pkgver
  # add --locked once there is a lock file: https://gitlab.com/openpgp-card/openpgp-card/-/issues/63
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --bin opgpcard
}

check() {
  cd $pkgname-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libhogweed.so libnettle.so
    pcsclite libpcsclite.so
  )

  cd $pkgname-v$pkgver
  install -vDm 755 target/release/opgpcard -t "$pkgdir/usr/bin/"
  install -vDm 644 {tools/scripting,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
