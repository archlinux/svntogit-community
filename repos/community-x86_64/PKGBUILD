# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=5.1.1.25
pkgrel=4
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
# startdde: for com.deepin.SessionManager
depends=('deepin-qt5integration' 'deepin-anything' 'disomaster' 'file' 'gio-qt' 'libmediainfo' 'avfs' 'polkit-qt5' 'poppler'
         'ffmpegthumbnailer' 'jemalloc' 'kcodecs' 'startdde' 'taglib')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-movie' 'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'deepin-compressor: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz"
         deepin-file-manager-qt5.15.patch)
sha512sums=('95ce6d9ae846adc54129fe06deba3b8e3b80fd0e706c822d5714f27d5402d2e4d5ba8b78033e46f64079f5388c4a02ed674d87b356babba12529c3ca70ac3551'
            '2b58868fdaee9d42f8ba2029bcaa727c146c64b219faa52bd2df1c1483290e95a7cb6622227a24d35f7d7167061d264839bfeb47872d84519097b47af036daf7')

prepare() {
  cd dde-file-manager-$pkgver
  sed -i 's|systembusconf.path = /etc/dbus-1/system.d|systembusconf.path = /usr/share/dbus-1/system.d|' dde-file-manager-daemon/dde-file-manager-daemon.pro

  patch -p1 -i ../deepin-file-manager-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
