# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.46.1
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('i686' 'x86_64')
url="http://msp.ucsd.edu/software.html"
license=('custom:BSD')
depends=('jack' 'tk') # fftw
provides=('puredata')
source=("http://msp.ucsd.edu/Software/pd-$_ver.src.tar.gz"
        "enable-fftw3.patch")
md5sums=('d25ff57bf3b567b91705a86540262290'
         'bb907907a9b33f6c63312274c8523d40')

prepare() {
  cd pd-$_ver

  # fftw3 patch
  patch -p1 -i ../${source[1]}

  # strip weakjack.h (cause segfault using Jack API)
  sed -i 's_.*weakjack_//&_' src/s_audio_jack.c
}

build() {
  cd pd-$_ver
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --disable-portaudio
# WIP: FFTW support is broken due to GCC 4.9
#              --enable-fftw
  make
}

package() {
  cd pd-$_ver
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/pd/LICENSE"
}
