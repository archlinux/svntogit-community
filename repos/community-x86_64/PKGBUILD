# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cabal-plan
pkgname=cabal-plan
pkgver=0.7.2.0
pkgrel=207
pkgdesc="Library and utility for processing cabal's plan.json file"
url="https://github.com/hvr/cabal-plan"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-async' 'haskell-base-compat'
         'haskell-base16-bytestring' 'haskell-cabal-install-parsers' 'haskell-optics-core'
         'haskell-optparse-applicative' 'haskell-semialign' 'haskell-singleton-bool' 'haskell-tar'
         'haskell-these' 'haskell-topograph' 'haskell-vector' 'haskell-zlib')
makedepends=('ghc' 'uusi')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        cabal-plan-aeson-2.patch)
sha256sums=('233a3d1460732fe3486169023758e431d37c5c9156fb28d829decaf7b9170f85'
            '9247a90d0098e6388feb6b1046894defacfa2d1c51136b838be25f04a011e0fc')

prepare() {
  cd $_hkgname-$pkgver
  patch -p1 -i ../cabal-plan-aeson-2.patch
  uusi -u aeson -u base -u base-compat -u Cabal -u optics-core -u optparse-applicative -u semialign -u vector $_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' \
    -fexe -flicense-report

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.GPLv2
}
