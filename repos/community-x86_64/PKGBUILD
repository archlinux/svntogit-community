# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: nfnty <arch@nfnty.se>

pkgname=shotcut
pkgdesc='Cross-platform Qt based Video Editor'
pkgver=23.05.07
pkgrel=1
arch=('x86_64')
url='https://www.shotcut.org'
license=('GPL3')
depends=('qt6-base' 'qt6-declarative' 'qt6-multimedia' 'qt6-websockets' 'qt6-translations'
         'mlt' 'movit' 'ffmpeg' 'libx264' 'libvpx' 'lame' 'frei0r-plugins' 'ladspa')
optdepends=('swh-plugins: Several audio filters')
makedepends=('qt6-tools' 'cmake' 'ninja' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mltframework/shotcut/archive/v$pkgver.tar.gz")
sha512sums=('3256df5e9f930b98224ebd419dacfd6e31d59397f216bd85ed24b899da322734225c549ed92f056a80fdd26e07b1dcdfec0fe0a4c637b882b151e576320df1a2')

prepare() {
  cd "${pkgname}-${pkgver}"

  sed -e 's|${Qt6_LUPDATE_EXECUTABLE}|/usr/lib/qt6/bin/lupdate|' -i translations/CMakeLists.txt
}

build() {
  cd "${pkgname}-${pkgver}"

  # https://github.com/mltframework/shotcut/issues/1275
  export CXXFLAGS+=" -DSHOTCUT_NOUPGRADE"

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSHOTCUT_VERSION=${pkgver}
  cmake --build build --verbose

  ninja -C build translations
}

package() {
  cd "${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ft=sh sw=2 sts=2 et:
