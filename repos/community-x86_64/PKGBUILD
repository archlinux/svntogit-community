# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=22.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=(x86_64)
url="https://www.rosegardenmusic.com/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc lirc hicolor-icon-theme qt5-base zlib)
makedepends=(alsa-lib cmake dssi fftw jack ladspa liblrdf liblo
libsamplerate libsndfile qt5-tools)
checkdepends=(lilypond python tex-gyre-fonts ttf-dejavu xorg-server-xvfb)
optdepends=(
  'cups: Printing support'
  'evince: Print preview support'
  'lilypond: Notation display support'
  'mupdf: Print preview support'
  'okular: Print preview support'
  'timidity++: Softsynth support'
  'flac: FLAC support'
  'wavpack: Lossless audio codec support'
)
provides=(dssi-host ladspa-host)
options=(debug)
source=(
  https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
  $pkgname-22.12-fix_static_init_order.patch
)
sha512sums=('b9397ddd51bc2a2efa249377a61b8c14e902c495ebf52eb72f5083d10a5fc7136e249e8570f7110efb8acf3fbaae1b424e6bf33758168571ec407804ae8acdfe'
            '11c256cc18805f6d392486e26ed654d75104d1b53857bba263b9c442a604221ffacb7141ff64c7fc872e29f48f14bdc05b507003d13d893faaa5d1705027cd79')
b2sums=('589863458b94876659ba64e65ff2e148aa7865c2d468bb7567fabf27af15f4026511bf6a2c06c103e73d0c94c7840146382261cb4952ae068605bdec05feaa6a'
        '803e4f3d80d3137c2198d4cbe5d9a62313925f7cc1b60bdaefb9c92f3ae29c5f27a2527b40331fcdd3616e4567aa52049b77838605e4f7cb4687f64616903c2e')

prepare() {
  # fix issue with static init ordering: https://sourceforge.net/p/rosegarden/bugs/1647/
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-22.12-fix_static_init_order.patch
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  cmake --build build
}

check() {
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="${PWD}/test-tmp"
  # disable broken lilypond_export_test: https://sourceforge.net/p/rosegarden/bugs/1640/
  # disable tests incompatible with debug symbols: https://sourceforge.net/p/rosegarden/bugs/1641/
  xvfb-run ctest --output-on-failure --test-dir build/test -E '^(lilypond_export_test|test_notationview_selection|convert)$'
}

package() {
  depends+=(
    alsa-lib libasound.so
    fftw libfftw3f.so
    jack libjack.so
    liblrdf liblrdf.so
    liblo liblo.so
    libsamplerate libsamplerate.so
    libsndfile libsndfile.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,CHANGELOG,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
