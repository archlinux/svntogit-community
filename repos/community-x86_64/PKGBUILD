# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mikael Eriksson <mikael_eriksson@miffe.org>
# Originally by: Denis Smirnov <detanator@gmail.com>

pkgname=intel-gpu-tools
pkgver=1.19
pkgrel=2
pkgdesc="Tools for development and testing of the Intel DRM driver"
arch=(i686 x86_64)
license=(MIT)
url='http://cgit.freedesktop.org/xorg/app/intel-gpu-tools/'
depends=(libdrm libpciaccess cairo python xorg-xrandr procps-ng kmod libxv)
makedepends=(swig gtk-doc xorg-util-macros dri2proto)
source=(https://xorg.freedesktop.org/releases/individual/app/$pkgname-$pkgver.tar.bz2{,.sig}
        segfault.patch::https://cgit.freedesktop.org/xorg/app/intel-gpu-tools/patch/?id=eeff6a1d9c4e2c195b30ad95ee36a58ef6ca3387)
md5sums=('4fdfa56acca3b046fc61fb12686656f3'
         'SKIP'
         '9dd72f324347d5535c05f5a9910b9620')
validpgpkeys=('775965B85650195ACE77E18D7370055DB74C2475')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i $srcdir/segfault.patch
}

build() {
  cd $pkgname-$pkgver
  export LANG=en_US.UTF-8
  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-tests --without-libunwind
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/intel-gpu-tools/COPYING"
}
