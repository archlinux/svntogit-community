# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=r2ghidra
_gitcommit=741a26aac3f463da8d528b5c68827e174a53b83d
_gitcutter=86ee0a9caedc9d515d62738d4b33aaa9e346ac2f
pkgver=5.1.1
pkgrel=2
pkgdesc='Deep ghidra decompiler integration for radare2 and r2cutter'
url='https://github.com/radareorg/r2ghidra'
arch=('x86_64')
license=('LGPL3')
depends=('radare2' 'glibc')
makedepends=('git' 'cmake' 'qt5-base')
provides=('r2cutter-r2ghidra'
          'radare2-r2ghidra')
replaces=('r2ghidra-dec')
source=("git+https://github.com/radareorg/r2ghidra#commit=${_gitcommit}"
        "git+https://github.com/radareorg/r2cutter#commit=${_gitcutter}"
        radare-ghidra::git+https://github.com/radareorg/ghidra.git
        git+https://github.com/zeux/pugixml.git)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${pkgname}
  git submodule init
  git config submodule."third-party/pugixml".url "${srcdir}/pugixml"
  git config submodule."ghidra/ghidra".url "${srcdir}/radare-ghidra"
  git submodule update --recursive
  sed 's|RadareOrg/Cutter|radareorg/r2cutter|g' -i CMakeLists.txt
}

build() {
  cd ${pkgname}
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_CUTTER_PLUGIN=ON \
    -DBUILD_SLASPECS=ON \
    -DCUTTER_SOURCE_DIR="${srcdir}/r2cutter"
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}
  make -C build -j1 DESTDIR="${pkgdir}" install
  # move shared library into R2_LIBR_PLUGINS
  r2version=$(r2 -version|grep radare2|sed -E 's/radare2 ([0-9\.]+) .*/\1/')
  install -d "${pkgdir}/usr/lib/radare2"
  mv "${pkgdir}/usr/lib/radare2/last" "${pkgdir}/usr/lib/radare2/${r2version}"
  ln -s /usr/lib/radare2/${r2version} "${pkgdir}/usr/lib/radare2/last"
}

# vim: ts=2 sw=2 et:
