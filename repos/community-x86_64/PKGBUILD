# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Astro Benzene <universebenzene at sina dot com>
_pkgname='pytest-mpl'
pkgname="python-${_pkgname}"
pkgver=0.13
pkgrel=1
pkgdesc="Pytest plugin to help with testing figures output from Matplotlib"
arch=('x86_64')
url="https://github.com/matplotlib/pytest-mpl"
license=('BSD')
depends=('python-pytest' 'python-matplotlib')
makedepends=('python-setuptools' 'git')
checkdepends=('python-pytest-cov')
source=("${pkgname}::git+https://github.com/matplotlib/pytest-mpl.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
    cd "${srcdir}/${pkgname}"
    python setup.py bdist_egg
}

check() {
    cd "${srcdir}/${pkgname}"
    cp -r "${_pkgname/-/_}".egg-info build/lib/
    PYTHONPATH="${srcdir}/build/lib" pytest tests
}

package() {
    cd "${srcdir}/${pkgname}"
    install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
    install -D -m644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
    python setup.py install --root="${pkgdir}" --prefix=/usr --optimize=1
}
