# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=libdom
pkgver=0.0.1
pkgrel=1
pkgdesc='Implementation of the W3C DOM'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/libdom/'
license=('MIT')
depends=('hubbub>=0.2.0' 'libwapcaplet>=0.2.0' 'expat')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('2aa75861c8a8291b6b3d1a3856c65ca615677afeec64ccfdf35a113a292c36b1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  export CFLAGS="-Wno-error=unused-but-set-variable"
  make PREFIX=/usr COMPONENT_TYPE="lib-shared"
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
