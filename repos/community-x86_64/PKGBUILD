# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  hydro <hydro@freenet.de>

pkgname=libmediainfo
pkgver=0.7.93
pkgrel=1
pkgdesc="shared library for mediainfo"
arch=('i686' 'x86_64')
url="http://mediaarea.net"
license=('GPL')
depends=( 'curl' 'libmms' 'libzen')
makedepends=('libtool' 'automake' 'autoconf')
source=(https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.bz2
	libmediainfo-0.7.50-libmms.patch)
sha256sums=('9643c4624aeb6dbcf12aded240bc773272644f17bad5d7534fdda1ca62ee75b0'
            '0adfbac0efee13e1f58dcfa5d2e50b3bc30029a9b1cf812e2d62d1e3308beeeb')

build() {
  cd "$srcdir"/MediaInfoLib/Project/GNU/Library
  sh ./autogen.sh
  ./configure --prefix=/usr --enable-shared --disable-static --with-libcurl --with-libmms
  make clean
  (cd ../../.. && patch -p1 <"$srcdir"/libmediainfo-0.7.50-libmms.patch)
  make
}

package() {
  cd "$srcdir"/MediaInfoLib/Project/GNU/Library
  make DESTDIR="$pkgdir" install
  for i in MediaInfo MediaInfoDLL; do
    install -dm 755 "$pkgdir"/usr/include/$i
    install -m 644 "$srcdir"/MediaInfoLib/Source/$i/*.h "$pkgdir"/usr/include/$i
  done
  install -dm 755 "$pkgdir"/usr/lib/pkgconfig
  install -m 644 "$srcdir"/MediaInfoLib/Project/GNU/Library/libmediainfo.pc "$pkgdir"/usr/lib/pkgconfig
  sed -i -e 's|Version: |Version: '$pkgver'|g' "$pkgdir"/usr/lib/pkgconfig/libmediainfo.pc
}
