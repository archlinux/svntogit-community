# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname='opensearch-dashboards-notifications-plugin'
_pluginname='notifications'
pkgver=2.6.0.0
_dashboardsver=2.6.0
pkgrel=1
pkgdesc='OpenSearch Dashboards Notifications Plugin'
url='https://opensearch.org/docs/latest/notifications-plugin/index/'
arch=('x86_64')
license=('Apache')
depends=("opensearch-dashboards=${_dashboardsver}" 'coffeescript')
makedepends=('yarn' 'python' 'git' 'unzip')
options=('!strip' 'emptydirs')
source=(
  "git+https://github.com/opensearch-project/${_pluginname}.git#tag=${pkgver}"
  "git+https://github.com/opensearch-project/OpenSearch-Dashboards.git#tag=${_dashboardsver}"
)
sha256sums=('SKIP'
            'SKIP')

prepare() {
  nodeVersion="$(node -v)"
  # Yes, you support this version. You just don't know it yet.
  sed -i "s/    \"node\": \"[0-9\.]*\",/    \"node\": \"${nodeVersion:1}\",/" \
    "OpenSearch-Dashboards/package.json"
}

build() {
  mv "${_pluginname}/dashboards-${_pluginname}" "OpenSearch-Dashboards/plugins/${_pluginname}"
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn osd bootstrap
  yarn build # --skip-archive breaks the build
}

check() {
  cd "OpenSearch-Dashboards/plugins/${_pluginname}"
  yarn test:jest
}

package() {
  cd "${_pluginname}"
  install -Dm644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  cd "${srcdir}/OpenSearch-Dashboards/plugins/${_pluginname}/build"
  unzip "${_pluginname}-dashboards-${pkgver}.zip"

  install -dm755 "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  cp -r "opensearch-dashboards/"* "${pkgdir}/usr/share/opensearch-dashboards/plugins/"

  find "${pkgdir}/usr/share/opensearch-dashboards/plugins" -type d -empty -delete
}

# vim: ts=2 sw=2 et:
