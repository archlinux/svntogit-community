# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=doctest-parallel
pkgname=haskell-doctest-parallel
pkgver=0.2.4
pkgrel=4
pkgdesc="Test interactive Haskell examples"
url="https://github.com/martijnbastiaan/doctest-parallel#readme"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-glob' 'haskell-base-compat' 'haskell-code-page' 'haskell-extra'
         'haskell-ghc' 'haskell-ghc-paths' 'haskell-random' 'haskell-syb'
         'haskell-unordered-containers')
makedepends=('ghc' 'expac' 'haskell-hunit' 'haskell-quickcheck' 'haskell-hspec' 'haskell-hspec-core'
             'haskell-hspec-discover' 'haskell-mockery' 'haskell-setenv' 'haskell-silently'
             'haskell-stringbuilder')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('7792d0846c56e52e4d9c10e956b1f26a3945bf795b9b3260dd25c851d586ee3b')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  # == An ugly hack to generate .ghc.environment without cabal-install ==
  # doctest-parallel relies on this cabal-install feature to configure GHCi.
  # https://github.com/martijnbastiaan/doctest-parallel/issues/22
  _ghc_env_filename=.ghc.environment.$CARCH-linux-$(expac %v ghc | cut -d - -f 1)
  echo -e "package-db dist/package.conf.inplace" > $_ghc_env_filename
  ls dist/package.conf.inplace/*.conf | sed 's|.*/\(.*\).conf$|package-id \1|' >> $_ghc_env_filename
  ls /usr/lib/ghc-9.0.2/package.conf.d/*.conf | sed 's|.*/\(.*\).conf$|package-id \1|' >> $_ghc_env_filename

  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
