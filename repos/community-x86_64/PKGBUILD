# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=blosc2
pkgver=2.6.1
pkgrel=1
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(x86_64)
url="https://www.blosc.org"
license=(BSD)
depends=(lz4 zlib zstd)
makedepends=(cmake)
source=(https://github.com/Blosc/c-blosc2/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('9f5daa59d5191784ee91deb18ed15a3b36504b05061ca8b1532eaf8e402c793944059894b277843e8da19f00af05615e0865d9fdb80decd83aa07867b0e661a0')
b2sums=('a3fdc8798eb70e97791b7b708ef872142d38b9c4fdb8efc6d5da2010149fa61f4115f27af630a43edd5f351cb8693fbb79457f19ccf1a518f057902b93196b3d')

build() {
  cmake -B build -S c-blosc2-$pkgver \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D BUILD_STATIC=OFF \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 c-blosc2-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
