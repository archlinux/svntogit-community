# Maintainer: David Runge <dave@sleepmap.de>

pkgname=eteroj.lv2
pkgver=0.10.0
pkgrel=2
pkgdesc="Open Sound Control for LV2"
arch=(x86_64)
url="https://git.open-music-kontrollers.ch/~hp/eteroj.lv2"
license=(Artistic2.0)
groups=(
  lv2-plugins
  pro-audio
)
depends=(
  glibc
  lv2-host
)
makedepends=(
  lv2
  meson
  sord
)
checkdepends=(lv2lint)
source=(
  $pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz
  $pkgname-$pkgver.tar.gz.asc::$url/archive/$pkgver.tar.gz.asc
)
sha512sums=('042a32a41fa2577a9220fdc20b0376c449d21b12fcb853477b7b17b48ea70f330fd0b1b8b1c357a7c660999eae6439d9b066ab0dd07bed9a23a15e70128fafab'
            'SKIP')
b2sums=('b58cd91f137ca9fc04a18c4f5e56b40ea7bac5fc3736a24e16a4e7e7477a1086bda7ed63549112b9026710065dfad6584effa81886452e11f0f5e018951c8025'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
