# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Andri Yngvason <andri@yngvason.is>

pkgname=neatvnc
pkgver=0.5.3
pkgrel=1
pkgdesc='Fast and neat VNC server library'
arch=(x86_64)
url=https://github.com/any1/neatvnc
license=(custom:ISC)
depends=(
  glibc
  gnutls
  libaml.so
  libavcodec.so
  libdrm
  libpixman-1.so
  libturbojpeg.so
  mesa
  zlib
)
makedepends=(
  git
  meson
  ninja
)
provides=(libneatvnc.so)
_tag=29ce32732f5c98cede0ae80bb6fb69101a6fe84d
source=(git+https://github.com/any1/neatvnc.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd neatvnc
  git describe --tags | sed 's/^v//'
}

build() {
  arch-meson neatvnc build \
    -Djpeg=enabled \
    -Dtls=enabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 neatvnc/COPYING -t "${pkgdir}"/usr/share/licenses/neatvnc
}

# vim: ts=2 sw=2 et:
