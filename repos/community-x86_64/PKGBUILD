# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20220501
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
source=("https://github.com/txthinking/brook/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('99b9974622475b39b83527216b3bd751a74518bc921030ec5f4a71d2cb7163992a9e3b6f7e3ace159d3bdcb592d140a85465d3300db00156ab363925945e0903')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd brook-$pkgver/cli/brook
  go build -o brook
}

package() {
  cd brook-$pkgver/cli/brook
  install -Dm755 brook -t "$pkgdir"/usr/bin/
}
