# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=unshield
pkgver=1.4.2
pkgrel=1
pkgdesc="Extracts CAB files from InstallShield installers"
arch=('i686' 'x86_64')
url="https://github.com/twogood/unshield"
license=('custom')
depends=('zlib' 'openssl')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/twogood/unshield/archive/$pkgver.tar.gz")
sha256sums=('5dd4ea0c7e97ad8e3677ff3a254b116df08a5d041c2df8859aad5c4f88d1f774')

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    .
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/unshield/LICENSE
}
