# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: <karolina.lindqvist@kramnet.se>

pkgname=(algol68g algol68g-doc)
pkgver=3.0.8
pkgrel=1
pkgdesc='Algol 68 compiler and interpreter'
arch=(x86_64)
url='https://jmvdveer.home.xs4all.nl/algol.html'
license=(GPL)
depends=(gsl plotutils)
optdepends=('postgresql-libs: for postgresql support')
source=("https://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
        'https://ftp.tw.freebsd.org/distfiles/a68g-doc.pdf')
b2sums=('8d5e0150237297e7e084094e90444b6cab5d60ac187a474c08b3f728a05a7762f8736a9510edef38295a4f1641386acf5235913e21c78cb88a78e26c9538533c'
        'f8ca710d9a4f611873de41785e70359bd566645268ebb848803d3cd2e9a9f873f8ac423e3e08699998083444b7f6d98df32fdb7a4c6b36692462ee50ddb1e410')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package_algol68g() {
  make -C $pkgname-$pkgver \
    DESTDIR="$pkgdir" docdir=/usr/share/doc/$pkgname install
}

package_algol68g-doc() {
  install -Dm644 a68g-doc.pdf \
    "$pkgdir/usr/share/doc/algol68g/learning_algol.pdf"
}
