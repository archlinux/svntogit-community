# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SmackleFunky <smacklefunky@optusnet.com.au>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=dictd
pkgver=1.12.0
pkgrel=3
pkgdesc="Online dictionary client and server"
url="http://sourceforge.net/projects/dict/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('sh' 'zlib' 'libmaa')
makedepends=('flex')
backup=(etc/dict/dictd.conf
	etc/dict/dict.conf
	etc/dict/site.info
	etc/conf.d/dictd
	etc/xinetd.d/dictd
	etc/dict/colorit.conf)
options=('!emptydirs')
source=("http://downloads.sourceforge.net/dict/$pkgname-$pkgver.tar.gz"
	dictd
	dict.conf
	dictd.conf
	dictd.confd
	site.info
	dictd.xinetd
	colorit.conf)
md5sums=('71aaf3a697ea2e9409643f11ea85b53a'
         '6542df4fc585773fc03f1a088730126b'
         'fb72f7d8c55f84b1b37bbc8d33a8e55a'
         '64cd6ffdb40fb3367224f91ac44926ac'
         '838887fee38ae4cebd652d399b1f2266'
         'b8cabf913876e544d919c7f20ff8db9e'
         'b7dbc9529df13ff895dae2b4abd080d6'
         '978c9c079a0fc8d7d42a4cd4000190e7')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --enable-dictorg --prefix=/usr --sysconfdir=/etc/dict
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m 0755 $srcdir/dictd $pkgdir/etc/rc.d/dictd
  install -D -m 0644 $srcdir/dictd.xinetd $pkgdir/etc/xinetd.d/dictd
  install -D -m 0644 $srcdir/dictd.conf $pkgdir/etc/dict/dictd.conf
  install -D -m 0644 $srcdir/dict.conf $pkgdir/etc/dict/dict.conf
  install -D -m 0644 $srcdir/site.info $pkgdir/etc/dict/site.info
  install -D -m 0644 $srcdir/dictd.confd $pkgdir/etc/conf.d/dictd
  install -D -m 0644 $srcdir/colorit.conf $pkgdir/etc/dict/colorit.conf
}
