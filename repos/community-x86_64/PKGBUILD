# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=vivid
pkgver=0.7.0
pkgrel=1
pkgdesc='LS_COLORS manager with multiple themes'
arch=('x86_64')
url=https://github.com/sharkdp/vivid
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$url/archive/v$pkgver/vivid-v$pkgver.tar.gz")
b2sums=('2f1cdf94cbab5163b078227b0200c61e05da32d542d9989eae58c5d757df557c074aa9c26cc7049a513cb5d55b01c0942aef82154d762169b02e38f37ebde94d')

build() {
  cd vivid-$pkgver
  cargo build --release --locked
}

package() {
  cd vivid-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/vivid
  install -Dm644 -t "$pkgdir"/usr/share/licenses/vivid LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
