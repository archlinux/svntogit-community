# Maintainer: Torsten Ke√üler <tpkessler at archlinux dot org>
# Contributor: Konstantin Gizdov <arch at kge dot pw>

pkgname=intel-oneapi-compiler-shared
pkgver=2023.1.0
_debpkgrel=46305
pkgrel=1
pkgdesc='Intel oneAPI compiler runtime libraries'
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("custom")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${pkgver}-${pkgver}-${_debpkgrel}_amd64.deb")
b2sums=('c6e3aeb4df24d4411531a618b88841c5ef563b7cff6a76d5269cc1f039d54536499a5ae7d0eae1f36702937ca0d77dfe8441359122d5797010beda89f49c4300')
depends=('intel-oneapi-compiler-shared-common' 'intel-oneapi-compiler-shared-runtime'
         'intel-oneapi-dpcpp-debugger' 'intel-oneapi-common')
provides=("${pkgname}-common")
conflicts=('intel-oneapi-basekit')

noextract=(
    "${pkgname}-${pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
)

package() {
    cd "${srcdir}"
    ar x "${srcdir}/${pkgname}-${pkgver}-${pkgver}-${_debpkgrel}_amd64.deb"
    tar xvf data.tar.xz -C "${pkgdir}"
    rm data.tar.xz
    # Remove symlink already created by intel-oneapi-compiler-shared-runtime
    rm "${pkgdir}/opt/intel/oneapi/compiler/${pkgver}/linux/compiler/lib/intel64"

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
