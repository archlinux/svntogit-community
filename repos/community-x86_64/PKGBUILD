# Maintainer: David Runge <dave@sleepmap.de>
pkgname=spectmorph
pkgver=0.4.0
pkgrel=3
pkgdesc="A free software project which allows to analyze samples of musical instruments, and to combine them (morphing)"
arch=('x86_64')
url="http://spectmorph.org/"
license=('LGPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'fftw' 'jack' 'libao' 'qt5-base')
makedepends=('lv2')
optdepends=('python2: For sminstbuilder')
source=("http://spectmorph.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        "http://spectmorph.org/files/instruments/${pkgname}-instruments-${pkgver}.tar.xz")
sha512sums=('6b8139e9ee7b60889a78a21b03ae1ad06bc8b1d75b8bfdf72af64dc812028f4e83cfd8ed20af834a90ed5d62ee7234257db198371f90a909c72ca897aa17be3d'
            '94fa1bc39f36677627b1f8699dc7e65be2567fedaf18b762ab5de83a2ffd8ef683e49b62a8a779346b359eb1438cbd0dd88b0bfb12e7fcdfb8f6e3b30ac1d495')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-lv2 \
              --with-jack \
              --with-qt
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # standard instruments
  install -vDm 644 ../instruments/standard/* \
    -t "${pkgdir}/usr/share/${pkgname}/instruments/standard/"
  # docs
  install -vDm 644 {AUTHORS,DESIGN,NEWS,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
