# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.15.19
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=(x86_64)
url="https://github.com/flacjacket/pywlroots"
license=(custom:UIUC)
depends=(glibc python-cffi python-pywayland python-xkbcommon wlroots
libwlroots.so)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
provides=(pywlroots)
conflicts=(pywlroots)
replaces=(pywlroots)
options=(debug)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('8edc3096797fff468e8db628e54d05d65c5e3589b2f3546bdb3322ae3d800477a3d879425e6e85cae59a4c00f83cc9f3a80092cae3df949a59f985270cb8e975')
b2sums=('c1149ba3936d596f759886e5c4ceaf2af9f1a2af0820ecf67085b7291e1c66717b8ce40c15979ce7a28297abcc6cb6597c6e72397377fc4715276a6b46ebb2d5')

build() {
  cd $_name-$pkgver
  python wlroots/ffi_build.py
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
