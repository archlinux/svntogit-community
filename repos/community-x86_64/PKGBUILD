# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=xchm
pkgver=1.27
pkgrel=2
pkgdesc="Cross-platform GUI front-end to chmlib"
arch=('x86_64')
url="https://github.com/rzvncj/xCHM"
license=('GPL')
depends=('chmlib' 'wxgtk3')
source=("https://github.com/rzvncj/xCHM/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"
        xchm.desktop
        xchm.appdata.xml)
sha256sums=('12989099d242282c43c709bf917da386e57c947f2d8f7dcfd1d0d728b6999650'
            '3d2eb40ca611bd4b27e20e70a18f506658db83f65d9289333c7af46f53c6bfc4'
            '5e785053bfa99f3f64efdcbcdebdb3800b61fa0c90eaa7e11b484b710922f91c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 art/xchm-16.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/xchm.png"
  install -Dm644 art/xchm-32.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/xchm.png"
  install -Dm644 art/xchm-48.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/xchm.png"
  install -Dm644 art/xchm-128.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/xchm.png"
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
  rm -r "$pkgdir"/usr/share/pixmaps
}
