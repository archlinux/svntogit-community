# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=2.5.1
pkgrel=2
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=(x86_64)
url="https://github.com/ocaml/dune"
license=(Apache)
depends=(glibc ocaml ocaml-findlib)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d7d3df74c33ef9b2d1fb95c3150e91056b69aefbad44542bd4af3ef310038a3b083fb178065ec0fbb5eb3dd564578d04d37944ec022fb92fe5d437a6ca7ec3fa')

build() {
    cd ${pkgname}-${pkgver}
    dune_release_pkgs='dune,dune-action-plugin,dune-build-info,dune-configurator,dune-glob,dune-private-libs'
    make dune.exe
    ./dune.exe  build -p $dune_release_pkgs --profile dune-bootstrap
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd ${pkgname}-${pkgver}

    # make DESTDIR="${pkgdir}" INSTALL_ARGS="--prefix=/usr --libdir='$(ocamlfind printconf destdir)'" install
    DESTDIR="${pkgdir}" ./dune.exe install --prefix "/usr" --libdir=$(ocamlfind printconf destdir)
    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
}
