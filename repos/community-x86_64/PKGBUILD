# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.18.0
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('6dbe2a25c50f10751caa371bbde44443e27fe3e2f33bd57d306be8b16f0ac5f1992a0627639674fb9841106bcd362b9f6527aab1cc7bdfa710fa69374d8d2de6')
b2sums=('65ced1ebc676845918879853cf810cd24262f7ebfac5f28727ae8595982989ed1099f8184365888f83b9a8421168c9f910811d0899ff1b67750b220283142d7a')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
