# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: William Giokas <1007380@gmail.com>

pkgname=pypy
pkgver=5.10.0
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('x86_64')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'tk' 'sqlite' 'mercurial' 'python2-pycparser')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
source=("https://bitbucket.org/pypy/pypy/downloads/pypy2-v${pkgver}-src.tar.bz2")
sha512sums=('11892418d753ddc59e928a5f59b3406b7c22b8689ad6995eeb183182dbb5184117e68bb567c770ffa60a14d6763550d658e260584e65da5025ed481f48501379')

build() {
  cd pypy2-v${pkgver}-src/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone

  # Compile binary modules
  PYTHONPATH=../.. ./pypy-c ../tool/build_cffi_imports.py
}

package() {
  cd pypy2-v${pkgver}-src

  # Prepare installation
  python2 pypy/tool/release/package.py --archive-name pypy --targetdir .
  mkdir unpacked
  tar xf pypy.tar.bz2 -C unpacked

  # Install pypy
  cd unpacked
  install -Dm755 pypy/bin/pypy "${pkgdir}"/opt/pypy/bin/pypy
  install -Dm755 pypy/bin/libpypy-c.so "${pkgdir}"/usr/lib/libpypy-c.so
  cp -r pypy/include pypy/lib_pypy pypy/lib-python pypy/site-packages "${pkgdir}"/opt/pypy/
  cd ..

  # Install symlink
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy/bin/pypy "${pkgdir}"/usr/bin/pypy

  # Install misc stuff
  install -Dm644 README.rst "${pkgdir}"/opt/pypy/README.rst
  install -Dm644 LICENSE "${pkgdir}"/opt/pypy/LICENSE
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy/LICENSE
}
# vim: ts=2 sw=2 et:
