# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Johan Rehnberg <cleanrock@gmail.com>

pkgname=naev
pkgver=0.9.0
pkgrel=1
pkgdesc='2D action/rpg space game'
arch=('x86_64')
url="https://github.com/naev/naev"
license=('GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libgl' 'libxml2' 'freetype2'
         'libpng' 'libzip' 'luajit' 'glpk' 'physfs' 'libunibreak' 'suitesparse' 'openblas')
makedepends=('freeglut' 'zip' 'meson' 'git' 'python-yaml')
source=("https://github.com/naev/naev/releases/download/v$pkgver/naev-$pkgver-source.tar.xz")
sha512sums=('68495661070b9e37000309ed0fb7f14e8a56d66dea27cbe79189a81cdb6f1a297d159753354850dc5450d5546f846245706a5191e4ecb450d41a30f5bf1841cf')

build() {
  cd "$pkgname-$pkgver"

  meson setup -Dprefix=/usr -Dndata_path=/usr/share/naev/ndata build .
  cd build
  meson compile
}

package() {
  cd "$pkgname-$pkgver/build"

  DESTDIR="$pkgdir" meson install
}
