# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lv2lint
pkgver=0.12.0
pkgrel=1
pkgdesc="Check whether a given LV2 plugin is up to the specification"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/lv2lint/"
license=('Artistic2.0')
depends=('glibc' 'libelf')
makedepends=('curl' 'lilv' 'lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('253d0af321e3f31347c13a877b7924c05c483788fb0b5105a13c4402e9029ca551ddd1d0fca338add119ebdd6972dc1e16a192b739c480ed4763fb14990d3e2a'
            'SKIP')
b2sums=('a3e365b2a7068a0c6734a554d466896f09fb125f842f189ea42cf3d61ed209cf9123e9c6f9616dc095b82663ccca98d507d49fcda63cb151bd835d6650a4fa49'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  arch-meson \
        -Donline-tests='enabled' \
        -Delf-tests='enabled' \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  depends+=('libcurl.so' 'liblilv-0.so')
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir/" ninja -C build install
  install -vDm 644 {ChangeLog,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
