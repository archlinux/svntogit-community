# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Storyteller <spiralsorrow@hotmail.com>

pkgname=lmms
pkgver=1.1.3
pkgrel=1
pkgdesc='The Linux MultiMedia Studio.'
arch=('i686' 'x86_64')
url='http://lmms.sourceforge.net/'
license=('GPL')
depends=('sdl' 'glib2' 'fluidsynth' 'libpng' 'libvorbis' 'libxft' 'libxinerama' 'qt4' 'sdl_sound'
         'libsamplerate' 'shared-mime-info' 'fltk')
optdepends=('wine: VST support (experimental)'
            'fftw: SpectrumAnalyzer plugin'
            'stk: STK instruments plugins'
            'pulseaudio: PulseAudio output')
makedepends=('cmake' 'ladspa' 'libxft' 'freetype2' 'fftw' 'stk' 'wine')
makedepends_x86_64=('gcc-multilib')
provides=('lmms-extras')
conflicts=('lmms-extras')
options=('!makeflags')
install=lmms.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/LMMS/${pkgname}/archive/v${pkgver}.tar.gz")
md5sums=('88d9e66d240b711c37315e3c9da644a1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|lib64|lib|g' cmake/modules/DetectMachine.cmake

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -USE=vst
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
