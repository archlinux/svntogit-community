# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-shortcut-overlay
pkgver=1.2.0
pkgrel=1
pkgdesc='A native, OS-wide shortcut overlay'
arch=(x86_64)
url=https://github.com/elementary/shortcut-overlay
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
)
makedepends=(
  git
  meson
  vala
)
_tag=d9ba4fcff6a8dfab17bdb1229e488047be8fccb5
source=(pantheon-shortcut-overlay::git+https://github.com/elementary/shortcut-overlay.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-shortcut-overlay
  git describe --tags
}

build() {
  arch-meson pantheon-shortcut-overlay build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
