# Maintainer: David Runge <dave@sleepmap.de>

_name=Gem
pkgname=pd-gem
pkgver=0.94
pkgrel=1
pkgdesc="Graphics Environment for Multimedia"
arch=('x86_64')
url="https://github.com/umlaeute/Gem"
license=('GPL2')
groups=('pd-externals')
# TODO: potentially add gmerlin, libmpeg3 and glewmx (if that's still a thing)
depends=('assimp' 'ftgl' 'glew' 'libdv' 'libiec61883' 'libquicktime' 'pd'
'sdl2' 'ttf-dejavu' 'zlib')
makedepends=('imagemagick' 'libvncserver' 'vlc')
optdepends=('vlc: video capture support'
            'libvncserver: video capture support'
            'imagemagick: image processing support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/umlaeute/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('356e4f2a27131f927cd5d20ba9010088cc22930f313f61f54dd107db041b6cb2042ab5d530bae069e3ccdccd0a8fed1152631c6f187f4f4602f37d763356d316')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-default-font=/usr/share/fonts/TTF/DejaVuSans.ttf
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
