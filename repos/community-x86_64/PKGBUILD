# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Andreas Wagner <andreas.wagner@lowfatcomputing.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=icmake
pkgver=10.03.01
pkgrel=1
pkgdesc='A program maintenance (make) utility using a C-like grammar'
arch=('x86_64')
url='https://fbb-git.gitlab.io/icmake/'
license=('GPL3')
source=("https://gitlab.com/fbb-git/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "disable-clear-screen.patch")
sha256sums=('4bd79717217d0f6d8de5f90d484fbc74120f74e9619d1cd547d569c688cb8308'
            'f141389f6a42ac8a66b8abcd1a2649e3d41cd661ab0c5fdafab0c37a8fa793ba')
depends=('libbobcat' 'libunwind')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/disable-clear-screen.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"

    export CXXFLAGS="${CXXFLAGS} -Wp,-U_GLIBCXX_ASSERTIONS -std=c++20"

    ./icm_prepare /
    ./icm_bootstrap /
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"

    ./icm_install all "${pkgdir}"

    # fix paths
    cd "${pkgdir}"/usr
    mv libexec lib
}

