# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tamarin-prover-utils
pkgname=haskell-tamarin-prover-utils
pkgver=1.4.1
pkgrel=35
pkgdesc="Utility library for the tamarin prover"
url="http://www.infsec.ethz.ch/research/software/tamarin"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-sha' 'haskell-base64-bytestring' 'haskell-blaze-builder'
         'haskell-dlist' 'haskell-fclabels' 'haskell-safe' 'haskell-syb')
makedepends=('ghc')
source=("tamarin-prover-$pkgver.tar.gz::https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver.tar.gz")
sha512sums=('4d3aeae02be5d430bff6c55d78656e3c9a648674d235bfeb36ce227a39abd3054a132b99ff8040abf05d8e26506fe85d21ecfb6fce73062dd45b5ba98d941144')

prepare() {
    cd tamarin-prover-$pkgver/lib/utils
    sed -i -e '/fail/d' -e '/MonadDisj/i instance Monad m => MonadFail (DisjT m) where\n    fail = error' src/Control/Monad/Trans/Disj.hs
}

build() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/utils"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/tamarin-prover-${pkgver}/lib/utils"
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
