# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: Jonathan Wiersma <arch aur at jonw dot org>
# Contributor: sracker <smb.sac@gmail.com>

pkgname=xdebug
pkgver=2.7.0RC1
pkgrel=1
pkgdesc="PHP debugging extension"
arch=('x86_64')
url="http://www.xdebug.org"
license=('GPL')
depends=('php')
backup=('etc/php/conf.d/xdebug.ini')
source=("https://xdebug.org/files/$pkgname-${pkgver}.tgz"
	'xdebug.ini')
sha256sums=('d401405c39460e5cff09103b0600989cd39d401d185b628f68f11d5c29a618ba'
            '7c66883dc2ade69069ef84e30188b25630748aa9c8b0dd123727c00505421205')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  phpize
  ./configure --prefix=/usr --enable-xdebug
  make

  cd "$srcdir"/$pkgname-$pkgver/debugclient
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/debugclient
  install -D -m 755 debugclient "$pkgdir"/usr/bin/debugclient

  cd "$srcdir"/$pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -D -m 644 "$srcdir"/xdebug.ini "$pkgdir"/etc/php/conf.d/xdebug.ini
}
