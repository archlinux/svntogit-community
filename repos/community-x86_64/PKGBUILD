# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-job-scheduler-plugin
pkgver=2.6.0.0
pkgrel=1
_opensearchver=2.6.0
_jdkver=11
pkgdesc="OpenSearch Job Scheduler Plugin"
arch=('x86_64')
url="https://github.com/opensearch-project/job-scheduler"
license=('Apache')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/job-scheduler/archive/${pkgver}.tar.gz"
)
sha256sums=('3664e715dbf774f7d8b4fc4b9447e60ca3001898ad7549c0bd40b949eac2685a')

build() {
  cd "job-scheduler-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  ./gradlew assemble \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-job-scheduler"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-job-scheduler"
  unzip "${srcdir}/job-scheduler-${pkgver}/build/distributions/opensearch-job-scheduler-${pkgver}.zip"

  install -Dm644 "${srcdir}/job-scheduler-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
