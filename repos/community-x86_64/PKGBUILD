# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Tharre <tharre3@gmail.com>

pkgname=age
pkgver=1.1.0
pkgrel=1
pkgdesc='A simple, modern and secure file encryption tool'
arch=('x86_64')
url="https://github.com/FiloSottile/$pkgname"
license=('BSD')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('4b7de3988934704f4d83b62fbed1923452c20bb4dca57be128b60cd3d262a48322f5325fd825898a76265ebd400ae9b87ce2b5a23b31c6d705a8ed9f7aff6e2f')

build() {
    cd "$pkgname-$pkgver"
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-X main.Version=$pkgver -linkmode external -extldflags \"$LDFLAGS\"" \
      -o . ./...
}

check() {
    cd "$pkgname-$pkgver"
    go test ./...
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm755 -t "$pkgdir/usr/bin/" age age-keygen
    install -Dm644 -t "$pkgdir/usr/share/man/man1/" doc/age.1 doc/age-keygen.1
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
