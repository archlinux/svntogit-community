# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.12.5
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('deepin-qt5integration' 'disomaster' 'karchive' 'kcodecs' 'libarchive' 'libzip' 'minizip' 'udisks2-qt5')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver.tar.gz"
        $pkgname-fix-ldflags.patch::https://github.com/linuxdeepin/deepin-compressor/pull/46.patch)
sha512sums=('da41ada90ef57c0b6f155b0f8e00c0788fc1a5db8a4a897e3c7555ce6b81520bed8660218afd072d0b49ebadd7e069c2769ca432192128b8e00f2aca882e058a'
            'cbc78863c920a21c4027a8177edf5fe3e5035ca5190811a4df98dcfe7aca2968f66e10961775cd83f2153997a90cab4f4498a783de86691c94325e1176136a7d')

prepare() {
  cd deepin-compressor-$pkgver
  patch -Np1 -i ../$pkgname-fix-ldflags.patch
}

build() {
  cd deepin-compressor-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-compressor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
