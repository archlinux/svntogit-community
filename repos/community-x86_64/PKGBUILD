# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=('python-scrypt' 'python2-scrypt')
pkgver=0.8.7
pkgrel=1
pkgdesc='Bindings for the scrypt key derivation function library'
arch=('x86_64')
url='https://pypi.python.org/pypi/scrypt/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
options=('!emptydirs')
source=(https://pypi.io/packages/source/s/scrypt/scrypt-${pkgver}.tar.gz)
sha512sums=('2919ceed50888bdfdad846db1ef5ec6ac8769ef00a6c44bbe2837318a4809854c2f4c957e7035593aa6cc47a42f431bb3a86004f9e21c1f33ade8a6f8f90be84')

prepare() {
  cp -a scrypt-${pkgver}{,-py2}
  sed 's|env python$|env python2|' -i scrypt-${pkgver}-py2/scrypt/scrypt.py
}

build() {
  (cd scrypt-${pkgver}
    python setup.py build
  )
  (cd scrypt-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd scrypt-${pkgver}
    python setup.py test
  )
  (cd scrypt-${pkgver}-py2
    python2 setup.py test
  )
}

package_python-scrypt() {
  depends=('python' 'glibc')
  cd scrypt-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-scrypt() {
  depends=('python2' 'glibc')
  cd scrypt-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
