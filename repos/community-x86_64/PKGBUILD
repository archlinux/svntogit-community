# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.7
pkgrel=1
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('x86_64')
url='https://goaccess.io'
license=('GPL')
depends=('ncurses' 'libmaxminddb' 'openssl')
optdepends=('geoip2-database: for geoip support')
backup=('etc/goaccess/goaccess.conf')
source=("https://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('4a1b742f5e0a1049ab15eb9e32241e5f4db3460ecad12bf03a114e2576878566fc38d30779ebe863f2f0429b47906b892f2cceacd07ba641038a8af9b05c1ba2')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip=mmdb --enable-utf8 --with-openssl
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}
