# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sauyon Lee <sauyonl@sauyon.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: korjjj <korjjj+aur@gmail.com>

pkgname=syncthing
pkgver=0.11.5
pkgrel=1
pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing"
url="http://syncthing.net/"
license=('MPLv2')
arch=('i686' 'x86_64')
depends=('glibc')
makedepends=('git' 'go' 'godep' 'inetutils' 'sed')
source=($pkgname-$pkgver::git+https://github.com/syncthing/syncthing.git#tag=v${pkgver}
        $pkgname.1)
sha1sums=('SKIP'
          '17be8b3f99c577e325765abc251f597e204d7711')

prepare() {
  cd "${srcdir}"
  mkdir -p "src/github.com/syncthing"
  mv "${pkgname}-${pkgver}" "src/github.com/syncthing/${pkgname}"
}

build() {
  export GOPATH="${srcdir}"
  cd "${srcdir}/src/github.com/syncthing/${pkgname}"
  go run build.go -no-upgrade build
}

check() {
  export GOPATH="${srcdir}"
  cd "${srcdir}/src/github.com/syncthing/${pkgname}"
  #go run build.go -no-upgrade test
}

package() {
  cd "${srcdir}/src/github.com/syncthing/${pkgname}"
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 "etc/linux-systemd/system/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
  install -Dm644 "etc/linux-systemd/user/${pkgname}.service" "${pkgdir}/usr/lib/systemd/user/${pkgname}.service"
  install -Dm644 "${srcdir}/${pkgname}.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"

# license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
