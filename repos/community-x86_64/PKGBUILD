# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

pkgname=python-igraph
pkgver=0.10.2
pkgrel=1
url='https://igraph.org/python/'
license=(GPL)
arch=(x86_64)
pkgdesc='Python bindings for the igraph library'
depends=(python-texttable igraph)
makedepends=(python-setuptools)
source=(python-igraph-$pkgver::https://files.pythonhosted.org/packages/source/i/igraph/igraph-$pkgver.tar.gz)
sha256sums=('215c53591b0bccd2082ef0e007e1919f25c040abb03014bdb4f20712173e33c2')

build() {
  cd igraph-$pkgver
  python setup.py build --use-pkg-config
}

check() {
  cd igraph-$pkgver
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd igraph-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1 
}
