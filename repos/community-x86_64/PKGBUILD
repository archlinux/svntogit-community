# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cargo-edit
pkgver=0.11.8
pkgrel=2
pkgdesc='Managing cargo dependencies from the command line'
url='https://github.com/killercup/cargo-edit/releases'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('cargo' 'libgit2.so' 'libssh2' 'openssl')
source=(https://github.com/killercup/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('09781398b62ef6746f6682ef98ed8f1137033d3785bba162fccb560adf293b963e102a088c6e30044e2082e149767753dfb70f32c28e75f09126ebf13e3a22b0')
b2sums=('4714269414ecc7509d8ee9dd28d705e56dc3b74486256ed95a8c93c86c6594ea1d708d5d36b3f0af9b2d363b7f09e137d08516c0929eb8df4074e387d88874b0')

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i '/\"vendored-libgit2\"/d' Cargo.toml
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=' -ffat-lto-objects'
  LIBSSH2_SYS_USE_PKG_CONFIG=1  cargo build --frozen --release
}

check() {
  cd ${pkgname}-${pkgver}
  # tests depend on target/debug/
  LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 \
    target/release/cargo-upgrade \
    target/release/cargo-set-version \
    -t "${pkgdir}/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
