# Maintainer: Johannes Löthberg <johannes@kyriasis.com>

pkgname=pacman-contrib
pkgver=1.1.0
pkgrel=1

pkgdesc="Contributed scripts and tools for pacman systems"
url="https://git.archlinux.org/pacman-contrib.git/about/"
arch=('x86_64')
license=('GPL')

depends=('pacman' 'fakeroot')
optdepends=('mlocate: pacdiff'
            'findutils: pacdiff')
makedepends=('asciidoc')

validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5') # Johannes Löthberg <johannes@kyriasis.com>
source=(https://sources.archlinux.org/other/community/pacman-contrib/pacman-contrib-$pkgver.tar.gz{,.asc})
sha256sums=('308c3b8dc15ed8bd419cba1eb3103afe9250cf415626334a0c3a753b550549a6'
            'SKIP')

build() {
  cd pacman-contrib-$pkgver

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var
  make
}

check() {
  make -C pacman-contrib-$pkgver check
}

package() {
  cd pacman-contrib-$pkgver

  make DESTDIR="$pkgdir" install
}
