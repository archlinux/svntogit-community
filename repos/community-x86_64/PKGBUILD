# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.0.59.1
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('GPL3')
provides=('deepin-qt5dxcb-plugin')
conflicts=('deepin-qt5dxcb-plugin')
replaces=('deepin-qt5dxcb-plugin')
options=('debug')
depends=('cairo' 'deepin-wayland' 'kwayland' 'qt5-x11extras')
makedepends=('expac' 'qt5-xcb-private-headers' 'libglvnd' 'libxcb')
groups=('deepin')
source=("https://github.com/linuxdeepin/qt5platform-plugins/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f6f88a680938d9d4c2b03f1d48dbd2ccf35c3df122fa0af6de86cae9cab7d8fab386220b9bf49b3d81da8597f869703919b1d47bab2867a356a52f3062697004')

prepare() {
  cd qt5platform-plugins-$pkgver

  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev

  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' xcb/linux.pri
  sed -i '/wayland/d' qt5platform-plugins.pro
}

build() {
  cd qt5platform-plugins-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5platform-plugins-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
