# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=5.10.40
pkgrel=1
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL3')
depends=('deepin-qt5integration' 'kcodecs' 'polkit-qt5' 'syntax-highlighting' 'libchardet' 'uchardet')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-editor/archive/$pkgver.tar.gz")
sha512sums=('c8d5e7286886f585e1d4e18d37f24f228d1b4102a6ff034ea29c91183962138327dde770213152f662f20860496c100338d61e23a5fab49e671dbaf70c19a8f9')

build() {
  cd deepin-editor-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  ninja
}

check() {
  cd deepin-editor-$pkgver
  ninja test || echo "Tests failed"
}

package() {
  cd deepin-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
