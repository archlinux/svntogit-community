# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.3.9
pkgrel=2

pkgdesc="Minimalistic document viewer"
url="http://pwmt.org/projects/zathura/"
arch=('x86_64')
license=('custom')

makedepends=('python-sphinx' 'texlive-bin' 'meson' 'ninja')
depends=('girara>=0.2.7' 'sqlite' 'desktop-file-utils' 'file')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support'
            'zathura-cb: Comic book support')

source=(https://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.xz
        bash-completion)

sha1sums=('23fd07120031f64a52c24eb90b6e0de9397ba011'
          '94a8236c483626a7f3acee053a1ea885aed45a82')

build() {
  cd zathura-$pkgver
  arch-meson -Denable-synctex=false build

  cd build
  ninja
}

package() {
  cd zathura-$pkgver/build
  DESTDIR="$pkgdir" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm0644 "$srcdir"/bash-completion "$pkgdir"/usr/share/bash-completion/completions/zathura
}
