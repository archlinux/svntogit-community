# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Gennadiy Mykhailiuta <gmykhailiuta@gmail.com>
# Contributor: Xuanwo <xuanwo@archlinuxcn.org>

pkgname=obsidian
pkgver=0.14.15
pkgrel=1
pkgdesc="A powerful knowledge base that works on top of a local folder of plain text Markdown files"
arch=('x86_64')
url="https://obsidian.md/"
license=('custom:Commercial')
depends=('zlib' 'hicolor-icon-theme' 'fuse' 'electron17')
makedepends=('asar')
options=(!strip)
source=(
    "${pkgname}.sh"
    "${pkgname}.desktop"
    "https://github.com/obsidianmd/obsidian-releases/releases/download/v${pkgver}/obsidian-${pkgver}.tar.gz"
)
sha256sums=('1a241233ac8ac2946f6ac05195afa66c60c954c03c7b169696f36d470f497f93'
            'be67713e873a5935d910efc6f6005c104b35c7b3a002dfcb5643fd185d1b0056'
            '6d22ede049e50adc5978a21a86beb5f2a32e2bda20521c67f8d28ed863e48ce4')

prepare() {
    cd "$srcdir"
    asar ef "${pkgname}-${pkgver}"/resources/obsidian.asar icon.png
}

package() {
    cd "$srcdir"

    # Install executable file
    install -Dm755 "$pkgname".sh "$pkgdir"/usr/bin/obsidian

    # Install desktop file
    install -Dm644 "$pkgname".desktop -t "$pkgdir"/usr/share/applications/
    install -Dm644 icon.png "$pkgdir"/usr/share/pixmaps/obsidian.png

    # Most of the release package is electron, but we use system's default one
    # So strip away asar packages and put them to /usr/lib/
    cd "${pkgname}-${pkgver}"/resources/
    install -dm0755 "$pkgdir"/usr/lib/obsidian
    find . -type d -exec install -d {,"$pkgdir"/usr/lib/obsidian/}{} \;
    find . -type f -exec install -D {,"$pkgdir"/usr/lib/obsidian/}{} \;
}
