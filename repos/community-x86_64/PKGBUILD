# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=sbv
pkgname=haskell-sbv
pkgver=7.4
pkgrel=1
pkgdesc="SMT Based Verification: Symbolic Haskell theorem prover using SMT solving"
url="http://leventerkok.github.com/sbv"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc' 'haskell-quickcheck' 'haskell-cracknum' 'haskell-async'
         'haskell-mtl' 'haskell-random' 'haskell-syb' 'haskell-data-binary-ieee754'
         'haskell-generic-deriving')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('73eac796c93fb43977428a476a3a63b844153c208b55dd3edb0dbc9e9426ffb9dbb3359d60ad423ae0260c6a0060f3b1c2408162b9ebd931c0a8bdcafd073058')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
