# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgbase=python-qrencode
pkgname=('python-qrencode' 'python2-qrencode')
pkgver=1.2
pkgrel=4
pkgdesc='Simple wrapper for the C qrencode library'
url='https://pypi.python.org/pypi/qrencode/'
arch=('x86_64')
license=('MIT')
makedepends=('python' 'python2' 'python-pillow' 'python2-pillow' 'qrencode' 'glibc')
source=(https://pypi.org/packages/source/q/qrencode/qrencode-${pkgver}.tar.gz)
sha512sums=('e6ebf0c2d2081408ef3d4f4a2a44712e86f70019e34da5de49c1a31a26d69cafac07afb501be24bc95a2525e038a47a74f02279981ade1d33b3e96d3dbb5ce5f')

package_python-qrencode() {
  depends=('python' 'python-pillow' 'qrencode' 'glibc')
  cd qrencode-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
}

package_python2-qrencode() {
  depends=('python2' 'python2-pillow' 'qrencode' 'glibc')
  cd qrencode-${pkgver}
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:
