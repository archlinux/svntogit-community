# Maintainer: Caleb Maclennan <caleb@alerque.com
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cocogitto
pkgver=4.1.0
pkgrel=1
pkgdesc="Set of CLI tools for the conventional commit and semver specifications"
arch=(x86_64)
url="https://github.com/$pkgname/$pkgname"
license=(MIT)
depends=(git
         gcc-libs
         libgit2
         libgit2.so
         zlib)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('0465267125058922462c197584007b6c92b08230acf21a5057faf5f4f5667d72')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	cargo build --frozen --release
}

check() {
	cd "$_archive"
    # Test suite is not atomic, relies on user environment such as git user configs
    # cargo test --frozen
}

package() {
	cd "$_archive"
	for bin in coco cog; do
		local target="target/release/$bin"
		install -Dm0755 -t "$pkgdir/usr/bin/" "$target"
		local gen="$target "
		[[ $bin == coco ]] && gen+='--completion' || gen+='generate-completions'
		$gen bash | install -Dm0644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/$bin"
		$gen fish | install -Dm0644 /dev/stdin "$pkgdir/usr/share/fish/vendor_completions.d/$bin.fish"
		$gen zsh |  install -Dm0644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_$bin"
	done
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
