# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=2.8.0
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL3')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
_commit='ef78f6c3ad11111e7a8345a4e2f33419bfdaf0e8'
source=("$pkgname::git+https://github.com/Canop/bacon.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  # untagged release
  # git describe --tags | sed 's/^v//'
  grep "^version" Cargo.toml | sed 's/version = \"\(.*\)"/\1/'
}

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
