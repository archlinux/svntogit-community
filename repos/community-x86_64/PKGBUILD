# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Henrik Nymann Jensen <h@henriknj.dk>

pkgname=pdnsd
pkgver=1.2.9.a
_pkgver=${pkgver/.a/a}
pkgrel=10
pkgdesc="Proxy DNS server with permanent caching"
arch=(i686 x86_64)
url="http://members.home.nl/p.a.rombouts/pdnsd/"
license=('GPL')
depends=('glibc')
install=${pkgname}.install
source=(http://members.home.nl/p.a.rombouts/pdnsd/releases/pdnsd-$_pkgver-par.tar.gz
	service)
md5sums=('2f3e705d59a0f9308ad9504b24400769'
         '750dc2c8274a0de95670758c7c34fd21')

build() {
  cd "$srcdir/$pkgname-$_pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --enable-ipv6
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 ../service "$pkgdir/usr/lib/systemd/system/pdnsd.service"

  install -dm0755 "$pkgdir/usr/share/doc/pdnsd"
  sed -i 's/"nobody"/"pdnsd"/' "$pkgdir/etc/pdnsd.conf.sample"
  mv "$pkgdir/etc/pdnsd.conf.sample" "$pkgdir/usr/share/doc/pdnsd/pdnsd.conf"

  chown -R 184:184 "$pkgdir/var/cache/pdnsd"
}
