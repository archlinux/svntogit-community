# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=lychee
pkgver=0.11.1
pkgrel=2
pkgdesc='Fast, async, resource-friendly link checker written in Rust'
arch=('x86_64')
url=https://github.com/lycheeverse/lychee
license=('Apache' 'MIT')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
conflicts=('lychee-link-checker' 'lychee-rs')
replaces=('lychee-link-checker' 'lychee-rs')
options=('!lto')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('4e16d17b4da72dad6d68367793a0c12236c09bf2cae24f303381ef12826cf2753302a28a4246d4d29bf1c9eb6a454cdc81ae78727124765be481b774e70ac7c8')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --frozen
}

check() {
  cd $pkgname-$pkgver
  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
