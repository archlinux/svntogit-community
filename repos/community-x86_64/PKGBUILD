# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=1.21.1
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('gcc-libs' 'zlib')
makedepends=('cargo')
arch=('x86_64')
license=('Apache')
source=("https://github.com/watchexec/watchexec/archive/refs/tags/v${pkgver}.tar.gz")
options=('!lto')
sha256sums=('c41a2215505be5a71ecca8d47f30c1de1f0db3644a0da844ca70bc1ed05a0be3')

build() {
  cd "watchexec-$pkgver"
  cargo build --release --locked
}

check() {
  cd "watchexec-$pkgver"
  cargo test --locked
}

package() {
  cd "watchexec-$pkgver"
  install -Dm644 "doc/watchexec.1" "$pkgdir/usr/share/man/man1/watchexec.1"
  gzip -9 "$pkgdir/usr/share/man/man1/watchexec.1"
  install -Dm644 "completions/zsh" "$pkgdir/usr/share/zsh/site-functions/_watchexec"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
