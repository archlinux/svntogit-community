# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Chocobo1 <chocobo1@archlinux.net>
# Contributor: Jean Lucas <jean@4ray.co>

pkgname=sccache
pkgver=0.2.15
pkgrel=2
pkgdesc='Shared compilation cache'
arch=(x86_64)
url=https://github.com/mozilla/sccache
license=(Apache)
depends=(
  gcc-libs
  glibc
  openssl
  zlib
)
makedepends=(
  git
  rust
)
optdepends=(
  'memcached: Memcached support'
  'redis: Redis support'
)
_tag=6b6d2f7d2dceefeb4f583712aa4c221db62be0bd
source=(git+https://github.com/mozilla/sccache.git#tag=${_tag})
b2sums=(SKIP)
options=(!lto)

pkgver() {
  cd sccache

  git describe --tags | sed 's/^v//'
}

prepare() {
  cargo fetch \
    --locked \
    --manifest-path sccache/Cargo.toml
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path sccache/Cargo.toml \
    --features all \
    --features native-zlib
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path sccache/Cargo.toml \
    --features all \
    --features native-zlib
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path sccache \
    --root "${pkgdir}"/usr \
    --features all \
    --features native-zlib
}

# vim: ts=2 sw=2 et:
