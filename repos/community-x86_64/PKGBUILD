# Maintainer: David Runge <dvzrv@archlinux.org>
_name=zopfli
pkgname=python-zopfli
pkgver=0.1.8
pkgrel=1
pkgdesc="Python bindings to zopfli"
arch=('x86_64')
url="https://github.com/obp/py-zopfli"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip")
sha512sums=('9db198b603ef627e8dd959df191a9739c507258e561d60123203f20a6d06eb47ee2468c23e8448dffff4a7e2c495c45de830cd3c5bd4f53f61e4f5a4df0cd497')
b2sums=('40a1b7562d6c14e46f001c4e2889ee1411063d50ee2cabe28e3992aeb63702613ddf1c4a3de5b44102df291429c2b14999356a5de21691cc2c2642d6b4bae1df')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _py_ver=$(python -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:$PYTHONPATH"
  python tests/tests.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
