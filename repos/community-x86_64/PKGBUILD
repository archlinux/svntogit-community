# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=1.4.7
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=('sqlite' 'postgresql-libs' 'libmariadbclient')
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('5cb65679388cfb0c15e83a3a259f29f022e769b12dcf4ee2eadb95887f18856dc6eb8dffef7d70be604c313512e9cf94e78581e695fc79c643f53018fcf315de')
b2sums=('75607f53be7cd41be405209390c93a3237ca03ac9b1180e564a89fe8ce28a39b0271d275f6e92c02d0d9a0bdca94ee54e70affb89f56a9f61be2bce27d6b0d62')

build() {
  cd "diesel-${pkgver}/diesel_cli"
  # --locked is broken
  RUSTFLAGS="--cap-lints allow" cargo build --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
