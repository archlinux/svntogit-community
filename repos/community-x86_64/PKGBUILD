# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.45
pkgrel=2
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('glibc')
optdepends=('lzo: for libddr_lzo')
arch=('x86_64' 'i686')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.gz")

prepare() {
  cd $pkgname
  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}

sha512sums=('88022f38ad0e1ea171f1db014ba5e3f421fe9c35782f01220e27beb03618998860694df6b5d2e96ea4927640952c57153f2a1633dee9c13a2227e17ddd46b160')
