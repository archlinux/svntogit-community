# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Baptiste Daroussin <baptiste.daroussin@gmail.com>

pkgname=tnftp
pkgver=20230409
pkgrel=1
pkgdesc='NetBSD FTP client with several advanced features'
url='https://ftp.netbsd.org/pub/NetBSD/misc/tnftp/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ncurses' 'libncursesw.so' 'dante' 'openssl')
source=(https://ftp.netbsd.org/pub/NetBSD/misc/tnftp/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('c6c631eb43cdeb5be47f8d37fa12b8d991894408b323d39ae753051f4271de2c'
            'SKIP')
b2sums=('54e21382168df653fcb930981909bdf0ef4eecccc6eb6e22d87104cee0c52e56f349b4e5bbd655e64eb5553cd17dd8d031df0baaa7d68c699488e8decf2c1ceb'
        'SKIP')
validpgpkeys=(
  '2A8E22EDB07B5414548D8507A4186D9A7F332472'  # Luke Mewburn <lukem@NetBSD.org>
)

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --with-socks \
    --enable-editcomplete \
    --enable-ssl \
    --enable-ipv6
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  install -Dm 755 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog INSTALL NEWS README THANKS todo -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
