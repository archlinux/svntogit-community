# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=dbus
pkgname=haskell-dbus
pkgver=0.10.13
pkgrel=8
pkgdesc="A client library for the D-Bus IPC system"
url="https://john-millikin.com/software/haskell-dbus/"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' "haskell-cereal" "haskell-libxml-sax" "haskell-network" "haskell-parsec"
         "haskell-random" "haskell-text" "haskell-vector" "haskell-xml-types")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e3cbb4153cd9df9308d89662b83659e13f07881f4d652c6f02dc6b6768ac1009552a7cfa9c610ededc9b630d26290eec153a9b08b7a3d70b44a6946af3d030ec')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
