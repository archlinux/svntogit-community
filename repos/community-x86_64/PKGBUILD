# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_name=Audacity
pkgbase=audacity
pkgname=(audacity audacity-docs)
pkgver=3.2.2
pkgrel=1
epoch=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=(x86_64)
url="https://audacityteam.org"
license=(GPL3)
makedepends=(
  alsa-lib
  chrpath
  cmake
  ffmpeg
  flac
  gcc-libs
  glibc
  gdk-pixbuf2
  glib2
  gtk3
  jack
  lame
  libid3tag
  libmad
  libogg
  libsbsms
  libsndfile
  libsoxr
  libvorbis
  lilv
  lv2
  mpg123
  portaudio
  portmidi
  portsmf
  python
  soundtouch
  sqlite
  suil
  twolame
  wavpack
  wxwidgets-gtk3
  vamp-plugin-sdk
  vst3sdk
)
options=(debug)
source=(
  https://github.com/audacity/audacity/archive/refs/tags/$_name-$pkgver.tar.gz
  https://github.com/$pkgbase/$pkgbase/releases/download/$_name-$pkgver/$pkgbase-manual-$pkgver.tar.gz
)
sha512sums=('629d502acc78b02d50a191045a8ececabf7096a77fe63f491291a36cca452c47dec74aef55d0f837345dab2e5f6296d40a2396dea5788a998d619820335abc73'
            '6bf84f50a722cb42f2f8bfeb523697e023121dea0dad63c253194e50c127368466395ec37b6fd58e7154b2b620186b46333c6615937202e376b7e234120ebb18')
b2sums=('9800f5f0d782c4f6a3d10765bc57cde6e8dd9bd47581bed80502120d1ecb098e67092fdeb0873f248acc8448958e907e74877bfa94e2b144f3e8289dcd79d26d'
        '890705377bec542210b6d41ce4851a85094b3496054e63c2e5a9d59c025ed8c512c62273721fb7088152504a11cf73b7c314e020f4e81682bee7d468bf9ce15a')

build() {
  local cmake_options=(
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -D AUDACITY_BUILD_LEVEL=2
    -D audacity_conan_enabled=OFF
    -D audacity_has_networking=OFF
    -D audacity_has_crashreports=OFF
    -D audacity_has_updates_check=OFF
    -D audacity_has_sentry_reporting=OFF
    -D audacity_lib_preference=system
    -D audacity_obey_system_dependencies=ON
    -D audacity_use_vst3sdk=system
  )

  export VST3SDK='/usr/share/vst3sdk'
  export CFLAGS+=" -DNDEBUG"
  export CXXFLAGS+=" -DNDEBUG"
  cmake "${cmake_options[@]}" -B build -S $pkgname-$_name-$pkgver -Wno-dev
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package_audacity() {
  license+=(BSD)
  groups=(pro-audio)
  depends=(
    alsa-lib libasound.so
    flac libFLAC.so libFLAC++.so
    gcc-libs
    glibc
    gdk-pixbuf2 libgdk_pixbuf-2.0.so
    glib2 libglib-2.0.so libgobject-2.0.so
    gtk3 libgdk-3.so libgtk-3.so
    jack libjack.so
    lame libmp3lame.so
    libid3tag libid3tag.so
    libmad
    libogg libogg.so
    libsbsms libsbsms.so
    libsndfile libsndfile.so
    libsoxr
    libvorbis libvorbis.so libvorbisenc.so libvorbisfile.so
    lilv liblilv-0.so
    mpg123 libmpg123.so
    portaudio libportaudio.so
    portmidi libportmidi.so
    portsmf libportSMF.so
    python
    soundtouch
    sqlite libsqlite3.so
    suil libsuil-0.so
    twolame libtwolame.so
    wxwidgets-gtk3
    vamp-plugin-sdk libvamp-hostsdk.so
    wavpack
  )
  optdepends=(
    'audacity-docs: for documentation'
    'ffmpeg: for additional import/export capabilities'
  )
  provides=(ladspa-host lv2-host vamp-host vst-host vst3-host)


  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$_name-$pkgver/lib-src/libnyquist/nyquist/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.nyquist"

  # remove unsafe RPATH: https://github.com/audacity/audacity/issues/3289
  (
  cd "$pkgdir/usr/lib/$pkgname"
  for _lib in *.so; do
    chrpath --delete "$_lib"
    chmod 755 "$_lib"
  done
  )
  (
  cd "$pkgdir/usr/lib/$pkgname/modules"
  for _lib in *.so; do
    chrpath --delete "$_lib"
    chmod 755 "$_lib"
  done
  )
  # NOTE: private libraries are public: https://github.com/audacity/audacity/issues/3812
}

package_audacity-docs() {
  license+=(CCPL)
  pkgdesc+=" - documentation"

  install -vdm 755 "$pkgdir/usr/share/doc/$pkgbase/html/"
  cp -av help/manual/* "$pkgdir/usr/share/doc/$pkgbase/html/"
}
