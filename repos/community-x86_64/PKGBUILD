# $Id: PKGBUILD 133367 2015-05-13 12:31:10Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=moosefs
pkgver=3.0.90_1
pkgrel=1
pkgdesc='High performing and fault tolerant distributed file system'
url='http://moosefs.com/'
replaces=('mfs-master' 'mfs-chunkserver' 'mfs-client')
conflicts=('mfs-master' 'mfs-chunkserver' 'mfs-client')
arch=('x86_64' 'i686')
license=('GPL2')
depends=('fuse2' 'python2' 'bash' 'xfsprogs')
source=("http://ppa.moosefs.com/src/moosefs-${pkgver/_/-}.tar.gz") # no https available
sha256sums=('4e24d4e79c66986c3f4b0f20b9078fc17acc5654601972b7231dd8bf4fa5928b')

build() {
  cd "moosefs-${pkgver%_*}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --with-default-user=mfs \
    --with-default-group=mfs \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  make DESTDIR="$pkgdir" -C "moosefs-${pkgver%_*}" install

  # Move mount.moosefs from sbin to usr/bin
  mv "$pkgdir/sbin/"* "$pkgdir/usr/bin/"
  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/sbin" "$pkgdir/usr/sbin"
}

# getver: moosefs.com/download/sources.html
# vim:set ts=2 sw=2 et:
