# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gmsynth.lv2
pkgver=0.5.3
pkgrel=1
pkgdesc="General MIDI LV2 Synth"
arch=(x86_64)
url="https://x42-plugins.com/x42/x42-gmsynth"
license=(GPL2)
groups=(
  pro-audio
  lv2-plugins
)
depends=(glibc)
makedepends=(
  fluidsynth
  lv2
)
checkdepends=(lv2lint)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/x42/$pkgname/archive/v$pkgver.tar.gz
  $pkgname-0.5.1-use_system_fluidsynth.patch
)
sha512sums=('352174b24411cb9b6e3432efeb899915cf2a92bf6eea2fdc6fad48923d261197219979b2dcee8c5ff8aecdfe3e0ce3eaba43dff69ba1b43fe22ad0f154350d4b'
            'a0f2a09a6a839c38dc3234a719a633eb221f2afca2a97bbecf17b7d0c8dc71befec3768b882deb20357d55186e81e342cad73eb5e08426a1e2108949d11b3cf2')
b2sums=('2843ab78004681bb033b1ab9a8420c35a000624ee246711ea00dbc2742a8611c979ff30dd20c511699190df70b9ecb93c1b5c7354f76d8344af97eeaceb525b6'
        'cb327b55536006a862540e769497d138a6815bbee83f9365f97778adf8c542f3ce49e83d07dd532e66c39ce57a4c1eedc79c982b8ac350f22311f9443f682954')

prepare() {
  # use system fluidsynth
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-0.5.1-use_system_fluidsynth.patch
}

build() {
  make -C $pkgname-$pkgver
}

check() {
  lv2lint -Mpack -I $pkgname-$pkgver/build "http://gareus.org/oss/lv2/gmsynth"
}

package() {
  depends+=(
    fluidsynth libfluidsynth.so
  )

  make DESTDIR="$pkgdir/" PREFIX=/usr install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
