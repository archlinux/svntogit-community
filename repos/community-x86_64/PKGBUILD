# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Drew DeVault

pkgname=mako
pkgver=1.7.1
pkgrel=2
license=('MIT')
pkgdesc='Lightweight notification daemon for Wayland'
makedepends=("meson" "scdoc" "systemd" "wayland-protocols")
depends=(
	"gdk-pixbuf2"
	"pango"
	"systemd-libs"
	"wayland"
)
optdepends=("jq: support for 'makoctl menu'")
arch=("x86_64")
url='https://mako-project.org'
options=(debug)
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/emersion/mako/releases/download/v$pkgver/mako-$pkgver.tar.gz"
	"$pkgname-$pkgver.tar.gz.sig::https://github.com/emersion/mako/releases/download/v$pkgver/mako-$pkgver.tar.gz.sig"
	"systemd-dbus-activation.patch"
)
validpgpkeys=("34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48") # emersion
sha256sums=('4312ff6bc61708115b43b967a42813374de7a2022f10c84e20127606416c3c12'
            'SKIP'
            '4579a2673dcf2114779e10ed52d771f3930c2192e8e35d3e145163d9e0b45c20')

prepare() {
	patch -Np1 -i "$srcdir/systemd-dbus-activation.patch" -d "$pkgname-$pkgver"
}

build() {
	arch-meson \
		-Dbash-completions=true \
		-Dfish-completions=true \
		-Dzsh-completions=true \
		-Dsd-bus-provider=libsystemd \
		"$pkgname-$pkgver" build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" "$pkgname-$pkgver/LICENSE"
	install -Dm644 -t "$pkgdir/usr/lib/systemd/user/" "$pkgname-$pkgver/contrib/systemd/mako.service"
}
