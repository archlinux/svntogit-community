# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=1.2.3
pkgrel=2
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm'
arch=('x86_64')
url='https://duplicity.gitlab.io/'
license=('GPL')
depends=(
  'gnupg'
  'librsync'
  'python-paramiko'
  'python-fasteners'
  'python-future'
)
makedepends=('python-setuptools-scm')
optdepends=(
  'lftp: backend for ftp, ftps, fish'
  'ncftp: alternative backend for ftp'
  'python-b2sdk: Backblaze B2 backend'
  'python-boto: Amazon S3 and Google Cloud Storage backend'
  'python-dropbox: Dropbox backend'
  'python-gobject: GIO backend'
  'gvfs: GIO backend'
  'python-pydrive2: Google Drive backend'
  'python-mediafire: Mediafire backend'
  'python-requests-oauthlib: OneDrive backend'
  'rsync: rsync backend'
)
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/duplicity/duplicity/-/archive/rel.$pkgver/duplicity-rel.$pkgver.tar.gz")
sha512sums=('9a8b2c4add41a41a06999ee8f9c56071ca3dacb0b684c558b9e83d84f0de97b436980f95570532290e254c042be75e852b4f0f0e3daf1024b287b27333b5de34')
b2sums=('697ebd7627ae8745949373290a038b2c7920e581e90fe10393048c569a60815d8adf36557d3c65272a80d1c46fd70f87ee3ad831e73c3d5057fbd1004b84aaab')

prepare() {
  cd "$pkgname-rel.$pkgver"

  # FS#76307 - fix version string
  sed -e "s/\$version/$pkgver/" -i "$pkgname/__init__.py"
}

package() {
  cd "$pkgname-rel.$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  # move documentation to the correct directory
  cd "$pkgdir/usr/share/doc"
  mv "$pkgname-$pkgver" "$pkgname"
}
