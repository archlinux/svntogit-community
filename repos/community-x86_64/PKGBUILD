# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dtkgui
pkgver=5.6.2.2
pkgrel=1
epoch=1
pkgdesc='Deepin Toolkit, gui module for DDE look and feel'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkgui"
license=('LGPL3')
options=('debug')
depends=('dtkcore' 'libqtxdg' 'librsvg')
makedepends=('cmake' 'ninja' 'qt5-tools' 'gtest' 'doxygen')
source=("https://github.com/linuxdeepin/dtkgui/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ff34aca1460347c443df320b24c7d735ee55b5eb1aaf1ed8ebe6dad7c99d0a70cb9ce81476b21be91c6d9e8b6821c8d111c4ca5168993ac2aadb17639f0ea83d')

build() {
  cd dtkgui-$pkgver
  cmake -GNinja \
    -DNOTPACKAGE=OFF \
    -DLINUXNAME="archlinux"\
    -DBUILD_DOCS=OFF \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd dtkgui-$pkgver
  DESTDIR="$pkgdir" ninja install
}
