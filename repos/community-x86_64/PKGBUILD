# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.12.1
pkgrel=1
pkgdesc='Basic, good-looking task manager for WMs'
arch=('x86_64' 'i686')
url='https://gitlab.com/o9000/tint2'
license=('GPL2')
conflicts=('ttm-svn')
depends=('gtk2' 'imlib2' 'startup-notification')
optdepends=('pygtk: for tint2wizard' 'python2')
makedepends=('pygtk' 'cmake' 'python2' 'startup-notification' 'git')
source=("$pkgname-$pkgver.tgz::https://gitlab.com/o9000/tint2/repository/archive.tar.gz?ref=$pkgver"
        'zombie-fix.patch')
md5sums=('f34335906d5eb3bf86ac6a162dc88513'
         'cdb83cd911e005a3529e5d1cd952a956')

prepare() {
  cd "$pkgname.git"

  # Applying all available patches
  for f in ../*.patch; do
    echo -e "--- ${f##*/} ---"
    patch -Np1 -i $f && echo "${f##*/} OK!" || echo "${f##*/} FAILED!"
  done

  # Fix for Python 2
  sed -i 's_python _python2 _' src/tint2conf/main.c
}

build() {
  cd "$pkgname.git"

  msg2 'Compiling...'
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_TINT2CONF=1
  make 
}

package() {
  make -C "$pkgname.git/build" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
