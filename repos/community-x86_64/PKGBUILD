# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

pkgname=freerdp
pkgver=1.2.0_beta1+android9
_pkgver=${pkgver/_/-}
_pkgver=${_pkgver/+/-}
pkgrel=2
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse' 'libxkbfile'
	 'libxrender' 'libxfixes' 'gst-plugins-base-libs')
makedepends=('krb5' 'cmake' 'damageproto' 'fixesproto' 'renderproto'
	     'xmlto' 'docbook-xsl')
conflicts=('freerdp-git')
source=($pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/FreeRDP/archive/${pkgver/_/-}.tar.gz
	build-fix.patch)
md5sums=('a4a0a4f6e69c871aeab0600091ca6118'
         '9e9488538fe5ab74c25e2fb69e3edf62')

prepare() {
  cd $srcdir/FreeRDP-${_pkgver}
  patch -p1 <$srcdir/build-fix.patch
}

build() {
  cd $srcdir/FreeRDP-${_pkgver}
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSEAUDIO=ON \
	-DWITH_CUPS=ON -DCHANNEL_PRINTER=ON \
	.
  make
}

package() {
  cd $srcdir/FreeRDP-${_pkgver}
  make DESTDIR="${pkgdir}" install
}
