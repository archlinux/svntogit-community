# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Marc Tiehuis <marctiehuis@gmail.com>

pkgname=zig
pkgver=0.9.1
pkgrel=2
pkgdesc='a general-purpose programming language and toolchain for maintaining robust, optimal, and reusable software'
arch=('x86_64')
url='https://ziglang.org/'
license=('MIT')
options=('debug')
depends=('clang13' 'llvm13-libs')
makedepends=('cmake' 'llvm13' 'lld13')
checkdepends=('lib32-glibc')
source=("https://ziglang.org/download/$pkgver/zig-$pkgver.tar.xz"
        "$pkgname-io_uring-cancel-test.patch::https://github.com/ziglang/zig/commit/52651ae7a0de.patch"
        "io_uring-timeout-remove-test.patch"
        "resolve_DNS.patch")
sha256sums=('38cf4e84481f5facc766ba72783e7462e08d6d29a5d47e3b75c8ee3142485210'
            '7cabeeb678a9acf8aecc765adc78b328baafe2f1e928518f40d97d0afaae8535'
            '9ed66bbef9420c0628c7054e4f192ad6faaaacfffcf28c4b3f08462d62769748'
            'c2b7e709dcc229f0e58e2529db900bcfb9a1cfb73da765c5c76ab1e6d0a333cc')

prepare() {
    cd "$pkgname-$pkgver"
    # https://github.com/ziglang/zig/issues/6898
    patch -p1 < ../resolve_DNS.patch
    # https://github.com/ziglang/zig/issues/11382
    patch -p1 --no-backup-if-mismatch < ../$pkgname-io_uring-cancel-test.patch
    # https://github.com/ziglang/zig/issues/11900
    patch -p1 --no-backup-if-mismatch < ../io_uring-timeout-remove-test.patch
}

build() {
    cd "$pkgname-$pkgver"

    # The zig CMakeLists uses build type Debug if not set
    # override it back to None so makepkg env vars are respected
    cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_PREFIX_PATH=/usr/lib/llvm13 \
        -DZIG_PREFER_CLANG_CPP_DYLIB=ON \
        .
    cmake --build build
}

check() {
    cd "$pkgname-$pkgver/build"

    ./zig build test
}

package() {
    cd "$pkgname-$pkgver"

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    DESTDIR="$pkgdir" cmake --install build
}
