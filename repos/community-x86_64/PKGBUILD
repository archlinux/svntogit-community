# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Niels Abspoel <aboe76 (at) Gmail.com>

_gemname=pg
pkgname=ruby-$_gemname
pkgver=1.2.3
pkgrel=1
pkgdesc='Ruby interface to the PostgreSQL RDBMS'
url='https://github.com/ged/ruby-pg'
arch=('x86_64')
license=('custom:BSDL')
depends=('ruby' 'ruby-rspec-core' 'postgresql-libs')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('27249d2916bdf3707d485a2ccebd582a6e8a9231501027b400bd9a4a540392e4428b4ec55e2e42d16be909ad80a8b45e5cef10973f2e95904160a9170314cb65')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|"ChangeLog".freeze, ||' -i ${_gemname}.gemspec
  sed 's|"lib/pg/deprecated_constants.rb".freeze, ||' -i ${_gemname}.gemspec
  sed "s|^  s.version = .*$|  s.version = '${pkgver}'|" -i ${_gemname}.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"

  # delete unnecessary files & folders
  cd "$pkgdir/$_gemdir"
  find . -type f \
    -name "page-Makefile.ri" -delete \
    -o -name 'gem_make.out' -delete \
    -o -name 'mkmf.log' -delete
  rm -rf cache
  cd "gems/$_gemname-$pkgver"
  rm -vrf spec ext POSTGRES Manifest.txt Rakefile* Gemfile

  # move documentation
  install -vd "$pkgdir/usr/share/doc/$pkgname"
  mv *.rdoc "$pkgdir/usr/share/doc/$pkgname"

  # move license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  mv LICENSE BSDL "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
