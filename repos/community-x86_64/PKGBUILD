# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: N30N <archlinux@alunamation.com>

pkgname=unarchiver
pkgver=1.10.7
_UniversalDetector_pkgver=1.1
pkgrel=8
pkgdesc="unar and lsar: Objective-C tools for uncompressing archive files"
arch=(x86_64)
url="https://github.com/MacPaw/XADMaster"
license=('LGPL2.1')
depends=(gnustep-base openssl bzip2 icu gcc-libs zlib wavpack)
makedepends=(gcc-objc gnustep-make)
source=("XADMaster-${pkgver}.tar.gz::https://github.com/MacPaw/XADMaster/archive/v${pkgver}.tar.gz"
        "UniversalDetector-${_UniversalDetector_pkgver}.tar.gz::https://github.com/MacPaw/universal-detector/archive/${_UniversalDetector_pkgver}.tar.gz"
        "native_obj_exceptions.patch")
sha256sums=('3d766dc1856d04a8fb6de9942a6220d754d0fa7eae635d5287e7b1cf794c4f45'
            '8e8532111d0163628eb828a60d67b53133afad3f710b1967e69d3b8eee28a811'
            '7592fb0f689e588ad9a6caee522eabe8bc64659eb71bb8629239856e79d3c757')

prepare() {
  # The project requires UniversalDetector next to the source dir. See the project's README for more info about the build requirements.
  ln -s universal-detector-${_UniversalDetector_pkgver} UniversalDetector

  cd "$srcdir/XADMaster-${pkgver}"
  patch < ../native_obj_exceptions.patch
}

build() {
  cd "$srcdir/XADMaster-${pkgver}"

  . /usr/share/GNUstep/Makefiles/GNUstep.sh
  make -f Makefile.linux
}

package() {
  cd "$srcdir/XADMaster-${pkgver}"
  install -d "$pkgdir/usr/bin/"
  install -m755 unar lsar "$pkgdir/usr/bin/"

  install -d "$pkgdir/usr/share/man/man1"
  install -m644 Extra/{lsar.1,unar.1} "$pkgdir/usr/share/man/man1/"
  install -d "$pkgdir/usr/share/bash-completion/completions/"
  install -m644 Extra/unar.bash_completion "$pkgdir/usr/share/bash-completion/completions/unar"
  install -m644 Extra/lsar.bash_completion "$pkgdir/usr/share/bash-completion/completions/lsar"
}
