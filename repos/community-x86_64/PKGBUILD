# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Florian Dejonckheere <florian@floriandejonckheere.be>
# Contributor: gori

pkgname=ipmitool
pkgver=1.8.18
pkgrel=7
pkgdesc="Command-line interface to IPMI-enabled devices"
arch=('x86_64')
url="http://ipmitool.sourceforge.net"
depends=('openssl')
license=('BSD')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
         ipmitool-openssl-1.1.patch
         https://github.com/ipmitool/ipmitool/commit/e824c23316ae50beb7f7488f2055ac65e8b341f2.patch
         https://github.com/ipmitool/ipmitool/commit/840fb1cbb4fb365cb9797300e3374d4faefcdb10.patch
         https://github.com/ipmitool/ipmitool/commit/41d7026946fafbd4d1ec0bcaca3ea30a6e8eed22.patch
         #https://github.com/ipmitool/ipmitool/commit/9452be87181a6e83cfcc768b3ed8321763db50e4.patch
         9452be87181a6e83cfcc768b3ed8321763db50e4-edited.patch
         https://github.com/ipmitool/ipmitool/commit/d45572d71e70840e0d4c50bf48218492b79c1a10.patch
         https://github.com/ipmitool/ipmitool/commit/7ccea283dd62a05a320c1921e3d8d71a87772637.patch
       )
sha256sums=('0c1ba3b1555edefb7c32ae8cd6a3e04322056bc087918f07189eeedfc8b81e01'
            '7da20584541279045b4a4743600925b70fe162e1437a8da6647414926e12b58f'
            'fcf8a1fce7f902adcb6500143ec04f6541474a2e0e78acfdf52276d3e421e84f'
            '9774148893ed44f5d85bec26cd54f31ef6f7491232eb3f44f65d391547d83cda'
            '42ce1143b05e160cee9cc6fb6ed13938ffc62dc11eec0343caccf463d49b76b8'
            'd51c1f481d4fc1d3fd5617ceeda16327fb6a6916103cf7334f6e44cd325ea0e0'
            '0b6535b7b54485a9ba107ae09fccdff9f816ae1c4d8a8fe334df6fb48d2ec63a'
            '146316f1b4001e3929c794d25ee2dacc7602676060da80b9c1655ec01a0032e3')

prepare() {
        cd ${pkgname}-${pkgver}
        # openssl 1.1 support (Fedora)
        patch -p1 -i ../ipmitool-openssl-1.1.patch

        # FS#69708 - [ipmitool] [Security] arbitrary code execution (CVE-2020-5208)
        patch -p1 -i ../e824c23316ae50beb7f7488f2055ac65e8b341f2.patch
        patch -p1 -i ../840fb1cbb4fb365cb9797300e3374d4faefcdb10.patch
        patch -p1 -i ../41d7026946fafbd4d1ec0bcaca3ea30a6e8eed22.patch
        patch -p1 -i ../9452be87181a6e83cfcc768b3ed8321763db50e4-edited.patch
        patch -p1 -i ../d45572d71e70840e0d4c50bf48218492b79c1a10.patch
        patch -p1 -i ../7ccea283dd62a05a320c1921e3d8d71a87772637.patch
}

build(){
	cd "${srcdir}/${pkgname}-${pkgver}"

	CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

	# Fix warning in ipmi_fru.c
	CFLAGS+=' -Wno-maybe-uninitialized'

	if [[ $pkgver = '1.8.18' ]]; then
		CFLAGS+=' -DMAX_CIPHER_SUITE_DATA_LEN=0x10'
	fi

	./configure --prefix=/usr --sbindir=/usr/bin --with-kerneldir
	make
}

package(){
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# Install license
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
