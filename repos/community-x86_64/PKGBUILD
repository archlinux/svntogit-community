# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-pkgconfig-libpkgconf
pkgver=0.10
pkgrel=3
pkgdesc="Perl bindings for libpkgconf"
arch=('x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/PkgConfig-LibPkgConf"
depends=('perl' 'pkgconf')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/PkgConfig-LibPkgConf-$pkgver.tar.gz")
sha512sums=('29f27e8e046567d43ee3c2b61cb2543bca8399655edc7f9e63268047bc65fb05f8f57ff69d09e623fea8ab9f0a03366c5a967ada73d0d798b7d859ebe6932a9f')

build() {
  cd PkgConfig-LibPkgConf-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PkgConfig-LibPkgConf-$pkgver
  make test
}

package() {
  cd PkgConfig-LibPkgConf-$pkgver
  make DESTDIR="$pkgdir" install
}
