# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-clevercsv
pkgver=0.7.0
pkgrel=3
pkgdesc="A Python package for handling messy CSV files"
url="https://github.com/alan-turing-institute/CleverCSV"
license=('MIT')
arch=('x86_64')
depends=('python-chardet' 'python-regex')
makedepends=('python-setuptools')
optdepends=('python-pandas' 'python-cleo' 'python-clikit' 'python-tabview')
checkdepends=('python-pytest-runner' 'python-pandas' 'python-cleo' 'python-clikit' 'python-tabview'
              'python-termcolor')
source=("https://github.com/alan-turing-institute/CleverCSV/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('241e4ac113c6542024d853f02b81c8f6a809ccbcefda78c250aeae10dd05a1510217a78c0ccc0359401364d77adcdfb22caf63689e55ff26495e4f2da92f8c4e')

build() {
  cd CleverCSV-$pkgver
  python setup.py build
}

check() {
  cd CleverCSV-$pkgver
  PYTHONPATH="$PWD"/build/lib.linux-$CARCH-3.10 python setup.py pytest
}

package() {
  cd CleverCSV-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
