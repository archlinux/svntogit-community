# Maintainer:  Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>
pkgname=dub
pkgver=v0.9.22.r198.g83b2926
pkgrel=1
pkgdesc="Developer package manager for D programming language"
arch=('i686' 'x86_64')
url="https://github.com/D-Programming-Language/dub"
license=('MIT')
# makedepends=('d-compiler' 'd-runtime' 'git')
makedepends=('ldc' 'git')
# depends=('d-runtime' 'curl')
depends=('liblphobos')
conflicts=('dub-git')
source=(
  "git+https://github.com/D-Programming-Language/dub.git#commit=83b2926f7ba88122be85a8841cedb854b0e3a630"
  "git+https://github.com/Dicebot/Arch-PKGBUILDs.git"
)
sha256sums=(
  'SKIP'
  'SKIP'
)

pkgver() {
  cd "$srcdir/$pkgname"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build()
{
  # DC=`$srcdir/Arch-PKGBUILDs/d-compiler.sh`
  DC=ldmd
  cd "${srcdir}/${pkgname}"

  echo Generating version file...
  GITVER=$(git describe) || GITVER=unknown
  echo "module dub.version_;" > source/dub/version_.d
  echo "enum dubVersion = \"$GITVER\";" >> source/dub/version_.d
  echo "enum initialCompilerBinary = \"$DC\";" >> source/dub/version_.d

  $DC -ofbin/dub -w -g -version=DubUseCurl -Isource -L-lcurl @build-files.txt
}

package()
{
  cd "${srcdir}/${pkgname}"
  install -D -m755 bin/dub "${pkgdir}/usr/bin/dub"
  install -D -m644 scripts/bash-completion/dub.bash $pkgdir/usr/share/bash-completion/completions/dub
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
