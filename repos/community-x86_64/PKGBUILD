# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Radek Podgorny <radek@podgorny.cz>
# Contributor: Dan Anderson <dan-anderson at cox dptnet>

pkgname=mosquitto
pkgver=2.0.12
pkgrel=2
pkgdesc="An Open Source MQTT Broker"
arch=('x86_64')
url="https://mosquitto.org/"
depends=('openssl' 'c-ares' 'util-linux-libs' 'cjson')
makedepends=('docbook-xsl' 'c-ares' 'libwebsockets')
license=('BSD')
source=(https://mosquitto.org/files/source/mosquitto-$pkgver.tar.gz{,.asc}
        "$pkgname.service"
        "sysusers_mosquitto.conf")
backup=("etc/$pkgname/$pkgname.conf")
sha512sums=('acf80980f124073fd2fdd99981bde840654fb40a6b6f1d6ef483e35880b45d029cfa9390a98753de6476f4ade068266cb4c6e3c753a0362c0309a5745ac4ebab'
            'SKIP'
            '7dd86bb454e6df45e609fc3cb53d3cae8cc1c36d459b1e23be9ab10c9770c7a406fbd34e47703b0db3056f4bc8550994666b8a398d4506f786bf274e4618b7e9'
            '21848b890c2db258138795ec21a009e022b6a8369217eb31939f976ad434229dd9f61d33e8109ade7bc001e8668e9d42b59c1ab079753860417961e102356f0e')
validpgpkeys=('A0D6EEA1DCAE49A635A3B2F0779B22DFB3E717B7')

prepare() {
  cd "$pkgname-$pkgver"
  # FIX upstream by making SBINDIR=foo or use CMAKE
  sed -i 's|/sbin|/bin|g' src/Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make WITH_WEBSOCKETS=yes
}

package() {
  depends+=('libwebsockets.so')
  cd "$pkgname-$pkgver"

  make prefix=/usr DESTDIR="$pkgdir/" install

  # Shipped in git.
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/etc/$pkgname/$pkgname.conf.example" "$pkgdir/etc/$pkgname/$pkgname.conf"

  install -D -m644 "${srcdir}"/sysusers_mosquitto.conf "${pkgdir}"/usr/lib/sysusers.d/mosquitto.conf
}
