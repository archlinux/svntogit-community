# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pycuda
pkgver=2022.1
pkgrel=3
pkgdesc="Python wrapper for Nvidia CUDA"
arch=('x86_64')
url="https://mathema.tician.de/software/pycuda"
license=('MIT' 'Apache')
depends=('boost-libs' 'cuda' 'nvidia-utils' 'python' 'python-numpy' 'python-pytools')
provides=('pycuda-headers')
conflicts=('pycuda-headers')
replaces=('pycuda-headers')
source=("https://pypi.io/packages/source/p/pycuda/pycuda-$pkgver.tar.gz")
makedepends=('ctags' 'python-setuptools' 'python-numpy' 'mesa' 'cuda' 'boost' 'nvidia-utils')
sha512sums=('2c26c7ff2271d2dd2f59c1f028b6823dd5c9d9bc38300bb8de1d38d5220108de9ea090fde64bfe1855ff94d2c0a9b57b163bff456fb39d16bec32e96d176859e')

build() {
   _arch=''
   [[ "$CARCH" = "x86_64" ]] && _arch='64'

   cd pycuda-$pkgver
   python ./configure.py \
                  --cuda-root=/opt/cuda \
                  --cuda-inc-dir=/opt/cuda/include \
                  --cudadrv-lib-dir=/opt/cuda/lib${_arch} \
                  --cudart-lib-dir=/opt/cuda/lib${_arch} \
                  --no-use-shipped-boost \
                  --boost-python-libname=boost_python3
   make
}

package() {
   cd pycuda-$pkgver
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

   install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
