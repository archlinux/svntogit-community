# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=nauty
pkgver=26r12
pkgrel=6
pkgdesc="A program for computing automorphism groups of graphs and digraphs"
arch=(x86_64)
url="http://pallini.di.uniroma1.it/"
license=(Apache)
depends=(glibc)
source=(http://pallini.di.uniroma1.it/nauty$pkgver.tar.gz
        nauty-ldflags.patch)
sha256sums=('862ae0dc3656db34ede6fafdb0999f7b875b14c7ab4fedbb3da4f28291eb95dc'
            '65e0527b27edd783f23ba3e4f114edd6d54195c4cc453293d140195f8a307f69')

prepare() {
  cd nauty$pkgver
  patch -p1 -i ../nauty-ldflags.patch # Honor LDFLAGS
}

build() {
  cd nauty$pkgver

  export CFLAGS+=' -fPIC'
  ./configure
  make
}

package() {
  cd nauty$pkgver

  mkdir -p "$pkgdir"/usr/bin
  for _program in addedgeg amtog biplabg catg complg converseg copyg countg cubhamg deledgeg delptg directg dreadnaut dretodot dretog \
    genbg genbgL geng genquarticg genrang genspecialg gentourng gentreeg hamheuristic labelg linegraphg listg multig newedgeg \
    pickg planarg ranlabg shortg showg subdivideg twohamg vcolg watercluster2 NRswitchg;
  do
   install -m755 $_program "$pkgdir"/usr/bin/$program
  done
  install -Dm644 nauty.a "$pkgdir"/usr/lib/libnauty.a
  install -Dm644 *.h -t "$pkgdir"/usr/include/nauty

  mkdir -p "$pkgdir"/usr/share/licenses/nauty
  install COPYRIGHT "$pkgdir"/usr/share/licenses/nauty/LICENSE
}
