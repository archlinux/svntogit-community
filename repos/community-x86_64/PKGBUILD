# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sound
pkgver=2.3.1
pkgrel=1
pkgdesc='Switchboard Sound Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sound
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libcanberra
  libgee
  libgranite.so
  libpulse
  libswitchboard-2.0.so
 )
makedepends=(
  git
  meson
  vala
)
_tag=4fd27d0dc1a85e3a527632073644a66cec85e6fb
source=(git+https://github.com/elementary/switchboard-plug-sound.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-sound

  git describe --tags
}

build() {
  arch-meson switchboard-plug-sound build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
