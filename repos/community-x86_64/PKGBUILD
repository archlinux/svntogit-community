# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: jsteel <mail at jsteel dot org>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Tdy  <tdy@gmx.com>
# Contributor: Biginoz <biginoz at free.fr>
# Contributor: Archan Paul <paul.archan@gmail.com>

pkgname=sweethome3d
pkgver=6.1.2
pkgrel=1
pkgdesc="An interior design application to draw the plan of your house in a 3D environment"
arch=('x86_64')
url="http://www.sweethome3d.com/"
license=('GPL' 'custom')
depends=('java-runtime>=8' 'shared-mime-info' 'desktop-file-utils' 'libxext' 'libgl' 'libxrender' 'libnsl')
makedepends=('ant')
source=("SweetHome3D-${pkgver}-src.zip"::"https://downloads.sourceforge.net/${pkgname}/SweetHome3D-${pkgver}-src.zip"
        "build_with_source_1.8.patch"
        "${pkgname}.sh"
        "${pkgname}.desktop"
        "${pkgname}.xml")
sha256sums=('43645e34c94bf5b514974428c4a0eefae403fcaf52258cbb9c6fe07d4da8f7db'
            '524286451144548fdc5ae0ca9e4f9180c96eb4bc00e86121d7f38ab3ecad07ff'
            '6a5e2fd8de2ec37975a6a299befe7523d106f82c20f8838b48c17191928d7e2d'
            '5eea3337d956d773b05ddef69fe9d34b940ff550370dc92bf307f1b9a3957f9e'
            'ec0ad1a0671f708af68ced46bec1f4ab377e24ca1a0a9984867ee5fe484f57c5')
install="${pkgname}.install"

prepare() {
  cd "SweetHome3D-${pkgver}-src"
  patch -Np1 -i "${srcdir}/build_with_source_1.8.patch"

}

build() {
  cd "SweetHome3D-${pkgver}-src"
  ant application textures furniture examples help java3dLibraries
}

package() {
  # Desktop and launchers
  install -m 755 -D "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -m 755 -D "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -m 644 -D "${pkgname}.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
  # Install licenses
  cd "SweetHome3D-${pkgver}-src"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 755 -t "${pkgdir}/usr/share/licenses/${pkgname}" THIRDPARTY-LICENSE-*
  # Install icon
  install -m 644 -D "deploy/SweetHome3DIcon48x48.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  # Install main program and libraries
  install -m 755 -d "${pkgdir}/usr/share/java/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgname}" build/*.jar
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgname}" libtest/jnlp.jar
  cd "lib"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgname}" *.jar
  # Java3D 1.5 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/java3d-1.5"
  mv -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.5" \
    "${pkgdir}"/usr/share/java/${pkgname}/{j3dcore.jar,j3dutils.jar,vecmath.jar}
  install -m 755 -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.5" linux/x64/libj3dcore-ogl.so
  # Java3D 1.6 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgname}/java3d-1.6"
  install -m 644 -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.6" java3d-1.6/*.jar
  install -m 755 -t "${pkgdir}/usr/lib/${pkgname}/java3d-1.6" java3d-1.6/linux/amd64/*.so
}
