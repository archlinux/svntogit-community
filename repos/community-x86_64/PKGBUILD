# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=sdl12-compat
pkgver=1.2.52
pkgrel=2
pkgdesc="SDL 1.2 runtime compatibility library using SDL 2.0"
url="https://github.com/libsdl-org/sdl12-compat"
depends=('sdl2' 'glu')
makedepends=('cmake')
arch=('x86_64')
conflicts=('sdl')
provides=('sdl=1:1.2.15+r406+gf1caf909-1')
replaces=('sdl<=1:1.2.15+r406+gf1caf909')
license=('MIT')
source=("https://github.com/libsdl-org/sdl12-compat/archive/refs/tags/release-1.2.52.tar.gz")
sha256sums=('5bd7942703575554670a8767ae030f7921a0ac3c5e2fd173a537b7c7a8599014')

build() {
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../sdl12-compat-release-${pkgver}
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/sdl12-compat-release-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
