# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=input-parsers
pkgname=haskell-input-parsers
pkgver=0.1.0.1
pkgrel=96
pkgdesc="Extension of the parsers library with more capability and efficiency"
url="https://github.com/blamario/input-parsers"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-monoid-subclasses' 'haskell-parsers')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        $_hkgname-import-fixes.patch::https://github.com/blamario/input-parsers/commit/999f30ca7c243ef46379b60bd1dcc7be43d28e65.patch)
sha512sums=('f8ede5b34b34a3b16e9fcda7f8eb22bce274a8c14b69be8836484d28729197be43a96cfe55bd852369662be68157afff6648538b1c4b05e919282b0ae3dc989d'
            '82897bcfc69d9658971854782232de9c64745159489f287735dac0aed32481ba12962c5108ff07e528038e94ab29391528a8783dc885b413a2fee77c50c889f5')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../$_hkgname-import-fixes.patch
    uusi -u monoid-subclasses $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
