# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openfec
pkgver=1.4.2.6
pkgrel=1
pkgdesc="Open Forward Erasure Codecs"
arch=(x86_64)
url="http://openfec.org/"
license=(custom:CeCILL-C)
depends=(glibc)
makedepends=(cmake)
provides=(libopenfec.so)
# NOTE: we are using roc-streaming's version, because the original upstream is effectively dead:
# https://github.com/roc-streaming/openfec
source=(https://github.com/roc-streaming/openfec/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('0819d97b4fb6d12bcf1a5e3d65aaf1d53eacca4b301b047724e8f16f89534dc5cadce8a9ddba3e74354962d1327747c9de915d9a6e19a8e7c1e4df57c00902c0')
b2sums=('1a9da7e5b0ba59a4f9d1159d974eb3fad87f072a1f93c3a34cf1b58cfaca9c842c290f8cd7aaf5f851cd41a4d5de74ab86dc9b566d5b234d0261cdb9519fbac6')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -DBUILD_SHARED_LIBS=ON
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{LICENCE,Licence}* -t "$pkgdir/usr/share/licenses/$pkgname/"
}
