# Maintainer: Anatol Pomozov

pkgname=aws-c-mqtt
pkgver=0.7.13
pkgrel=1
pkgdesc='C99 implementation of the MQTT 3.1.1 specification'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-mqtt'
license=(Apache)
depends=(aws-c-common aws-c-http)
makedepends=(cmake)
source=(aws-c-mqtt-$pkgver.zip::https://github.com/awslabs/aws-c-mqtt/archive/v$pkgver.zip)
sha256sums=('ff4a72e679dec00a5490d14b34569feeb1ec84273ccc884bbf9ec9736c8c1eb5')

build() {
  cd aws-c-mqtt-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-mqtt-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-mqtt-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
