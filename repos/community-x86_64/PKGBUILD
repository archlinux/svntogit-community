# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=singleton-bool
pkgname=haskell-singleton-bool
pkgver=0.1.5
pkgrel=26
pkgdesc="Type level booleans"
url="https://github.com/phadej/singleton-bool"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-dec')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('a2b70137f598026d0b330a5ee8265f07056e2073544f62b60cba7fec2345796f5b0d295536a536877bcb01439401047a026d8234531e27431e38701711d5df1a')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *4.13/<5/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
