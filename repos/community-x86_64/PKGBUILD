# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Johan Rehnberg <cleanrock@gmail.com>

pkgname=naev
pkgver=0.8.2
pkgrel=1
pkgdesc='2D action/rpg space game'
arch=('x86_64')
url="https://github.com/naev/naev"
license=('GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libgl' 'libxml2' 'freetype2' 'libpng' 'libzip' 'luajit')
makedepends=('freeglut' 'zip' 'meson' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/naev/naev/archive/v${pkgver}.tar.gz")
sha512sums=('c37a514dbe7ed1bca21563d20a4bb05892a07ee437eeaddefe00b5abe7009c2671da3c587db19bc8f1407db4ff3518f0cf5b28cb095778ba61946dcf87f61e5e')

build() {
  cd "$pkgname-$pkgver"

  meson setup -Dprefix=/usr -Dndata_path=/usr/share/naev/ndata build .
  cd build
  meson compile
}

package() {
  cd "$pkgname-$pkgver/build"

  DESTDIR="$pkgdir" meson install
}
