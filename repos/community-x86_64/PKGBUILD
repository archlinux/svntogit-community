# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=2.4.2
pkgrel=2
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=(x86_64)
url='https://github.com/CDrummond/cantata'
license=(GPL3)
depends=(libmtp libmusicbrainz5 mpg123 qt5-multimedia qt5-svg taglib media-player-info libcdio-paranoia udisks2)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback' 'ffmpeg: ReplayGain support' 'libebur128: ReplayGain support'
            'sshfs: remote devices support')
makedepends=(cmake qt5-tools ffmpeg libebur128)
source=(https://github.com/CDrummond/cantata/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2
        ffmpeg5.patch)
sha256sums=('3ed25d0c838720af7ed08b89a30f3e05d6952d40a5f520f29b3558b2eacdec82'
            'b11e8fa86e45b436de9ac02bd2cf728a5617893402ac9fba050e6df036b379fe')

prepare() {
  patch -d $pkgname-$pkgver -p1 < ffmpeg5.patch # Fix build with FFmpeg 5
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
