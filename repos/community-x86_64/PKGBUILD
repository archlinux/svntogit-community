# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=patchmatrix
pkgver=0.26.0
pkgrel=1
pkgdesc="A JACK patchbay in flow matrix style"
arch=('x86_64')
url="https://git.open-music-kontrollers.ch/lad/patchmatrix/about/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'libglvnd' 'libx11')
makedepends=('glew' 'glu' 'jack' 'lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lad/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('ed5f71c1aecb1cb95f4952afb16554cb653328ce08f6e9efbb6db6fdd45d6dfd9057ba592850b79f19ab889df8b22467f5a5c34849d488b83bb4f7d0cd5028a1'
            'SKIP')
b2sums=('21eafbaa4ac9283025b641ea2606a60e9f0de7db89d1ae9947d8959a3841134b43f178c1e8847efc5aaee23008c6199f691c11f064a11453700b9e9799fb232c'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  depends+=('libGLEW.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
