# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.8.16
pkgrel=3
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=('LGPL3')
depends=('glibc' 'gcc-libs' 'libmediainfo' 'qt5-declarative' 'qt5-multimedia'
         'qt5-svg' 'quazip-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('bbb195fde2c1c5019326bc275013b772da92e1325f3ff47b266474e230545611')
b2sums=('76c57eb0e6ef215a2fcaacffbcf2d10406f7a4ebbea9e420195d5391d758e7789c276e32a359a7d8bfbf74e2f3e7bd7bf75293911d3ae7aa9e8d370bccbe38ce')

prepare() {
  cd MediaElch-${pkgver}
  sed 's|quazip5/|QuaZip-Qt5-1.1/quazip/|g' -i src/*/*.cpp src/*/*.h
  sed 's|quazip5|quazip1-qt5|g' -i */CMakeLists.txt */*/CMakeLists.txt MediaElch.pro
}

build() {
  cd MediaElch-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DDISABLE_UPDATER=ON \
    -DUSE_EXTERN_QUAZIP=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd MediaElch-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
