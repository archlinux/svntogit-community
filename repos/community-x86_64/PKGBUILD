# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=5.6.0.26
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-dock' 'deepin-polkit-agent' 'deepin-file-manager'
         'deepin-session-shell' 'deepin-session-ui' 'libgnome-keyring')
makedepends=('cmake' 'coffeescript' 'golang-github-linuxdeepin-go-dbus-factory' 'golang-deepin-gir'
             'golang-deepin-lib' 'golang-deepin-dde-api' 'go' 'git' 'jq'
             'golang-golang-x-net' 'golang-github-linuxdeepin-go-x11-client')
optdepends=('deepin-wm: Legacy 3D window manager'
            'deepin-metacity: Legacy 2D window manager'
            'deepin-kwin: Preferred window manager')
replaces=('deepin-wm-switcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/startdde/archive/$pkgver.tar.gz"
        wmcmd-fix.patch::https://github.com/linuxdeepin/startdde/commit/352fa3595494ec69970fb0a02e4d6053ec6927cf.patch)
sha512sums=('b9490faa6ba74d3e1c32f6c342d032a773315290d799dc969d5ce5bb6f7f6bfcfa8f42222bad0096df004c6ae84bcaf8978d176773c8afc1e74a08c81826d8f3'
            '7aa0ee32be3276bd9cc4b1594221ca0217d2b78ffa948294d1e9d9049b965ec3bfe70a0bb799450dc342fe7cabc713f9bf2db3f4bb1207fdff6355d4eb6ecedd')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/cryptix/wav golang.org/x/xerrors github.com/godbus/dbus github.com/fsnotify/fsnotify

  sed -i 's/sbin/bin/' startdde-$pkgver/Makefile
  patch -d startdde-$pkgver -p1 < wmcmd-fix.patch
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install

  # Fix env file permission
  chmod +x "$pkgdir"/etc/X11/Xsession.d/*

  # Don't rely on deepin-session's location
  install -dm755 "$pkgdir"/etc/X11/xinit/xinitrc.d
  mv "$pkgdir"/etc/X11/Xsession.d/* "$pkgdir"/etc/X11/xinit/xinitrc.d/
  rmdir "$pkgdir"/etc/X11/Xsession.d
}
