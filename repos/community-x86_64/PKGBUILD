# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-visualization-projectm
epoch=1
pkgver=19.0.1
_codename=Matrix
pkgrel=1
pkgdesc="ProjectM visualizer for Kodi"
arch=('x86_64')
url='https://github.com/xbmc/visualization.projectm'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-visualization')
makedepends=('cmake' 'projectm' 'kodi-dev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xbmc/visualization.projectm/archive/$pkgver-$_codename.tar.gz"
)
sha512sums=('be7a898e2646e0042efb93334ec61a668ffecaa64c51ce041f4a044461dd46b6964eea5a8ef9de5e97c6fa8bb841be4d6e153ca985e727eae1f399b15b5fa973')

build() {
    cd "visualization.projectm-$pkgver-$_codename"
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi' 'projectm')
    cd "visualization.projectm-$pkgver-$_codename"
    make DESTDIR="$pkgdir/" install
}

