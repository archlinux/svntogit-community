# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=cppcheck
pkgver=1.63.1
pkgrel=2
pkgdesc="A tool for static C/C++ code analysis"
arch=('i686' 'x86_64')
url="http://cppcheck.wiki.sourceforge.net/"
license=('GPL')
depends=('qt5-base')
makedepends=('docbook-xsl')
source=(https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz)
sha1sums=('19ad7251603356a82dc75a836dfad9629a3d12a0')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make CFGDIR=/usr/share/cppcheck/cfg
   make DB2MAN=/usr/share/xml/docbook/xsl-stylesheets-1.78.1/manpages/docbook.xsl man

   cd gui
   export HAVE_RULES=yes 
   qmake
   make 
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install

   install -D -p -m 644 cppcheck.1 "${pkgdir}"/usr/share/man/man1/cppcheck.1

   install -m755 gui/cppcheck-gui "${pkgdir}"/usr/bin

   for _f in cfg/*; do
     install -D -p -m 644 $_f "${pkgdir}"/usr/share/"${pkgname}"/$_f
   done
}
