# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alex Dewar <alex.dewar@gmx.co.uk>

pkgname=lifeograph
pkgver=2.0.2
pkgrel=1
pkgdesc='Private journal, diary and note taking application'
arch=('x86_64')
url='http://lifeograph.sourceforge.net/'
license=('GPL3')
depends=('enchant' 'gtkmm3' 'libchamplain')
makedepends=('meson')
source=("https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('b4271233bad26ed0cee191d2084d09741ed7e9e8bcdc36552d29f5039a1bd9e9'
            'SKIP')
validpgpkeys=(A1DD7BBF56F328A5B0679657BC797409ACE27593) # Ahmet Öztürk <aoz_2@yahoo.com>

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
