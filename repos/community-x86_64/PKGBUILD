# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.10.14
pkgrel=1
pkgdesc="COIN-OR collection of utility classes"
arch=(i686 x86_64)
url="https://projects.coin-or.org/CoinUtils"
license=(EPL)
depends=(zlib bzip2 glpk lapack)
makedepends=(gcc-fortran doxygen)
groups=(coin-or)
source=("http://www.coin-or.org/download/source/CoinUtils/CoinUtils-$pkgver.tgz")
sha256sums=('85a99b70b0271c0409c2b10299529ffa1623b176ee0ba1567c46d7fc17371628')

build() {
  cd CoinUtils-$pkgver

  ./configure --prefix=/usr --with-blas-lib='-lblas' --with-lapack-lib='-llapack' --with-glpk-lib='-lglpk' --enable-dependency-linking
  make
}

check() {
  cd CoinUtils-$pkgver
  make test
}

package() {
  cd CoinUtils-$pkgver
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
