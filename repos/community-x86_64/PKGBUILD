# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: David Moore <davidm@sjsoft.com>

pkgbase=python-simplejson
pkgname=("$pkgbase" 'python2-simplejson')
pkgver=3.7.2
pkgrel=1
pkgdesc='Simple, fast, extensible JSON encoder/decoder for Python'
license=('MIT')
arch=('x86_64' 'i686')
url='https://github.com/simplejson/simplejson'
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git://github.com/simplejson/simplejson.git#tag=v$pkgver")
md5sums=('SKIP')

package_python-simplejson() {
  depends=('python')

  cd simplejson
  python setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir/simplejson/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-simplejson() {
  depends=('python2')

  cd simplejson
  python2 setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir/simplejson/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
