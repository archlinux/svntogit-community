# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=openFPGALoader
pkgname=openfpgaloader
pkgver=0.5.0
pkgrel=2
pkgdesc='Universal utility for programming FPGA'
arch=('x86_64')
url='https://github.com/trabucayre/openFPGALoader'
license=('Apache2')
depends=('libftdi' 'libftdi-compat' 'libhidapi-libusb.so' 'libudev.so')
makedepends=('cmake' 'libusb' 'systemd')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('067405358ea06d21da7952db8b797c4067e6a41a531b5b946bd04eedac3d176ac61595f13e151b4f51915a602c8f74ee17f8089004ab00ccaee563766487db17')

prepare() {
  sed -i 's|MODE="664", GROUP="plugdev"|MODE="666"|g' $_pkgname-$pkgver/99-openfpgaloader.rules
}

build() {
  mkdir -p $_pkgname-$pkgver/build
  cd $_pkgname-$pkgver/build

  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DENABLE_UDEV=True \
    -DENABLE_CMSISDAP=True

  make
}

package() {
  cd $_pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 ../99-openfpgaloader.rules "$pkgdir"/usr/lib/udev/rules.d/99-openfpgaloader.rules
}

