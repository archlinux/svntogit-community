# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=safecopy
pkgname=haskell-safecopy
pkgver=0.9.4.3
pkgrel=9
pkgdesc="Binary serialization with version control."
url="http://acid-state.seize.it/safecopy"
license=('custom:PublicDomain')
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-old-time' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('65ad018a6300f80e0c37568ca564a4762ab458db29ae6fcf7d52d10c6438292a071eb20dbee25113c958f3883c15039447c9a30692ab8f7e5e2f3e1848828d48')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
}
