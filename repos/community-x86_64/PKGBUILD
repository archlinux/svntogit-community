# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=networkmanager-fortisslvpn
pkgver=1.4rc1
pkgrel=2
pkgdesc='Fortinet SSLVPN support for NetworkManager'
arch=(x86_64)
license=(GPL)
url='https://gitlab.gnome.org/GNOME/NetworkManager-fortisslvpn'
depends=(networkmanager nm-connection-editor openfortivpn libsecret)
makedepends=(git intltool python)
_commit=9688d63ecacec97d0c9217281eb7b85053f1f4a1
source=("git+https://gitlab.gnome.org/GNOME/NetworkManager-fortisslvpn.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd NetworkManager-fortisslvpn
  autoreconf -fi
}

build() {
  cd NetworkManager-fortisslvpn
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/networkmanager \
    --disable-static
  make
}

package() {
  cd NetworkManager-fortisslvpn
  make DESTDIR="$pkgdir" install
}
