# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=idris
pkgver=0.10
pkgrel=1
pkgdesc="Functional Programming Language with Dependent Types"
url="http://www.idris-lang.org/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=7.10.3" "haskell-annotated-wl-pprint" "haskell-ansi-terminal" "haskell-ansi-wl-pprint"
         "haskell-async" "haskell-base64-bytestring" "haskell-blaze-html" "haskell-blaze-markup"
         "haskell-cheapskate" "haskell-fingertree" "haskell-fsnotify" "haskell-mtl" "haskell-libffi"
         "haskell-network" "haskell-optparse-applicative" "haskell-parsers" "haskell-safe"
         "haskell-split" "haskell-text" "haskell-transformers-compat" "haskell-trifecta"
         "haskell-uniplate" "haskell-unordered-containers" "haskell-utf8-string" "haskell-vector"
         "haskell-vector-binary-instances" "haskell-zip-archive")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5593feca2cdd00ff819f37135da496111b3af06b664f4cd1f4aecba6ac6e6a10')

prepare() {
    sed -i "s/, async < 2.1/, async < 2.2/" ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgbase" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fFFI -fGMP # -fcurses
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build
    LC_CTYPE=en_US.UTF-8 runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    depends=('gmp' 'libffi' 'zlib')

    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
}

