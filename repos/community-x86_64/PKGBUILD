# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=blosc2
pkgver=2.9.1
pkgrel=1
pkgdesc="A fast, compressed, persistent binary data store library for C."
arch=(x86_64)
url="https://www.blosc.org"
license=(BSD)
depends=(lz4 zlib zstd)
makedepends=(cmake)
source=(https://github.com/Blosc/c-blosc2/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('9430c026f9d7a98e38915e3d308402989073aa6fa1e8e1d5b9bdb155c70c5cc24ca118cdb5b3584647f72ae80a13be0a8467250af7ef336a1ac72eb8ec8a67f5')
b2sums=('1853611566f081f5c6236ade1882b07aa6d596f65bb303823f418faaf0e971c1850e3e78ca501650880d4611110200c302896e81a5b13cdf05bc43c81b81dd3a')

build() {
  cmake -B build -S c-blosc2-$pkgver \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D PREFER_EXTERNAL_LZ4=ON \
    -D PREFER_EXTERNAL_ZLIB=ON \
    -D PREFER_EXTERNAL_ZSTD=ON
  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 c-blosc2-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
