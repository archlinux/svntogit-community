# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.9636
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses zlib)
makedepends=(git cmake clang lld llvm python libunwind)
options=(!emptydirs !lto)
source=(${url}/archive/igc-${pkgver}.tar.gz
        ${pkgname}-fix-lld-prebuilt.patch::https://github.com/intel/intel-graphics-compiler/pull/210.patch
        ${pkgname}-fix-missing-limits.patch::https://github.com/intel/intel-graphics-compiler/pull/220.patch
        git+https://github.com/intel/vc-intrinsics.git#commit=8ee879314584e6630688b0a3b290d065dcabb383
        git+https://github.com/KhronosGroup/SPIRV-Tools.git
        git+https://github.com/KhronosGroup/SPIRV-Headers.git)
sha256sums=('be8d1b162b838da8eb141ef103e136874c4542a8c80f3d1fd0f27ce1aadaf485'
            'b2da1a00ac59ce7e98fd6cfb554f8974b1500bc1b9973387181414ffa4cffea5'
            '4505488fa483446ff50d041609fca63762e5f82711ada1321a94f97165115c88'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd ${pkgname}-igc-${pkgver}
  patch -p1 < ../${pkgname}-fix-lld-prebuilt.patch
  patch -p1 < ../${pkgname}-fix-missing-limits.patch
}

build() {
  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='13.0.0' \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__USE_KHRONOS_SPIRV_TRANSLATOR_IN_VC=ON \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DVC_INTRINSICS_SRC="${srcdir}/vc-intrinsics" \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
