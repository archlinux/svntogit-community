# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=texworks
pkgver=0.6.3
pkgrel=6
pkgdesc='Unicode-based, TeX-aware editor with integrated PDF viewer'
arch=('x86_64')
url='https://www.tug.org/texworks/'
license=('GPL')
depends=('hunspell' 'libsynctex' 'poppler-qt5' 'qt5-script')
makedepends=('cmake' 'lua' 'python' 'qt5-tools')
checkdepends=('gsfonts' 'poppler-data' 'xorg-server-xvfb')
optdepends=('gsfonts: correctly display PDF files without embedded fonts'
            'lua: Lua scripts support'
            'poppler-data: correctly display certain CJK documents'
            'python: Python scripts support'
            'texlive-core: typeset TeX documents')
source=("$pkgname-$pkgver.tar.gz::https://github.com/TeXworks/texworks/archive/release-$pkgver.tar.gz")
sha512sums=('389cf54441e74164fc842f2f293cdee45c6ff9fe202f262d32f3fb076de9a29813a62cd75052969293f2f784e0722215bbbf2206e75bc836a13d495bf5aed653')

prepare() {
	mkdir build
}

build() {
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	      -DTW_BUILD_ID='Arch Linux' \
	      -DWITH_PYTHON=ON \
	      "$srcdir/texworks-release-$pkgver"
	make
}

check() {
	cd build
	xvfb-run ctest --output-on-failure
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
