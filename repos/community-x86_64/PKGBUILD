# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=incremental-parser
pkgname=haskell-incremental-parser
pkgver=0.4.0.2
pkgrel=11
pkgdesc="Generic parser library capable of providing partial results from partial input."
url="https://hackage.haskell.org/package/${_hkgname}"
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-monoid-subclasses' 'haskell-parsers' 'haskell-rank2classes')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-checkers' 'haskell-tasty'
             'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('ba9c7a597c6d7a78e5653c3408e2ba1c815ce530276be7817edcb76e0cdd12015908c9a47b8c37539756a37834117c04db5638eb2a52022d4b76367f13848f2e')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE.txt"
}
