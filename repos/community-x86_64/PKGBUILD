# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=latte-integrale
pkgver=1.7.3
pkgrel=3
pkgdesc="Computer software dedicated to the problems of counting lattice points and integration inside convex polytopes"
arch=(i686 x86_64)
url="https://www.math.ucdavis.edu/~latte/"
license=(GPL)
depends=(4ti2 ntl cddlib lidia)
source=("https://www.math.ucdavis.edu/~latte/software/packages/latte_current/latte-int-$pkgver.tar.gz")
sha256sums=('b6777067d11a454e4dbaf5af8fb227d2b66960d79245ea967192738ad2a1e76c')

build() {
  cd latte-int-$pkgver

  export CXXFLAGS="-DNTL_STD_CXX $CXXFLAGS"
  ./configure --prefix=/usr --enable-shared --with-ntl=/usr
  make
}

package() {
  cd latte-int-$pkgver
  make DESTDIR="$pkgdir" install

# Fix conflicts with LLVM
  mv "$pkgdir"/usr/bin/{count,latte-count}
}

