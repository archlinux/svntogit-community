# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=40.1
pkgrel=2
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/DejaDup"
license=('GPL')
depends=('duplicity' 'gnome-keyring' 'gnome-online-accounts' 'gvfs' 'python2-gobject' 'python2-pydrive')
makedepends=('appstream-glib' 'gobject-introspection' 'intltool' 'itstool' 'meson' 'nautilus' 'vala' git)
optdepends=('nautilus: backup extension')
_commit=a44f15478e7ad8d02807b113a4d4f9490a27980e  # tags/40.1
source=("git+https://gitlab.gnome.org/World/deja-dup.git#commit=$_commit")
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
