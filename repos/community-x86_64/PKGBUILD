# Maintainer: Anatol Pomozov
# Contributor: Keinv Yue <yuezk001@gmail.com>

pkgname=globalprotect-openconnect
pkgver=1.3.4
pkgrel=3
pkgdesc="A GlobalProtect VPN GUI client based on Openconnect and built with Qt5, supports SAML auth mode"
arch=(x86_64)
url="https://github.com/yuezk/GlobalProtect-openconnect"
license=(GPL3)
depends=(openconnect qt5-base qt5-webengine qt5-websockets)
makedepends=(git cmake)
provides=('gpclient' 'gpservice')
source=(git+https://github.com/yuezk/GlobalProtect-openconnect#tag=v$pkgver) # github does not allow to download sources with submodules, thus use git directly
sha256sums=('SKIP')

build() {
  cd GlobalProtect-openconnect
  cmake -B build -DCMAKE_BUILD_TYPE=Release
  cmake --build build
}

package() {
  cd GlobalProtect-openconnect
  make DESTDIR="$pkgdir/" install -C build
}
