# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: feuri

pkgname=dunst
pkgver=1.7.1
pkgrel=1
pkgdesc="Customizable and lightweight notification-daemon"
url="https://dunst-project.org/"
arch=('x86_64')
license=('BSD')
depends=('libxinerama' 'dbus' 'systemd' 'wayland' 'libxss' 'pango' 'gdk-pixbuf2' 'libxrandr' 'glib2')
makedepends=('libnotify')
optdepends=('libnotify: dunstify')
backup=('etc/dunst/dunstrc')
provides=('notification-daemon')
source=("dunst-$pkgver.tar.gz::https://github.com/dunst-project/dunst/archive/v$pkgver.tar.gz")
md5sums=('0a994d35635406726036270ce40ba361')

build() {
  cd dunst-$pkgver

  make PREFIX=/usr SYSCONFDIR=/etc X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 all
}

package() {
  cd dunst-$pkgver

  make DESTDIR="$pkgdir" SYSCONFDIR=/etc PREFIX=/usr install
  install -Dm755 dunstify "$pkgdir"/usr/bin/dunstify
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
