# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.2.1
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=(x86_64)
url="https://yoshimi.github.io/"
license=(GPL2)
groups=(pro-audio)
depends=(cairo gcc-libs glibc zlib)
makedepends=(alsa-lib cmake fftw fltk jack lv2 mxml ncurses readline)
checkdepends=(kxstudio-lv2-extensions lv2lint)
optdepends=('lv2-host: for LV2 plugin')
# plugin exposes symbols globally if built with LTO:
# https://github.com/Yoshimi/yoshimi/issues/164
options=(!lto debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz)
sha512sums=('9f1895273b8e6ecedf503d59f351f568c189a03d5111d3bc7de8cc0c025f89f48383540f462184b9fad9487ea3cc18c0cc6cb6b1ba7ccd2619ddb2c28c4c66f0')
b2sums=('8ef8851b6e0808190f8bf97fc6502448d703cd910726f44c8c1f839299badcd858872291f54d4d219d12e11acc31ec1d342f5df5009d71b0fc1d8eae2f6cb7d1')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build\
        -S $pkgname-$pkgver/src
  make VERBOSE=1 -C build
}

check() {
  cp -v build/LV2_Plugin/${pkgname}_lv2.so $pkgname-$pkgver/src/LV2_Plugin/
  lv2lint -Mpack -I $pkgname-$pkgver/src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v $pkgname-$pkgver/src/LV2_Plugin/${pkgname}_lv2.so
}

package() {
  depends+=(libasound.so libfftw3f.so libfltk.so libfltk_images.so libjack.so
  libmxml.so libncursesw.so libreadline.so)

  make DESTDIR="$pkgdir" VERBOSE=1 install -C build
  install -vDm 644 $pkgname-$pkgver/{Changelog,Dependencies,README.txt,Yoshimi_Helpers} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
