# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.7.3221
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator'
arch=(x86_64)
url=https://www.pcsx2.net
license=(
  GPL2
  GPL3
  LGPL2.1
  LGPL3
)
depends=(
  glibc
  libaio.so
  libasound.so
  libfmt.so
  libgl
  libharfbuzz.so
  libpcap.so
  libpng
  libpulse.so
  libryml.so
  libsamplerate.so
  libudev.so
  libx11
  libxcb
  libxrandr
  libzip.so
  libzstd.so
  qt6-base
  sdl2
  soundtouch
  xz
  zlib
)
makedepends=(
  cmake
  git
  glslang
  ninja
  png++
  qt6-tools
  qt6-wayland
  vulkan-headers
  zstd
)
optdepends=('qt6-wayland: Wayland support')
_tag=7285eca24982c0562d47958022ac0dfdc3cebca5
source=(
  git+https://github.com/PCSX2/pcsx2.git#tag=${_tag}
  git+https://github.com/mozilla/cubeb.git
  git+https://github.com/KhronosGroup/glslang.git
  git+https://github.com/ocornut/imgui.git
  git+https://github.com/rtissera/libchdr.git
  pcsx2-system-libs.patch
  pcsx2-resources-directory.patch
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '29c8f279f96c67b3a35eb39440c6dfa134ef1cda36026f69f7b9931c59700e61431d7b38002bd811f6e38089296128a16c737e1c0eb2120b342790089c1e4017'
        '7e9ea4915021d48a619c2e37cc01de15ae239bb08fef367a9903edefcbc362cd2537f741b89f6c09144319bf2228bea0c7c366e5f0a85f1f282ef95ee3f05321')
install=pcsx2.install

prepare() {
  cd pcsx2
  git apply -3 ../pcsx2-system-libs.patch
  git apply -3 ../pcsx2-resources-directory.patch

  local submodule
  for submodule in \
    3rdparty/cubeb/cubeb \
    3rdparty/glslang/glslang \
    3rdparty/imgui/imgui \
    3rdparty/libchdr/libchdr
  do
    git submodule init ${submodule}
    git submodule set-url ${submodule} "$srcdir/${submodule##*/}"
    git submodule update ${submodule}
  done
}

pkgver() {
  cd pcsx2
  git describe --tags | sed 's/^v//'
}

build() {
  # Speed up build
  CFLAGS+=" -flto=$(nproc)"
  CXXFLAGS+=" -flto=$(nproc)"

  cmake -S pcsx2 -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
    -DDISABLE_ADVANCE_SIMD=ON \
    -DDISABLE_BUILD_DATE=ON \
    -DDISABLE_PCSX2_WRAPPER=ON \
    -DDISABLE_SETCAP=ON \
    -DENABLE_TESTS=OFF \
    -DPACKAGE_MODE=ON \
    -DQT_BUILD=ON \
    -DUSE_SYSTEM_LIBS=ON \
    -DUSE_VTUNE=OFF \
    -DWAYLAND_API=ON \
    -DX11_API=ON \
    -DXDG_STD=ON \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  sed -i 's/Exec=env GDK_BACKEND=x11 MESA_NO_ERROR=1 pcsx2/Exec=env QT_QPA_PLATFORM=xcb MESA_NO_ERROR=1 pcsx2-qt/g' "${pkgdir}"/usr/share/applications/PCSX2.desktop
}

# vim: ts=2 sw=2 et:
