# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=5.5.17.1
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('LGPL3')
depends=('dtkcommon' 'deepin-desktop-base' 'python' 'gsettings-qt' 'lshw')
makedepends=('qt5-tools' 'gtest')
source=("https://github.com/linuxdeepin/dtkcore/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b055552b352a0c244a826a364706d53790594736b77d4723ce3086ad346e33942f090f2082611432c35d502afde696eff298f5b2e0a49e3884f31ca60c810268')

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver LIB_INSTALL_DIR=/usr/lib
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
