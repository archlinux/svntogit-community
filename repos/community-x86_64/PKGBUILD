# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.5.4
pkgrel=1
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='http://pari.math.u-bordeaux.fr/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gmp' 'readline' 'libx11')
makedepends=('perl' 'texlive-core')
optdepends=('perl: gphelp, tex2mail')
source=("${url}pub/pari/unix/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha1sums=('471226fd86fea0ad1f236249a49bdaee16aa34bf' 'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/\$addlib64//g' -i config/get_libpth
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./Configure --prefix=/usr --with-gmp --with-readline
	make all
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make bench
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -sf gp.1.gz "${pkgdir}"/usr/share/man/man1/pari.1
}
