# Maintainer: David Runge <dave@sleepmap.de>

pkgname=rtosc
pkgver=0.2.0
pkgrel=2
pkgdesc="Realtime safe OSC Messaging"
arch=('x86_64')
url="https://github.com/fundamental/rtosc"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake' 'doxygen')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fundamental/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}-fix-pkg-config-file.patch"
        "${pkgname}-include-missing-headers.patch")
sha512sums=('f19aed587c752f33a063849fce2b4f531b734d8ae68d9f3b623da2872260b258b92c24b6160734a01f091ba821a4bd72cae98a138570ef6c741b03e1353d835a'
            '65f6ada68c74b4c7f5e1bf3939dce3f04fbc704a8fd5386e003951201c927dc6776327c520de0b6cca2c9b09445072f931fd9cc08ed4a50c94a500f06cce4244'
            '25db71244ce931919c6c95ca3b90fe3d4f21537a913b1c1056559b28fe59abbe6643113d46d5fda02ff3c11de4c4e51ac0f03b1a9b550176f3d547f698dccfe4')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -p build
  # add missing headers: https://github.com/fundamental/rtosc/issues/41
  patch -Np1 -i ../"${pkgname}-include-missing-headers.patch"
  # fix libdir in pkg-config file:
  # https://github.com/fundamental/rtosc/issues/42
  patch -Np1 -i ../"${pkgname}-fix-pkg-config-file.patch"
  sed -e 's/STATIC/SHARED/g' -i CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DPERF_TEST=ON
  make VERBOSE=1
  doxygen
}

check() {
  cd "$pkgname-$pkgver/build"
  make -k test
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 html/search/*.{js,png,html} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html/search"
  install -vDm 644 html/*.{css,js,png,html} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 ../README.adoc \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 ../LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
