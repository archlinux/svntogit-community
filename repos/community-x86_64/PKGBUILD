# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-picker
pkgver=5.0.16
pkgrel=1
pkgdesc='Color picker tool for deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-picker"
license=('GPL3')
depends=('deepin-qt5integration' 'libxtst' 'qt5-svg')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-picker/archive/$pkgver.tar.gz")
sha512sums=('83f90b6c3fd23cb66b8050d530fc59ad6ce1e8ba36b8df7ed0884eaee0b34b38ed2afa7c524318aad1d57dbe60121ac067a585032f12d3e97c8cdb8b3986a6f9')

prepare() {
  cd deepin-picker-$pkgver
  sed -i '/#include <QPainter>/a #include <QPainterPath>' src/animation.cpp src/picker.cpp
}

build() {
  cd deepin-picker-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-picker-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
