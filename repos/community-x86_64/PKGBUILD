# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lex Black <autumn-wind at web dot de>
# Contributor: Michael Jakl <jakl.michael@gmail.com>
# Contributor: devmotion <nospam-archlinux.org@devmotion.de>
# Contributor: Valentin Churavy <v.churavy@gmail.com>

pkgbase=julia
pkgname=('julia' 'julia-docs')
epoch=2
pkgver=0.6.2
pkgrel=1
arch=('x86_64')
pkgdesc='High-level, high-performance, dynamic programming language'
url='https://julialang.org/'
license=('MIT')
makedepends=('arpack' 'cmake' 'fftw' 'gcc-fortran' 'gmp' 'libgit2' 'libunwind' 'llvm39' 'mpfr' 'patchelf' 'pcre2' 'python2' 'suitesparse')
options=('!emptydirs' 'staticlibs')
source=("https://github.com/JuliaLang/$pkgbase/releases/download/v$pkgver/$pkgbase-$pkgver-full.tar.gz"{,.asc}
        julia-libunwind-version.patch
        julia-makefile.patch)
sha256sums=('1e34c13091c9ddb47cf87a51566d94a06613f3db3c483b8f63b276e416dd621b'
            'SKIP'
            '16009ce454f58463464027cbaf6186ccfab84c37660a52fb1ec3a0f514df64f9'
            '342b43532fbace3508688eee879113160e01cbbda29610b53f2f34796bc0312a')
validpgpkeys=('3673DF529D9049477F76B37566E3C7DC03D6E495') # Julia (Binary signing key) <buildbot@julialang.org>

prepare() {
  # For /etc/ld.so.conf.d/
  echo '/usr/lib/julia' > julia.conf

  cd "$pkgbase-$pkgver"
  patch -p1 -i ../julia-libunwind-version.patch
  sed -e 's|-lLLVM|-lLLVM-3.9|' -i src/Makefile # link against llvm39
  patch -p0 -i ../julia-makefile.patch # make 'make install' really just install
}

build() {
  # Make sure not to set -march
  export CFLAGS="-mtune=generic -O3 -pipe -fstack-protector-strong"
  export CXXFLAGS="-mtune=generic -O3 -pipe -fstack-protector-strong"

  unset ARCH

  make -C "$pkgbase-$pkgver" \
    prefix=/usr \
    sysconfdir=/etc \
    MARCH="x86-64" \
    JULIA_BUILD_MODE=release \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_BLAS=0 \
    USE_SYSTEM_LAPACK=0 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=1 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=0 \
    USE_INTEL_MKL=0 \
    USE_BLAS64=0 \
    USE_LLVM_SHLIB=1 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_DSFMT=0
}

package_julia() {
  backup=('etc/ld.so.conf.d/julia.conf' 'etc/julia/juliarc.jl')
  depends=('arpack' 'fftw' 'hicolor-icon-theme' 'libgit2' 'libunwind' 'llvm39-libs' 'mpfr' 'patchelf' 'pcre2' 'suitesparse')
  optdepends=('gnuplot: If using the Gaston Package from julia')
  install='sysfix.install'

  # Make sure not to set -march
  export CFLAGS="-mtune=generic -O3 -pipe -fstack-protector-strong"
  export CXXFLAGS="-mtune=generic -O3 -pipe -fstack-protector-strong"

  unset ARCH

  make -C "$pkgbase-$pkgver" \
    DESTDIR="$pkgdir" \
    prefix=/usr \
    sysconfdir=/etc  \
    MARCH="x86-64" \
    USE_SYSTEM_LLVM=1 \
    USE_SYSTEM_LIBUNWIND=1 \
    USE_SYSTEM_PCRE=1 \
    USE_SYSTEM_LIBM=1 \
    USE_SYSTEM_OPENLIBM=0 \
    USE_SYSTEM_OPENSPECFUN=0 \
    USE_SYSTEM_BLAS=0 \
    USE_SYSTEM_LAPACK=0 \
    USE_SYSTEM_FFTW=1 \
    USE_SYSTEM_GMP=1 \
    USE_SYSTEM_MPFR=1 \
    USE_SYSTEM_ARPACK=1 \
    USE_SYSTEM_SUITESPARSE=1 \
    USE_SYSTEM_LIBUV=0 \
    USE_SYSTEM_UTF8PROC=0 \
    USE_INTEL_MKL=0 \
    USE_BLAS64=0 \
    USE_LLVM_SHLIB=1 \
    USE_SYSTEM_LIBGIT2=1 \
    USE_SYSTEM_PATCHELF=1 \
    USE_SYSTEM_DSFMT=0 \
    install

  # Remove duplicate man-page from julia/doc
  rm -rvf "$pkgdir/usr/share/julia/doc/man"

  # For /etc/ld.so.conf.d, FS#41731
  install -Dm644 "$srcdir/julia.conf" "$pkgdir/etc/ld.so.conf.d/julia.conf"

  # Documentation and examples are in the julia-docs package
  rm -rvf "$pkgdir/usr/share/doc/"
  rm -rvf "$pkgdir/usr/share/julia/doc/"
  rm -rvf "$pkgdir/usr/share/julia/examples/"

  # License
  install -Dm644 "$pkgbase-$pkgver"/LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"

  # Minor improvement
  patchelf --set-rpath /usr/lib "$pkgdir/usr/bin/julia"
}

package_julia-docs() {
  pkgdesc='Documentation and examples for Julia'

  cd "$pkgbase-$pkgver"

  install -d "$pkgdir/usr/share/doc"
  cp -rv doc "$pkgdir/usr/share/doc/$pkgbase"
  cp -rv examples "$pkgdir/usr/share/doc/$pkgbase/examples"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# getver: julialang.org/downloads
# vim: ts=2 sw=2 et:
