# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libtraceevent
pkgver=1.3.3
pkgrel=1
epoch=1
pkgdesc="Linux kernel trace event library"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/libs/libtrace/libtraceevent.git/about/"
license=('GPL2' 'LGPL2.1')
depends=('glibc')
makedepends=('asciidoc' 'xmlto')
provides=('libtraceevent.so')
source=("$pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/libs/libtrace/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('36e14e44c74b43f740b1eeaa5edbe630467dd7b084e92c3037e18a4b711e3f66254cdaa2fee7d11e6171aeffe6ffb6283a2661ce8f97671b588e6bd431da133a')
b2sums=('650ff01e85955eeca9ddcf2b2a304c5d72c6911d4fc2ff9631f037f4f1771407b7e54b308e20337dbe02a9f32f12a7e76695336e3a01a5cf00907d7f885525b6')

build() {
  cd "$pkgname-$pkgver"
  make
  make -C Documentation
}

package() {
  cd "$pkgname-$pkgver"
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C Documentation
}
