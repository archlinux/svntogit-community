# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Christian Pfeiffer <cpfeiffer at live dot de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jwwolf <jwwolf+arch@gmail.com>
# Contributor: Bernhard Walle <bernhard@bwalle.de>

pkgname=crosstool-ng
pkgver=1.25.0
pkgrel=2
pkgdesc='A versatile (cross-)toolchain generator'
arch=(x86_64)
url=https://crosstool-ng.github.io/
license=(GPL2)
depends=(
  git
  gperf
  help2man
  lzip
  meson
  ncurses
  python
  rsync
  unzip
)
_tag=8fa98eeeff9bc53478d97ef722f366fea151ae64
source=(git+https://github.com/crosstool-ng/crosstool-ng.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd crosstool-ng
  ./bootstrap
}

build() {
  cd crosstool-ng
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --with-bash-completion \
    --with-ncurses
  make
}

package() {
  cd crosstool-ng
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
