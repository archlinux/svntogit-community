# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=openpgp-asciiarmor
pkgname=haskell-openpgp-asciiarmor
pkgver=0.1
pkgrel=24
pkgdesc="OpenPGP (RFC4880) ASCII Armor codec"
url="http://floss.scru.org/openpgp-asciiarmor"
license=('custom:ISC')
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-base64-bytestring' 'haskell-cereal')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('16b76973c276f7bdc871b529eb362d654dfdf55991cd0cbf16d52f649ea108987c9c7a26c216738e85b5a86bb367693bfc3e9134d1c2cf68b63a80bbc10da9ad')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
