# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Mohamed Sobh <mohamed.alhusieny@gmail.com>
# Contributor: Drew Noel <drewmnoel@gmail.com>
# Contributor: Manuel Hüsers <manuel.huesers@uni-ol.de>
# Contributor: fstirlitz <felix.von.s@posteo.de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Matej Lach <matej.lach@gmail.com>

pkgname=birdfont
pkgver=2.29.0
pkgrel=2
pkgdesc='Font editor which can generate fonts in TTF, EOT and SVG formats'
arch=(x86_64)
url='https://birdfont.org/'
license=(GPL3)
depends=(libgee libxmlbird webkit2gtk)
makedepends=(python vala)
source=("https://birdfont.org/releases/$pkgname-$pkgver.tar.xz"{,.sig}
        appdata.patch)
sha256sums=('1c6d6da20b0705f61aa9478797e8c336ef6a6b986ce04e1800f6baa5f3c2e3a3'
            'SKIP'
            'e01980dbac7bb0ee4caaaf6b150cae1bc80d77570d86f81259d3fb1233b29868')
validpgpkeys=('FB3BEFA59A6FF7F0E0682B68BCD31D4CCCEB9DD4') # Johan Mattsson

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../appdata.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix /usr --cflags="$CFLAGS" --ldflags="$LDFLAGS"
  ./build.py
}

package() {
  cd $pkgname-$pkgver
  ./install.py --dest "$pkgdir" --libdir /lib --manpages-directory /share/man/man1
}
