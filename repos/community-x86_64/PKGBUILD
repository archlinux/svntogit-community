# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vis
_tag='b1bba35b097639865a4f2accc92194d9ce8e39e0' # git rev-parse v${pkgver}
pkgver=0.7
pkgrel=2
pkgdesc='modern, legacy free, simple yet efficient vim-like editor'
arch=('x86_64')
url='https://github.com/martanne/vis#vis-a-vim-like-text-editor'
depends=('acl' 'libacl.so' 'bash' 'ncurses' 'libncursesw.so' 'libtermkey'
         'libtermkey.so' 'lua' 'lua-lpeg' 'tre' 'file')
optdepends=('wl-clipboard: wayland clipboard support'
            'xclip: X11 clipboard support'
            'xsel: X11 clipboard support')
makedepends=('git')
checkdepends=('vim')
license=('custom:ISC')
backup=('etc/vis/visrc.lua')
validpgpkeys=('54A47F12462D26A7D078A932C83A52CD1962562C') # Marc Andr√© Tanner <mat@brain-dump.org>
source=("git://github.com/martanne/vis.git#tag=${_tag}?signed"
        'git://github.com/martanne/vis-test.git')
sha256sums=('SKIP'
            'SKIP')

_backports=(
  # lexers: fix bug in bash lexer for last here-doc 
  'e57851806592f5ea4ee48303b97a5f59731370c5'
)

_reverts=(
)

prepare() {
  cd vis/

  git config --file=.gitmodules submodule.test.url ../vis-test/
  git update-index --assume-unchanged .gitmodules
  git submodule init
  git submodule update
  
  local _c
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"
    git cherry-pick -n "${_c}"
  done
  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done
}

build() {
  cd vis/

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd vis/

  make -C test/
}

package() {
  cd vis/

  make DESTDIR="${pkgdir}" install

  install -D -m0644 lua/visrc.lua "${pkgdir}"/etc/vis/visrc.lua

  install -D -m0644 'LICENSE' "${pkgdir}/usr/share/licenses/vis/LICENSE"
}

