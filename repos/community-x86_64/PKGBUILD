# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=hiredis
pkgver=0.13.3
pkgrel=2
pkgdesc='Minimalistic C client library for Redis'
arch=('x86_64')
url="https://github.com/redis/hiredis/"
license=('BSD')
depends=('glibc')
checkdepends=('redis')
source=(https://github.com/redis/$pkgname/archive/v$pkgver/hiredis-$pkgver.tar.gz)
sha256sums=('717e6fc8dc2819bef522deaca516de9e51b9dfa68fe393b7db5c3b6079196f78')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make PREFIX=/usr
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
