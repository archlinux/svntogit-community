# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>

pkgname=libmediainfo
pkgver=17.12
pkgrel=1

pkgdesc="shared library for mediainfo"
url="http://mediaarea.net"
arch=('x86_64')
license=('GPL')

depends=('curl' 'libmms' 'libzen')
makedepends=('libtool' 'automake' 'autoconf')

source=(https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz)

sha256sums=('b482bb34df5d59ae8b735b9631ad0ad150dc71b715e851f1d3155ceb4921a452')

build() {
  cd MediaInfoLib/Project/GNU/Library

  sh ./autogen.sh
  ./configure --prefix=/usr \
              --enable-shared \
              --disable-static \
              --with-libcurl \
              --with-libmms

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install
  for i in MediaInfo MediaInfoDLL; do
    install -dm 755 "$pkgdir"/usr/include/$i
    install -m 644 "$srcdir"/MediaInfoLib/Source/$i/*.h "$pkgdir"/usr/include/$i
  done
  install -dm 755 "$pkgdir"/usr/lib/pkgconfig
  install -m 644 "$srcdir"/MediaInfoLib/Project/GNU/Library/libmediainfo.pc "$pkgdir"/usr/lib/pkgconfig
  sed -i -e 's|Version: |Version: '$pkgver'|g' "$pkgdir"/usr/lib/pkgconfig/libmediainfo.pc
}
