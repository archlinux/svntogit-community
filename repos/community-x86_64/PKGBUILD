# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=xdg-desktop-portal-wlr
pkgver=0.6.0
pkgrel=1
pkgdesc='xdg-desktop-portal backend for wlroots'
url="https://github.com/emersion/xdg-desktop-portal-wlr"
arch=('x86_64')
license=('MIT')
provides=('xdg-desktop-portal-impl')
depends=('xdg-desktop-portal' 'pipewire' 'pipewire-session-manager' 'libinih')
makedepends=('meson' 'wayland-protocols' 'wayland' 'scdoc')
optdepends=(
    'slurp: to choose which output to screencast using slurp'
    'wofi: to choose which output to screencast using wofi'
    'bemenu: to choose which output to screencast using bemenu'
)
source=(
    "${pkgname}-${pkgver}::https://github.com/emersion/xdg-desktop-portal-wlr/archive/v${pkgver}.tar.gz"
)
sha512sums=('e325c2de2a98ffc4612da3492138843b6c9927e536f52f7a87e794380c099f84ab8d344ee989bf88e71faaad2e5300fc31ac74058cea5040654ac74ce270ee53')

build() {
    cd "${pkgname}-${pkgver}"
    arch-meson -Dsd-bus-provider=libsystemd build
    ninja -C build
}

package() {
    cd "${pkgname}-${pkgver}"
    DESTDIR="${pkgdir}" ninja -C build install
    install -Dm644 -t "$pkgdir/usr/share/licenses/${pkgname}" LICENSE
}
