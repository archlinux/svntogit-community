# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.9.16
pkgrel=1
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/'
license=('GPL2')
depends=('fltk' 'gnutls' 'libjpeg-turbo' 'libpng' 'libxpm' 'zlib'
         'hicolor-icon-theme')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('b1086385c4c16cdabe428ab1aa041266baebee39f222cf6a3947b41027b7dae3'
            'SKIP')
validpgpkeys=('9086C3CDC66C3F563CF8F405BE67C75EC81F3244') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix='/usr' \
        --enable-ssl \
        --enable-gnutls \
        --with-gui
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
