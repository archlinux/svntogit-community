# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=42
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('gcc-libs' 'openssl')
makedepends=('systemd')  # systemd.pc
checkdepends=('gtest' 'bind')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz")
sha512sums=('7ad4ffbad056e84894dd23a18396afe6174a1137fa3edee4e12bf1487dd40d91158c89fd9e3c894603a51f639826acccdfdf1ddb8a25e77158dfe5bd54eb6f20')

prepare() {
  # Unfortunately upstream does not provide a way to reuse .o files
  cp -a smartdns-Release$pkgver smartdns-Release$pkgver-test
}

build() {
  cd smartdns-Release$pkgver
  make RUNSTATEDIR=/run SBINDIR=/usr/bin
}

check() {
  cd smartdns-Release$pkgver-test
  make -C test test
}

package() {
  cd smartdns-Release$pkgver
  make RUNSTATEDIR=/run SBINDIR=/usr/bin DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/etc/init.d
}
