# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=2.0.0
pkgrel=1
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="https://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=('sqlite' 'postgresql-libs' 'libmariadbclient')
makedepends=('cargo')
source=(https://github.com/diesel-rs/diesel/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        Cargo.lock)
sha512sums=('06fd83c781aa212922df4dd4f95145d36fec041dd1daeeef4e7770774315330b6e932e09f13db88b4b12ec06ccb4477cda766fc5f81d586ab24376b99341730a'
            '44b6fa793d2c69a62ebff11e85def41d5d0b7672f1ed6e9039b701c8da53db58205f207e6b843cba4cecd8497c05555c3718e8f38f14ed3b2365929ae4e35bb0')
b2sums=('e19dd241a014c9419e8c3cb5a75c6d8d2ffc1429304d486b252846e851a9fce349f9e9b770251408840dc4f07894d880f794266680a709aee89f9bd282d8a08f'
        'e1942e36a621a25219b411a729af1bf45af5414c5c702b69e7fa8f3fe2ff7eeed59aaa72d0e8e07c2e099daaa527680a07d67cffbf42123bee13dc253722cb83')

prepare() {
  cp Cargo.lock "diesel-${pkgver}"
  cd "diesel-${pkgver}/diesel_cli"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "diesel-${pkgver}/diesel_cli"
  RUSTFLAGS="--cap-lints allow" cargo build --frozen --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  RUSTFLAGS="--cap-lints allow" cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
