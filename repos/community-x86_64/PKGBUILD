# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-bluetooth
pkgver=2.3.3
pkgrel=1
pkgdesc='Switchboard Bluetooth Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-bluetooth
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=49217de2894a89702e11156b6c906c97acf870f8
source=(git+https://github.com/elementary/switchboard-plug-bluetooth.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-bluetooth

  git describe --tags
}

build() {
  arch-meson switchboard-plug-bluetooth build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
