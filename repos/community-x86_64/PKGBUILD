# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Konstantin Shabanov <kes.eclipse@gmail.com>
# Contributor: Evka
# Maintainer: Jose Riha <jose 1711 gmail com>

pkgname=udpxy
pkgver=1.0.23.12
_pkgver=1.0.23-12
_commit=612d2271b0d3ccb92f5e1c1c54aa3c0674af75cf
pkgrel=1
pkgdesc="small-footprint UNIX/Linux daemon to relay multicast UDP traffic to client's TCP (HTTP) connection."
arch=(x86_64)
url="https://github.com/pcherenkov/udpxy"
license=('GPL3')
makedepends=('git')
source=("git+https://github.com/pcherenkov/udpxy.git#commit=${_commit}"
	$pkgname.service)
sha256sums=('SKIP'
            '8ea0c97ccd00bff2bc3b1d987d5e55b215bc4cd966623be669160445bdd5a25a')

build() {
  cd "$srcdir"/udpxy/chipmunk
  make PREFIX=/usr CC="gcc -O2"
}

package() {
  cd "$srcdir"/udpxy/chipmunk
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -D -m644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
