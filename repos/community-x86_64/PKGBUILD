# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Daenyth <Daenyth+Arch [at] gmail [dot] com>
# Contributor: kagan <juanynie@mi.madritel.es>
# Contributor: Hans-Kristian Arntzen <maister@archlinux.us>

pkgname=mednafen
pkgver=1.22.1
pkgrel=1
pkgdesc="Command-line multi-system gaming emulator"
url="https://mednafen.github.io"
license=('GPL2')
arch=('x86_64')
depends=('libsndfile' 'sdl2' 'zlib')
makedepends=('mesa' 'glu')
source=(https://mednafen.github.io/releases/files/$pkgname-$pkgver.tar.xz)
md5sums=('c16f28032a7c575052a1707cf6d52f02')

build() {
  cd $pkgname

  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" install

  for i in ABOUT-NLS AUTHORS ChangeLog COPYING INSTALL NEWS \
           README{,.DOS,.PORTING} TODO; do
    install -Dm644 $i "$pkgdir"/usr/share/doc/$pkgname/$i
  done
}
