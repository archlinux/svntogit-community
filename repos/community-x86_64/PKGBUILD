# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=python-scikit-learn
pkgver=1.1.3
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=(x86_64)
url="https://scikit-learn.org"
license=(BSD)
depends=(python-scipy python-joblib python-numpy python-threadpoolctl)
optdepends=('python-matplotlib: plotting capabilities')
makedepends=(cython
             openmp
             python-{build,installer,wheel}
             python-setuptools)
options=(!emptydirs)
_archive=${pkgname#python-}-${pkgver}
source=(https://github.com/scikit-learn/scikit-learn/archive/${pkgver}/$_archive.tar.gz)
sha256sums=('b7c0a9fb8dfcefcfc7ef8fe02a064d194980251d57efaea972cb76005afb3c63')

prepare() {
  cd $_archive
  sed -i -E '/setuptools/s/<[0-9.]+//;/numpy/d' pyproject.toml
}

build() {
  cd $_archive
  python -m build -wn
}

package() {
  cd $_archive
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 -t "${pkgdir}"/usr/share/licenses/${pkgname}/ COPYING

  # See FS#49651
  install -d "${pkgdir}"/usr/share/doc/${pkgname}
  cp -r doc/tutorial "${pkgdir}"/usr/share/doc/${pkgname}/tutorial
}
