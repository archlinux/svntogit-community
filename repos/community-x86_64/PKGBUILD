# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=kresus
pkgver=0.15.2
pkgrel=1
pkgdesc="Self-hosted personal finance manager"
arch=(x86_64)
url="https://kresus.org"
license=(MIT)
backup=('etc/webapps/kresus/config.ini')
depends=(python nodejs-lts-erbium weboob-headless)
makedepends=(yarn npm libpng)
optdepends=('python-pdfminer: For IBAN extraction from PDF RIB')
source=(https://framagit.org/kresusapp/kresus/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        config.ini
        ${pkgname}.service
        ${pkgname}.sysusers
        ${pkgname}.tmpfiles)
sha256sums=('420ed94313e3343f47d84095bd9adade166cc1e5d83f211d39cb2dd254bb63a6'
            '63e105f26d46b9402519b314a2d43e4c3363dc990198eab68d0c816b8ce56b43'
            '3d4b07c2c029be80597e84d9fbbf67c479c6231347b83cf429af4441ec5a491e'
            'd9d30f5470c7165e4917487b69d7ab82e463da4e1355056e1035ee501d3f1adc'
            'ba8ad7d9eb5d2b47fde5f6a3ab98596e5c679141b78d76d54b44830604b67632')

build() {
    cd ${pkgname}-${pkgver}
    npm install && npm run build:prod
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    # Fix npm crazyness
    chmod -R go-w "${pkgdir}"/usr
    chown -R root:root "${pkgdir}"/usr

    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    cd ..
    install -Dm600 config.ini -t "${pkgdir}"/etc/webapps/kresus/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
