# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.41.0
pkgrel=2
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('x86_64')
url="https://guitarix.org"
license=('GPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio')
depends=('atkmm' 'bluez-libs' 'cairo' 'cairomm' 'gcc-libs' 'gdk-pixbuf2'
'glibc' 'glibmm' 'gtkmm3' 'libsigc++' 'libx11' 'pangomm' 'ttf-roboto')
makedepends=('avahi' 'curl' 'boost' 'eigen' 'faust' 'fftw' 'glib2' 'gperf'
'gtk3' 'intltool' 'jack' 'ladspa' 'liblo' 'liblrdf' 'lilv' 'lv2' 'pango'
'sassc' 'waf' 'zita-convolver' 'zita-resampler')
optdepends=('new-session-manager: for session management')
provides=('guitarix2' 'libgxw.so' 'libgxwmm.so' 'ladspa-host' 'lv2-host')
conflicts=('guitarix2')
replaces=('guitarix2')
source=("${pkgname}-${pkgver}.tar.xz::https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}2-${pkgver}.tar.xz")
sha512sums=('27fe6f2b87c8913feafae1c17472150d37df46ed3437b033cd22f509edf67420fb8f4024e404d5bec901a687dbe948acda702bf37cb0c738feb5b3b8e5ebb8aa')
b2sums=('d3caa51f297153c16df624ababaffc1405ddb10f04ae01672720bb2229d4be3426b33f75021b1862f996fb09466eaad008f748b42cb8382f08d14e23758cfc9b')

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --enable-nls \
                --ladspa \
                --new-ladspa \
                --shared-lib \
                --lib-dev \
                --ldflags="${LDFLAGS}"
  waf build -vv
}

package() {
  depends+=('libavahi-common.so' 'libavahi-gobject.so' 'libboost_iostreams.so'
  'libcurl.so' 'libfftw3f.so' 'libgdk-3.so' 'libgio-2.0.so' 'libglib-2.0.so'
  'libgobject-2.0.so' 'libjack.so' 'liblilv-0.so' 'liblo.so' 'liblrdf.so'
  'libpangocairo-1.0.so' 'libpango-1.0.so' 'libsndfile.so'
  'libzita-convolver.so' 'libzita-resampler.so')
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
