# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrew Rabert <ar@nullsum.net>

pkgname=scrcpy
pkgver=1.23
pkgrel=2
pkgdesc='Display and control your Android device'
arch=(x86_64)
url='https://github.com/Genymobile/scrcpy'
license=(Apache)
depends=(android-tools ffmpeg sdl2)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$url/releases/download/v$pkgver/$pkgname-server-v$pkgver")
b2sums=('3081c02ba43a7c1d089ea1908323591b737515c7708c64162393b6d16eb2916931218aaef8d0a1ed13d04b6ce83a074e266174919751deee8ffb5290d382a398'
        '909cd015e5acddffe93ecac1bb2f1e022cb30a21edfef02565879db6763035c759ea8d3b274a0d0d7aeec8da7d66c5eafe3c443f6e178e645a88f782882d42ca')

build() {
  mkdir -p build
  arch-meson build $pkgname-$pkgver \
    --buildtype release \
    -D b_lto=true \
    -D b_ndebug=true \
    -D prebuilt_server="../$pkgname-server-v$pkgver"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname-$pkgver/LICENSE -t \
    "$pkgdir/usr/share/licenses/$pkgname"
}
