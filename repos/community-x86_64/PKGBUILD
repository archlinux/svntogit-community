# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-anomaly-detection-plugin
pkgver=2.2.1.0
pkgrel=1
_opensearchver=2.2.1
_jdkver=11
pkgdesc="OpenSearch Anomaly Detection Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/monitoring-plugins/ad"
license=('Apache')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/anomaly-detection/archive/${pkgver}.tar.gz"
  '0001-Add-support-for-reproducible-builds.patch'
)
sha256sums=('c3def75f9ea48e040d1f74d54dc8aaa986e85d7ab9232ea1e8f1bd888cd30068'
            'f3e6649cd60834252dc97aaf913ba7003b859ee6955bfeeb38b7fd46230d600b')

prepare() {
  cd "anomaly-detection-${pkgver}"
  patch -Np1 -i "$srcdir"/0001-Add-support-for-reproducible-builds.patch
}

build() {
  cd "anomaly-detection-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-anomaly-detection"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-anomaly-detection"
  unzip "${srcdir}/anomaly-detection-${pkgver}/build/distributions/opensearch-anomaly-detection-${pkgver}.zip"

  install -Dm644 "${srcdir}/anomaly-detection-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
