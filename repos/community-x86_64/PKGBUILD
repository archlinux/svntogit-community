# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gtk-sharp-3
_pkgname=gtk-sharp
pkgver=2.99.3
pkgrel=1
pkgdesc="C# bindings for GTK+ 3"
arch=('i686' 'x86_64')
url="http://mono-project.com/GtkSharp"
license=('LGPL')
depends=('mono' 'gtk3')
source=(https://download.gnome.org/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz)
sha256sums=('6440f571416267ae0cb5698071d087b31e3084693fa2c829b1db37ca7ea2c3a2')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  sed -i 's/gmcs/mcs/' configure{,.ac}
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
