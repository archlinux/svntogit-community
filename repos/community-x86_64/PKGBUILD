# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yangtse <yangtsesu@gmail.com>
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=2.6.1
_modelver=19
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('kyotocabinet' 'glib2')
makedepends=('gnome-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz"
        "$pkgname-model$_modelver.text.tar.gz::https://downloads.sourceforge.net/libpinyin/model$_modelver.text.tar.gz")
noextract=("$pkgname-model$_modelver.text.tar.gz")
sha512sums=('b45723a68aed9d484817556d65429d5a0c11e3a3bd67027014942759093ce3fbfa463f3708411754f791ecb7572a8bcf97bed988f10fcdd948c5ddb50eb8b462'
            '93c70423ba14faa3402bce775f82769dd4ee3a49083beddc540825f88facbd847328e9d01d99473adb7cf26de0f853e2b2e14fe849b1c49531134d1ca4ed2ef2')

prepare() {
  cd $pkgname-$pkgver

  ln -s "$srcdir"/$pkgname-model$_modelver.text.tar.gz data/model$_modelver.text.tar.gz
  sed -i "/wget.*model$_modelver\.text\.tar\.gz/ d" data/Makefile.am
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --with-dbm=KyotoCabinet --enable-libzhuyin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
