# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent B. <vb@luminar.eu.org>

pkgname=opam
pkgver=1.2.2
pkgrel=5
pkgdesc='OCaml package manager'
arch=('x86_64')
url='https://opam.ocaml.org/'
license=('GPL')
depends=('aspcud' 'ocaml' 'unzip')
optdepends=('darcs: For downloading packages with darcs'
            'git: For downloading packages with git'
            'mercurial: For downloading packages with mercurial'
            'rsync: For downloading packages with rsync')
source=("https://github.com/ocaml/opam/releases/download/$pkgver/opam-full-$pkgver.tar.gz")
sha256sums=('15e617179251041f4bf3910257bbb8398db987d863dd3cfc288bdd958de58f00')

build() {
  cd "opam-full-$pkgver"

  ./configure --prefix=/usr
  make -j1 lib-ext all OCAMLC="ocamlc -unsafe-string" OCAMLOPT="ocamlopt -unsafe-string"
}

package() {
  make -C "opam-full-$pkgver" DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
