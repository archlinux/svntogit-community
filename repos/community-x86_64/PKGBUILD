# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.7
pkgrel=2
pkgdesc="A simple video player that is synchronized to jack transport."
arch=('x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL2')
depends=('ffmpeg' 'imlib2' 'liblo>=0.28' 'libltc' 'libxpm' 'libxv' 'portmidi')
makedepends=('glu')
backup=('etc/xjadeorc')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
sha512sums=('69be3da40fd09f2364a7362d950c72e34472156a249efdce69b43837ca3b1dec138c98b0742e77bc1911fe7e5a1c17d6df3fe9f1b2d5d030fa260b19320e7afd'
            '50239162b461feeeb4d9d0d7d947d176d182e279a772977fc7300877565ed6b49244033ab63606d32f6b01f2a38ceecb29ab6da537288974627111306e2dc718')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install

  # rc file
  install -Dm644 doc/xjadeorc "${pkgdir}/etc/xjadeorc"

  # desktop file
  install -Dm644 "../${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # icon
  install -Dm644 "src/${pkgname}/icons/xjadeoH128.png" \
    "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -Dm644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/README"

}
