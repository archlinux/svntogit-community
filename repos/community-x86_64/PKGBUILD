# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=system-tools-backends
pkgver=2.10.1
pkgrel=1
pkgdesc='Backends for Gnome System Tools.'
arch=('i686' 'x86_64')
url='http://system-tools-backends.freedesktop.org/'
license=('GPL')
depends=('perl>=5.12.1' 'perl-xml-twig' 'dbus>=1.2.24' 'polkit>=0.96')
makedepends=('perlxml' 'pkgconfig' 'intltool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.10/${pkgname}-${pkgver}.tar.gz)
install=system-tools-backends.install
md5sums=('cf633d333c4a3dc67414056517e58941')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --with-dbus-sys=/etc/dbus-1/system.d \
    --localstatedir=/var \
    --mandir=/usr/share \
    --disable-static

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  #clean up man
  rm -rf $pkgdir/usr/share/system-tools-backends-2.0/modules/share/
}
