# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Julius Michaelis <gitter@liftm.de>

pkgname=wasmer
pkgver=2.1.1
pkgrel=1
pkgdesc="Universal Binaries Powered by WebAssembly"
arch=('x86_64')
url="https://github.com/wasmerio/wasmer"
license=('MIT')
depends=('gcc-libs' 'zlib' 'ncurses' 'libffi')
makedepends=('rust' 'cmake' 'llvm11')
checkdepends=('lld' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wasmerio/wasmer/archive/${pkgver}.tar.gz")
sha512sums=('0bb5c3b7a9b97e5ed2f34c64f562fc904e2b43b76252f4ff7e98015d5a0b22e5abde43ea116d9bc295d420b2e13f240374dba3f8ba063aac18c44edfeeccaa0b')
options=('staticlibs' '!lto')

WASMER_INSTALL_PREFIX=/usr
ENABLE_LLVM=1

build() {
  cd "$pkgname-$pkgver"
  make all
}

check() {
  cd "$pkgname-$pkgver"
  make test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir"/usr make install
}

# vim:set ts=2 sw=2 et:
