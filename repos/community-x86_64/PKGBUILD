# Maintainer: Konstantin Gizdov <arch at kge dot pw>

pkgname=intel-oneapi-compiler-shared-runtime
pkgver=2022.1.0
_debpkgrel='3768'
pkgrel=6
pkgdesc="Intel oneAPI compiler runtime libraries"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("custom")
source=("https://apt.repos.intel.com/oneapi/pool/main/${pkgname}-${pkgver}-${pkgver}-${_debpkgrel}_amd64.deb")
b2sums=('1e014fd80d1146f8c8cc9339dd002722e2378c51d0ff83935aad64d0386ce14dbde37291f9630ca85b3eadaa83ddf7954c7e83898e0c80a31eb8b9b949fa2e1a')
depends=('intel-oneapi-openmp' 'intel-oneapi-common')

build() {
    cd "${srcdir}"

    mkdir package
    tar xvf data.tar.xz -C "${srcdir}"/package
}

package() {
    mv "${srcdir}"/package/opt "${pkgdir}"

    # allow libs to be found
    local _lib_path='/opt/intel/oneapi/compiler'
    local _ldso_conf="${pkgdir}"/etc/ld.so.conf.d
    install -d "${_ldso_conf}"
    echo "${_lib_path}/latest/linux/lib" > "${_ldso_conf}/${pkgname}.conf"
    echo "${_lib_path}/latest/linux/lib/x64" >> "${_ldso_conf}/${pkgname}.conf"
    echo "${_lib_path}/latest/linux/compiler/lib/intel64_lin" >> "${_ldso_conf}/${pkgname}.conf"

    install -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
    ln -s /usr/share/licenses/intel-oneapi "${pkgdir}"/usr/share/licenses/"${pkgname}"/oneapi
}
