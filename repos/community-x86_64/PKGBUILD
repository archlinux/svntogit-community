# $Id$
# Maintainer : David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Christoph Zeiler <arch at moonblade dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

_stopver=0.3.0
pkgname=aeolus
pkgver=0.9.5
pkgrel=4
pkgdesc="Synthesised pipe organ emulator"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/aeolus/"
license=('GPL3')
groups=('pro-audio')
depends=('clthreads' 'clxclient' 'jack' 'zita-alsa-pcmi')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2"
        "https://kokkinizita.linuxaudio.org/linuxaudio/downloads/stops-$_stopver.tar.bz2"
        "$pkgname.conf")
sha512sums=('e80e7a861934b287afefe0186bd78c2d51662c29d3b7292469244c52b4949698c512dff72bebe1f76cf98e17e3f9183d3ce7a7da4856bf936dab091d3b9f4b0b'
            'a6572a42c6796aa08b9085bfc4cb9e252091310224a0b8867eaf5d41f399b2649744c357506ae5fb64a82018f0da702bacbdc7e96965a1116f08d0a248d28ad3'
            'b04689673aec928e2594a86b595a6f6d5d70408467c99379f6282f98dd9fbc1cab42b196927193a708e26229e665a121a22edf42e8ab49bb24d68d45bde3181c')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip ldconfig and -march=native
  sed -i '/ldconfig\|-march=native/d' Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr LIBDIR=lib
}

package(){
  cd "${pkgname}-${pkgver}/source"
  make DESTDIR="${pkgdir}/" PREFIX=/usr LIBDIR=lib install

  # system config file
  install -Dm644 "${srcdir}/${pkgname}.conf" "${pkgdir}/etc/${pkgname}.conf"

  # stops
  cd "${srcdir}/stops-${_stopver}"
  find . -name definition -exec install -Dm644 {} "${pkgdir}/usr/share/${pkgname}/stops/"{} \;
  find . -name presets -exec install -Dm644 {} "${pkgdir}/usr/share/${pkgname}/stops/"{} \;
  find . -name '*.ae0' -exec install -Dm644 {} "${pkgdir}/usr/share/${pkgname}/stops" \;
}
