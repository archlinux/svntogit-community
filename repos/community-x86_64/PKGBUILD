# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.8.2
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11'
         'qt5-declarative' 'qt5-quickcontrols2' 'qt5-svg' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python' 'qbs')
optdepends=('python: Python plugin'
            'qt5-imageformats: Support for additional image formats (including WebP)')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('698add45d425d16a7b68c9b4d847cafb1d2b0633157614cbcec5f4dba23c1a65597bd2211b8c9f6b37af8ea2a4ca967fc71662672d79a8ea209dd9b0e3863413')

build() {
  cd $pkgname-$pkgver
  qbs qbs.installPrefix:"/usr" projects.Tiled.useRPaths:false projects.Tiled.installHeaders:true projects.Tiled.enableZstd:true
}

package() {
  cd $pkgname-$pkgver
  qbs install --install-root "$pkgdir"
}
