# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: sseneca <me at ssene dot ca>
# Contributor: ml <ml@visu.li>

pkgname=kubeseal
_pkgname=sealed-secrets
pkgver=0.20.5
pkgrel=1
pkgdesc="A Kubernetes controller and tool for one-way encrypted Secrets"
arch=('x86_64')
url="https://github.com/bitnami-labs/sealed-secrets"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('27d3661a6c08f756f1eb28ac24d9808a5cdb2bad86c5c49461588b1c6d2c372bdc9ce84af1284abaee9e7177483ff2847128c613f062b2c9ac302abfbfb13e98')
options=('!lto')

build() {
  cd "${_pkgname}-${pkgver}/cmd/kubeseal"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  go build -ldflags="-linkmode=external -X=main.VERSION=v${pkgver}" .
}

check() {
  cd "${_pkgname}-${pkgver}"

  go test ./cmd/kubeseal/... ./pkg/...
}

package() {
  cd "${_pkgname}-${pkgver}/cmd/kubeseal/"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
}
