# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgbase=gap
pkgname=(gap gap-data gap-doc gap-packages)
pkgver=4.8.9
_gapver=4r8p9_2017_12_18-23_44
pkgrel=2
pkgdesc="Groups, Algorithms, Programming: a system for computational discrete algebra"
arch=(x86_64)
url="https://www.gap-system.org/"
license=(GPL)
source=("https://www.gap-system.org/pub/gap/gap48/tar.gz/gap$_gapver.tar.gz" gap-no-packages-by-default.patch
        gap-polymake-3.1.patch)
sha256sums=('944230a7c0c04c981ed67768854ab293e1536fe7b76c979f24cf00526bedaba2'
            'e8f19968d998172b7c289d0499b8b8a05bc31119a1ce073181d3842ece6fe3a2'
            '6d8d57a9947575a3f95e682172040618be3d6a144424496229ae4ec50c0ab4e9')
makedepends=(libxaw openmpi givaro mpfi normaliz polymake boost c-xsc) # fplll

prepare() {
  cd gap4r8

# Use system normaliz
  sed -e '/build-normaliz.sh/d' -i bin/BuildPackages.sh
# Fix build of guava package
  sed -e "s|MAKE=make|MAKE='make -j1'|" -i bin/BuildPackages.sh
  sed -e '/cd leon make/d' -i pkg/guava-*/src/Makefile
  sed -e '/AM_INIT_AUTOMAKE/d' -i pkg/guava-*/src/leon/configure.ac
# Don't load any packages by default
  patch -p1 -i ../gap-no-packages-by-default.patch
# Fix build of PolymakeInterface with polymake 3.1
  patch -p1 -i ../gap-polymake-3.1.patch
# Fix https://bugs.archlinux.org/task/55174
  sed -e '/xgap/d' -i pkg/sonata/PackageInfo.g
# Disable fplll in float package, doesn't support 5.2
  pushd pkg/float-*
    sed -e '/FPLLL/d' -i configure.ac
    sed -e '39,44d' -i src/Makefile.am
    ./autogen.sh
  popd
# Disable anupq package, it's i686 only
  rm -r pkg/anupq-*
}

build() {
  cd gap4r8
  CC=mpicc ./configure --prefix=/usr --with-gmp=system
  make

  cd pkg
  export CXXFLAGS+=' -I/usr/include/cxsc' # Fix build of float package
  sh ../bin/BuildPackages.sh
# These packages fail to build:
# linboxing (doesn't support givaro 4)
# semigroups

  cd ../etc
  sh install-tools.sh
}

package_gap() {
  depends=(gmp openmpi)
  optdepends=('gap-packages: extra packages' 'gap-data: additional databases' 'gap-doc: documentation')
  replaces=(gap-math)
  conflicts=(gap-math)
  cd gap4r8
  
  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r bin etc grp lib CITATION "$pkgdir"/usr/lib/gap
  
  mkdir -p "$pkgdir"/usr/lib/gap/pkg
  cp -r pkg/GAPDoc-* "$pkgdir"/usr/lib/gap/pkg

  mkdir -p "$pkgdir"/usr/bin
  install -m755 "$pkgdir"/usr/lib/gap/bin/gap.sh "$pkgdir"/usr/bin/gap

# fix location
  sed -e 's|/build/gap/src/gap4r8|/usr/lib/gap|' -i "$pkgdir"/usr/bin/gap -i "$pkgdir"/usr/lib/gap/bin/*/sysinfo.gap  

# remove unneeded stuff
  rm "$pkgdir"/usr/lib/gap/bin/*/*.o
  rm "$pkgdir"/usr/lib/gap/bin/gap.sh
  find "$pkgdir"/usr/lib/gap/bin/ -maxdepth 1 -type f -exec rm {} \;
  rm -r "$pkgdir"/usr/lib/gap/bin/*/{config*,extern,gac,Makefile}
}

package_gap-data() {
  depends=(gap)
  pkgdesc="Additional databases for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r prim small trans "$pkgdir"/usr/lib/gap
}

package_gap-doc() {
  depends=(gap)
  pkgdesc="Documentation for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r doc "$pkgdir"/usr/lib/gap
}

package_gap-packages() {
  depends=(gap)
  optdepends=('normaliz: Normaliz interface package' 'polymake: Polymake interface package' 'libxaw: xgap package'
              'c-xsc: float package' 'mpfi: float package' 'libmpc: float package') # 'fplll: float package'
  pkgdesc="Extra packages for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r pkg "$pkgdir"/usr/lib/gap

# GAPDoc is in main package
  rm -r "$pkgdir"/usr/lib/gap/pkg/GAPDoc-*
}
