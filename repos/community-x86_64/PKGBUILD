# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgname=python-gmpy2
pkgver=2.1.3
pkgrel=1
pkgdesc='Provides C-coded Python modules for fast multiple-precision arithmetic'
arch=(x86_64)
url='https://github.com/aleaxit/gmpy'
license=(LGPL2.1)
depends=(python libmpc)
makedepends=(python-setuptools)
source=(https://github.com/aleaxit/gmpy/archive/refs/tags/gmpy2-$pkgver.tar.gz)
sha256sums=('211ac0dd56764900b4c209448d651ace0dbe0ddf6789e3c228382383b1b03f13')

build() {
  cd gmpy-gmpy2-$pkgver
  python setup.py build
}

package() {
  cd gmpy-gmpy2-$pkgver 
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}
