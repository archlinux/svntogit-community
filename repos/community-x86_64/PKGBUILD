# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.4.3
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base)
makedepends=(help2man meson jack python-jinja python-yaml)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('19965a42fd509cabaf03e703bad3e721fb68ba84b6c84bd96b0dee7915d28b951afc61daccfc5d517a07d5898c7ae492283813b734a675a96280cb2101681267')
b2sums=('af324c91d9891a78e707d132839aeda2440924737a46999455f0ab8ecea2713d2aa1ea636cc2d21b5d25fee9a2a3917e0c3b1a0ce7330b82e7ee62a5110235b6')

prepare() {
  cd "${pkgname}-${pkgver}"
  rm -fv build
}

build() {
  cd "${pkgname}-${pkgver}"
  arch-meson -Drtaudio=disabled build
  ninja -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build

  # licenses
  install -vDm 644 LICENSE.md LICENSES/MIT.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
