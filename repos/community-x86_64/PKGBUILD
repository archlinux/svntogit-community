# Maintainer: David Runge <dvzrv@archlinux.org>

_name=myMPD
pkgname=mympd
pkgver=10.2.2
pkgrel=1
pkgdesc="A standalone and lightweight web-based MPD client"
arch=(x86_64)
url="https://github.com/jcorporation/myMPD"
license=(GPL3)
depends=(glibc lua)
makedepends=(cmake flac jq libid3tag openssl pcre2 perl)
source=($pkgname-$pkgver.tar.gz::https://github.com/jcorporation/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('d227d21c6db2d551eb84ab767166ec7f35daf50074df3b9fda187816bf5739e9ca332e4fd882ecfa55b5335153bbf2fcfd5117fb10e8f547fb9d955ac1e9dfba')
b2sums=('cadefcaa6a71d5b83ee45baaee5ba75de28925c1c8c0201588295e588009dc3a17e7b78cf5ec93d4d9d7f0a50ec3d180ec1ab3b3dad8bc9b3a0300eaf59842a2')

build() {
  local cmake_options=(
    -B build
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DENABLE_FLAC=ON
    -S $_name-$pkgver
    -Wno-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    flac libFLAC.so
    libid3tag libid3tag.so
    openssl libssl.so
    pcre2 libpcre2-8.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $_name-$pkgver/{CHANGELOG,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
