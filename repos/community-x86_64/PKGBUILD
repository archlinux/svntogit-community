# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Tinxy <arch at tinx dot eu>

pkgname=hedgewars
pkgver=0.9.22
pkgrel=7
pkgdesc="Turn-based strategy artillery game similiar to Worms"
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL' 'custom')
depends=('qt4' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'lua51' 'ffmpeg' 'glut' 'physfs')
makedepends=('fpc' 'cmake'
	     # haskell specific
	     'ghc'
	     'haskell-network'
	     'haskell-bytestring-show'
	     'haskell-utf8-string'
	     'haskell-dataenc'
	     'haskell-hslogger'
	     'haskell-vector'
	     'haskell-entropy'
	     'haskell-sha'
	     'haskell-zlib'
	     'haskell-random'
	     # for scaling the icon
	     'imagemagick')
source=("http://download.gna.org/hedgewars/${pkgname}-src-${pkgver}.tar.bz2" hedgewars-ffmpeg3.patch)
sha256sums=('b699c8971ff420c3edd6533527ae2e99040f1e79207c9140826945bcf0e62192'
            '8c77091440f0e9c6ca365e160f56b2195a573dca206ee29d762ff6a35af84604')

prepare() {
  cd ${pkgname}-src-${pkgver}
  sed -i 's|instance NFData (Chan a)$|instance NFData (Chan a) where rnf x = seq x ()|' gameServer/CoreTypes.hs

# Fix build with ffmpeg 3.0 (Debian)
  patch -p1 -i ../hedgewars-ffmpeg3.patch
}

build() {
  cd ${pkgname}-src-${pkgver}
  mkdir -p ${srcdir}/bin
  ln -sf /usr/bin/lrelease-qt4 ${srcdir}/bin/Qt4::lrelease
  export PATH=${srcdir}/bin:${PATH}
  cmake \
	-DCMAKE_BUILD_TYPE="Release" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DDATA_INSTALL_DIR=/usr/share/hedgewars \
	-DNOSERVER=0 \
	.
  make

  # resize icon
  for _size in 16 32 48 64 128 256; do
    convert misc/hedgewars.png -resize ${_size}x${_size} hedgewars_${_size}.png
  done
}

package() {
  cd ${pkgname}-src-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D -m644 Fonts_LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/Fonts_LICENSE.txt

  # install icons
  install -D -m644 misc/hedgewars.png ${pkgdir}/usr/share/icons/hicolor/512x512/apps/hedgewars.png
  for _size in 16 32 48 64 128 256; do
    install -D -m644 hedgewars_${_size}.png ${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/hedgewars.png
  done
  rm -rf ${pkgdir}/usr/share/pixmaps

  install -D -m644 share/hedgewars/Data/misc/hedgewars.desktop ${pkgdir}/usr/share/applications/hedgewars.desktop
}
