# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>

pkgname=fortune-mod
pkgver=2.10.0
pkgrel=1
pkgdesc="The Fortune Cookie Program from BSD games"
arch=('x86_64')
url="https://www.shlomifish.org/open-source/projects/fortune-mod/"
license=('BSD')
depends=('recode')
makedepends=('cmake' 'git')
source=("https://github.com/shlomif/fortune-mod/archive/fortune-mod-$pkgver.tar.gz")
md5sums=('aec74b13c4129e69a272dcae9e7dcf8c')

# todo: handle https://github.com/shlomif/rinutils/ and remove git dep
# or at least make a deterministic build

prepare() {
  cd "$srcdir/fortune-mod-fortune-mod-$pkgver/fortune-mod"
  # probably a better way to do this but it is baked in hard
  sed -i 's|/games/|/|' CMakeLists.txt
  sed -i 's|"sbin"|"bin"|' CMakeLists.txt
  sed -i 's|"games"|"bin"|' CMakeLists.txt
  sed -i 's|/games/fortunes|/fortune|' datfiles/{,off/}CMakeLists.txt
}

build() {
  cd "$srcdir/fortune-mod-fortune-mod-$pkgver/fortune-mod"
  mkdir -p build
  cd build
  cmake  .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DNO_OFFENSIVE=OFF \
        -DLOCALDIR=/usr/share/fortune/ \
        -DLOCALODIR=/usr/share/fortune/off/ \
        -DCOOKIEDIR=/usr/share/fortune/ \
        -DOCOOKIEDIR=/usr/share/fortune/off/
  make REGEXDEFS='-DHAVE_REGEX_H -DPOSIX_REGEX -DHAVE_STDBOOL'
}

package() {
  cd "$srcdir/fortune-mod-fortune-mod-$pkgver/fortune-mod/build"
  make DESTDIR="$pkgdir" \
       FORTDIR="$pkgdir/usr/bin" \
       COOKIEDIR="$pkgdir/usr/share/fortune" \
       BINDIR="$pkgdir/usr/bin" \
       BINMANDIR="$pkgdir/usr/share/man/man1" \
       FORTMANDIR="$pkgdir/usr/share/man/man6" install
  rm "$pkgdir/usr/share/fortune/"{,off/}*.u8
  install -Dm644 ../../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
