# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ian Yang <doit.ian@gmail.com>

pkgname=google-glog
pkgver=0.5.0
pkgrel=2
pkgdesc="Logging library for C++"
arch=('x86_64')
license=('custom:BSD3')
url='https://github.com/google/glog'
depends=('gflags')
makedepends=('cmake')
source=("glog-$pkgver.tar.gz::https://github.com/google/glog/archive/v$pkgver.tar.gz"
        https://github.com/google/glog/commit/0b3d4cb4.patch)
sha512sums=('445e4338f3d81cd0b065f2da9c6ce343c243263ca144cea424ef97531a4e9e09c06ffd6942ac01c5213a8003c75cfbbede3c4028d12f0134f23ff29314769c1a'
            '89fecff7e0c23b8ade1ce6f2292790b0d01545e086ad89c4acad632a7d9937ca0ed572319b7b810082b75bcb329774076ed5471730bb24b9f1ec360dc903eaa9')

prepare() {
  patch -d glog-$pkgver -p1 < 0b3d4cb4.patch # Fix ABI break
}

build() {
  cmake -B build -S glog-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # Lazy way of dealing with conflicting man and info pages...
#  rm -rf "$pkgdir"/usr/share

  install -Dm644 glog-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
