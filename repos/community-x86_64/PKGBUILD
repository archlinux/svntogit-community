# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.30.0
pkgrel=3
pkgdesc="Small applications for the GNOME Panel"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GnomeApplets"
license=('GPL')
depends=('dbus-glib' 'gnome-panel' 'libgtop' 'tracker' 'cpupower' 'upower')
makedepends=('autoconf-archive' 'git' 'gnome-settings-daemon' 'intltool' 'python' 'yelp-tools')
_commit=ac51b5ea297cf9239d718b4776e92ce4afc82470  # tags/3.30.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-applets.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Use correct D-Bus config location
  sed -i '/^dbus_confdir =/ s/sysconfdir/datadir/' cpufreq/src/cpufreq-selector/Makefile.am

  # Disable buggy applets
  # https://gitlab.gnome.org/GNOME/gnome-applets/issues/6
  sed -i '/window-buttons/d
          /window-title/d' Makefile.am

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --enable-compile-warnings=minimum
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
