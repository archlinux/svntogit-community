# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gnome-mplayer
pkgver=1.0.9
pkgrel=3
pkgdesc='A simple MPlayer GUI.'
arch=('i686' 'x86_64')
url='http://gnome-mplayer.googlecode.com/'
license=('GPL')
depends=('mplayer' 'dbus-glib' 'libnotify' 'hicolor-icon-theme' 'dconf' 'gmtk')
makedepends=('nautilus' 'nemo' 'caja' 'gconf')
source=("https://${pkgname}.googlecode.com/svn/packages/${pkgname}-${pkgver}.tar.gz"
        "caja.patch")
md5sums=('26c3ab335b6fcdda220b05c977f5b0f1'
         '60e40a9a3de78f82a99582bc70aa9f91')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/caja.patch"
  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed -i 's@Exec=gnome-mplayer %U@Exec=gnome-mplayer %F@' \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
