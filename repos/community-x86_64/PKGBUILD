# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Mintsuki <mintsuki@protonmail.com>

pkgname=limine
pkgver=4.20230408.0
pkgrel=2
pkgdesc="An advanced, portable, multiprotocol bootloader"
arch=('x86_64')
url="https://limine-bootloader.org/"
license=('BSD')
source=("https://github.com/limine-bootloader/limine/releases/download/v${pkgver}/limine-${pkgver}.tar.xz")
b2sums=('c4a5d0a93082b4f9569b2af6d8b345ca7b0e00aeff42af112e2f07541b091f54451af7be969fad3d28c6108cf2106ce7d5d463f0237090bfd5b3548e50ca935f')
makedepends=('nasm' 'mtools' 'llvm' 'lld' 'clang')

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-all TOOLCHAIN_FOR_TARGET=llvm
	make
}

package() {
	cd "${pkgname}-${pkgver}"
  	make DESTDIR="${pkgdir}" install
  	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
