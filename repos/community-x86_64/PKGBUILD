# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Davorin Učakar <davorin.ucakar@gmail.com>

pkgname=libsquish
pkgver=1.15
pkgrel=4
pkgdesc='DXT compression library'
arch=(x86_64)
url='https://sourceforge.net/projects/libsquish'
license=(MIT)
makedepends=(cmake ninja openmp)
source=(libsquish.pc
        "https://downloads.sourceforge.net/project/libsquish/libsquish-$pkgver.tgz") # exploding tarball
b2sums=('39f9db4957e2a1e5875c3bfc438acabad8b76cd19e5b9a79f3101972165650577e7fd2a7c03df2ca629d4f332fccb6c84025e335fd8d0cf794f30d627e63937d'
        'd2cdf274baf9cf8890ee4c5c434448a34bc6d3d8967df6e2e9334fe1eff66ce5371597396c564c80a128709a8849f1f622d90aaf470eacc1ad67811cef38bd60')

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D BUILD_SHARED_LIBS=ON \
    -G Ninja \
    -S .
  ninja -C build
}

package() {
  (cd build; cmake -D CMAKE_INSTALL_PREFIX="$pkgdir/usr" -P cmake_install.cmake)
  install -Dm644 CMakeModules/FindlibSquish.cmake "$pkgdir/usr/share/cmake/Modules/FindlibSquish.cmake"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 libsquish.pc "$pkgdir/usr/lib/pkgconfig/libsquish.pc"
}
