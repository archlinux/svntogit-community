# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastnumbers
pkgname=python-fastnumbers
pkgver=4.0.0
pkgrel=1
pkgdesc="Super-fast and clean conversions to numbers"
arch=(x86_64)
url="https://github.com/SethMMorton/fastnumbers"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-hypothesis python-pytest python-typing_extensions)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('16f4e488c420585358fb39c90c412cf2fb41f2291367dd5dc39e39c69385ed9a3627dbcb8e4ee81ed002b4cf809438b80eb3b98f90c27a8a6fee886dca569fad')
b2sums=('184a8fbf2e685ed84c7e6adafbba87564f0934962b1d13e8af71e8b1bdd56e1ba0f9d543d208a459ac94b6c98cda4ebf2450dec2cdc9275438e089e540770a3e')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
