# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

_name=FreeRDP
pkgname=freerdp
pkgver=2.8.1
pkgrel=2
epoch=2
pkgdesc="Free implementation of the Remote Desktop Protocol (RDP)"
arch=(x86_64)
url="https://www.freerdp.com/"
license=(Apache)
depends=(dbus-glib glibc gstreamer gst-plugins-base-libs libcups libgssglue
libx11 libxcursor libxext libxdamage libxfixes libxkbcommon libxi libxinerama
libxkbfile libxrandr libxrender libxtst openssl pcsclite wayland)
makedepends=(alsa-lib cmake docbook-xsl ffmpeg icu krb5 libjpeg-turbo libpulse
libusb pam systemd-libs xmlto xorgproto)
provides=(libfreerdp2.so libfreerdp-client2.so libfreerdp-server2
libfreerdp-shadow2.so libfreerdp-shadow-subsystem2.so libwinpr2.so
libwinpr-tools2.so libuwac0.so)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz
  $pkgname-2.0.0-manpage_formatting.patch
)
sha512sums=('8b7cd77d21216abdfa64c8ee8ddaf9cfcec1a4f90b099de2bf829d7e9ad702827a07807ee85fab4eb50e0ecfd035e0fc8c41ddbf1479e795cbb2a7f3f7d54c44'
            'd960e042d1527b5d5721136b6b20fc36f65beafd010581ea5b908668537fe9fe622de6689a29c0274b0d6f1e513615f0d02e56c1d1d1e613d093e145d39af8d7')
b2sums=('885de2861d305fa854c3a3c67d5d3cfab820848e3e5713d8076ff3f086531eb599e8812e5b8ec4e2359f76212f269340a358a5d4c52bb88a9674a589bb1273f0'
        '8242fe56ba9cf5352d836f656645a46142c1aecea3e8200203117a0cccc226135591c16fa0a24cfa196ade7b703832ff6871a948fd3bbbcc1d9c6df9885dc360')

prepare() {
  # fix man page formatting:
  # https://bugs.archlinux.org/task/64814
  patch -d $_name-$pkgver -p1 -i ../$pkgname-2.0.0-manpage_formatting.patch
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=None \
        -DPROXY_PLUGINDIR=/usr/lib/freerdp2/server/proxy/plugins \
        -DWITH_DSP_FFMPEG=ON \
        -DWITH_FFMPEG=ON \
        -DWITH_PULSE=ON \
        -DWITH_CUPS=ON \
        -DWITH_PCSC=ON \
        -DWITH_ICU=ON \
        -DWITH_JPEG=ON \
        -DWITH_SERVER=ON \
        -DWITH_SWSCALE=ON \
        -DWITH_CHANNELS=ON \
        -DWITH_CLIENT_CHANNELS=ON \
        -DWITH_SERVER_CHANNELS=ON \
        -DCHANNEL_URBDRC_CLIENT=ON \
        -Wno-dev \
        -B build \
        -S $_name-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so libavcodec.so libavutil.so libicuuc.so libjpeg.so
  libpam.so libpulse.so libswscale.so libswresample.so libsystemd.so
  libusb-1.0.so)

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $_name-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
