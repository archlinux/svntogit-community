# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: John Gerritse <john.gerritse@gmail.com>

pkgname=atop
pkgver=2.1_1
pkgrel=3
pkgdesc="A system and process level monitor."
arch=(i686 x86_64)
url="http://www.atoptool.nl/"
license=('GPL')
depends=('ncurses' 'sh' 'zlib' 'cron')
source=(http://www.atoptool.nl/download/atop-${pkgver/_/-}.tar.gz)
md5sums=('d956f5b0c7e0705cff6cf44898d664d7')

prepare() {
  cd $srcdir/$pkgname-*
  sed -i 's#root ##' atop.cron
}

build() {
  cd $srcdir/$pkgname-*
  make
}

package() {
  cd $srcdir/$pkgname-*
  make DESTDIR=$pkgdir INIPATH=/etc/rc.d SBINPATH=/usr/bin install
  rm -f $pkgdir/etc/cron.d/atop
  install -dm0755 $pkgdir/etc/cron.daily
  mv $pkgdir/etc/atop/atop.daily $pkgdir/etc/cron.daily/atop
  install -Dm0644 atop.service $pkgdir/usr/lib/systemd/system/atop.service
  rm -rf $pkgdir/etc/rc.d
}
