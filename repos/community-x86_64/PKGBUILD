# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Gordin <gordin@koeln.ccc.de>

pkgname=wit
pkgver=3.01a
_revision=7464
pkgrel=1
pkgdesc='Wiimms ISO Tools'
arch=('x86_64')
url='http://wit.wiimm.de'
license=('GPL2')
depends=('fuse2' 'zlib')
makedepends=('subversion')
optdepends=('bash: Update titles lists'
            'wget: Update titles lists')
install='wit.install'
source=("wit::svn+http://opensvn.wiimm.de/wii/trunk/wiimms-iso-tools#revision=${_revision}"
        'wit-titles.patch')
sha256sums=('SKIP'
            'ace3f1245430e9db8962ecb0d4f459ab73bb43f2d5d536a6fac82ad9b34186fa')

prepare() {
  cd wit

  patch -Np1 -i ../wit-titles.patch
}

build() {
  cd wit

  export LDFLAGS="$LDFLAGS,-z,noexecstack"

  make INSTALL_PATH="${pkgdir}/usr" tools
  make doc
}

package () {
  cd wit

  make install
  install -dm 755 "${pkgdir}"/usr/share/doc/wit
  install -m 644 doc/*.txt "${pkgdir}"/usr/share/doc/wit/
}

# vim: ts=2 sw=2 et:
