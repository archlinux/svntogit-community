# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=0.13.2
pkgrel=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(GPL3)
depends=(
  cairo
  enchant
  gcr
  gdk-pixbuf2
  glib2
  gmime
  gnome-keyring
  gnome-online-accounts
  gtk3
  iso-codes
  libcanberra
  libgee
  libnotify
  libsecret
  libsoup
  libxml2
  pango
  sqlite
  webkit2gtk
)
makedepends=(
  git
  gnome-doc-utils
  gobject-introspection
  intltool
  itstool
  meson
  vala
)
source=(git+https://gitlab.gnome.org/GNOME/geary.git#tag=geary-${pkgver})
sha256sums=('SKIP')

build() {
  arch-meson geary build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
