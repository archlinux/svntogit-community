# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: cyrant <cyrant at tuta dot io>

pkgname=opentoonz
pkgver=1.6.0
pkgrel=4
pkgdesc='Software for producing a 2D animation'
arch=('x86_64')
url='https://opentoonz.github.io/e/'
license=('BSD')
depends=('cblas' 'ffmpeg' 'freeglut' 'glew' 'libmypaint' 'opencv' 'qt5-multimedia' 'qt5-script'
         'qt5-serialport' 'qt5-svg' 'superlu')
makedepends=('boost' 'cmake' 'ninja' 'qt5-tools')
source=("https://github.com/opentoonz/$pkgname/archive/v${pkgver}/$pkgname-$pkgver.tar.gz"
         https://github.com/opentoonz/opentoonz/commit/1e6244f3.patch)
sha256sums=('9a6c28cb3933756dbebb45e849bdafc9e6d93d0328c4fd4d7bc8ec3ae481f684'
            '37bc0b828c2aa3aa6353f815413b6412f25a32a74dc2af5fb68d992c35428345')

prepare() {
  cd $pkgname-$pkgver

  # Specify path for ffmpeg
  sed -i 's|"ffmpegPath", QMetaType::QString, ""|"ffmpegPath", QMetaType::QString, "/usr/bin"|' \
    toonz/sources/toonzlib/preferences.cpp

  patch -p1 -i ../1e6244f3.patch # Fix build with GCC 13
}

build() {
  pushd $pkgname-$pkgver/thirdparty/tiff-4.0.3
  ./configure --with-pic --disable-jbig
  make
  popd

  cmake -S $pkgname-$pkgver/toonz/sources -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_SKIP_RPATH=YES \
    -DWITH_TRANSLATION=OFF
  # WITH_TRANSLATION doesn't do anything, generated translations aren't installed
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
