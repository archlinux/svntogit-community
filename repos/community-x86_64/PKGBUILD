# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lillian rose winter <hi@neko.vg>
# Contributor: Evan McCarthy <evan@mccarthy.mn>

pkgname=libretls
pkgver=3.7.0
pkgrel=1
pkgdesc="libtls for OpenSSL"
arch=(x86_64)
url="https://git.causal.agency/${pkgname}"
license=(ISC BSD MIT)
depends=(openssl)
source=(https://causal.agency/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('9aa5d3a9133932c362075259b0b17bb0c89741fa1b2535136df2ded7a0c13392')
b2sums=('291e4da4038b1baf88694c9e38e31b8d656d8ecb2ab397e7ce6efe4cfd55d69ccf03cfbe84fd7858d7d64c2ebb00dd43102dfd2ff9e6a3f3df95924930d2e099')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  sed -n '1,/*\//p' tls.c | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
