# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=ogre
pkgver=1.7.1
pkgrel=2
pkgdesc="A scene-oriented, flexible 3D engine written in C++"
arch=('i686' 'x86_64')
url='http://www.ogre3d.org'
license=('MIT')
depends=('boost-libs' 'freeimage' 'freetype2' 'libxaw' 'libxrandr' 
         'nvidia-cg-toolkit' 'mesa' 'zziplib' 'ois')
makedepends=('boost' 'cmake' 'doxygen' 'dot2tex')
optdepends=('cppunit: unit testing'
            'intel-tbb: better threading support'
            'poco: portability')
#conflicts=('ogre')
#provides=('ogre')
install=ogre.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_src_v${pkgver//./-}.tar.bz2")
md5sums=('85083083dc65d49572654ea8c8b5c552')

build() {
  cd ${srcdir}/${pkgname}_src_v${pkgver//./-}
  [[ -d build ]] && rm -rf build
	mkdir build
	cd build

  cmake .. \
 	 -DCMAKE_INSTALL_PREFIX=/usr \
	 -DOGRE_INSTALL_PLUGINS_HEADERS=TRUE \
	 -DOGRE_INSTALL_SAMPLES=TRUE \
	 -DOGRE_INSTALL_DOCS=TRUE \
	 -DOGRE_INSTALL_MEDIA=TRUE \
	 -DOGRE_INSTALL_SAMPLES_SOURCE=TRUE

  make || return 1

  # Check for optdepends and then attemt to generate docs
  if [[ $(which dot) && $(which doxygen) ]]; then
    make doc 2>/dev/null || return 1
  fi
  make DESTDIR=${pkgdir} install || return 1

  # Fix up samples
  install -dm777 ${pkgdir}/opt/OGRE/samples/
  mv ${pkgdir}/usr/share/OGRE/*.cfg ${pkgdir}/opt/OGRE/samples/
  mv ${pkgdir}/usr/bin/SampleBrowser ${pkgdir}/opt/OGRE/samples/

  echo "#!/bin/bash" > ${pkgdir}/usr/bin/OgreSampleBrowser
  echo "cd /opt/OGRE/samples && ./SampleBrowser" >> ${pkgdir}/usr/bin/OgreSampleBrowser
  chmod +x ${pkgdir}/usr/bin/OgreSampleBrowser
}
# vim:set ts=2 sw=2 et:
