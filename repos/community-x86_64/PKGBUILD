# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox
_pkgname=qTox
pkgver=1.16.2
pkgrel=2
_gitver='c1521d7b981ac59777ed2269ec560881602b6742'
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines'
arch=('x86_64')
url='https://github.com/tux3/qTox'
license=('GPL3')
depends=('desktop-file-utils'
         'libxss'
         'openal'
         'gtk2'
         'ffmpeg'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'libexif'
         'toxcore'
         'libgl')
makedepends=('qt5-tools' 'git' 'check' 'cmake' 'mesa' 'libglvnd')

source=( 
        "https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz"
        "https://github.com/$_pkgname/$_pkgname/releases/download/v$pkgver/v$pkgver.tar.lz.asc"
        )
sha512sums=('d21028e2404bf3df030c9f4dc54bce9c442ee3559ca4d5fe157b6b67cbb4b777f000d3c4c03b7ad4f3ffe89c175d62e6b60fcfa0055fd8ae2d8ea1099c5383ce'
            'SKIP')

## GPG key fingerprints taken from https://github.com/qTox/qTox/blob/$_gitver/README.md
validpgpkeys=('DA262CC93C0E1E525AD21C8596775D454B8EBF44'  # sudden6 <sudden6@gmx.at>
              'BA7883E22F9D35945BA3376053137C3033F09008'  # Zetok Zalbavar <zetok@openmailbox.org>
	      '1157616BBD860C539926F8139591A163FF9BE04C'  # - antis81
	      '31039166FA902CA50D05D6085AF9F2E29107C727'  # - Diadlo
	      'C7A2552D0B250F983827742C133203A3AC399151'  # - initramfs
	      '2880C860D95C909D3DA45C687E086DD661263264'  # - tux3
          '74ADB7A7F8ECC1009916C4E42AA8E0DA1B31FB3C'  # - AnthonyBilinski subkey
          '7EB339FE881747E701B7D472EBE36E66A8429B99'  # - AnthonyBilinski
          'CA9221C5389B7C50AA5F779352A50775BE13DF17'  # - noavarice
          '141C880E8BA25B198D0F850F7C132143C1A3A7D4'  # – tox-user
             )

prepare() {
  # disable -Werror for release packaging
  sed -e 's|-Werror||' -i CMakeLists.txt
}

check() {
  cd build
  make test
}

build() {
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
	  -DGIT_VERSION=$_gitver \
	  -DGIT_DESCRIBE=v$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
