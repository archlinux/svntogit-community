# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc-crossref
pkgver=0.3.8.2
pkgrel=57
pkgdesc="Pandoc filter for cross-references"
url="https://hackage.haskell.org/package/${pkgname}"
license=("GPL2")
arch=('x86_64')
depends=('ghc-libs' 'haskell-data-default' 'haskell-data-accessor' 'haskell-data-accessor-template'
         'haskell-data-accessor-transformers' 'haskell-gitrev' 'haskell-open-browser'
         'haskell-optparse-applicative' 'pandoc' 'haskell-pandoc-types' 'haskell-roman-numerals'
         'haskell-syb' 'haskell-utility-ht')
makedepends=('ghc' 'uusi' 'haskell-hspec')
conflicts=('haskell-pandoc-crossref')
replaces=('haskell-pandoc-crossref')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lierdakil/pandoc-crossref/archive/v$pkgver.tar.gz")
sha512sums=('9ffb9a727aa1000b5e3e1b3d14941015a411463db30e339c43ce75bc701cb7e3bf9510179d0369ec13693fb6962d5a5a936ef8c21536a42a1a9abf62d10f6405')

prepare() {
    uusi $pkgname-$pkgver/$pkgname.cabal
}

build() {
    cd $pkgname-$pkgver
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
	    -f-enable_flaky_tests
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test || echo "Tests failed: https://github.com/lierdakil/pandoc-crossref/issues/287"
}

package() {
    cd $pkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
