# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=ejabberd
pkgver=2.1.9
pkgrel=1
pkgdesc="An erlang jabber server"
arch=(i686 x86_64)
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang' 'pam')
makedepends=()
backup=(etc/ejabberd/ejabberd.cfg \
	etc/logrotate.d/ejabberd)
install=ejabberd.install
source=(http://www.process-one.net/downloads/ejabberd/${pkgver/_/-}/ejabberd-${pkgver}.tar.gz \
	ejabberd \
	ejabberd.logrotate)
md5sums=('29ca14ea62f90736322e4e6720331352'
         '8b15d22edef4e9fdd310aa617435aaf2'
         'f97c8a96160f30e0aecc9526c12e6606')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}/src

  sed -i "s|EJABBERDDIR = \$(DESTDIR)@prefix@/var/lib/ejabberd|EJABBERDDIR = \$(DESTDIR)@prefix@/usr/lib/ejabberd|g" ./Makefile.in
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-pam --enable-odbc
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}/src
  make DESTDIR=${pkgdir} install

  install -D -m 0755 ${srcdir}/ejabberd ${pkgdir}/etc/rc.d/ejabberd
  install -d ${pkgdir}/var/spool/ejabberd
  install -d ${pkgdir}/var/lib/ejabberd

  install -D -m0644 ${srcdir}/ejabberd.logrotate ${pkgdir}/etc/logrotate.d/ejabberd
  chmod ug+r ${pkgdir}/etc/ejabberd/*
  rm -rf $pkgdir/var/lock
}
