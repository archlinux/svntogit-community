# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher Reimer <mail+aur@c-reimer.de>

pkgname=cargo
pkgver=0.13.0
pkgrel=1
pkgdesc='Rust package manager'
url='http://crates.io/'
arch=('x86_64' 'i686')
license=('APACHE' 'MIT' 'custom')
depends=('curl' 'rust')
makedepends=('git' 'python' 'cmake')
groups=('rust')
options=('!emptydirs')
source=("git+https://github.com/rust-lang/cargo.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  git submodule update --init --recursive
  sed 's^share/doc^share/licenses^g' -i Makefile.in
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr --enable-optimize
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # Remove files that contains references to $srcdir or $pkgdir,
  # or that conflicts with the rust package.
  rm -f "$pkgdir/usr/lib/rustlib/"{install.log,manifest-cargo,uninstall.sh}

  install -d "$pkgdir/usr/share/bash-completion/completions"
  mv "$pkgdir/usr/etc/bash_completion.d/cargo" \
    "$pkgdir/usr/share/bash-completion/completions/cargo"
}

# vim:set ts=2 sw=2 et:
