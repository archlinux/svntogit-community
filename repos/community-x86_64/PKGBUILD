# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=2.5.0
pkgrel=1
pkgdesc='A background rust code checker'
arch=('x86_64')
url='https://dystroy.org/bacon'
license=('AGPL3')
depends=('gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
_commit='a611a6bda67dd9acf95ee0f4e1fc646d65a5876c'
source=(
  "$pkgname::git+https://github.com/Canop/bacon.git#commit=$_commit"
  'Cargo.lock.patch'
)
b2sums=('SKIP'
        '0e265d376c0126aabcd3f5aa210e899206827339aac0b45602aad9b1ddd1c5c26497eaf9a17de486a93910fce961e9df483270e39e081327cb12c0199c169304')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/Cargo.lock.patch"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
