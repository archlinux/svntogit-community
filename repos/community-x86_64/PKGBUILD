# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=http2
pkgname=haskell-http2
pkgver=2.0.5
pkgrel=57
pkgdesc="HTTP/2 library"
url="https://github.com/kazu-yamamoto/http2"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-case-insensitive' 'haskell-http-types' 'haskell-network'
         'haskell-network-byte-order' 'haskell-psqueues' 'haskell-time-manager')
makedepends=('ghc' 'haskell-aeson' 'haskell-aeson-pretty' 'haskell-base16-bytestring'
             'haskell-doctest' 'haskell-glob' 'haskell-hspec' 'haskell-unordered-containers'
             'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        http2-base16-bytestring-1.0.patch::https://github.com/kazu-yamamoto/http2/commit/e8f218ee42e575bb124613fdbd6b9aec8398cdf9.patch)
sha512sums=('94a45aa97090db4a12eb64a0a1e8596049182d8011b6a1edb22c4841e7164fbd6c118770a638605f999296e71e796ec660c1701e7bc25d4c4a8d4c801b3b773e'
            'f0a2b0ffe081760a5da874dca4fa9bff359f9e95f833f48d4195285c11069eca5aba453d7cb8b342557c00458ae18157c6e29160189473bb2671e53c2cd6bc37')

prepare() {
    patch -d $_hkgname-$pkgver -p1 < http2-base16-bytestring-1.0.patch || :
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-devel
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
