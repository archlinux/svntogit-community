# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.7.0
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=('x86_64')
url="https://www.laborejo.org/fluajho/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('fluidsynth' 'glib2' 'jack' 'libsndfile' 'util-linux-libs' 'ncurses')
optdepends=('new-session-manager: Session management')
provides=('soundfont-synthesizer')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('e660c95a1f6c56162cf5adbd7c26f9a7ba4f264c0bd9ae4810b9501e9308c3d5226b66f78f25439ac32a8c1a7e5e97bddce1eaf71357eab5bf8ca07f757da98f'
            'SKIP')
b2sums=('7d7aec68cdc30081acdecbf21d7642d1cb951494420ea57317eef48ecfb293b9243380e85598a950e878e84e3417b8852a8715c8b73fed0c5c50038a8e845d74'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfluidsynth.so' 'libglib-2.0.so' 'libjack.so' 'libncursesw.so'
  'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
