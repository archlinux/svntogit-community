# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-scikit-learn
pkgver=0.22.2.post1
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=('x86_64')
url="http://scikit-learn.sourceforge.net/"
license=('BSD')
depends=('python-scipy' 'python-joblib')
optdepends=('python-matplotlib: plotting capabilities')
makedepends=('python-setuptools' 'cython' 'openmp')
options=(!emptydirs)
source=("scikit-learn-${pkgver}.tar.gz::https://github.com/scikit-learn/scikit-learn/archive/${pkgver}.tar.gz")
sha256sums=('c6c57326941b1854112e6730961f31995ea60fa254e79daae1994e4cd5cb6f44')

build() {
  cd "$srcdir"/scikit-learn-$pkgver
  python setup.py build
}

package() {
  cd "$srcdir"/scikit-learn-$pkgver

  python setup.py install --root="$pkgdir"/ --optimize=1

  install -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # See FS#49651
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r doc/tutorial "$pkgdir"/usr/share/doc/$pkgname/tutorial
}
