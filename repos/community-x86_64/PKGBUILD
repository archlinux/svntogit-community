# $Id$
# Maintainer: Kyle Keen <keenerd at gmail dot com>
pkgbase=python-pyzmq
pkgname=(python-pyzmq python2-pyzmq)
pkgver=17.0.0
pkgrel=1
pkgdesc="Python bindings for zeromq, written in Cython"
arch=('x86_64')
url="http://www.zeromq.org/bindings:python"
license=('LGPL')
depends=('zeromq' 'python')
makedepends=('python' 'python2')
checkdepends=('python-nose' 'python2-nose' 'python-tornado' 'python2-tornado'
    'python-pytest' 'python2-pytest')
#source=("https://github.com/zeromq/pyzmq/downloads/pyzmq-$pkgver.tar.gz")
#source=("https://pypi.python.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/p/pyzmq/pyzmq-$pkgver.tar.gz")
md5sums=('fa06e1c62c8ef47fe8b49ce8a41e60e2')

prepare() {
  cd "$srcdir"
  cp -a pyzmq-${pkgver} py2zmq-${pkgver}

  cd "$srcdir/py2zmq-$pkgver"
  sed -i 's|^#!/usr/bin/env python$|&2|' $(find ./ -name '*.py')
}

build() {
  cd "$srcdir"
}

package_python-pyzmq() {
  pkgdesc="Python3 bindings for zeromq, written in Cython"
  cd "$srcdir/pyzmq-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=0
}

package_python2-pyzmq() {
  pkgdesc="Python2 bindings for zeromq, written in Cython"
  depends=('zeromq' 'python2')
  cd "$srcdir/py2zmq-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
}

check() {
  cd "$srcdir/pyzmq-$pkgver"
  python3 setup.py build_ext --inplace
  python3 setup.py test
  cd "$srcdir/py2zmq-$pkgver"
  python2 setup.py build_ext --inplace
  python2 setup.py test
}

