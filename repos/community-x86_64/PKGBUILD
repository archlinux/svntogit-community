# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.6.1
pkgrel=5
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('x86_64')
license=('GPL')
url='https://github.com/definite/ibus-chewing'
depends=('ibus' 'libchewing' 'libxtst' 'gtk2')
makedepends=('swig' 'gob2' 'cmake-fedora')
source=("https://github.com/definite/ibus-chewing/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b093db79c656433614b9f72719a2b0768a30c2fb6fa066eda1e79cc9932ce1836f7f1e5611612fb44f260fe80a9baa9e18fdc9754b285e8a5f12a87c376cbd39')

build() {
  cd $pkgname-$pkgver

  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DLIBEXEC_DIR=/usr/lib/ibus

  # XXX: updated *.po files contain no translated strings. Maybe because fedora.zanata.org is down
  # https://github.com/definite/ibus-chewing/issues/150
  touch build/po/ibus-chewing.pot
  cp -v po/*.po build/po/

  make -C build
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
