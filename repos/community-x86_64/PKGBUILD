# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgexiv2
pkgver=0.10.0
pkgrel=1
pkgdesc='GObject-based wrapper around the Exiv2 library'
url='https://wiki.gnome.org/Projects/gexiv2'
arch=('x86_64' 'i686')
license=('GPL2')
depends=('exiv2' 'glib2')
makedepends=('python-gobject' 'python2-gobject')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname/lib/}/${pkgver%.0}/${pkgname/lib/}-$pkgver.tar.xz")
sha256sums=('9c8c97b36a0d4ca6656488b88908e4c94efb0dfc1b0e3e0850e38b90a9a30031')

build() {
  cd "${pkgname/lib/}-$pkgver"

  # In order for the build process to include any python-related files,
  # 'import gi; print(gi._overridesdir)' must work with either python or
  # python2. The gi module is provided by python-gobject and python2-gobject.
  ./configure --prefix=/usr --enable-introspection
  make
}

package() {
  make -C "${pkgname/lib/}-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
