# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=bv-sized
pkgname=haskell-bv-sized
pkgver=1.0.1
pkgrel=4
pkgdesc="A bitvector datatype that is parameterized by the vector width"
url="https://github.com/GaloisInc/bv-sized"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bitwise' 'haskell-panic' 'haskell-parameterized-utils'
         'haskell-th-lift')
makedepends=('ghc' 'haskell-hedgehog' 'haskell-tasty' 'haskell-tasty-hedgehog')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('c9e8f793f90aaf0ea4816c6b70fe84d47e2a2bb29ff91b5ed4dd2927d5bce5ae9f53a7b76ada86314ab99ecf9561f05f54afa7d5997632848a0ddacbeee9e10c')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *1.3/<2/' $_hkgname.cabal
    echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
