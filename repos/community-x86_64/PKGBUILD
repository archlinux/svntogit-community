# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.99
pkgrel=3
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('glibc')
optdepends=('lzo: for libddr_lzo')
arch=('x86_64' 'i686')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.bz2"
        'dd_rescue-1.99-disable-openssl.patch')
sha512sums=('b33f6118fc996252ceafa90d43ff01cd5b54dd03eec9c852d006f15192cbffa99e289ea81c5c10ac91cfa2a26ab2f9e32a70e73d6a5183f9e2d973a75de45b85'
            'fff318506e57a6737f523ecebd90751a35e908ca71fb29985132c9c2e6b476f341c241a2abb8a86b86bf24d03e185f15d2bc89f3c3d834cbb34aecb8e5e85d55')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi

  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile

  # Disable OpenSSL support
  patch -p1 -i ../dd_rescue-1.99-disable-openssl.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}
