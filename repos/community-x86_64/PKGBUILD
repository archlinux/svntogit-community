# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=qbittorrent
pkgname=(qbittorrent qbittorrent-nox)
pkgver=4.3.5
pkgrel=1
arch=('x86_64')
url="https://www.qbittorrent.org"
license=('custom' 'GPL')
depends=('libtorrent-rasterbar' 'qt5-base')
makedepends=('boost' 'qt5-tools' 'qt5-svg')
optdepends=('python: needed for torrent search tab')
source=("https://downloads.sourceforge.net/sourceforge/qbittorrent/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('0a2feec6c16ef9a82c9a6a6ffacd619ede243ed4037ae04e7b9f3da74f704cd9d724f47e87d2da00eb8d0c0c3c9228e0578a6bc71fa530ef42cb5b65586f9db0'
            'SKIP')
b2sums=('eadc46330bfc04778510123f143578ea12ec9a34d3f3e56ef47267d7fe4deaea92318e4213f75b8849b6cc047d05e0daa62a27c1c1d4f00acbc535589c26ca63'
        'SKIP')
validpgpkeys=('D8F3DA77AAC6741053599C136E4A2D025B7CC9A2') # sledgehammer999 <sledgehammer999@qbittorrent.org>

build() {
  cd ${pkgbase}-${pkgver}

  # tell qmake not to break makepkg's debug/!strip options
  export QBT_ADD_CONFIG='nostrip'

  mkdir -p ${pkgbase}
  pushd ${pkgbase}
  ../configure --prefix=/usr
  make
  popd

  # Build nox variant
  mkdir -p "${pkgbase}-nox"
  pushd "${pkgbase}-nox"
  ../configure --prefix=/usr --disable-gui --enable-systemd
  make
}

package_qbittorrent() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends+=('qt5-svg' 'hicolor-icon-theme')

  cd ${pkgbase}-${pkgver}/${pkgbase}

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/${pkgbase}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

package_qbittorrent-nox() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui"

  cd ${pkgbase}-${pkgver}/${pkgbase}-nox

  make INSTALL_ROOT="${pkgdir}" install
  install -Dm644 "${srcdir}"/${pkgbase}-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
