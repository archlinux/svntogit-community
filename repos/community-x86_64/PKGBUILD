# $Id$$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffms2
pkgver=2.20
pkgrel=1
pkgdesc='A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access'
arch=('i686' 'x86_64')
url='https://github.com/FFMS/ffms2'
license=('GPL')
depends=('ffmpeg')
makedepends=('git')
provides=('vapoursynth-plugin-ffms2')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('c7d880172756c253f2c5673044dabf03c19890dcfe64da5104ee9f42a1a573a0')

build() {
  cd ${pkgname}-${pkgver}

  ./autogen.sh --prefix='/usr' --enable-shared --disable-static --enable-avresample
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/lib/vapoursynth
  ln -s ../libffms2.so "${pkgdir}"/usr/lib/vapoursynth/
}

# vim: ts=2 sw=2 et:
