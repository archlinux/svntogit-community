# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mihai Militaru <mihai militaru at xmpp dot ro>
# Contributor: carstene1ns <arch carsten-teibes.de>

pkgname=mbedtls
pkgver=1.3.10
pkgrel=2
pkgdesc="Portable cryptographic and SSL/TLS library, aka polarssl"
arch=('i686' 'x86_64')
url="https://www.polarssl.org/"
license=('Apache')
provides=('polarssl')
replaces=('polarssl')
conflicts=('polarssl')
source=("https://polarssl.org/download/$pkgname-$pkgver-gpl.tgz"
        "programs.makefile.patch")
sha1sums=('a3a94c7fd70ed173543a6024961407336a03b838'
          '1e9e7d3dcdd6932b02d6dcabdf45041a3726f1be')
depends=('glibc')
options=('staticlibs')

# http://sources.gentoo.org/cgi-bin/viewvc.cgi/gentoo-x86/net-libs/polarssl/files/polarssl-1.3.8-ssl_pthread_server.patch
# https://github.com/alucryd/aur-alucryd/blob/master/personal/polarssl/PKGBUILD
# https://aur.archlinux.org/packages/po/polarssl-git/PKGBUILD

build() {
  cd "$pkgname-$pkgver"
  sed -i 's|//\(#define POLARSSL_THREADING_C\)|\1|' include/polarssl/config.h
  sed -i 's|//\(#define POLARSSL_THREADING_PTHREAD\)|\1|' include/polarssl/config.h
  # enable cert_write
  patch -p1 -d programs -i "$srcdir/programs.makefile.patch"
  LDFLAGS+=" -I../include " make SHARED=1 no_test
}

check() {
  cd "$pkgname-$pkgver"
  make SHARED=1 check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/usr" install
  rm "$pkgdir/usr/lib/libmbedtls.so.8"
  ln -s /usr/lib/libmbedtls.so "$pkgdir/usr/lib/libmbedtls.so.8"
}
