# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-menus
pkgver=1.22.1
pkgrel=1
pkgdesc="MATE menu specifications"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=('glib2')
makedepends=('gobject-introspection' 'intltool' 'python2')
optdepends=('python2: python2 bindings')
groups=('mate')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('800d26bdba7762660ec8513cf5bcc58ff173fa1ae96621b21d17e6e0ccf1064e')

build() {
    	cd "${pkgname}-${pkgver}"
    	PYTHON=/usr/bin/python2 ./configure \
        	--prefix=/usr \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--enable-python

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
