# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastian Lau <archlinux _at_ slau _dot_ info>
# Contributor: Eric Ozwigh <ozwigh at gmail dot com>

# Nice unique path name in an archive. we are also using the zip archive because
# it seems like its too fucking hard to create a proper tarball with gzip /o\
_incompetencepath=VeraCrypt-master-8220ae94c64722fbe39e92dfcfce84f8da96a91e
pkgname=veracrypt
_pkgname=VeraCrypt
pkgver=1.19
pkgrel=2
pkgdesc='Disk encryption with strong security based on TrueCrypt'
url='http://veracrypt.codeplex.com/'
arch=('i686' 'x86_64')
license=('custom:TrueCrypt')
depends=('fuse2>=2.8.0' 'wxgtk2>=3.0' 'libsm' 'device-mapper')
optdepends=('sudo: mounting encrypted volumes as nonroot users'
            'desktop-file-utils: update desktop database')
makedepends=('nasm' 'pkg-config' 'unzip')
source=("${pkgname}_${pkgver}_Source.zip::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${_pkgname}_${pkgver}_Source.zip/download"
        "${pkgname}_${pkgver}_Source.zip.sig::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${_pkgname}_${pkgver}_Source.zip.sig/download"
        no-makeself.patch
        no-exec-stack.patch
        veracrypt.desktop)
sha512sums=('66d838fbc685e2790e50a2583445525436422e1bd6ddbf8bafbc868a31571d30476a7f3b0f1fade249d0e5dcd60db4fb043cb25cca5a45de4805665961cac516'
            'ea9b76b7fc5f4d2716b13ea73f3d1d94697a97f76deb105880bb4172c23f345ebd45b0ed47fcbad10150d58e2aa63870719caf24134d54b12f06486524996149'
            'e3972fd239fd83807c5ea9dd9268a0e962b0bd9cb9cf8f6251ac51a44a94f3c57f0b1e6857b42e4296cff531030c8278c543b386ebf463d4e19fe9a536acbcf9'
            '94f1b0bd00a84a8294c8efa72058dba71acbe5c846c9761874faa36ba9c3f1ac3d8a7d3b7452c62e2baf770efa4eecf2d989fc6d2181b3b3e11808e5cd6eb726'
            'f689ca64bac7042030de7714aed8cc89f2c5f87b407444b1b121491c1d89c147babaaa454ddc2a93b70ae20d4da59f96ad64f01b04bea9017d658c377faeb75d')
validpgpkeys=('993B7D7E8E413809828F0F29EB559C7C54DDD393') # VeraCrypt Team <veracrypt@idrix.fr>

prepare() {
  cd ${_incompetencepath}/src
  patch -p1 < "${srcdir}/no-makeself.patch"
  patch -p0 < "${srcdir}/no-exec-stack.patch"
}

build() {
  cd ${_incompetencepath}/src
  make PKG_CONFIG_PATH=/usr/lib/pkgconfig \
    WX_CONFIG=/usr/bin/wx-config \
    TC_EXTRA_LFLAGS+="-ldl ${LDFLAGS}" \
    TC_EXTRA_CXXFLAGS="${CXXFLAGS}" \
    TC_EXTRA_CFLAGS="${CFLAGS}"
}

package() {
  cd ${_incompetencepath}/src
  install -Dm 755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/veracrypt.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 Resources/Icons/VeraCrypt-256x256.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -Dm 644 License.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
