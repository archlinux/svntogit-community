# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Sid Karunaratne

pkgname=tor
pkgver=0.4.4.5
pkgrel=4
pkgdesc='Anonymizing overlay network.'
arch=('x86_64')
url='https://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'bash' 'libseccomp' 'zstd' 'libcap' 'systemd-libs')
optdepends=('torsocks: for torify')
makedepends=('ca-certificates' 'systemd')
backup=('etc/tor/torrc')
source=("https://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'torrc.patch'
        'torservice.patch'
        'tor.sysusers'
        'tor.tmpfiles')
b2sums=('b1c7342d5f1998b372529a8da1719a4f31c4e2516f9b666755b0edf29c7d66fa84a730fceed11e5c0bd1346f6fe06d7c96dd6a2161b0b2c3824468cd2f88f077'
        'SKIP'
        'ac2f3008108e736de3dd37895ad543d5c8cdcfa5ec3e09c93eed227d00a30a31636e3ef1db4cf8649a3c666e7844ea692ecd632a0ddba672efa3cfc1a64c529d'
        '941e1b3ffdb9035219f484888c942db9c295b23feff571de0af8d3fe8836e1a4b0e606b6a3583aa581f2cd6e4f975aca0b883ef6ee7f2d5b50a4ef677fb5db52'
        '9053da53926f2120ac57b6c1442238f5bbd89bf9270347c4e00b721b39939bebc6adfcf814a9d7289dfd14d085d91c193529305336db93190da5b7f586a031df'
        '5d55d9a7e42b6ce78b8ab985bab37afe8f0bacddb5abd895c4a490adb8f98b9422f90b40066fef05ecf37b7b21e80aadc615c4b7f6e12b05581304113a1b1f1d')
validpgpkeys=('2133BC600AB133E1D826D173FE43009C4607B1FB'  # Nick Mathewson
              'F65CE37F04BA5B360AE6EE17C218525819F78451') # Roger Dingledine

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  # install arch custom files
  install -Dm0644 tor.sysusers "${pkgdir}"/usr/lib/sysusers.d/tor.conf
  install -Dm0644 tor.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/tor.conf
  
  cd ${pkgname}-${pkgver}
  # patch service file to fix FS#68157
  patch -Np1 < "${srcdir}/torservice.patch"
  # uncomment essential config sections in the torrc file
  patch -Np1 < "${srcdir}/torrc.patch"

  # invoke upstream install routine
  make DESTDIR="${pkgdir}" install
  install -Dm0644 contrib/dist/tor.service "${pkgdir}"/usr/lib/systemd/system/tor.service

  # install torrc
  install -Dm0644 src/config/torrc.sample "${pkgdir}"/etc/tor/torrc
  rm "${pkgdir}"/etc/tor/torrc.sample

  install -Dm0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
