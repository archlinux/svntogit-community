# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=statistics
pkgname=haskell-statistics
pkgver=0.14.0.2
pkgrel=71
pkgdesc="A library of statistical types, data, and functions"
url="https://github.com/bos/statistics"
license=("custom:BSD2")
arch=('x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-base-orphans" "haskell-erf" "haskell-math-functions"
         "haskell-monad-par" "haskell-mwc-random" "haskell-primitive" "haskell-vector"
         "haskell-vector-algorithms" "haskell-vector-binary-instances" "haskell-vector-th-unbox")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8ee0d6ae6f7a27b4b5a6dddd1e2c9a9b086224cfb7157efc396052263c5c89aea5eeb625a312e0c08f78cee3d819cdaa139f5a7bfc4413847592db45b1b697e4')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<.*0.7/<1/' $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
