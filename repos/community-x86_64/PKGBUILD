# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.3.0
pkgrel=1
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('procps-ng' 'hwdata' 'quazip-qt5' 'kauth' 'qt5-charts' 'qt5-quickcontrols2' 
         'qt5-base>=5.9' 'botan>=2.2.0' 'qt5-svg' 'qt5-xmlpatterns' 'qt5-base' 'dbus' 'polkit')
makedepends=('cmake>=3.3' 'qt5-tools' 'quazip-qt5' 'gcc>=8' 'extra-cmake-modules' 'ninja')
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha512sums=('50b2d6b0cf446da7d74d988b0a1e9328bcac016e2436e9458341524e1beea3e3aaa1683030ffe1947e0048e3dc5f396c6e0da98be7e4f866957699600d9086f3')

build() {
  cd "${pkgname}-v${pkgver}"
  cmake \
	-Bbuild \
	-GNinja \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-Wno-dev
  cmake --build build --verbose
}

check () {
  cd "${pkgname}-v${pkgver}"
  ninja -C build test
}

package() {
  cd "${pkgname}-v${pkgver}"
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
