# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.10.0
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11'
         'qt5-declarative' 'qt5-quickcontrols2' 'qt5-svg' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python' 'qbs')
optdepends=('python: Python plugin'
            'qt5-imageformats: Support for additional image formats (including WebP)')
source=($pkgname-$pkgver.tar.gz::https://github.com/mapeditor/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('595a3f4c9563a2ed323e786613e6c1eee6589830b3e4e4c0e061fcbeb84656b18f1aba94c3d933a2e54e4889a645b0fdd3ad595cd7fce58ec4bbd4814862731e')

build() {
  cd $pkgname-$pkgver
  qbs qbs.installPrefix:"/usr" projects.Tiled.useRPaths:false projects.Tiled.installHeaders:true
}

package() {
  cd $pkgname-$pkgver
  qbs install --install-root "$pkgdir"
}
