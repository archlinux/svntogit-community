# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Dwayne Bent <dbb.0@liqd.org>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>

pkgname=prosody
pkgver=0.8.2
pkgrel=4
pkgdesc="Lightweight and extensible Jabber/XMPP server written in Lua."
arch=('i686' 'x86_64')
url="http://prosody.im/"
license=('MIT')
depends=('lua>=5.1' 'luasocket' 'luaexpat' 'luafilesystem' 'libidn>=0.5.18' 'openssl')
optdepends=('luasec: TLS encryption support'
	    'lua-zlib: compression support'
	    'luadbi: SQL storage support')
install=prosody.install
backup=('etc/logrotate.d/prosody'
	'etc/prosody/prosody.cfg.lua')
source=("http://prosody.im/depot/${pkgver}/prosody-${pkgver}.tar.gz"
	'prosody.rcd'
	'prosody.logrotated'
	'fix-config.patch'
	'prosody.tmpfile.d'
	'prosody.service')
md5sums=('6e907bf0d0acf24f1011083020ba6ffb'
         'd2b5f9c8e42bb31819e715eb1756ee53'
         '26466fdbea87963a3ca6f48f76fe4a29'
         '59a1bf2dfd0bd386cef6fa646e2a3752'
         'e2f5a1df410b05696a30dcb058841084'
         'e74045f27cb60908d535969906781f75')

build() {
    cd "$srcdir/prosody-$pkgver"
    patch -p1 <$srcdir/fix-config.patch
    ./configure --prefix=/usr --sysconfdir=/etc/prosody \
        --datadir=/var/lib/prosody
    make
}

package() {
    cd "$srcdir/prosody-$pkgver"

    make DESTDIR="$pkgdir" install

    rm $pkgdir/etc/prosody/certs/*

    install -d "$pkgdir/etc/rc.d"
    install -d "$pkgdir/etc/logrotate.d"
    install -d "$pkgdir/var/log/prosody"

    install -o root -g root -m 755 "$srcdir/prosody.rcd" \
        "$pkgdir/etc/rc.d/prosody"
    install -o root -g root -m 644 "$srcdir/prosody.logrotated" \
        "$pkgdir/etc/logrotate.d/prosody"

    install -Dm0644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
    install -Dm0644 $srcdir/prosody.tmpfile.d $pkgdir//usr/lib/tmpfiles.d/prosody.conf
    install -Dm0644 $srcdir/prosody.service $pkgdir/usr/lib/systemd/system/prosody.service
}
