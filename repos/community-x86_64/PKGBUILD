# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: David Keogh <davekeogh@archlinux.us>

pkgname=pycharm-community-edition
pkgver=2022.1.4
_build=221.6008.17
pkgrel=2
pkgdesc='Python IDE for Professional Developers'
arch=(x86_64)
url=https://www.jetbrains.com/pycharm/
license=(APACHE)
depends=(
  giflib
  glib2
  java-environment=11
  java11-openjfx
  python
  sh
  ttf-font
  fontconfig
  libdbusmenu-glib
)
optdepends=(
  'ipython: IPython integration for Python 3'
)
makedepends=(
  git
  java-environment=11
  python-setuptools
)
source=(
  "git+https://github.com/JetBrains/intellij-community.git#tag=pycharm/${_build}"
  idea-android::"git://git.jetbrains.org/idea/android.git#tag=pycharm/${_build}"
  pycharm.desktop
  pycharm.sh
)
sha256sums=('SKIP'
            'SKIP'
            'f727119ec7c8b96820e80712b1ee342de6a94ff72402e8f2d390cbff702f9b21'
            '7b5360b4977e934931acab775d04915417a5da9f3ea1dd44355525b0016cf65d')

prepare() {
  cd intellij-community

  echo ${_build} > build.txt
  mv "${srcdir}"/idea-android android

  sed '/String targetOS/c   String targetOS = OS_LINUX' -i platform/build-scripts/groovy/org/jetbrains/intellij/build/BuildOptions.groovy
  sed 's|../build/plugins-autoupload.txt|plugins-autoupload.txt|' -i platform/build-scripts/groovy/org/jetbrains/intellij/build/impl/DistributionJARsBuilder.groovy
  touch plugins-autoupload.txt
  # Fix pycharm python/installer.cmd & python/plugin.cmd
  sed 's|intellij.pycharm.community.build|"$(cd "$(dirname "$0")"; pwd)/../" intellij.pycharm.community.build|' -i python/installers.cmd
  #sed 's|intellij.pycharm.community.build|"$(cd "$(dirname "$0")"; pwd)/../" intellij.pycharm.community.build|' -i python/plugin.cmd
}

build() {
  cd intellij-community

  unset _JAVA_OPTIONS
  export JAVA_HOME=/usr/lib/jvm/java-11-openjdk
  export PATH="${JAVA_HOME}/bin:${PATH}"
  export JDK_16_x64=/usr/lib/jvm/java-11-openjdk
  export JDK_18_x64=/usr/lib/jvm/java-11-openjdk

  ./python/installers.cmd -Dintellij.build.target.os=linux
  #./python/plugin.cmd -Dintellij.build.target.os=linux

  tar -xf out/pycharm-ce/artifacts/pycharmPC-${_build}-no-jbr.tar.gz -C "${srcdir}"

  cd ../pycharm-community-${pkgver}

  python3 plugins/python-ce/helpers/pydev/setup_cython.py build_ext --build-temp build --build-lib .
}

package() {
  cd pycharm-community-${pkgver}

  # workaround FS#40934
  sed -i 's/lcd/on/' bin/*.vmoptions

  install -dm 755 "${pkgdir}"/usr/share/{licenses,pixmaps,pycharm}
  cp -dr --no-preserve='ownership' bin lib plugins "${pkgdir}"/usr/share/pycharm/
  cp -dr --no-preserve='ownership' license "${pkgdir}"/usr/share/licenses/pycharm/
  ln -s /usr/share/pycharm/bin/pycharm.png "${pkgdir}"/usr/share/pixmaps/
  install -Dm 644 ../pycharm.desktop -t "${pkgdir}"/usr/share/applications/
  install -Dm 755 ../pycharm.sh "${pkgdir}"/usr/bin/pycharm
}

# vim: ts=2 sw=2 et:
