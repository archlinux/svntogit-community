# Maintainer: christian rebischke <chris.rebischke@archlinux.org>

pkgname=git-credential-gopass
pkgver=1.14.3
pkgrel=1
pkgdesc="git-credentials helper for gopass"
arch=('x86_64')
url="https://github.com/gopasspw/git-credential-gopass"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/git-credential-gopass/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('c344ea5655f44be17292254223792c12bc668bcbd0750c1d63a1347b3af8ee154c64862e9b56d427694841327c752f761abea4da62ec68d2d9f25bdeeefefcfa')
options=('!lto')

prepare(){
  cd ${pkgname}-${pkgver}
  sed -i 's|-gcflags="-trimpath=$(GOPATH)" -asmflags="-trimpath=$(GOPATH)"||' Makefile
  sed -i 's/install-completion//g' Makefile
}

build(){
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
