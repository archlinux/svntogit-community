# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Rich Li <rich@dranek.com>
# Contributor: Sebastien Binet <binet@lblbox>

_pkg=h5py
pkgbase=python-$_pkg
pkgname=(python-$_pkg python2-$_pkg)
pkgver=2.7.1
pkgrel=1
pkgdesc="General-purpose Python bindings for the HDF5 library"
url="http://www.h5py.org/"
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('hdf5' 'cython' 'cython2' 'python-numpy' 'python2-numpy' 'python-six' 'python2-six'
             'python-pkgconfig' 'python2-pkgconfig')
source=("https://pypi.io/packages/source/h/$_pkg/$_pkg-$pkgver.tar.gz"{,.asc})
sha512sums=('b702e3875c2767d2fabe66601820c517671fb5fc3321334aba081950b625435a1262554f39187cd1b41c6069e2b3826db6f0b610bfed2abe5db8a4dd8a29ecfd'
            'SKIP')
validpgpkeys=('AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7') # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>

prepare() {
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" $_pkg-$pkgver/setup_build.py

  cp -a $_pkg-$pkgver{,-py2}
}

build() {
  cd $_pkg-$pkgver
  python setup.py build

  cd ../$_pkg-$pkgver-py2
  python2 setup.py build
}

check() {
  cd $_pkg-$pkgver
  python setup.py test

  cd ../$_pkg-$pkgver-py2
  python2 setup.py test
}

package_python-h5py() {
  depends=('hdf5' 'python-numpy' 'python-six' 'cython')
  conflicts=('hdf5-openmpi')

  cd $_pkg-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -D licenses/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-h5py() {
  depends=('hdf5' 'python2-numpy' 'python2-six' 'cython2')
  conflicts=('hdf5-openmpi')

  cd $_pkg-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -D licenses/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
