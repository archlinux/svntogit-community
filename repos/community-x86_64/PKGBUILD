# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=schroot
pkgver=1.6.5
pkgrel=2
pkgdesc="Allows users to execute shell commands under different root filesystems. (Successor to dchroot)."
url="http://packages.qa.debian.org/s/schroot.html"
license=('GPL3')
depends=('pam' 'lockdev' 'boost' 'e2fsprogs')
optdepends=('btrfs-progs-unstable' 'lvm2')
arch=('i686' 'x86_64')
conflicts=('dchroot')
replaces=('dchroot')
provides=('schroot' 'sbuild' 'dchroot')
backup=('etc/schroot/schroot.conf'
	'etc/schroot/arch32/config'
	'etc/schroot/arch32/copyfiles'
	'etc/schroot/arch32/mount'
	'etc/schroot/arch32/nssdatabases')
options=(!libtool)
source=("http://ftp.debian.org/debian/pool/main/s/$pkgname/${pkgname}_${pkgver}.orig.tar.xz"
	'arch32-example'
	'arch32-config'
	'arch32-copyfiles'
	'arch32-mount'
	'arch32-nssdatabases'
	'pam.d.schroot.patch'
	'colon-completion.patch'
	'fix-bash-completion.patch')
md5sums=('99f679d68f180a56289b3ec40d214ae4'
         '54239847f89b9a4772910415bde6276a'
         '5a3f7b839c7e0b8933748da7c5b6385b'
         'ddb2f09c02b24dab777110f9808472e1'
         'f0d5d5b5e34a860f6f90b5628c680f46'
         'af1da6edd8c8c0dafeeb2c2c4e0c840b'
         'a8d77cac806a0a9adef3f93cdbeb280a'
         'f0d03bf6c60ed119781b27b98234299c'
         '5ba775d4f401e2c167414caa548b71e5')

build() {
	cd ${pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/fix-bash-completion.patch
	patch -p1 -i ${srcdir}/colon-completion.patch
	export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-dchroot \
		--enable-lvm-snapshot \
		--enable-btrfs-snapshot \
		--with-bash-completion-dir=/usr/share/bash-completion/completions/ \
		BTRFS=/sbin/btrfs \
		BTRFSCTL=/sbin/btrfsctl \
		LVCREATE=/sbin/lvcreate \
		LVREMOVE=/sbin/lvremove
		# --enable-csbuild
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR=$pkgdir install
	install -dm 755 ${pkgdir}/etc/schroot/arch32
	install -m 644 ${srcdir}/arch32-{config,mount,copyfiles,nssdatabases} ${pkgdir}/etc/schroot/arch32
	rename 'arch32-' '' ${pkgdir}/etc/schroot/arch32/*
	install -m 644 ${srcdir}/arch32-example ${pkgdir}/etc/schroot/chroot.d/arch32.conf.example
	patch -i ${srcdir}/pam.d.schroot.patch ${pkgdir}/etc/pam.d/schroot
}
