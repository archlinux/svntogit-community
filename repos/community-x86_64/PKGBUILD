# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=dbus
pkgname=haskell-dbus
pkgver=1.2.7
pkgrel=104
pkgdesc="A client library for the D-Bus IPC system"
url="https://john-millikin.com/software/haskell-dbus/"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-conduit' 'haskell-exceptions' 'haskell-lens'
         'haskell-network' 'haskell-random' 'haskell-split' 'haskell-th-lift'
         'haskell-vector' 'haskell-xml-conduit' 'haskell-xml-types')
makedepends=('ghc' 'haskell-extra' 'haskell-quickcheck' 'haskell-resourcet' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        xml-conduit-1.9.patch)
sha512sums=('07ab78ae93b0249f7f2da0af59b5f958daa1b140287eab21962eb8ed53bc608dbd0d906d91a6a7e1bc8a9a82a2ad8e294b540ab0a6c3e01337fbb151a2211ca3'
            '0c29f5b6519b0122273ce1bf64b8af06c226063a11af375d6b0d8529d6da20f784c919cae1d31afe8bb31e32544fd4829d621bb2e146b604793f5fb14a78e99f')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../xml-conduit-1.9.patch || :
    sed -i -e 's/< *4/<5/' -e 's/< *2/<3/' -e 's/< *1/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
