# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ecasound
pkgver=2.9.2
pkgrel=4
pkgdesc="Command-line multitrack audio processor"
arch=('x86_64')
url="https://ecasound.seul.org/ecasound/"
license=('GPL' 'LGPL')
groups=('pro-audio')
depends=('alsa-lib' 'glibc' 'gcc-libs' 'libjack.so' 'libaudiofile.so'
'liblo.so' 'liblilv-0.so' 'libreadline.so' 'libsndfile' 'libncursesw.so')
makedepends=('lv2' 'ruby')
optdepends=('ruby: ECI API'
            'mpg123: MP3 decoding'
            'lame: MP3 encoding'
            'vorbis-tools: Ogg Vorbis decoding/encoding'
            'faad2: AAC decoding'
            'faac: AAC encoding'
            'timidity++: MIDI file input'
            'libmikmod: tracker module')
source=("https://ecasound.seul.org/download/$pkgname-$pkgver.tar.gz")
options=('staticlibs')
sha512sums=('fca6b37b5f669ba8616e48afe24fbfd53da3cb4987d2d6a2d6167843de34371a4c2cdceb6faf677462d6efb4069dd1d27ffafc1ceb19de56f4a051f077ada07b')

prepare() {
  cd "$pkgname-$pkgver"

  # install ruby integration to vendor_ruby
  sed -e 's/sitedir/vendordir/' -i configure.ac

  # lv2core is deprecated in favor of lv2:
  # https://sourceforge.net/p/ecasound/bugs/52/
  sed -e 's/lv2core/lv2/g' -i configure.ac
  autoreconf -vfi
}
build() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="/usr/bin/python2"
  ./configure --prefix=/usr \
              --disable-pyecasound \
              --disable-python
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # docs
  install -vDm 644 {AUTHORS,BUGS,NEWS,README,RELNOTES,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # ecamonitor is python2 only and not supported until upstream ports it
  rm -v "${pkgdir}/usr/bin/ecamonitor"
}

# vim:set ts=2 sw=2 et:
