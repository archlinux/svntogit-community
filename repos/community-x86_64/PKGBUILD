# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=kiwix-desktop
pkgver=2.3.0
pkgrel=1
pkgdesc="Offline reader for Web content, especially intended to make Wikipedia available offline"
url="https://github.com/kiwix/kiwix-desktop"
arch=("x86_64")
license=("GPL3")
depends=("qt5-base" "qt5-webengine" "qt5-svg" "qt5-imageformats" "libkiwix")
makedepends=("qt5-tools")
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('feb177a24ae65ccc462a61019174f23dd41434a7c32633a24690b98ee47f28b1')

build() {
	cd "${pkgname}-${pkgver}"
	export CFLAGS+=" ${CPPFLAGS}"
	export CXXFLAGS+=" ${CPPFLAGS}"
	qmake PREFIX="/usr"
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	make INSTALL_ROOT="$pkgdir/" install
}
