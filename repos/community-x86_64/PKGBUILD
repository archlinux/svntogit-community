# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-security-privacy
pkgver=2.3.0
pkgrel=1
pkgdesc='Switchboard Security & Privacy Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-security-privacy
license=(GPL3)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
  light-locker
  polkit
  ufw
  zeitgeist
)
makedepends=(
  git
  meson
  vala
)
_tag=6171f5c9a7e23724244121831de0a9779a5049fc
source=(git+https://github.com/elementary/switchboard-plug-security-privacy.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd switchboard-plug-security-privacy

  git cherry-pick -n c8e422e630bbee0badcf4df26364c9e83e06bad0
}

pkgver() {
  cd switchboard-plug-security-privacy

  git describe --tags
}

build() {
  arch-meson switchboard-plug-security-privacy build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
