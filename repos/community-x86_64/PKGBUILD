# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-parallel-n64
pkgver=4910
pkgrel=2
pkgdesc='Nintendo 64 core'
arch=('x86_64')
url='https://github.com/libretro/parallel-n64'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc' 'libgl')
makedepends=('git')
_commit='8c7466029074c9a465fd47dcea812fed7e4d49d3'
source=("libretro-parallel-n64::git+https://github.com/libretro/parallel-n64.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd libretro-parallel-n64

  git rev-list --count HEAD
}

build() {
  cd libretro-parallel-n64

  make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-parallel-n64

  install -Dm 644 parallel_n64_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
