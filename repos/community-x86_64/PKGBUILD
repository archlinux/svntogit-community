# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Samuel Littley <aur@toastwaffle.com>
# Contributor: rnons <remotenonsense at gmail dot com>

_pkg=ruamel.yaml.clib
pkgname=python-$_pkg
pkgver=0.2.1
pkgrel=1
pkgdesc="C version of reader, parser and emitter for ruamel.yaml derived from libyaml"
arch=('x86_64')
url="https://sourceforge.net/projects/ruamel-yaml-clib/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(https://files.pythonhosted.org/packages/source/r/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha512sums=('d2728759dd435318e984b138d379f6644b0dbd956871df0731cf75ae69b9b20f8b1d52dc41ae528a5c3bd06dc6acb9fe6618f5e622b09ea1748bf4da695d0db3')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkg}-${pkgver}
  # https://bitbucket.org/ruamel/yaml/issues/114/setuppy-requirement-for-pip-prevents
  RUAMEL_NO_PIP_INSTALL_CHECK=1 python setup.py install --skip-build --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
