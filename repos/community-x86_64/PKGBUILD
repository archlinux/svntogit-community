# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=42.0
pkgrel=1
pkgdesc='An enhanced gettext po file editor for the GNOME desktop environment'
arch=('x86_64')
url='https://projects.gnome.org/gtranslator/'
license=('GPL3')
depends=('gtksourceview4' 'gspell' 'gettext' 'libgda6' 'iso-codes'
         'hicolor-icon-theme' 'libdazzle' 'libhandy')
makedepends=('meson' 'gobject-introspection' 'gtk-doc' 'yelp-tools')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('2a67bcbfe643061b0696d89a10847d50bf35905ff3361b9871357d3e3422f13b')

build() {
  arch-meson $pkgname-$pkgver build -Dgtk_doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
