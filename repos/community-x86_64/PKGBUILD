# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cbor2
pkgname=python-cbor2
pkgver=5.4.2
pkgrel=3
pkgdesc="Pure Python CBOR (de)serializer with extensive tag support"
arch=('x86_64')
url="https://github.com/agronholm/cbor2"
license=('MIT')
depends=('python')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('e283e70b55a049ff364cc5e648fde587e4d9b0e87e4b2664c69e639135e6b3b8')
b2sums=('1ad8b7bf54343ad7e969c797a444653e9cf501139facb98f92727be4d31148acedff29885e99c1535a44bcc9add4e4a1890c8f4605c2b82c9975821ee029d7bb')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we do not care about coverage
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
