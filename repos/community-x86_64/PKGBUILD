# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>

pkgname=libfdk-aac
pkgver=0.1.6
pkgrel=1
pkgdesc='Fraunhofer FDK AAC codec library'
arch=('x86_64')
url='http://sourceforge.net/projects/opencore-amr/'
license=('custom')
depends=('glibc')
makedepends=('git')
provides=('libfdk-aac.so')
_commit='a30bfced6b6d6d976c728552d247cb30dd86e238'
source=("git+https://github.com/mstorsjo/fdk-aac.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd fdk-aac

  git describe | sed 's/^v//; s/-/.r/; s/-g/./'
}

build() {
  cd fdk-aac

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --disable-static
  make
}

package () {
  cd fdk-aac

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/libfdk-aac
  install -m 644 NOTICE "${pkgdir}"/usr/share/licenses/libfdk-aac/
}

# vim: ts=2 sw=2 et:
