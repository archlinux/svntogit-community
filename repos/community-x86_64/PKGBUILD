# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=moosefs
pkgver=3.0.111
pkgrel=1
pkgdesc='High performing and fault tolerant distributed file system'
url='https://moosefs.com/'
replaces=(mfs-master mfs-chunkserver mfs-client)
conflicts=(mfs-master mfs-chunkserver mfs-client)
arch=(x86_64)
license=(GPL2)
depends=(fuse2 libpcap python xfsprogs)
source=("$pkgname-$pkgver.tgz::https://github.com/moosefs/moosefs/archive/v$pkgver.tar.gz"
        moosefs.sysusers)
sha256sums=('dba7a17356403323bb365ea0230c3cecaaf8332b7b4b5f143d56b2ec3ca23e89'
            'c7fb746f904bf4ecaaa6b15b9bce7a5b3bbe3008247148c561280eba8df250dc')

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --localstatedir=/var/lib \
    --mandir=/usr/share/man \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-default-group=mfs \
    --with-default-user=mfs \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  make DESTDIR="$pkgdir" -C "$pkgname-$pkgver" install

  # Move mount.moosefs from sbin to usr/bin. They end up in
  # /usr/bin just the same, but this silences a warning:
  mv "$pkgdir/sbin/"* "$pkgdir/usr/bin/"
  mv "$pkgdir/usr/sbin/"* "$pkgdir/usr/bin/"
  rmdir "$pkgdir/sbin" "$pkgdir/usr/sbin"

  install -Dm644 "$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}

# getver: moosefs.com/download/sources.html
# vim: ts=2 sw=2 et:
