# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: zhuqin <zhuqin83@gmail.com>
# Contributor: tri1976 <trile7@gmail.com>
# Contributor: snoopy33 <snoopy33@no-log.org>

pkgname=cairo-dock-plug-ins
pkgver=3.4.1
pkgrel=11
pkgdesc='Plugins for Cairo-Dock'
arch=('x86_64')
url='http://glx-dock.org'
license=('GPL')
depends=('cairo-dock')
makedepends=('alsa-lib' 'cmake' 'dbus-sharp-glib' 'fftw' 'gnome-menus'
             'gtk-sharp-3' 'gvfs' 'libetpan' 'libexif' 'libical' 'libpulse'
             'libxklavier' 'lm_sensors' 'python' 'python2' 'ruby' 'upower'
             'vala' 'vte3' 'zeitgeist')
optdepends=('alsa-lib: Sound Control, Sound Effects applets'
            'dbus-sharp-glib: Mono API'
            'fftw: Impulse applet'
            'gnome-menus: Applications Menu applet'
            'gtk-sharp-3: Mono API'
            'gvfs: GVFS integration'
            'libetpan: Mail applet'
            'libexif: Slider applet'
            'libical: Clock applet'
            'libpulse: Impulse applet'
            'libxklavier: Keyboard Indicator applet'
            'lm_sensors: System Monitor applet'
            'python: Python 3 API'
            'python2: Python 2 API'
            'ruby: Ruby API'
            'upower: Power Manager applet'
            'vte3: Terminal applet'
            'wireless_tools: Wifi applet'
            'zeitgeist: Recent Events applet')
replaces=('cairo-dock-plugins')
source=("cairo-dock-plug-ins-${pkgver}.tar.gz::https://github.com/Cairo-Dock/cairo-dock-plug-ins/archive/${pkgver}.tar.gz"
        'cairo-dock-plug-ins-mono.patch'
        cairo-dock-plugins-3.4.1-time_h-confict.patch)
sha256sums=('321113fb8fa214d965c9827b82890651dc4cd0beb23aef0e2d3f5caf806ebdc2'
            '061115f3638c47eab04d48808d4cc61e294877643a61c82061d7af2523b4f724'
            'f5b859f9acd069cadcfc6188fa1551aa25053abaddf3aa1041cdf10cab3a8fa5')

prepare() {
  cd cairo-dock-plug-ins-${pkgver}

  patch -Np1 -i ../cairo-dock-plug-ins-mono.patch
  patch -Np1 -i ../cairo-dock-plugins-3.4.1-time_h-confict.patch
  sed 's/gmcs/mcs/' -i CMakeLists.txt

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd cairo-dock-plug-ins-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-plug-ins-${pkgver}/build

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/lib/{cli,mono}
}

# vim: ts=2 sw=2 et:
