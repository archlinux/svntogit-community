# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=prettyprinter
pkgname=haskell-prettyprinter
pkgver=1.7.1
pkgrel=107
pkgdesc="A modern, easy to use, well-documented, extensible prettyprinter."
url="https://github.com/quchen/prettyprinter"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-doctest' 'haskell-pgp-wordlist' 'haskell-quickcheck-instances'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('26289e579cc1a2826dc11daedbdfd5ce31acf4a2382f827c20194d910f05201e3f332a6544aa0c3f941188d9eaee8d6dee3cf3d319dc3c5a7bfe4f35d77b4dd9')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-buildReadme
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
