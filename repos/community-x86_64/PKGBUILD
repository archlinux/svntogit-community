# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=pacutils
pkgver=0.6.2
pkgrel=3

pkgdesc='Helper tools for libalpm'
url='https://github.com/andrewgregory/pacutils'
arch=('x86_64')
license=('MIT')

depends=('pacman')
makedepends=('git')

source=("git+https://github.com/andrewgregory/pacutils.git#commit=db60ff5ec278cb3f1db8babcaa20c36d870affbc" # tags/v0.6.2
        "git+https://github.com/andrewgregory/mINI.c.git"
        "git+https://github.com/andrewgregory/tap.c.git")
sha1sums=('SKIP'
          'SKIP'
          'SKIP')

prepare() {
  cd pacutils
  git submodule init
  git config submodule.ext/mini.c.url "$srcdir/mINI.c"
  git config submodule.t/tap.c.url "$srcdir/tap.c"
  git submodule update
}

build() {
  cd pacutils
  make SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

check() {
  cd pacutils
  make check
}

package() {
  cd pacutils
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}

# vim: set ft=PKGBUILD et sw=2:
