# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gpac
pkgver=2.0.0
pkgrel=1
epoch=1
pkgdesc='A multimedia framework based on the MPEG-4 Systems standard'
arch=(x86_64)
url=https://gpac.wp.imt.fr/
license=(LGPL)
depends=(
  glibc
  openssl
  xz
  zlib
)
makedepends=(git)
provides=(libgpac.so)
_tag=418db4149af78773815b5f6a7030a120037ba140
source=(git+https://github.com/gpac/gpac.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd gpac
  git describe --tags | sed 's/^v//'
}

build() {
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"
  cd gpac
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-oss-audio \
    --disable-dvb4linux \
    --disable-qjs \
    --enable-pic \
    --use-js=no
  make
}

package() {
  make DESTDIR="${pkgdir}" STRIP=true -C gpac install
  make DESTDIR="${pkgdir}" STRIP=true -C gpac install-lib
}

# vim: ts=2 sw=2 et:
