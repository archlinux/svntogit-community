# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.3.2
pkgrel=1
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL3')
arch=('x86_64')
depends=(procps-ng hwdata quazip-qt5 qt5-charts qt5-quickcontrols2 qt5-base
         botan qt5-svg qt5-xmlpatterns qt5-base dbus polkit fmt pugixml hicolor-icon-theme)
makedepends=(cmake qt5-tools extra-cmake-modules ninja easyloggingpp)
checkdepends=(catch2 trompeloeil)
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2"
        "0001-Add-easylogging-configuration-to-tests.patch")
sha256sums=('928fd1a7358e948dad81ee04094025ecab912e6d651295ab3daecd781e464ec7'
            '4cb28d50347d09a7182144a87ca14e7d8469e843f8ad6fee2a73278236f41898')
b2sums=('6e243c6f02b4ccdcbbc7a0e184a3c5f20f380e96fc49ee0e89aa816f8041bb7b41645ee47e5edca510c93358d06a8961552f92bb45c99442cd152fc720f8014e'
        'bca22ca8cfde48eab6386f862b247aa41acf1e5385f405c347cc35f7d76873df06f8d71b1a847947c2034a41fb0099a9edfb487fc0eab7f86d20a1066f8ee4d7')

prepare() {
  cd "${pkgname}-v${pkgver}"
  patch -Np1 < "${srcdir}/0001-Add-easylogging-configuration-to-tests.patch"
}

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "${pkgname}-v${pkgver}" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -DBUILD_TESTING=ON \
    -Wno-dev
  cmake --build build --verbose
}

check () {
  CTEST_OUTPUT_ON_FAILURE=1 ninja -C build test
}

package() {
  depends+=(
    fmt libfmt.so
  )
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
