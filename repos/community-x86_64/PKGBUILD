# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.13.2
pkgrel=1
pkgdesc='Plan your next outdoor trip'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack/wiki'
license=('GPL3')
depends=('desktop-file-utils' 'gdal' 'hicolor-icon-theme' 'proj' 'qt5-webengine'
         'quazip' 'routino>=3.1' 'alglib')
makedepends=('cmake' 'qt5-tools' 'patch')
source=($pkgname-$pkgver.tar.gz::https://github.com/Maproom/$pkgname/archive/V_$pkgver.tar.gz
        FindPROJ4.patch
        FindQuaZip5.patch
        https://gist.githubusercontent.com/svetlemodry/d3a727821b18356afca05f2de89d703a/raw/83e1b75ec9cc5200b2dc563ad45c6ed6ff6e61b8/c6e5240ec25b91d3320f265346a904b5bd906363-without-PNGs.patch
        https://github.com/Maproom/qmapshack/commit/79a266943a40bee8fa5e71776c6a76c4d46bfbf8.patch
        https://github.com/Maproom/qmapshack/commit/763cfc149566325cce9e4690cb7b5f986048f86a.patch
        https://raw.githubusercontent.com/Maproom/qmapshack/dev/src/qmaptool/pic/line_3px_horizontal.png
        https://raw.githubusercontent.com/Maproom/qmapshack/dev/src/qmaptool/pic/line_3px_vertical.png
        https://raw.githubusercontent.com/Maproom/qmapshack/dev/src/qmaptool/pic/splash.png)
sha256sums=('659409e3b6ef44ed67b9eb4fc54274a7d496885ba4728308a80722659f3b8935'
            '1ffb847b098fedf01e347971da33c58b3fbf1a0f27205d48f6eb52206e228046'
            '23f32e58daa7c69072ea59465f0541e86dcff3085e4e73a948713a02ae421889'
            '6fb0d92dd91866df27e50775163024e98b02d330f65e486cdc9d46f34188fdce'
            'f2bcd793d45c1c93e7ab74db52fcec0df67f7e825713d06f85a1b38897b91e06'
            'f7865db07a58edf4e17146e0d5f2822aad480ec03ed47c1212491e4be5ddce60'
            '9362cd3b8d967d071c0c2bd25a77583e354ad539c130f661466b7716aebd67d8'
            '21d77ec0e84eff9ba2f29a6f4a5c79d4988c626c939c5ed34677a7e67874608e'
            'edf1ef7f73a79b1722e3b18c919ae0706bc52c64553e622006ac8a9985373cf7')

prepare() {
  cd "$pkgname-V_$pkgver"
  patch -Np1 -i "${srcdir}"/FindPROJ4.patch
  patch -Np1 -i "${srcdir}"/FindQuaZip5.patch
  patch -Np1 -i "${srcdir}"/c6e5240ec25b91d3320f265346a904b5bd906363-without-PNGs.patch
  patch -Np1 -i "${srcdir}"/79a266943a40bee8fa5e71776c6a76c4d46bfbf8.patch
  patch -Np1 -i "${srcdir}"/763cfc149566325cce9e4690cb7b5f986048f86a.patch

  install -Dm644 "${srcdir}"/line_3px_horizontal.png src/qmaptool/pic/line_3px_horizontal.png
  install -Dm644 "${srcdir}"/line_3px_vertical.png src/qmaptool/pic/line_3px_vertical.png
  install -Dm644 "${srcdir}"/splash.png src/qmaptool/pic/splash.png
}

build() {
  cd "$pkgname-V_$pkgver"

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-V_$pkgver"

  make DESTDIR="${pkgdir}" install
}
