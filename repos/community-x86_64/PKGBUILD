# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pyproj
pkgname=('python-pyproj' 'python2-pyproj')
pkgver=1.9.5.1
pkgrel=6
pkgdesc="Python interfaces to PROJ.4 library"
arch=('x86_64')
url="https://jswhit.github.io/pyproj/"
license=('custom')
makedepends=('python-setuptools' 'python2-setuptools' 'proj' 'cython')
source=($pkgname-$pkgver.tar.gz::https://github.com/jswhit/pyproj/archive/v${pkgver}rel.tar.gz)
sha256sums=('0e19f02ebbf4c253813142ffe06e12489f3861877c3fab75b6c769c1a1960cac')

prepare() {
  cd "${srcdir}"

  # regenerate for python 3.7
  cythonize -f pyproj-${pkgver}rel/_proj.pyx

# Remove RPATH
  sed -i "s/runtime_library_dirs=libdirs,//" pyproj-${pkgver}rel/setup.py

  cp -a pyproj-${pkgver}rel{,-python2}
}

build() {
  cd "${srcdir}"

  export PROJ_DIR=/usr

# Build python 3 module
  cd pyproj-${pkgver}rel
  python setup.py build

# Build python 2 module
  cd ../pyproj-${pkgver}rel-python2
  python2 setup.py build
}

package_python-pyproj() {
  depends=('proj' 'python')

  cd "${srcdir}"/pyproj-${pkgver}rel
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyproj() {
  depends=('proj' 'python2')

  cd "${srcdir}"/pyproj-${pkgver}rel-python2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
