# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Mike Roberts <noodlesgc@gmail.com>

pkgname=libcli
pkgver=1.10.7
pkgrel=1
pkgdesc='Library for including a Cisco-like command-line interface into other software'
url='https://github.com/dparrish/libcli'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc' 'libxcrypt' 'libcrypt.so')
provides=('libcli.so')
source=(https://github.com/dparrish/libcli/archive/V${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('4c341260e554d7e7a4181bbc85fd2e4cd57cb3fd4304cd423af63a407950f394a318ea69588e12b1ffc845e14d836409dc087cd737b29861c62bf2e14c702a63')
b2sums=('bbb156d564dabf5198d99b92b32edc9cd7a34e399a8cc39e57c387ddceda6c3f00d89ee110863d35194c35bf5f93ff6d96c149c65a7e55b39b2bb846fbd1c7eb')

prepare() {
  sed -i 's/-Werror//' ${pkgname}-${pkgver}/Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:
