# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.24.5
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/crate-ci/cargo-release'
arch=('x86_64')
license=('MIT' 'Apache-2.0')
depends=('gcc-libs' 'libgit2' 'libssh' 'openssl' 'zlib')
makedepends=('cargo' 'pkg-config')
options=('!lto')
source=(https://github.com/crate-ci/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('b759d2404080c5e6569dd46893e558b49709dd9239434a344fb0b1b3d9368b23f1f54c68983383094abd84362b126af090a620057fcad2f6732e7bf0639804e3')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/"vendored-libgit2"//' Cargo.toml
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  LIBSSH2_SYS_USE_PKG_CONFIG=1 cargo build --frozen --release
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
