# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.5
pkgrel=4
pkgdesc="A simple video player that is synchronized to jack transport."
arch=('i686' 'x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL')
depends=('ffmpeg' 'imlib2' 'jack' 'liblo>=0.28' 'libltc' 'libxpm' 'libxv' 'mesa' 'portmidi')
makedepends=('glu')
backup=('etc/xjadeorc')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
	"$pkgname.desktop")
md5sums=('cc645fd249551cc679ee6f128072bedf'
         '13af7a942e95d9a0079084f24b357e36')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # rc file
  install -Dm644 doc/xjadeorc "$pkgdir/etc/xjadeorc"

  # desktop file
  install -Dm644 ../xjadeo.desktop \
    "$pkgdir/usr/share/applications/xjadeo.desktop"

  # icon
  install -Dm644 src/xjadeo/icons/xjadeoH128.png \
    "$pkgdir/usr/share/pixmaps/xjadeo.png"
}
