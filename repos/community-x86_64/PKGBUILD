# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-editor
pkgver=1.0.15
pkgrel=1
pkgdesc='Public library for deepin-image-viewer and deepin-album'
arch=('x86_64')
url="https://github.com/linuxdeepin/image-editor"
license=('GPL3')
depends=('dtkwidget' 'freeimage' 'opencv')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/image-editor/archive/$pkgver.tar.gz"
        remove-broken-flags.patch)
sha512sums=('48a04c747a3db9f80936b2583da59a7c066bd103877be6026778edb381c75f83b251ef33d3e6655cd4af7c1d6be210cd86883ab9dbf9d4849e25a238556ce052'
            '033b26be1d5e4c5b2d4452fa1e191cd06377eb1a6ae57adfa9a2e79ead3da2433dc26301ed0cc826c3b0daffc94119b2767cf003b41beb235bcccdcdd34f7491')

prepare() {
  patch -d image-editor-$pkgver -p2 < remove-broken-flags.patch
}

build() {
  cd image-editor-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd image-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
