# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=thin
pkgname=ruby-${_gemname}
pkgver=1.7.2
pkgrel=2
pkgdesc='Thin and fast Ruby web server'
url='http://code.macournoyer.com/thin/'
arch=('x86_64')
license=('RUBY')
depends=('glibc' 'ruby' 'ruby-rack' 'ruby-eventmachine' 'ruby-daemons')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/macournoyer/thin/archive/v${pkgver}.tar.gz)
sha256sums=('667a78697331b917ad0f14974537c000c6e06d67a0d9b2a6592791adfed2c48f')
sha512sums=('2c3c831eed4444d42fc88fc2c46e557284748b6b6bf331d6fd827073918cc3a869e5a60bf7db19ba82add28e07c8a8c92dac609a56cda82b41b37ddfb247e7bf')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a example "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}/${_gemdir}" \( -name 'mkmf.log' -or -name 'gem_make.out' \) -delete
}

# vim: ts=2 sw=2 et:
