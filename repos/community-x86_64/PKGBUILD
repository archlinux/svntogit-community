# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=git-repair
pkgver=1.20200504
pkgrel=47
pkgdesc="Repairs a damaged git repository"
url="https://git-repair.branchable.com/"
license=("AGPL3")
arch=('x86_64')
depends=('git' 'ghc-libs' 'haskell-async' 'haskell-attoparsec' 'haskell-data-default'
         'haskell-filepath-bytestring' 'haskell-hslogger' 'haskell-ifelse'
         'haskell-network' 'haskell-network-uri' 'haskell-optparse-applicative' 'haskell-quickcheck'
         'haskell-split' 'haskell-unix-compat' 'haskell-utf8-string')
makedepends=('ghc')
source=("$pkgname::git://git-repair.branchable.com/#tag=$pkgver")
sha512sums=('SKIP')

build() {
  cd git-repair

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/$pkgname" --ghc-option='-pie'
  runhaskell Setup build $MAKEFLAGS
}

package() {
  cd git-repair
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 git-repair.1 "$pkgdir"/usr/share/man/man1/git-repair.1

  rm "$pkgdir"/usr/share/doc/git-repair/COPYRIGHT
  rmdir "$pkgdir"/usr/share/doc/git-repair "$pkgdir"/usr/share/doc
}
