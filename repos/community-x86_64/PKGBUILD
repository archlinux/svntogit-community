# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=chell
pkgname=haskell-chell
pkgver=0.4.0.2
pkgrel=7
pkgdesc="A simple and intuitive library for automated testing"
url="https://john-millikin.com/software/chell/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-options' 'haskell-patience' 'haskell-random'
        )
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('050c5818ad2cd954ef79b230de2f03fdfb19e99c8624a24bb7845bb20e3b5d5d427a9623491ffc706c5635f545eb99466bde7a851c4344d11df1a70bfb2bbe22')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/<.*0.8/<1/' $_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcolor-output
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
