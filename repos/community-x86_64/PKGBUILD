# $Id$
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Denis Tikhomirov <dvtikhomirov@gmail.com>

pkgname=minicom
pkgver=2.7.1
pkgrel=2
pkgdesc="A serial communication program"
arch=('x86_64')
url="http://alioth.debian.org/projects/minicom/"
license=('GPL')
depends=('bash')
optdepends=('lrzsz: for xmodem, ymodem and zmodem file transfer protocols')
backup=('etc/minirc.dfl')
source=("${pkgname}-${pkgver}.tar.gz::http://ftp.debian.org/debian/pool/main/m/minicom/minicom_${pkgver}.orig.tar.gz")
sha256sums=('532f836b7a677eb0cb1dca8d70302b73729c3d30df26d58368d712e5cca041f1')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}/" install
  install -Dm644 doc/minirc.dfl ${pkgdir}/etc/minirc.dfl
}
