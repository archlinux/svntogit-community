# $Id: PKGBUILD 209996 2017-02-05 14:39:04Z alucryd $
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=libretro-mednafen-psx
pkgname=('libretro-mednafen-psx' 'libretro-mednafen-psx-hw')
pkgver=1430
pkgrel=1
pkgdesc='Sony PlayStation core'
arch=('i686' 'x86_64')
url='https://github.com/libretro/mednafen-psx-libretro'
license=('GPL2')
groups=('libretro')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'libgl' 'mesa' 'vulkan-icd-loader')
_commit='e3aae3d47a830cb7ce3f6ee604d1dd75d4ac1be9'
source=("libretro-mednafen-psx::git+https://github.com/libretro/beetle-psx-libretro.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mednafen_psx_libretro.info'
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/mednafen_psx_hw_libretro.info')
sha256sums=('SKIP'
            '62a5cc23016b0de899e3a5014540d39d912f08c34b20ed6e10ddae8c11f1ba5d'
            '0eacb7ea75df0bb6f458754290ed7ab21e197e170fe3e80579b8161782963df1')

pkgver() {
  cd libretro-mednafen-psx

  git rev-list --count HEAD
}

build() {
  cd libretro-mednafen-psx

  make
  make HAVE_HW=1
}

package_libretro-mednafen-psx() {
  cd libretro-mednafen-psx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mednafen_psx_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mednafen_psx_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

package_libretro-mednafen-psx-hw() {
  depends+=('libgl')

  cd libretro-mednafen-psx

  install -dm 755 "${pkgdir}"/usr/{lib/libretro,share/libretro/info}
  install -m 644 mednafen_psx_hw_libretro.so "${pkgdir}"/usr/lib/libretro/
  install -m 644 ../mednafen_psx_hw_libretro.info "${pkgdir}"/usr/share/libretro/info/
}

# vim: ts=2 sw=2 et:
