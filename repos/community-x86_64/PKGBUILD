# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Johan Rehnberg <cleanrock@gmail.com>

pkgname=naev
pkgver=0.10.0
pkgrel=1
pkgdesc='2D action/rpg space game'
arch=('x86_64')
url="https://github.com/naev/naev"
license=('GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libgl' 'libxml2' 'freetype2'
         'libpng' 'libzip' 'luajit' 'glpk' 'physfs' 'libunibreak' 'suitesparse' 'openblas' 'cblas')
makedepends=('freeglut' 'zip' 'meson' 'git' 'python-yaml')
source=("https://github.com/naev/naev/releases/download/v$pkgver/naev-$pkgver-source.tar.xz"
         binutils-2.39.patch)
sha512sums=('419573ce6812e87ee73527df0c68853405046b6e3ac16a1de32b754c1a7aefaeecd285d0252115c421154e0695d64c28b06810fcff230b9bed722b694368ac35'
            '274b2189a9c81d75ff649ae777fdca0cba0ed41fd1c62e8079fc3cd4e48617cde49d930742e7786dbe98a46a02e8733ac9a6d551d6e418f27ecfaeff91c63679')

# prepare() {
#   # patch -d $pkgname-$pkgver -p1 < binutils-2.39.patch # Fix build with binutils 2.39
# }

build() {
  cd "$pkgname-$pkgver"

  export LDFLAGS+=' -lcblas' # Workaround our openblas not providing cblas
  meson setup -Dprefix=/usr -Dndata_path=/usr/share/naev/ndata build .
  cd build
  meson compile
}

package() {
  cd "$pkgname-$pkgver/build"

  DESTDIR="$pkgdir" meson install
}
