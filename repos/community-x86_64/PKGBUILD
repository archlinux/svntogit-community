# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alex <mail.avatar@gmail.com>

pkgname=exfat-utils
pkgver=1.2.0
pkgrel=1
pkgdesc="Utilities for exFAT file system"
arch=('i686' 'x86_64')
url="http://code.google.com/p/exfat/"
license=('GPL2')
depends=('glibc')
source=("https://github.com/relan/exfat/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a2d3132cff90153b875b3be5eda6bc7b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sbindir=/usr/bin

  make CCFLAGS="${CFLAGS} -std=c99" LINKFLAGS="${LDFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # Install man pages
  install -Dm444 dump/dumpexfat.8 "${pkgdir}/usr/share/man/man8/dumpexfat.8"
  install -m444 fsck/exfatfsck.8 "${pkgdir}/usr/share/man/man8"
  install -m444 mkfs/mkexfatfs.8 "${pkgdir}/usr/share/man/man8"
  install -m444 label/exfatlabel.8 "${pkgdir}/usr/share/man/man8"
}
