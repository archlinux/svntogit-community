# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.9.4
pkgrel=2
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
url=https://github.com/lotabout/skim
license=('MIT')
depends=('ncurses')
makedepends=('rust')
optdepends=('bash: for resp. completions and key bindings, and for sk-tmux script'
            'fish: for resp. key bindings'
            'tmux: for sk-tmux script'
            'vim-plugin-runtime: for N/Vim plugin'
            'zsh: for resp. completions and key bindings')
source=("skim-$pkgver.tar.gz::https://static.crates.io/crates/skim/skim-$pkgver.crate")
b2sums=('b41f0f0f22f0bef615ec6b013c2d95149ad4a13fde438b7b25064d706388531d25fafaa8c5b576f7fbb231c2c11007e7e38446033ab23af59ffea82412b50c83')

prepare() {
  cd skim-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd skim-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd skim-$pkgver
  cargo test --locked --offline
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}

# vim:set ts=2 sw=2 et:
