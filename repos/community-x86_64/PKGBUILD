# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Philipp Wolfer <ph.wolfer@gmail.com>

pkgname=gifski
pkgver=1.8.1
pkgrel=1
pkgdesc='GIF encoder based on libimagequant (pngquant). Squeezes maximum possible quality from the awful GIF format.'
arch=('x86_64')
url='https://gif.ski'
license=('AGPL3')
depends=('ffmpeg4.4')
makedepends=('cargo' 'clang')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ImageOptim/$pkgname/archive/$pkgver.tar.gz"
        "$pkgname-$pkgver-cargo-lock.patch::https://github.com/ImageOptim/gifski/commit/88581c5384f8910b813b41d96f226c155d537ef5.patch")
sha512sums=('ac04db56c8a46f98c25599749b2d05eaf9c8549916a39c33062d34808cffe6cecde1b4e61d05ec646a87d962141fb1bd89189a98c5b60831c2c8780a1f118b66'
            '8653f80b3a0396e413dc5980d8c7dfed7814e7e7b37cc4c7c282a399a859198fb594854ed02799b0821460e53a026092149e4ce28eba8a465b2359ebb52ea97f')
options=('!lto')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i "$srcdir/$pkgname-$pkgver-cargo-lock.patch"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}


build() {
  cd $pkgname-$pkgver

  export PKG_CONFIG_PATH='/usr/lib/ffmpeg4.4/pkgconfig'
  cargo build --frozen --release --features=video
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen
}

package() {
  cd $pkgname-$pkgver

  install -Dm 755 target/release/gifski "$pkgdir"/usr/bin/gifski
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
