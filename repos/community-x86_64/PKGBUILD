# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>

pkgname=rekor
pkgver=0.1.1
pkgrel=1
pkgdesc="Signature Transparency Log -- Sigstore client and server tools"
arch=('x86_64')
url="https://github.com/sigstore/rekor-cli"
license=('Apache')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/rekor/archive/v${pkgver}.tar.gz")
sha256sums=('554bb6e23f1f071a1d887975e3870dae2bfacb9b754cee5116c4c5ee4594c88e')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -o . ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 cli "${pkgdir}/usr/bin/${pkgname}-cli"
  install -Dm755 server "${pkgdir}/usr/bin/${pkgname}-server"
}
