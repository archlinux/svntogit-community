# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastnumbers
pkgname=python-fastnumbers
pkgver=4.0.1
pkgrel=1
pkgdesc="Super-fast and clean conversions to numbers"
arch=(x86_64)
url="https://github.com/SethMMorton/fastnumbers"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-hypothesis python-pytest python-typing_extensions)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('d3ed9811a64c2292925b30891414b69e2a65411d919fb05fc15905a5e38a651fbfdcfccc52084b7757d5f5f52a0763129934cdcf4e36e6bb727e08d6f44d41b2')
b2sums=('f1acbd713abb9cb99c933a3f0e50212948aa66a53e3b90718618c96ec6981c0f26c56ea361730402d633aff701201f5672f0948a751263241c1079bbcafa0413')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  cd $_name-$pkgver
  python -m installer --destdir=test_dir dist/*.whl
  export PYTHONPATH="test_dir/$site_packages:$PYTHONPATH"
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
