# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=28.0
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('i686' 'x86_64')
url="https://launchpad.net/deja-dup"
license=('GPL')
depends=('duplicity' 'libpeas' 'libnotify' 'python2-gobject' 'gvfs' 'xdg-utils')
makedepends=('vala' 'intltool' 'itstool' 'nautilus')
optdepends=('gnome-keyring: save passwords'
            'nautilus: backup extension'
            'python2-boto: Amazon S3 backend'
            'ubuntuone-client: Ubuntu One backend')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz)
md5sums=('480516869f49813880f2bbb35700c9af')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's/\"python\", \"-c\"/\"python2\", \"-c\"/' libdeja/PythonChecker.vala
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --without-unity
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
