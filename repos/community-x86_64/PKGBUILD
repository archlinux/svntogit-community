# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: TingPing <tingping@tingping.se>

pkgname=hexchat
pkgver=2.12.4
pkgrel=8
pkgdesc='A popular and easy to use graphical IRC (chat) client'
arch=('x86_64')
url='https://hexchat.github.io/'
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'libcanberra' 'libnotify' 'libproxy' 'openssl' 'pango' 'pciutils')
makedepends=('intltool' 'iso-codes' 'lua' 'perl' 'python' 'autoconf-archive')
optdepends=('enchant: Spell check'
            'iso-codes: Display language names instead of codes'
            'lua: Lua plugin'
            'perl: Perl plugin'
            'python: Python plugin')
source=("https://dl.hexchat.net/hexchat/hexchat-${pkgver}.tar.xz"{,.asc}
        'hexchat-enchant2.0.patch::https://github.com/hexchat/hexchat/commit/005858795878b92fc17b4c2127bce90a1d053483.patch'
        'hexchat-enchant2.1.patch::https://github.com/hexchat/hexchat/commit/234fe8698781f149df0e6928e48859f66b5e947b.patch')
sha256sums=('fa35913158bbc7d0d99de79371b6df3e8d21802f1d2c7c92f0e5db694acf2c3a'
            'SKIP'
            'd5f1e70b696337b15d40386292748483d2b27631a9bba6cfa5c97eae4f6987f1'
            'b9687999f8e013716ec997458de1db0d3618382a6963d5f6eb578fea253daa24')
validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis

prepare() {
  cd hexchat-${pkgver}

  patch -Np1 -i ../hexchat-enchant2.0.patch
  patch -Np1 -i ../hexchat-enchant2.1.patch
}

build() {
  cd hexchat-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-python='python3' \
    --enable-textfe
  make
}

package() {
  cd hexchat-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
