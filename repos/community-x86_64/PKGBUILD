# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: mdv

pkgname=tellico
pkgver=2.3
pkgrel=2
pkgdesc="A collection manager for KDE"
arch=('i686' 'x86_64')
url="http://www.periapsis.org/tellico/"
license=('GPL')
depends=('kdebase-workspace' 'yaz' 'exempi' 'kdegraphics-libs' 'taglib'
	'kdemultimedia-kioslave' 'poppler-qt' 'qjson')
makedepends=('automoc4' 'cmake' 'docbook-xsl')
install=${pkgname}.install
source=("http://tellico-project.org/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('54936f0b34f612a428d68d5276d6f478')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
     -DCMAKE_BUILD_TYPE=Release \
     -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install

  # fix python 2.7 path
  find "$pkgdir" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
