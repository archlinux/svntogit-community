# Maintainer: Dan Printzell <arch@vild.io>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>

pkgname=dub
pkgver=1.26.0
pkgrel=1
pkgdesc="Developer package manager for D programming language"
arch=('x86_64')
url="https://github.com/dlang/dub"
license=('MIT')
groups=('dlang')
makedepends=('ldc')
depends=('curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dlang/dub/archive/v$pkgver.tar.gz")
sha512sums=('0ca0fa2d286e7fe86a78fd8e3791e7603f299be85007d658d53653364c9ec93a1bbe5b6a231ce4ff0fa11b96bbb528d5c215a9c9ce09137269c562786bafd688')

build() {
	cd "$pkgname-$pkgver"

	echo Generating version file...
	echo "module dub.version_;" > source/dub/version_.d
	echo "enum dubVersion = \"$pkgver\";" >> source/dub/version_.d

	ldmd2 -ofbin/dub -release -O5 -version=DubUseCurl -Isource -L-lcurl -L="$LDFLAGS" -release -flto=full -linker=gold -link-defaultlib-shared=false -L--build-id @build-files.txt
}

package() {
	# binaries
	install -Dm755 "$pkgname-$pkgver/bin/dub" "$pkgdir/usr/bin/dub"

	# license
	install -Dm644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# bash, fish and zsh completion
	install -Dm644 "$pkgname-$pkgver/scripts/bash-completion/dub.bash" "$pkgdir/usr/share/bash-completion/completions/dub"
	install -Dm644 "$pkgname-$pkgver/scripts/fish-completion/dub.fish" "$pkgdir/usr/share/fish/vendor_completions.d/dub.fish"
	install -Dm644 "$pkgname-$pkgver/scripts/zsh-completion/_dub" "$pkgdir/usr/share/zsh/site-functions/_dub"
}
