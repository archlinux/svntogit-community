# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=unicode-transforms
pkgname=haskell-unicode-transforms
pkgver=0.3.5
pkgrel=2
pkgdesc="Unicode transforms (normalization NFC/NFD/NFKC/NFKD)"
url="https://github.com/ekmett/unicode-transforms/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-bitarray')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('3d1da63160b5207b360979ad12c79127effb63196eedc0024c23b751ac58b82d1fc0772e263b0db58f9ea071d1c310bb780871185462eb013a32c07db2d28cc8')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev -f-has-icu -f-has-llvm
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
