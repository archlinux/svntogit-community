# Maintainer: Anatol Pomozov

pkgname=aws-crt-cpp
pkgver=0.17.27
pkgrel=1
pkgdesc='C++ wrapper around the aws-c-* libraries. Provides Cross-Platform Transport Protocols and SSL/TLS implementations for C++.'
arch=(x86_64)
url='https://github.com/awslabs/aws-crt-cpp'
license=(Apache)
depends=(gcc-libs aws-c-common aws-c-http aws-c-mqtt aws-c-auth aws-c-s3 aws-c-event-stream)
makedepends=(cmake)
source=(aws-crt-cpp-$pkgver.zip::https://github.com/awslabs/aws-crt-cpp/archive/v$pkgver.zip)
sha256sums=('907420357ec135241a3df145ba852f6e61b1ff3aeb637f2d305100ae14c93ee2')

build() {
  cd aws-crt-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -DBUILD_DEPS=OFF -S . -B build
  cmake --build build
}

check() {
  cd aws-crt-cpp-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-crt-cpp-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
