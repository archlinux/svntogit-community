# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=3.0.7
_golibver=0.4.9
_dbusfactoryver=3.0.3
_girgeneratorver=0.9.3
pkgrel=1
pkgdesc='Go-lang bingdings for dde-daemon'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill' 'gcc-libs>=5.3.0-3' 'poppler-glib' 'deepin-metacity' 'xcur2png')
makedepends=('git' 'deepin-dbus-generator' 'deepin-gir-generator' 'bzr' 'gcc-go')
conflicts=('dde-api')
replaces=('dde-api')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/dde-api.git#tag=$pkgver"
        "git+https://cr.deepin.io/go-lib.git#tag=$_golibver"
        "git+https://cr.deepin.io/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://cr.deepin.io/go-gir-generator.git#tag=$_girgeneratorver")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i "s|\"\${LDFLAGS}\"|\"\${LDFLAGS} $CFLAGS $LDFLAGS\"|" dde-api/Makefile
  sed -i 's| generator$||' go-gir-generator/makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/lib
  cp -a go-lib/* build/src/pkg.deepin.io/lib

  cd "$srcdir/dde-api"
  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2
}

build(){
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/go-gir-generator"
  make GENERATOR=gir-generator build
  make copyfile
  cp -r out/src/gir "$GOPATH/src"/

  cd "$srcdir/dde-api"
  make USE_GCCGO=1
}

package() {
  cd dde-api
  make DESTDIR="${pkgdir}" SYSTEMD_LIB_DIR=/usr/lib install-binary
}
