# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Fabla
pkgname=fabla
pkgver=1.3.2
pkgrel=4
pkgdesc="An open-source LV2 drum sampler plugin instrument"
arch=(x86_64)
url="http://openavproductions.com/fabla/"
license=(GPL2)
groups=(
  lv2-plugins
  pro-audio
)
depends=(
  cairo
  gcc-libs
  glibc
)
makedepends=(
  cmake
  libglvnd
  libsndfile
  lv2
  ntk
)
checkdepends=(lv2lint)
source=(
  https://github.com/openAVproductions/openAV-$_name/archive/release-$pkgver/openAV-$_name-$pkgname-release-$pkgver.tar.gz
  $pkgname-1.3.2-lv2-1.18.0.patch::https://github.com/openAVproductions/openAV-Fabla/commit/266cb8f6ca252d777a3d81a9dc621eab8bbee434.patch
  $pkgname-1.3.2-cmake.patch::https://github.com/openAVproductions/openAV-Fabla/commit/fc15f87dd48e751b7b11e306c0c6c051d6b83340.patch
  $pkgname-1.3.2-ttl1.patch::https://github.com/openAVproductions/openAV-Fabla/commit/7f87d032cfd2db6d0ad62f3fcc7f569d856ebb3a.patch
  $pkgname-1.3.2-ttl2.patch::https://github.com/openAVproductions/openAV-Fabla/commit/c94e6a043c43467550118ade6591f9fd0400200c.patch

  $pkgname-1.3.2-plugin_optimization.patch::https://github.com/openAVproductions/openAV-Fabla/commit/f04313e9e948d4bce433afba6c8ea8e02e9911c6.patch
  $pkgname-1.3.2-plugin_visibility.patch::https://github.com/openAVproductions/openAV-Fabla/commit/d5172152f782084d3e4c5e6501599518c736b8f1.patch
  $pkgname-1.3.2-plugin_data.patch::https://github.com/openAVproductions/openAV-Fabla/commit/6f4203a24c3a6dd01a5700efb10a856387a7fce2.patch
)
sha512sums=('009aa11b50743fe3a8f401657b0c67eb9e8b757f35d713d07e763a23f5e3e5be398858a766e6b116be85fae11969baddafd5d98623858cf0ebedc2a8e3b02f66'
            '208b94860f67cf8033ebcce9a6152fa6312ddc3563cb10a41e2c1366383e71bd019552b7050cb1bc2335899181cdbcce50d3e5772eaa37b0d8f26542b8a4ca93'
            'acf6bcd362e22fbb853385c29219429f97db62fdfa30672fd7ce2e94724b57bae7b7cb46b39f791b3c1fef4af4e8f75d1f08b7554bb56686fe81203d4b2f9a99'
            '86294e35f9a7c490c1c834983aae847858f751d1c7780080d868d72b04818152ead244432808a224787f3dd2b14c369ecca7383b1aaca1fc75d5d891d653ccbd'
            '080e8e41b2f82a9d62052a1bddcdce1fa19a0ff6c6409c9f2e3eba385b5601e3abfdf89723e00bad882ac4885ca73456ad16b405e3df23ffd190e7f440475b7b'
            'c81952eefb11e2d51841e232db723b0ede4b52da8a1e07edec09febbf4d6e0f0168ee8ace1aae6588ab62f3d82f23af9a557565ec71cfdfddc0e256dbcaa8cd2'
            '9aabd5b2e4b7628aa8aeb6f09c9c59331081be13136aa4b16768f636c030f72ac0212dbed3539b34dfa945da6962b155c462664e25aa0a79451a8b6239c7c71a'
            '5246745b1d9d54e2cd37eb3167ebe2a8a70837704ace408a81f59ae91f34b15ecc8cddd6c82c1b3ba10f80d547f4c9cdfe35669ddea6036a055c0981a7f4605c')

prepare() {
  # fixing build with lv2 >= 1.18.0
  patch -Np1 -d openAV-$_name-release-$pkgver -i ../$pkgname-1.3.2-lv2-1.18.0.patch
  # fixing cmake setupt to respect CFLAGS/CXXFLAGS/LDFLAGS
  patch -Np1 -d openAV-$_name-release-$pkgver -i ../$pkgname-1.3.2-cmake.patch
  # fix issue with lv2 metadata: https://github.com/openAVproductions/openAV-Fabla/issues/59
  patch -Np1 -d openAV-$_name-release-$pkgver -i ../$pkgname-1.3.2-ttl1.patch
  patch -Np1 -d openAV-$_name-release-$pkgver -i ../$pkgname-1.3.2-ttl2.patch
  patch -Np1 -d openAV-$_name-release-$pkgver -i ../$pkgname-1.3.2-plugin_optimization.patch
  patch -Np1 -d openAV-$_name-release-$pkgver -i ../$pkgname-1.3.2-plugin_visibility.patch
  patch -Np1 -d openAV-$_name-release-$pkgver -i ../$pkgname-1.3.2-plugin_data.patch
}

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_PREFIX=/usr
    -S openAV-$_name-release-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  cp -v build/$pkgname.so openAV-$_name-release-$pkgver/dsp/
  lv2lint -Mpack -I openAV-$_name-release-$pkgver/dsp/ "http://www.openavproductions.com/fabla"
}

package() {
  depends+=(
    ntk libntk.so
    libsndfile libsndfile.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 openAV-$_name-release-$pkgver/{CHANGELOG,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
