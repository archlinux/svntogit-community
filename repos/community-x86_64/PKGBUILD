# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=tree-diff
pkgname=haskell-tree-diff
pkgver=0.3
pkgrel=4
pkgdesc="Diffing of (expression) trees"
url="https://github.com/phadej/tree-diff"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-wl-pprint' 'haskell-ansi-terminal'
         'haskell-base-compat' 'haskell-data-array-byte' 'haskell-hashable' 'haskell-parsers'
         'haskell-primitive' 'haskell-quickcheck' 'haskell-scientific' 'haskell-semialign'
         'haskell-strict' 'haskell-tagged' 'haskell-these' 'haskell-unordered-containers'
         'haskell-uuid-types' 'haskell-vector')
makedepends=('ghc' 'uusi' 'haskell-trifecta' 'haskell-tasty' 'haskell-tasty-golden'
             'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('60c1c777544f288b2920806e76df42116a996fb0d40cd8a55e9e97480e3504f1c07a4b3281d9ffb44189c8140ea3e0859c6f1256501b4b693a05e2cb0feba585')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
    uusi -d bytestring-builder $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
