# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sigil
pkgver=1.8.0
pkgrel=1
pkgdesc="multi-platform EPUB2/EPUB3 ebook editor"
arch=('x86_64')
url="https://sigil-ebook.com/"
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'mathjax2' 'minizip' 'python-css-parser'
         'python-dulwich' 'python-lxml' 'qt5-svg' 'qt5-webengine')
makedepends=('qt5-tools' 'cmake')
optdepends=('hunspell-en_US: for English dictionary support'
            'hyphen-en: for English hyphenation support in plugins'
            'pageedit: external editor to replace BookView'
            'python-html5lib: recommended for plugins'
            'python-chardet: recommended for plugins'
            'python-cssselect: recommended for plugins'
            'python-pillow: recommended for plugins'
            'python-regex: recommended for plugins'
            'python-pyqt5: recommended for plugins'
            'python-pyqt5-webengine: recommended for plugins'
            'tk: recommended for plugins')
source=("Sigil-${pkgver}.tar.gz::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "https://github.com/Sigil-Ebook/Sigil/releases/download/${pkgver}/Sigil-${pkgver}.tar.gz.sig")
sha256sums=('33d84331f259ab8d92fc2c5fed6f05fd5f011695c48f83537423635a7afdd2ec'
            'SKIP')
b2sums=('78615652859d1f61bab18b1b248d342dc6892943a2310659859ab577981c4452fbaeb567758097427453c8dfe95e42b499616a423b5c11b42238f763ac0653c8'
        'SKIP')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

build() {
    mkdir -p "${srcdir}"/Sigil-${pkgver}/build
    cd "${srcdir}"/Sigil-${pkgver}/build

    cmake -G "Unix Makefiles" \
        -DUSE_SYSTEM_LIBS=1 \
        -DSYSTEM_LIBS_REQUIRED=1 \
        -DINSTALL_BUNDLED_DICTS=0 \
        -DINSTALL_HICOLOR_ICONS=1 \
        -DDISABLE_UPDATE_CHECK=1 \
        -DMATHJAX_DIR=/usr/share/mathjax2 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
        -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
        -DCMAKE_SKIP_RPATH=ON ..

    make
}

package() {
    cd "${srcdir}"/Sigil-${pkgver}/build

    make install DESTDIR="${pkgdir}"

    # Compile python bytecode
    python -m compileall "${pkgdir}"/usr/share/sigil/{plugin_launchers/python/,python3lib}
    python -O -m compileall "${pkgdir}"/usr/share/sigil/{plugin_launchers/python/,python3lib}
}
