# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Christian Muehlhaeuser <muesli at gmail dot com>

pkgname=skate
pkgver=0.1.3
pkgrel=2
pkgdesc="A personal key value store"
arch=('x86_64')
url="https://github.com/charmbracelet/skate"
license=('MIT')
depends=('glibc')
makedepends=('git' 'go')
options=('!lto')
_commit='f846a6cd60bfcafcf008bde5f0cc73699d2e88b3'
source=("git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd skate

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd skate

  # create directory for build output
  mkdir build

  # download dependencies
  go mod download
}

build() {
  cd skate

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags $LDFLAGS \
    -X main.Version=$pkgver \
    -X main.CommitSHA=$_commit" \
    -o build \
    .
}

check() {
  cd skate

  go test -v ./...
}

package() {
  cd skate

  install -vDm755 -t "$pkgdir/usr/bin" build/skate

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
