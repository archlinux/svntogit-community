# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jacek Poplawski <jacekpoplawski@gmail.com>

pkgname=rocksndiamonds
pkgver=4.3.0.0
pkgrel=2
pkgdesc="game similiar to Boulderdash with lots of levels"
arch=(x86_64)
url="https://www.artsoft.org/rocksndiamonds/"
license=('GPL')
depends=('sdl2' 'sdl2_image' 'sdl2_mixer' 'sdl2_net' 'smpeg')
makedepends=('unzip' 'p7zip')
source=(https://www.artsoft.org/RELEASES/unix/rocksndiamonds/rocksndiamonds-$pkgver.tar.gz
	rocksndiamonds.desktop)
sha256sums=('520626a883b2a1badfe9d775452b2c928c06631091f53cc2657e33b9b70f79af'
            '90008d356dd30034287ca763a05feb482d7e0c4508572abb08b5215fcb4fe922')

prepare() {
  cd "$srcdir"/rocksndiamonds-$pkgver
  sed -i 's|inline void InitElementSmallImagesScaledUp|void InitElementSmallImagesScaledUp|g' src/init.c
}

build() {
  cd "$srcdir"/rocksndiamonds-$pkgver
  make clean
  make RO_GAME_DIR=/usr/share/rocksndiamonds RW_GAME_DIR=/usr/share/rocksndiamonds
}

package() {
  depends+=('rocksndiamonds-data' 'rocksndiamonds-contrib')

  cd "$srcdir"/rocksndiamonds-$pkgver

  install -d "$pkgdir"/usr/bin
  cp "$srcdir"/rocksndiamonds-$pkgver/rocksndiamonds "$pkgdir"/usr/bin/rocksndiamonds

#  install -d "$pkgdir"/usr/share/man/man1
#  cp "$srcdir"/rocksndiamonds-$pkgver/rocksndiamonds.1 "$pkgdir"/usr/share/man/man1

  install -d "$pkgdir"/usr/share/doc/$pkgname/
  cp -a docs/elements "$pkgdir"/usr/share/doc/$pkgname/

  install -Dm0644 "$srcdir"/rocksndiamonds.desktop "$pkgdir"/usr/share/applications/rocksndiamonds.desktop
}
