# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-turbo
pkgver=0.0.4
pkgrel=1
pkgdesc='A daemon that helps to launch applications faster'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-turbo"
license=('GPL3')
depends=('qt5-base')
makedepends=('cmake' 'ninja')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-turbo/archive/$pkgver.tar.gz")
sha512sums=('e9ebf5f890dd854d95a00500fde09c90947ad74851597c03c61e37118a358246c55e21959fc2973ceb2dd2902c2b528095548087a08d06b33bc7bb19adb8b622')

prepare() {
  cd deepin-turbo-$pkgver
  sed -i 's/MODULE/SHARED/' src/launcherlib/CMakeLists.txt
  sed -i '/-ldeepin-turbo/d;/Qt5::Widgets/a\    deepin-turbo' src/booster-dtkwidget/CMakeLists.txt
}

build() {
  cd deepin-turbo-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  ninja
}

package() {
  cd deepin-turbo-$pkgver
  DESTDIR="$pkgdir" ninja install
}
