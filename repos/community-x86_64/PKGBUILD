# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
pkgname=zeromq
pkgver=4.0.6
pkgrel=1
pkgdesc="Fast messaging system built on sockets.  C and C++ bindings.  aka 0MQ, ZMQ."
arch=('i686' 'x86_64')
url="http://www.zeromq.org"
license=('LGPL')
depends=('gcc-libs' 'util-linux' 'libsodium')
makedepends=('python2')
options=('staticlibs')
source=(http://download.zeromq.org/$pkgname-$pkgver.tar.gz
        https://raw.githubusercontent.com/zeromq/cppzmq/ac705f6047/zmq.hpp)
md5sums=('d47dd09ed7ae6e7fd6f9a816d7f5fdf6'
         'c1388ca58d417610b6c00d66c81a93f2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure prefix=/usr --with-pgm --with-libsodium
  sed -i 's/python$/&2/' foreign/openpgm/build-staging/openpgm/pgm/{Makefile,version_generator.py}
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/zmq.hpp" "$pkgdir/usr/include/zmq.hpp"
}

