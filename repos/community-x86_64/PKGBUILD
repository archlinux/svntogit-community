# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark
pkgver=0.28.0.gfm.11
pkgrel=1
pkgdesc="GitHub's fork of cmark, a CommonMark parsing and rendering library and program in C"
arch=('i686' 'x86_64')
url="https://github.com/github/cmark"
license=('custom:BSD2')
depends=('glibc')
makedepends=('cmake' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/github/cmark/archive/$pkgver.tar.gz")
sha512sums=('5c67752fec5a0bad5c95691de680f36dfd4d35705b44f506669dfdcace338372613683268a6aaabc5b4a20153c22c1ae74a8757034a5aafead6aa13067cc2c1f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib ../cmark-$pkgver
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"

  install -D -m 644 "$srcdir"/cmark-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
