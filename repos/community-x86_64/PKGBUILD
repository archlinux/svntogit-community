# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-djvu
pkgver=0.2.9
pkgrel=2

pkgdesc="DjVu support for Zathura"
url="https://pwmt.org/projects/zathura-djvu/"
arch=('x86_64')
license=('custom')

depends=('djvulibre' 'zathura' 'desktop-file-utils')
makedepends=('meson' 'ninja')

source=(zathura-djvu-$pkgver.tar.gz::https://github.com/pwmt/zathura-djvu/archive/$pkgver.tar.gz)

sha256sums=('84fae6da42e7cdc2e49ed4cd4f0315ac09716e95358b9c7487803a2cff47dbca')

build(){
  cd zathura-djvu-$pkgver
  arch-meson build
  ninja -C build
}

package(){
  DESTDIR="$pkgdir" ninja -C zathura-djvu-$pkgver/build install
  install -Dm0644 zathura-djvu-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

