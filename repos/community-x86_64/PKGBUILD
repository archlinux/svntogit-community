# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filter-rspamd
pkgname=opensmtpd-${_pkg}
pkgver=0.1.7
pkgrel=3
pkgdesc="OpenSMTPD filter integration for Rspamd"
arch=(x86_64)
url=https://github.com/poolpOrg/filter-rspamd
license=(ISC)
depends=(glibc rspamd)
makedepends=(go)
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('7f098450a19b59c97d050c25ff4d2295c7d42b92e8d312867b9734e4f38f99833290e67402dd1f9bccd31ebd6245c6eb1461fbd8434d41dfb791498315b4ebf5')

build() {
  cd ${_pkg}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -v -ldflags "-linkmode external" -o ${_pkg}
}

package() {
  cd ${_pkg}-$pkgver
  install -D filter-rspamd -t "${pkgdir}"/usr/lib/smtpd/opensmtpd
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}/
}
