# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Pete Alexandrou <pete@ozmartians.com>

pkgname=vidcutter
pkgver=6.0.5.1
pkgrel=3
pkgdesc='Modern, simple to use, constantly evolving and hella fast media cutter + joiner with frame-accurate SmartCut technology'
arch=('x86_64')
url='https://github.com/ozmartian/vidcutter'
license=('GPL3')
depends=('ffmpeg' 'mediainfo' 'mpv' 'python-opengl' 'python-pyqt5' 'python-simplejson')
makedepends=('python-setuptools' 'cython')
source=("https://github.com/ozmartian/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-python310.patch::https://github.com/ozmartian/vidcutter/commit/85bf70698ed9.patch)
sha256sums=('c6374eaedb845f9e580e555dd12c1db1cf2ffa28d3756b08e2ae28c1a4cd4a96'
            'e7f9ff874ff18c0be25ac3f48c164678684ad9e66de623733db8bc250f538a32')

prepare() {
  cd $pkgname-$pkgver
  # https://bugs.archlinux.org/task/73064
  patch -Np1 -i ../$pkgname-python310.patch
  # remove mpv.c so it gets regenerated by current cython
  rm vidcutter/libs/pympv/mpv.c
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
