# Maintainer: Anatol Pomozov

pkgname=aws-c-http
pkgver=0.7.5
pkgrel=1
pkgdesc='C99 implementation of the HTTP/1.1 and HTTP/2 specifications'
arch=(x86_64)
url='https://github.com/awslabs/aws-c-http'
license=(Apache)
depends=(aws-c-common aws-c-compression aws-c-io)
makedepends=(cmake)
source=(aws-c-http-$pkgver.zip::https://github.com/awslabs/aws-c-http/archive/v$pkgver.zip)
sha256sums=('f3252905ecdbfaea297122c69d729807fc2e58bbb8b84de1d535642ab1f47301')

build() {
  cd aws-c-http-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd aws-c-http-$pkgver
  cmake --build build --target test
}

package() {
  cd aws-c-http-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
