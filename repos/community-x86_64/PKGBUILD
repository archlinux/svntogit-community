# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.8.0
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
url=https://github.com/lotabout/skim
license=('MIT')
depends=('ncurses')
makedepends=('rust')
source=("skim-$pkgver.tar.gz::https://static.crates.io/crates/skim/skim-$pkgver.crate")
sha512sums=('913ca17b460a8aec78bbd3c7732d8fe2a90f98c38cb6902e45d1b165f01c8cf94f33dde78f2ea4b6ea6fd98cc5c6cf26eea668efe46b981fac984e695abd9215')

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}

# vim:set ts=2 sw=2 et:
