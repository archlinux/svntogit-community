# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=1.22.9.0
pkgrel=1
pkgdesc="The command-line interface for Cabal and Hackage."
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('gmp' 'zlib' 'libffi')
makedepends=("ghc=7.10.3" "haskell-http" "haskell-mtl" "haskell-network" "haskell-network-uri"
             "haskell-random" "haskell-stm" "haskell-zlib")
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
md5sums=('460995d67a9dc65f04bd2a68eaccf6a5')

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
    -fnetwork-uri -f-old-directory
  runhaskell Setup build
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"

  install -Dm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 bash-completion/cabal \
    "${pkgdir}/usr/share/bash-completion/completions/cabal"
}
