# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kubeconform
pkgver="0.4.13"
pkgrel=2
pkgdesc="FAST Kubernetes manifests validator, with support for Custom Resources!"
url="https://github.com/yannh/kubeconform"
arch=("x86_64")
license=("Apache")
depends=('glibc')
makedepends=("go" "git")
source=("${pkgname}-${pkgver}::https://github.com/yannh/kubeconform/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('f68a1ebf4df8688f4076e1335ebfb39fc9cb5d2e7fd31d9c8f448241974e6e76b62eeb07f725eee2698ba35d18b117f09c7f693a9be483a0a41a1289d693b022')
b2sums=('629c31938db7372a48a5381221c8c1e2b70b0367fc27ae2d0eda5aff192e612a7dcfe6236561e1e8b6dd209092711fcd014db1983b93b438ab2d263fff382294')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}/cmd/kubeconform"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-w -s -linkmode=external" .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "cmd/kubeconform/kubeconform" "${pkgdir}/usr/bin/${pkgname}"
}
