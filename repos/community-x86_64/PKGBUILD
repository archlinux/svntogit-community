# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=git-smash
pkgver=0.0.2
pkgrel=1
pkgdesc='Smash staged changes into previous commits to support your Git workflow'
url='https://github.com/anthraxx/git-smash'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'gcc-libs')
makedepends=('cargo' 'scdoc')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig)
options=('!makeflags')
sha512sums=('60d9d408b97c14f624eb3b82f5d79005e2b23eb6a16f8801b495fd390dfceeb1093c8c6b529f791ae88ec2062b2b8893536637895cd0f26906a3b6527ce12f44'
            'SKIP')
b2sums=('015dc68f525cf0709496af439c5e0d3d6cec661f92bf5b6969e7f0d95240ef887ac0e94b085ad320e6cd9a0787e117cf7f9418858301c29f60d0017be5c0f863'
        'SKIP')
validpgpkeys=(
  'E240B57E2C4630BA768E2F26FC1B547C8D8172C8'  # Levente Polyak
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
