# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-lib
pkgname=lib32-alsa-lib
pkgver=1.2.5.1
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('lib32-glibc' "alsa-lib=${pkgver}")
provides=('libasound.so' 'libatopology.so')
source=("https://www.alsa-project.org/files/pub/lib/${_name}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('01998ffa449e925ff552c13aea47f9540903afdc533086067c78dcaba4d239c347180d3d28bb0000e6d19b7779c7249fcc77a30057930ca22d18ba55e163fa1c'
            'SKIP')
b2sums=('5464cc7f263bdf7b10aa7e1f542ad6dbc55bf300156fd1bded00375c41a518d4b14f96e28eb1c60a6ce7cc6e27da4b2e8598cd867f15417f260fc23513f93e11'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -fiv
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --without-debug \
              --disable-python
  make
}

check() {
  cd "${_name}-$pkgver"
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
