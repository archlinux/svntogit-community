# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.3.1
pkgrel=6
pkgdesc='A Sony PlayStation 2 emulator'
arch=('i686' 'x86_64')
url='http://www.pcsx2.net'
license=('GPL2' 'GPL3' 'LGPL2.1' 'LGPL3')
depends_i686=('glew' 'libaio' 'libcanberra' 'libjpeg-turbo' 'nvidia-cg-toolkit'
              'portaudio' 'sdl' 'soundtouch' 'wxgtk')
depends_x86_64=('lib32-glew' 'lib32-libaio' 'lib32-libcanberra'
                'lib32-libjpeg-turbo' 'lib32-nvidia-cg-toolkit'
                'lib32-portaudio' 'lib32-sdl' 'lib32-soundtouch' 'lib32-wxgtk')
makedepends=('cmake')
makedepends_x86_64=('gcc-multilib')
optdepends_x86_64=('lib32-gtk-engines: GTK2 engines support'
                   'lib32-gtk-engine-murrine: Murrine GTK3 engine support'
                   'lib32-gtk-engine-unico: Unico GTK2 engine support')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PCSX2/pcsx2/archive/v${pkgver}.tar.gz"
        '0001-common-fix-gcc-5.0-issue.patch')
sha256sums=('60ba9f7f85bf4337ca6962f9b437d549f16fc04ea0b6b261d7521e0416c2b8c9'
            'dc3bdfaea2e024da1d16dd6142030b13b3e2a4540ba1685399e3109d026866c4')

prepare() {
  cd pcsx2-${pkgver}

  # Fix startup crash with GCC 5.1
  # https://github.com/PCSX2/pcsx2/issues/560
  patch -Np1 -i ../0001-common-fix-gcc-5.0-issue.patch
}

build() {
  cd pcsx2-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  if [[ $CARCH == i686 ]]; then
    cmake .. \
      -DCMAKE_BUILD_TYPE='Release' \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DCMAKE_LIBRARY_PATH='/usr/lib' \
      -DPLUGIN_DIR='/usr/lib/pcsx2' \
      -DGAMEINDEX_DIR='/usr/share/pcsx2' \
      -DDISABLE_ADVANCE_SIMD='TRUE' \
      -DEXTRA_PLUGINS='TRUE' \
      -DREBUILD_SHADER='TRUE' \
      -DGLSL_API='TRUE' \
      -DPACKAGE_MODE='TRUE' \
      -DXDG_STD='TRUE'
  elif [[ $CARCH == x86_64 ]]; then
    cmake .. \
      -DCMAKE_BUILD_TYPE='Release' \
      -DCMAKE_TOOLCHAIN_FILE='cmake/linux-compiler-i386-multilib.cmake' \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DCMAKE_LIBRARY_PATH='/usr/lib32' \
      -DPLUGIN_DIR='/usr/lib32/pcsx2' \
      -DGAMEINDEX_DIR='/usr/share/pcsx2' \
      -DDISABLE_ADVANCE_SIMD='TRUE' \
      -DEXTRA_PLUGINS='TRUE' \
      -DREBUILD_SHADER='TRUE' \
      -DGLSL_API='TRUE' \
      -DPACKAGE_MODE='TRUE' \
      -DXDG_STD='TRUE'
  fi

  make
}

package() {
  cd pcsx2-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
