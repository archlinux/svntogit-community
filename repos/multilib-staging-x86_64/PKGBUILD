# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=libsamplerate
pkgname=lib32-$_pkgbasename
pkgver=0.1.9
pkgrel=2
pkgdesc="Secret Rabbit Code - aka Sample Rate Converter for audio (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/SRC/index.html"
license=('BSD')
depends=('lib32-libsndfile' $_pkgbasename)
makedepends=('gcc-multilib')
source=(http://www.mega-nerd.com/SRC/libsamplerate-${pkgver}.tar.gz)
sha1sums=('ed60f957a4ff87aa15cbb1f3dbd886fa7e5e9566')
validpgpkeys=('73571E85C19F4281D8C97AA86CA41A7743B8D6C8')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
