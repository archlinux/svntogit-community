# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.9.8
pkgrel=3
pkgdesc="XML parsing library, version 2 (32-bit)"
arch=(x86_64)
license=('custom')
depends=(lib32-zlib lib32-readline lib32-ncurses lib32-icu libxml2)
makedepends=(gcc-multilib git)
url="http://www.xmlsoft.org/"
_commit=18890f471c420411aa3c989e104d090966ec9dbf # tags/v2.9.8^0
source=(git+https://git.gnome.org/browse/libxml2#commit=$_commit)
md5sums=('SKIP')

+pkgver() {
  cd "${srcdir}/${_pkgbasename}"
  git describe --tags | sed 's/-rc/rc/;s/^v//;s/-/+/g'
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}"
  autoreconf -fi

  ./configure --prefix=/usr --with-threads --with-history --with-icu --libdir=/usr/lib32 --without-lzma
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool

  make
}

package() {
  cd "${srcdir}/${_pkgbasename}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
