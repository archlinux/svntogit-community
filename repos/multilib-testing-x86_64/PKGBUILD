
pkgname=lib32-pcre2
_pkgname=pcre2
pkgver=10.39
pkgrel=1
pkgdesc='A library that implements Perl 5-style regular expressions. 2nd version (32-bit)'
arch=('x86_64')
url='https://www.pcre.org/'
license=('BSD')
depends=('lib32-gcc-libs' 'lib32-readline' 'lib32-zlib' 'lib32-bzip2' 'bash')
source=("https://github.com/PhilipHazel/pcre2/releases/download/$_pkgname-$pkgver/$_pkgname-$pkgver.tar.bz2"{,.sig})
validpgpkeys=('45F68D54BBE23FB3039B46E59766E084FB0F43D8') # Philip Hazel <ph10@hermes.cam.ac.uk>
sha512sums=('b3d898198f4b5ffc3453d2ba56fe2a7298c01c52e5f67d45f1e046fc0dee62e16a4024fcb65839ac9c367beedb531647affd6f8599fbeb102f19423c150d80d4'
            'SKIP')

build() {
  cd $_pkgname-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-pcre2-16 \
    --enable-pcre2-32 \
    --enable-jit \
    --enable-pcre2grep-libz \
    --enable-pcre2grep-libbz2 \
    --enable-pcre2test-libreadline
  make
}

check() {
  cd $_pkgname-$pkgver

  make -j1 check
}

package() {
  cd $_pkgname-$pkgver
  
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{share,bin,include}

  install -Dm644 LICENCE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
