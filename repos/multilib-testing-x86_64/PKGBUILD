# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Janax <janax99@yahoo.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=lib32-pam
pkgver=1.3.0
pkgrel=2
pkgdesc='Pluggable Authentication Modules'
arch=('x86_64')
url='http://linux-pam.org'
license=('GPL2')
depends=('lib32-cracklib' 'lib32-libnsl' 'lib32-libtirpc' 'pam')
makedepends=('gcc-multilib' 'lib32-flex')
source=("http://linux-pam.org/library/Linux-PAM-${pkgver}.tar.bz2")
sha256sums=('241aed1ef522f66ed672719ecf2205ec513fd0075ed80cda8e086a5b1a01d1bb')
options=('!emptydirs')

build() {
  cd Linux-PAM-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --sbindir='/usr/bin' \
    --disable-db
  make
}

package() {
  cd Linux-PAM-${pkgver}

  make DESTDIR="${pkgdir}" SCONFIGDIR='/etc/security' install
  rm -rf "${pkgdir}"/{etc,usr/{include,share,bin}}
}

# vim: ts=2 sw=2 et:
