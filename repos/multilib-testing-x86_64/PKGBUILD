# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-dbus
pkgname=(
  lib32-dbus
)
pkgver=1.14.6
pkgrel=2
pkgdesc="Freedesktop.org message bus system - 32-bit"
url="https://wiki.freedesktop.org/www/Software/dbus/"
arch=(x86_64)
license=(
  GPL
  custom
)
depends=(
  dbus
  lib32-systemd
)
makedepends=(
  autoconf-archive
  git
  lib32-expat
  lib32-glib2
)
source=(
  "git+https://gitlab.freedesktop.org/dbus/dbus.git?signed#tag=dbus-$pkgver"
)
b2sums=('SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90') # Simon McVittie <simon.mcvittie@collabora.co.uk>

prepare() {
  cd dbus

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --libdir=/usr/lib32
    --libexecdir=/usr/lib/dbus-1.0
    --runstatedir=/run
    --with-console-auth-dir=/run/console/
    --with-dbus-user=dbus
    --with-system-pid-file=/run/dbus/pid
    --with-system-socket=/run/dbus/system_bus_socket
    --with-systemdsystemunitdir=/usr/lib/systemd/system
    --enable-inotify
    --disable-libaudit
    --enable-systemd
    --enable-user-session
    --disable-xml-docs
    --disable-doxygen-docs
    --disable-ducktype-docs
    --disable-static
    --without-x
  )

  export CC="gcc -m32 -mstackrealign"
  export CXX="g++ -m32 -mstackrealign"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cd dbus
  ./configure "${configure_options[@]}"
  make
}

check() {
  make -C dbus -j1 check
}

package_lib32-dbus() {
  depends+=(
    libsystemd.so
  )
  provides=(
    lib32-libdbus
    libdbus-1.so
  )
  conflicts=(lib32-libdbus)
  replaces=(lib32-libdbus)

  DESTDIR="$pkgdir" make -C dbus install

  rm -r "$pkgdir"/{etc,var}
  rm -r "$pkgdir"/usr/{bin,include,lib,share}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 dbus/COPYING
}

# vim:set sw=2 sts=-1 et:
