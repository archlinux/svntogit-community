# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lib32-libpsl
pkgver=0.21.1
pkgrel=2
pkgdesc='Public Suffix List library (32-bit)'
url='https://github.com/rockdaboot/libpsl'
arch=('x86_64')
license=('MIT')
depends=('libpsl' 'lib32-glibc' 'lib32-libidn2' 'lib32-libunistring')
makedepends=('git' 'libxslt' 'meson' 'ninja' 'python' 'publicsuffix-list')
provides=('libpsl.so')
_tag=a5e6f26cce82898c54a0caa6dda0ab924b806daf
source=(git+https://github.com/rockdaboot/libpsl.git#tag=${_tag})
sha512sums=('SKIP')

pkgver() {
  cd libpsl

  git describe --tags | sed 's/^V//'
}

prepare() {
  cd libpsl

  rm -frv list
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson libpsl build \
    --libdir=/usr/lib32 \
    -Dbuiltin=libidn2 \
    -Druntime=libidn2 \
    -Dpsl_file=/usr/share/publicsuffix/effective_tld_names.dat \
    -Dpsl_distfile=/usr/share/publicsuffix/public_suffix_list.dat \
    -Dpsl_testfile=/usr/share/publicsuffix/test_psl.txt
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libpsl "${pkgdir}"/usr/share/licenses/lib32-libpsl
}

# vim: ts=2 sw=2 et:
