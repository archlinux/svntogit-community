# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgbase=lib32-harfbuzz
pkgname=(lib32-harfbuzz lib32-harfbuzz-icu)
pkgver=5.3.1
pkgrel=3
pkgdesc="OpenType text shaping engine - 32-bit"
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(
  git
  harfbuzz
  lib32-freetype2
  lib32-glib2
  lib32-icu
  meson
  python
  ragel
)
checkdepends=(
  python-fonttools
  python-setuptools
)
options=(debug)
_commit=970321db7bddbe8c579b73751fc655a924ea3ce6  # tags/5.3.1^0
source=("git+https://github.com/harfbuzz/harfbuzz#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd harfbuzz
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd harfbuzz

  # https://github.com/harfbuzz/harfbuzz/issues/3850
  git cherry-pick -n b0b7a65388da25ae3fa01e969ad6abc67eed4f49
}

build() {
  local meson_options=(
    --libdir=/usr/lib32
    -D cairo=disabled
    -D chafa=disabled
    -D introspection=disabled
    -D docs=disabled
  )

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  # Harfbuzz wants no exceptions
  CFLAGS="${CFLAGS/-fexceptions/}"
  CXXFLAGS="${CXXFLAGS/-fexceptions/}"

  arch-meson harfbuzz build "${meson_options[@]}"
  meson compile -C build
}

check() {
  mkdir -p tmp
  TMPDIR="$PWD/tmp" meson test -C build --print-errorlogs
  rm -r tmp
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_lib32-harfbuzz() {
  depends=(
    harfbuzz
    lib32-freetype2
    lib32-glib2
    libfreetype.so
    libglib-2.0.so
    libgobject-2.0.so
  )
  provides=(libharfbuzz{,-subset,-gobject}.so)

  meson install -C build --destdir "$pkgdir"

  ( cd "$pkgdir"
    rm -r usr/{bin,include}

    _pick hb-icu usr/lib32/libharfbuzz-icu*
    _pick hb-icu usr/lib32/pkgconfig/harfbuzz-icu.pc
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

package_lib32-harfbuzz-icu() {
  pkgdesc+=" - ICU integration"
  depends=(
    "lib32-harfbuzz=$pkgver-$pkgrel"
    harfbuzz-icu
    libharfbuzz.so
    libicuuc.so
  )
  provides=(libharfbuzz-icu.so)

  mv hb-icu/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

# vim:set sw=2 sts=-1 et:
