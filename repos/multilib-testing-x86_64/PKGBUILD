# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-glib2
pkgver=2.58.3
pkgrel=1
pkgdesc="Low level core library (32-bit)"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL2.1)
arch=(x86_64)
depends=(lib32-pcre lib32-libffi lib32-util-linux lib32-zlib glib2)
makedepends=(gettext gtk-doc shared-mime-info python lib32-libelf git util-linux meson lib32-dbus)
checkdepends=(desktop-file-utils lib32-glib2)
options=(!emptydirs)
_commit=f2d1128ffae0de87680e411fb62ccfccafed6b1c  # tags/2.58.3^0
source=("git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
        gio-querymodules-32.hook)
sha256sums=('SKIP'
            '73b6791b58180e2a02fe908938e6c547ee1b263f8c387262f9a8c4105ffa999a')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd glib
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  arch-meson glib build \
    -D selinux=false \
    --libdir=/usr/lib32
  ninja -C build
}

check() {
  meson test -C build -t 2
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  rm -r "$pkgdir"/usr/{share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 et:
