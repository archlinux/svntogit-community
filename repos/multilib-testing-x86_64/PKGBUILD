# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

pkgname=lib32-nspr
pkgver=4.34
pkgrel=1
pkgdesc="Netscape Portable Runtime (32-bit)"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSPR"
arch=(x86_64)
license=(MPL GPL)
depends=(lib32-glibc nspr)
makedepends=(zip mercurial)
options=(debug)
_revision=e7a3ef720ee023dce074f57820a627c6c87a7bb5
source=("hg+https://hg.mozilla.org/projects/nspr#revision=$_revision")
sha256sums=('SKIP')

pkgver() {
  cd nspr
  hg id -t | sed 's/^NSPR_//;s/_RTM$//;s/_/./g'
}

prepare() {
  cd nspr
}

build() {
  cd nspr
  ./configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug \
      --disable-64bit
  make ${SOURCE_DATE_EPOCH:+
    SH_NOW="${SOURCE_DATE_EPOCH}000000"
    SH_DATE="$(date --utc --date="@$SOURCE_DATE_EPOCH" '+%Y-%m-%d %T')"
  }
}

package() {
  cd nspr
  make DESTDIR="$pkgdir" install
  ln -s nspr.pc "$pkgdir/usr/lib32/pkgconfig/mozilla-nspr.pc"

  rm -r "$pkgdir"/usr/{bin,share,include}
}

# vim:set sw=2 et:
