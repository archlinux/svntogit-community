# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib32-libxcrypt
pkgver=4.4.28
pkgrel=1
pkgdesc='Modern library for one-way hashing of passwords (32-bit)'
arch=('x86_64')
url='https://github.com/besser82/libxcrypt/'
license=('GPL')
depends=('lib32-glibc' 'libxcrypt')
makedepends=('gcc-multilib')
validpgpkeys=('678CE3FEE430311596DB8C16F52E98007594C21D') # Bj√∂rn 'besser82' Esser
source=("${url}/releases/download/v${pkgver}/libxcrypt-${pkgver}.tar.xz"{,.asc})
sha256sums=('9e936811f9fad11dbca33ca19bd97c55c52eb3ca15901f27ade046cc79e69e87'
            'SKIP')

build() {
  cd "${srcdir}/libxcrypt-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib32 \
    --disable-static \
    --enable-hashes=strong,glibc \
    --enable-obsolete-api=no \
    --disable-failure-tokens
  make 
}

package() {
  cd "${srcdir}/libxcrypt-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
