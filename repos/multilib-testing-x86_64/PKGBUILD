# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=lib32-xz
pkgver=5.4.2
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files (32-bit)'
arch=('x86_64')
url='https://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('lib32-glibc' 'xz')
provides=('liblzma.so')
validpgpkeys=('3690C240CE51B4670D30AD1C38EE757D69184620') # Lasse Collin <lasse.collin@tukaani.org>
source=("https://tukaani.org/xz/xz-${pkgver}.tar.gz"{,.sig}
        '0001-liblzma-Silence-Wsign-conversion-in-SSE2-code-in.patch')
sha256sums=('87947679abcf77cc509d8d1b474218fd16b72281e2797360e909deaee1ac9d05'
            'SKIP'
            '42c76fa7640fd47b611515b8ef047b861e9db0e1e078ee13c1b3417738508799')
sha512sums=('149f980338bea3d66de1ff5994b2b236ae1773135eda68b62b009df0c9dcdf5467f8cb2c06da95a71b6556d60bd3d21f475feced34d5dfdb80ee95416a2f9737'
            'SKIP'
            '6d65b7e4c1108d1133eaf563ac1a880411d7bac013668e56842cea17f1cdd9a4696130c0105706d35c161b3e09ca86c9f79da3bc20128d0404b162d4a56a9913')

prepare() {
  cd "${srcdir}/xz-${pkgver}"

  patch -Np1 < ../0001-liblzma-Silence-Wsign-conversion-in-SSE2-code-in.patch
}

build() {
  cd "${srcdir}/xz-${pkgver}"

  export CC='gcc -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-rpath \
    --enable-werror
  make
}

check() {
  cd "${srcdir}/xz-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/xz-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
  install -d -m0755 "${pkgdir}"/usr/share/licenses
  ln -s xz "${pkgdir}"/usr/share/licenses/lib32-xz
}
