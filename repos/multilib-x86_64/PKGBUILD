# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=lib32-mesa-amber
pkgdesc="classic OpenGL (non-Gallium3D) drivers (32-bit)"
pkgver=21.3.9
pkgrel=5
arch=('x86_64')
makedepends=('python-mako' 'lib32-libxml2' 'lib32-libx11' 'xorgproto' 'lib32-libdrm'
             'lib32-libxshmfence' 'lib32-libxxf86vm' 'lib32-libxdamage'
             'lib32-wayland' 'wayland-protocols' 'lib32-zstd' 'lib32-libelf'
             'lib32-libglvnd' 'lib32-libunwind' 'lib32-libxrandr' 'cmake' 'meson')
url="https://www.mesa3d.org/"
license=('custom')
source=(https://mesa.freedesktop.org/archive/mesa-${pkgver}.tar.xz{,.sig}
        LICENSE)
sha512sums=('4cec6f4f50f444fcd327f7c40f8899c2f265e348e121455262b540b1f890a1468bbea59965af0876c548fa97aa0a05a1b23fa6ca7d308bd60328cfdeab757684'
            'SKIP'
            'f9f0d0ccf166fe6cb684478b6f1e1ab1f2850431c06aa041738563eb1808a004e52cdec823c103c9e180f03ffc083e95974d291353f0220fe52ae6d4897fecc7')
#validpgpkeys=('57551DE15B968F6341C248F68D8E31AFC32428A6') # Eric Engestrom <eric@engestrom.ch>
validpgpkeys=('71C4B75620BC75708B4BDB254C95FAAB3EB073EC') # Dylan Baker <dylan@pnwbakers.com>

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"
  cat >crossfile.ini <<END
[binaries]
llvm-config = '/usr/bin/llvm-config32'
END

  # swr driver is broken with some cpu see FS#66972

  arch-meson mesa-$pkgver build \
    --native-file crossfile.ini \
    --libdir=/usr/lib32 \
    -D b_lto=true \
    -D b_ndebug=true \
    -D amber=true \
    -D platforms=auto \
    -D dri-drivers=i915,i965,r100,r200,nouveau \
    -D gallium-drivers=swrast \
    -D vulkan-drivers=auto \
    -D dri3=enabled \
    -D egl=enabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=auto \
    -D glx=dri \
    -D libunwind=enabled \
    -D llvm=disabled \
    -D lmsensors=disabled \
    -D osmesa=true \
    -D shared-glapi=enabled \
    -D microsoft-clc=disabled \
    -D valgrind=disabled

  # Print config
  meson configure build

  ninja -C build
  meson compile -C build
}

package() {
  depends=('lib32-libglvnd' 'mesa-amber')
  conflicts=('lib32-mesa')
  provides=("lib32-mesa=${pkgver}" 'lib32-opengl-driver')

  DESTDIR="${pkgdir}" meson install -C build

  rm -rv "${pkgdir}"/usr/share/drirc.d
  rm -rv "${pkgdir}"/usr/share/glvnd
  rm -rv "${pkgdir}"/usr/include

  # indirect rendering
  ln -s /usr/lib32/libGLX_amber.so.0 "${pkgdir}/usr/lib32/libGLX_indirect.so.0"

  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}
