# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens <jan.steffens@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>

pkgbase=lib32-libxkbcommon
pkgname=(
  lib32-libxkbcommon
  lib32-libxkbcommon-x11
)
pkgver=0.10.0
pkgrel=1
url=https://xkbcommon.org/
arch=(x86_64)
license=(custom)
makedepends=(
  git
  lib32-libxcb
  lib32-wayland
  meson
  wayland-protocols
  xorg-util-macros
)
checkdepends=(
  xorg-server-xvfb
  libgl
)
source=(git+https://github.com/xkbcommon/libxkbcommon#tag=e3c3420a7146f4ea6225d6fb417baa05a79c8202)
sha256sums=('SKIP')

pkgver() {
  cd libxkbcommon

  git describe --tags | sed 's/^xkbcommon-//'
}

prepare() {
  mkdir -p build x11/usr/lib32/pkgconfig
}

build() {
  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson libxkbcommon build \
    --libdir=/usr/lib32 \
    -D enable-docs=false
  ninja -C build
}

check() {
  xvfb-run -a meson test -C build --print-errorlogs
}

package_lib32-libxkbcommon() {
  pkgdesc='Keymap handling library for toolkits and window systems'
  depends=(
    lib32-glibc
    libxkbcommon
  )

  DESTDIR="${pkgdir}" meson install -C build
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon

  mv "${pkgdir}"/usr/lib32/*x11* x11/usr/lib32
  mv "${pkgdir}"/usr/lib32/pkgconfig/*x11* x11/usr/lib32/pkgconfig
}

package_lib32-libxkbcommon-x11() {
  pkgdesc='Keyboard handling library using XKB data for X11 XCB clients'
  depends=(
    lib32-libxcb
    lib32-libxkbcommon
    libxkbcommon-x11
  )

  mv x11/* "${pkgdir}"/
  find "${pkgdir}" -type d -exec chmod 755 {} +

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libxkbcommon-x11 "${pkgdir}"/usr/share/licenses/lib32-libxkbcommon-x11
}

# vim: ts=2 sw=2 et:
