# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Maximilian Stein <theoddbird@posteo.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libsoup
pkgver=2.68.2
pkgrel=1
pkgdesc='HTTP client/server library for GNOME'
arch=(x86_64)
url=https://wiki.gnome.org/Projects/libsoup
license=(LGPL)
depends=(
  lib32-brotli
  lib32-glib-networking
  lib32-krb5
  lib32-libxml2
  lib32-sqlite
  lib32-libpsl
  libsoup
)
makedepends=(
  git
  meson
  samba
)
optdepends=('samba: Windows Domain SSO')
source=(git+https://gitlab.gnome.org/GNOME/libsoup.git#tag=8588e449fbc31ceffdf278be79163d319b9cefc8)
sha256sums=(SKIP)

pkgver() {
  cd libsoup

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson libsoup build \
    --libdir=/usr/lib32 \
    -D introspection=disabled \
    -D vapi=disabled
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
