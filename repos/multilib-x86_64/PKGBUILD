# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.15.12
pkgrel=1
pkgdesc="2D graphics library with support for multiple output devices (32-bit)"
url="https://cairographics.org/"
arch=(x86_64)
license=(LGPL MPL)
depends=(lib32-libpng lib32-libxrender lib32-libxext lib32-fontconfig lib32-pixman lib32-glib2 $_pkgbasename)
makedepends=(lib32-librsvg lib32-gtk2 gtk-doc valgrind git)
_commit=7149686456ec3c481fa1d3dbe76a0dab1e42b519  # tags/1.15.12^0
source=("git+https://anongit.freedesktop.org/git/cairo#commit=$_commit"
        utf-8.diff)
sha1sums=('SKIP'
          '3f680dea8533bcce4ca2af5d25c546235ef7b339')

pkgver() {
  cd $_pkgbasename
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgbasename

  # Fixup tag for pkgver()
  git tag -f 1.15.10 5e6b9aeb5e3402ff17ae8ee8548ae2341c2f7d59

  # Fix non-UTF-8 sources giving gtk-doc trouble
  patch -Np1 -i ../utf-8.diff

  # Update gtk-doc
  cp /usr/share/aclocal/gtk-doc.m4 build/aclocal.gtk-doc.m4
  cp /usr/share/gtk-doc/data/gtk-doc.make build/Makefile.am.gtk-doc

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename
  ./configure --prefix=/usr \
        --libdir=/usr/lib32 \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-gl \
        --enable-tee \
        --enable-svg \
        --enable-ps \
        --enable-pdf \
        --enable-gobject \
        --disable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $_pkgbasename
  # https://people.gnome.org/~federico/blog/helping-cairo.html
  #make check
}

package() {
  cd $_pkgbasename
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share,bin}
}
