# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxdamage
pkgname=lib32-$_pkgbasename
pkgver=1.1.4
pkgrel=3
pkgdesc="X11 damaged region extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes>=4.0.4' $_pkgbasename)
makedepends=('pkg-config' 'xorgproto' gcc-multilib)
source=(${url}/releases/individual/lib/libXdamage-${pkgver}.tar.bz2)
sha256sums=('7c3fe7c657e83547f4822bfde30a90d84524efb56365448768409b77f05355ad')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXdamage-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXdamage-${pkgver}"

  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
