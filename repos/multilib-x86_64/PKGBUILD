# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: GordonGR <ntheo1979@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-rest
pkgver=0.7.93
pkgrel=1
pkgdesc='A helper library for RESTful services'
arch=('x86_64')
url='http://www.gtk.org/'
license=('GPL2')
depends=('lib32-libsoup' 'rest')
makedepends=('gcc-multilib' 'gobject-introspection')
source=("http://download.gnome.org/sources/rest/${pkgver%.*}/rest-${pkgver}.tar.xz")
sha256sums=('c710644455340a44ddc005c645c466f05c0d779993138ea21a62c6082108b216')

build() {
  cd rest-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make
}

package() {
  cd rest-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
