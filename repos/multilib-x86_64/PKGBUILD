# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-plugins
pkgname=lib32-alsa-plugins
pkgver=1.2.5
pkgrel=1
pkgdesc="Additional ALSA plugins (32-bit)"
arch=(x86_64)
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=("alsa-plugins=1:${pkgver}" 'lib32-alsa-lib' 'lib32-glibc')
makedepends=('lib32-dbus' 'lib32-jack' 'lib32-libavtp' 'lib32-libpulse'
'lib32-libsamplerate' 'lib32-speexdsp')
optdepends=('lib32-dbus: for maemo plugin'
            #'lib32-ffmpeg: for pcm_a52 and rate_lavrate plugins'
            'lib32-jack: for pcm_jack plugin'
            'lib32-libavtp: for pcm_aaf plugin'
            'lib32-libsamplerate: for rate_samplerate plugin'
            'lib32-libpulse: for conf_pulse, ctl_pulse and pcm_pulse plugins'
            'lib32-speexdsp: for pcm_speex and rate_speexrate plugins')
source=("https://www.alsa-project.org/files/pub/plugins/${_name}-$pkgver.tar.bz2")
sha512sums=('32aa475ec4af040861c2bfb01eac71042242d2109d3c5102d0111f3327e3f6c9031c32cbb50eb4b6568a6bd9408691a047a39972f09d6087e31ec11f19ddc9cf')
b2sums=('a1bb2f187c92a2999722e22775baf2c1fc4240162bb71f454c60359ef61b46934ff8ecd5af97589bafe2c7f737530cf5a4dcc139af7ed001d8dfe24bf3ae045c')

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -fvi
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --enable-maemo-plugin
  make
}

package() {
  depends+=(libasound.so)

  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/share}
}
# vim:set sw=2 et:
