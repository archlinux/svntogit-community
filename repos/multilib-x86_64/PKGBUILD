# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libsamplerate
pkgname=lib32-libsamplerate
pkgver=0.2.2
pkgrel=2
pkgdesc="An audio sample rate conversion library"
arch=(x86_64)
url="https://libsndfile.github.io/libsamplerate/"
license=(BSD)
depends=(
  lib32-gcc-libs
  lib32-glibc
  "$_name=$pkgver"
)
makedepends=(
  cmake
  lib32-alsa-lib
  lib32-libsndfile
  lib32-opus
)
# TODO: package lib32-fftw for tests
# checkdepends=(lib32-fftw)
provides=(libsamplerate.so)
source=(https://github.com/libsndfile/$_name/releases/download/$pkgver/$_name-$pkgver.tar.xz{,.sig})
sha512sums=('d23ae54d23209ba22baae9e5fd178dd8e0e99205dada7e7c3a7b3a3d8cf816ed427a411bfeb008427f64da7767d645edce40811f238af11c8c386f5ef25a9f0c'
            'SKIP')
b2sums=('71b1e8a1644a94fff019abc6bf98c557df84994293f1e9bc651ad3ad3f0afeb24f54b8c75fc26e5202ae7c3a3e34e783fd1be7f7c6fdefe9eac9a5dfe7100b91'
        'SKIP')
validpgpkeys=('31D95CAB6D80D262244A1750A47620E801E47E95')  # David Seifert soap@gentoo.org

build() {
  local cmake_options=(
    -B build
    -D BUILD_SHARED_LIBS=ON
    -D CMAKE_BUILD_TYPE=None
    -D CMAKE_INSTALL_LIBDIR=lib32
    -D CMAKE_INSTALL_PREFIX=/usr
    -S $_name-$pkgver
    -W no-dev
  )
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

# check() {
#   ctest --test-dir build --output-on-failure
# }

package() {
  DESTDIR="$pkgdir" cmake --install build
  # remove everything that is provided by libsamplerate
  rm -rfv "$pkgdir"/usr/{include,share,bin}
  install -vDm 644 $_name-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
