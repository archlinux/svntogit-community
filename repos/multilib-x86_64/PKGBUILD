# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.5.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-libffi' 'lib32-expat' "${pkgname#*-}")
source=("http://wayland.freedesktop.org/releases/wayland-${pkgver}.tar.xz")
sha256sums=('0069e1e9af888b3e05384380ad8cc6c976ea3e81d08ba19b7675ce1d693a41b5')

build() {
  cd ${pkgname#*-}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-{documentation,static}
  make
}

package() {
  cd ${pkgname#*-}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${pkgname#*-} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
