# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: carstene1ns <arch.carsten@teibes.de>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=lib32-sdl_net
pkgver=1.2.8
pkgrel=3
pkgdesc='Small cross-platform networking library'
url='https://www.libsdl.org/projects/SDL_net/'
arch=(x86_64)
license=(zlib)
depends=(lib32-sdl)
makedepends=(gcc-multilib)
source=("https://www.libsdl.org/projects/SDL_net/release/SDL_net-$pkgver.tar.gz")
b2sums=('656fb9c86949bc962656edc7b950e64eee9b9b1ce1b257e6c4974c2d9be99714128c5556d3a3b7ea92cc6d294846af7a51a1925d17418afc8409e44a7b71099a')

build() {
  cd SDL_net-$pkgver
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  ./configure --disable-static --libdir=/usr/lib32 --prefix=/usr
  make
}

package() {
  cd SDL_net-$pkgver
  make DESTDIR="$pkgdir" install
  rm -frv "$pkgdir/usr/include"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
