# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-soundtouch
pkgver=1.9.0
pkgrel=2
pkgdesc='An audio processing library'
arch=('x86_64')
url='http://www.surina.net/soundtouch/'
license=('LGPL')
depends=('lib32-gcc-libs' "${pkgname#*-}")
makedepends=('gcc-multilib')
source=("http://www.surina.net/soundtouch/soundtouch-${pkgver}.tar.gz")
sha256sums=('1fe4fc03cbfe696c1281fd623d089baffe1b70265acc60eac7a6d9b5742971ad')

build() {
  cd soundtouch

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./bootstrap
  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --enable-shared
  make
}

package() {
  cd soundtouch

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,doc,include,share}
}

# vim: ts=2 sw=2 et:
