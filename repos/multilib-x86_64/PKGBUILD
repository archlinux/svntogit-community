# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: farwayer <farwayer [at] gmail [dot] com>

pkgbase=lib32-opus
pkgname=(
  lib32-opus
)
pkgver=1.4
pkgrel=1
pkgdesc="Totally open, royalty-free, highly versatile audio codec (32-bit)"
url="https://www.opus-codec.org/"
arch=(x86_64)
license=(BSD)
depends=(
  lib32-glibc
  opus
)
makedepends=(
  git
  meson
)
_commit=82ac57d9f1aaf575800cf17373348e45b7ce6c0d  # tags/v1.4^0
source=("git+https://github.com/xiph/opus#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd opus
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd opus
}

build() {
  local meson_options=(
    --libdir=/usr/lib32
    -D asm=disabled
    -D custom-modes=true
    -D docs=disabled
  )

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  arch-meson opus build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_lib32-opus() {
  provides=(libopus.so)

  meson install -C build --destdir "$pkgdir"
  rm -r "$pkgdir/usr/include"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 opus/COPYING
}

# vim:set sw=2 sts=-1 et:
