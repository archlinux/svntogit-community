# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Sébastien Luttringer <sеblu@аrchlіnux․org>
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=lib32-wayland
pkgver=1.16.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='https://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-glibc' 'lib32-libffi' 'lib32-expat' 'lib32-libxml2' 'wayland')
makedepends=('git')
source=("git+https://gitlab.freedesktop.org/wayland/wayland.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd wayland

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd wayland

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-documentation \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make DESTDIR="${pkgdir}" -C wayland install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s wayland "${pkgdir}"/usr/share/licenses/lib32-wayland
}

# vim: ts=2 sw=2 et:
