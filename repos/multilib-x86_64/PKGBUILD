# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cmocka
pkgname=lib32-cmocka
pkgver=1.1.5
pkgrel=2
pkgdesc='Elegant unit testing framework for C with support for mock objects'
url='https://cmocka.org/'
arch=(x86_64)
license=(Apache)
depends=(
  cmocka
  lib32-glibc
)
makedepends=(cmake)
provides=(libcmocka.so)
options=(!lto)
source=(https://cmocka.org/files/1.1/$_name-$pkgver.tar.xz{,.asc})
sha512sums=('cad7f04757183d004f6eaad39036fc0e24c5e0e987f80e85bc43bc66dba22389cb02b08e25531cc28a541d0a24a86b29be134a2d6fc339128e87d66952f502bd'
            'SKIP')
b2sums=('5625dc293ad481c654848f3de806ef88d1b651192c2461e52b54bf9d443b5e2ea5d2a685ab85b044685f57e5723ade6c4c17e38624f59c4eab240cb4feeaab1e'
        'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_LIBDIR=lib32
    -D CMAKE_INSTALL_PREFIX=/usr
    -D UNIT_TESTING=ON
    -S $_name-$pkgver
    -W no-dev
  )

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -rvf "$pkgdir/usr/include"
}
# vim: ts=2 sw=2 et:
