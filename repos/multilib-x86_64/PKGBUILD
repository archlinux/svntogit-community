# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lubosz Sarnecki <lubosz0@gmail.com>

pkgname=lib32-lm_sensors
pkgver=3.4.0
_ver=${pkgver//\./-}
pkgrel=1
pkgdesc="Collection of user space tools for general SMBus access and hardware monitoring (32-bit)"
arch=('x86_64')
url="http://www.lm-sensors.org/"
license=('GPL' 'LGPL')
depends=('lm_sensors' 'lib32-glibc')
source=(lm_sensors-${pkgver}::https://github.com/groeck/lm-sensors/archive/V${pkgver//\./-}.tar.gz
	lm_sensors-fancontrol.patch)
sha1sums=('4a9026e4db894c98ee7cea0bec1188108e415f71'
          'b0bc977348610d6a008d75a43f65800251c4c9f7')

prepare() {
  cd lm-sensors-${_ver}

  sed -i 's|/etc/sysconfig|/etc/conf.d|' prog/{detect/sensors-detect,init/{sensord,lm_sensors}.service}
  sed -i 's/EnvironmentFile=/EnvironmentFile=-/' prog/init/lm_sensors.service
  sed -i 's/CC := gcc/CC := gcc -m32/' Makefile

  patch -p0 -i "${srcdir}/lm_sensors-fancontrol.patch"
}

build() {
  cd lm-sensors-${_ver}

  make PREFIX=/usr
}

package() {
  cd lm-sensors-${_ver}
  
  make PREFIX=/usr LIBDIR=/usr/lib32 DESTDIR="${pkgdir}" install 
  
  rm -r ${pkgdir}/etc/
  rm -r ${pkgdir}/usr/{bin,sbin,include,man}
}

