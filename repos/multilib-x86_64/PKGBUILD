# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-librsvg
pkgver=2.54.4
pkgrel=1
epoch=2
pkgdesc="SVG rendering library (32-bit)"
url="https://wiki.gnome.org/Projects/LibRsvg"
arch=(x86_64)
license=(LGPL)
depends=(lib32-cairo lib32-freetype2 lib32-gdk-pixbuf2 lib32-glib2
         lib32-harfbuzz lib32-libxml2 lib32-pango librsvg)
makedepends=(gobject-introspection git rust lib32-rust-libs)
checkdepends=(ttf-dejavu)
options=(debug)
_commit=6539eef7be69c3f7089489c9a8f39d085e677bad  # tags/2.54.4^0
source=("git+https://gitlab.gnome.org/GNOME/librsvg.git#commit=$_commit"
        multilib.diff)
sha256sums=('SKIP'
            '2bf8570948e0ff3da4c26a8c5d600bd3a66f26bc81abcaeb437df28c01aa9a2a')

pkgver() {
  cd librsvg
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd librsvg

  # Hack to remove docs and cross-compile rust only
  git apply -3 ../multilib.diff

  NOCONFIGURE=1 ./autogen.sh
}

# Use LTO
export CARGO_PROFILE_RELEASE_LTO=true CARGO_PROFILE_RELEASE_CODEGEN_UNITS=1

# Use debug
export CARGO_PROFILE_RELEASE_DEBUG=2

build() {
  cd librsvg

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config
  export RUST_TARGET=i686-unknown-linux-gnu

  ./configure --prefix=/usr --disable-static --disable-vala \
    --disable-introspection --libdir=/usr/lib32
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

check() {
  # Test suite is very dependent on the versions of
  # Cairo, Pango, FreeType and HarfBuzz
  make -C librsvg check || :
}

package() {
  provides=(librsvg-${pkgver%%.*}.so)

  make -C librsvg DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,include,share}
}

# vim:set sw=2 et:
