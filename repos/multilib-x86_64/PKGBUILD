# Maintainer: Florian Pritz <bluewind@xinu.at>

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=0.9.14
pkgrel=1
pkgdesc="OpenType text shaping engine. (32-bit)"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('lib32-icu' 'lib32-glib2' 'lib32-freetype2' $_pkgbasename)
makedepends=('gcc-multilib')
options=(!libtool)
source=(http://www.freedesktop.org/software/harfbuzz/release/${_pkgbasename}-${pkgver}.tar.bz2
        remove-graphite.patch)
sha256sums=('d07c0ffdbbbfdfbb6c65e73fe9c76466e87dbf04b094cbd0abf5fd7d571a4004'
            'bf89c8387b94638cd8f371931f8101a7c32b744023df252e1a0b67f4e82e9df4')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  patch -p0 -i "$srcdir/remove-graphite.patch"

  # work around autogen.sh requiring ragel
  autoreconf --force --install --verbose
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-silent-rules
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
