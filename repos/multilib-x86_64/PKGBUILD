# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libdrm
pkgname=lib32-$_pkgbasename
pkgver=2.4.112
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
url="https://dri.freedesktop.org/"
arch=(x86_64)
license=('custom')
depends=('lib32-libpciaccess' $_pkgbasename)
makedepends=('valgrind' 'meson')
checkdepends=('lib32-cairo')
source=(https://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.xz{,.sig})
sha512sums=('df8a207d9cce220ee2cf67877eda65394733fbfd0ff85fdbadb2b20ba9bacc41421b32452506c8caecddaf8361564ca68cd6c74d4faf3b1569ab30a3243e4340'
            'SKIP')
#validpgpkeys=('10A6D91DA1B05BD29F6DEBAC0C74F35979C486BE') # David Airlie <airlied@redhat.com>
#validpgpkeys=('2617F6F8DEBB0A7A15C405C983FD600E340FB5FF') # Samuel Pitoiset <samuel.pitoiset@gmail.com>
validpgpkeys=('34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48') # emersion <contact@emersion.fr>

prepare() {
  cd $_pkgbasename-$pkgver

}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  arch-meson $_pkgbasename-$pkgver build \
    --libdir=/usr/lib32 \
    -Dudev=false \
    -Dvalgrind=false
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
