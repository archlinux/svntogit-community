# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

_pkgbasename=attr
pkgname=lib32-$_pkgbasename
pkgver=2.4.47
pkgrel=2
pkgdesc="Extended attribute support library for ACL support (32-bit)"
arch=(x86_64)
url="https://savannah.nongnu.org/projects/attr"
license=('LGPL')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib' 'gettext')
source=(https://download.savannah.gnu.org/releases/attr/attr-${pkgver}.src.tar.gz{,.sig})
sha256sums=('25772f653ac5b2e3ceeb89df50e4688891e21f723c460636548971652af0a859'
            'SKIP')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34') # Brandon Philips <brandon@ifup.co>

build() {
  cd "${srcdir}"/attr-${pkgver}

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export INSTALL_USER=root INSTALL_GROUP=root
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}"/attr-${pkgver}

  make DIST_ROOT="${pkgdir}" install-lib install-dev

  # tidy up
  rm -f "$pkgdir"/usr/lib32/libattr.a
  chmod 0755 "$pkgdir"/usr/lib32/libattr.so.*.*.*

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
