# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

_pkgbasename=libxshmfence
pkgname=lib32-$_pkgbasename
pkgver=1.2
pkgrel=1
pkgdesc="a library that exposes a event API on top of Linux futexes (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org/"
license=('GPL')
#groups=()
depends=('lib32-glibc' ${_pkgbasename})
makedepends=('xorg-util-macros' 'xproto' 'gcc-multilib')
source=(${url}/releases/individual/lib/${_pkgbasename}-${pkgver}.tar.bz2{,.sig})
sha256sums=('d21b2d1fd78c1efbe1f2c16dae1cb23f8fd231dcf891465b8debe636a9054b0c'
            'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # Keith Packard <keithp@keithp.com>

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  # fix some libtools errors
  autoreconf -vfi
	
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd $_pkgbasename-$pkgver
  make -k check
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir/" install

  rm -r ${pkgdir}/usr/include
	
  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}

