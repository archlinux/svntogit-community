# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-soundtouch
pkgver=1.8.0
pkgrel=1
pkgdesc='An audio processing library'
arch=('x86_64')
url='http://www.surina.net/soundtouch/'
license=('LGPL')
depends=('lib32-gcc-libs' "${pkgname#*-}")
makedepends=('gcc-multilib')
source=("${url}/${pkgname#*-}-${pkgver}.tar.gz")
sha256sums=('3d4161d74ca25c5a98c69dbb8ea10fd2be409ba1a3a0bf81db407c4c261f166b')

build() {
  cd ${pkgname#*-}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./bootstrap
  ./configure --prefix='/usr' --libdir='/usr/lib32' --enable-shared --with-pic
  make
}

package() {
  cd ${pkgname#*-}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,doc,include,share}
}

# vim: ts=2 sw=2 et:
