# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgbase=lib32-mesa-demos
pkgname=('lib32-mesa-demos' 'lib32-mesa-utils')
pkgver=9.0.0
pkgrel=1
arch=(x86_64)
url="http://mesa3d.sourceforge.net"
license=('custom')
makedepends=('lib32-mesa' 'lib32-freetype2' 'lib32-glew' 'lib32-freeglut' 'meson' 'wayland-protocols' 'lib32-libxkbcommon-x11'
             'lib32-libdecor' 'glslang' 'vulkan-headers')
source=(https://mesa.freedesktop.org/archive/demos/mesa-demos-${pkgver}.tar.xz{,.sig}
        'LICENSE')
sha512sums=('af33ef72a521416e39d93370b2b4ccb768f08084c9e4c0aa62868210d9465c858e5cb8e5d23952295a3073946f609eb8723ee60b39dd9fb6696c4e45aafbb2c1'
            'SKIP'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2')
validpgpkeys=('E390B9700582FAEA959ACAD41EEF53D38A3A9C67') # "Andreas Boll <andreas.boll.dev@gmail.com>"
validpgpkeys+=('FF4CF638C624C3CF21ED2CF227EF763A2AA39F96') # Erik Faye-Lund <erik.faye-lund@collabora.com>

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson mesa-demos-$pkgver build \
    -D gles1=disabled \
    -D with-system-data-files=true

  # Print config
  meson configure build

  meson compile -C build
}

package_lib32-mesa-demos() {
  pkgdesc="Mesa demos (32-bit)"
  depends=('lib32-libgl' 'lib32-glew' 'lib32-freeglut' 'mesa-demos' 'lib32-libdecor' 'lib32-libxkbcommon-x11')
  install=mesa-demos.install

  DESTDIR="$pkgdir" meson install -C build

  # remove utils
  rm "${pkgdir}"/usr/bin/{glxinfo,glxgears,eglinfo}

  local i
  for i in "$pkgdir/usr/bin/"*; do
    mv "$i" "$i"32
  done

  rm -r "$pkgdir/usr/share/mesa-demos"

  install -Dm 0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_lib32-mesa-utils() {
  pkgdesc="Essential Mesa utilities (32-bit)"
  depends=('lib32-libgl')
  provides=('glxinfo32' 'glxgears32' 'eglinfo32')

  cd build
  install -Dm 0755 src/egl/opengl/eglinfo -t "${pkgdir}/usr/bin/"
  install -Dm 0755 src/xdemos/{glxinfo,glxgears} -t "${pkgdir}/usr/bin/"

  local i
  for i in "$pkgdir/usr/bin/"*; do
    mv "$i" "$i"32
  done

  install -Dm 0644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
