# $Id$

_pkgbasename=libogg
pkgname=lib32-$_pkgbasename
pkgver=1.2.0
pkgrel=4
pkgdesc="Library for the Ogg media container (32-bit)"
arch=('x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://downloads.xiph.org/releases/ogg/$_pkgbasename-$pkgver.tar.gz)
md5sums=('c95b73759acfc30712beef6ce4e88efa')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd $srcdir/${_pkgbasename}-${pkgver}
    ./configure --prefix=/usr --libdir=/usr/lib32
    make
}

package() {
    cd $srcdir/${_pkgbasename}-${pkgver}

    make DESTDIR=$pkgdir install

    rm -rf "${pkgdir}"/usr/{include,share,bin}
    mkdir -p "$pkgdir/usr/share/licenses"
    ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
