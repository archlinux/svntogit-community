# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

_pkgbasename=libva-intel-driver
pkgname=lib32-libva-intel-driver
pkgver=1.8.3
pkgrel=1
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family (32-bit)'
arch=('x86_64')
url='https://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('lib32-libva')
source=("https://github.com/01org/intel-vaapi-driver/releases/download/$pkgver/intel-vaapi-driver-$pkgver.tar.bz2")
sha512sums=('e1d7da8bc4a6fd2e5ea9794b3c7d169c502248f4aafcbdc422cd47f0f53b8ef05d0fa9d0d00117e02b9815578d07d1b4fafa50ddd4e6e44dd33c73eeb6a3e45b')

prepare() {
  cd intel-vaapi-driver-${pkgver}

  # Only relevant if intel-gpu-tools is installed,
  # since then the shaders will be recompiled
  sed -i '1s/python$/&2/' src/shaders/gpp.py
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd intel-vaapi-driver-${pkgver}

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make 
}

package() {
  cd intel-vaapi-driver-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/COPYING
}
