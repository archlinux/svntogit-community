# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_basename=gst-plugins-good
pkgname=lib32-gst-plugins-good
pkgver=1.14.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins (32-bit)"
url="https://gstreamer.freedesktop.org/"
arch=('x86_64')
license=('LGPL')
depends=(lib32-libpulse lib32-jack lib32-libsoup lib32-gst-plugins-base-libs lib32-wavpack
         lib32-aalib lib32-taglib lib32-libdv lib32-libshout lib32-libvpx lib32-gdk-pixbuf2
         lib32-libcaca lib32-libavc1394 lib32-libiec61883 lib32-libxdamage lib32-v4l-utils
         lib32-cairo lib32-libgudev lib32-speex lib32-flac lib32-libraw1394 lib32-mpg123
         lib32-twolame gst-plugins-good)
makedepends=(lib32-gstreamer aalib python autoconf-archive git)
checkdepends=(lib32-gst-plugins-base)
options=(!emptydirs)
_commit=4733e9715675d2dc52ed00e58d52388c56411cd2 # tags/1.14.2^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-plugins-good#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common")
sha512sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_basename
  git describe --tags | sed 's/-//g'
}

prepare() {
  cd $_basename

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_basename

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --build=i686-pc-linux-gnu \
    --libdir=/usr/lib32 \
    --with-package-name="GStreamer Good Plugins (Arch Linux)" \
    --with-package-origin="https://www.archlinux.org/" \
    --enable-experimental \
    --disable-static \
    --disable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# FAIL: elements/rtp-payloading
#check() {
# cd $_basename
# make check
#}

package() {
  cd $_basename
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir"/usr/share
}
