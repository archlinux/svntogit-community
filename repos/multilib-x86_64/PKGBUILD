# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxcomposite
pkgname=lib32-$_pkgbasename
pkgver=0.4.6
pkgrel=1
pkgdesc="X11 Composite extension library (32-bit)"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes' $_pkgbasename)
makedepends=('xorg-util-macros' gcc-multilib)
source=(${url}/releases/individual/lib/libXcomposite-${pkgver}.tar.xz)
sha512sums=('f35a0da8f9f22fd788eee01209557fe3a1b1abdc9744f76330ba42b21b6e3af5464ca4451c33415856417c30da5aa5624e3707980c644327249bfeda5a62a59b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXcomposite-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXcomposite-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
