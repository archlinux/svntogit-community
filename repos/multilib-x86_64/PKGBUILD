# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cmocka
pkgname=lib32-cmocka
pkgver=1.1.7
pkgrel=1
pkgdesc='Elegant unit testing framework for C with support for mock objects'
url='https://cmocka.org/'
arch=(x86_64)
license=(Apache)
depends=(
  cmocka
  lib32-glibc
)
makedepends=(cmake)
provides=(libcmocka.so)
options=(!lto)
source=(https://cmocka.org/files/1.1/$_name-$pkgver.tar.xz{,.asc})
sha512sums=('fe451893474dce1270e12af707a9a8fe1f0217e1782b4e1a67d25dadf56ff4a5e7dbc9ba4431f774aedffa46a40a28a6a0488df24feefb2f93e90fd2369c2c88'
            'SKIP')
b2sums=('6c584f9de6905d91bc243f8d33e3b96c7e2bca7f8f1b2f14025413a450eba2cd68e1d17a4adfd92ed659da22c32ec82f4feaa617aac561e2aa05500c9a63450e'
        'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

build() {
  local cmake_options=(
    -B build
    -D CMAKE_BUILD_TYPE=Release
    -D CMAKE_INSTALL_LIBDIR=lib32
    -D CMAKE_INSTALL_PREFIX=/usr
    -D UNIT_TESTING=ON
    -S $_name-$pkgver
    -W no-dev
  )

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  rm -rvf "$pkgdir/usr/include"
}
# vim: ts=2 sw=2 et:
