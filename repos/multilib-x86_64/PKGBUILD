# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-librsvg
pkgver=2.40.11
pkgrel=1
pkgdesc='A SVG viewing library (32-bit)'
arch=('x86_64')
url='https://live.gnome.org/LibRsvg'
license=('LGPL')
depends=('lib32-gdk-pixbuf2' 'lib32-libcroco' 'lib32-pango' 'librsvg')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python2' 'vala')
install='librsvg.install'
source=("http://ftp.gnome.org/pub/gnome/sources/librsvg/${pkgver:0:4}/librsvg-${pkgver}.tar.xz")
sha256sums=('611ac0f544debd0c9bf1ef5509c990d218e83dd1d8c83297075c4b29796a2e02')

build() {
  cd librsvg-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --enable-vala \
    --disable-static
  make
}

package() {
  cd librsvg-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

# vim: ts=2 sw=2 et:
