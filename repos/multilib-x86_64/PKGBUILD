# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=alsa-lib
pkgname=lib32-${_pkgbasename}
pkgver=1.1.5
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/lib/${_pkgbasename}-$pkgver.tar.bz2)
sha1sums=('636ac0b2b759108ca9b0f4efa1c3e8650cf9c9f6')

build() {
  cd $srcdir/${_pkgbasename}-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

package() {
  cd $srcdir/${_pkgbasename}-$pkgver

  make DESTDIR=$pkgdir install

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
