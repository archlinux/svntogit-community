# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libsm
pkgname=lib32-$_pkgbasename
pkgver=1.2.3
pkgrel=2
pkgdesc="X11 Session Management library (32-bit)"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('lib32-libice' 'lib32-util-linux' $_pkgbasename)
makedepends=('xorg-util-macros' 'xtrans' 'gcc-multilib' 'xorgproto')
source=(${url}/releases/individual/lib/libSM-${pkgver}.tar.bz2)
sha512sums=('74c42e27029db78475e62025b4711dbac5e22d2f8e8a24be98a1c31b03c0fc4afe859928f851800ea0b76854f12147900dc4f27bbfd3d8ea45daaaf24b70a903')

build() {
  cd libSM-${pkgver}

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd libSM-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
