# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

_pkgbasename=libva-intel-driver
pkgname=lib32-libva-intel-driver
pkgver=1.6.1
pkgrel=1
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family (32-bit)'
arch=('x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('lib32-libva')
source=(http://freedesktop.org/software/vaapi/releases/$_pkgbasename/$_pkgbasename-$pkgver.tar.bz2)
md5sums=('ed1b04c1a3c029ad389b7e23822a2762')

prepare() {
  cd $_pkgbasename-$pkgver

  # Only relevant if intel-gpu-tools is installed,
  # since then the shaders will be recompiled
  sed -i '1s/python$/&2/' src/shaders/gpp.py
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib32
  make 
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
