# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Frank Tetzel <frank.tetzel@mailbox.tu-dresden.de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes
pkgver=1.51
pkgrel=14
pkgdesc="Super Nintendo emulator"
url="http://www.zsnes.com/"
arch=('i686' 'x86_64')
license=('GPL')
options=(!buildflags)

if [[ $CARCH == "x86_64" ]]; then
  depends=('lib32-gcc-libs' 'lib32-sdl' 'lib32-libpng>=1.5.0' 'lib32-libgl' 'lib32-ncurses' 'lib32-mesa' 'lib32-libpulse' 'lib32-alsa-plugins')
  makedepends=('nasm' 'gcc-multilib' 'gendesk')
else
  depends=('gcc-libs' 'sdl' 'libpng>=1.5.0' 'libgl' 'ncurses' 'mesa')
  makedepends=('nasm' 'gcc' 'gendesk')
fi

source=(http://downloads.sourceforge.net/${pkgname}/zsnes151src.tar.bz2
        zsnes.patch
        zsnes-1.51-libpng15.patch
        zsnes.png)
md5sums=('7071186bf80632ae88a153239498d8c9'
         'fd13769c13de49601a234b46fb802a75'
         'ea82b4dbf31c87dbcc6c67d02ddfb299'
         'b5260c02adfc3b7ff725041214b00bfd')

build() {
  cd "$srcdir/${pkgname}_1_51"

  patch -p1 -i ../zsnes.patch
  patch -p0 -i ../zsnes-1.51-libpng15.patch
  
  cd src
  if [ $CARCH == "x86_64" ]; then
    export CC="gcc -m32"
    export CXX="g++ -m32"
  fi
  ./autogen.sh --prefix=/usr \
    x_libraries=/usr/lib \
    force_arch=i686 \
    --enable-release
  
  make
}

package() {
  cd "$srcdir"

  gendesk

  cd "${pkgname}_1_51/src"

  make DESTDIR=${pkgdir} install
  
  # adding --mandir= to autoconf line fails
  install -dm755 ${pkgdir}/usr/share

  install -Dm644 ${srcdir}/zsnes.desktop ${pkgdir}/usr/share/applications/zsnes.desktop
  install -Dm644 ${srcdir}/zsnes.png ${pkgdir}/usr/share/pixmaps/zsnes.png

  mv ${pkgdir}/usr/man ${pkgdir}/usr/share
}

# vim:set ts=2 sw=2 et:
