# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename
pkgver=2.5.4
pkgrel=1
pkgdesc="TrueType font rendering library (32-bit)"
arch=(x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' 'lib32-harfbuzz' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2{,.sig}
        freetype-2.2.1-enable-valid.patch
        freetype-2.5.1-enable-spr.patch
        freetype-2.5.1-enable-sph.patch
        env_FT2_NO_SUBPIXEL_HINTING.diff
        0001-pcf-Fix-Savannah-bug-43774.patch)
sha1sums=('f30b929e2edc5d7ca64c1ccc65f0c2068e84c5e2'
          'SKIP'
          'f279d922a873d62a8af50bfc873051839d194dca'
          '13ee8d558593db991ad29fa090b461f914536104'
          'c31fa3d342ead56f3acfa1f267b474a7686d0014'
          'ddba70eef4270e5a89e098d6ee1e7ceb03ea98da'
          '16057f51b084e9ce5c614a4add41b532d32648b4')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-spr.patch"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"

  # https://bugs.archlinux.org/task/35274
  patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-sph.patch"
  # Provide a way to disable the above patch at runtime.
  # Expected to be a temporary measure until fontconfig picks up
  # the necessary configurables.
  patch -Np1 -i "${srcdir}/env_FT2_NO_SUBPIXEL_HINTING.diff"

  # http://savannah.nongnu.org/bugs/index.php?43774
  patch -Np1 -i "${srcdir}/0001-pcf-Fix-Savannah-bug-43774.patch"
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/freetype-${pkgver}"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/freetype-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
