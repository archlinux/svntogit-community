# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.9.10
pkgrel=2
pkgdesc='XML parsing library, version 2 (32-bit)'
url="http://www.xmlsoft.org/"
arch=(x86_64)
license=(MIT)
depends=(lib32-zlib lib32-readline lib32-ncurses lib32-xz lib32-icu libxml2)
makedepends=(git)
_commit=41a34e1f4ffae2ce401600dbb5fe43f8fe402641  # tags/v2.9.10^0
source=("git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbasename}
  git describe --always --tags | sed 's/-rc/rc/;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${_pkgbasename}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}

  ./configure \
    --prefix=/usr \
    --with-threads \
    --with-history \
    --with-icu \
    --libdir=/usr/lib32
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

package() {
  cd ${_pkgbasename}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
