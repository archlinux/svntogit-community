# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Justin Wilcox <nat1192 at gmail dot com>
pkgname=restbed
pkgver=4.5
pkgrel=3
pkgdesc="A framework for asynchronous RESTful functionality in C++11 applications"
arch=('i686' 'x86_64')
url="https://github.com/Corvusoft/restbed"
license=('AGPL3')
depends=('openssl')
replaces=('restbed-latest')
conflicts=('restbed-latest')
makedepends=('cmake' 'asio' 'kashmir')
source=("https://github.com/Corvusoft/restbed/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "strand.patch"
        "dns-resolution-error.patch")
sha256sums=('dfa20b1cd68105b1f742120df4f9a3d8ffc42ba3ad56b4db7a02b209d341892b'
            '58ed74b3db6d989a871d181e6875226cef4e81f77dac6b9a45b04ae5d4e96299'
            'fd80b2ddf4bf62ff2234bc4d38eb583667eb8f3d71cd35d0018beca6c681ea8c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Necessary to build against asio 1.10.X
  patch -p1 < "$srcdir"/strand.patch
  # Upstream commit d81cb41aed489c59ce98c78a9ffcb130fc7337d4
  patch -p1 < "$srcdir"/dns-resolution-error.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED=on
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  cd build/
  make DESTDIR="$pkgdir" install
}
