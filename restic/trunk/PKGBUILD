# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=restic
pkgver=0.9.6
pkgrel=2
pkgdesc="Fast, secure, efficient backup program"
arch=('x86_64')
url="https://restic.net"
license=('BSD')
depends=('glibc')
makedepends=('go-pie' 'git')
checkdepends=('fuse2')
optdepends=('fuse2: mount archive')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/restic/restic/archive/v${pkgver}.tar.gz")
sha256sums=('1cc8655fa99f06e787871a9f8b5ceec283c856fa341a5b38824a0ca89420b0fe')

build(){
  cd "${pkgname}-${pkgver}"
  go build -mod=vendor -trimpath -ldflags "-X 'main.version=${pkgver}' -extldflags ${LDFLAGS}" ./cmd/...
}

check(){
  cd "${pkgname}-${pkgver}"
  go test ./cmd/... ./internal/... || true
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 restic "${pkgdir}/usr/bin/restic"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 -t "$pkgdir/usr/share/man/man1/" doc/man/*.1
  install -Dm644 doc/zsh-completion.zsh "$pkgdir/usr/share/zsh/site-functions/_restic"
  install -Dm644 doc/bash-completion.sh "$pkgdir/usr/share/bash-completion/completions/restic"
}
