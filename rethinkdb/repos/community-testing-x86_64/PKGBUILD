# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>

pkgname=rethinkdb
pkgver=1.15.0
_tag=rethinkdb-$pkgver-1
pkgrel=2
pkgdesc='An open-source distributed database built with love.'
arch=(i686 x86_64)
url='http://www.rethinkdb.com/'
license=(AGPL)
depends=(protobuf ncurses gperftools curl)
makedepends=(boost python2 wget)
backup=(etc/rethinkdb/instances.d/default.conf)
install=rethinkdb.install
options=(!emptydirs)
source=(
  http://download.rethinkdb.com/dist/$_tag.tgz
  rethinkdb-tmpfile.conf
  rethinkdb.service
  boost_1.56_compat.patch
  protobuf-2.6-compat.patch
)
sha256sums=('8cb1cdd176d9a6e87b315c65de084158a1b2fc9910127d21ec930181c52d838e'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            'e56bffa2b9ebc3a00ef566ab2be0719a633c89d961a2461dfa2d9ffdb258c1a2'
            '1c55b12df452ceb2470d3ec48dde21bcdc2785be371f6f0858af034e282b1f21'
            '0bc035df3e84945efac2ed6e48c781e2a3a33b6e4f9a551ec9bc4958462e0bbe')

prepare() {
  cd $_tag
  # boost 1.56 workaround https://github.com/rethinkdb/rethinkdb/issues/3044
  rm src/unittest/print_secondary.cc
  patch -p1 < ../boost_1.56_compat.patch
  patch -p1 < ../protobuf-2.6-compat.patch
}

build() {
  cd $_tag
  export PYTHON=/usr/bin/python2
  ./configure CXXFLAGS="-DBOOST_VARIANT_DO_NOT_USE_VARIADIC_TEMPLATES" --fetch v8 --dynamic all --enable-precompiled-web --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd $_tag

  make build/release/rethinkdb-unittest
  ./build/release/rethinkdb-unittest
  # some tests might be flaky on btrfs filesystem!
}

package() {
  cd $_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/rethinkdb-tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/rethinkdb.conf
  install -Dm644 "$srcdir"/rethinkdb.service "$pkgdir"/usr/lib/systemd/system/rethinkdb@.service

  # create 'default' database instance
  mv "$pkgdir"/etc/rethinkdb/default.conf.sample "$pkgdir"/etc/rethinkdb/instances.d/default.conf
  sed -e 's|# directory=/var/lib/rethinkdb|directory=/var/lib/rethinkdb|' \
      -e 's|# pid-file=/var/run/rethinkdb/rethinkdb.pid|pid-file=/var/run/rethinkdb/default.pid|' \
      -i "$pkgdir"/etc/rethinkdb/instances.d/default.conf

  # Arch uses systemd, no need for init.d scripts
  rm -r "$pkgdir"/etc/init.d
}
