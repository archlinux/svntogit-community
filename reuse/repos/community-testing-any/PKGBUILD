# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Max Mehl <aur at mehl dot mx>
# SPDX-FileCopyrightText: 2019 Max Mehl
# SPDX-License-Identifier: CC0-1.0

pkgname='reuse'
_pkgname="reuse-tool"
pkgver=0.13.0
pkgrel=3
pkgdesc='Helper tool for providing and confirming copyright and licensing information'
arch=('any')
url='https://github.com/fsfe/reuse-tool'
license=('GPL3' 'Apache' 'custom:CC-BY-SA-4.0' 'custom:CC0-1.0')
_deps=(
  'debian'
  'requests'
  'license-expression'
  'boolean.py'
  'jinja'
  'binaryornot'
)
_makedeps=(
  'setuptools'
  'setuptools-scm'
)
depends=("${_deps[@]/#/python-}")
makedepends=("${_makedeps[@]/#/python-}")
optdepends=('git: git repository support'
            'mercurial: mercurial repository support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('050fafc0f23c91b9c6d4371a14b2c3562eadabc7b638de0a6078d27883fc4fe516753bcac3f676672150201ff21d8a8722ace2ce5d98abaf1d971b72b6b4f10e')

build() {
  cd "$_pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  # install custom licenses
  cd "$srcdir/$_pkgname-$pkgver/LICENSES"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    CC-BY-SA-4.0.txt \
    CC0-1.0.txt
}
