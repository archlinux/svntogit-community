# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rfc
pkgver=20171118
pkgrel=1
pkgdesc="RFCs from rfc-editor.org"
arch=('any')
url="https://www.rfc-editor.org/"
license=("custom:as-is")
options=('docs')
source=(rfc-index-$pkgver.txt::https://www.rfc-editor.org/rfc/rfc-index.txt
        license
        https://www.rfc-editor.org/rfc/tar/RFCs0001-0500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs0501-1000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs1001-1500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs1501-2000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs2001-2500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs2501-3000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs3001-3500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs3501-4000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs4001-4500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs4501-5000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs5001-5500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs5501-6000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs6001-6500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs6501-7000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs7001-7500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs7501-8000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs8001-latest.tar.gz)
sha256sums=('bf21ca33502c921c5353d8da040171b8d022f10bf846187c8a853980230a48c9'
            'ac9cd91c2e15b0840b6ecd4f7d30d6d12c49e18b4f9eb383a0ce05a64901624e'
            '10cb4c4df9180cb65fbedf0a5abe21bed233318bf1dd891e04c7ce2dc498a229'
            '5aa25f12e5d6295da5f3afe478bde7aae3edb5f0406b4866932ca518e6f877f0'
            '25b6a9400aef45c4a4095e41614c4325f3ccb0f13ae15698ad6479d9d0d59e0d'
            'fe6970efba7cbdd0358f9904804b45525e76e5af4f93b8a65b713f90db8cb947'
            'cbc57da5047ba55c98149de707508fc8de3054a526c8aa99bb6533813b633db4'
            '61e7dcbfb4fd907115cd38bba95299df31542f7d920c640075a8625b107c7393'
            '4f723d485bfd9fb2ed1aba7aa74fa09409846b1556e9c420845ff7226cb39792'
            'c0a9d53c07c602d78984af94a18866770d251d02421038da9a39cf3946777777'
            '77dc6fe66beaecc73fb2f697b66138bab9b081637965cae046cbc68b135394af'
            '1a7993e786b1a49c87943206dcc2cf5d4010e222653c5dc76c0f58a7ac72d22d'
            'd4871177bbe86a734d518cd57eb30d8144f289731215fe4965f9fe09edf17992'
            '7579035abb19d67af618528d51765e496ae2064a091ec7417fe0df6d8c39d9d9'
            '22a75fab9aa7b693808d293dd8f40aafe34239ed457e47e1c7bf4fbe83f8de0c'
            '9090c460cfbcf4fd6c805ba72be48074afa9f6be737d6e0b1eebd2d3ebbd5f70'
            'a74369aa7b9da5f20cd47d66edc71434bd5ac1e73886bb0c63e2a8c9ab5b832f'
            '491a9fdfb511e53940336e14e4f5adfdd566d326714e9027f92d2473a903a668'
            'be841aba71bdf1cd030b07c19f872ef7cd5278fe059f7f6613bddb960c66bf81')

package() {
    cd "$srcdir"

    mkdir -p "$pkgdir/usr/share/doc/rfc"
    cp rfc-index-$pkgver.txt "$pkgdir/usr/share/doc/rfc/rfc-index.txt"
    rm -f rfc-index-$pkgver.txt

    tar xf rfc1305.tar
    rm -f rfc1305.tar
    for n in ntpv3*; do
      mv "$n" "rfc1305-$n"
    done

    for fmt in txt ps pdf; do
      mkdir -p "$pkgdir/usr/share/doc/rfc/${fmt}"
      mv *.${fmt} "$pkgdir/usr/share/doc/rfc/${fmt}/"
    done

    cd "$pkgdir/usr/share/doc/rfc"
    find . -type f -exec chmod 444 {} \;
    find . -type d -exec chmod 555 {} \;
    chown -R root:root .
    install -D -m0644 "$srcdir"/license "$pkgdir"/usr/share/licenses/$pkgname/license
}
