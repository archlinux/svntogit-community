# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rfc
pkgver=20160616
pkgrel=1
pkgdesc="RFCs from rfc-editor.org"
arch=('any')
url="http://www.rfc-editor.org/"
license=("custom:as-is")
options=('docs')
source=(rfc-index-$pkgver.txt::http://www.rfc-editor.org/rfc/rfc-index.txt
        license
        http://www.rfc-editor.org/rfc/tar/RFCs0001-0500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs0501-1000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs1001-1500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs1501-2000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs2001-2500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs2501-3000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs3001-3500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs3501-4000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs4001-4500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs4501-5000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs5001-5500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs5501-6000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs6001-6500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs6501-7000.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs7001-7500.tar.gz
        http://www.rfc-editor.org/rfc/tar/RFCs7501-latest.tar.gz)
md5sums=('62cef6285bfb93e332659822fbd1c80d'
         '06a779406b45edddbd19fda96696ab4a'
         'd40628d29784606b34a6d945867a11ca'
         'a5d15abb0c0a78d865a2b0cd11303eed'
         '1410514ce98023ff10e103c517ad0843'
         '444290d891fb47cb6420508a34ae79db'
         '754ddb53327c72697d916e6b121de811'
         '6b14d0c655a4511f33fb426fa0991566'
         'd2d608ddd3d435b42f2db78a0d584556'
         '2999c61dc74a21569913e921b5f84296'
         '457b84111de54fe416f6ff4f62043ff0'
         '46cfcfb628d2d410946d4cb91d94a341'
         'de50a40ef02eb993fa062ce7df4b90a1'
         '01e5ef1defb86ddca74c2bb01219e4ce'
         '42cbba4786377ccec755951785712396'
         'f16f1ca5cef2cb1756c23d541914efb1'
         '831d8503ed7652e668666bac29d7e929'
         'e240c32221721145d2cbf3f7d33248db')

package() {
    cd "$srcdir"

    mkdir -p "$pkgdir/usr/share/doc/rfc"
    cp rfc-index-$pkgver.txt "$pkgdir/usr/share/doc/rfc/rfc-index.txt"
    rm -f rfc-index-$pkgver.txt

    tar xf rfc1305.tar
    rm -f rfc1305.tar
    for n in ntpv3*; do
      mv "$n" "rfc1305-$n"
    done

    for fmt in txt ps pdf; do
      mkdir -p "$pkgdir/usr/share/doc/rfc/${fmt}"
      mv *.${fmt} "$pkgdir/usr/share/doc/rfc/${fmt}/"
    done

    cd "$pkgdir/usr/share/doc/rfc"
    find . -type f -exec chmod 444 {} \;
    find . -type d -exec chmod 555 {} \;
    chown -R root:root .
    install -D -m0644 $srcdir/license $pkgdir/usr/share/licenses/$pkgname/license
}
