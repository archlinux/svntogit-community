# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rfc
pkgver=20101023
pkgrel=1
pkgdesc="RFCs from rfc-editor.org"
arch=('any')
url="http://www.rfc-editor.org/"
license=("custom:as-is")
options=('docs')
source=(ftp://ftp.nic.it/mirrors/in-notes/rfc-index.txt \
        license \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs0001-0500.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs0501-1000.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs1001-1500.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs1501-2000.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs2001-2500.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs2501-3000.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs3001-3500.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs3501-4000.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs4001-4500.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs4501-5000.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs5001-5500.tar.gz \
        ftp://ftp.nic.it/mirrors/in-notes/tar/RFCs5501-latest.tar.gz)
md5sums=('7706d852a6ef9a37a857a014274545cd'
         '06a779406b45edddbd19fda96696ab4a'
         'da798aebedacbc68ab44b3a2b33c4d63'
         'd02f5269c5c79b913dd146c5e4766afe'
         '2c390c883c1981f1c4d98aa41be0566f'
         '2aaf02cb36f62999c061f56b804253ab'
         '00720cf556fef196a3092b591e25a78a'
         'c82a53661aa6bcaa595bd3dab2902ad1'
         '42e24329c59c22bc34a2f53897e06085'
         '424c64eb81a89a0dbad22897f2360e51'
         'fd97928a9a221a83c51390addaf36c64'
         '2d8a03c779ae80e6db078c522950904c'
         '650dcb067c5a5b869312b6ecb96d7ddc'
         '9c5e85b1f2ead58a11428a4b5b78eaec')

build() {
    cd "$srcdir"

    mkdir -p "$pkgdir/usr/share/doc/rfc"
    cp rfc-index.txt "$pkgdir/usr/share/doc/rfc/"
    rm -f rfc-index.txt

    tar xf rfc1305.tar
    rm -f rfc1305.tar
    for n in ntpv3*; do
      mv "$n" "rfc1305-$n"
    done

    for fmt in txt ps pdf; do
      mkdir -p "$pkgdir/usr/share/doc/rfc/${fmt}"
      mv *.${fmt} "$pkgdir/usr/share/doc/rfc/${fmt}/"
    done

    cd "$pkgdir/usr/share/doc/rfc"
    find . -type f -exec chmod 444 {} \;
    find . -type d -exec chmod 555 {} \;
    chown -R root:root .
    install -D -m0644 $srcdir/license $pkgdir/usr/share/licenses/$pkgname/license
}
