# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=rgbds
pkgver=0.5.2
pkgrel=1
pkgdesc='Rednex GameBoy Development System'
arch=(x86_64)
url=https://github.com/rednex/rgbds/
license=(MIT)
depends=(
  glibc
  libpng
)
makedepends=(
  cmake
  git
  ninja
)
_tag=9d993d84e85eeb8dd304e48463d89865795e97ed
source=(git+https://github.com/rednex/rgbds.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd rgbds

  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S rgbds -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 rgbds/LICENSE -t "${pkgdir}"/usr/share/licenses/rgbds/
  find rgbds/contrib/zsh_compl -type f -exec install -Dm 644 {} -t "${pkgdir}"/usr/share/zsh/site-functions/ \;
}

# vim: ts=2 sw=2 et:
