# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=rhonabwy
pkgver=0.9.9999
pkgrel=2
pkgdesc='JWK, JWKS, JWS, JWE and JWT C library'
arch=(x86_64)
url=https://github.com/babelouest/rhonabwy
license=(LGPL2.1)
depends=(
  glibc
  gnutls
  jansson
  liborcania.so
  libulfius.so
  libyder.so
  zlib
)
makedepends=(
  cmake
  doxygen
  git
  graphviz
  ninja
)
provides=(librhonabwy.so)
_tag=7bc47f2cd36fbf3bc10dab88d6cdc02d837320a2
source=(git+https://github.com/babelouest/rhonabwy.git#tag=${_tag})
validpgpkeys=(8405B02FCC28EF9744C8F253FE82139440BD22B9) # Nicolas Mora <nicolas@babelouest.org>
b2sums=(SKIP)

pkgver() {
  cd rhonabwy
  git describe --tags | sed 's/^v//'
}

build() {
  export CFLAGS+=" -Wno-maybe-uninitialized"
  cmake -S rhonabwy -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_RHONABWY_DOCUMENTATION=ON
  ninja -C build doc
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -dm 755 "${pkgdir}"/usr/share/doc/rhonabwy
  cp -dr --no-preserve=ownership rhonabwy/doc/html "${pkgdir}"/usr/share/doc/rhonabwy/
}

# vim: ts=2 sw=2 et:
