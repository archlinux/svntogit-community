# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-loengfan
pkgver=0.0.0.20210704
_commit=7c9b0a0cf7b8c2c0c92fb9ddb25d6c9cca5d312f
pkgrel=1
pkgdesc="Cantonese version of Liang Fen input method"
arch=('x86_64')
url="https://github.com/CanCLID/rime-loengfan"
license=('CCPL:by')
depends=('rime-cangjie')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/CanCLID/rime-loengfan/archive/$_commit/rime-loengfan-$_commit.tar.gz")
sha512sums=('17e5a88ba83eb60c222ad7000114ac43decf533c222c08c7d81c3a63d3abfe5286c33dfd31a359358058805dfe4c9aa8fd4d9e91bad768c1fc12ee9c90eeaed5')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
