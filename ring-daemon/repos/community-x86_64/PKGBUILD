# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=ring-daemon
pkgver=20180330.1.0d611bb
pkgrel=1
epoch=2
_pjprojectver=2.6
pkgdesc="Ring is a free and universal communication platform which preserves the users' privacy and freedoms (daemon)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=("ring")
depends=('opendht' 'yaml-cpp' 'alsa-lib' 'libpulse' 'jack' 'jsoncpp'
         'libsamplerate' 'libsndfile' 'dbus-c++' 'ffmpeg' 'gnutls'
         'expat' 'gsm' 'libupnp' 'libnatpmp' 'libva' 'libvdpau' 'restbed'
         'libsecp256k1' 'util-linux' 'opus')
makedepends=('git' 'boost' 'msgpack-c' 'autoconf-archive')
#checkdepends=('cppunit')
source=("git+https://gerrit-ring.savoirfairelinux.com/ring-daemon#commit=1026c687d06dfef6dba7f94bf5463e7492fa0329"
        "http://www.pjsip.org/release/${_pjprojectver}/pjproject-${_pjprojectver}.tar.bz2")
noextract=("pjproject-${_pjprojectver}.tar.bz2")
sha256sums=('SKIP'
            '2f5a1da1c174d845871c758bd80fbb580fca7799d3cfaa0d3c4e082b5161c7b4')

prepare() {
  cd "${pkgname}"
  cp ../pjproject-${_pjprojectver}.tar.bz2 contrib/tarballs
  mkdir contrib/native
  autoreconf -fvi
}

build() {
  cd "${pkgname}/contrib/native"
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject
  make DEPS_pjproject=

  cd ../..
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-ipv6
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# Disabled because some tests (TURN) use the network.
#check() {
#  cd "${pkgname}"
#  make -k check
#}

package() {
  cd "${pkgname}"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

# vim:set ts=2 sw=2 et:
