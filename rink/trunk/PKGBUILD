# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rink
pkgver=0.6.1
pkgrel=1
pkgdesc='Unit conversion tool and library written in rust'
arch=('x86_64')
url=https://rinkcalc.app
license=('GPL3' 'MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("rink-$pkgver.tar.gz::https://static.crates.io/crates/rink/rink-$pkgver.crate")
b2sums=('50ac59c66237b0f418e76124da248c133919b8e8b8b9cc94ee53f2140f82a5ff15a6c275e579bcf1e26ddc7847e4fed7e6c5a515aa24556d3fadaab988a530c5')

build() {
  cd rink-$pkgver
  cargo build --release --locked
}

check() {
  cd rink-$pkgver
  cargo test --release --locked
}

package() {
  cd rink-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rink
}

# vim:set ts=2 sw=2 et:
