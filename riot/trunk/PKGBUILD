# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>

pkgbase=riot
pkgname=('riot-desktop' 'riot-web')
pkgver=1.0.7
pkgrel=1
pkgdesc="A glossy Matrix collaboration client, web and desktop versions."
arch=('any')
url="https://riot.im"
_url="https://github.com/vector-im/riot-web"
license=('Apache')
makedepends=('npm' 'git' 'yarn')
source=(${pkgbase}-${pkgver}.tar.gz::"${_url}/archive/v${pkgver}.tar.gz"
        ${pkgbase}-${pkgver}.tar.gz.asc::"${_url}/releases/download/v${pkgver}/v${pkgver}-src.tar.gz.asc"
        "${pkgbase}.desktop"
        "${pkgbase}-desktop.sh")
sha256sums=('0b1b58b0ac69e6c27bc67b16cb74dc663d3f76fde19b578e3be77e41824cd12b'
            'SKIP'
            '043fbaa29be96f52541143e1684fd7ba33585edb215c06d1ed4e59ddc48a9fa7'
            '94807ecb4210b09672d39f39b5be6f4e8fbd58e9d674c3b7ca795a6dd9c14ff7')
validpgpkeys=('6FEB6F83D48B93547E7DFEDEE019645248E8F4A1') # riot.im (Package Signing key) <packages@riot.im>

prepare() {
  cd ${pkgbase}-web-${pkgver}
  sed -i 's@https://riot.im/download/desktop/update/@null@g' electron_app/riot.im/config.json
}

build() {
  cd ${pkgbase}-web-${pkgver}
  npm install --cache ../npm-cache
  npm run build --cache ../npm-cache
}

package_riot-web() {
  pkgdesc="A glossy Matrix collaboration client for the web."
  provides=('vector-web')  # @vith
  replaces=('vector-web')  # @vith
  cd ${pkgbase}-web-${pkgver}

  install -d "${pkgdir}"/{usr/share/webapps,etc/webapps}/${pkgbase}

  cp -r webapp/* "${pkgdir}"/usr/share/webapps/${pkgbase}/
  install -Dm644 config.sample.json -t "${pkgdir}"/etc/webapps/${pkgbase}/
  ln -s /etc/webapps/${pkgbase}/config.json "${pkgdir}"/usr/share/webapps/${pkgbase}/
  echo "${pkgver}" > "${pkgdir}"/usr/share/webapps/${pkgbase}/version
}

package_riot-desktop() {
  pkgdesc="A glossy Matrix collaboration client for the desktop."
  depends=('riot-web' 'electron')
  backup=("etc/${pkgbase}/config.json")
  cd ${pkgbase}-web-${pkgver}

  cd electron_app
  npm install --cache "${srcdir}"/npm-cache
  cd ..

  install -d "${pkgdir}"{/usr/lib/${pkgbase}/electron_app,/etc/webapps/${pkgbase}}

  ln -s /usr/share/webapps/${pkgbase} "${pkgdir}"/usr/lib/${pkgbase}/webapp
  ln -s /etc/${pkgbase}/config.json "${pkgdir}"/etc/webapps/${pkgbase}/config.json

  install -Dm644 package.json -t "${pkgdir}"/usr/lib/${pkgbase}/
  cp -r origin_migrator "${pkgdir}"/usr/lib/${pkgbase}/
  cp -r electron_app/src "${pkgdir}"/usr/lib/${pkgbase}/electron_app/
  cp -r electron_app/node_modules "${pkgdir}"/usr/lib/${pkgbase}/electron_app/
  install -Dm644 electron_app/img/riot.png -t "${pkgdir}"/usr/lib/${pkgbase}/electron_app/img
  install -Dm644 electron_app/riot.im/config.json -t "${pkgdir}"/etc/${pkgbase}

  install -Dm644 "${srcdir}"/${pkgbase}.desktop "${pkgdir}"/usr/share/applications/${pkgbase}.desktop
  install -Dm755 "${srcdir}"/${pkgname}.sh "${pkgdir}"/usr/bin/${pkgname}

  install -Dm644 res/themes/riot/img/logos/riot-im-logo.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/${pkgbase}.svg
  for i in 16 24 48 64 96 128 256 512; do
    install -Dm644 electron_app/build/icons/${i}x${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/${pkgbase}.png
  done
}
