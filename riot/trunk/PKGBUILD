# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Steef Hegeman <mail@steefhegeman.com>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Julian Schacher <jspp@posteo.net>

pkgbase=riot
pkgname=(riot-web riot-desktop)
pkgver=1.6.2
pkgrel=2
pkgdesc="A glossy Matrix collaboration client, web and desktop versions."
arch=(x86_64)
url="https://riot.im"
license=(Apache)
makedepends=(npm git yarn python rust sqlcipher electron)
_url="https://github.com/vector-im/riot"
source=(${pkgbase}-web-${pkgver}.tar.gz::${_url}-web/archive/v${pkgver}.tar.gz
        ${pkgbase}-web-${pkgver}.tar.gz.asc::${_url}-web/releases/download/v${pkgver}/v${pkgver}-src.tar.gz.asc
        ${pkgbase}-desktop-${pkgver}.tar.gz::${_url}-desktop/archive/v${pkgver}.tar.gz
        ${pkgbase}-desktop-${pkgver}.tar.gz.asc::${_url}-desktop/releases/download/v${pkgver}/v${pkgver}-src.tar.gz.asc
        ${pkgbase}.desktop
        ${pkgbase}-desktop.sh)
sha256sums=('4f03f29ed15c1336f6582e4b04a37d9016e1dbeed0467b020ae380b78f39b874'
            'SKIP'
            '509a2ac5376b25e6e88eb91d658aa38ca386533dcb84774fcf6918cfc6db822a'
            'SKIP'
            'ba8157d7b951400d1c590c2a75ec879a4f3770dac6baf38d6010c24e69f0c3fc'
            'dc14c79694bac4f523493c26a73c8000f9a2374f36dd21c1945bfb93d19277cd')
validpgpkeys=(A878CDF66CF4A9B4807CEBE574692659BDA3D940) # Riot Releases <releases@riot.im>

prepare() {
  cd ${pkgbase}-web-${pkgver}
  # Switch target to output to directory rather than .deb package
  sed -i 's/"target": "deb"/"target": "dir"/g' package.json
  yarn install

  cd ../${pkgbase}-desktop-${pkgver}
  sed -i 's@"https://packages.riot.im/desktop/update/"@null@g' riot.im/release/config.json
  yarn install
}

build() {
  cd ${pkgbase}-web-${pkgver}
  yarn build

  cd ../${pkgbase}-desktop-${pkgver}
  yarn run build:native
  yarn run build
}

package_riot-web() {
  pkgdesc="Glossy Matrix collaboration client for the web."
  provides=(vector-web)
  replaces=(vector-web)

  cd ${pkgbase}-web-${pkgver}

  install -d "${pkgdir}"/{usr/share/webapps,etc/webapps}/${pkgbase}

  cp -r webapp/* "${pkgdir}"/usr/share/webapps/${pkgbase}/
  install -Dm644 config.sample.json -t "${pkgdir}"/etc/webapps/${pkgbase}/
  ln -s /etc/webapps/${pkgbase}/config.json "${pkgdir}"/usr/share/webapps/${pkgbase}/
  echo "${pkgver}" > "${pkgdir}"/usr/share/webapps/${pkgbase}/version
}

package_riot-desktop() {
  pkgdesc="Glossy Matrix collaboration client for the desktop."
  depends=("riot-web=${pkgver}" electron sqlcipher)
  backup=('etc/riot/config.json')

  cd ${pkgbase}-desktop-${pkgver}

  install -d "${pkgdir}"{/usr/lib/${pkgbase}/,/etc/webapps/${pkgbase}}

  # Install the app content, replace the webapp with a symlink to the system package
  cp -r dist/linux-unpacked/resources/* "${pkgdir}"/usr/lib/${pkgbase}/
  ln -s /usr/share/webapps/${pkgbase} "${pkgdir}"/usr/lib/${pkgbase}/webapp

  # Config file
  ln -s /etc/${pkgbase}/config.json "${pkgdir}"/etc/webapps/${pkgbase}/config.json
  install -Dm644 riot.im/release/config.json -t "${pkgdir}"/etc/${pkgbase}

  # Required extras
  install -Dm644 ../${pkgbase}.desktop "${pkgdir}"/usr/share/applications/${pkgbase}.desktop
  install -Dm755 ../${pkgname}.sh "${pkgdir}"/usr/bin/${pkgname}

  # Icons
  install -Dm644 ../${pkgbase}-web-${pkgver}/res/themes/riot/img/logos/riot-im-logo.svg "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/${pkgbase}.svg
  for i in 16 24 48 64 96 128 256 512; do
    install -Dm644 build/icons/${i}x${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/${pkgbase}.png
  done
}
