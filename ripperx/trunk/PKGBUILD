# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=ripperx
pkgver=2.7.3
pkgrel=5
pkgdesc="GTK program to rip and encode mp3 files"
arch=(i686 x86_64)
url="http://ripperx.sourceforge.net/"
depends=('gtk2' 'id3lib')
license=('GPL')
source=(http://downloads.sourceforge.net/ripperx/ripperX-$pkgver.tar.gz)
md5sums=('f07c55edf08a1cd57537f1b9379b4e4d')

prepare() {
  cd $srcdir/ripperX-$pkgver
  sed -i 's|Icon=.*|Icon=ripperX.xpm|g' ripperX.desktop
  echo "Categories=GTK;GNOME;AudioVideo;DiscBurning;" >>ripperX.desktop
  patch ripperX.pc.in <<EOF
diff -r ripperX-2.7.3/ripperX.pc.in ripperX-2.7.3.y/ripperX.pc.in
3a4
> includedir=@includedir@
EOF
}

build() {
  cd $srcdir/ripperX-$pkgver
  LDFLAGS=-lm ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/ripperX-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm0644 ripperX.desktop $pkgdir/usr/share/applications/ripperX.desktop
  install -Dm0644 ripperX.xpm $pkgdir/usr/share/icons/ripperX.xpm
}
