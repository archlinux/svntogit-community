# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.8.0
pkgrel=1
pkgdesc='A fast and lightweight picture-viewer for Xfce'
arch=('i686' 'x86_64')
url='http://goodies.xfce.org/projects/applications/ristretto'
license=('GPL')
depends=('libxfce4ui' 'libexif' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('librsvg: SVG support'
            'tumbler: thumbnailing support')
groups=('xfce4-goodies')
install=ristretto.install
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
md5sums=('94c778850325a4e5a12e3433c8a05432')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-debug
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
