# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.8.2
pkgrel=2
pkgdesc='Fast and lightweight picture-viewer for Xfce4'
arch=('x86_64')
url='http://docs.xfce.org/apps/ristretto/start'
license=('GPL')
depends=('libxfce4ui' 'libexif' 'file')
makedepends=('intltool')
optdepends=('librsvg: SVG support'
            'tumbler: thumbnailing support')
groups=('xfce4-goodies')
source=("https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2"
        upstream-fixes.patch)
md5sums=('a8d8bb6b8fa7f868cfa3ae778630946e'
         '6d8eb1cd430de4b4d8227cb12a8bb063')

prepare() {
  cd "$pkgname-$pkgver"
  # Fix opening symlinks and urls (FS#54705)
  patch -Np1 -i ../upstream-fixes.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: git.xfce.org/apps/ristretto/plain/NEWS
# vim:set ts=2 sw=2 et:
