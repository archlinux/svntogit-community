# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.8.2
pkgrel=1
pkgdesc='Fast and lightweight picture-viewer for Xfce4'
arch=('x86_64')
url='http://docs.xfce.org/apps/ristretto/start'
license=('GPL')
depends=('libxfce4ui' 'libexif' 'file')
makedepends=('intltool')
optdepends=('librsvg: SVG support'
            'tumbler: thumbnailing support')
groups=('xfce4-goodies')
source=("http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
md5sums=('a8d8bb6b8fa7f868cfa3ae778630946e')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: git.xfce.org/apps/ristretto/plain/NEWS
# vim:set ts=2 sw=2 et:
