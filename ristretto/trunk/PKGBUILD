# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.8.0
pkgrel=2
pkgdesc='Fast and lightweight picture-viewer for Xfce'
arch=('i686' 'x86_64')
url='http://goodies.xfce.org/projects/applications/ristretto'
license=('GPL')
depends=('libxfce4ui' 'libexif')
makedepends=('intltool')
optdepends=('librsvg: SVG support'
            'tumbler: thumbnailing support')
groups=('xfce4-goodies')
source=("http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
md5sums=('94c778850325a4e5a12e3433c8a05432')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-debug
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
