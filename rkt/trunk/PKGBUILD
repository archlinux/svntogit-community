# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

# Contributor: Yuval Adam <yuval at y3xz dot com> PGP-Key: 271386AA2EB7672F
# Contributor: Kenny Rasschaert <kenny dot rasschaert at gmail dot com> PGP-Key: 1F70454121E41419
# Contributor: Adrián Pérez de Castro <adrian at perezdecastro dor org> PGP-Key: 91C559DBE4C9123B
# Contributor: Carl George <arch at cgtx dot us> PGP-Key: 4BA2F7E101D9F512

pkgname=rkt
pkgver=1.15.0
pkgrel=3
pkgdesc="App container runtime"
arch=('x86_64')
url="https://github.com/coreos/rkt"
license=(apache)
depends=('glibc' 'openssl' 'zlib' 'systemd')
makedepends=('cpio' 'go' 'wget' 'squashfs-tools' 'perl-capture-tiny'
             'intltool' 'gperf' 'git' 'libseccomp' 'bc')

# stage1/usr_from_coreos/coreos-common.mk
CCN_IMG_RELEASE=1151.0.0
CCN_SYSTEMD_VERSION=v231

# stage1/usr_from_kvm/kernel.mk
KERNEL_VERSION=4.3.1

source=(https://github.com/coreos/rkt/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        coreos_production_pxe_image-$CCN_IMG_RELEASE.cpio.gz::http://alpha.release.core-os.net/amd64-usr/$CCN_IMG_RELEASE/coreos_production_pxe_image.cpio.gz
        coreos_production_pxe_image-$CCN_IMG_RELEASE.cpio.gz.sig::http://alpha.release.core-os.net/amd64-usr/$CCN_IMG_RELEASE/coreos_production_pxe_image.cpio.gz.sig
        https://www.kernel.org/pub/linux/kernel/v4.x/linux-$KERNEL_VERSION.tar.xz
        rkt.sysusers
        rkt-1.15.0-stage1-host-reorder-systemd-nspawn-args.patch)
noextract=(coreos_production_pxe_image.cpio.gz
           linux-$KERNEL_VERSION.tar.xz)
validpgpkeys=('04127D0BFABEC8871FFB2CCE50E0885593D2DCB4' '48F9B96A2E16137F')
sha256sums=('3efd0dc96c8783d5b378862c7239f733863a181bb3148f3b2e8e10023df06e01'
            '6f08697bea372af916a150f9846110e8855305653e5582b2c2ea04f14253e5c8'
            'SKIP'
            '11faaff6e7546038b868f524cdf42a5a1b67be9fdfd37d931723a8deb1811b72'
            '2aee4e8547843f4e6c032761b97cb723c1ecd384d508b86f44d16826bc34d6d6'
            '1dc68b38d58858840e99537c9c17f9a9ac087a6b2eb393aa48c79f98d7dcc2ee')
install="rkt.install"

prepare() {
  cd $pkgname-$pkgver
  mkdir -p build-rkt-$pkgver/tmp/usr_from_kvm/kernel/
  cd build-rkt-$pkgver/tmp/usr_from_kvm/kernel/
  ln -s "$srcdir"/linux-$KERNEL_VERSION.tar.xz

  # stage1-host fails to start container on systemd 231
  # https://github.com/coreos/rkt/issues/3215
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 -i "$srcdir"/rkt-1.15.0-stage1-host-reorder-systemd-nspawn-args.patch
}

build() {
  cd $pkgname-$pkgver
  export GOPATH="$PWD/Godeps/_workspace/src"

  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-tpm=auto \
    --with-stage1-flavors=coreos,kvm,host,fly \
    --with-stage1-default-flavor=host \
    --with-stage1-default-images-directory=/usr/lib/rkt/stage1-images \
    --with-stage1-default-location=/usr/lib/rkt/stage1-images/stage1-host.aci \
    --with-coreos-local-pxe-image-path="$srcdir"/coreos_production_pxe_image-$CCN_IMG_RELEASE.cpio.gz \
    --with-coreos-local-pxe-image-systemd-version=$CCN_SYSTEMD_VERSION

  make manpages
  make bash-completion
  make
}

package() {
  cd $pkgname-$pkgver/dist/init/systemd

  for unit in *.service *.timer *.socket; do
    install -Dm644 $unit "$pkgdir"/usr/lib/systemd/system/$unit
  done

  for tmpfile in tmpfiles.d/*.conf; do
    install -Dm644 $tmpfile "$pkgdir"/usr/lib/$tmpfile
  done

  cd "$srcdir"/$pkgname-$pkgver
  install -Dm644 "$srcdir"/rkt.sysusers "$pkgdir"/usr/lib/sysusers.d/rkt.conf
  install -Dm644 dist/bash_completion/rkt.bash "$pkgdir"/usr/share/bash-completion/completions/rkt

  cd dist/manpages
  for f in *; do
    install -Dm644 "$f" "$pkgdir/usr/share/man/man1/$f"
  done
  cd ../..

  cd build-$pkgname-$pkgver
  install -dm755 "$pkgdir"/usr/bin "$pkgdir"/usr/lib/rkt/stage1-images
  mv target/bin/rkt tools/actool "$pkgdir"/usr/bin
  mv target/bin/stage1-*.aci "$pkgdir"/usr/lib/rkt/stage1-images/
}

# vim:set ts=2 sw=2 et:
