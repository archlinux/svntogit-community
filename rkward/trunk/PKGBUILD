# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor : Vassilis Palassopoulos <palasso [at] gmail [dot] com>

pkgname=rkward
pkgver=0.7.4
pkgrel=2
pkgdesc='An easy to use and easily extensible IDE/GUI for R'
url='https://rkward.kde.org/'
arch=(x86_64)
license=(GPL)
depends=(qt5-webengine qt5-script ktexteditor r)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/rkward/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('7633f3b269f6cf2c067b3b09cbe3da3e0ffdcd9dc3ecb9a9fa63b4f865e8161e')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
