# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: wizzomafizzo <wizzomafizzo@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Benjamin Andresen <benny AT klapmuetz DOT org>
# Contributor: Douglas Thrift <douglas@douglasthrift.net>

pkgname=rlwrap
pkgver=0.46
pkgrel=1
pkgdesc='Adds readline-style editing and history to programs'
arch=('x86_64')
url='https://github.com/hanslub42/rlwrap'
license=('GPL')
depends=('glibc' 'readline' 'ncurses' 'perl')
optdepends=('python: filters')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('407cbfae8aae6042726572e6950c8e3439ee6eaa7a909fd3a7001d3e8725461c811f670b98f11e89a932624a82ab62767867355c8161e30b852e995398d72e5c')
b2sums=('e6a6193a3956c8e7f262da2eae85d8b679267888e0641f5aefd381a92a49d445083c2280cb7db7c74faff8a762875ee425b0fc399beeb553f6109812fe64b83d')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
