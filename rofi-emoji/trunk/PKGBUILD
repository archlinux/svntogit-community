# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Magnus Bergmark <magnus.bergmark@gmail.com>

pkgname=rofi-emoji
pkgver=3.0.1
pkgrel=1
pkgdesc='A Rofi plugin for selecting emojis'
url='https://github.com/Mange/rofi-emoji'
arch=('x86_64')
license=('MIT')
depends=('rofi')
optdepends=(
  'xsel: X11 support'
  'xclip: X11 support'
  'wl-clipboard: Wayland support'
)
source=(https://github.com/Mange/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('a3e649cebfc5af577c6ca402b022dd40f3d634ef00b09fb46affb0974d93f0b4e09094af220b1991d8c2b48100ba3149870395b395c7eda1bcf8f226080e24ea')

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
