# $Id$
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Anatol Pomozov
# Contributor: Benjamin Chr√©tien <chretien + aur [at] lirmm [dot] fr>
# Contributor: Eric Engestrom <aur [at] engestrom [dot] ch>
# Contributor: Rasi <rasi@xssn.at>
# Contributor: Sean Pringle <sean.pringle@gmail.com>
# Contributor: SanskritFritz (gmail)

pkgname=rofi
pkgver=1.5.1
pkgrel=1
_commit=374d4be9d7f5a7d3dcae3925e8192c6b5c97eee0
pkgdesc='A window switcher, application launcher and dmenu replacement'
arch=(x86_64)
url='https://github.com/DaveDavenport/rofi/'
license=(MIT)
depends=(libx11 libxft freetype2 libxdg-basedir 
	pango startup-notification libxcb 
	libxkbcommon libxkbcommon-x11 xcb-util 
	xcb-util-wm xcb-util-xrm librsvg)
makedepends=(meson git check)
optdepends=('i3-wm: use as a window switcher')
source=("git+https://github.com/DaveDavenport/rofi#commit=$_commit"
	'git+https://github.com/sardemff7/libgwater'
	'git+https://github.com/sardemff7/libnkutils')
sha256sums=('SKIP'
            'SKIP'
            'SKIP')
#source=("https://github.com/DaveDavenport/rofi/releases/download/$pkgver/rofi-$pkgver.tar.xz")
#sha256sums=('1a8df8c84316ac0c2b1e9985b346875d8ae4142cbec83f9961a63c73de624984')

prepare() {
  cd rofi

  for module in libgwater libnkutils; do
    local submodule="subprojects/${module}"
    git submodule init "${submodule}"
    git config "submodule.${submodule}.url" "${srcdir}/${module}"
    git submodule update "${submodule}"
  done
}

build() {
  #cd rofi
  #arch-meson build
  cd rofi
  autoreconf -i
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd rofi
  LC_ALL=C make check
}

package() {
  cd rofi
  make install install-man DESTDIR="$pkgdir"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/rofi/COPYING"
  install -dm755 "$pkgdir/usr/share/doc/rofi/examples"
  install -Dm755 Examples/*.sh "$pkgdir/usr/share/doc/rofi/examples"
}
