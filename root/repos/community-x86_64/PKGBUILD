# Maintainer: Konstantin Gizdov < arch at kge dot pw >
# Contributor: Frank Siegert < frank.siegert at googlemail dot com >
# Contributor: Scott Lawrence < bytbox at gmail dot com >
# Contributor: Thomas Dziedzic < gostrc at gmail dot com >
# Contributor: Sebastian Voecking < voeck at web dot de >

pkgbase=root
pkgname=('root' 'root-cuda')
pkgver=6.24.00
pkgrel=2
pkgdesc='C++ data analysis framework and interpreter from CERN'
arch=('x86_64')
url='https://root.cern'
license=('LGPL2.1' 'GPL' 'custom:University of California and Stanford University License')
makedepends=(
    'ccache'
    'cern-vdt'
    'chromium'
    'cfitsio'
    'cmake'
    'cuda'
    'cudnn'
    'gcc-fortran'
    'git'
    'go'
    'libxml2'
    'libmariadbclient'
    'nlohmann-json'
    'ocaml'
    'ocaml-ctypes'
    'openmp'
    'openmpi'
    'openssl'
    'openui5'
    'postgresql-libs'
    'pythia8>=8.2.40-1'
    'python-pygments'
    'qt5-webengine'
    'sqlite'
    'unuran'
    'vc'
    'xrootd>5'
    'z3'
)
depends=(
    'blas'
    'desktop-file-utils'
    'fcgi'
    'fftw'
    'ftgl'
    'giflib'
    'gl2ps'
    'glew'
    'graphviz'
    'gsl'
    'hicolor-icon-theme'
    'intel-tbb'
    'libafterimage'
    'librsvg'
    'libxpm'
    'python'
    'python-numpy'
    'tex-gyre-fonts'
    'unixodbc'
    'xxhash>=0.6.5-1'
    'zstd'
)
optdepends=(
    'cern-vdt: Add a set of fast and vectorisable mathematical functions'
    'chromium: Support for WebGUI'
    'cfitsio: Read images and data from FITS files'
    'libmariadbclient: MySQL support'
    'libxml2: XML parser interface'
    'nlohmann-json: JSROOT support'
    'openmp: Support OpenMP extensions in Minuit2'
    'openmpi: Support OpenMPI extensions in Minuit2'
    'openssl: OpenSSL support'
    'openui5: JSROOT support'
    'postgresql-libs: PostgreSQL support'
    'pythia8>=8.2.40-1: Pythia8 EG support'
    'python-pygments: syntax highlighting in interactive console'
    'qt5-webengine: Support for WebGUI'
    'sqlite: SQLite support'
    'tcsh: Legacy CSH support'
    'unuran: Support non-uniform random numbers'
    'vc: Add types for portable and intuitive SIMD programming'
    'xrootd>5: Support remote file server and client'
    'z3: Suuport the Z3 theorem prover'
)
source=(
    "https://root.cern.ch/download/root_v${pkgver}.source.tar.gz"
    'ROOFIT_LICENSE'
    'root.xml'
    'root.pc.tpl'
    'settings.cmake'
    'settings-cuda.cmake'
    'jupyter_notebook_config.py'
    'nbman-for-arch.patch'
    'thisroot.fail'
)
sha512sums=('bba72cbe310c3ce9a64adc990aaeba6df970f17dc7132d2c3fdc83cc72d24973a3e15c02067a95870794e576e4065478d7a3ed52bfb8f0598294356a7fceff03'
            'af8f178fc9df66997d5495b271e38adcd1636aab4c8fc994c6600c2496127829d831250d73d3fc229b02dfe49b9867d0be979beacb959f2f3a05351b8118a4a6'
            '1fe6f4aa09d583d33f27cc766f4935510bb7ab6bbb8d4700baa1aaab92ea6c876500b67da1e4f6e0b510aa5616e4e193b860264b86925de85f2d9f558d75d5dc'
            '3c81d255a17b902ffac0187af1752847036137e16641a88b17eef0d9c944e6f0d3c954bc93307d6270603f43f6c23f2e04f98dc7a68f9d076dbaa8006a2527d6'
            '4ef971eac7520d27fa88848c8d477243b1fc372e42122e3928092dc68ed2eea19ad0dea6203cf77b61406eea1da7a260a39d2bc116a19e59de99e99a51fb6bf5'
            '99cfdd351f1ec1eaca84fffefa82d1f9f9af691e5d5dd8955b047aac324b8cb0ba2b657074d8e0a707e44f024d2f465a9a410bf7c729f12c0d84fcd17c63ca01'
            '1c905ee7a3f8f5f3f567d957f9be6b503a8631565d4d9b9bfea5e496ef86865c5a8be1a1f8c7842754029879cf0afd2465249f532a116cc43660aa2e460ae682'
            '12814f50b7016bd86d3f91e0e31c052783a0c0fa72b7d6a072d3ae6f86c2437323d585e531235377ebbfdd9cb76abd7da84d9631de821151547f1d4b13417e69'
            'ff555ac4db568affe139701907f86d919a2206f3e304f69dd317b756ea0904b5934d9364a524060778aa507809ce78448621619bb34039ba34c5a71af71a4a8c')

get_pyver () {
    python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "  -> Applying patch $src..."
        patch -Np1 -i "../$src" -d "${srcdir}/${pkgbase}-${pkgver}"
    done

    # specify some custom flags
    # needed by vc to link properly
    CUSTOM_CMAKE_FLAGS="-DTARGET_ARCHITECTURE:STRING=generic"
    # make sure it finds python
    CUSTOM_CMAKE_FLAGS+=" -DPYTHON_EXECUTABLE:PATH=/usr/bin/python"
    # need to set install prefix like so
    CUSTOM_CMAKE_FLAGS+=" -DINSTALL_PREFIX=/usr"
    export CUSTOM_CMAKE_FLAGS

    # update system flags
    # don't let ROOT play around with lib paths
    # the following is no longer necessary
    # sed -i -e 's@SetLibraryPath();@@g' \
    #     "${srcdir}/${pkgbase}-${pkgver}/rootx/src/rootx.cxx"
    # now only depends on IS_RPATH_BUILD being set
    # so pass it to GCC
    export CPPFLAGS="${CPPFLAGS} -DIS_RPATH_BUILD=1"
    # make sure pthread gets detected
    CUSTOM_COMPILER_FLAGS="${CPPFLAGS} -pthread"
    export CFLAGS="${CFLAGS} ${CUSTOM_COMPILER_FLAGS}"
    export CXXFLAGS="${CXXFLAGS} ${CUSTOM_COMPILER_FLAGS}"
    # do not link undefined
    CUSTOM_COMPILER_FLAGS+=" -Wl,--no-undefined"
    export LDFLAGS="${LDFLAGS} ${CUSTOM_COMPILER_FLAGS}"

    # go flags for built-in clang
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -modcacherw"

    # pass CUDA architectures to nvcc calls
    # Not yet supported in root: TBD
    export CUSTOM_CUDA_ARCH='-DCMAKE_CUDA_ARCHITECTURES="52-real 52-virtual
              53-real 53-virtual 60-real 60-virtual
              61-real 61-virtual 62-real 62-virtual
              70-real 70-virtual 72-real 72-virtual
              75-real 75-virtual 80-real 80-virtual
              86-real 86-virtual"'

    cp -r "${pkgbase}-${pkgver}" "${pkgbase}-${pkgver}-cuda"
}

build() {
    ## ROOT
    mkdir -p "${srcdir}/build"
    cd "${srcdir}/build"

    cmake -C "${srcdir}/settings.cmake" \
        ${CUSTOM_CMAKE_FLAGS} \
        "${srcdir}/${pkgbase}-${pkgver}"
    make

    ## ROOT with CUDA
    mkdir -p "${srcdir}/build-cuda"
    cd "${srcdir}/build-cuda"

    cmake -C "${srcdir}/settings-cuda.cmake" \
        ${CUSTOM_CMAKE_FLAGS} \
        ${CUSTOM_CUDA_ARCH} \
        "${srcdir}/${pkgbase}-${pkgver}-cuda"
    make
}

_package() {
    local bld_dir="${srcdir}/${1}"
    cd "${bld_dir}"

    make DESTDIR="${pkgdir}" install

    # fix missing hardlinks for genreflex and rootcint
    cd "${pkgdir}"/usr/bin
    ln -f rootcling rootcint
    ln -f rootcling genreflex
    cd "${bld_dir}" # go back

    # rename the modulemap
    mv "${pkgdir}"/usr/include/module.modulemap "${pkgdir}"/usr/include/root.modulemap

    # fix python env call
    sed -e 's/@python@/python/' -i "${pkgdir}/usr/lib/root/cmdLineUtils.py"

    # try to deal with weird PyROOT, PyMVA and JupyROOT stuff
    rm -rf "${pkgdir}/usr/lib/root/__pycache__"
    local _pyver=$(get_pyver)
    local _pydir="${pkgdir}/usr/lib/python${_pyver}/site-packages"
    install -d "${_pydir}"
    find "${pkgdir}/usr/lib/root" -maxdepth 1 -mindepth 1 \( -iname '*py*' -or -name '*Js*' -or -name 'ROOT' \) \
            ! \( -name '*EGPythia8*' -or -iname '*.rootmap' -or -iname '*.pcm' \) -print0 | while read -rd $'\0' _lib; do
        _base=$(basename "${_lib}")
        ln -sf "/usr/lib/root/${_base}" "${pkgdir}/usr/lib/python${_pyver}/site-packages/${_base}"
    done

    # recompile pycache to strip $pkgdir from embedded paths
    python -m compileall -d "/usr/lib/python${_pyver}" \
        "${pkgdir}/usr/lib/python${_pyver}"
    python -O -m compileall -d "/usr/lib/python${_pyver}" \
        "${pkgdir}/usr/lib/python${_pyver}"

    # icon, shortcut and mime
    install -Dm644 "${srcdir}/${pkgbase}-${pkgver}/icons/Root6Icon.png" \
        "${pkgdir}/usr/share/icons/hicolor/48x48/apps/root.png"
    install -Dm644 "${srcdir}/${pkgbase}-${pkgver}/etc/root.desktop" \
        "${pkgdir}/usr/share/applications/root.desktop"
    echo 'Icon=root.png' >> "${pkgdir}/usr/share/applications/root.desktop"
    install -Dm644 "${srcdir}/root.xml" \
        "${pkgdir}/usr/share/mime/packages/root.xml"

    # use a file that pacman can track instead of adding directly to ld.so.conf
    install -d "${pkgdir}/etc/ld.so.conf.d"
    echo '/usr/lib/root' > "${pkgdir}/etc/ld.so.conf.d/root.conf"

    # create pkg-config file
    local _prefix _exec_prefix _bindir _libdir _incdir _pkg_ver _libs _cflags _requires
    _prefix="$("${pkgdir}"/usr/bin/root-config --prefix)"
    _exec_prefix="$("${pkgdir}"/usr/bin/root-config --exec-prefix)"
    _bindir="$("${pkgdir}"/usr/bin/root-config --bindir)"
    _libdir="$("${pkgdir}"/usr/bin/root-config --libdir)"
    _incdir="$("${pkgdir}"/usr/bin/root-config --incdir)"
    _pkg_ver="$(sed -n 's,.*ROOT_RELEASE *\"\(.*\)\".*,\1,p' < "${pkgdir}"/usr/include/RVersion.h)"
    _libs="$("${pkgdir}"/usr/bin/root-config --libs)"
    _cflags="$("${pkgdir}"/usr/bin/root-config --cflags)"
    printf -v _requires '%s,' "${depends[@]}"
    cp "${srcdir}/root.pc.tpl" "${bld_dir}"/
    sed -e "s@_PREFIX@${_prefix}@" -e "s@_EXECPREFIX@${_exec_prefix}@" \
        -e "s@_LIBDIR@${_libdir}@" -e "s@_INCDIR@${_incdir}@" \
        -e "s@_PKGVERSION@${_pkg_ver}@" -e "s@_LIBRARIES@${_libs}@" \
        -e "s@_CFLAGS@${_cflags}@" -e "s@_UPSTREAM_URL@${url}@" \
        -e "s@_REQUIRES@${_requires}@" \
        -i "${bld_dir}/root.pc.tpl"
    install -Dm644 "${bld_dir}/root.pc.tpl" "${pkgdir}/usr/lib/pkgconfig/root.pc"

    # install all licenses & docs
    install -d "${pkgdir}/usr/share/licenses/roofit"
    install "${srcdir}/ROOFIT_LICENSE" "${pkgdir}/usr/share/licenses/roofit/LICENSE"
    install -d "${pkgdir}/usr/share/licenses/${pkgname}"
    ln -s '/usr/share/doc/root/LICENSE' "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    for _fold in fonts js; do
      install -d "${pkgdir}/usr/share/licenses/${pkgname}/${_fold}"
      ln -s "/usr/share/root/${_fold}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/${_fold}"/
    done
    ln -s '/usr/share/licenses/roofit' "${pkgdir}/usr/share/licenses/${pkgname}/roofit"
    if [ "${pkgname}" != "root" ]; then
        ln -s "/usr/share/licenses/${pkgname}" "${pkgdir}/usr/share/licenses/root"
        ln -s "/usr/share/doc/root" "${pkgdir}/usr/share/doc/${pkgname}"
    fi

    # install jupyter kernels and `root --notebook` config
    install -d "${pkgdir}/usr/share/jupyter/kernels"
    ln -s '/etc/root/notebook/kernels/root' "${pkgdir}/usr/share/jupyter/kernels/root"
    install "${srcdir}/jupyter_notebook_config.py" "${pkgdir}/etc/root/notebook"/

    # drop thisroot.* shell files
    rm -rf "${pkgdir}"/usr/bin/thisroot.*
    install -Dm755 "${srcdir}/thisroot.fail" "${pkgdir}/usr/bin/thisroot.sh"
    for suffix in csh fish; do
        ln -s '/usr/bin/thisroot.sh' "${pkgdir}/usr/bin/thisroot.${suffix}"
    done

    # set correct LD_LIBRARY_PATH at runtime
    sed -i.orig \
        -e "s@#Unix.*.Root.DynamicPath:.*@Unix.*.Root.DynamicPath:    .:${_libdir}:/usr/lib@" \
        "${pkgdir}/etc/root/system.rootrc"
}

package_root() {
    optdepends+=('gcc-fortran: Enable the Fortran components of ROOT')
    _package build
}

package_root-cuda() {
    pkgdesc='C++ data analysis framework and interpreter from CERN with GPU (CUDA) features enabled'
    provides=('root')
    conflicts=('root')
    depends+=('cuda' 'cudnn')
    optdepends+=('gcc-fortran: Enable the Fortran components of ROOT')
    _package build-cuda
}
