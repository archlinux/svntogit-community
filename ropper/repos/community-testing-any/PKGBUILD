# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=ropper
pkgname=('ropper' 'python-ropper' 'python2-ropper')
_pkgname=Ropper
pkgver=1.11.6
pkgrel=2
pkgdesc='Show information about binary files and find gadgets to build rop chains for different architectures'
url='https://github.com/sashs/Ropper'
arch=('any')
license=('GPL2')
makedepends=('python-setuptools' 'python-capstone' 'python-filebytes'
             'python2-setuptools' 'python2-capstone' 'python2-filebytes')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('3609aaa60650c48602a48a2f83702b41227fa749cbfcfd76a2dd880900a69b9fc75208fa875eb262766c66c67727046a0687670cfbd203f77c806facd5fa59e8')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
  sed -r 's|(env python)|\12|' -i ${_pkgname}-${pkgver}-py2/sample.py
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    python test.py
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 test.py
  )
}

package_ropper() {
  depends=('python' "python-ropper>=${pkgver}")
  optdepends=('python-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/lib" "${pkgdir}/usr/bin/ropper2"
}

package_python-ropper() {
  depends=('python-capstone' 'python-filebytes')
  optdepends=('python-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/bin"
}

package_python2-ropper() {
  depends=('python2-capstone' 'python2-filebytes')
  optdepends=('python2-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
