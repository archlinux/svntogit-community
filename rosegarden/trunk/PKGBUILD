# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=22.06
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=(x86_64)
url="https://www.rosegardenmusic.com/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc lirc hicolor-icon-theme qt5-base zlib)
makedepends=(alsa-lib cmake dssi fftw jack ladspa liblrdf liblo
libsamplerate libsndfile qt5-tools)
checkdepends=(lilypond python tex-gyre-fonts ttf-dejavu xorg-server-xvfb)
optdepends=(
  'cups: Printing support'
  'evince: Print preview support'
  'lilypond: Notation display support'
  'mupdf: Print preview support'
  'okular: Print preview support'
  'timidity++: Softsynth support'
  'flac: FLAC support'
  'wavpack: Lossless audio codec support'
)
provides=(dssi-host ladspa-host)
options=(debug)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
sha512sums=('42ce03a65f9c9401cb3f641c0ae80a827e18d6f47420add382f63273f4644eac5997befe70cac972380441a43d74153a4e37a276fc3dc8d9889eb8671759ab5a')
b2sums=('d4ba13ac6b4cba6d0b5bc555933737d696d380737e56a3e4e1dd782aff641dcbd49af50aade75550b8a0e22a45f491585ffb4166b21cb9889eca1a2e546c585a')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  cmake --build build
}

check() {
  mkdir -p test-tmp
  chmod 700 test-tmp
  export XDG_RUNTIME_DIR="${PWD}/test-tmp"
  # disable broken lilypond_export_test: https://sourceforge.net/p/rosegarden/bugs/1640/
  # disable tests incompatible with debug symbols: https://sourceforge.net/p/rosegarden/bugs/1641/
  xvfb-run ctest --output-on-failure --test-dir build/test -E '^(lilypond_export_test|test_notationview_selection|convert)$'
}

package() {
  depends+=(libasound.so libfftw3f.so libjack.so liblrdf.so liblo.so
  libsamplerate.so libsndfile.so)

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,CHANGELOG,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
