# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <bytecurry.software@gmail.com>

pkgname=roswell
pkgver=21.10.14.111
pkgrel=2
pkgdesc='Environment for Common Lisp development'
arch=('x86_64')
url='https://roswell.github.io'
license=('MIT')
depends=('curl')
makedepends=('git')
_commit='5be94a34eb2f3214aa7afca7f8fe7a16c67b9fb7'
source=("$pkgname::git+https://github.com/roswell/roswell#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  ./bootstrap

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" COPYING
}
