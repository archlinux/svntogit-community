# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=roundcubemail
pkgver=1.2.1
pkgrel=1
pkgdesc="A PHP web-based mail client"
arch=('any')
url="http://www.roundcube.net"
license=('GPL')
depends=('php')
makedepends=('php-composer' 'git')
optdepends=('python2')
backup=('etc/webapps/roundcubemail/.htaccess'
	'etc/webapps/roundcubemail/apache.conf')
install=roundcubemail.install
options=('!strip' 'emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/roundcube/roundcubemail/archive/${pkgver/rc/-rc}.tar.gz"
	apache.conf)
md5sums=('656349f1001ffbcf74066db2f5ebde05'
         'f11b17e2a80b383cde4af963fb307541')

prepare() {
#  mv ${srcdir}/roundcubemail-${_commit} ${srcdir}/roundcubemail-${pkgver/rc/-rc}
  cd ${srcdir}/roundcubemail-${pkgver/rc/-rc}
  sed -i \
    -e "s|RCUBE_INSTALL_PATH . 'temp.*|'/var/cache/roundcubemail';|" \
    -e "s|RCUBE_INSTALL_PATH . 'logs.*|'/var/log/roundcubemail';|" \
    config/defaults.inc.php \
    program/lib/Roundcube/rcube_config.php
  mv composer.json-dist composer.json
  composer install --no-dev
}

package() {
  mkdir -p ${pkgdir}/etc/webapps/roundcubemail
  mkdir -p ${pkgdir}/usr/share/webapps
  mkdir -p ${pkgdir}/var/log
  cd ${pkgdir}/usr/share/webapps
  cp -ra ${srcdir}/roundcubemail-${pkgver/rc/-rc} roundcubemail
  cd roundcubemail

  mv .htaccess $pkgdir/etc/webapps/roundcubemail/
  ln -s /etc/webapps/roundcubemail/.htaccess .htaccess

  mv config $pkgdir/etc/webapps/roundcubemail/
  ln -s /etc/webapps/roundcubemail/config config

  install -dm0750 $pkgdir/var/{log,cache}/roundcubemail
  install -Dm0644 $srcdir/apache.conf $pkgdir/etc/webapps/roundcubemail/apache.conf

#  install -dm0755 $pkgdir/etc/php/conf.d/
#  cat <<'EOF' >$pkgdir/etc/php/conf.d/$pkgname.ini
#open_basedir = ${open_basedir}:/etc/webapps/roundcubemail:/usr/share/webapps/roundcubemail:/var/log/roundcubemail:/var/cache/roundcubemail
#EOF

  rm -rf temp logs

  sed -i "s|$srcdir/roundcubemail-${pkgver/rc/-rc}/vendor/pear-pear.php.net/Console_CommandLine/data|/usr/share/webapps/roundcubemail/vendor/pear-pear.php.net|g" \
    vendor/pear-pear.php.net/Console_CommandLine/Console/CommandLine/XmlParser.php

  sed -i "s|$srcdir/roundcubemail-${pkgver/rc/-rc}|/usr/share/webapps/roundcubemail|g" \
    vendor/pear-pear.php.net/Crypt_GPG/bin/crypt-gpg-pinentry \
    vendor/pear-pear.php.net/Crypt_GPG/Crypt/GPG/Engine.php \
    vendor/pear-pear.php.net/Crypt_GPG/Crypt/GPG/PinEntry.php
}
