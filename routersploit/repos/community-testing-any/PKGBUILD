# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=routersploit
pkgver=3.4.0
pkgrel=4
pkgdesc='Open-source exploitation framework dedicated to embedded devices'
url='https://github.com/threat9/routersploit'
arch=('any')
license=('BSD')
depends=('python' 'python-future' 'python-requests' 'python-paramiko' 'python-pysnmp' 'python-pycryptodome')
checkdepends=('python-pytest' 'python-pytest-forked' 'python-pytest-xdist' 'flake8' 'python-threat9-test-bed')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/threat9/routersploit/archive/v${pkgver}.tar.gz
        ${pkgname}-py38.patch::https://github.com/threat9/routersploit/commit/47183c74a49f.patch
        ${pkgname}-py39.patch::https://github.com/threat9/routersploit/commit/8c4bc6f79ed6.patch)
sha512sums=('9182598105c4d1c971dc63ac24059469dd1862af204f59994cfc85012091663a85faee4c7c04cce573eef1fa91525b44a3963b8c24460d3b31595c4ef6a6c4e5'
            'aaa6c84f4ce1874b0d389a75bfeec370ff6b5b3d078756556caad2a4f8b5219912f879c0e38637fa37e613f07001768b66a225cfdc4a92336eaecf5e5a38f9be'
            '81e376e545acd1a1b00bc00f504d031b99709d4fa5076cca2d72866627b597290b2561a3729d88e92e389e2ef30df903bc53f9522cbfaeb9b6a303f020e0d2f4')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../${pkgname}-py38.patch
  patch -Np1 -i ../${pkgname}-py39.patch

  # Remove failing tests
  rm tests/exploits/misc/miele/test_pg8528_path_traversal.py
  rm tests/exploits/cameras/xiongmai/test_uc_httpd_path_traversal.py
  rm tests/exploits/routers/dlink/test_dns_320l_327l_rce.py
  rm tests/exploits/routers/tplink/test_wdr740nd_wdr740n_path_traversal.py
}

check() {
  cd ${pkgname}-${pkgver}
  make tests
}

package() {
  cd ${pkgname}-${pkgver}

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  rm -rf "${pkgdir}"/usr/lib/python*/site-packages/tests
  mv "${pkgdir}/usr/bin/rsf"{.py,}

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
