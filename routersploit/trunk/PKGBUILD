# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordian Edenhofer <gordian.edenhofer@gmail.com>

pkgname=routersploit
pkgver=3.4.0
pkgrel=2
pkgdesc='Open-source exploitation framework dedicated to embedded devices'
url='https://github.com/threat9/routersploit'
arch=('any')
license=('BSD')
depends=('python' 'python-future' 'python-requests' 'python-paramiko' 'python-pysnmp' 'python-pycryptodome')
checkdepends=('python-pytest' 'python-pytest-forked' 'python-pytest-xdist' 'flake8' 'python-threat9-test-bed')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/threat9/routersploit/archive/v${pkgver}.tar.gz)
sha512sums=('9182598105c4d1c971dc63ac24059469dd1862af204f59994cfc85012091663a85faee4c7c04cce573eef1fa91525b44a3963b8c24460d3b31595c4ef6a6c4e5')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/iteritems(attrs)/iteritems(attrs.copy())/' routersploit/core/exploit/exploit.py
}

check() {
  cd ${pkgname}-${pkgver}
  rm tests/exploits/routers/dlink/test_dns_320l_327l_rce.py
  make tests
}

package() {
  cd ${pkgname}-${pkgver}

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  rm -rf "${pkgdir}"/usr/lib/python*/site-packages/tests
  mv "${pkgdir}/usr/bin/rsf"{.py,}

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/* "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
