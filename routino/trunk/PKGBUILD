# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>

pkgname=routino
pkgver=3.2
pkgrel=1
pkgdesc='Router for OpenStreetMap Data'
arch=('i686' 'x86_64')
url='http://www.routino.org/'
license=('AGPL3')
depends=('zlib' 'bzip2')
source=(http://www.routino.org/download/$pkgname-$pkgver.tgz)
sha256sums=('e2a431eaffbafab630835966d342e4ae25d5edb94c8ed419200e1ffb50bc7552')

prepare() {
  cd "$pkgname-$pkgver"

#paths fix
  sed -i 's|prefix=/usr/local|prefix=/usr|' Makefile.conf
  sed -i 's|/doc/routino|/share/doc/routino|' Makefile.conf

#RPATH fix
  sed -i 's|LDFLAGS_LDSO=-Wl,-R.||' Makefile.conf
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
