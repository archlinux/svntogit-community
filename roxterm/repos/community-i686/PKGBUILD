# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=roxterm
pkgver=2.9.3
pkgrel=2
pkgdesc='Tabbed, VTE-based terminal emulator'
arch=('i686' 'x86_64')
url='http://roxterm.sourceforge.net/'
license=('GPL3')
depends=('dbus-glib' 'vte290' 'hicolor-icon-theme' 'libsm')
makedepends=('docbook-xsl' 'xmlto' 'po4a' 'python2' 'python2-lockfile' 'imagemagick' 'librsvg' 'itstool')
install=roxterm.install
source=("http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.bz2"
        "http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.bz2.sign")
md5sums=('b42d633332ce0c71665f87bc734107d0'
         'SKIP')
sha1sums=('53bc33e69d199151d2d5bb63bc5d38d5b0b4b089'
          'SKIP')


build() {
  cd "${srcdir}/roxterm-${pkgver}"

  # FS#38967: Reenable deprecated VTE3 functions
  python2 mscript.py configure --prefix='/usr' \
      --enable-deprecated-bg-opts
  python2 mscript.py build
}

package() {
  cd "${srcdir}/roxterm-${pkgver}"
  python2 mscript.py install --destdir="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
