# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=roxterm
pkgver=2.9.7
pkgrel=1
pkgdesc='Tabbed, VTE-based terminal emulator'
arch=('i686' 'x86_64')
url='http://roxterm.sourceforge.net/'
license=('GPL3')
depends=('dbus-glib' 'vte290' 'hicolor-icon-theme' 'libsm')
makedepends=('docbook-xsl' 'xmlto' 'po4a' 'python2' 'python2-lockfile'
             'imagemagick' 'librsvg' 'itstool')
install=roxterm.install
source=("http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.bz2"
        "http://downloads.sourceforge.net/roxterm/roxterm-$pkgver.tar.bz2.sign")
md5sums=('496b5ddc078cede502f168dadef46cb6'
         'SKIP')
sha1sums=('b88fe4d807b5cd96128d8320793569405c4b208e'
          'SKIP')
validpgpkeys=('2FF283656D0745E54850B1C0BF0EBCD13D97CD09') # Tony Houghton

build() {
  cd "${srcdir}/roxterm-${pkgver}"

  # FS#38967: Reenable deprecated VTE3 functions
  python2 mscript.py configure --prefix='/usr' \
      --enable-deprecated-bg-opts
  python2 mscript.py build
}

package() {
  cd "${srcdir}/roxterm-${pkgver}"
  python2 mscript.py install --destdir="${pkgdir}"
}

# vim:set ts=2 sw=2 et:
