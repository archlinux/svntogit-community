# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: kpcyrd <git@rxv.cc>

pkgname=rshijack
pkgver=0.3.0
pkgrel=2
pkgdesc='TCP connection hijacker, rust rewrite of shijack'
url='https://github.com/kpcyrd/rshijack'
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('GPL3')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('f9cc6be73002f6dff621eafb6570fcf876c234142489b3c7f84012d9dc8239e3')
sha512sums=('357d2cf47c73213d24546300c5e90ea01d6bbd196a736fb886ac65a02742ce4ca35f8c4bfd93c3d61ad3e82bdbae10d7b8a82101811cf8fbc96d01299b23007b')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
