# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rsibreak
pkgver=0.12.14
pkgrel=1
pkgdesc='Takes care of your health and regularly breaks your work to avoid repetitive strain injury (RSI)'
url='https://apps.kde.org/rsibreak/'
arch=(x86_64)
license=(GPL)
depends=(knotifyconfig kidletime hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1ce37ef875ffa1964db80ddb0898b3f762134592f573fabb9950c467b92a5b7a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
