# $Id$
# Maintainer:
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Thorsten Tasch <tht@thorstentasch.de>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=rsnapshot
pkgver=1.4.0
pkgrel=1
pkgdesc="A remote filesystem snapshot utility"
arch=('any')
url="http://www.rsnapshot.org"
license=('GPL')
depends=('perl' 'rsync' 'openssh' 'perl-lchown')
backup=('etc/rsnapshot.conf')
source=("http://www.rsnapshot.org/downloads/${pkgname}-${pkgver}.tar.gz"
        'rsnapshot'
		'0001-added-missing-back.patch'
		"pod-fix.patch")
md5sums=('4011768eb6ec122a0f9479275ed64a27'
         '4cd16eaa51d58a652cffde945985e91c'
         'a7075d9366eda5e699be86f7ffe8246e'
         '0d889cbef6fddf8401611bc16ed63c5c')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i "$srcdir/0001-added-missing-back.patch"
	patch -p1 -i "$srcdir/pod-fix.patch"
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
	sed -i 's:/usr/bin/pod2man:/usr/bin/core_perl/pod2man:' Makefile
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR=${pkgdir} install
	mv ${pkgdir}/etc/rsnapshot.conf.default ${pkgdir}/etc/rsnapshot.conf
	install -Dm644 ${srcdir}/rsnapshot ${pkgdir}/etc/logrotate.d/rsnapshot
}
