# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rstcheck
pkgver=3.3.1
pkgrel=6
pkgdesc='Checks syntax of reStructuredText and code blocks nested within it'
arch=('any')
url=https://github.com/myint/rstcheck
license=('MIT')
depends=('python-docutils')
makedepends=('python-setuptools')
checkdepends=('python-sphinx')
optdepends=('python-sphinx: Sphinx support')
source=("$url/archive/v$pkgver/rstcheck-v$pkgver.tar.gz")
b2sums=('3aa20104645add4fbf7d47423def9166b98d67a7b132759f0ef0a0c10b36857dd0bd9a8ba180256b0208e8e7fc27fe20065bb2b5995988a9e738be20b14ef765')

build() {
  cd rstcheck-$pkgver
  python setup.py build
}

check() {
 cd rstcheck-$pkgver
 ./test_rstcheck.py
}

package() {
  cd rstcheck-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/rstcheck LICENSE
}

# vim:set ts=2 sw=2 et:
