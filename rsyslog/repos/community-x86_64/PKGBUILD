# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rsyslog
pkgver=5.8.4
pkgrel=2
pkgdesc="An enhanced multi-threaded syslogd with a focus on security and reliability"
url="http://www.rsyslog.com/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('zlib')
makedepends=('postgresql-libs>=8.4.1' 'libmysqlclient' 'net-snmp' 'gnutls')
optdepends=('postgresql-libs: PostgreSQL Database Support'
	    'libmysqlclient: MySQL Database Support'
	    'net-snmp'
	    'gnutls')
backup=('etc/rsyslog.conf' \
	'etc/logrotate.d/rsyslog'
	'etc/conf.d/rsyslog')
options=('strip' 'zipman' '!libtool')
source=("http://www.rsyslog.com/files/download/rsyslog/rsyslog-$pkgver.tar.gz"
	'rsyslog'
	'rsyslog.logrotate'
	'rsyslog.conf.d')
md5sums=('a2c2a65ac84d9a895c52a754aff61986'
         'a18bbcbb6ebdaa13a6ec6d9f3d9eb2da'
         '8065db4bef3061a4f000ba58779f6829'
         '1a0cd4530dd5d1439456d5ae230574d9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --enable-mysql \
              --enable-pgsql \
              --enable-mail \
              --enable-imfile \
              --enable-imtemplate \
              --enable-snmp \
              --enable-gnutls \
              --enable-inet \
              --with-systemdsystemunitdir=/lib/systemd/system
  make
}
package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make install DESTDIR=${pkgdir}
  # Install Daemons and Configuration Files
  install -D -m755 ${srcdir}/${pkgname} ${pkgdir}/etc/rc.d/${pkgname}d
  install -D -m644 ${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf
  install -D -m644 $srcdir/${pkgname}.logrotate ${pkgdir}/etc/logrotate.d/${pkgname}
  install -D -m644 ${srcdir}/${pkgname}.conf.d ${pkgdir}/etc/conf.d/${pkgname}
}
