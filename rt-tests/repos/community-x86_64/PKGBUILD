# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=2.1
pkgrel=1
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/about/"
license=('GPL2')
groups=('realtime')
depends=('glibc')
makedepends=('numactl' 'python')
optdepends=('bash: for determine_maximum_mpps.sh'
            'numactl: for cyclictest, oslat and signaltest'
            'python: for get_cyclictest_snapshot and hwlatdetect')
source=("https://git.kernel.org/pub/scm/utils/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('80d69d4ea787d6f635b7c26909940d7a2b1529778f61de47a95e6dda4baf165be5cca03aaf04d22428f623ae54011d7fc7cd177594fc618ee61122de5b26e0e2')
b2sums=('1c1f1cbba952b23b4ce63cc2c78ddbda6dc6a3ead60cc51a1b0e020800872a076ae13db6312d546fd2e9b7c826f50ebde90eb1f5fb12fb91627af407c7081a9b')

build() {
  cd "${pkgname}-${pkgver}"
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -vDm 644 {MAINTAINERS,README.markdown} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
