# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=2.4
pkgrel=1
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/about/"
license=(GPL2)
groups=(realtime)
depends=(glibc)
makedepends=(numactl python)
optdepends=(
  'bash: for determine_maximum_mpps.sh'
  'numactl: for cyclictest, oslat and signaltest'
  'python: for get_cyclictest_snapshot and hwlatdetect'
)
source=(https://git.kernel.org/pub/scm/utils/$pkgname/$pkgname.git/snapshot/$pkgname-$pkgver.tar.gz)
sha512sums=('34c157cc0ad049146560812ea61be073730610081723c5d1ab93c8d0c83017991a6c5b06eac7bb6dc974b35a57591f0056a1e0ab912906e2c6d31ba8a1ced497')
b2sums=('d359843009818d54c999724731810f61d9411b8c839f3ca0758a07d5e48abbcbcc4742dd9fad7e8d4c5b380be933f13df24dca80ba2a1aec34510e2b417b0bf4')

build() {
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make -C $pkgname-$pkgver
}

package() {
  make prefix=/usr DESTDIR="${pkgdir}" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{MAINTAINERS,README.markdown} -t "$pkgdir/usr/share/doc/$pkgname/"
}
