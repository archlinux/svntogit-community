# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor:  Daenyth <Daenyth [at] gmail [dot] com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=rtorrent
pkgver=0.9.6
pkgrel=3
pkgdesc='Ncurses BitTorrent client based on libTorrent'
url='http://rakshasa.github.io/rtorrent/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('libtorrent=0.13.6' 'curl' 'xmlrpc-c' 'libsigc++')
#source=("http://libtorrent.rakshasa.no/downloads/${pkgname}-${pkgver}.tar.gz")
#sha1sums=('e997822e9b0d53cf8ecfb6b836e380065890e10d')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rakshasa/${pkgname}/archive/${pkgver}.tar.gz")
sha1sums=('27505081254618077c291eb1ee36bfb41f974834')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed '/AM_PATH_CPPUNIT/d' -i configure.ac
	./autogen.sh

	export CXXFLAGS="${CXXFLAGS} -fno-strict-aliasing"
	./configure \
		--prefix=/usr \
		--disable-debug \
		--with-xmlrpc-c \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D doc/rtorrent.rc "${pkgdir}"/usr/share/doc/rtorrent/rtorrent.rc
}
