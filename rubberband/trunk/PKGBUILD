# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

pkgname=rubberband
pkgver=2.0.0
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=('x86_64')
url="https://www.breakfastquay.com/rubberband/"
license=('GPL2')
depends=('gcc-libs' 'glibc')
makedepends=('fftw' 'ladspa' 'meson' 'java-environment' 'libsamplerate'
'libsndfile' 'vamp-plugin-sdk')
optdepends=(
  'ladspa-host: for LADSPA plugin'
  'vamp-host: for VAMP plugin'
  'vamp-plugin-sdk: for VAMP plugin'
)
provides=('librubberband.so' 'librubberband-jni.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/breakfastquay/rubberband/archive/v$pkgver.tar.gz")
sha512sums=('e8ad34d77f196df971630a182dc214775386a43f35aa990678170e9203d97c19550a8f6467d46df29b5766702b10df5aaf612f804068da83872af621a8ea1d67')
b2sums=('48cc6fd80b182c87ed5fcc1fe3d9eb9c3bb748b713866099c6deceba70435fbc2035240ab7055a8aea2f89f8bf80dc29c5fb567506532504fd2b0151248215c0')

build() {
  local java_major=$(java --version 2>/dev/null |grep 'openjdk'| cut -d ' ' -f2| cut -d '.' -f1)
  export JAVA_HOME="/usr/lib/jvm/java-${java_major}-openjdk"
  cd "$pkgname-$pkgver"
  arch-meson -Dfft=fftw \
            -Dresampler=libsamplerate \
            -Dextra_include_dirs="/usr/lib/jvm/java-${java_major}-openjdk/include,/usr/lib/jvm/java-${java_major}-openjdk/include/linux" \
            build
  ninja -C build
}

package() {
  depends+=('libfftw3.so' 'libsamplerate.so' 'libsndfile.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
