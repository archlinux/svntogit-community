# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=rubinius
pkgname=(rubinius rubinius-ruby)
pkgver=3.107
pkgrel=1
pkgdesc="Ruby runtime written in Ruby, designed for concurrency"
arch=(x86_64)
url="https://rubinius.com"
license=(custom:BSD MPL2)
depends=(libffi openssl-1.0 libyaml)
makedepends=(llvm ruby ruby-rake clang valgrind)
options=(!emptydirs)
source=(http://releases.rubinius.com/${pkgbase}-${pkgver}.tar.bz2
        gemrc dirs.patch)
sha256sums=('46d68cb26ce83fb503b25776770abad6a55ef03a14cd4fd05f44e17becb71589'
            '4bb7eb2fe66e396ed16b589cdb656831407b39ad4e138d88536754c0448ac614'
            '01eaefc509d3192ade1115282b42e06a72b0ab9353b61d1779db296bb832e2f2')

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../dirs.patch
}

build() {
  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig

  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --includedir=/usr/include/rubinius \
    --appdir=/usr/lib/rubinius \
    --gemsdir=/usr/lib/rubinius/gems \
    --bin-link bundle \
    --bin-link bundler \
    --preserve-prefix \
    --without-rpath \
    --release-build
  rake build
}

check() {
  cd $pkgbase-$pkgver
  rake vm:test
}

package_rubinius() {
  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig

  cd $pkgbase-$pkgver
  DESTDIR="$pkgdir" rake install:files
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE BSD_LICENSE

### Split rubinius-ruby
  mkdir -p "$srcdir/ruby/usr/bin"
  for _f in "$pkgdir"/usr/bin/*; do
    [[ $_f == */rbx ]] && continue
    mv $_f "$srcdir/ruby/usr/bin"
  done
}

package_rubinius-ruby() {
  pkgdesc="Ruby compat for Rubinius"
  depends=("rubinius=$pkgver-$pkgrel")
  provides=(ruby)
  conflicts=(ruby)
  backup=(etc/gemrc)

  mv ruby/* "$pkgdir"
  install -Dt "$pkgdir/etc" -m644 gemrc

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s rubinius "$pkgdir/usr/share/licenses/$pkgname"
}
