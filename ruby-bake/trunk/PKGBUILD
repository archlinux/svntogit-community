# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-bake
pkgver=0.18.2
pkgrel=3
pkgdesc='A replacement for rake with a simpler syntax'
arch=(any)
url='https://github.com/ioquatix/bake'
license=(MIT)
depends=(ruby-samovar)
checkdepends=(ruby-covered ruby-rspec)
options=(!emptydirs)
source=(https://github.com/ioquatix/bake/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('78297c5c9f3218b7d774a6623166ea63bdc8f45398f65c9fbfc390c2d0ba59ee')

prepare() {
  cd bake-$pkgver
  sed -r -e 's|~>|>=|g' -e '/signing_key/d' -i bake.gemspec
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$pkgver
  gem build bake.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    bake-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd bake-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd bake-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
