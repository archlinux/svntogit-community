# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=colorator
pkgname=ruby-colorator
pkgver=1.1.0
pkgrel=4
pkgdesc='Colorize your text in the terminal'
url='https://github.com/octopress/colorator'
arch=('any')
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/octopress/colorator/archive/v${pkgver}.tar.gz)
sha256sums=('9b96af8a6b58aa23aa2a006e2f0696dd54a022251ec39db11ccbfa0f7d51da14')
sha512sums=('7bc2e85f8c4db8ca6db5140c9ebb471d3b1733c1c408595eb0bec531ec5f08fc86f4ee793a451bf51f359c8d4f07beac9e5ae9f418e2752be1c4fb0442b802de')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0\|sed "s,\\\\./,,g"|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.markdown -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
