# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='concurrent-ruby'
pkgname='ruby-concurrent'
pkgver=1.1.5
pkgrel=2
pkgdesc='Modern concurrency tools including agents, futures, promises, thread pools, actors, supervisors, and more.'
arch=('any')
url='https://github.com/ruby-concurrency/concurrent-ruby'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
optdepends=('java-runtime>=8: jruby support')
options=('!emptydirs')
source=("https://rubygems.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('357fa6bbf65ff4143adba9de6713d28eacb03170b3b4218f53d80433de13ba50b1c3741a62ec355a3aa01388bcbd059e1bca3dd82c88cf56b8b3110c24b37613')

package() {
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
