# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-cucumber-ci-environment
pkgver=9.1.0
pkgrel=2
pkgdesc='Detect CI Environment from environment variables'
arch=(any)
url='https://github.com/cucumber/ci-environment'
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rspec)
options=(!emptydirs)
source=(https://github.com/cucumber/ci-environment/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a6059b98cd7cfff242902bd69275f327ade36db0e84af76e36c1601c451219e2')

build() {
  local _gemdir="$(gem env gemdir)"
  cd ci-environment-$pkgver/ruby
  rake lib/cucumber/ci_environment/CiEnvironments.json
  gem build cucumber-ci-environment.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-ci-environment-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd ci-environment-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd ci-environment-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
