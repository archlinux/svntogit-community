# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-cucumber-gherkin
pkgver=26.0.1
pkgrel=1
pkgdesc="Gherkin for Ruby"
arch=(any)
url='https://github.com/cucumber/gherkin/blob/main/ruby'
license=(MIT)
depends=(ruby-cucumber-messages)
makedepends=(ruby-rake ruby-rspec)
options=(!emptydirs)
source=(https://github.com/cucumber/gherkin/archive/v$pkgver/cucumber-gherkin-$pkgver.tar.gz)
sha256sums=('e7a6f800377a8f0f9b16c37cc3b4ddfd9b2fe8582abf435bf36119acd4a77052')

prepare() {
  cd gherkin-$pkgver/ruby
  sed -r 's|~>|>=|g' -i cucumber-gherkin.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  gem build cucumber-gherkin.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    cucumber-gherkin-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd gherkin-$pkgver/ruby
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd gherkin-$pkgver/ruby
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
