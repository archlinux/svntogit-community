# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contributor: Jochen Schalanda <jochen+aur@schalanda.name>

pkgname=ruby-faraday
pkgver=0.14.0
pkgrel=1
pkgdesc='HTTP/REST API client library.'
arch=('any')
url='https://github.com/lostisland/faraday'
license=('MIT')
depends=('ruby' 'ruby-multipart-post')
options=('!emptydirs')
source=("https://rubygems.org/downloads/faraday-$pkgver.gem")
noextract=("faraday-$pkgver.gem")
sha512sums=('9e1f6b604aed676679a4ca1b62f6b7f01d6953a4b0187f40bb52b74448ecd7cdf4a22b1a3ba1b3424da13fa4e4ca16b248d3f6e4c85b77d58130967e5f9b6aef')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" faraday-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/faraday-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/faraday-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
