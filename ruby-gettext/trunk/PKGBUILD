# Maintainer: tim@bastelfreak.de
# Contributor: Viktor Drobot (aka dviktor) linux776 [at] gmail [dot] com

_gemname='gettext'
pkgname="ruby-${_gemname}"
pkgver=3.3.7
pkgrel=2
pkgdesc='Gettext gem is a pure Ruby Localization(L10n) library and tool which is modeled after the GNU gettext package.'
arch=(any)
url='https://github.com/ruby-gettext/gettext'
license=('RUBY' 'LGPL3')
makedepends=('ruby-rdoc' 'ruby-yard' 'ruby-bundler')
depends=('ruby' 'ruby-locale' 'ruby-text')
checkdepends=('ruby-test-unit' 'ruby-rake' 'ruby-test-unit-rr')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
options=("!emptydirs")
sha512sums=('ba65a2c2e631acb2830440593602f680d0698934fff2850dab6bfc0363fd514b32ab8cc841ec8c28a5fad05bfc646f2f3f3c1e196fc4208c58c658666bb109ee')

# update the gemspec to allow newer versions of rake
# remove deprecated options from the gemspec
prepare() {
  cd "${_gemname}-${pkgver}"

  sed --in-place 's/s.license.*/s.licenses = ["Ruby", "LGPL-3.0+"]/' "${_gemname}.gemspec"
  sed --in-place '/test\/unit\/notify/d' test/run-test.rb
  sed --in-place '/s.add_development_dependency("test-unit-notify")/d' "${_gemname}.gemspec"
}


build() {
  cd "${_gemname}-${pkgver}"

  gem build "${_gemname}.gemspec"
}

# tests are currently broken upstream
check() {
  cd "${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  rm -rf "${pkgdir}/${_gemdir}/cache"

  install -Dm 644 README.md doc/text/news.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/text/lgpl-3.0.txt doc/text/ruby-license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
