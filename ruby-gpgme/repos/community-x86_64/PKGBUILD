# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=gpgme
pkgname=ruby-gpgme
pkgver=2.0.14
pkgrel=1
pkgdesc='Ruby interface to GnuPG Made Easy (GPGME)'
url='https://github.com/ueno/ruby-gpgme'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('ruby' 'ruby-mini_portile2')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ueno/ruby-gpgme/archive/v${pkgver}.tar.gz)
sha256sums=('9dd50700aea198b49e9e208224a74b98d4d3c09e30802712e0a20fb3795f3772')
sha512sums=('eb29fa374041b3d9717cf2ca025f28648bb87eb74784451f3703690f5a403d7fb8c6c22cb255c88baa8efbad44346a288f5f47ed89326d3ad4b9e21ec8d7846f')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${pkgname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}" \( -name gem_make.out -o -name mkmf.log \) -delete
}

# vim: ts=2 sw=2 et:
