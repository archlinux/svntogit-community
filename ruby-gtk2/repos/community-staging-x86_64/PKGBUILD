# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Eric Bélanger <eric@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>
# Contributor: kritoke <kritoke@nospam.gmail.com>

pkgbase=ruby-gtk2
pkgname=('ruby-atk' 'ruby-gdkpixbuf2' 'ruby-gio2' 'ruby-glib2' 'ruby-gtk2'
         'ruby-pango')
pkgver=2.1.0
pkgrel=2
arch=('x86_64' 'i686')
url='http://ruby-gnome2.sourceforge.jp/'
license=('LGPL')
makedepends=('ruby-pkgconfig' 'ruby-cairo' 'gtk2' 'glib2')
source=("http://downloads.sourceforge.net/ruby-gnome2/ruby-gtk2-$pkgver.tar.gz")
sha256sums=('939251c1f82b41c3cf657552396cf4e1198365316904957cfb7167d1298db204')

prepare() {
  cd "ruby-gtk2-$pkgver"

  LANG="en_US.UTF-8" \
    ruby extconf.rb atk gdk_pixbuf2 gio2 glib2 gtk2 pango --vendor
  mkdir -p \
    "$pkgdir/ruby-glib2/usr/lib/ruby/vendor_ruby/2.0.0/i686-linux" \
    "$pkgdir/ruby-atk/usr/lib/ruby/vendor_ruby/2.0.0/i686-linux" \
    "$pkgdir/ruby-gio2/usr/lib/ruby/vendor_ruby/2.0.0/i686-linux" \
    "$pkgdir/ruby-atk/usr/lib/ruby/vendor_ruby/2.0.0/i686-linux" \
    "$pkgdir/ruby-pango/usr/lib/ruby/vendor_ruby/2.0.0/x86_64-linux" \
    "$pkgdir/ruby-glib2/usr/lib/ruby/vendor_ruby/2.0.0/x86_64-linux" \
    "$pkgdir/ruby-gio2/usr/lib/ruby/vendor_ruby/2.0.0/x86_64-linux"
}

build() {
  make -C "ruby-gtk2-$pkgver"
}

package_ruby-atk() {
  pkgdesc='Ruby bindings for atk'
  depends=("ruby-glib2=$pkgver" 'atk')

  cd "$srcdir/ruby-gtk2-$pkgver/atk"
  make includedir="/usr/include" DESTDIR="$pkgdir" install -j1
}

package_ruby-gdkpixbuf2() {
  pkgdesc='Ruby bindings for gdkpixbuf2'
  depends=("ruby-glib2=$pkgver" 'gdk-pixbuf2')

  cd "$srcdir/ruby-gtk2-$pkgver/gdk_pixbuf2"
  make includedir="/usr/include" DESTDIR="$pkgdir" install -j1
}

package_ruby-gio2() {
  pkgdesc='Ruby bindings for gio2'
  depends=("ruby-glib2=$pkgver")

  cd "$srcdir/ruby-gtk2-$pkgver/gio2"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install -j1
}

package_ruby-glib2() {
  pkgdesc='Ruby bindings for glib2'
  depends=('glib2' 'ruby')

  cd "$srcdir/ruby-gtk2-$pkgver/glib2"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install -j1
}

package_ruby-gtk2() {
  pkgdesc='Ruby bindings for gtk2'
  depends=('gtk2' "ruby-glib2=$pkgver" "ruby-pango=$pkgver" "ruby-atk=$pkgver"
           "ruby-gdkpixbuf2=$pkgver")

  cd "$srcdir/ruby-gtk2-$pkgver/gtk2"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install -j1
}

package_ruby-pango() {
  pkgdesc='Ruby bindings for pango'
  depends=("ruby-glib2=$pkgver" 'ruby-cairo' 'pango')

  cd "$srcdir/ruby-gtk2-$pkgver/pango"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install -j1
}

# vim:set ts=2 sw=2 et:
