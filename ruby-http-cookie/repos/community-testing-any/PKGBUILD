# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sigmund Vestergaard <sigmundv at gmail dot com>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='http-cookie'
pkgname="ruby-${_gemname}"
pkgver=1.0.3
pkgrel=6
pkgdesc='Ruby library to handle HTTP Cookies based on RFC 6265'
arch=('any')
url='https://github.com/sparklemotion/http-cookie'
license=('MIT')
makedepends=('ruby-rdoc' 'ruby-bundler')
checkdepends=('ruby-rake' 'ruby-test-unit')
depends=('ruby' 'ruby-domain_name')
options=(!emptydirs)
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('a06ab81253b527d115570568b3b4db9dfb32d19bae274f96d6e2a719a99ee66f4f6abc03359043b8ced78458264433902d7edf01f58ca09472ce2e0cefdd33c0')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"

  # allow latest gem dependencies
  sed --in-place 's|git ls-files|find|' "${_gemname}.gemspec"

  # disable simplecov reporting.
  # it's not required and just adds another dependency
  sed --in-place '/simplecov_start.rb/d' Rakefile
  sed --in-place '/simplecov/d' "${_gemname}.gemspec"

  # Don't load rubygems.
  # It would require us to install all development gems
  sed --in-place '/rubygems/d' test/helper.rb
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
