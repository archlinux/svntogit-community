# Maintainer: David Runge <dvzrv@archlinux.org>

_name=i18n
pkgname=ruby-i18n
pkgver=1.8.1
pkgrel=3
pkgdesc="New wave internationalisation support for Ruby"
arch=('any')
url="https://github.com/ruby-i18n/i18n"
license=('MIT')
depends=('ruby' 'ruby-activesupport' 'ruby-json' 'ruby-mocha')
makedepends=('ruby-bundler' 'ruby-rake' 'ruby-rdoc')
checkdepends=('ruby-minitest' 'ruby-test_declarative')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ruby-i18n/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('8bdf4864f649b33d6f71a4bfeeabcaaa4f7baaea7765831c72c1d5c3a139af5be478786997e7829e4dce09152ca885a01ac78938dc8da93f2fac86f0635ca3f4')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd ${pkgname}-${pkgver}
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i "${_name}.gemspec" Gemfile
}

build() {
  cd ${pkgname}-${pkgver}
  gem build "${_name}.gemspec"
}

check(){
  cd "${pkgname}-${pkgver}"
  rake test
}

package() {
  cd "${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
