# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ruby-iconv
pkgver=1.0.5
pkgrel=1
pkgdesc='Wrapper library for iconv'
arch=('x86_64')
url='https://github.com/nurse/iconv'
license=('BSD')
depends=('ruby')
options=('!emptydirs')
source=("http://rubygems.org/downloads/${pkgname#*-}-${pkgver}.gem")
noextract=("${pkgname#*-}-${pkgver}.gem")
sha256sums=('ef913e92e05002d37bebed57e8ecbad91b8a9da5da3b0f80bafd32d3ccc171da')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} ${pkgname#*-}-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/${pkgname}
  ln -s ${_gemdir}/gems/${pkgname#*-}-${pkgver}/BSDL "${pkgdir}"/usr/share/licenses/${pkgname}/
}

# vim: ts=2 sw=2 et:
