# Maintainer: David Runge <dvzrv@archlinux.org>

_name=json
pkgname=ruby-json
pkgver=2.6.1
pkgrel=1
pkgdesc="JSON implementation for Ruby"
arch=('x86_64')
url="https://github.com/flori/json"
license=('BSD')
depends=('ruby')
makedepends=('ragel' 'ruby-rake' 'ruby-rdoc')
checkdepends=('git' 'ruby-bundler' 'ruby-test-unit')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flori/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('e9d0ef77d77ae54a0300b490e3e5c445d0b5aefbce1b160454916bd4f4f8f943131223047e6a7416347cc40d9e7fb1870101c3cbf3a2663a9053f07bdfbd13cf')
b2sums=('5a8e83b8d11e360402324427de5d2520f39ee1567e2fbf3c92c738546792bb5dec4a9a3c36f3286a3de63161bb5d9f9e7b059c2560f7a3fa50271ce2aa3dc0fc')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "${pkgname}-${pkgver}"
  gem build "${_name}.gemspec"
}

check(){
  cd "${pkgname}-${pkgver}"
  rake test
}

package() {
  cd "${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache \
    "doc/${_name}-${pkgver}/ri/ext/" \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out} \
    gems/${_name}-${pkgver}/{ext,lib/*/*.so,*.gemspec}
}
