# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='kpeg'
pkgname="ruby-${_gemname}"
pkgver=1.3.2
pkgrel=3
pkgdesc='A simple PEG library for ruby'
arch=('any')
url="https://github.com/evanphx/${_gemname}"
license=('MIT')
depends=('ruby')
makedepends=('ruby-hoe' 'ruby-rake')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8c05529af8c5ee1defb4401109ad8fd62cd39b6c278202e01781b294ee94395e8acdfe10d7e984b9ed5f1c11006dcbfa694c3315a0d9e5a085adb81bfbaa0848')
b2sums=('91494f7e92bed6e285b4ed9be20738afba82ca75316e7e4537c4d15032dff906416c94f02ca1f5dec56d412c3910cbac072b63918b3ca1bbb71d1ca896d015da')

prepare() {
  cd "${_gemname}-${pkgver}"

  # update gemspec/Gemfile to allow newer version of the dependencies
  sed --in-place --regexp-extended 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  gem build "${_gemname}.gemspec"

  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/${_gemdir}" \
    --bindir "tmp_install/usr/bin" \
    "${_gemname}-${pkgver}.gem"

  # remove unrepreducible files
  rm --force --recursive --verbose \
    "tmp_install/${_gemdir}/cache/" \
    "tmp_install/${_gemdir}/gems/${_gemname}-${pkgver}/vendor/" \
    "tmp_install/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"

  find "tmp_install/${_gemdir}/gems/" \
    -type f \
    \( \
      -iname "*.o" -o \
      -iname "*.c" -o \
      -iname "*.so" -o \
      -iname "*.time" -o \
      -iname "gem.build_complete" -o \
      -iname "Makefile" \
    \) \
    -delete

  find "tmp_install/${_gemdir}/extensions/" \
    -type f \
    \( \
      -iname "mkmf.log" -o \
      -iname "gem_make.out" \
    \) \
    -delete
}

check() {
  cd "${_gemname}-${pkgver}"

  local _gemdir="$(gem env gemdir)"

  # workaround to get tests working - https://github.com/evanphx/kpeg/issues/56
  export GEM_HOME="tmp_install/${_gemdir}"
  find test/ -iname '*.rb' | while read testfile; do ruby "${testfile}"; done
  unset GEM_HOME

  #GEM_HOME="tmp_install/${_gemdir}" rake test
}

package() {
  cd "${_gemname}-${pkgver}"

  cp --archive --verbose tmp_install/* "${pkgdir}"

  install --verbose --directory --mode=0755 "${pkgdir}/usr/share/vim/vimfiles/"
  mv --verbose vim/syntax_kpeg/* "${pkgdir}/usr/share/vim/vimfiles/"

  install --verbose -D --mode=0644 LICENSE --target-directory "${pkgdir}/usr/share/licenses/${pkgname}"
  install --verbose -D --mode=0644 *.rdoc --target-directory "${pkgdir}/usr/share/doc/${pkgname}"
}
