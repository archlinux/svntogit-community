# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=kramdown
pkgname=ruby-kramdown
pkgver=2.1.0
pkgrel=2
pkgdesc='Fast, pure Ruby Markdown superset converter, using a strict syntax definition'
url='https://kramdown.gettalong.org/'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gettalong/kramdown/archive/REL_${pkgver//./_}.tar.gz)
sha256sums=('8e689117a0eb530d72b398eaf501d031c7501ccea43b86430fcb020af3506d77')
sha512sums=('d71c9b3fe237e4b20ce3882dfd180e604f216ffcdcbb884c2ca6c24a2ed9ef94a49a415a8c5494a61b752d45be1f7240bc73ebf3545660c6c1e4c9a52b05dfa5')

prepare() {
  cd ${_gemname}-REL_${pkgver//./_}
  rake gemspec
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${_gemname}-REL_${pkgver//./_}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-REL_${pkgver//./_}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/man/man1"
  mv "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/man/man1/kramdown.1" "${pkgdir}/usr/share/man/man1"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  rm -r "${pkgdir}/${_gemdir}/gems/kramdown-${pkgver}/test"
}

# vim: ts=2 sw=2 et:
