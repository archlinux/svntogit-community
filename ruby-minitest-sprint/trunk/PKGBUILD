# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=minitest-sprint
pkgname=ruby-${_gemname}
pkgver=1.2.1
pkgrel=2
pkgdesc="Runs (Get it? It's fast!) your tests and makes it easier to rerun individual failures"
url='https://github.com/seattlerb/minitest-sprint'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-minitest' 'ruby-path_expander')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-hoe')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('958ec57ea8957616b9769561981c3e5102795fb9687c34bb643e5e9bd4f64dd452c7696f06cb0de37cffc67c9e46c524a4d00af4f558a4cb645975bccc76e81c')
b2sums=('9c629b097d6c02c3cec2b20ee0c8f060a29000ec035afb4f648fb82e21deba9db52e2dfbe192e49f0b0bb47f2c59957887c238b84e7e9b61012b9525b4c0d36f')

build() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i Rakefile
  rake gem
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem

  install -Dm 644 README.rdoc History.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/licenses/${pkgname}"

  rm -r "${pkgdir}/${_gemdir}/"{cache,gems/${_gemname}-${pkgver}/test}
}

# vim: ts=2 sw=2 et:
