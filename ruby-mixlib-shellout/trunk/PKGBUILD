# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Joel Goguen <contact+aur@jgoguen.ca>
# Contributor: Christopher Loen <christopherloen at gmail dot com>
# Contributor: Artem Vorotnikov <artem@vorotnikov.me>

pkgname=ruby-mixlib-shellout
_pkgname="${pkgname#ruby-}"
pkgver=3.2.7
pkgrel=2
pkgdesc='Run external commands on Unix'
arch=('any')
url='https://github.com/chef/mixlib-shellout'
license=('Apache')
depends=('ruby' 'ruby-chef-utils')
makedepends=('git' 'ruby-rake' 'ruby-rdoc')
options=('!emptydirs')
_commit='ca1f88cf1d6a1d56a1b89c79c3fca507db7b4b0f'
source=("$pkgname::git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {
  cd "$pkgname"

  gem build "$_pkgname.gemspec"
}

package() {
  cd "$pkgname"

  local _gemdir="$(gem env gemdir)"

    #--no-document \
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "$_pkgname-$pkgver.gem"

  # delete cache
  rm -vrf "$pkgdir/$_gemdir/cache"
}
