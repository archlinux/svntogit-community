# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname=msgpack
pkgname="ruby-${_gemname}"
pkgver=1.5.6
pkgrel=1
pkgdesc='MessagePack implementation for Ruby'
url='https://github.com/msgpack/msgpack-ruby'
arch=('x86_64')
license=('Apache')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rake-compiler' 'ruby-yard' 'ruby-rspec' 'ruby-bundler' 'ruby-webrick')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('b6e616b786dc10daddcbdeb4d8c478799c234c1b04866d60695a522a49794bd6')
sha512sums=('9d425fae115342711f4107478c43d165f771102b3069fa320e066533e7d080a2c0db9a1eb85278b45b62840836e05b4fea76154e038117461020227e58714183')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"
  sed 's|git ls-files|find|' -i "${_gemname}.gemspec"
  sed 's|~>|>=|g' -i Gemfile
}

build() {
  cd "${_gemname}-ruby-${pkgver}"
  rake compile
  rake build
  rake clean
}

check() {
  cd "${_gemname}-ruby-${pkgver}"
  rake spec
}

package() {
  cd "${_gemname}-ruby-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin"  \
    "pkg/${_gemname}-${pkgver}.gem"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/"${_gemname}-${pkgver}"/{ext,tmp,lib/*/*.so} \
    extensions/*/*/"${_gemname}-${pkgver}"/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
