# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='nats-pure'
pkgname="ruby-${_gemname}"
pkgver=0.6.2
pkgrel=2
pkgdesc="Ruby client for the NATS messaging system"
url="https://nats.io"
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://rubygems.org/gems/${_gemname}-${pkgver}.gem"
        'LICENSE')
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('dca041d933497793a98d69f604909f47b94113c96ca0f3f76b2b23d0fe45a0722226903451147e1146c4f6143ba8a3bc57f1babca350cfa5df7ab1f61571655d'
            'ec05133944c3dc861ae437ce8681f54a871b614ff020fd4db1b3b3eb75f9e612222b682d1cceb5a3335e479e3541072abd4abc8f272d2e4d8803f97fe9f58b08')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
