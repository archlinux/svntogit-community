# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=net-dns
pkgname=ruby-${_gemname}
pkgver=0.8.0
pkgrel=4
pkgdesc='Pure Ruby DNS library, with a clean OO interface and an extensible API'
url='https://github.com/bluemonk/net-dns'
arch=('any')
license=('BSDL')
depends=('ruby')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/bluemonk/net-dns/archive/v${pkgver}.tar.gz)
sha512sums=('48db69a5547899d447fb953dab1e4054d7d7ed9afb796b39bf5ea7c875c9980e7d60040ee570b53f003ca9dfe054d6c3e65ec1291646332e1acbbd3721267146')

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 CHANGELOG.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
