# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=nio4r
pkgname=ruby-${_gemname}
pkgver=2.5.4
pkgrel=2
pkgdesc='Abstract, cross-platform stateful I/O selector API for Ruby'
url='https://github.com/celluloid/nio4r'
arch=('x86_64')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/celluloid/nio4r/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('d4674ba545dd5933e1d1126b70dfb221e5fd58e5caa281f9ad8e1209070d637f0b6d533c95905db352a2388801cbc138207b173508d6ee590de5b99a51546444')
b2sums=('382e1a2748de377022308c93f771584a39f00223d4316288cc2f42ae1c4575a8d2eea71f6739d129b5c43cb431fd0d857f6beeb81469e71666565d504cf989d3')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md CHANGES.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
