# Generated by gem2arch (https://github.com/anatol/gem2arch)
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname='nokogiri'
pkgname="ruby-${_gemname}"
pkgver=1.13.3
pkgrel=2
pkgdesc='Nokogiri (é‹¸) is an HTML, XML, SAX, and Reader parser'
arch=('x86_64')
url='https://nokogiri.org'
license=('MIT')
depends=('ruby' 'ruby-mini_portile2' 'libxslt')
options=(!emptydirs)
source=("https://rubygems.org/downloads/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('d704ed5ea6af3e066e3ddefd74cb15d0feed359b90c3b75b83a8f1ff85d8d4ca5b22a47c6848a2043a84f217e9d6b5ca861125d60ed0f88cb53690eb3726b3e7')

package() {
  CFLAGS+=' -ffat-lto-objects'

  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  local _platform="$(gem env platform | cut -d':' -f2)"
  local _extension_api_version="$(ruby -e'puts Gem.extension_api_version')"

  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem" -- --use-system-libraries

  sed -r 's|~>|>=|g' -i "${pkgdir}/${_gemdir}/specifications/${_gemname}-${pkgver}.gemspec"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem" \
      "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/nokogiri"/{*.o,*.c,*.h} \
      "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/ext/nokogiri/Makefile" \
      "${pkgdir}/${_gemdir}/extensions/${_platform}/${_extension_api_version}/${_gemname}-${pkgver}/gem_make.out"

  install -D -m644 "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/LICENSE-DEPENDENCIES.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-DEPENDENCIES.md"
  install -D -m644 "${pkgdir}/${_gemdir}/gems/${_gemname}-${pkgver}/LICENSE.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
