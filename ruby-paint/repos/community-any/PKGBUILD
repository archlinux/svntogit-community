# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

_gemname=paint
pkgname=ruby-${_gemname}
pkgver=2.0.0
pkgrel=1
pkgdesc='Ruby paint manages terminal colors and effects'
url='https://github.com/janlelis/paint'
arch=('any')
license=('MIT')
depends=('ruby-rspec' 'ruby-rainbow' 'ruby-term-ansicolor')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/janlelis/paint/archive/v${pkgver}.tar.gz)
sha512sums=('3a30635b193fcd2d929a09f64226fefdf01dabf3c33427ca28d8d5a550d56642c8069d5f1334d845977282a6083cdfa16177f3bd23560f61c36d3822c891c5fe')

build() {
  cd ${_gemname}-${pkgver}
  gem build paint.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -Dm 644 MIT-LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
