# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=path_expander
pkgname=ruby-${_gemname}
pkgver=1.1.0
pkgrel=3
pkgdesc='Helps pre-process command-line arguments expanding directories into their constituent file'
url='https://github.com/seattlerb/path_expander'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-hoe')
checkdepends=('ruby-minitest')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('303172fe956f468ee2d34d43f6741f4482077998eadba86a155a2b529ef5ed3264352085ff663d9397d1dc6bbc26ea4637d24cd9bc0871429ad7ff650b20349b')
b2sums=('1ae00393cc82cdaa71eda22de83763262c506f8731f8ca5046d2109113bcecf14c458486d1ea8a2ba351ce760b7e82011f8cdc0de4a5ac4adad0a42c4bd58d20')

build() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i Rakefile
  rake gem
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem

  install -Dm 644 README.rdoc History.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/licenses/${pkgname}"

  rm -r "${pkgdir}/${_gemdir}/"{cache,gems/${_gemname}-${pkgver}/test}
}

# vim: ts=2 sw=2 et:
