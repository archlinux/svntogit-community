# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-patron
pkgver=0.13.3
pkgrel=1
pkgdesc='Ruby HTTP client library based on libcurl'
arch=(x86_64)
url='https://github.com/toland/patron'
license=(MIT)
depends=(ruby curl)
makedepends=(ruby-rake-compiler ruby-rack ruby-rspec ruby-puma ruby-yard)
options=(!emptydirs)
source=(https://github.com/toland/patron/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        $pkgname-puma-5.patch)
sha256sums=('4ae57af4ea6d0eaaa5ccdbf78f6b3a0157109a7d7d2a72c7b048a16af9ef771a'
            '85977d0082234adb7b31308c4338f870a6ae30c3f2182c81d7c1736c33c0dc25')

prepare() {
  cd patron-$pkgver
  patch -p1 -i ../$pkgname-puma-5.patch
  # we don't do version pinning
  sed -r -e 's|~>|>=|g' -e 's|git ls-files -z|find . -print0|' -i patron.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd patron-$pkgver
  rake build
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    pkg/patron-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd patron-$pkgver
  # https://github.com/toland/patron/pull/193
  GEM_HOME="tmp_install/$_gemdir" rake spec || echo "Tests failed"
}

package() {
  cd patron-$pkgver
  cp -a tmp_install/* "$pkgdir"/

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
