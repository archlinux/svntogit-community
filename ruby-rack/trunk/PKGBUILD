# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexsandr Pavlov <kidoz at mail dot ru>

_gemname=rack
pkgname=ruby-rack
pkgver=2.2.3
pkgrel=1
pkgdesc='Minimal, modular, and adaptable interface for developing web applications'
url='https://rack.github.io/'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-minitest'
             'ruby-minitest-global_expectations' 'ruby-minitest-sprint')
checkdepends=('ruby-webrick')
options=('!emptydirs')
source=(https://github.com/rack/rack/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('d9faed72b1751494a88b7d1379ab0777114ca5fb25a2d850fbefab50482bb365')
sha512sums=('0daefd1fd7d1c83a5aaf0e8a3c0d734f805f6718c3f2d20673e78242f10fa99c24f8e8d480cb7f0ee299d96a4b691e4a19c967785de946dc863e9b78e0c3ac53')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec Rakefile
}

build() {
  cd ${_gemname}-${pkgver}
  rake build
}

check() {
  cd ${_gemname}-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 MIT-LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 example/* -t "${pkgdir}/usr/share/doc/${pkgname}/example"

  cd "${pkgdir}/${_gemdir}"
  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
