# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux AT free DOT fr>
# Contributor: Juan Diego Tascon
# Contributor: Tom Vincent <http://tlvince.com/contact/>

_gemname=rainbow
pkgname=ruby-${_gemname}
pkgver=3.0.0
pkgrel=3
pkgdesc="Rainbow is extension to ruby's String class adding support for colorizing text on ANSI terminal"
url='https://rubygems.org/gems/rainbow'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sickill/rainbow/archive/v${pkgver}.tar.gz)
sha256sums=('5abcf7d46cd3ab35e46ae5218befeca3a4c4438c45856f09b203b1e69e0b278d')
sha512sums=('863b89152696b6a13606a1e92b68a788b9b5bcd6f521f02f26fb189fc6c9613e5db032dd5deed1aa95247ab0aa08a44981756f941af6eb786076c271881a38c1')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i rainbow.gemspec # don't give a fuck about rubys bla bla
  sed -r 's|git ls-files|find|g' -i rainbow.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build rainbow.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" rainbow*.gem
  install -Dm 644 README.markdown Changelog.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}"/${_gemdir}/cache/${_gemname}-${pkgver}.gem
}

# vim: ts=2 sw=2 et:
