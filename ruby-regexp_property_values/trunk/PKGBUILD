# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-regexp_property_values
pkgver=1.3.0
pkgrel=1
pkgdesc="Inspect property values supported by Ruby's regex engine"
arch=(x86_64)
url='https://github.com/jaynetics/regexp_property_values'
license=(MIT)
depends=(ruby)
checkdepends=(ruby-character_set ruby-rake ruby-rake-compiler ruby-range_compressor ruby-rspec)
options=(!emptydirs)
source=(https://github.com/jaynetics/regexp_property_values/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('22d69e9b76a560a39bf83f01cdcbb9a1db17c52224a929a273e9e9dcea4bfa30')

prepare() {
  cd regexp_property_values-$pkgver
  sed -i -r -e 's|git ls-files -z|find . -type f -not -path "*/\.git/*" -not -path "*\.gem" -printf "%P\\\\0"|' regexp_property_values.gemspec
  sed -i -r 's|~>|>=|' Gemfile
  # Don't recompile
  sed -i '/compile/d' Rakefile
  sed -i '/bundler/d' spec/spec_helper.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd regexp_property_values-$pkgver
  gem build regexp_property_values.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    regexp_property_values-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd regexp_property_values-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd regexp_property_values-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
