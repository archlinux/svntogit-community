# Maintainer: Anatol Pomozov

_gemname=reline
pkgname=ruby-$_gemname
pkgver=0.1.4
pkgrel=1
pkgdesc="Alternative GNU Readline or Editline implementation by pure Ruby."
arch=('any')
url='https://github.com/ruby/reline'
license=('BSD-2')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/$_gemname-$pkgver.gem")
noextract=("$_gemname-$pkgver.gem")
sha512sums=('e69d356841db666ff6ae3661dd4e9c2aa7947b90a6f425513dac0e5534f01e892e36b466aa9abc4f94df61115f91c85b54beb446f4cd0859875b827ab8f37a54')

package() {
  cd "$srcdir"

  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  HOME="/tmp" GEM_HOME="$_gemdir" GEM_PATH="$_gemdir" gem install -N --no-user-install --ignore-dependencies \
     -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" "$_gemname-$pkgver.gem"
  rm "$pkgdir/$_gemdir/cache/$_gemname-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
