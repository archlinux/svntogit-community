# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
_gemname=semantic_puppet
pkgname="ruby-${_gemname}"
pkgver=1.0.2
pkgrel=1
pkgdesc='Tools used by Puppet to parse, validate, and compare Semantic Versions and Version Ranges and to query and resolve module dependencies.'
arch=('any')
url='https://github.com/puppetlabs/semantic_puppet#semanticpuppet'
depends=('ruby')
makedepends=('ruby-rdoc' 'rubygems')
license=('Apache')
options=('!emptydirs')
source=("https://rubygems.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('b059d8c7b13ae96dfe012318e7c6ee483cc09986d5d065c8422167a27a14d8e99433229c90388a7ff9ee425a0f5b6a6a6d5bc55f8b81901490afd8039ea86480')
package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    --verbose \
    ${_gemname}-${pkgver}.gem

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"

  install -D -m644 "${pkgdir}/usr/lib/ruby/gems/2.5.0/gems/semantic_puppet-1.0.2/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
