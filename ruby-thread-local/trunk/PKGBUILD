# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-thread-local
pkgver=1.1.0
pkgrel=1
pkgdesc='Provides a class-level mixin to make thread local state easy'
arch=(any)
url='https://github.com/socketry/thread-local'
license=(MIT)
depends=(ruby)
makedepends=(ruby-rspec)
options=(!emptydirs)
source=(https://github.com/socketry/thread-local/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('17124384a6b0253ed0c1103ef34297b1271cdbec504a8d3c4c8328ae982c7d3e')

prepare() {
  cd thread-local-$pkgver
  sed -r -e '/covered/d' -i thread-local.gemspec spec/spec_helper.rb
  echo gemspec > gems.rb
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd thread-local-$pkgver
  gem build thread-local.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    thread-local-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd thread-local-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rspec
}

package() {
  cd thread-local-$pkgver
  cp -a tmp_install/* "$pkgdir"/
}
