# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>

pkgname=ruby-tins
pkgver=1.16.3
pkgrel=1
pkgdesc='All the stuff that is not good/big enough for a real library'
arch=('any')
url='https://github.com/flori/tins'
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/tins-${pkgver}.gem")
noextract=("tins-${pkgver}.gem")
sha256sums=('68cde57b4d12de20248284b9a6921f385170509b6b3ab2a030b6f46d89ee253d')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -N -i "${pkgdir}"/${_gemdir} tins-${pkgver}.gem
  find "${pkgdir}" -type f -name *.gem -delete

  install -dm 755 "${pkgdir}"/usr/share/licenses/ruby-tins
  ln -s ${_gemdir}/gems/tins-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/ruby-tins/
}
