# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: VojtÄ›ch Aschenbrenner <v@asch.cz>

_gemname=trollop
pkgname=ruby-${_gemname}
pkgver=2.1.3
pkgrel=4
pkgdesc='Commandline option parser for Ruby that just gets out of your way'
url='http://manageiq.github.io/trollop/'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ManageIQ/trollop/archive/v${pkgver}.tar.gz)
sha512sums=('a3b4c5f82edc10652d9b7cca363488e6993919b7e1c6c5bb98638d80207b93ce2026002eceab09e1cea5bf73264bbbf627531235e202ed27421a2419b289ff16')

prepare() {
  cd optimist-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
  sed 's|git ls-files -z|find -type f -print0\|sed "s,\\\\./,,g"|' -i ${_gemname}.gemspec
}

build() {
  cd optimist-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd optimist-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${_gemname}-${pkgver}/README.md" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
