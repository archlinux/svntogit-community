# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tzinfo
pkgname=ruby-tzinfo
pkgver=2.0.3
pkgrel=2
pkgdesc='Daylight savings aware transformations between times in different time zones'
arch=('any')
url="https://github.com/tzinfo/tzinfo"
license=('MIT')
depends=('ruby' 'ruby-concurrent')
makedepends=('ruby-rake' 'ruby-rdoc')
# TODO: package ruby-simplecov
# checkdepends=('ruby-minitest' 'ruby-simplecov')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('c9e94af668217226533d2810b647cc7a0d0a553177ab9c4102e5676fa21d4384e833c887f5cb3f9c00cf738763dcd39b25d991483641426c407237938e076adf')
b2sums=('975c2278ba86c7aaf11cb870b12fa41a9fb3eb8b8b87628bcb0b09f54d7f2490b2d9864dd411f646f20c3760ed0949a8ee4266c28a6b0607927e9807743a9f13')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i ${_name}.gemspec Gemfile
}

build() {
  cd ${pkgname}-${pkgver}
  gem build "${_name}.gemspec"
}

# check(){
#   cd "${pkgname}-${pkgver}"
#   rake test
# }

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so,*.gemspec} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
