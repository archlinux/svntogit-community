# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-xpath
pkgver=3.2.0
pkgrel=1
pkgdesc='A Ruby DSL for generating XPath expressions'
arch=(any)
url='https://github.com/teamcapybara/xpath'
license=(MIT)
depends=(ruby-nokogiri)
makedepends=(ruby-pry ruby-rake ruby-rspec ruby-yard)
options=(!emptydirs)
source=(https://github.com/teamcapybara/xpath/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('51b617bcadfa366766f09c4fc3dc27df694519a6af1d6f78e04e0697d4219c77')

prepare() {
  cd xpath-$pkgver
  sed -i 's|~>|>=|' xpath.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd xpath-$pkgver
  gem build xpath.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    xpath-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd xpath-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake
}

package() {
  cd xpath-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
