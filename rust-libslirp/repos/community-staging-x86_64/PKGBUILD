# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: loqs

pkgname=rust-libslirp
pkgver=4.3.0
pkgrel=2
pkgdesc='High-level bindings & helper process for libslirp.'
url='https://gitlab.freedesktop.org/slirp/libslirp-rs'
arch=('x86_64')
license=('MIT')
depends=('dbus' 'libslirp')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://crates.io/api/v1/crates/libslirp/${pkgver}/download")
b2sums=('a5a87d8f99e69f332de7adcb6bf5213348945e711d5cedd41a87de52c326fc0b1e43a1903396c3fd0aca776b3c7aabe2592cbb636c8e83f2de4453a13cece10d')

build() {
  cd ${pkgname#rust-}-${pkgver}
  cargo build --release --locked --all-features --target-dir=target
}

package() {
  cd ${pkgname#rust-}-${pkgver}
  install -Dm755 "target/release/libslirp-helper" "${pkgdir}"/usr/bin/slirp-helper
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
