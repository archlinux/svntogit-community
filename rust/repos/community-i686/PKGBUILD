# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=rust
_pkgver=1.0.0-alpha.2
pkgver=1.0.0.alpha.2
pkgrel=1
arch=(i686 x86_64)
pkgdesc='A safe, concurrent, practical language'
url='http://www.rust-lang.org/'
license=(MIT Apache)
depends=(gcc)
makedepends=(libffi perl python2 curl)
checkdepends=(git valgrind)
source=(http://static.rust-lang.org/dist/rustc-$_pkgver-src.tar.gz{,.asc})
sha256sums=('a931b945e98f409df68fdff23e98b688024461c28901106896e73708381956c8'
            'SKIP')
install=rust.install
options=(staticlibs)
validpgpkeys=(108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE) # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

build() {
  cd rustc-$_pkgver

  ./configure --prefix=/usr --disable-debug --disable-docs --disable-verify-install

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules

  make
}

check() {
  cd rustc-$_pkgver
  #make check
}

package() {
  cd rustc-$_pkgver
  make DESTDIR="$pkgdir" install

  install -D -m644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -D -m644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
}
