# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgname=rust
pkgver=1.0.0.beta.3
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Safe, concurrent and practical programming language from Mozilla'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
makedepends=('libffi' 'perl' 'python2' 'curl')
checkdepends=('git' 'valgrind')
source=("http://static.rust-lang.org/dist/rustc-${pkgver/.b/-b}-src.tar.gz"{,.asc})
sha256sums=('e751bc8a8ad236c8865697f866b2863e224af56b0194ddf9f3edd71f9ff6545f'
            'SKIP')
install="$pkgname.install"
options=('staticlibs')
#gpg --keyserver hkp://keys.gnupg.net --recv-keys 108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

build() {
  cd "rustc-${pkgver/.b/-b}"

  ./configure --prefix=/usr --disable-debug --disable-docs

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules

  make
}

#check() {
#  cd "rustc-${pkgver/.b/-b}"
#  make check
#}

package() {
  cd "rustc-${pkgver/.b/-b}"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
}

# vim:set ts=2 sw=2 et:
