# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=rust
pkgver=1.3.0
epoch=1
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
makedepends=('libffi' 'perl' 'python2' 'curl')
source=("http://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc})
sha256sums=('ea02d7bc9e7de5b8be3fe6b37ea9b2bd823f9a532c8e4c47d02f37f24ffa3126'
            'SKIP')
install="$pkgname.install"
options=('staticlibs' '!strip')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
#gpg --keyserver hkp://keys.gnupg.net --recv-keys 108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE

prepare() {
  cd "rustc-$pkgver"

  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules
}

build() {
  cd "rustc-$pkgver"

  ./configure --prefix=/usr --disable-docs --release-channel=stable
  make
}

package() {
  cd "rustc-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
}

# vim:set ts=2 sw=2 et:
