# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jonathon Fernyhough <jonathon_at_manjaro_dot_org>
# Contributor: Jon Gjengset <jon@tsp.io>
pkgname=rustup
pkgver=1.17.0
_commit=beab5ac2bd6e25e1bb756e14404ed81a6cc49e9e
pkgrel=1
pkgdesc="The Rust toolchain installer"
arch=('x86_64')
url="https://github.com/rust-lang/rustup.rs"
license=('MIT' 'Apache')
depends=('curl')
makedepends=('cargo')
optdepends=('lldb: rust-lldb script'
            'gdb: rust-gdb script')
provides=('rust' 'cargo' 'rust-nightly' 'cargo-nightly' 'rustfmt')
conflicts=('rust' 'cargo' 'rustfmt')
install='post.install'
source=("rustup-${pkgver}.tar.gz::https://github.com/rust-lang/rustup.rs/archive/${_commit}.tar.gz")
sha512sums=('63ac7d1f50f4733875f1f2f02f5152ebb5b4c989fe629aec41e5abf81e9949a90811da7c29ddb38b9c49371c9fa965e48abed4a5f65fb0e4f9a14a1ca1318c6c')
_binlinks=('cargo' 'rustc' 'rustdoc' 'rust-gdb' 'rust-lldb' 'rls' 'rustfmt' 'cargo-fmt' 'cargo-clippy' 'clippy-driver')

build() {
    cd "$srcdir/$pkgname.rs-${_commit}"
    cargo build --release --features no-self-update --bin rustup-init
}

package() {
    cd "$pkgname.rs-${_commit}"
    install -Dm755 "target/release/rustup-init" "${pkgdir}/usr/bin/rustup"
    for link in "${_binlinks[@]}"; do
        ln -s /usr/bin/rustup "${pkgdir}/usr/bin/${link}"
    done

    # Generate completion files.
    mkdir -p "$pkgdir/usr/share/bash-completion/completions"
    $pkgdir/usr/bin/rustup completions bash > "$pkgdir/usr/share/bash-completion/completions/rustup"
    mkdir -p "$pkgdir/usr/share/fish/completions"
    $pkgdir/usr/bin/rustup completions fish > "$pkgdir/usr/share/fish/completions/rustup.fish"
    mkdir -p "$pkgdir/usr/share/zsh/site-functions"
    $pkgdir/usr/bin/rustup completions zsh > "$pkgdir/usr/share/zsh/site-functions/_rustup"

    install -Dm644 LICENSE-MIT "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE-MIT
    install -Dm644 LICENSE-APACHE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

# vim:filetype=sh:
