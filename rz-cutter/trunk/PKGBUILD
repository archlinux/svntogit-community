# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: Aaron McDaniel (mcd1992) <'aur' at the domain 'fgthou.se'>

pkgname=rz-cutter
_gitcommit=c0f260ca6913f3928259309766692e50131f7270
pkgver=2.2.0
pkgrel=1
pkgdesc='Qt and C++ GUI for rizin reverse engineering framework'
url='https://github.com/rizinorg/cutter'
arch=('x86_64')
license=('GPL3')
depends=(
  rizin
  capstone
  qt5-base
  qt5-svg
  qt5-webengine
  icu
  python
  pyside2
  python-shiboken2
  graphviz
  gcc-libs
  syntax-highlighting
)
makedepends=('git' 'cmake' 'ninja' 'shiboken2' 'qt5-tools')
optdepends=('rz-ghidra: ghidra decompiler plugin')
source=("git+https://github.com/rizinorg/cutter#commit=${_gitcommit}"
        "git+https://github.com/rizinorg/cutter-translations")
sha512sums=('SKIP'
            'SKIP')
b2sums=('SKIP'
        'SKIP')

pkgver() {
  cd cutter
  git describe --tags --match 'v*' | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd cutter
  git config 'submodule.src/translations.url' "${srcdir}/cutter-translations"
  git -c protocol.file.allow=always submodule update --init src/translations
}

build() {
  cd cutter
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCUTTER_ENABLE_PYTHON=ON \
    -DCUTTER_ENABLE_PYTHON_BINDINGS=ON \
    -DCUTTER_USE_BUNDLED_RIZIN=OFF \
    -DCUTTER_USE_ADDITIONAL_RIZIN_PATHS=OFF \
    -DCUTTER_ENABLE_CRASH_REPORTS=OFF \
    -DCUTTER_ENABLE_GRAPHVIZ=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd cutter
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -a docs/* "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
