# Maintainer: Anatol Pomozov

pkgname=s2n-tls
pkgver=1.0.0
pkgrel=1
pkgdesc='A C99 implementation of the TLS/SSL protocols that is designed to be simple, small, fast, and with security as a priority'
arch=(x86_64)
url='https://github.com/aws/s2n-tls'
license=(Apache)
provides=(s2n) # upstream renamed the project from s2n to s2n-tls
conflicts=(s2n)
replaces=(s2n)
depends=(openssl)
makedepends=(cmake)
source=(s2n-tls-$pkgver.zip::https://github.com/aws/s2n-tls/archive/v$pkgver.zip
        missing_symbol_workaround.patch)
sha256sums=('4b1136d4dc7c82dfc3dce9c1ac7900abeecba77a69397fdac0bc42309c52d933'
            'd86e81598a79de7353262eeffc490c1e266dc8a92f7057166d447cd36d04eb35')

prepare() {
  cd s2n-tls-$pkgver

  patch -p1 < ../missing_symbol_workaround.patch # https://github.com/awslabs/s2n/issues/2401
}

build() {
  cd s2n-tls-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_SHARED_LIBS=ON -S . -B build
  cmake --build build
}

check() {
  cd s2n-tls-$pkgver
  cmake --build build --target test
}

package() {
  cd s2n-tls-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
