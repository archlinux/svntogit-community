# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Roheim <david dot roheim at gmail dot com>
# Contributor: Chris Heien <chris.h.heien@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=s3fs-fuse
pkgver=1.80
pkgrel=2
pkgdesc="FUSE-based file system backed by Amazon S3"
arch=('i686' 'x86_64')
url="https://github.com/s3fs-fuse/s3fs-fuse/wiki"
license=('GPL2')
depends=('fuse2' 'curl' 'libxml2')
conflicts=('s3fs')
provides=('s3fs')
replaces=('s3fs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/s3fs-fuse/s3fs-fuse/archive/v$pkgver.tar.gz")
sha256sums=('de8381c9bd73e491904e73561882dc91e5fad9c28fe88834a93d7b4fe12cbd35')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
