# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joost Bremmer <contact at madeofmagicandwires dot online>
# Contributor: Eliezio Oliveira <eliezio@pm.me>
# Contributor: Matthew Gamble <internet@matthewgamble.net>

pkgname=sad
pkgver=0.4.17
pkgrel=1
pkgdesc="Space Age seD"
arch=('x86_64')
url="https://github.com/ms-jpq/sad"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
optdepends=(
  'fzf: for tui usage'
  'diff-so-fancy: for diff colourizer'
  'git-delta: for diff colourizer'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('4cb667679c02b73c17c2f00ed34e4e458b6f07692700f5a7bc37fa63f74ea13f2d89f79659b3d7a973e890de8d647ca59b57f6a682ee42415af02b79b78022e0')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: set ts=2 sts=2 sw=2 et :
