# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sage-mathematics-doc
pkgver=6.4.1
pkgrel=1
pkgdesc="HTML documentation and inline help for Sage"
arch=('any')
url="http://www.sagemath.org"
license=('GPL')
depends=('sage-notebook' 'python2-sphinx')
source=("https://github.com/sagemath/sage/archive/$pkgver.tar.gz")
md5sums=('e40736461992e62af3a84cf9a212c9d1')

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC="$SAGE_SRC"/doc
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="/usr/lib/sage/site-packages"
  python2 doc/common/builder.py -k all html
}

package() {
  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r * "$pkgdir"/usr/share/doc/sage
  rm "$pkgdir"/usr/share/doc/sage/Makefile
  rm -r "$pkgdir"/usr/share/doc/sage/output/doctrees
}
