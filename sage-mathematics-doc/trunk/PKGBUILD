# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=sage-mathematics-doc
pkgname=('sage-mathematics-doc' 'sage-mathematics-src')
pkgver=6.4.1
pkgrel=6
arch=('any')
url="http://www.sagemath.org"
license=('GPL')
makedepends=('sage-notebook' 'python2-sphinx')
source=("https://github.com/sagemath/sage/archive/$pkgver.tar.gz" 'paths.patch' 'gap-hap.patch' 'fes02.patch')
md5sums=('e40736461992e62af3a84cf9a212c9d1'
         '9a39301dedeb560f7bfddb81b6853f28'
         '631ee6b8b3e7d12bb7858cfd841af483'
         '506944613082ba7f5b34360939ca90eb')

prepare() {
  cd sage-$pkgver

# add optional packages manually (Fedora)  
  sed -e 's|is_package_installed("nauty")|True|' -i src/sage/graphs/graph_generators.py
# fix paths in python imports
  patch -p0 -i $srcdir/paths.patch
# fix linking to blas/cblas
  sed -e "s|cblas(), atlas(),|'cblas', 'blas',|" -i src/sage/misc/cython.py
# don't use is_package_installed function
  patch -p0 -i $srcdir/gap-hap.patch
# supress warning about GAP install dir
  sed -e "s|gapdir = os.path.join(SAGE_LOCAL, 'gap', 'latest')|gapdir = '/usr/lib/gap'|" -i src/sage/libs/gap/util.pyx 
# use small Cremona database
  sed -e "s|is_package_installed('database_cremona_ellcurve')|False|" -i src/sage/databases/cremona.py
# fix build against libfes 0.2 http://trac.sagemath.org/ticket/15209
  patch -p0 -i $srcdir/fes02.patch

# use python2
  sed -e 's|cython %s %s|cython2 %s %s|' -i src/sage/misc/cython.py
  sed -e 's|python setup.py|python2 setup.py|' -i src/sage/misc/cython.py
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC="$SAGE_SRC"/doc
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="/usr/lib/sage/site-packages"
  python2 doc/common/builder.py -k all html
}

package_sage-mathematics-doc() {
  pkgdesc="HTML documentation and inline help for Sage"
  depends=('sage-notebook' 'python2-sphinx')
  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r * "$pkgdir"/usr/share/doc/sage
  rm "$pkgdir"/usr/share/doc/sage/Makefile
  rm -r "$pkgdir"/usr/share/doc/sage/output/doctrees
}

package_sage-mathematics-src() {
  pkgdesc="Source files for sage-mathematics"
  
  mkdir -p "$pkgdir"/usr/share/sage/src
  cp -r sage-$pkgver/src/sage "$pkgdir"/usr/share/sage/src

  mkdir -p "$pkgdir"/usr/include/csage
  cp -r sage-$pkgver/src/c_lib/include/* "$pkgdir"/usr/include/csage
}
