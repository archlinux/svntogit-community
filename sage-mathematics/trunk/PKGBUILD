# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Antonio Rojas <nqn1976 at gmail dot com>
# Contributor: Thomas Dziedzic <gostrc at gmail dot com>
# Contributor: Osman Ugus <ugus11 at yahoo dot com>
# Contributor: Stefan Husmann <stefan-husmann at t-online dot de>
# Special thanks to Nareto for moving the compile from the .install to the PKGBUILD

pkgname=sage-mathematics
pkgver=6.4
pkgrel=1
pkgdesc="Open Source Mathematics Software, free alternative to Magma, Maple, Mathematica, and Matlab"
arch=('i686' 'x86_64')
url="http://www.sagemath.org"
license=('GPL')
#depends=('desktop-file-utils' 'java-environment=7' 'libjpeg-turbo' 'libtiff' 'libxmu' 'sqlite' 'xz')
depends=('libatomic_ops')
makedepends=('desktop-file-utils' 'gcc-fortran' 'gendesk' 'python2')
optdepends=('imagemagick: some plotting functionality benefits from it'
            'texlive-core: some plotting functionality benefits from it, also to use SageTeX'
            'openssh: to use the notebook in secure mode'
            'ffmpeg: to show animations'
            'cairo: R plots')
source=("http://boxen.math.washington.edu/home/sagemath/sage-mirror/src/sage-${pkgver}.tar.gz"
        "sage.service"
        "gf2x-sse2-i686.patch")
install="${pkgname}.install"
md5sums=('5e7de1eab233055fd3456dff07702810'
         '985da1c1d1dcdc3ea9aa73035cb7996b'
         'f9d7aba4f758f4605164eb84b9e1e3ba')
changelog=ChangeLog

prepare() {
  # create *.desktop file
  gendesk -f -n \
          --pkgname="sage-notebook" \
          --pkgdesc="Sage notebook" \
          --name="Sage" \
          --exec="/opt/sage/sage -notebook" \
          --terminal=true \
          --categories="Education;Science;Math" \
          --custom="X-DCOP-ServiceType=
X-KDE-SubstituteUID=false
X-KDE-Username="

  # create DOT_SAGE directory
  rm -rf "${srcdir}/build"
  mkdir "${srcdir}/build"

  # disable building gf2x with sse2 for i686
  if [ "${CARCH}" == "i686" ]; then
    cp "${srcdir}/gf2x-sse2-i686.patch" "${srcdir}/sage-${pkgver}/build/pkgs/gf2x/patches/sse2.patch"
  fi
}

build() {
  cd "sage-${pkgver}"

  # disable default makepkg flags (required by singular, libgap and zeromq)
  unset CFLAGS
  unset CXXFLAGS
  unset CPPFLAGS
  unset LDFLAGS
  ## flags
  # do not build own gcc
  export SAGE_INSTALL_GCC='no'
  # disable debug
  export SAGE_DEBUG='no'
  # enable fat binaries
  export SAGE_FAT_BINARY='yes'
  # can't write to root in a clean chroot
  export DOT_SAGE="${srcdir}/build"
  # singular is broken
  export CPP='/usr/bin/cpp'

  make
}

<< COMMENT
check() {
  cd "sage-${pkgver}"

  make test
  #make ptestlong
}
COMMENT

package() {
  cd "${srcdir}/sage-${pkgver}/"
  # remove build logs
  rm -f *.log
  rm -rf "${srcdir}/sage-${pkgver}/"{logs,upstream}
  # do NOT remove build directory!

  # cp because make install is experimental and will corrupt the install
  install -dm755 "${pkgdir}/opt/sage"
  cp -r * "${pkgdir}/opt/sage/"

  # move SageTeX files to more appropriate directory
  install -dm755 "${pkgdir}/usr/share"
  mv "${pkgdir}/opt/sage/local/share/texmf" "${pkgdir}/usr/share"

  # according to FS#37090
  # install scripts
  install -dm755 "${pkgdir}/usr/bin"
  ./sage -c "install_scripts('${pkgdir}/usr/bin', ignore_existing=True)"
  # rename scripts to avoid conflicts
  for ITEM in $(ls "${pkgdir}/usr/bin"); do
    mv "${pkgdir}/usr/bin/${ITEM}" "${pkgdir}/usr/bin/sage-${ITEM}"
  done
  ln -s "/opt/sage/sage" "${pkgdir}/usr/bin/sage"

  # FIXME fix bad mtime
  cd "${pkgdir}/opt/sage/local/lib/python2.7"
  find . -name '*.py' -exec sh -c "rm {}c 2> /dev/null && python2 -m compileall {}" \;

  # install a systemd user unit
  install -Dm644 "${srcdir}/sage.service" "${pkgdir}/usr/lib/systemd/user/sage.service"
  # install *.desktop and icon files
  install -Dm644 "${srcdir}/sage-notebook.desktop" \
                 "${pkgdir}/usr/share/applications/sage-notebook.desktop"
  install -Dm644 "${pkgdir}/opt/sage/local/lib/python2.7/site-packages/sagenb-0.11.1-py2.7.egg/sagenb/data/sage/images/icon48x48.png" \
                 "${pkgdir}/usr/share/pixmaps/sage-notebook.png"
}

# vim :set ts=2 sw=2 et:
