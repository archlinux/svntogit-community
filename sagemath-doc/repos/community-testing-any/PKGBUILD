# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=sagemath-doc
pkgname=(sagemath-doc sagemath-src)
pkgver=6.7
pkgrel=1
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
makedepends=(sagemath sage-notebook python2-sphinx python2-pyzmq python2-pillow)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
	'package.patch' 'paths.patch' 'fes02.patch' 'ntl9.patch')
md5sums=('df744e882a4b25b42dd8347e202cd497'
         '0eb5ac1537aac93bd67a2925c0cfd84d'
         '020bdb34b6fc0f2c9f46341bdb79937a'
         '506944613082ba7f5b34360939ca90eb'
         '308abbb1a49db9b8f0e088aff69567ff')

prepare() {
  cd sage-$pkgver

# Arch-specific patches
# assume all optional packages are installed
  patch -p0 -i "$srcdir"/package.patch
# fix paths in python imports
  patch -p0 -i "$srcdir"/paths.patch
# fix cython linking
  sed -e "s| atlas(),||" -i src/sage/misc/cython.py
# supress warning about GAP install dir
  sed -e "s|gapdir = os.path.join(SAGE_LOCAL, 'gap', 'latest')|gapdir = '/usr/lib/gap'|" -i src/sage/libs/gap/util.pyx 
# fix Cremona database detection
  sed -e "s|is_package_installed('database_cremona_ellcurve')|os.path.exists('/usr/share/sage/cremona/cremona.db')|" \
   -i src/sage/databases/cremona.py
# fix IPython kernel path
  sed -e "s|os.path.join(SAGE_ROOT, 'sage')|'/usr/bin/sage'|" -i src/sage/repl/ipython_kernel/install.py
# find bliss headers
  sed -e 's|graph.hh|bliss/graph.hh|' -i src/sage/graphs/bliss.pyx
# Rename class to fix conflicts with NTL
  sed -e 's|WrappedPtr|SageWrappedPtr|' -i src/sage/libs/polybori/decl.pxd -i src/sage/libs/polybori/pb_wrap.h  

# Upstream patches
# fix build against libfes 0.2 http://trac.sagemath.org/ticket/15209
  patch -p0 -i "$srcdir"/fes02.patch
# fix build aginst NTL 9 (Fedora)
  patch -p0 -i "$srcdir"/ntl9.patch

# use python2
  sed -e 's|cython %s %s|cython2 %s %s|' -e 's|python setup.py|python2 setup.py|' -i src/sage/misc/cython.py

# remove developer interface
  rm -r src/sage/dev
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC="$SAGE_SRC"/doc
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="/usr/lib/sage/site-packages"
  python2 doc/common/builder.py -k all html
}

package_sagemath-doc() {
  pkgdesc="HTML documentation and inline help for SageMath"
  depends=(sage-notebook python2-sphinx)
  conflicts=(sage-mathematics-doc)
  replaces=(sage-mathematics-doc)

  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r * "$pkgdir"/usr/share/doc/sage
  rm "$pkgdir"/usr/share/doc/sage/Makefile
  rm -r "$pkgdir"/usr/share/doc/sage/output/doctrees
}

package_sagemath-src() {
  pkgdesc="Source files for SageMath"
  conflicts=(sage-mathematics-src)
  replaces=(sage-mathematics-src)
  
  mkdir -p "$pkgdir"/usr/share/sage/src
  cp -r sage-$pkgver/src/{sage,sage_setup} "$pkgdir"/usr/share/sage/src
  ln -s /usr/share/doc/sage "$pkgdir"/usr/share/sage/src/doc

  mkdir -p "$pkgdir"/usr/include/csage
  cp -r sage-$pkgver/src/c_lib/include/* "$pkgdir"/usr/include/csage
}
