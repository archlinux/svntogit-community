# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-doc
pkgver=8.6
pkgrel=2
pkgdesc="HTML documentation and inline help for SageMath"
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
depends=(python2-sphinx gap-doc thebe)
makedepends=(sagemath python2-pyzmq python2-docutils python2-jupyter_client python2-pkgconfig cython2 python2-ipywidgets)
source=("$pkgname-$pkgver.tar.gz::https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
        docbuild_main.patch
        sagemath-doc-sphinx-1.8.patch
        sagemath-doc-segfault.patch
        sagemath-ecl-sigfpe.patch
        sagemath-cypari2.patch)
sha256sums=('dea613fc955cada76aaa7ab379bc5a219fe8b496064824f8c798b2b1651442fa'
            'ea5e17bb7a7cb36a22e5e3872fcc2585852bc971c4b139b0b2cd69a36c1b009b'
            '337d1d84749c1415c120a95d621d8cb0a4ef6e29245193c673e4b3f3a5d56fe9'
            '141ac064305526785fb6d9507896876cbf893cd41ec1e42d86fc836f766c200f'
            'a42f3b152b1aedb8abf16bc70971419919d1fe30328574e7fef8305f9d07d938'
            'ca47248d2ed5edfe663ea02e261ddbb26a7cb03bef67928dbec690d9b9a8f129')

prepare() {
  cd sage-$pkgver

# fix relative imports
  patch -p0 -i ../docbuild_main.patch
# use sage_setup imports from sagemath source, but use system sage ones (which include compiled modules)
  mkdir -p local-python
  ln -sr src/sage_setup local-python
# fix build with sphinx 1.8
  patch -p1 -i ../sagemath-doc-sphinx-1.8.patch
# Partially revert https://trac.sagemath.org/ticket/24655 to workaround a segfault
  patch -Rp1 -i ../sagemath-doc-segfault.patch
# Fix build with cypari 2.0 https://trac.sagemath.org/ticket/26442
  patch -p1 -i ../sagemath-cypari2.patch
# Fix SIGFPE crashes with ecl 16.1.3 https://trac.sagemath.org/ticket/22191
  patch -p1 -i ../sagemath-ecl-sigfpe.patch
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_ROOT="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC_SRC="$SAGE_SRC"/doc
  export SAGE_DOC="$SAGE_DOC_SRC"
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="$srcdir"/sage-$pkgver/local-python
  python2 sage_setup/docbuild --no-pdf-links all html
}

package() {
  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage/en
  cp -r en/introspect "$pkgdir"/usr/share/doc/sage/en
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
    rm -r $_dir
    ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}
