# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-doc
pkgver=9.7
pkgrel=2
pkgdesc='HTML documentation for SageMath'
arch=(any)
url='http://www.sagemath.org'
license=(GPL)
depends=(python-pplpy)
makedepends=(sagemath python-pyzmq python-docutils python-jupyter_client python-pkgconfig cython
             python-jupyter-sphinx mathjax python-sphinx-furo)
source=(https://github.com/sagemath/sage/archive/$pkgver/sagemath-$pkgver.tar.gz
        sagemath-gap-4.12.patch
        sagemath-pari-2.15.patch
        sagemath-sphinx-5.2.patch)
sha256sums=('9f26f14aa322e3cf38a71835b12ac34b23026b467f74d54b064c5d025e76fbfd'
            '84c1700e285ab1d94d16d0a602417a414447d8a23ac2e55a093285cc4bd2916d'
            '8930fcfc0ec8dc2c49c7bc1e853dbc33d8bf8fc0508f22f28980858b7264048f'
            '182cd2626d4a80db03a89e7d817c0e4aa27b1ea0a638662c431587991ab5e429')
options=(!strip) # nothing to strip, save packaging time

prepare() {
  cd sage-$pkgver

# Fix tests with GAP 4.12 and pari 2.15
  patch -p1 < ../sagemath-gap-4.12.patch
  patch -p1 < ../sagemath-pari-2.15.patch
# Fix build with sphinx 5.2
  patch -p1 < ../sagemath-sphinx-5.2.patch

# https://trac.sagemath.org/ticket/25833#comment:98
  mkdir mathjax-tmp
  cp -r /usr/share/mathjax mathjax-tmp

  SAGE_ROOT="$PWD" PATH="$PWD/build/bin:$PATH" src/doc/bootstrap
  make -C src/doc doc-src

  rm -r src/sage # Use the installed sage, not the one from the source tree
}

build() {
  cd sage-$pkgver/src

  export SAGE_SRC="$PWD" \
         SAGE_DOC="$PWD"/doc \
         MATHJAX_DIR="$srcdir"/sage-$pkgver/mathjax-tmp \
         SAGE_NUM_THREADS=10 \
         LANGUAGE=C
  python -m sage_docbuild --no-pdf-links --mathjax all html
}

package() {
  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
    rm -r $_dir
    ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}
