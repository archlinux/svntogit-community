# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contibutor: Christer Edwards <christer.edwards@gmail.com>

pkgname=salt
pkgver=2014.1.1
pkgrel=1
pkgdesc='Central system and configuration manager'
arch=('any')
url='http://saltstack.org/'
license=('Apache')
depends=('python2'
         'python2-crypto'
         'python2-jinja'
         'python2-m2crypto'
         'python2-msgpack'
         'python2-psutil'
         'python2-pyzmq'
         'python2-systemd'
         'python2-yaml'
         'apache-libcloud'
         'sshpass')
optdepends=('dmidecode: decode SMBIOS/DMI tables')
backup=('etc/salt/master'
        'etc/salt/minion')
install=salt.install
source=("http://pypi.python.org/packages/source/s/salt/salt-$pkgver.tar.gz"
        'salt-master.service'
        'salt-syndic.service'
        'salt-minion.service'
        'salt.tmpfiles')

md5sums=('c5c3ed043ad81cd71aff067c4e44d596'
         '22d15fdc9b05c580c2927adf192ec76e'
         '1839f3b46e5567a91b4ef0fcf943ddb6'
         'ac8023a1e2ab9c107cdc20658212caf4'
         '8772c329a5a90aac495d14d5797cc0fd')

prepare() {
  # we run master as user salt
  sed -ri 's|^#user: root|user: salt|' $pkgname-$pkgver/conf/master
}

package() {
  # systemd
  install -Dm644 salt-master.service "$pkgdir/usr/lib/systemd/system/salt-master.service"
  install -Dm644 salt-syndic.service "$pkgdir/usr/lib/systemd/system/salt-syndic.service"
  install -Dm644 salt-minion.service "$pkgdir/usr/lib/systemd/system/salt-minion.service"
  install -Dm644 salt.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/salt.conf"

  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1 \
    --salt-pidfile-dir="/run/salt"

  # default config
  install -Dm644 conf/master "$pkgdir/etc/salt/master"
  install -Dm644 conf/minion "$pkgdir/etc/salt/minion"

  # salt directories (cache must be owned by salt)
  install -dm750 -o 141 -g 141 "$pkgdir/var/cache/salt"
  install -dm750 -o 141 -g 141 "$pkgdir/var/log/salt"
  install -dm750 -o 141 -g 141 "$pkgdir/srv/salt"

  # salt config editable by salt user
  chown -R 141:141 "$pkgdir/etc/salt"
  chmod 750 "$pkgdir/etc/salt"
}

# vim:set ts=2 sw=2 et:
