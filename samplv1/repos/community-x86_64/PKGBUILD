# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.23
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=('x86_64')
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'lv2-host' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'liblo' 'libsndfile' 'lv2' 'qt6-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application'
            'qt6-wayland: for native wayland support')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('96c28d87b98c9f26733dfdbcb81b67186f41611dc357cfd88cdd619716c28bc839b17662cd72f252fd3283d702ff769502b95f0f7bccf74693038340560a8125')
b2sums=('5c7890b7ab7b73f152827fbece622a071e145469f1f76dd7c7fd8a9783af1a1bc1a41f1293a1540250a98a179d0f1eab32601bc3d590474e4af0566d04171c5a')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
