# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: JJDaNiMoTh <jjdanimoth.aur@gmail.com>
# Contributor: Tino Reichardt <milky-archlinux@mcmilk.de>

pkgname=sarg
pkgver=2.3.5
pkgrel=1
pkgdesc="Squid Analysis Report Generator"
arch=('i686' 'x86_64')
url="http://sarg.sourceforge.net"
license=('GPL')
depends=('gd' 'libldap')
makedepends=('cmake')
backup=('etc/sarg/css.tpl'
	'etc/sarg/exclude_codes'
	'etc/sarg/sarg.conf'
	'etc/sarg/user_limit_block')
source=(http://downloads.sourceforge.net/sourceforge/sarg/$pkgname-$pkgver.tar.gz)
md5sums=('a10545aa0571f73789d81dcfca5bfb3c')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr # po files workaround
  rm -f Makefile
  LDFLAGS=-lm cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc/sarg -DMANDIR=/usr/share/man/man1 .
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  mkdir -p $pkgdir/usr/share/sarg $pkgdir/etc
  make DESTDIR="$pkgdir" install
  make -C po DESTDIR="$pkgdir" install
  cp -a sarg-php ${pkgdir}/usr/share/sarg/
  chmod -R o+r ${pkgdir}{/etc,/usr/share/sarg}
  chmod o+x ${pkgdir}/usr/share/sarg/sarg-php
  chmod -R o+x ${pkgdir}/usr/share/sarg/sarg-php/locale
}
