# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: JJDaNiMoTh <jjdanimoth.aur@gmail.com>
# Contributor: Tino Reichardt <milky-archlinux@mcmilk.de>

pkgname=sarg
pkgver=2.3.11
pkgrel=1
pkgdesc="Squid Analysis Report Generator"
arch=('x86_64')
url="http://sarg.sourceforge.net"
license=('GPL')
depends=('gd' 'libldap')
makedepends=('cmake')
backup=('etc/sarg/css.tpl'
	'etc/sarg/exclude_codes'
	'etc/sarg/sarg.conf'
	'etc/sarg/user_limit_block')
source=(https://downloads.sourceforge.net/sourceforge/sarg/$pkgname-$pkgver.tar.gz
	fix-sizes.patch)
sha256sums=('d0f8fbefb5fdc716e75c778525d96ac9eef622d3de94da8d70e2bdc7ab81daa2'
            '6034552698c55e012047feebc47640768202202f9cda8b1af1503e83722746f3')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 <"$srcdir"/fix-sizes.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  export CFLAGS="$CFLAGS -Wno-error"
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc/sarg \
    -DMANDIR=/usr/share/man/man1 \
    .
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  mkdir -p "$pkgdir"/usr/share/sarg "$pkgdir"/etc
  make DESTDIR="$pkgdir" install
}
