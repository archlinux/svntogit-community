# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=sassc
pkgver=3.5.0
pkgrel=2
pkgdesc="C implementation of Sass CSS preprocessor."
arch=("x86_64")
url="http://libsass.org/"
license=("MIT")
depends=("libsass")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha256sums=("26f54e31924b83dd706bc77df5f8f5553a84d51365f0e3c566df8de027918042")


build() {
	cd "$srcdir/$pkgname-$pkgver"

	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir/" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
