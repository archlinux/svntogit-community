# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=1.0.2
pkgrel=1
pkgdesc='The interactive build tool'
arch=('any')
url='http://www.scala-sbt.org/'
license=('BSD')
depends=('java-runtime-headless=8' 'sh')
IFS='.' read -a _pkgver <<< "${pkgver}"
_majorver=${_pkgver[0]}.x
install=sbt.install
source=(https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tgz{,.asc}
        LICENSE)
validpgpkeys=('2EE0EA64E40A89B84B2DF73499E82A75642AC823') # sbt build tool
sha256sums=('c3f063d8122d87f0d80b25221d76b0a164e9247d35190f31ec360edcb4352ef0'
            'SKIP'
            '70b7b72694a56a3e922deaf5482273e7c33bab2c00c0c67fc0ccc5d33927ffed')
backup=(etc/${pkgname}/sbtopts)

package() {
  cd "${pkgname}"

  mkdir -p "${pkgdir}"/usr/share/${pkgname}
  cp -r lib "${pkgdir}"/usr/share/${pkgname}
  cp -r bin "${pkgdir}"/usr/share/${pkgname}
  rm "${pkgdir}"/usr/share/${pkgname}/bin/*.bat
  chmod -x "${pkgdir}"/usr/share/${pkgname}/bin/*
  chmod +x "${pkgdir}"/usr/share/${pkgname}/bin/sbt
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /usr/share/${pkgname}/bin/sbt "${pkgdir}"/usr/bin/sbt

  mkdir -p "${pkgdir}/etc"
  cp -r conf "${pkgdir}"/etc/${pkgname}
  rm "${pkgdir}"/etc/${pkgname}/sbtconfig.txt
  ln -s /etc/${pkgname} "${pkgdir}"/usr/share/${pkgname}/conf

  install -D "${srcdir}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
