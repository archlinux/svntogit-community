# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

# TODO: remove old files from svn (Licenses, sbt, â€¦)

pkgname=sbt
epoch=1
pkgver=0.13.9
pkgrel=1
pkgdesc="A minimally intrusive build tool for Scala projects"
arch=('any')
url='http://www.scala-sbt.org/'
license=('BSD')
depends=('java-runtime-headless' 'sh')
IFS='.' read -a _pkgver <<< "${pkgver}"
source=(https://dl.bintray.com/${pkgname}/native-packages/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tgz
        https://raw.githubusercontent.com/${pkgname}/${pkgname}/${_pkgver[0]}.${_pkgver[1]}/LICENSE
        sbt.sh
        sbt.conf.sh
        sbt.conf.csh)
sha256sums=('148f2801f2993773de6f8859fe0e6520fcabe649d66bb316e13aff8b2fd7f504'
            '70b7b72694a56a3e922deaf5482273e7c33bab2c00c0c67fc0ccc5d33927ffed'
            '68f25719dde7e7de53e5f42eddd5d36e275a83cbb9f6a28208f245404233a6c7'
            'b90676fc30faa4ad2ec3beb7815491bb58cd6e057ec0593664344d6711482357'
            'b8825593744535787d2c227b61e4ffa2e95a7da071a6488c108ad79abeb9e182')

package() {
  install -d "${pkgdir}"/usr/share
  cp -r "${srcdir}/${pkgname}" "${pkgdir}"/usr/share
  rm "${pkgdir}/usr/share/${pkgname}/bin/sbt.bat"
  rm "${pkgdir}/usr/share/${pkgname}/conf/sbtconfig.txt"
  find "${pkgdir}/usr/share/${pkgname}" -type f ! -name sbt -exec chmod -x {} \;

  install -d "${pkgdir}"/usr/bin
  ln -s "/usr/share/${pkgname}/bin/sbt" "${pkgdir}"/usr/bin/sbt

  install -d "${pkgdir}/etc/${pkgname}"
  ln -s "/usr/share/${pkgname}/conf/sbtopts" "${pkgdir}/etc/${pkgname}/sbtopts"

  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
