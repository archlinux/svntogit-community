# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sc3-plugins
pkgver=3.11.1
pkgrel=2
pkgdesc="Extension plugins for the SuperCollider3 audio synthesis server."
arch=(x86_64)
url="https://github.com/supercollider/sc3-plugins"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc supercollider)
makedepends=(cmake fftw stk)
source=("https://github.com/supercollider/${pkgname}/releases/download/Version-${pkgver}/${pkgname}-${pkgver}-Source.tar.bz2"{,.asc})
sha512sums=('5ba79f4e691615ac2a508bad2043a64ab1359cd88f1cf3a258cb3cc218a2d4d2d077d91ad86ff87df4896b8f42ab711b847cb67d9aebbdc9208e1c88513b25aa'
            'SKIP')
b2sums=('94a4a2dc8d6076af02205ca7ec822e1b58832ae8152a7f6942b1564710853cf28b5e3cf5cd8711988999bb7e311b38baa17691f157bc823b7df69bac27f4c370'
        'SKIP')
validpgpkeys=('0B7D1EA4E32B5C3C4275D3C70796CC538BEFF177') # Brian Heim <self@brianlheim.com>

prepare() {
  mv -v "$pkgname-$pkgver-Source" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DSC_PATH=/usr/include/SuperCollider/ \
        -DSYSTEM_STK=ON \
        -DSUPERNOVA=ON \
        -W no-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfftw3f.so libstk-4.6.2.so)

  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 {{DEVELOPING,README}.md,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
