# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>

pkgbase=scala
pkgname=(scala scala-devel-docs scala-sources)
pkgver=2.8.1
pkgrel=2
pkgdesc="A Java-interoperable language with object-oriented and functional features"
arch=('any')
url="http://www.scala-lang.org"
license=('custom')
depends=('java-runtime')
makedepends=('apache-ant')
optdepends=('sbaz: bazaar package management'
	    'scala-devel-docs'
	    'scala-sources')
source=(http://www.scala-lang.org/downloads/distrib/files/scala-$pkgver.final-sources.tgz
	http://www.scala-lang.org/downloads/distrib/files/scala-$pkgver.final-devel-docs.tgz)
md5sums=('6a6cd9dbbabf74d9d43f69e5328671e9'
         'afd5c7d3073bd735a25cfc4ed61f3543')

build(){
    install -d ${pkgdir}/usr/{bin,share} ${pkgdir}/usr/share/man/man1 ${pkgdir}/usr/share/scala/{bin,lib}

    cd ${srcdir}/${pkgname}-${pkgver}.final-sources
    ant docs
    ant dist.src
    ant dist
}

package_scala()
{
    cd ${srcdir}/${pkgname}-${pkgver}.final-sources/dists/latest/

    mkdir -p ${pkgdir}/usr/share/scala/bin ${pkgdir}/usr/share ${pkgdir}/usr/bin

    cp -r {lib,misc} ${pkgdir}/usr/share/scala/
    cp -r man ${pkgdir}/usr/share/
    install -m 755 bin/{fsc,scala,scalac,scalap,scaladoc} ${pkgdir}/usr/share/scala/bin
    install -D -m0644 doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

    ln -s ../share/scala/bin/fsc ${pkgdir}/usr/bin/fsc
    ln -s ../share/scala/bin/scala ${pkgdir}/usr/bin/scala
    ln -s ../share/scala/bin/scalac ${pkgdir}/usr/bin/scalac
    ln -s ../share/scala/bin/scalap ${pkgdir}/usr/bin/scalap
    ln -s ../share/scala/bin/scaladoc ${pkgdir}/usr/bin/scaladoc
}

package_scala-devel-docs()
{
    replaces=('scala-doc')
    pkgdesc="Scala documentation"
    depends=('scala')

    cd ${srcdir}
    mkdir -p $pkgdir/usr/share/doc/
    cp -r scala-$pkgver.final-devel-docs $pkgdir/usr/share/doc/scala
}

package_scala-sources()
{
    replaces=('scala-src')
    pkgdesc="Scala sources"
    depends=('scala')

    cd ${srcdir}/${pkgname}-${pkgver}.final-sources/dists/latest/
    mkdir -p $pkgdir/usr/share/scala/
    cp -r src $pkgdir/usr/share/scala/src
}
