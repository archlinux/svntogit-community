# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>

pkgbase=scala
pkgname=(scala scala-docs scala-sources)
pkgver=2.10.3
pkgrel=1
pkgdesc="A Java-interoperable language with object-oriented and functional features"
arch=('any')
url="http://www.scala-lang.org"
license=('BSD')
depends=('java-runtime')
source=("http://www.scala-lang.org/files/archive/scala-${pkgver}.tgz"
	"http://www.scala-lang.org/files/archive/scala-docs-${pkgver}.txz")
md5sums=('7665a125ceb38c1ba32cbb9acba9070f'
         '869af24fe0f5df49ff90982635d9aa23')

package_scala() {
  depends=('java-runtime')
  optdepends=('scala-docs' 'scala-sources' 'graphviz: generate diagrams')

  cd ${srcdir}/${pkgbase}-${pkgver}

  install -d ${pkgdir}/usr/{bin,share} ${pkgdir}/usr/share/man/man1 ${pkgdir}/usr/share/scala/{bin,lib}
  cp -r {lib,misc} ${pkgdir}/usr/share/scala/
  cp -r man ${pkgdir}/usr/share/
  install -m 755 bin/{fsc,scala,scalac,scalap,scaladoc} ${pkgdir}/usr/share/scala/bin
  install -D -m0644 doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  ln -s ../share/scala/bin/fsc ${pkgdir}/usr/bin/fsc
  ln -s ../share/scala/bin/scala ${pkgdir}/usr/bin/scala
  ln -s ../share/scala/bin/scalac ${pkgdir}/usr/bin/scalac
  ln -s ../share/scala/bin/scalap ${pkgdir}/usr/bin/scalap
  ln -s ../share/scala/bin/scaladoc ${pkgdir}/usr/bin/scaladoc
}

package_scala-docs() {
  replaces=('scala-doc' 'scala-devel-docs')
  pkgdesc="Scala documentation"
  depends=()

  cd ${srcdir}
  mkdir -p $pkgdir/usr/share/doc/
  cp -r scala-docs-${pkgver} $pkgdir/usr/share/doc/scala
  install -D -m0644 ${srcdir}/${pkgbase}-${pkgver}/doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_scala-sources() {
  replaces=('scala-src')
  pkgdesc="Scala sources"
  depends=()

  cd ${srcdir}/${pkgbase}-${pkgver}
  mkdir -p $pkgdir/usr/share/scala/
  cp -r src $pkgdir/usr/share/scala/src
  install -D -m0644 doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
