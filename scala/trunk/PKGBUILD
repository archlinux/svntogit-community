# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>

pkgbase=scala
pkgname=(scala scala-devel-docs scala-sources)
pkgver=2.9.2
pkgrel=3
_distdate=20120330-163119
_distsha=949a4804e4
pkgdesc="A Java-interoperable language with object-oriented and functional features"
arch=('any')
url="http://www.scala-lang.org"
license=('custom')
depends=('java-runtime=6')
makedepends=('apache-ant' 'git')
source=(http://www.scala-lang.org/downloads/distrib/files/scala-sources-$pkgver.tgz
	http://www.scala-lang.org/downloads/distrib/files/scala-docs-$pkgver.txz)
md5sums=('8b4b3347e2350dcba0aa1615f76de746'
         '6e40b5d76de3d39a9ad834af1f10841d')

build(){
    cd ${srcdir}/${pkgname}-${pkgver}-sources
    chmod 0755 tools/*
    echo -e "#!/bin/bash\necho ${_distdate}" >tools/get-scala-commit-date
    echo -e "#!/bin/bash\necho ${_distsha}" >tools/get-scala-commit-sha
    ant docs
    ant dist.src
    ant dist-opt -Dbuild.release=true
}

package_scala()
{
  depends=('java-runtime')
  optdepends=('sbaz: bazaar package management'
	    'scala-devel-docs'
	    'scala-sources')

    cd ${srcdir}/${pkgname}-${pkgver}-sources/dists/latest/
    install -d ${pkgdir}/usr/{bin,share} ${pkgdir}/usr/share/man/man1 ${pkgdir}/usr/share/scala/{bin,lib}
    cp -r {lib,misc} ${pkgdir}/usr/share/scala/
    cp -r man ${pkgdir}/usr/share/
    install -m 755 bin/{fsc,scala,scalac,scalap,scaladoc} ${pkgdir}/usr/share/scala/bin
    install -D -m0644 doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    ln -s ../share/scala/bin/fsc ${pkgdir}/usr/bin/fsc
    ln -s ../share/scala/bin/scala ${pkgdir}/usr/bin/scala
    ln -s ../share/scala/bin/scalac ${pkgdir}/usr/bin/scalac
    ln -s ../share/scala/bin/scalap ${pkgdir}/usr/bin/scalap
    ln -s ../share/scala/bin/scaladoc ${pkgdir}/usr/bin/scaladoc
}

package_scala-devel-docs()
{
    replaces=('scala-doc')
    pkgdesc="Scala documentation"
    depends=('scala')

    cd ${srcdir}
    mkdir -p $pkgdir/usr/share/doc/
    cp -r scala-docs-$pkgver $pkgdir/usr/share/doc/scala
}

package_scala-sources()
{
    replaces=('scala-src')
    pkgdesc="Scala sources"
    depends=('scala')

    cd ${srcdir}/scala-${pkgver}-sources/dists
#    latest=$(readlink latest)
#    latestdir=${latest##*/}
#    cd latestdir
    cd latest
    mkdir -p $pkgdir/usr/share/scala/
    cp -r src $pkgdir/usr/share/scala/src
}
