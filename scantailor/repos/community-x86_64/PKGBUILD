# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Denis Terskov aka neurosurgeon <terskov.den@gmail.com>

pkgname=scantailor
pkgver=0.9.11.1
pkgrel=4
pkgdesc="Interactive post-processing tool for scanned pages"
arch=(i686 x86_64)
url="http://scantailor.sourceforge.net"
license="GPL"
depends=('qt4')
makedepends=('cmake' 'boost')
options=('!makeflags')
install=scantailor.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	scantailor.desktop)
md5sums=('6cdca1b6d1dafd022ea94b4800dad340'
         '7fdbdef6167d124762421ec99a98f8de')

build() {
  cd $srcdir/$pkgname-$pkgver
  cmake \
	-DCMAKE_CXX_FLAGS="-DBOOST_NO_MEMBER_TEMPLATE_FRIENDS" \
	-DCMAKE_CXX_FLAGS_DEBUG="-DBOOST_NO_MEMBER_TEMPLATE_FRIENDS" \
	-DCMAKE_CXX_FLAGS_RELEASE="-DBOOST_NO_MEMBER_TEMPLATE_FRIENDS" \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm0644 "resources/appicon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/scantailor.svg"
  install -Dm0644 "$srcdir/scantailor.desktop" "$pkgdir/usr/share/applications/scantailor.desktop"
}
