# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Denis Terskov aka neurosurgeon <terskov.den@gmail.com>

pkgname=scantailor
pkgver=0.9.12.2
pkgrel=4
#_realver=EXPERIMENTAL_2016_02_22
# use experimental branch
_commit=ce74e53f89883b35bb12ec7ed4fd654c016e71e4
pkgdesc="Interactive post-processing tool for scanned pages"
arch=(x86_64)
url="http://scantailor.org/"
license=("GPL")
depends=('qt5-base' 'libtiff' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'eigen' 'boost' 'git')
#source=($pkgname-$_realver.tar.gz::https://github.com/Tulon/$pkgname/archive/$_realver.tar.gz
source=(git://github.com/Tulon/scantailor.git#commit=${_commit}
        scantailor.desktop
        build-fix.patch)
sha256sums=('SKIP'
            '3c191c0380f991709bd38a0ace6adbf351fca441a2c5fd432e56a0da6b68a728'
            'e21ec75ed1302fadffd53989f4f262f05ef2236261861fd88db9b3c8867b2671')

prepare() {
#  cd "$srcdir"/$pkgname-${_realver}
  cd "$srcdir"/$pkgname
  patch -p1 -i "$srcdir"/build-fix.patch
}

build() {
  cd "$srcdir"/$pkgname
  cmake \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	.
  make
}

package() {
  cd "$srcdir"/$pkgname
  make DESTDIR="$pkgdir" install
  install -Dm0644 "resources/appicon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/scantailor.svg"
  install -Dm0644 "$srcdir/scantailor.desktop" "$pkgdir/usr/share/applications/scantailor.desktop"
}
