# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Ebubekir KARUL <ebubekirkarul@yandex.com>
# Contributor: s1gma,shirokuro

pkgname=scapy
pkgver=2.2.0
pkgrel=9
pkgdesc="A powerful interactive packet manipulation program written in Python"
url="http://www.secdev.org/projects/scapy/"
depends=('tcpdump' 'python2' )
optdepends=('python2-crypto: WEP decryption support'
            'python2-pyx: psdump() and pdfdump() functions'
            'python2-gnuplot: plotting support'
            'graphviz: conversations() method support')
arch=('any')
license=('GPL')
source=(http://www.secdev.org/projects/scapy/files/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c5363b224df0efbd78d7dc4d8a518e5518b2e7affc2e5f1fcecd4efa3ab815af')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/env python$/env python2/g' -i doc/scapy/*.rst \
    run_scapy scapy/tools/check_asdis.py scapy/layers/inet6.py setup.py
  sed -e 's/exec python$/exec python2/g' -i run_scapy setup.py 
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install -O1 --prefix="${pkgdir}/usr"
  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/scapy/*.rst "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim: ts=2 sw=2 et:
