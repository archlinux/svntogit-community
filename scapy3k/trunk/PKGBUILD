# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=scapy3k
_pkgname=scapy
pkgver=0.23
pkgrel=2
pkgdesc='Powerful interactive packet manipulation program (based on scapy) written in Python'
url='https://github.com/phaethon/scapy'
depends=('tcpdump' 'python' )
optdepends=('python-cryptography: WEP decryption, IPsec and certificate support'
            'python-pyx: psdump() and pdfdump() functions'
            'python-matplotlib: plotting support'
            'python-netifaces: network interface information support'
            'graphviz: conversations() method support'
            'sox: for VOIP support')
checkdepends=('python-cryptography' 'python-pyx' 'python-matplotlib' 'python-netifaces' 'graphviz' 'sox')
conflicts=('scapy')
arch=('any')
license=('GPL2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/phaethon/scapy/archive/v${pkgver}.tar.gz)
sha256sums=('b4cd88724f2c4ce3af11f98af89dc118e180bb7b462e0d4bedd493b3f6813a2d')
sha512sums=('2c341bc0459024311b1724cd6ee2d05651197b413c9b79f9bd2b4d60feb8baead1991c7c7dd1c229f3f06ff2901c5c50de15bac1d4fd367e6969fd9053649418')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|((bin\|doc)/(UT)?scapy)|\13k|g' -i setup.py
  mv doc/scapy.1.gz doc/scapy3k.1.gz
  mv bin/scapy{,3k}
  mv bin/UTscapy{,3k}
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}/test
  # Run only main tests, without network access
  #./run_tests -K netaccess -F
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
