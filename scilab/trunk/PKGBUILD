# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>
# Paulo Matias

pkgname=scilab
pkgver=5.2.2
pkgrel=1
pkgdesc='Scilab is a scientific software package for numerical computations.'
arch=('i686' 'x86_64')
url='http://www.scilab.org/'
provides=('scilab=$pkgver')
conflicts=('scilab4' 'scilab5')
license=('custom:CeCILL')
depends=('libxml2' 'pcre' 'java-saxon6<7' 'lapack' 'java-batik' 'java-flexdock' 'jogl' 'fop' 'jeuclid-core' 'fftw' 'libmatio'
         'bash' 'tk' 'jrosetta' 'ocaml' 'javahelp2' 'hdf5' 'jgraphx' 'jlatexmath' 'antlr')
makedepends=('apache-ant')
source=("http://www.scilab.org/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        'java.patch'
        'scilab.desktop')
options=('!libtool')

build() {
  cd ${pkgname}-${pkgver}

  # fix undefined references (scilab needs to fix its cyclic dependencies)
  LDFLAGS=${LDFLAGS/ -Wl,--as-needed}

#  patch -p1 < ../java.patch

  ./configure \
    --prefix=/usr \
    --disable-build-help \
    --with-install-help-file \
    --enable-build-localization \
    --without-pvm \
    --with-matio \
    --without-umfpack \
    --with-fftw \
    --without-hdf5 \
    --with-gfortran  # Needed for 64 bits, don't hurt for 32 bits

  make all doc 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install install-html

  install -d ${pkgdir}/usr/share/scilab/thirdparty
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install --mode=644 ${srcdir}/scilab-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}
  #fix icon
  install -Dm644 ${pkgdir}/usr/share/scilab/icons/scilab.xpm ${pkgdir}/usr/share/pixmaps/scilab.xpm
  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
  sed -i 's#/jni##' ${pkgdir}/usr/share/scilab/etc/librarypath.xml
}
md5sums=('35e8a22edb348f405360d3a07ed4faab'
         '7cf31756e3c995e56ec657a0f9930be7'
         '460800803b34bfeb826b9b37d8c8fdd6')
