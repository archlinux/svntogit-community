# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=scponly
pkgver=4.8
pkgrel=7
pkgdesc="A limited shell for ssh/scp"
arch=(i686 x86_64)
url="http://sublimation.org/scponly/"
depends=('glibc')
makedepends=('openssh')
license=('custom')
options=(docs)
source=(http://downloads.sourceforge.net/sourceforge/scponly/scponly-$pkgver.tgz
	setup_chroot.sh)
md5sums=('139ac9abd7f3b8dbc5c5520745318f8a'
         'a5e26c355cf4f127a1f2fdf2fb1582cc')

build() {
  cd $srcdir/$pkgname-$pkgver

  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --sysconfdir=/etc \
	--enable-scp-compat --enable-winscp-compat --enable-chrooted-binary
  sed -i 's|^#elif|#else|' helper.c
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m0644 COPYING $pkgdir/usr/share/licenses/scponly/COPYING
  mv $pkgdir/usr/man $pkgdir/usr/share/
  mkdir -p $pkgdir/usr/share/doc/scponly
  cp $srcdir/setup_chroot.sh config.h $pkgdir/usr/share/doc/scponly/
}
