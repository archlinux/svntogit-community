# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=1.0.5
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=(any)
license=('BSD')
url="http://scrapy.org"
depends=('python2-twisted' 'libxml2' 'python2-w3lib' 'python2-lxml' 'python2-six' 'python2-queuelib' 
         'python2-setuptools' 'python2-cssselect' 'python2-pyopenssl' 'python2-service-identity')
makedepends=('git')
checkdepends=('python2-pytest' 'python2-mock' 'python2-boto' 'python2-pillow' 'python2-testfixtures' 'python2-jmespath' 'mitmproxy')
optdepends=('ipython2: for enhanced support of the interactive scraping shell')
source=("git+https://github.com/scrapy/scrapy.git#tag=${pkgver}")
md5sums=('SKIP')

check() {
  cd "${srcdir}/scrapy"
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1

  # https://github.com/scrapy/scrapy/pull/1757
  PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD" py.test2 scrapy tests || warning "Tests failed"
}

package() {
  cd "${srcdir}/scrapy"
  python2 setup.py install -O1 --root="${pkgdir}"
  
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "README.rst" "${pkgdir}/usr/share/doc/${pkgname}/README.rst"
  install -D -m644 "docs/intro/install.rst" "${pkgdir}/usr/share/doc/${pkgname}/INSTALL.rst"
}
