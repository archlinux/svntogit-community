# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=0.8.17
pkgrel=2
pkgdesc='Simple command-line screenshot utility for X'
url='http://scrot.sourcearchive.com/'
arch=('x86_64')
license=('MIT')
depends=('giblib')
source=("http://scrot.sourcearchive.com/downloads/${pkgver%.*}-${pkgver##*.}/${pkgname}_${pkgver%.*}.orig.tar.gz"
        "http://scrot.sourcearchive.com/downloads/${pkgver%.*}-${pkgver##*.}/${pkgname}_${pkgver%.*}-${pkgver##*.}.debian.tar.xz")
sha256sums=('613d1cf524c2b62ce3c65f1232ea4f05c7daf248d5e82ff2a6892c98093994f2'
            'a7dfe8d07351809b6597d623421fcb80e4ecab320f5dbea34005c8efc6ad1431')
sha512sums=('cba8f589e45758ddbfe4e276399a1ecb0dbe29569be5d85d97733e7f64de2911bd2d03e62700ad0c718a1fc886c2e3def9dee1de5cac884f9e65e772cebe838c'
            'a5185a2f76b24af3ad5e8b0eac4bc5fcbcbf35e340c808f920c1557b81bfd10bdd953945cc9dd651c132acdb155049e329a4ab2d1b0b05ff2b0aeaa9f8f684dc')

prepare(){
    for patch in $(<"${srcdir}/debian/patches/series"); do
        msg2 "Applying ${patch}..."
        patch -Np1 -d "${srcdir}/${pkgname}-${pkgver%.*}" -i "${srcdir}/debian/patches/${patch}"
    done
}

build() {
  cd ${pkgname}-${pkgver%.*}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver%.*}
  make DESTDIR=${pkgdir} docsdir=/usr/share/doc/scrot install
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
