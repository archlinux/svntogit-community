# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=scrotwm
pkgver=0.9.33
pkgrel=1
pkgdesc="A minimalistic dynamic tiling window manager that tries to stay out of the way."
arch=('i686' 'x86_64')
url="http://www.scrotwm.org"
license=('custom:ISC')
depends=('dmenu' 'libxrandr' 'profont')
makedepends=('libxt')
optdepends=('scrot: screenshots' 'xlockmore: screenlocking' 'terminus-font: great font')
backup=(etc/scrotwm.conf)
source=(http://opensource.conformal.com/snapshots/$pkgname/$pkgname-$pkgver.tgz \
	LICENSE \
        baraction.sh)
md5sums=('fcfd0aba8e48217e26db823226640547'
         'a67cfe51079481e5b0eab1ad371379e3'
         '6132ba773ee00f39d8f67ac0347a9814')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  # it is like a patch, only less fragile
  sed -i 's|\"/usr/local/lib/libswmhack.so\"|\"libswmhack.so\"|' scrotwm.c
  sed -i 's/# modkey = Mod1/modkey = Mod4/' scrotwm.conf
  sed -i 's/-\*-terminus-medium-\*-\*-\*-\*-\*-\*-\*-\*-\*-\*-\*/-*-profont-*-*-*-*-12-*-*-*-*-*-*-*/' scrotwm.conf

  cd linux
  make PREFIX="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/linux"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 scrotwm.desktop "$pkgdir/usr/share/xsessions/scrotwm.desktop"
  cd ..
  install -Dm644 scrotwm.conf "$pkgdir/etc/scrotwm.conf"
  install -Dm755 screenshot.sh "$pkgdir/usr/share/scrotwm/screenshot.sh"
  #cd ../..
  cd "$srcdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm755 baraction.sh "$pkgdir/usr/share/scrotwm/baraction.sh"

  ln -s /usr/lib/libswmhack.so.0.0 "$pkgdir/usr/lib/libswmhack.so.0"
  ln -s /usr/lib/libswmhack.so.0.0 "$pkgdir/usr/lib/libswmhack.so"

  # fix this for real in the makefile
  mkdir -p "$pkgdir"/usr/share/man/{es,it,pt,ru}/man1/
  mv "$pkgdir/usr/share/man/man1/scrotwm_es.1" "$pkgdir/usr/share/man/es/man1/"
  mv "$pkgdir/usr/share/man/man1/scrotwm_it.1" "$pkgdir/usr/share/man/it/man1/"
  mv "$pkgdir/usr/share/man/man1/scrotwm_pt.1" "$pkgdir/usr/share/man/pt/man1/"
  mv "$pkgdir/usr/share/man/man1/scrotwm_ru.1" "$pkgdir/usr/share/man/ru/man1/"
}
