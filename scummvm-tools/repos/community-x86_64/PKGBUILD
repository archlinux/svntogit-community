# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Jérémie Dimino <jeremie@dimino.org>
# Contributor: Sebastian Hase <sebastian_hase@gmx.de>
# Contributor: Joe Davison <joe@thosedamnpeanuts.com>
pkgname=scummvm-tools
pkgver=2.6.0
pkgrel=2
pkgdesc="A set of tools that enable you to convert, compress and decode game datafiles for ScummVM"
url="http://www.scummvm.org/"
license=('GPL')
depends=('libpng' 'wxwidgets-gtk3' 'flac' 'libvorbis')
arch=('x86_64')
source=("https://www.scummvm.org/frs/scummvm-tools/${pkgver}/scummvm-tools-${pkgver}.tar.xz")
sha512sums=('2d79915c82ca60f7e0f1c3e55867dd3732eec1afd9ac0c92a6636313295c31d52140db821def850682140ac1c0f756ebca0bd7c20feffb6c4df46c359793631c')

build() {
  cd ${pkgname}-${pkgver}

  export CXXFLAGS+=" -ffat-lto-objects"
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}"/usr/bin

  make DESTDIR="${pkgdir}" install

  install -Dm 644 README "${pkgdir}"/usr/share/doc/scummvm-tools/README
  install -Dm 644 convert_dxa.sh "${pkgdir}"/usr/share/doc/scummvm-tools/samples/convert_dxa.sh
}
#vim: sw=2 ts=2 et:
