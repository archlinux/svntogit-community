# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=2.5.0
pkgrel=4
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl2' 'sdl2_net' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2' 'curl' 'a52dec' 'giflib' 'glew' 'libspeechd' 'gtk3' 'sndio')
makedepends=('mesa')
source=("https://downloads.scummvm.org/frs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('7ea702cf0f993994209fd692e82ab65f9686fb60d65f36624a31bd642067fe7dd35e556666281c68d1592d7dd07333f085c124038f7a45acefae8f30bceff534')
b2sums=('a5153280999e321be5bf7b56411c163d5ba1d4fb142fc900e1c3cb709d357d199d3764a94af57ab7218ee201bdbe6288adf222add9ffc941324b84d0e6954fe8')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --enable-c++11 \
    --enable-release \
    --enable-all-engines \
    --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -vDm644 dists/${pkgname}.desktop -t "${pkgdir}"/usr/share/applications/
}
