# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Maintainer: JJDaNiMoTh <jjdanimoth@gmail.com>

pkgname=sdlmame
pkgver=0.173
pkgrel=1
pkgdesc="A port of the popular Multiple Arcade Machine Emulator using SDL with OpenGL support."
url="http://mamedev.org/"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('sdl2' 'libxinerama' 'sdl2_ttf' 'alsa-lib' 'qt5-base')
makedepends=('unzip' 'nasm' 'mesa' 'glu' 'wget' 'python2')
install=sdlmame.install
source=("https://github.com/mamedev/mame/releases/download/mame${pkgver/./}/mame${pkgver/./}s.zip"
	"sdlmame.sh"
	"extras.tar.gz"
	${_patches})
md5sums=('69bd811dd252f8d79e31ea05e6aa15ab'
         '0cced0fe9868cc56129600fb4bd5b9ed'
         '420b61240bf5ae11615ba7c6100ee00d')

prepare() {
  cd $srcdir/
  unzip mame.zip
}

build() {
  cd $srcdir/
  make NOWERROR=1 OPTIMIZE=2 PYTHON_EXECUTABLE=/usr/bin/python2 TOOLS=1
}

package() {
  cd $srcdir/
  # Install the sdlmame script
  install -Dm755 $srcdir/${pkgname}.sh $pkgdir/usr/bin/${pkgname}

  # Install the applications and the UI font in /usr/share
  install -Dm755 mame $pkgdir/usr/lib/${pkgname}/${pkgname} || \
  install -Dm755 mame64 $pkgdir/usr/lib/${pkgname}/${pkgname}

  install -m755 chdman $pkgdir/usr/lib/${pkgname}/chdman
  install -m755 jedutil $pkgdir/usr/lib/${pkgname}/jedutil
  install -m755 regrep $pkgdir/usr/lib/${pkgname}/regrep
  install -m755 romcmp $pkgdir/usr/lib/${pkgname}/romcmp
#  install -m755 testkeys $pkgdir/usr/lib/${pkgname}/testkeys
  install -m755 src2html $pkgdir/usr/lib/${pkgname}/src2html
  install -m755 srcclean $pkgdir/usr/lib/${pkgname}/srcclean
  install -m755 ldverify $pkgdir/usr/lib/${pkgname}/ldverify
  install -m755 ldresample $pkgdir/usr/lib/${pkgname}/ldresample

  # ln binaries
  install -dm755 $pkgdir/usr/share/${pkgname}
  for i in $pkgname chdman jedutil regrep romcmp testkeys src2html srcclean ldverify ldresample; do
    ln -s /usr/lib/$pkgname/$i $pkgdir/usr/share/$pkgname/$i
  done

  # Install the extra bits
  install -d $pkgdir/usr/share/${pkgname}/{artwork,ctrlr,hash,keymaps,shader}
  install -d $pkgdir/usr/share/man/man1
  install -d $pkgdir/usr/share/man/man6
  install -m644 src/osd/modules/opengl/shader/glsl*.*h $pkgdir/usr/share/${pkgname}/shader/
#  install -m644 src/osd/sdl/man/*.1* $pkgdir/usr/share/man/man1/
#  install -m644 src/osd/sdl/man/*.6* $pkgdir/usr/share/man/man6/

  cp -a $srcdir/artwork/* $pkgdir/usr/share/${pkgname}/artwork/
  install -m644 $srcdir/ctrlr/* $pkgdir/usr/share/${pkgname}/ctrlr/
  install -m644 $srcdir/keymaps/* $pkgdir/usr/share/${pkgname}/keymaps/
  install -m644 $srcdir/hash/* $pkgdir/usr/share/${pkgname}/hash/

  # Include the license
  install -Dm644 docs/LICENSE $pkgdir/usr/share/licenses/${pkgname}/LICENSE

  # FS#28203
  sed -i 's|KEYCODE_2_PAD|KEYCODE_2PAD|' $pkgdir/usr/share/sdlmame/ctrlr/*.cfg
  sed -i 's|KEYCODE_4_PAD|KEYCODE_4PAD|' $pkgdir/usr/share/sdlmame/ctrlr/*.cfg
  sed -i 's|KEYCODE_6_PAD|KEYCODE_6PAD|' $pkgdir/usr/share/sdlmame/ctrlr/*.cfg
  sed -i 's|KEYCODE_8_PAD|KEYCODE_8PAD|' $pkgdir/usr/share/sdlmame/ctrlr/*.cfg

  find $pkgdir -type f -exec strip {} \;

  # documentation
  install -dm0755 $pkgdir/usr/share/doc
  cp -a $srcdir/docs $pkgdir/usr/share/doc/$pkgname
}
