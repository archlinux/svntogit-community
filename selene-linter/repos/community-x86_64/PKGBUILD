# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=selene
pkgname=selene-linter
pkgver=0.22.0
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
options=('!lto')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
b2sums=('b30f6717f943bf4468f0aa546fba9c82b54deff557d0ee6c8ce1d6735d053ac25b501826249e9a7c146c4f8cd699ca6b5d9448d09ddc93d37fee783849fada37')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $_name-$pkgver
  cargo test --locked --offline
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
}

# vim:set ts=2 sw=2 et:
