# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

_name=selene
pkgname=selene-linter
pkgver=0.23.1
pkgrel=1
pkgdesc='Blazing-fast modern Lua linter written in Rust'
arch=('x86_64')
url=https://github.com/Kampfkarren/selene
license=('MPL2')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
options=('!lto')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz")
b2sums=('3403a8e800f9bb7f34d18aa0b9384725c5538bfc77d772b82255cbd3e7918d40a191c1d80b1faeaba614a3b92e4d4fd941958f72c4e70b57e3fae941f2492bb7')

prepare() {
  cd $_name-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $_name-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $_name-$pkgver
  cargo test --locked --offline
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
}

# vim:set ts=2 sw=2 et:
