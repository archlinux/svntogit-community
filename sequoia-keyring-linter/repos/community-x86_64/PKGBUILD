# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sequoia-keyring-linter
pkgver=0.5.0
pkgrel=1
pkgdesc='OpenPGP certificate linter'
url='https://sequoia-pgp.org/'
arch=('x86_64')
license=('GPL2')
group=('sequoia')
depends=('glibc' 'nettle' 'libhogweed.so' 'libnettle.so' 'gcc-libs' 'gmp')
makedepends=('git' 'cargo' 'llvm' 'clang')
source=(https://gitlab.com/sequoia-pgp/keyring-linter/-/archive/v${pkgver}/keyring-linter-v${pkgver}.tar.gz)
sha512sums=('21608ae47d1c0fe8542892615ac9a2e285a91f20565e3419b9d8cb6681190c8fd55c231f89b868623b4033abe40eb820fcb7175a5df308552418564d2c170084')
b2sums=('959cd694b8c191c6cb2278fd974ec6d3510c9b62f024261673a488e2a263c496b47213f19410704c988549460be427e2a206cad6a01173d045e347469ebd5bf7')

prepare() {
  cd keyring-linter-v${pkgver}
  sed 's/debug/release/g' -i -- Makefile
}

build() {
  cd keyring-linter-v${pkgver}
  cargo build --release
  make
}

check() {
  cd keyring-linter-v${pkgver}
  cargo test --release
}

package() {
  cd keyring-linter-v${pkgver}
  install -Dm 755 target/release/sq-keyring-linter -t "${pkgdir}/usr/bin"
  install -Dm 644 sq-keyring-linter.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
