# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Kai Michaelis <kai@sequoia-pgp.org>

pkgbase=sequoia
pkgname=(sequoia python-sequoia)
pkgver=0.20.0
pkgrel=4
pkgdesc='Cool new OpenPGP implementation'
url='https://sequoia-pgp.org/'
arch=('x86_64')
license=('GPL3')
makedepends=('git' 'cargo' 'clang' 'python' 'python-setuptools' 'python-cffi'
             'glibc' 'nettle' 'sqlite' 'capnproto' 'openssl' 'gcc-libs' 'gmp'
             'bzip2' 'libbz2.so')
checkdepends=('python-pytest' 'python-pytest-runner')
options=('!makeflags')
source=(${pkgname}::"git+https://gitlab.com/sequoia-pgp/sequoia.git#tag=v${pkgver}"
        sequoia-0.20-fix-install.patch::https://gitlab.com/sequoia-pgp/sequoia/-/commit/1f3ec0784818bcbe0e73ea4743a19060163fd9b3.patch)
sha512sums=('SKIP'
            '6510f0da18dc4895aa7a8d99e88c6a4acf09898928b5ce9fb6cd60fa419a3ae1d04312d90b6752bf637998b4530c87fd654fccd163093de36bca311f523671e4')

prepare() {
  cd ${pkgname}
  sed 's/debug/release/g' -i -- */Makefile */*/Makefile */*/*/Makefile
  sed "s/-L/${LDFLAGS},-L/g" -i ffi/lang/python/Makefile
  patch -Np1 < ../sequoia-0.20-fix-install.patch
}

build() {
  cd ${pkgname}
  make PREFIX=/usr CARGO_FLAGS='--locked' build-release
}

check() {
  cd ${pkgname}
  cargo test --release --locked \
    --workspace \
    --exclude sequoia-store
  # python tests currently fail horribly
  # make -C ffi/lang/python test
}

package_sequoia() {
  depends=('glibc' 'nettle' 'libhogweed.so' 'libnettle.so' 'sqlite' 'capnproto'
           'openssl' 'gcc-libs' 'gmp' 'bzip2' 'libbz2.so')
  cd ${pkgbase}
  make DESTDIR="${pkgdir}" PREFIX=/usr CARGO_FLAGS='--locked' install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -rf "${pkgdir}/usr/lib/python"*
}

package_python-sequoia() {
  depends=('python' 'python-cffi' 'sequoia')
  pkgdesc+=' - python bindings'
  cd ${pkgbase}
  make -C ffi/lang/python DESTDIR="${pkgdir}" PREFIX=/usr install
}

# vim: ts=2 sw=2 et:
