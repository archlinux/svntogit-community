# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=ser2net
pkgver=2.10.0
pkgrel=2
pkgdesc="A proxy that allows telnet/tcp connections to be made to serial ports"
arch=(x86_64)
url="http://sourceforge.net/projects/ser2net"
license=('GPL')
depends=('glibc')
backup=(etc/ser2net.conf)
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
	ser2net.service)
sha256sums=('98f6193225338e25f35302fef5e1f16688693ed43e7b3c3e9e09187eb54547ac'
            'b093e1f5bf7dd826816828623a5c6858f83a9f0dcb1a9785f545b29efd1c320c')

build() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  make DESTDIR="$pkgdir" install
  install -Dm0644 ser2net.conf "$pkgdir"/etc/ser2net.conf
  install -Dm0644 "$srcdir"/ser2net.service "$pkgdir"/usr/lib/systemd/system/ser2net.service
}
