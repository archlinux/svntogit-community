# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=serd
pkgver=0.30.8
pkgrel=1
pkgdesc="Lightweight C library for RDF syntax supporting reading/ writing Turtle and NTriples."
arch=('x86_64')
url="https://drobilla.net/software/serd/"
license=('custom:ISC')
depends=('glibc')
makedepends=('waf')
provides=('libserd-0.so')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('d12c4dea1a15b2aed9fb9d640affae23a4fbfb53b7f873af30b462ce96f3c22ee464f5b111f87a221e6ac51f4885c23bba023892331ab10d0a8aa0e27fcbd0fa'
            'SKIP')
b2sums=('31a485fccfb13e54e124cde0626ad16f3cc7f9c1f8d8c051ace7a98bd2bd5d6a2d159943f15f5734e028a31ce5eeea9d602f55bf6cf1277d532ae799e0c1895f'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove call to local ldconfig
  sed -i "/ldconfig/d" wscript
  # let wscript(s) find the custom waf scripts
  mkdir -pv tools
  touch __init__.py
  cp -v waflib/extras/{autoship,autowaf,lv2}.py tools/
  mkdir -pv plugins/tools/
  cp -v waflib/extras/{autoship,autowaf,lv2}.py plugins/tools/
  rm -rv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -e "s/load('lv2'/load('lv2', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --mandir=/usr/share/man \
                --test
  waf -v build
}

check() {
  cd "${pkgname}-${pkgver}"
  waf test
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "$pkgdir/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,NEWS,README.md}
}
# vim:set ts=2 sw=2 et:
