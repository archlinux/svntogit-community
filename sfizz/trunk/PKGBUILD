# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: RedTide <redtid3@gmail.com>

pkgname=sfizz
pkgver=1.0.0
pkgrel=1
pkgdesc="SFZ library and LV2 plugin"
arch=('x86_64')
url="https://github.com/sfztools/sfizz"
license=('BSD')
groups=('lv2-plugins' 'pro-audio' 'vst3-plugins')
# TODO: devendor abseil-cpp
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'xcb-util' 'xcb-util-cursor'
'libxkbcommon' 'libxkbcommon-x11' 'ttf-roboto' 'zenity')
makedepends=('cmake' 'jack' 'freetype2' 'lv2' 'xcb-util-keysyms')
checkdepends=('lv2lint')
optdepends=(
  'jack: for sfizz_jack'
  'lv2-host: for the LV2 plugin'
  'vst3-host: for the VST3 plugin'
)
provides=('libsfizz.so' 'soundfont-synthesizer')
source=("https://github.com/sfztools/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('541f07c18e96ebf7dfa9977b4e6aa69b37e7146f3b2e88276363f7cc5c6f34d3d71a166854fcdc688f89368beeb8eb3a78edf5c8fedb3c89bd2f79f32b31d941')
b2sums=('2d9b1cabc58d312ee0ac6837bb917369d8ec1785d1dd59a9d0e00d51b4371f98cfddb58fc713f9cafcec072129b8d5cf13870efbf00715fff44aa38a59ec3ddf')

prepare() {
  cd "$pkgname-$pkgver"
  # devendor lv2
  rm -rfv lv2/lv2
  sed -e 's|"lv2/core/lv2.h"|<lv2/core/lv2.h>|g' \
      -i plugins/lv2/external/ardour/ardour/lv2_extensions.h
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE='None' \
        -DSFIZZ_TESTS=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  ./build/tests/${pkgname}_tests
  lv2lint -Mpack -I "build/${pkgname}.lv2" "http://sfztools.github.io/sfizz"
}

package() {
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # devendor ttf-roboto
  ln -svf /usr/share/fonts/TTF/Roboto-Regular.ttf "${pkgdir}/usr/lib/vst3/${pkgname}.vst3/Contents/Resources/Fonts/"
}
