# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: jiangxq <jiangxueqian at gmail dot com>
# Contributor: zh99998 <zh99998@gmail.com>
# Contributor: 4679kun <admin at 4679 dot us>

pkgname=shadowsocks-libev
pkgver=3.3.5
pkgrel=1
pkgdesc='A lightweight secured socks5 proxy for embedded devices and low end boxes'
arch=('x86_64')
url='https://github.com/shadowsocks/shadowsocks-libev'
license=('GPL3')
depends=('libcap' 'mbedtls' 'libsodium' 'libev' 'c-ares' 'pcre' 'libcorkipset' 'libbloom')
makedepends=('asciidoc' 'xmlto')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/$pkgname/archive/v$pkgver.tar.gz"
        'shadowsocks-libev@.service'
        'shadowsocks-libev-server@.service'
        'shadowsocks-libev-redir@.service'
        'shadowsocks-libev-tunnel@.service')

sha512sums=('c0cc869b4b7661ebd348bd5559c6a17c44b66a5f42374d97023adcfb525d934da3ed96245ff792ebc74870d30dd8f71fadcd89c79b2cd8a98d551ff7592508d0'
            'eac17ca83d9f754a82a8fbb1c98bfaf0aa0ea3017cb834bca354b77948decd23c58211aef9824498a28d2dd9d227fb75ae14fe63e59090e83a37fd0ca4d1dc73'
            '462c79ef946bd1e7d575e816d7aff02449c38f776e9c407f22ed6e68bc42087cff7c53cc963ad120601ef8901f10c6981808dad30c8026ab06829a35a2b854c0'
            '90cd2e75759399aebe0a6c96a7d40fb8328b80d10e210ce1ef0f730ba4e48b1d19f509ed21ef28af1dd10217fa4f2389b6cdabd0fb7e9273d5102cb06e65a369'
            '0a657839825902ba305bdceada25534f32350cc8fd914dd49e0e2bc81e81d5ef1a01f5f09a96f24a21b1e1008ac2960c1713431e4f5127d2f4e8eaca51d44527')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  sed -i 's|AC_CONFIG_FILES(\[libbloom/Makefile libcork/Makefile libipset/Makefile\])||' configure.ac
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --enable-system-shared-lib
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/shadowsocks-libev@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-server@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-server@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-redir@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-redir@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-tunnel@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-tunnel@.service"
}
