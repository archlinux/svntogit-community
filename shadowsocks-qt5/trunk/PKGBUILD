# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: yk <yk at archlinuxcn dot org>

pkgname=shadowsocks-qt5
pkgver=2.9.0
_commit=ba961cd962e5adf00a2ca34b93e3210440a366f4
pkgrel=2
pkgdesc="A fast, reliable and cross-platform GUI fronted for Shadowsocks."
arch=("i686" "x86_64")
url="https://github.com/shadowsocks/shadowsocks-qt5"
license=("GPL")
depends=('qrencode' 'libqtshadowsocks' 'zbar')
makedepends=('cmake')
source=("$pkgname-$_commit.tar.gz::https://github.com/shadowsocks/shadowsocks-qt5/archive/$_commit.tar.gz")
sha512sums=('8eca22b9cf2c764d4b825cb4d403346704ba12f00f08bb67ad4097ca8452b3986e78fc4e8f36edf1de07a3e804155d054daaa7538e2794933e7c52c5e66c9f06')

prepare() {
  mv $pkgname-$_commit $pkgname-$pkgver

  cd shadowsocks-qt5-$pkgver

  # Disable appindicator (was needed for Qt < 5.4.2)
  sed -i '/APPINDICATOR/d' CMakeLists.txt
}

build() {
  cd shadowsocks-qt5-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd shadowsocks-qt5-$pkgver
  make DESTDIR="$pkgdir" install
}
