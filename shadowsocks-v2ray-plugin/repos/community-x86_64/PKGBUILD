# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=shadowsocks-v2ray-plugin
pkgver=5.0.2
pkgrel=1
pkgdesc='A SIP003 plugin based on v2ray for shadowsocks'
arch=('x86_64')
url='https://github.com/teddysun/v2ray-plugin'
license=('GPL')
options=('!lto')
depends=('glibc')
makedepends=('go' 'git')
source=("https://github.com/teddysun/v2ray-plugin/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2b6fcedc83bb40d50bdeac191e17ad4d7c9487af213bfd305cc4e0b770ee10fb718089f6d5ee60632315c18b89a4694a7a36b9648c1780084940ed12d7f529d1')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd v2ray-plugin-$pkgver
  go build
}

package() {
  cd v2ray-plugin-$pkgver
  install -Dm755 v2ray-plugin "$pkgdir"/usr/bin/v2ray-plugin
}
