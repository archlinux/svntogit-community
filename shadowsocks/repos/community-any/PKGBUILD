# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=shadowsocks
pkgver=2.2.2
pkgrel=1
pkgdesc="A lightweight tunnel proxy"
license=("MIT")
url="http://pypi.python.org/pypi/shadowsocks"
depends=('python2' 'python2-setuptools')
checkdepends=('python2-m2crypto' 'python2-salsa20')
optdepends=('python2-m2crypto: For any encryption method other than "table"'
            'python2-numpy: For salsa20 support'
            'python2-salsa20: For salsa20 support')
source=("http://pypi.python.org/packages/source/s/shadowsocks/shadowsocks-${pkgver}.tar.gz"
        "$pkgname-LICENSE::https://raw.github.com/clowwindy/shadowsocks/master/LICENSE"
        "shadowsocks@.service"
        "shadowsocks-server@.service")
arch=('any')
install=${pkgname}.install

prepare() {
  cd shadowsocks-$pkgver/shadowsocks
  # python2 fix
  sed -e 's_#!/usr/bin/env python$_#!/usr/bin/env python2_' \
      -e 's_#!/usr/bin/python$_#!/usr/bin/python2_' \
      -i local.py server.py encrypt.py __init__.py utils.py
}

check() {
  cd shadowsocks-$pkgver

  # Tests not included
  return
  python2 tests/test.py -c tests/table.json
  python2 tests/test.py -c tests/aes.json
  python2 tests/test.py -c tests/rc4-md5.json
  python2 tests/test.py -c tests/salsa20.json
  python2 tests/test.py -c tests/server-multi-ports.json
  python2 tests/test.py -c tests/server-multi-passwd.json
  python2 tests/test.py -c tests/server-multi-passwd-table.json
  python2 tests/test.py -c tests/workers.json
}

package() {
  cd "$srcdir/shadowsocks-$pkgver"
  python2 setup.py install -O1 --root="$pkgdir"
  
  install -d "$pkgdir/etc/shadowsocks"
  install -Dm644 "$srcdir/shadowsocks@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks@.service"
  install -Dm644 "$srcdir/shadowsocks-server@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-server@.service"
  install -Dm644 "$srcdir/$pkgname-LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('9cafac6c300456691002178f0c43cb96d5583b8d1ebe81d87a15c67d5b6adf9f6bfd7f016b8eb5e29cecb24be80829371ed6b1ebd10d6e3ff13dcf5cf8ff6612'
            '88abf42d51d7b7baa2b9cef016bfd194373fd178980c2c6f1bbb3ce31d3267dfca71efb013c6596c57c0390fdb95d716ff4acca97bdf960e56e9edf16f770352'
            '96ecb174a476f64dec3ec086732d93a76d27e9a1cc56b1dc3fa87bf8e00d4b3c44076a01a0d525e42174cacc58fd5c16d5c8af5d4636dae13df549dec8204f7d'
            'f2f8240c18a4483bf3e03b3a5ac8822c3deae713224f732bf28f7fddfcc5d0b02aa91652665ff93f05731e162a9407109c18f43d732a3a9b19d9eacfc806a4df')
