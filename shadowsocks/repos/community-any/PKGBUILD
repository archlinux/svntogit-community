# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=shadowsocks
pkgver=2.0.7
pkgrel=1
pkgdesc="A lightweight tunnel proxy"
license=("MIT")
url="http://pypi.python.org/pypi/shadowsocks"
depends=('python2' 'python2-setuptools')
optdepends=('python2-m2crypto: For any encryption method other than "table"'
            'python2-numpy: For salsa20 support'
            'python2-salsa20: For salsa20 support')
source=("http://pypi.python.org/packages/source/s/shadowsocks/shadowsocks-${pkgver}.tar.gz"
        "https://raw.github.com/clowwindy/shadowsocks/master/LICENSE"
        "shadowsocks@.service"
        "shadowsocks-server@.service")
arch=('any')
install=${pkgname}.install

prepare() {
  cd shadowsocks-$pkgver/shadowsocks
  # python2 fix
  sed -e 's_#!/usr/bin/env python$_#!/usr/bin/env python2_' \
      -e 's_#!/usr/bin/python$_#!/usr/bin/python2_' \
      -i local.py server.py encrypt.py __init__.py utils.py
}

package() {
  cd "$srcdir/shadowsocks-$pkgver"
  python2 setup.py install -O1 --root="$pkgdir"
  
  install -d "$pkgdir/etc/shadowsocks"
  install -Dm644 "$srcdir/shadowsocks@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks@.service"
  install -Dm644 "$srcdir/shadowsocks-server@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-server@.service"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('a654332362baf001233fffd0b50758c0f596bf380f4762ecc9bdb75f66b79c3abaa48c912bf8684ddcf637220e713b21cbe047a328aae4af6eb968f40be1fd45'
            '36c025d78d9d2b355516360c8d4642290dc4102543eb4b1473a6f3201785ef28727ff9cb662757f013a62fa4a73f62a2655508a5ae554e124404fc3077aef8cb'
            '96ecb174a476f64dec3ec086732d93a76d27e9a1cc56b1dc3fa87bf8e00d4b3c44076a01a0d525e42174cacc58fd5c16d5c8af5d4636dae13df549dec8204f7d'
            'f2f8240c18a4483bf3e03b3a5ac8822c3deae713224f732bf28f7fddfcc5d0b02aa91652665ff93f05731e162a9407109c18f43d732a3a9b19d9eacfc806a4df')
