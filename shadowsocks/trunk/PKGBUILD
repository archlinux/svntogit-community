# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=shadowsocks
pkgver=1.3.3
pkgrel=1
pkgdesc="A lightweight tunnel proxy"
license=("MIT")
url="http://pypi.python.org/pypi/shadowsocks"
depends=('python2')
makedepends=('python2-distribute')
optdepends=('python2-gevent: Recommended for much better performance'
            'python2-m2crypto: For any encryption method other than "table"')
source=("http://pypi.python.org/packages/source/s/shadowsocks/shadowsocks-${pkgver}.tar.gz"
        "https://raw.github.com/clowwindy/shadowsocks/master/LICENSE"
	"shadowsocks@.service"
	"shadowsocks-server@.service")
arch=('any')
install=${pkgname}.install

prepare() {
  cd "$srcdir/shadowsocks-$pkgver"
  # python2 fix
  find . -name '*.py' -type f -print0 | xargs -0 sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_'
}

package() {
  cd "$srcdir/shadowsocks-$pkgver"
  python2 setup.py install --root="$pkgdir"

  install -Dm644 "$srcdir/shadowsocks@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks@.service"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha512sums=('6dba08a886e476b0d9e17047de114c3cf9f8bf8eb450a7957dd9c54c94c3e52a5630201e252f8b098c0e2c489304d62cf7d49f2e39a5d295046b150712faa31c'
            '36c025d78d9d2b355516360c8d4642290dc4102543eb4b1473a6f3201785ef28727ff9cb662757f013a62fa4a73f62a2655508a5ae554e124404fc3077aef8cb'
            '96ecb174a476f64dec3ec086732d93a76d27e9a1cc56b1dc3fa87bf8e00d4b3c44076a01a0d525e42174cacc58fd5c16d5c8af5d4636dae13df549dec8204f7d'
            'f2f8240c18a4483bf3e03b3a5ac8822c3deae713224f732bf28f7fddfcc5d0b02aa91652665ff93f05731e162a9407109c18f43d732a3a9b19d9eacfc806a4df')
