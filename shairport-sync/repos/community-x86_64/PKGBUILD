# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=shairport-sync
pkgver=3.1.7
pkgrel=1
pkgdesc='Emulates an AirPort Express for the purpose of streaming music from iTunes and compatible iPods and iPhones'
url='https://github.com/mikebrady/shairport-sync'
arch=(x86_64)
license=(GPL)
backup=(etc/shairport-sync.conf)
depends=(openssl avahi libsoxr popt alsa-lib libconfig libpulse)
makedepends=(xmltoman)
source=(shairport-sync-$pkgver.zip::https://github.com/mikebrady/shairport-sync/archive/$pkgver.zip
        shairport-sync.sysusers
        useradd.patch::https://github.com/mikebrady/shairport-sync/commit/15c376ad57629dad16f1120dbf4c080f7de48475.patch)
sha1sums=('2cfc85732a9dcbe26eef032b0b29524dfaff2a33'
          'b806f9cd3eeaf8585a51d79c7b5681e3d3e4748a'
          '0d216ac98a7c390476c79732e7361c4e66651340')

prepare() {
  cd shairport-sync-$pkgver
  # Revert adding users with 'make install'. We have a separate *.install script for it.
  patch -R < ../useradd.patch
}

build() {
  cd shairport-sync-$pkgver

  autoreconf -i -f
  ./configure --prefix=/usr --sysconfdir=/etc --with-alsa --with-pa --with-avahi --with-ssl=openssl --with-soxr --with-dns_sd --with-pkg-config --with-systemd --with-configfiles
  make

  sed 's|/usr/local/bin/|/usr/bin/|' -i scripts/shairport-sync.service
}

package() {
  cd shairport-sync-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/shairport-sync.sysusers "$pkgdir"/usr/lib/sysusers.d/shairport-sync.conf
  install -D -m664 LICENSES "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm "$pkgdir"/etc/shairport-sync.conf.sample
}
