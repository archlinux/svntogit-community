# $Id$
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Bob Finch <w9ya@arrl.net>

pkgname=shapelib
pkgver=1.3.0
pkgrel=1
pkgdesc='simple C API for reading and writing ESRI Shapefiles'
arch=('i686' 'x86_64')
url='http://shapelib.maptools.org/'
license=('LGPL' 'MIT')
options=('!libtool')
source=("http://download.osgeo.org/$pkgname/$pkgname-$pkgver.tar.gz"
        "LICENSE")
sha256sums=('23d474016158ab5077db2f599527631706ba5c0dc7c4178a6a1d685bb014f68f'
            '67e45b04045e27a9d6f64f92b295831b8739b32668ce497d3fa02999e349a789')

build() {
  cd "$pkgname-$pkgver"
  
  sed -i -e s:'-O2':"${CFLAGS}": Makefile
  make lib
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/lib" "$pkgdir/usr/include"

  # Fix the broken makefile
  sed -i -e s:/usr/local:${pkgdir}/usr: Makefile
  sed -i -e s:/usr/bin/install:/bin/install: Makefile
  make lib_install
  
  # Fix world readable bit not set
  chmod 644 $pkgdir/usr/include/shapefil.h

  install -D -m644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
