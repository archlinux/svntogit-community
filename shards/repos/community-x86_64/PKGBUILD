# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.17.1
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git libevent gc pcre)
makedepends=(crystal)
checkdepends=(git mercurial fossil)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz)
sha256sums=('cfae162980ef9260120f00ba530273fc2e1b595906b6d39db0cd41323f936e03')

build() {
  cd shards-$pkgver
  CRYSTAL_OPTS="--release" make
}

check() {
  cd shards-$pkgver

  # remove git config settings once https://github.com/crystal-lang/shards/issues/430 is fixed
  git config --global user.email "you@example.com"
  git config --global user.name "Your Name"
  git config --global column.ui always

  make test -j1 # -j1 is due to race condition in test code
}

package() {
  cd shards-$pkgver
  PREFIX=/usr DESTDIR="$pkgdir" make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
