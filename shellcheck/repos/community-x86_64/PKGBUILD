# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=ShellCheck
pkgname=shellcheck
pkgver=0.4.6
pkgrel=1
pkgdesc="Shell script analysis tool"
url="http://www.shellcheck.net"
license=("GPL")
arch=('i686' 'x86_64')
depends=('libffi' 'gmp')
makedepends=("ghc=8.0.1" "haskell-json" "haskell-mtl" "haskell-parsec" "haskell-quickcheck"
             "haskell-regex-tdfa")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b053a7f581d73f1e5be51046f2fd77f6e7452acecf841321ef363156a5f72d98c13a723885d1888ab366550625e05396ca207925539fec1d70a00212af920ca5')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    # Remove library and doc (license only)
    rm -r "$pkgdir"/usr/{lib,share}
}
