# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=ShellCheck
pkgname=shellcheck
pkgver=0.4.4
pkgrel=4
pkgdesc="Shell script analysis tool"
url="http://www.shellcheck.net"
license=("GPL")
arch=('i686' 'x86_64')
depends=('libffi' 'gmp')
makedepends=("ghc=8.0.1" "haskell-json" "haskell-mtl" "haskell-parsec" "haskell-quickcheck"
             "haskell-regex-tdfa")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('6cc50790d25b6f330037c3612c21460aa75839cc32c65e10ea6b35f9f4488768')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    # Remove library and doc (license only)
    rm -r "$pkgdir"/usr/{lib,share}
}
