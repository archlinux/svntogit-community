# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=ShellCheck
pkgname=shellcheck
pkgver=0.4.3
pkgrel=5
pkgdesc="Shell script analysis tool"
url="http://www.shellcheck.net"
license=("GPL")
arch=('i686' 'x86_64')
depends=('libffi' 'gmp')
makedepends=("ghc=7.10.3" "haskell-json" "haskell-mtl" "haskell-parsec" "haskell-quickcheck"
             "haskell-regex-tdfa")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('df71c303c43ae79846c9f9198a4d4ba2c4c2ee4c06974491d7130fcea0b8efdf')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    # Remove library and doc (license only)
    rm -r "$pkgdir"/usr/{lib,share}
}
