# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sherlock.lv2
pkgver=0.24.0
pkgrel=1
pkgdesc="An investigative plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/sherlock/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11')
makedepends=('flex' 'glu' 'lv2' 'meson' 'serd' 'sord' 'sratom')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz"{,.asc})
sha512sums=('40603ca53978e0fad7b593a3ed8ff429ad15930e6da6119da21058d5eb10c6183a4ea8ed93c9561bd187a2b8966f822d9434f4d0098e807c258fa2ab15234fd1'
            'SKIP')
b2sums=('a95dd9a4677179a2aa345c64b490111c931b32a63df36279581beb0da365b43c0492e8708773d8c5d7459afce64b7750ebf289236b7b9ead47ea8de1594f7ac2'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        --libdir=lib \
        --libexecdir lib \
        --sbindir bin \
        --buildtype plain \
        --auto-features enabled \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('atom_inspector' 'midi_inspector' 'osc_inspector')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/sherlock#${_plugin}"
  done
}

package() {
  depends+=('libserd-0.so' 'libsratom-0.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
