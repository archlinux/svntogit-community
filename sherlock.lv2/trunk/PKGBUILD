# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sherlock.lv2
pkgver=0.28.0
pkgrel=1
pkgdesc="An investigative plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/sherlock/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11' 'lv2-host')
makedepends=('flex' 'glew' 'glu' 'lv2' 'meson' 'serd' 'sord' 'sratom')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz"{,.asc})
sha512sums=('e30ec9084b67e0b7065ad548473d3a33be5300dd37a1353be0ae7c88742989a06a1b6c02bd90f065d02f55157f47bdc694e3c415422466c718c6fc2116e3bcd7'
            'SKIP')
b2sums=('90984c0c39d69439791c1d84d8aa29668f391923957c9bb50cd1cefc85b7997f8c7ef363fcf7e9d5b9390fc22da468af70c9142c0a4380646f7ca2c066fab06a'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  depends+=('libGLEW.so' 'libserd-0.so' 'libsratom-0.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
