# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.5.0
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('x86_64')
depends=('glibc')
makedepends=('git' 'go' 'scdoc')
options=('!lto')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('9071a015369088dbba8eb7221305893095ebeaa72124e1ca964b7b1e6f34dedc9d11f2daa2f679d24637b04925cbf58427a24cc4885d8e918cb81844e71d3c6b')

build() {
    cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build -ldflags "-linkmode=external -extldflags=$LDFLAGS -X main.version=${pkgver}"

    scdoc < "${pkgname}.1.scd" | gzip > "${pkgname}.1.gz"
}

check() {
    cd "${srcdir}/sh-${pkgver}/"
    go test ./...
}

package() {
    cd "${srcdir}/sh-${pkgver}/"
    install -Dm755 -t "${pkgdir}/usr/bin/" "cmd/${pkgname}/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/man/man1/" "cmd/${pkgname}/${pkgname}.1.gz"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
