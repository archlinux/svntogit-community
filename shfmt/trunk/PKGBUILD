# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
_pkg=sh
_name="${pkgname}"
pkgver=2.6.2
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/${_pkg}"
license=('BSD')
arch=('x86_64')
makedepends=('git' 'go-pie')
_sh_commit=7434b626f1731a87d421bdc72082f6dfbba33edc
source=("git+${url}#commit=${_sh_commit}")
sha512sums=('SKIP')

prepare() {
	cd "${srcdir}"
	mkdir -p "src/mvdan.cc"
	mv "${_pkg}" "src/mvdan.cc/${_pkg}"
}

build() {
	cd "${srcdir}/src/mvdan.cc/${_pkg}/cmd/${_name}"
	GOPATH="${srcdir}" go build -ldflags='-s -w'
}

package() {
	cd "${srcdir}/src/mvdan.cc/${_pkg}"
	install -Dm755 "cmd/${_name}/${_name}" "${pkgdir}/usr/bin/${_name}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
