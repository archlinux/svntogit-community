# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>

pkgname=shfs-utils
pkgver=0.35
pkgrel=7
pkgdesc='SHell FileSystem Linux utils'
url='http://shfs.sourceforge.net/'
license=('GPL')
arch=('x86_64')
depends=('glibc' 'openssh')
makedepends=('patch')
source=("https://downloads.sourceforge.net/sourceforge/shfs/shfs-$pkgver.tar.gz"
        'shfs-utils-make.patch')
sha256sums=('0d48ffe4d801180c15466f20aaa5802adb9d22067e8941e051dc3c64717ec3c2'
            'e607fcc079cfe4d3af2f5301d485682650fcb5675dc5e58c8087f854b1c0f19c')

prepare() {
  patch -Np0 -d shfs-$pkgver < $pkgname-make.patch
}

build() {
  cd shfs-$pkgver
  make -j1 utils
}

package() {
  cd shfs-$pkgver
  make -j1 ROOT="$pkgdir" utils-install
  # remove sbin symlinks
  rm -r "$pkgdir/sbin"
}
