# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Malte Rabenseifner <malte@zearan.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=(shorewall shorewall6)
pkgver=4.4.13
pkgrel=1
arch=('any')
url="http://www.shorewall.net/"
license=('GPL')
source=(http://www.shorewall.net/pub/$pkgname/4.4/$pkgname-4.4.13/$pkgname-$pkgver.tar.bz2
        http://www.shorewall.net/pub/$pkgname/4.4/$pkgname-4.4.13/shorewall6-$pkgver.tar.bz2)
md5sums=('70b6e8033f93a7985a8a614645218305'
         'a21eaf8e15313a62de4917e60f32253b')

package_shorewall() {
  pkgdesc="An iptables-based firewall for Linux systems"
  depends=('iptables' 'iproute2')
  backup=(etc/shorewall/{accounting,actions,blacklist,clear,ecn,findgw,hosts,init,initdone,interfaces,isusable,lib.private,maclist,masq,nat,netmap,notrack,params,policy,providers,proxyarp,refresh,refreshed,restored,route_rules,routestopped,rules,secmarks,shorewall.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcfilters,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/$pkgname-$pkgver"

  do_install configfiles/
}

package_shorewall6() {
  pkgdesc="An iptables-based firewall for Linux systems (with IPv6 support)"
  depends=('shorewall')
  backup=(etc/shorewall6/{accounting,actions,blacklist,clear,hosts,init,interfaces,isusable,maclist,notrack,params,policy,providers,refresh,refreshed,restored,route_rules,routestopped,rules,secmarks,shorewall6.conf,start,started,stop,stopped,tcclasses,tcclear,tcdevices,tcinterfaces,tcpri,tcrules,tos,tunnels,zones})

  cd "$srcdir/shorewall6-$pkgver"

  do_install '' 6
}

do_install() {
  local _conf_prefix=$1
  local _name_suffix=$2

  sed -i -e 's|LOGFILE=/var/log/messages|LOGFILE=/var/log/messages.log|' \
         -e 's|/subsys||' \
      ${_conf_prefix}shorewall$_name_suffix.conf

  cp init.archlinux.sh init.sh
  DEST=/etc/rc.d INIT=shorewall$_name_suffix PREFIX="$pkgdir" ./install.sh
  cp -r Samples$_name_suffix "$pkgdir/usr/share/shorewall$_name_suffix/"

  chmod 755 "$pkgdir/etc/rc.d/shorewall$_name_suffix"
  chmod -R 644 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 755 "$pkgdir/etc/shorewall$_name_suffix/"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/modules"
  chmod 644 "$pkgdir/usr/share/shorewall$_name_suffix/helpers"
}

# vim:set ts=2 sw=2 et:
