# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
pkgver=0.11.0
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('gconf' 'libgee' 'libgphoto2' 'libunique' 'libwebkit' 'libgexiv2' 'libraw'
	 'librsvg' 'json-glib' 'desktop-file-utils')
makedepends=('intltool' 'vala')
install=shotwell.install
source=("http://yorba.org/download/shotwell/0.11/shotwell-${pkgver}.tar.bz2")
md5sums=('f0e942b639d899a91c3cf343f6f553bb')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr \
	--with-gconf-schema-file-dir=/usr/share/gconf/schemas \
	--disable-schemas-install \
	--disable-desktop-update \
	--disable-icon-update
     make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
