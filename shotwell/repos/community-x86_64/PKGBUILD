# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
pkgver=0.11.1
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('gconf' 'libgee' 'libgphoto2' 'libunique' 'libwebkit' 'libgexiv2' 'libraw'
	 'librsvg' 'json-glib' 'desktop-file-utils' 'dconf')
makedepends=('intltool' 'vala')
install=shotwell.install
source=("http://yorba.org/download/shotwell/0.11/shotwell-${pkgver}.tar.bz2")
md5sums=('e2c388bfce992b5437281c4fc29357d9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--with-gconf-schema-file-dir=/usr/share/gconf/schemas \
	--disable-schemas-install \
	--disable-desktop-update \
	--disable-icon-update
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f $pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled
}
