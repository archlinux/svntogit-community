# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
pkgver=0.5.2
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('gconf' 'libgee' 'libgphoto2' 'libunique' 'libwebkit')
makedepends=('intltool' 'vala>=0.7.10')
install=shotwell.install
source=(http://yorba.org/download/shotwell/0.5/${pkgname}-${pkgver}.tar.bz2)
md5sums=('a347765f3a6ae8408a97d559ed50b0b8')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr \
	--with-gconf-schema-file-dir=/usr/share/gconf/schemas \
	--disable-schemas-install \
	--disable-desktop-update \
	--disable-icon-update || return 1
     make || return 1
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install || return 1
}
