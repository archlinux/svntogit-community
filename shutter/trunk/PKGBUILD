# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: PhotonX <photon89 [at] gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Shanto <shanto@hotmail.com>
# Contributor: Athurg <athurg@gooth.cn>
# Contributor: TDY <tdy@gmx.com>

pkgname=shutter
pkgver=0.99.2
pkgrel=1
pkgdesc="The feature-rich screenshot tool"
arch=('any')
url="https://shutter-project.org/"
license=('GPL')
depends=('xdg-utils' 'imagemagick' 'procps' 'librsvg' 'libwnck3'
         'perl-glib-object-introspection'
         'perl-number-bytes-human'
         'perl-x11-protocol'
         'perl-proc-simple'
         'perl-proc-processtable'
         'perl-net-dbus'
         'perl-sort-naturally'
         'perl-json'
         'perl-json-maybexs'
         'perl-xml-simple'
         'perl-www-mechanize'
         'perl-locale-gettext'
         'perl-file-which'
         'perl-file-basedir'
         'perl-file-copy-recursive'
         'perl-xml-simple'
         'perl-carp-always'
         'perl-gtk3'
         'perl-gtk3-imageview'
         'perl-readonly'
         'perl-goocanvas2'
         'perl-goocanvas2-cairotypes'
         'pango-perl')
optdepends=('perl-image-exiftool: read and write EXIF data'
            'nautilus-sendto: send screenshots via mail'
            'perl-path-class: Imgur upload support'
            'perl-lwp-protocol-https: Imgur upload support'
            'perl-net-oauth: Imgur upload support'
            'bc: 3D Rotate and 3D Reflection plugins support'
            'libappindicator-gtk3: display the tray icon in some environments (like GNOME)')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/shutter-project/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('5fcb094f0e6f732da3648e58ef62aa1e5537132ffb0bff80972e1891f58fe5e63ec63bc7bec63e84268c095e3cd43d60d358be61e2d050dfcc49ffb042d34cfb')

package() {
  cd "${pkgname}-${pkgver}"
  make install prefix="${pkgdir}/usr"
}

