# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Sven Kauber <celeon@gmail.com>

pkgname=siege
pkgver=4.0.5
pkgrel=2
arch=('x86_64')
license=('GPL')
pkgdesc="An http regression testing and benchmarking utility"
url="https://www.joedog.org/siege-home/"
depends=('openssl')
optdepends=('perl: siege2csv.pl')
source=("http://download.joedog.org/siege/siege-${pkgver}.tar.gz")
backup=(etc/siegerc etc/urls.txt)

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  install -d -m755 "$pkgdir/etc"
  install -d -m755 "$pkgdir/usr/share"
  make DESTDIR="$pkgdir" install
  sed -i 's/^# file =$/file = \/etc\/urls.txt/' "$pkgdir/etc/siegerc"
}

md5sums=('b84ff983534e92d887165eadc70e5dae')
