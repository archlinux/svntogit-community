# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Alad Wenter <alad@mailbox.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sigil
pkgver=1.9.10
pkgrel=2
pkgdesc='multi-platform EPUB2/EPUB3 ebook editor'
arch=('x86_64')
url=https://sigil-ebook.com
_url=https://github.com/Sigil-Ebook/Sigil
license=(GPL3)
depends=(hicolor-icon-theme
         hunspell
         mathjax2
         minizip
         python-css-parser
         python-dulwich
         python-lxml
         qt5-svg
         qt5-webengine)
makedepends=(cmake
             qt5-tools)
optdepends=('hunspell-en_US: for English dictionary support'
            'hyphen-en: for English hyphenation support in plugins'
            'pageedit: external editor to replace BookView'
            'python-html5lib: recommended for plugins'
            'python-chardet: recommended for plugins'
            'python-cssselect: recommended for plugins'
            'python-pillow: recommended for plugins'
            'python-regex: recommended for plugins'
            'python-pyqt5: recommended for plugins'
            'python-pyqt5-webengine: recommended for plugins'
            'tk: recommended for plugins')
_archive="${pkgname^}-$pkgver"
source=("$_url/archive/$pkgver/$_archive.tar.gz"
        "$_url/releases/download/${pkgver}/$_archive.tar.gz.sig"
         https://github.com/Sigil-Ebook/Sigil/commit/f1aae749.patch
         https://github.com/Sigil-Ebook/Sigil/commit/ab629582.patch)
sha256sums=('35b6051d966c2848a8a031ebe691449229b3c95a56145647dc71857acc18fad5'
            'SKIP'
            '968618f6b6714492866f6845be33d526b590c7579b113459c883f4d65993002f'
            '0b915c687d964eacf54cdbcc0f56a37f9207c7f8e3a303c33b6a4d41c3c53544')
b2sums=('ef8d6b5552689f0501971563b429efa050456826ee85dbf524ecb1c1931506de95909e3443bc259353c012b3e4a2b6ce3299f20af8e65d07324818a8f208c2a8'
        'SKIP'
        '43603e516cd9384346c2bcf02b8ddac238c9c7ffd2e479c6c800e92fd7c4b66dd49b93e89743741ed652ab259c21a3992c32414c4b41df70dd603f457c89725c'
        'f3f7f34718cb571e0d69b0ab35d0f57c007a5279c220fcb687e5707faf9e7d1294dce670f9890149e78c53e2e20402a6360ab34f547093c1c5851362ca4cb739')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

prepare() {
	cd "$_archive"
	mkdir -p build

	# Fix segfault when compiled with -lto
	patch -p1 -i ../f1aae749.patch
	patch -p1 -i ../ab629582.patch
}

build() {
	cd "$_archive/build"
	cmake -G "Unix Makefiles" \
		-DUSE_SYSTEM_LIBS=1 \
		-DSYSTEM_LIBS_REQUIRED=1 \
		-DINSTALL_BUNDLED_DICTS=0 \
		-DINSTALL_HICOLOR_ICONS=1 \
		-DDISABLE_UPDATE_CHECK=1 \
		-DMATHJAX_DIR=/usr/share/mathjax2 \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DCMAKE_C_FLAGS:STRING="$CFLAGS" \
		-DCMAKE_CXX_FLAGS:STRING="$CXXFLAGS" \
		-DCMAKE_SKIP_RPATH=ON ..
	make
}

package() {
	cd "$_archive/build"
	make install DESTDIR="$pkgdir"
	python -m compileall "$pkgdir/usr/share/sigil/"{plugin_launchers/python/,python3lib}
	python -O -m compileall "$pkgdir/usr/share/sigil/"{plugin_launchers/python/,python3lib}
}
