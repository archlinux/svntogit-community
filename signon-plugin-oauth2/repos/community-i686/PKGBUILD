# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-plugin-oauth2
_pkgname=signon-oauth2
pkgver=0.15
pkgrel=1
pkgdesc="OAuth 2 plugin for signon"
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('signon-ui' 'qjson')
makedepends=('signon')
source=(http://accounts-sso.googlecode.com/files/$_pkgname-$pkgver.tar.bz2)
sha1sums=('31f63f73eca2c8341ab41ec0d63650c9a0b4820a')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  # Do not install tests and example
  echo 'INSTALLS =' >>tests/tests.pro
  echo 'INSTALLS =' >>example/example.pro

  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}
