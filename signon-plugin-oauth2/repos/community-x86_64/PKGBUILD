# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-plugin-oauth2
_pkgname=signon-oauth2
pkgver=0.18
pkgrel=1
pkgdesc="OAuth 2 plugin for signon"
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('signon-ui' 'qjson')
makedepends=('signon')
source=(http://accounts-sso.googlecode.com/files/$_pkgname-$pkgver.tar.bz2)
sha1sums=('4b7aabd69a67814b19447215b6466a93523d5dce')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  sed -i '/-Werror/d' common-project-config.pri

  # Do not install tests and example
  echo 'INSTALLS =' >>tests/tests.pro
  echo 'INSTALLS =' >>example/example.pro

  qmake-qt4 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}
