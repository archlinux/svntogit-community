# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=sigutils
pkgver=0.1.0
pkgrel=2
pkgdesc='Small signal processing utility library'
arch=('x86_64')
url='https://github.com/BatchDrake/sigutils'
license=('GPL3')
depends=('libsndfile' 'fftw' 'libvolk')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('871bbd72739463f1094748edbddf07a9641fb34b29ba78b4d241ad1116f3a85128e99800561f72348a8c7a2590e2d0b3a3b99c368246807bac7a5665fb5829ca')

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  export CFLAGS+=" ${CPPFLAGS}"
  export CXXFLAGS+=" ${CPPFLAGS}"

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None

  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}

