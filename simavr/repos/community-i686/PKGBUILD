# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=simavr
pkgver=1.5
pkgrel=1
pkgdesc='A lean, mean and hackable AVR simulator'
arch=('i686' 'x86_64')
url="https://github.com/buserror-uk/simavr"
license=('GPL3')
depends=('elfutils')
makedepends=('avr-libc' 'git')
source=("${pkgname}::git+https://github.com/buserror-uk/simavr.git#tag=v${pkgver}")
options=(!strip)
md5sums=('SKIP')

build() {
  cd "$srcdir/$pkgname"

  make AVR_ROOT=/usr/avr RELEASE=1 \
      CFLAGS="-Wall -Wextra -fPIC -O2 -std=gnu99 -Wno-sign-compare -Wno-unused-parameter" \
      build-simavr
}

package() {
  cd "$srcdir/$pkgname"
  make PREFIX="/usr" DESTDIR="$pkgdir/usr" install
}

