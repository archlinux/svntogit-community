# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: jiangxq <jiangxueqian at gmail dot com>
# Contributor: zh99998 <zh99998@gmail.com>
# Contributor: 4679kun <admin at 4679 dot us>

pkgname=simple-obfs
pkgver=0.0.5
pkgrel=2
pkgdesc='A simple obfusacting tool designed as plugin server of shadowsocks'
arch=('x86_64')
url='https://github.com/shadowsocks/simple-obfs'
license=('GPL')
depends=('libev' 'libcork')
makedepends=('asciidoc' 'xmlto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/simple-obfs/archive/v$pkgver.tar.gz")
sha512sums=('aadb812d5a57fea664958b66aae0c5be5e82309758706181ed5f1c76a986c1a6eaa82a6cb8b242052a951dd2d9d178d734f04052be887b5dd8f336808db61bcb')

prepare() {
  cd simple-obfs-$pkgver
  patch -p1 -i debian/patches/0001-Use-libcork-dev-in-system.patch
}

build() {
  cd simple-obfs-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd simple-obfs-$pkgver
  make DESTDIR="$pkgdir" install
}
