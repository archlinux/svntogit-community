# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=3.20.0
pkgrel=1
pkgdesc='Simple scanning utility'
arch=('i686' 'x86_64')
url='http://launchpad.net/simple-scan'
license=('GPL3')
depends=('sane' 'dconf' 'gtk3' 'colord' 'hicolor-icon-theme' 'libgusb' 'packagekit')
makedepends=('yelp-tools' 'intltool' 'vala')
install=$pkgname.install
source=(https://launchpad.net/$pkgname/${pkgver:0:4}/$pkgver/+download/$pkgname-$pkgver.tar.xz)
sha256sums=('8df5a5a5b06220e98770f11c53db8fc5d0d8d675d1c395f2bcd19fc8656eb62c')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
