# $Id$
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=3.26.3
pkgrel=1
pkgdesc="Simple scanning utility"
url="https://launchpad.net/simple-scan"
arch=(x86_64)
license=(GPL3)
groups=(gnome)
depends=(sane dconf gtk3 colord libgusb)
makedepends=(yelp-tools meson vala git)
_commit=81eadec7bf420e7d1bdc039a1470de3f156b92b5  # tags/3.26.3^0
source=("git+https://gitlab.gnome.org/GNOME/simple-scan.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
