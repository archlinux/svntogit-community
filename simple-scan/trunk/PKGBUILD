# $Id$
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=3.26.2
pkgrel=1
pkgdesc="Simple scanning utility"
url="https://launchpad.net/simple-scan"
arch=(x86_64)
license=(GPL3)
depends=(sane dconf gtk3 colord libgusb)
makedepends=(yelp-tools meson vala git)
_commit=6ed023f9d94d1d394d7a98505004b53b9bf99a38  # tags/3.26.2^0
source=("git+https://git.gnome.org/browse/simple-scan#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
