# $Id$
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=3.24.1
pkgrel=1
pkgdesc="Simple scanning utility"
url="https://launchpad.net/simple-scan"
arch=(i686 x86_64)
license=(GPL3)
depends=(sane dconf gtk3 colord libgusb)
makedepends=(yelp-tools intltool vala)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('5eae832b14664183d55aa599801cf32f19ff2174dfba4e3d7e4eb4d55810efb3')

prepare() {
  cd $pkgname-$pkgver

  # Force regeneration
  touch src/*.vala
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --disable-schemas-compile \
    --disable-packagekit
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
