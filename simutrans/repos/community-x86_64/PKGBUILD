# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gilles Gagniard <gilles@gagniard.org>
# Contributor: JD Steffen <jd at steffennet dot org>

pkgname=simutrans
pkgver=120.4.1
_pkgver=120-4-1
pkgrel=1
pkgdesc="Transportation simulation game"
arch=('x86_64')
url="https://www.simutrans.com/"
license=('custom:Artistic')
depends=('gcc-libs' 'zlib' 'sdl_mixer' 'bzip2' 'simutrans-pak64')
makedepends=('imagemagick' 'dos2unix')
optdepends=('timidity++: play MIDI music')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-src-$_pkgver.zip
        settings-folder.patch
        path-for-game-data.patch
        config.patch
        Makefile
        common.mk
        simutrans.desktop)
sha256sums=('2cee0d067b3b72fa3a8b4ff31ad2bf5fc77521e7ba8cf9aa10e07e56b7dc877b'
            'cbe5d7c7be96e6e7262c1e3b1c16155f5c179609f081e3ee050bbfc277845f71'
            '1a0fc75fe1422404e424c91f5a23301234b2997cb727b4985c98ac1af6a4582a'
            '24bf2b611706c42fd52dc00d61fa2e28f54a614f17e43bc463817406e63eba3a'
            '1107a873273bd0a02dc7877137816d2ca4fbfd403a3443fc244d31c8a611b81b'
            'd17d3b91a989ddca1735875e8d01c4c39ef318a3490d3f24a05b0713fcb69983'
            '99545152f5e739b7eb028152383fa10d3e3d303c99167e1c6e5a6bd7dcd00fa3')

prepare() {
  # Some files are distributed in DOS format
  find . -type f -exec dos2unix -q '{}' \;

  # Adjust paths
  patch -Np0 -i settings-folder.patch
  patch -Np1 -i path-for-game-data.patch

  # Configure the build process
  cp config.template config.default
  patch -Np0 -i config.patch
}

build() {
  convert simutrans.ico +set date:create +set date:modify -alpha on simutrans.png
  make
}

package() {
  #binary
  install -Dm755 build/default/sim "$pkgdir/usr/bin/simutrans"

  #data
  mkdir -p "$pkgdir/usr/share/games"
  cp -r $pkgname "$pkgdir/usr/share/games/"

  #desktop file and icon
  install -Dm644 simutrans.png "$pkgdir/usr/share/pixmaps/simutrans.png"
  install -Dm644 simutrans.desktop "$pkgdir/usr/share/applications/simutrans.desktop"

  #license
  install -Dm644 simutrans/license.txt "$pkgdir/usr/share/licenses/simutrans/license.txt"
}
