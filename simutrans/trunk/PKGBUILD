# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Gilles Gagniard <gilles@gagniard.org>
# Contributor: JD Steffen <jd at steffennet dot org>

pkgname=simutrans
pkgver=120.2.2
_pkgver=120-2-2
pkgrel=1
pkgdesc="An open source transportation simulation game"
arch=('x86_64')
url="https://www.simutrans.com/"
license=('custom:Artistic')
depends=('gcc-libs' 'zlib' 'sdl_mixer' 'bzip2' 'simutrans-pak64')
makedepends=('imagemagick' 'dos2unix')
optdepends=('timidity++: play MIDI music')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-src-$_pkgver.zip
        https://downloads.sourceforge.net/$pkgname/simulinux-$_pkgver.zip
        no-optimise.patch
        settings-folder.patch
        path-for-game-data.patch
        config.patch
        simutrans.desktop)
sha256sums=('7f22c144377abf1a0ee49432f3f6f5eb9a620567960df31d2ea6399b17cf26fa'
            '029891edb59036183a6e2757a6a01eb7eddec263043650aabaf49e948497328a'
            '2425da5c5b211a2688fa8809caf6362b731d897254d2c5ae2293d28276aa9c58'
            'cbe5d7c7be96e6e7262c1e3b1c16155f5c179609f081e3ee050bbfc277845f71'
            '59a606ecac52b338a3eea120ab8a04b95457fdd4ae6ea4e10087ef0da4724aa2'
            '24bf2b611706c42fd52dc00d61fa2e28f54a614f17e43bc463817406e63eba3a'
            '99545152f5e739b7eb028152383fa10d3e3d303c99167e1c6e5a6bd7dcd00fa3')

prepare() {
  # Some files are distributed in DOS format
  find . -type f -exec dos2unix -q '{}' \;

  # Don't overwrite default optimization flags
  patch -Np1 -i no-optimise.patch

  # Adjust paths
  patch -Np0 -i settings-folder.patch
  patch -Np1 -i path-for-game-data.patch

  # Configure the build process
  cp config.template config.default
  patch -Np0 -i config.patch
}

build() {
  convert simutrans.ico -alpha on simutrans.png
  make
}

package() {
  #binary
  install -Dm755 build/default/sim "$pkgdir/usr/bin/simutrans"

  #data
  mkdir -p "$pkgdir/usr/share/games/$pkgname"
  cp -r $pkgname/{config,font,music,text,themes} "$pkgdir/usr/share/games/$pkgname"

  #desktop file and icon
  install -Dm644 simutrans.png "$pkgdir/usr/share/pixmaps/simutrans.png"
  install -Dm644 simutrans.desktop "$pkgdir/usr/share/applications/simutrans.desktop"

  #license
  install -Dm644 simutrans/license.txt "$pkgdir/usr/share/licenses/simutrans/license.txt"
}
