# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lone_Wolf <lonewolf@xs4all.nl> 
# Contributor: sausageandeggs <s_stoakley at hotmail.co.uk>

pkgname=singularity
pkgver=0.30c
pkgrel=8
pkgdesc="Strategy game - simulation of true AI"
arch=('any')
url="http://emhsoft.com/singularity/index.html"
license=('GPL2' 'custom')
depends=('python2' 'python2-pygame' 'sdl_image' 'sdl_mixer' 'python2-numpy')
source=("http://www.emhsoft.com/singularity/singularity-${pkgver}-src.tar.gz"
        "http://www.emhsoft.com/singularity/endgame-singularity-music-007.zip"
        "singularity"
        "singularity.desktop")
md5sums=('ea4e7d28dd19c33585691ee608672a63'
         '36ab600d1f6df24e3550b37289cc7710'
         'fe74e41d2d35dd1907342dea8b078651'
         'c0f49fb6d31a2cf4c0b80393df2aad16')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|/usr/bin/env python|/usr/bin/env python2|g' "code/singularity.py"

  find . -name \*.pyc -delete
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # install game
  install -Dm644 singularity.py $pkgdir/usr/share/singularity/singularity.py
  cp -R code data "$pkgdir/usr/share/$pkgname"
  cp -R "$srcdir/endgame-$pkgname-music-007" "$pkgdir/usr/share/$pkgname/music"

  # install shell script
  install -Dm755 "$srcdir/singularity" "$pkgdir/usr/bin/singularity"

  # desktop file
  install -Dm644 "$srcdir/singularity.desktop" "$pkgdir/usr/share/applications/singularity.desktop"

  # license: code is GPL2, but data & music fall under other licenses
  install -Dm644 README.txt "$pkgdir/usr/share/doc/$pkgname/README.txt"
  install -d "$pkgdir/usr/share/licenses/$pkgname/"
  cd "$pkgdir/usr/share/licenses/$pkgname/"
  ln -s ../../doc/$pkgname/README.txt .

  python2 -m compileall "$pkgdir/usr/share/$pkgname"
  python2 -O -m compileall "$pkgdir/usr/share/$pkgname"
}
# vim: sw=2:ts=2 et:
