# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=skanlite
pkgver=2.0.1
pkgrel=1
pkgdesc="Image Scanning Application for KDE"
arch=(x86_64)
url='http://www.kde.org/applications/graphics/skanlite'
license=(GPL)
depends=(libksane kio)
makedepends=(extra-cmake-modules kdoctools)
source=("http://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
md5sums=('95e310b2030bb55562268fe50252feb0')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
