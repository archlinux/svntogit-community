# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: noonov <noonov@gmail.com>

pkgname=skktools
pkgver=1.3.3
pkgrel=3
pkgdesc="Dictionary maintenance tools for the SKK Japanese input method"
arch=('x86_64')
url="http://openlab.ring.gr.jp/skk/"
license=('GPL2')
depends=('gdbm' 'glib2')
optdepends=('ruby' 'python2' 'perl')
source=(http://openlab.ring.gr.jp/skk/tools/${pkgname}-${pkgver}.tar.gz)
sha512sums=('2a7f9543017fbb7447d38725e5cbfeccdbb39dcd93020f1691443ebd42cdf3aec017492fe164c2b8589b664b22e570a893990d09c8df62298b2f2d4428e821e3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --with-gdbm
  make

  sed -i '1s|python|&2|' skk2cdb.py
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/skktools"
  cp -a -t "${pkgdir}/usr/share/skktools" \
    convert2skk dbm filters \
    saihenkan.rb skk2cdb.py skkdic-diff.scm unannotation.awk

  install -D -m644 skk-xml.el "${pkgdir}/usr/share/emacs/site-lisp/skk-xml.el"
}
