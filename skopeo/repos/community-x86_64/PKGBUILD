# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=skopeo
pkgver=1.2.2
pkgrel=2
pkgdesc='A command line utility for various operations on container images and image repositories.'
arch=(x86_64)
url='https://github.com/projectatomic/skopeo'
license=(APACHE)
depends=(gpgme device-mapper containers-common)
makedepends=(go go-md2man btrfs-progs git systemd)
_commit=e72dd9c5c834f3cd7fb8b1aab4021d9d4412f305	#refs/tags/v1.2.2
source=(git+$url#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make bin/skopeo BUILDTAGS='containers_image_ostree_stub'
  make docs
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/etc/containers/registries.d"
  rm "${pkgdir}/etc/containers/policy.json"
}
