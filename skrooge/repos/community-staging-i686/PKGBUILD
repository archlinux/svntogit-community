# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.8.1
pkgrel=2
pkgdesc="A personal finances manager for KDE"
arch=('i686' 'x86_64')
url="http://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qt5-tools' 'qca-qt5' 'qt5-script'  'qt5-webkit' 'libofx' 'grantlee'
         'kross' 'knewstuff' 'krunner' 'knotifyconfig' 'kdelibs4support' 'sqlcipher')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'python' 'kdesignerplugin')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('8f166db74a27db0ce0ea197f89dfd021224afc3e4875e36438a4496f10746460'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  export
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=`kf5-config --prefix` \
          -DKDE_INSTALL_QTPLUGINDIR=`kf5-config --qt-plugins` \
          -DKDE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
