# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.10.5
pkgrel=1
pkgdesc="A personal finances manager for KDE"
arch=('i686' 'x86_64')
url="http://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qca-qt5' 'qt5-script' 'qt5-webkit' 'libofx' 'grantlee'
         'kross' 'knewstuff' 'krunner' 'knotifyconfig' 'sqlcipher')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'python' 'qt5-tools')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('56a0124dec34e6e96a5e71ff0e825a7ec79f32a69ef0ccdc5f0f9b753d8c3eb0'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

#   export
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_LIBDIR=/usr/lib \
          -DSKG_DESIGNER=OFF
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
