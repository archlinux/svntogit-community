# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.26.1
pkgrel=2
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qca' 'qt5-script' 'qt5-webengine' 'libofx'
         'grantlee' 'knewstuff' 'krunner' 'knotifyconfig' 'kparts'
         'sqlcipher' 'python' 'hicolor-icon-theme')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt5-tools' 'kdesignerplugin')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('eba86803e1434de31b340227afd4e54d29f0532c094234d1cfcecc91d3589f94'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  cmake . -DSKG_WEBENGINE=ON \
          -DSKG_WEBKIT=OFF \
          -DSKG_BUILD_TEST=OFF
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
