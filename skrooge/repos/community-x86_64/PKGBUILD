# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=1.10.0
pkgrel=2
pkgdesc="A personal finances manager for KDE"
arch=('i686' 'x86_64')
url="http://skrooge.org/"
license=('GPL')
depends=('kdebase-runtime' 'libofx' 'qca-ossl' 'qjson' 'grantlee-qt4')
makedepends=('cmake' 'automoc4' 'docbook-xsl' 'boost' 'kdepim-libkdepim')
optdepends=('kdepim-libkdepim: for KDE PIM integration')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('aae15e7abf91a7fc217247d3ae5e672f4215a9090f12389cddab68fe5a175c67')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
