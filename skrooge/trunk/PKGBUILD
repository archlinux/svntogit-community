# $Id $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=0.7.2
pkgrel=2
pkgdesc="Allows you to manage your personal finances in KDE"
arch=('i686' 'x86_64')
url="http://skrooge.org"
license=('GPL')
depends=('kdebase-runtime' 'libofx' 'qca-ossl')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
install="${pkgname}.install"
source=("http://skrooge.org/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('b8f90af42f8ac86aa30d4d8506f506d7')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
