# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tarinaky <tarinaky@hunity.co.uk>

pkgname=skype-call-recorder
pkgver=0.9
pkgrel=2
pkgdesc="Open source tool that allows you to record your Skype calls on Linux"
arch=('i686' 'x86_64')
url="http://atdot.ch/scr/"
license=('GPL')
depends=('qt4' 'libvorbis' 'lame' 'id3lib' 'hicolor-icon-theme')
makedepends=('cmake')
install=skype-call-recorder.install
source=($pkgname-$pkgver.tar.gz::https://github.com/jlherren/$pkgname/archive/$pkgver.tar.gz
        skype-call-recorder-0.9-dso-fix.patch)
sha256sums=('b143a75f66c16c7ae3d6a55aac50b0217b9e1097c838a9e406f00ce1561ac98e'
            'a2d4d5640970c90205285945b08891238287b488beb958abd42e08565aef747b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/skype-call-recorder-0.9-dso-fix.patch"

  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
