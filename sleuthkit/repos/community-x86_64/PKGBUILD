# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=sleuthkit
pkgver=3.2.1
pkgrel=1
pkgdesc='File system and media management forensic analysis tools'
arch=('i686' 'x86_64')
url='http://www.sleuthkit.org/sleuthkit'
license=('GPL2' 'CPL' 'custom:"IBM Public Licence"')
depends=('perl')
options=('!libtool')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('e0f257d2c6856dc1c1cd2a704c6cb8906ca688f0cdbb1d0665fd7a5a2f83ad37')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --without-afflib --without-ewf --prefix=/usr
  make LDFLAGS+="-lpthread -ldl"
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

# licenses
  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -Dm0644 licenses/* ${pkgdir}/usr/share/licenses/${pkgname}
}
