# Contributor: Johannes Weiner <hannes@saeurebad.de>
# Contributor: Daniel Leidisch <spam@leidisch.net>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=slime-cvs
pkgver=2011_08_28
pkgrel=1
arch=('any')
pkgdesc="The Superior Lisp Interaction Mode for Emacs"
url="http://common-lisp.net/project/slime/"
depends=('emacs')
makedepends=('cvs' 'awk' 'texlive-core')
optdepends=('awk: for recreating the documentation' 'texlive-core: for recreating the documentation')
license=('custom')
conflicts=('slime')
provides=('slime')
install='slime.install'
source=(slime-$pkgver.tar.gz::http://common-lisp.net/project/slime/snapshots/slime-current.tgz \
  licenses)
md5sums=('3496ab6d066d3377e7edebebd751ee4e'
         '1cdfb69afc10f0d0b690884591678081')

package() {
  cd $srcdir/slime-${pkgver//_/-}
  
  install -d $pkgdir/usr/share/emacs/site-lisp/slime
  cp -r $srcdir/slime-${pkgver//_/-}/* \
    $pkgdir/usr/share/emacs/site-lisp/slime

  install -d $pkgdir/usr/share/common-lisp/systems
  ln -s /usr/share/emacs/site-lisp/slime/swank.asd \
    $pkgdir/usr/share/common-lisp/systems/

  find $pkgdir -iname "*cvs*" | xargs rm -rf
  install -D -m644 $srcdir/licenses \
    $pkgdir/usr/share/licenses/$pkgname/licenses 
  cd $srcdir/slime-${pkgver//_/-}/doc
  make 
  make infodir=$pkgdir/usr/share/info install 
  rm $pkgdir/usr/share/info/dir
  install -Dm644 slime.pdf $pkgdir/usr/share/doc/slime/slime.pdf 
  install -Dm644 slime-refcard.pdf \
    $pkgdir/usr/share/doc/slime/slime-refcard.pdf 
  install -d $pkgdir/usr/share/doc/slime/html 
  install -m644 html/* $pkgdir/usr/share/doc/slime/html
}

