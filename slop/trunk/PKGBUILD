# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: naelstrof <naelstrof@gmail.com>

pkgname=slop
pkgver=7.6
pkgrel=2
pkgdesc='Utility to query the user for a selection and print the region to stdout'
arch=('x86_64')
url='https://github.com/naelstrof/slop'
license=('GPL3')
depends=('libxext' 'libx11' 'mesa' 'glew' 'libxrender')
makedepends=('cmake' 'glm')
source=("${pkgname}-${pkgver}.tar.gz::$url/archive/v${pkgver}.tar.gz"
        bugfix-add-missed-header.patch)
md5sums=('381851df1007fc60cc473b3c8c6a81a9'
         'f2bb3f2760b8a128b13db80e1ee1377c')

prepare() {
	cd "$pkgname-$pkgver"
	patch -Np1 -i ../bugfix-add-missed-header.patch
}

build() {
	cd "$pkgname-$pkgver"
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
	make
}

package() {
	make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
