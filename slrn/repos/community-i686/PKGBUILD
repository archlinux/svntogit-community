# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=slrn
pkgver=1.0.2
pkgrel=1
pkgdesc="An open source text-based news client"
arch=('i686' 'x86_64')
url="http://www.slrn.org/"
license=('GPL')
depends=('openssl' 'slang')
makedepends=('uudeview')
optdepends=('metamail')
options=('!makeflags' 'docs' 'zipman')
backup=(etc/slrnrc)
source=("http://jedsoft.org/releases/slrn/slrn-$pkgver.tar.bz2")
md5sums=('4962fc3311023c7a343919ae6fb440e7')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
	--enable-setgid-code \
	--with-slrnpull --with-ssl --with-uu \
	--with-uulib=/usr/lib/uudeview \
	--with-uuinc=/usr/include/uudeview \
	--enable-spoilers
  make UUDEVIEW_LIB="/usr/lib/uudeview/*.o"
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 doc/slrn.rc $pkgdir/etc/slrnrc
}
