# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>

pkgname=slrn
pkgver=1.0.1
pkgrel=1
pkgdesc="An open source text-based news client"
arch=('i686' 'x86_64')
url="http://www.slrn.org/"
license=('GPL')
depends=('openssl' 'slang')
makedepends=('uudeview')
optdepends=('metamail')
options=('!makeflags' 'docs' 'zipman')
backup=(etc/slrnrc)
source=(http://downloads.sourceforge.net/slrn/$pkgname-$pkgver.tar.gz)
md5sums=('232670ab76a045967b137e5124b3d681')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
	--enable-setgid-code \
	--with-slrnpull --with-ssl --with-uu \
	--with-uulib=/usr/lib/uudeview \
	--with-uuinc=/usr/include/uudeview \
	--enable-spoilers
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 doc/slrn.rc $pkgdir/etc/slrnrc
}
