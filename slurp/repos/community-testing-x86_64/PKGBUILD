# Maintainer: Ainola <ainola@archlinux.org>

pkgname=slurp
pkgver=1.2.0
pkgrel=2
pkgdesc='Select a region in a Wayland compositor'
arch=('x86_64')
url="https://github.com/emersion/slurp"
license=('MIT')
depends=('cairo' 'wayland')
makedepends=('meson' 'scdoc' 'wayland-protocols')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/emersion/slurp/releases/download/v$pkgver/slurp-$pkgver.tar.gz"
    "$pkgname-$pkgver.tar.gz.sig::https://github.com/emersion/slurp/releases/download/v$pkgver/slurp-$pkgver.tar.gz.sig"
)
validpgpkeys=("34FF9526CFEF0E97A340E2E40FDE7BE0E88F5E48") # emersion
sha256sums=('050aada0dd7925a7fc5811f3c1b1c88236fdbcdc75587e8a9393373951c2c145'
            'SKIP')

build() {
    meson --prefix=/usr --buildtype=plain "$pkgname-$pkgver" build
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    cd "$pkgname-$pkgver"
    install -Dm 0644 LICENSE -t "$pkgdir/usr/share/licenses/slurp/"
    install -Dm 0644 README.md -t "$pkgdir/usr/share/doc/slurp/"
}
