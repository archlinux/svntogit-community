# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mrshpot <mrshpot at gmail dot com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=smalltalk
pkgver=3.2.5
pkgrel=2
pkgdesc='Implementation of Smalltalk-80'
url='http://smalltalk.gnu.org/'
license=('GPL' 'LGPL')
arch=('x86_64' 'i686')
options=('!libtool' '!emptydirs' 'staticlibs')
depends=('gmp' 'libffi' 'libsigsegv' 'readline' 'libltdl')
makedepends=('gdbm' 'gtk2' 'sqlite' 'tk' 'zip')
install="$pkgname.install"
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('06e574e818ec49f0555d948ae53b1453d8c1df59ef597dad911a4fd1ffba1cce')
optdepends=('tk: for gst-blox'
            'sqlite: database in a file'
            'sed: for examples'
            'gtk2: GUI toolkit')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/smalltalk \
    --with-imagedir=/var/lib/smalltalk \
    --enable-gtk=yes \
    --with-system-libffi \
    --with-system-libsigsegv \
    --with-readline \
    --with-tcl \
    --with-tk \
    --with-x \
    --without-emacs
  echo '#define USE_INTERP_RESULT 1' >> config.h
  make
}

check() {
  cd "$pkgname-$pkgver"

  make check || return 1
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Fix manpage symlink
  rm -f "$pkgdir/usr/share/man/man1/gst-reload.1"
  ln -s gst-load.1 "$pkgdir/usr/share/man/man1/gst-reload.1"
}

# vim:set ts=2 sw=2 et:
