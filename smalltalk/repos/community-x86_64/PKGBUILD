# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mrshpot <mrshpot at gmail dot com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=smalltalk
pkgver=3.2.4
pkgrel=2
pkgdesc='A free implementation of Smalltalk-80 by the GNU project'
url='http://smalltalk.gnu.org/'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
options=('!libtool')

depends=('gmp' 'libffi' 'libsigsegv' 'readline')
makedepends=('gdbm' 'gtk2' 'sqlite3' 'tk' 'zip')

source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('a36a7c9beddca08dc492b500738efc82')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/smalltalk \
    --with-imagedir=/var/lib/smalltalk \
    --enable-gtk=yes \
    --with-system-libffi \
    --with-system-libsigsegv \
    --with-readline \
    --with-tcl --with-tk \
    --with-x --without-emacs
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install

  # fix manpage symlink
  rm -f $pkgdir/usr/share/man/man1/gst-reload.1
  ln -s gst-load.1 $pkgdir/usr/share/man/man1/gst-reload.1
}
