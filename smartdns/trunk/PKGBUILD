# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=38.1
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('gcc-libs' 'openssl')
makedepends=('systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz")
sha512sums=('345010a9d4e43403e2ad068a8c6a0b871de8d4c524856c062909db61ac36b20988ae17b15be2f330589f93a7b7d521e885384c93987b81715e42a49798dec598')

build() {
  cd smartdns-Release$pkgver/src
  make
}

package() {
  cd smartdns-Release$pkgver
  make DESTDIR="$pkgdir" RUNSTATEDIR=/run SBINDIR=/usr/bin install

  rm -r "$pkgdir"/etc/init.d
}
