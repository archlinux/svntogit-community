# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=smbnetfs
pkgver=0.5.3a
pkgrel=5
pkgdesc="small C program that mounts Microsoft network neighborhood in single directory."
arch=('i686' 'x86_64')
url="http://smbnetfs.sourceforge.net/"
license=('GPL')
depends=('fuse' 'smbclient')
backup=('etc/smbnetfs/.smb/smb.conf'
	'etc/smbnetfs/.smb/smbnetfs.conf'
	'etc/smbnetfs/.smb/smbnetfs.auth'
	'etc/smbnetfs/.smb/smbnetfs.host')
install=smbnetfs.install
options=(docs)
source=(http://downloads.sourceforge.net/smbnetfs/$pkgname-$pkgver.tar.bz2
	rc-smbnetfs
	conf-smbnetfs
	smbnetfs.service)
md5sums=('66d1477ca981f5a07b4b21c6bdbdc442'
         'b6cff0ee75cafd739ad44216236b5e28'
         'e150c4cd894cc6ade11b94c95cf17ec4'
         '0fdd0dbaa5f16b1ecd00ec1ddcdfc223')

build() {
  cd $srcdir/$pkgname-$pkgver
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --docdir=/usr/share/doc/smbnetfs --with-gnome-keyring=no
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  mv $pkgdir/usr/share/doc/smbnetfs* $pkgdir/usr/share/doc/smbnetfs

  mkdir -p $pkgdir/etc/smbnetfs/.smb
  install -m0644 $srcdir/$pkgname-$pkgver/conf/* $pkgdir/etc/smbnetfs/.smb/
  install -Dm0644 $srcdir/$pkgname-$pkgver/README $pkgdir/usr/share/doc/smbnetfs/README
  install -Dm0755 $srcdir/rc-smbnetfs $pkgdir/etc/rc.d/smbnetfs
  install -Dm0644 $srcdir/conf-smbnetfs $pkgdir/etc/conf.d/smbnetfs
  install -Dm0644 $srcdir/smbnetfs.service $pkgdir/usr/lib/systemd/system/smbnetfs.service
  touch $pkgdir/etc/smbnetfs/.smb/smb.conf
}
