# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.99.1
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
makedepends=(smlnj)
provides=(sml)
install=smlnj.install
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.amd64-unix.tgz::${_url}boot.amd64-unix.tgz"
  "$pkgname-$pkgver-boot.x64-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('80782f3931c9e1c062abb6a53e520d4fc96d80eeb322c0651a400ffa5a8a2b45'
            'f65380050c7dcb7f604ab3d568f2a3911a1bc4b27f7ada107a062b6cfe8a7ec2'
            '1fe1121e833053be2a659840a464f0c4cb3fc3e7a5b0beff6acd0e0b21b5a488'
            '3702d67c5c2634d003c39480a23bf7a6bb7ae5d9b415fc24327ccc3c86db72b7'
            '3311c748e1b8bb6bfb2449f96c2da7f2da9de17461c36e3667dbd3af5709166d'
            '413c389afed2550c2e80965a5b38b09b3157d29f154def00a687327a86c733ff'
            'c5c57d2300b25ea90393cabb5c2fb0e157d8aa59c2adb4a73883bc5898da5d82'
            '883d70824c8bedf3a854d83e80730963ba572b796f06a5c1dfe9b1022ce0643f'
            '2b45d27353ed88cb9bd97b2589d705ddfac98fab0181a4a06e2e9c463fa11cfd'
            '89b529614bf1a25ad8789a432f7d0305a98b0aee40427b740bfaa9a599b5f61a'
            'f95fc81e82748fcceb3b60038091a6d48db8997d51e9c91eb623128a25fd8b39'
            '690b8514ffb6dc70485967e6f06f1018e6f8791bf6222aaf5322235e4d177479'
            '61ddf3574dfd01978c4723d1e0a5b0b2591bcaba0f01a8b063ce08099328b41b'
            '747206662c80ee817e7a449bd195088921f3cfa3b428ba088bed7c4a5be78448'
            'e81cdbfb95ab245bcb66d7c8a5868b84d936735c999b5d32cf6d4c219c37626a'
            '6ab342a70f4ee7a4455ee55fe515387932c7b9881f6eda28c4aca591f75e26d8'
            '7e49fd7b90d5b2640aa8f204d27434953fd0c0b1ac9ec10b6787f2bc80c488ba'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

build() {
  mkdir -p install
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  export CFLAGS="$CFLAGS -Wl,-z,relro,-z-now"
  export LDFLAGS="$LDFLAGS -Wl,-z,relro,-z-now"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh -default 64
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
