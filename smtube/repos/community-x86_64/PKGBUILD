# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Andreas Schnaiter <rc.poison@gmail.com>

pkgname=smtube
pkgver=18.1.0
pkgrel=1
pkgdesc="A small youtube browser"
arch=('x86_64')
url="https://www.smtube.org/"
license=('GPL')
depends=('qt5-script' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('qt5-tools')
optdepends=('mplayer: play videos with MPlayer'
            'smplayer: play videos with SMPlayer'
            'gnome-mplayer: play videos with GNOME MPlayer'
            'dragon: play videos with Dragon Player'
            'totem: play videos with Totem'
            'vlc: play videos with VLC'
            'mpv: play videos with MPV'
            'youtube-dl: download videos')
source=("https://downloads.sourceforge.net/project/${pkgname}/SMTube/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('d127b1cb8050c4b4d737f927e7782a34b39511e839460051f70329b9aad33c85660b7874d2f074524e8c442e9ed26f79b7a2d977d5622ceaf413330c77a99174')

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}/" install
}
