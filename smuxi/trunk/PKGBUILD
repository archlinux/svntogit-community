# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Madek <gonzaloseguel@gmail.com>
# Contributor: Marcelo Cavalcante <marcelo@marcelocavalcante.net>
# Contributor: Jens Kleikamp <jens@codes-concepts.com>
# Contributor: Philipp Gildein <rmbl@openspeak-project.org>

pkgbase=smuxi
pkgname=('smuxi-server' 'smuxi')
pkgver=1.0.5
pkgrel=2
pkgdesc="User-friendly and cross-platform IRC client for sophisticated users for GNOME/GTK+"
arch=('any')
url="https://smuxi.im/"
license=('GPL')
makedepends=('notify-sharp' 'log4net' 'nini' 'gtkspell' 'stfl' 'intltool')
options=('!emptydirs')
source=(https://smuxi.im/jaws/data/files/$pkgbase-$pkgver.tar.gz{,.asc})
validpgpkeys=('ABE195E150A8DBE7809D3F427127E5ABEEF946C8') # Mirco Bauer
md5sums=('af1a0c7d4a60f97063aa119531942fd1'
         'SKIP')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --enable-frontend-stfl \
              --with-vendor-package-version="Arch Linux $pkgver-$pkgrel"
  LANG=C make -j1
}

package_smuxi-server() {
  pkgdesc+=" (common files and server)"
  depends=('log4net' 'nini')
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # Split frontend
  [[ -d $srcdir/frontend ]] && rm -r "$srcdir/frontend/"
  mkdir "$srcdir"/frontend
  mv "$pkgdir"/usr/bin/smuxi-frontend-{gnome,stfl} \
     "$pkgdir"/usr/lib/pkgconfig/smuxi-frontend.pc \
     "$pkgdir"/usr/lib/smuxi/smuxi-frontend* \
     "$pkgdir"/usr/share/{appdata,applications,icons} \
     "$pkgdir"/usr/share/man/man1/smuxi-frontend* \
     "$srcdir/frontend/"
}

package_smuxi() {
  pkgdesc+=" (frontend)" 
  depends=('smuxi-server' 'notify-sharp' 'gtkspell' 'stfl')
  cd "$srcdir/frontend"

  mkdir -p "$pkgdir"/usr/{bin,lib/{pkgconfig,smuxi},share/man/man1}
  mv smuxi-frontend-{gnome,stfl} "$pkgdir/usr/bin"
  mv smuxi-frontend.pc "$pkgdir/usr/lib/pkgconfig"
  mv smuxi-frontend*.1 "$pkgdir/usr/share/man/man1"
  mv smuxi-frontend* "$pkgdir/usr/lib/smuxi"
  mv {appdata,applications,icons} "$pkgdir/usr/share"
}
