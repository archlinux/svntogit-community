# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=snd
pkgver=21.5
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('glibc' 'glu' 'gmp' 'gsl' 'libglvnd' 'libmpc' 'libx11' 'libxext'
'libxpm' 'libxt' 'mpfr' 'openmotif' 'ruby')
# TODO: maybe replace mpg123 with mpg321
makedepends=('alsa-lib' 'fftw' 'flac' 'gendesk' 'jack' 'ladspa' 'libpulse'
'libsamplerate' 'mpg123' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'pulseaudio: For pulseaudio support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ba7d8b8257af9c26b9ec044853da4632cc4d058fda1c3952513c973429972c011cca98821d8e8e1aa00198d94d02296f18776274b57a9512eec242190b91c950')
b2sums=('9a45a0511af51ac8b4a7ba2f7dfb2432e1537d991009b546edf79d2c74750092d4a6f5cc8a3c9cf2ac1353071fd0b98ff2622ae9bd291590cd8e4b33d86c95aa')

prepare() {
  cd "${pkgname}-${pkgver}"
  # create XDG desktop file, as the default is broken
  gendesk -f -n \
          --pkgname "${pkgname}" \
          --name "${pkgname}" \
          --exec "${pkgname}" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  depends+=('libasound.so' 'libfftw3.so' 'libjack.so' 'libsamplerate.so')
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 *.html -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 pix/*.png -t "${pkgdir}/usr/share/doc/${pkgname}/html/pix"
}

# vim:set ts=2 sw=2 et:
