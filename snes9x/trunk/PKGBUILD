# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=snes9x
pkgname=('snes9x' 'snes9x-gtk')
pkgver=1.55
pkgrel=2
arch=('x86_64')
url="http://www.snes9x.com/"
license=('custom')
makedepends=('intltool' 'nasm' 'mesa' 'libpulse'  'libpng' 'sdl' 'gtk2' 'libxv'
             'adwaita-icon-theme' 'minizip')
source=(https://github.com/snes9xgit/snes9x/archive/$pkgver.tar.gz snes9x-buildflags.patch)
sha1sums=('366d14f77f70e6eb7b4fac24cde6d79fcfc0add3'
          'a1b68aaa710ee261a3e9ee1a80e9a82864f14374')

prepare() {
  cd $pkgbase-$pkgver
  # Respect build flags (rmpfusion)
  patch -p1 -i ../snes9x-buildflags.patch
  cd unix
  autoreconf -vi
}

build() {
  cd ${pkgbase}-${pkgver}/unix
  ./configure --prefix=/usr --enable-netplay
  make

  cd ../gtk
  ./autogen.sh
  ./configure --prefix=/usr --with-netplay --with-opengl
  make
}

package_snes9x() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System"
  depends=('libpng' 'libxext' 'libsm' 'libxv' 'minizip')

  cd ${pkgbase}-${pkgver}/unix
  install -D -m755 snes9x "${pkgdir}/usr/bin/snes9x"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_snes9x-gtk() {
  pkgdesc="A portable Emulator for the Super Nintendo Entertainment System - GTK version"
  license=('custom' 'LGPL')
  depends=('sdl' 'libpulse' 'gtk2' 'libxv' 'adwaita-icon-theme' 'minizip')

  cd ${pkgbase}-${pkgver}/gtk
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -D -m644 ../docs/{snes9x.conf.default,{control-inputs,controls,snapshots}.txt} \
    "${pkgdir}/usr/share/doc/${pkgname}/"
  install -D -m644 "${srcdir}/${pkgbase}-${pkgver}/docs/snes9x-license.txt" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
