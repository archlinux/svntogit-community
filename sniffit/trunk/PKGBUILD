# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>

pkgname=sniffit
pkgver=0.3.7.beta
pkgrel=16
pkgdesc="Very good packet sniffer for unix with ncurses interactive mode"
arch=('x86_64')
url="http://packages.ubuntu.com/source/sniffit"
#seems the website are out, ubuntu have all sources
#url=http://reptile.rug.ac.be/~coder/sniffit/sniffit.html"
license=('BSD')
depends=('ncurses' 'libpcap')
makedepends=('libtool')
source=("http://archive.ubuntu.com/ubuntu/pool/universe/s/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
	'sniffit-fix.patch')
md5sums=('2697cc18878480199fe6db1e61134d5a'
         'e66e45dac8fd088accd62160cf8b569f')

build() {
  cd "${srcdir}"
  patch -Np0 < "$srcdir"/sniffit-fix.patch

  cd "${srcdir}/${pkgname}.${pkgver}"

  rm -f config.sub config.guess
  cp /usr/share/libtool/build-aux/config.{guess,sub} .

  ./configure --prefix=/usr --no-recursion --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}.${pkgver}"
  install -dm0755 -g root -o root "$pkgdir"/usr/{bin,share/licenses/${pkgname}}
  install -Dm0744 -g root -o root sniffit "$pkgdir"/usr/bin/sniffit
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}

  # docs
  install -Dm0644 sniffit.5 "$pkgdir"/usr/share/man/man5/sniffit.5
  install -Dm0644 sniffit.8 "$pkgdir"/usr/share/man/man8/sniffit.8

  for i in BETA-TESTING PLUGIN-HOWTO README.FIRST sniffit-FAQ; do
    install -Dm0644 $i "$pkgdir"/usr/share/doc/sniffit/$i
  done
  for i in dns_plugin.plug dummy_plugin.plug sample_config_file; do
    install -Dm0644 $i "$pkgdir"/usr/share/doc/sniffit/examples/$i
  done
}
