# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>

pkgname=sniffit
pkgver=0.3.7.beta
pkgrel=11
pkgdesc="Very good packet sniffer for unix with ncurses interactive mode"
arch=('i686' 'x86_64')
url="http://packages.ubuntu.com/source/sniffit"
#seems the website are out, ubuntu have all sources
#url=http://reptile.rug.ac.be/~coder/sniffit/sniffit.html"
license=('GPL')
depends=('ncurses' 'libpcap')
makedepends=('libtool')
source=("http://archive.ubuntu.com/ubuntu/pool/universe/s/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz"
	'sniffit-fix.patch')
md5sums=('2697cc18878480199fe6db1e61134d5a'
         'e66e45dac8fd088accd62160cf8b569f')

build() {
  cd "${srcdir}"
  patch -Np0 < ${srcdir}/sniffit-fix.patch

  cd "${srcdir}/${pkgname}.${pkgver}"

  rm -f config.sub config.guess
  cp  /usr/share/libtool/config/config.{guess,sub} .

  ./configure --prefix=/usr --no-recursion
  make
}

package() {
  cd "${srcdir}/${pkgname}.${pkgver}"
  install -d -m0755 -g root -o root ${pkgdir}/usr/{sbin,share/licenses/${pkgname}}
  install -D -m0744 -g root -o root sniffit ${pkgdir}/usr/sbin/sniffit
  install -D -m0644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}
}
