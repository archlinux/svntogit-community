# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>

pkgname=snort
pkgver=2.9.1
pkgrel=1
pkgdesc='A lightweight network intrusion detection system.'
arch=('i686' 'x86_64')
url='http://www.snort.org'
license=('GPL')
depends=('libdaq' 'libdnet' 'libpcap' 'pcre' 'zlib')
makedepends=('ca-certificates')
backup=('etc/conf.d/snort'
        'etc/snort/snort.conf'
        'etc/snort/threshold.conf'
        'etc/snort/confreference.config'
        'etc/snort/classification.config')
options=('!makeflags' '!libtool')
install='snort.install'
source=('http://www.snort.org/downloads/1107'
        'snort'
        'snort.conf.d')
md5sums=('f11e01c7946b7b4e07e6e98bb1645fb5'
         '361b8b9e40b9af0164f6b3e3da2e8277'
         'b4fb8a68490589cd34df93de7609bfac')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc/snort --with-libpcap-includes=/usr/include/pcap \
              --without-mysql --without-postgresql --without-oracle --without-odbc --enable-zlib \
              --enable-ipv6
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  mkdir -p "${pkgdir}/"{etc/rc.d,etc/snort/rules}

  install -d -m755 "${pkgdir}/var/log/snort"
  install -D -m644 etc/{*.conf*,*.map} "${pkgdir}/etc/snort/"
  install -D -m644 "${srcdir}/snort.conf.d" "${pkgdir}/etc/conf.d/snort"
  install -D -m755 "${srcdir}/snort" "${pkgdir}/etc/rc.d/snort"

  sed -i 's#/usr/local/lib/#/usr/lib/#' "${pkgdir}/etc/snort/snort.conf"
}
