# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=soapyrtlsdr
_pkgname=SoapyRTLSDR
pkgver=0.3.2
_gitver=soapy-rtl-sdr-$pkgver
pkgrel=1
pkgdesc='Soapy SDR plugin for RTL-SDR devices'
arch=('x86_64')
url='https://github.com/pothosware/SoapyRTLSDR'
license=('MIT')
depends=('soapysdr' 'rtl-sdr')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha512sums=('f8279afebb251d72fea14d89889835be5ed8109c1400fdcc785c1ec120e978ae7b1de53cc84d16bf609de4cca2ddbc9d432d046e4c6967e1e494fd5571d46cc2')

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/$_pkgname-$_gitver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
