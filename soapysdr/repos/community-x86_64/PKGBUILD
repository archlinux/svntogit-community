# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=soapysdr
_pkgname=SoapySDR
pkgver=0.8.0
_gitver=soapy-sdr-$pkgver
pkgrel=1
pkgdesc='Vendor and platform neutral SDR support library'
arch=('x86_64')
url='https://github.com/pothosware/SoapySDR'
license=('Boost')
depends=('python')
makedepends=('cmake' 'swig' 'doxygen' 'graphviz')
optdepends=('soapyairspy: Airspy backend'
            'soapyaudio: Audio devices backend'
            'soapybladerf: BladeRF backend'
            'soapyhackrf: HackRF backend'
            'soapynetsdr: NetSDR backend'
            'soapyosmo: OsmoSDR backend'
            'soapyremote: SoapySDR remote support'
            'soapyrtlsdr: rtl-sdr backend'
            'soapyuhd: UHD backend')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha512sums=('309420c0212a20338b71dae1e939f9658e805604d04fbcd39f6eb6b42fcbb12d4c385ef6a8704d979e5ead5a98cbf8362280cc62094ca0891581b87db6e0e892')

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
    -DSOAPY_SDR_EXTVER=ARCH \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install

  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership docs/html "$pkgdir"/usr/share/doc/$pkgname
}
