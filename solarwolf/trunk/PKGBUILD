# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=solarwolf
pkgver=1.5
pkgrel=5
arch=('any')
pkgdesc="Scramble through 60 levels collecting space boxes while dodging obstacles"
url="http://www.pygame.org/shredwheat/solarwolf/"
depends=('python-pygame')
license=('LGPL')
source=(http://www.pygame.org/shredwheat/$pkgname/$pkgname-$pkgver.tar.gz
        solarwolf.desktop)
md5sums=('52fbed0a33c3c4c08d9dc0e533f90f8c'
         '9d0b8c95d84eacf86a1fb1f3249d97bb')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' solarwolf.py
  sed -i 's|/usr/share/games/solarwolf|/usr/share/solarwolf|g' solarwolf.py
  sed -i 's|/usr/lib/games/solarwolf|/usr/lib/solarwolf|g' solarwolf.py
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -Dm755 solarwolf.py $pkgdir/usr/bin/solarwolf

  install -dm755 $pkgdir/usr/{lib/solarwolf,share/solarwolf/data/{audio,music}}
  install -m644 data/*.{png,gif,txt,pal} $pkgdir/usr/share/solarwolf/data
  install -m644 data/audio/* $pkgdir/usr/share/solarwolf/data/audio
  install -m644 data/music/* $pkgdir/usr/share/solarwolf/data/music
  install -m644 code/* $pkgdir/usr/lib/solarwolf

  install -Dm644 dist/solarwolf.png $pkgdir/usr/share/pixmaps/solarwolf.png
  install -Dm644 $srcdir/solarwolf.desktop $pkgdir/usr/share/applications/solarwolf.desktop
}
