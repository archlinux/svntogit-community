# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=solr
pkgver=8.11.0
pkgrel=1
pkgdesc="Open source enterprise search platform built on Apache Lucene"
arch=('any')
url="https://lucene.apache.org/solr/"
license=('Apache')
depends=('bash' 'java-runtime>=8')
makedepends=('ant' 'ivy' 'java-environment>=8' 'strip-nondeterminism')
backup=("etc/${pkgname}/server/jetty-http.xml"
        "etc/${pkgname}/server/jetty-https.xml"
        "etc/${pkgname}/server/jetty-https8.xml"
        "etc/${pkgname}/server/jetty-ssl.xml"
        "etc/${pkgname}/server/jetty.xml"
        "etc/${pkgname}/server/webdefault.xml"
        "etc/${pkgname}/${pkgname}.in.sh"
        "etc/${pkgname}/${pkgname}.xml"
        "etc/${pkgname}/zoo.cfg")
source=("https://archive.apache.org/dist/lucene/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tgz"{,.asc}
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha512sums=('cf40198276e5e282287bd7d96ba4f89df8d54b9542b960e82763ba06e63c070d33cc49a90efd4fd4f33177c1af5e656f7038d20bce64c1dd0bc71faf8309e7ad'
            'SKIP'
            'd9a5dcc7a30bf449abf9028bc4aa88e196c953d82b884661ec3191d4a06ec50b106c01ecdcd225e3526ab4f4ce5634d6dcd4a03f1e128fdd4c26febb1b742532'
            '06e5e40b96d2b6668790e4b166fc2867b9e694a2c72fd57eec702526e009b8b0495acbe16a5a27e259827477f4783ce87742f1f806254d8a2baec23b0b317058'
            '97252d1ba1e4e211a6b5a038981cbc9d6663a0d7a980b23484f838eebb2f8194571a3f34ea6b92ed0efd4b7f862e020b5e3e8478ad1de1c6832e232d0b20ff6d')
b2sums=('6b3741d78a1abbe78209801bf37789d0c2cb667cf0546021a198b05c38041d60530d57db30bbac79e52aa2ec75be126d9c30516efcbfde7337d02a8ab81bd5d5'
        'SKIP'
        'f970329fad0358cb19af9b9337047612d5949af8aea2dc7acd6c8424ba494644940d22f46b98a9cf1362ea2e5d966b863907de35e7c0edc2e48f7b0ed00473b7'
        '60ff37059a4ab8362551518d56ee105e7d19199727605d5ad6f3236bd31dde1cc5fa37ffa37009820ee3115da36ae64df4754454cef1db51d1c13cac039245cb'
        '37ab5d9af1da1178fcc58ce39654fdecb842b24d4e2264a2eec95c10223e7d003f27a6b2957a267a17ce0fa72258d96642d5b909963576a5a7d13e4e05c47a51')
# list of trusted signing keys: https://downloads.apache.org/lucene/KEYS
validpgpkeys=('2085660D9C1FCCACC4A479A3BF160FF14992A24C'  # Ishan Chattopadhyaya <ishan@apache.org>
              'E58A6F4D5B2B48AC66D5E53BD4F181881A42F9E6'  # Ignacio Vera (CODE SIGNING KEY) <ivera@apache.org>
              '81D3EB0408B4E1EB10AF443BA4F4C886B29BC2F4'  # Alan Woodward (CODE SIGNING KEY) <romseygeek@apache.org>
              '86EDB9C33B8517228E88A8F93E48C0C6EF362B9E'  # Mike Drob (CODE SIGNING KEY) <mdrob@apache.org>
              '38DA0C3CE8181703A08E4D57377C3BA26AD29C0A'  # Bruno Roustant <broustant@apache.org>
              '50E3EE1C91C7E0CB4DFB007B369424FC98F3F6EC'  # Houston Paul Putman IV (CODE SIGNING KEY) <houston@apache.org>
              '902CC51935C140BF820230961FD5295281436075'  # Jason Gerlowski (CODE SIGNING KEY) <gerlowskija@apache.org>
              '7D8D90F8F64F23077AC87CF7CB77CB79928BB0EC'  # Atri Sharma <atri@apache.org>
              'CFCE5FBB920C3C745CEEE084C38FF5EC3FCFDB3E'  # Noble Paul (CODE SIGNING KEY) <noble@apache.org>
              'FBC25D7E1712025294FE66590A6AA179B9BBF45E'  # Timothy Potter (CODE SIGNING KEY) <thelabdude@apache.org>
              '9722F25F650057E26C803B60A6D064D833B3A969'  # Mayya Sharipova (CODE SIGNING KEY) <mayya@apache.org>
              'E6E21FFCDCEA14C95910EA65051A0FAF76BC6507'  # Adrien Grand (CODE SIGNING KEY) <jpountz@apache.org>
)

prepare() {
  cd "$pkgname-$pkgver"
  ant ivy-bootstrap
  rm -rvf "${pkgname}/bin/init.d"
}

build() {
  cd "$pkgname-$pkgver"
  cd "${pkgname}"
  ant compile
  ant server
  ant dist
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . \
    -type f \
    -iname "*.jar" \
    -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

# TODO: make org.apache.solr.cloud.MetricsHistoryIntegrationTest.testGet pass
# seemingly requires lucene to be built as well X_X
# check() {
#   cd "$pkgname-$pkgver"
#   cd "${pkgname}"
#   ant test
# }

package() {
  cd "$pkgname-$pkgver"
  # removing unneeded sources and build artifacts
  rm -rvf ${pkgname}/contrib/*/src
  find . -type f \( -iname "*build.xml" -o -iname "*ivy.xml" \) -delete
  # remove unneeded and OS specific scripts
  rm -rvf "${pkgname}/bin/"{install_solr_service.sh,solr.cmd,solr.in.cmd}

  # make target script executable
  chmod 755 "${pkgname}/bin/${pkgname}"
  # symlink script into PATH
  install -vdm 755 "${pkgdir}/usr/bin/"
  ln -sv "/usr/share/${pkgname}/bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # configuration
  install -vDm 644 "${pkgname}/bin/${pkgname}.in.sh" \
    -t "${pkgdir}/etc/${pkgname}"
  install -vDm 644 "${pkgname}/server/etc/"*.xml \
    -t "${pkgdir}/etc/${pkgname}/server"
  install -vDm 644 "${pkgname}/server/${pkgname}/${pkgname}.xml" \
    -t "${pkgdir}/etc/${pkgname}/"
  install -vDm 644 "${pkgname}/server/${pkgname}/zoo.cfg" \
    -t "${pkgdir}/etc/${pkgname}/"
  install -vdm 755 "${pkgdir}/usr/share/${pkgname}"

  # copy application
  cp -rvL "${pkgname}/"{bin,contrib,dist,docs,example,licenses,server} \
    "${pkgdir}/usr/share/${pkgname}"

  # symlink configuration into place
  ln -svf "/etc/${pkgname}/${pkgname}.in.sh" \
    "${pkgdir}/usr/share/${pkgname}/"

  install -vdm 750 "${pkgdir}/var/lib/${pkgname}"
  ln -svf "/etc/${pkgname}/${pkgname}.xml" \
    "${pkgdir}/var/lib/${pkgname}"
  ln -svf "/etc/${pkgname}/zoo.cfg" \
    "${pkgdir}/var/lib/${pkgname}"
  for config in {jetty,jetty-{http,https,https8,ssl},webdefault}.xml; do
    ln -svf "/etc/${pkgname}/server/${config}" \
      "${pkgdir}/usr/share/${pkgname}/server/etc/${config}"
  done
  # logs directory
  install -vdm 750 "${pkgdir}/var/log/${pkgname}"
  # docs
  install -vDm 644 "${pkgname}/"{CHANGES,LUCENE_CHANGES,NOTICE,README}.txt \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "../${pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 "../${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
