# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=4.5
pkgrel=3
pkgdesc="A viewer and analyser of music audio files."
arch=(x86_64)
url="https://www.sonicvisualiser.org/"
license=(GPL2)
groups=(pro-audio)
depends=(bzip2 gcc-libs glibc hicolor-icon-theme libglvnd libmad libx11
opusfile qt5-base qt5-svg speex)
makedepends=(alsa-lib capnproto fftw libfishsound libid3tag liblo liblrdf
liboggz libpulse libsamplerate libsndfile meson portaudio rubberband serd sord)
provides=(vamp-host)
options=(debug)
source=(https://github.com/$pkgname/$pkgname/releases/download/sv_v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('eca7f7b2a56a4c70f38c8fafdd20d5c0e773dc1a99a75189998dd1f50ae777df4999e1a4b3431f9202edfb8ce2686d05cf6448493c263c52ab5f068169a7385d')
b2sums=('a5e39a755e991663390370c06ce5d54b2ff9422081f8b9046097645f1f747972d270233a0d4fe05d4e6adde5239ca6d2d0210a8a67d9ef6ede60089471ce6452')

build() {
  arch-meson build $pkgname-$pkgver
  ninja -C build
}

package() {
  depends+=(libasound.so libcapnp.so libfftw3.so libfftw3f.so libfishsound.so
  libid3tag.so libkj.so liblo.so liblrdf.so liboggz.so libportaudio.so
  libpulse.so librubberband.so libsamplerate.so libserd-0.so libsndfile.so
  libsord-0.so)

  DESTDIR="$pkgdir" meson install -C build
  # samples
  install -vDm 644 $pkgname-$pkgver/samples/* -t "$pkgdir/usr/share/$pkgname/samples"
  # templates
  install -vDm 644 $pkgname-$pkgver/templates/* -t "$pkgdir/usr/share/$pkgname/templates"
  # docs
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,CITATION,README{,_OSC}.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
