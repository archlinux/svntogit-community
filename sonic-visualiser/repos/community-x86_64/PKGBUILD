# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

_download_id=2755
pkgname=sonic-visualiser
pkgver=4.2
pkgrel=1
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('bzip2' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd'
'libmad' 'libx11' 'opusfile' 'qt5-base' 'qt5-svg' 'speex')
makedepends=('alsa-lib' 'capnproto' 'fftw' 'libfishsound' 'libid3tag' 'liblo'
'liblrdf' 'liboggz' 'libpulse' 'libsamplerate' 'libsndfile'
'portaudio' 'rubberband' 'serd' 'sord')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_download_id}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('186d723c74d3b2865588370d9ff3afaf9f5356957e670483ea52303477c6acd17bb99b44c05db0fe4c0dc9757ba17c5e25b7b2fda5a15e85d9e193322ce8f2c3')
b2sums=('8cc92b2cd932d3c9f014161635cd767aa6ffa671217fca7c63ec6b882ae46cc1ed790e108e9be59d536aab0cbead726057987c2920c501d451fd353b2bf83018')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libasound.so' 'libcapnp.so' 'libfftw3.so' 'libfftw3f.so'
  'libfishsound.so' 'libid3tag.so' 'libkj.so' 'liblo.so' 'liblrdf.so'
  'liboggz.so' 'libportaudio.so' 'libpulse.so' 'librubberband.so'
  'libsamplerate.so' 'libserd-0.so' 'libsndfile.so' 'libsord-0.so')
  cd "${pkgname}-${pkgver}"
  # INSTALL_ROOT used in favor of DESTDIR
  # https://sourceforge.net/p/sv1/bugs/254
  make INSTALL_ROOT="${pkgdir}/" install
  # xdg desktop file
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
  # samples
  install -vDm 644 samples/* -t "${pkgdir}/usr/share/${pkgname}/samples"
  # templates
  install -vDm 644 templates/* -t "${pkgdir}/usr/share/${pkgname}/templates"
  # docs
  install -vDm 644 {CHANGELOG,CITATION,README{,_OSC}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
