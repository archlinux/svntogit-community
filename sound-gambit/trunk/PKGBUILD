# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sound-gambit
pkgver=0.6
pkgrel=1
pkgdesc="Audio File Peak Limiter"
arch=('x86_64')
url="https://github.com/x42/sound-gambit"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc')
makedepends=('libsndfile')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('6a827e6ec94d4cfe5934ad7fd6d0b1cefa2f6e2f973501830193f879a20d71f857a592721c91f8cf310f330cb4d3c18b7f7268440adce7f811d0796094fb654e')
b2sums=('d7f1ff257db311ab489aa7fbce5b96a96206d23e4ed01d5f8ec57b3fbd66344fa9e9b144e5f67397d1773918046f0c408dda64efb03a5f0f4d4790dfe35dac5b')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make PREFIX='/usr' DESTDIR="$pkgdir/" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
