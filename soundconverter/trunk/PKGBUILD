# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=soundconverter
pkgver=2.1.5
pkgrel=1
pkgdesc="A simple sound converter application for GNOME"
arch=('any')
license=('GPL3')
url="http://soundconverter.org/"
depends=('python2-gconf' 'python2-libgnome' 'gstreamer0.10-python' 
  'hicolor-icon-theme' 'gstreamer0.10-ugly-plugins' 
  'gstreamer0.10-good-plugins' 'desktop-file-utils')
makedepends=('perl-xml-parser' 'intltool')
optdepends=('gstreamer0.10-plugins: additional codec support')
install=soundconverter.install
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.xz")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2  ./configure --prefix=/usr 

  make
  sed -i 's#/usr/bin/python#/usr/bin/python2#' bin/soundconverter
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

md5sums=('28639e204b9952d401e3ebad8d5b3351')
