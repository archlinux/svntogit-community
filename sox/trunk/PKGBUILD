# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: mysta

pkgname=sox
# using a git snapshot as 14.4.2 has many unfixed security vulns
pkgver=14.4.2
pkgrel=8
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('x86_64')
url="http://sox.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm')
makedepends=('autoconf-archive' 'libao' 'libmad' 'libid3tag' 'wavpack' 'libpulse' 'opusfile' 'twolame')
checkdepends=('time')
optdepends=('libao: for ao plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin'
            'libpulse: for pulse plugin'
            'opusfile: for opus plugin'
            'twolame: for mp3 plugin')
_commit=("42b3557e13e0fe01a83465b672d89faddbe65f49")
source=("https://sourceforge.net/code-snapshots/git/s/so/sox/code.git/sox-code-${_commit}.zip")
sha512sums=('4868f9b77141b7e2caf2571ceb68a48bd744732e72f63ed787f8e0b9fe39b77de4e998b32d99c2500e10b017a1fd1a3379f4f9f8b67db6093b85d02cb3447934')

prepare() {
  cd sox-code-${_commit}
  # don't overwrite cflags
  sed -i -e 's:CFLAGS="-g":CFLAGS="$CFLAGS -g":' configure.ac
  autoreconf -vfi
}

build() {
  cd sox-code-${_commit}
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-dyn-default \
              --with-distro="Arch Linux"
  make
}

# Disabled until broken tests are fixed
#check() {
#  cd sox-code-${_commit}
#  make bindir=. installcheck
#}

package() {
  cd sox-code-${_commit}
  make DESTDIR="${pkgdir}" install
  install -vDm 644 {AUTHORS,ChangeLog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
