# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
# Contributor: ridikulus_rat <the.ridikulus.rat@gmail.com>

pkgname=spacefm
pkgver=0.9.3
pkgrel=1
pkgdesc='Multi-panel tabbed file manager'
arch=('i686' 'x86_64')
url='http://ignorantguru.github.com/spacefm/'
license=('GPL3')
install=$pkgname.install
depends=('gtk3' 'shared-mime-info' 'desktop-file-utils' 'startup-notification'
         'udev' 'bash')
makedepends=('intltool' 'gettext')
optdepends=('lsof: device processes'
            'wget: plugin download'
            'gksu: perform as root functionality'
            'udevil: mount as non-root user and mount networks'
            'udisks: mount as non-root user'
            'udisks2: mount as non-root user')
source=(https://github.com/IgnorantGuru/spacefm/archive/$pkgver.tar.gz)
md5sums=('6bbf41bc97a46a797500dbf0ae359971')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-pixmaps \
    --with-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
