# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: IgnorantGuru http://igurublog.wordpress.com/contact-ignorantguru/
# Contributor: ridikulus_rat <the.ridikulus.rat@gmail.com>

pkgname=spacefm
pkgver=0.9.4
pkgrel=3
pkgdesc='Multi-panel tabbed file manager'
arch=('i686' 'x86_64')
url='http://ignorantguru.github.com/spacefm/'
license=('GPL3')
install=$pkgname.install
depends=('gtk2' 'shared-mime-info' 'desktop-file-utils' 'startup-notification'
         'systemd' 'bash')
makedepends=('intltool' 'gettext')
optdepends=('lsof: device processes'
            'wget: plugin download'
            'gksu: perform as root functionality'
            'udevil: mount as non-root user and mount networks'
            'udisks: mount as non-root user'
            'udisks2: mount as non-root user')
source=(https://github.com/IgnorantGuru/spacefm/archive/$pkgver.tar.gz
        spacefm-0.9.4-glib-2.41.patch)
md5sums=('daeee7dcccea33d6258a0a9d783470c4'
         '1ec85afcb4fc881a8e876b3cf60c7028')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../spacefm-0.9.4-glib-2.41.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-pixmaps
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
