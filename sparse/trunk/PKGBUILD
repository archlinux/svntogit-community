# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Omar Sandoval <osandov AT osandov DOT com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski AT archlinux.org>
# Contributor: Dan McGee <dpmcgee AT gmail.com>

pkgname=sparse
pkgver=0.6.4
pkgrel=2
pkgdesc='Semantic parser for C'
arch=('x86_64')
url='http://sparse.wiki.kernel.org/'
license=('MIT')
depends=(
  'perl'
  'libxml2'
  'sqlite'
)
makedepends=(
  'git'
  'llvm'
  'gtk3'
)
optdepends=('gtk3: for test-inspect')
_commit='c4706aa764f3ae68258ba60be6325a5662900362'
source=("git+https://git.kernel.org/pub/scm/devel/sparse/sparse.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd sparse

  git describe --tags | sed 's/^v//'
}

build() {
  make -C sparse
}

check() {
  make -C sparse check
}

package() {
  cd sparse

  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
