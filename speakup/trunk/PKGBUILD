# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
_kernver='2.6.34-ARCH'
pkgname=speakup
pkgver=3.1.5
pkgrel=2
pkgdesc='Kernel-based screenreader for the Linux console'
arch=('i686' 'x86_64')
url="http://linux-speakup.org/"
license=('GPL2')
source=("ftp://linux-speakup.org/pub/speakup/$pkgname-$pkgver.tar.bz2"
            kmalloc.patch)
install='speakup.install'
depends=('speakup-utils>=3.1.0' 'kernel26>=2.6.34' 'kernel26<2.6.35')
makedepends=('kernel26-headers>=2.6.34')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  # -p2 because we're already under src/
  patch -N -p2 < "$srcdir/kmalloc.patch" || return 1

  make KERNELDIR="/lib/modules/${_kernver}/build" || return 1
  install -d "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname" \
    || return 1
  install -d "$pkgdir/usr/share/doc/$pkgname" || return 1
  install -m 0644 speakup*.ko \
    "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname" || return 1
  install -m 0644 ../doc/* "$pkgdir/usr/share/doc/$pkgname" || return 1
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" \
    "$startdir/speakup.install"
}

md5sums=('ff80980c75cd5ebcb23ba23d5716811e'
         'daf7c8563488c52990c26c5896e0d304')
