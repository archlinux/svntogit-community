# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=speed-dreams-data
pkgver=2.0.0a3
_pkgver=2.0.0-a3-r3412
pkgrel=2
pkgdesc="A racing simulator with rich graphics and physics (data files)"
arch=('any')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=()
makedepends=('sdl' 'freealut' 'freeglut' 'libpng' 'libxmu' 'libxrandr' 'plib' 'libjpeg' 'zlib' 'enet' 'cmake' 'p7zip')
source=("http://downloads.sourceforge.net/sourceforge/speed-dreams/speed-dreams-${_pkgver}-src.tar.7z")
md5sums=('659837e8866c59c85279c31fcaba4144')

build() {
  7z x speed-dreams-${_pkgver}-src.tar.7z
  tar xf speed-dreams-${_pkgver}-src.tar

  cd speed-dreams-${_pkgver}-src

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSD_BINDIR=/usr/bin \
    -DSD_DATADIR=/usr/share/speed-dreams-2 \
    -DSD_LIBDIR=/usr/lib/speed-dreams-2
}

package() {
  cd "$srcdir/speed-dreams-${_pkgver}-src"
  
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/bin"
  rm -r "${pkgdir}/usr/lib"
}

# vim:set ts=2 sw=2 et:
