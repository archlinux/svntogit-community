# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=speed-dreams-data
pkgver=2.0.0b1
_pkgver=2.0.0-b1-r3937
pkgrel=1
pkgdesc="A racing simulator with rich graphics and physics (data files)"
arch=('any')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=()
makedepends=('sdl' 'freealut' 'freeglut' 'libpng' 'libxmu' 'libxrandr' 'plib' 'libjpeg' 'zlib' 'enet' 'cmake' 'p7zip')
source=("http://downloads.sourceforge.net/sourceforge/speed-dreams/speed-dreams-${_pkgver}-src.tar.7z")
md5sums=('5299fe23aec3641827039ae3c69de460')

build() {
  7z x speed-dreams-${_pkgver}-src.tar.7z
  tar xf speed-dreams-${_pkgver}-src.tar

  cd speed-dreams-${_pkgver}-src

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSD_BINDIR=/usr/bin \
    -DSD_DATADIR=/usr/share/speed-dreams-2 \
    -DSD_LIBDIR=/usr/lib/speed-dreams-2 \
    -DOPTION_OFFICIAL_ONLY:BOOL=ON
}

package() {
  cd "$srcdir/speed-dreams-${_pkgver}-src"
  
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/bin"
  rm -r "${pkgdir}/usr/lib"
  rm -r "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
