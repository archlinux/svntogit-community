# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=speed-dreams-data
_pkgname=speed-dreams
pkgver=2.1.0
_pkgver=2.1.0-r5799
pkgrel=1
pkgdesc="A racing simulator with rich graphics and physics (data files)"
arch=('any')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=()
makedepends=('sdl' 'freealut' 'freeglut' 'libpng' 'libxmu' 'libxrandr' 'plib' 'libjpeg' 'zlib' 'enet' 'cmake' 'tar' 'libvorbis' 'glu')
noextract=("${_pkgname}-src-base-${_pkgver}.tar.xz"
           "${_pkgname}-src-unmaintained-${_pkgver}.tar.xz"
           "${_pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
           "${_pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
           "${_pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz")
source=("http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-base-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-unmaintained-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${_pkgname}/${_pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz")
md5sums=('4e45e6ebf35b5afe52be4c4d24e670a7'
         '293ff3117d9b82d7a509bb935cdd4d4b'
         '1f4f32f726ee27fce30bbcd91c611b7f'
         '09efa3454b1b0812169b5714821279e8'
         '71f4be0d2b5589c32deb9b3686e69128')

prepare() {
  # extract these manually as bsdtar can't handle these
  tar xf "${_pkgname}-src-base-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-unmaintained-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
  tar xf "${_pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz"
}

build() {
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSD_BINDIR=/usr/bin \
    -DSD_DATADIR=/usr/share/speed-dreams-2 \
    -DSD_LIBDIR=/usr/lib/speed-dreams-2 \
    -DOPTION_OFFICIAL_ONLY=ON
}

package() {
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/bin"
  rm -r "${pkgdir}/usr/lib"
  rm -r "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
