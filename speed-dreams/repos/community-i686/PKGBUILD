# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adrián Chaves Fernández, aka Gallaecio <adriyetichaves@gmail.com>
# Contributor: Shahar Weiss <sweiss4@gmx.net> (build() code from his TORCS' PKGBUILD)
pkgname=speed-dreams
pkgver=2.0.0
_pkgver=2.0.0-r4687
pkgrel=2
pkgdesc="A racing simulator with rich graphics and physics"
arch=('i686' 'x86_64')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=('sdl' 'freealut' 'freeglut' 'libpng' 'libxi' 'libxmu' 'libxrandr' 'plib>=1.8.3' 'libjpeg' 'zlib' 'enet' 'speed-dreams-data')
makedepends=('cmake' 'p7zip')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-base-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-unmaintained-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz"
        "${pkgname}.desktop")
md5sums=('a6842887fc289ed6057be2ba9d2afaa2'
         '8505c46aae6ce56a43353b8899d40ce4'
         'edcc842291737dbb13a3083252bf51d4'
         '57cf5c475491294ce1fb249e2dc0273b'
         'c8ab7ef068fa3fbe67d2b3246b30ea8a'
         '634f8cb8f4f96686c8049c8a025e72d0')

build() {
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSD_BINDIR=/usr/bin \
    -DSD_DATADIR=/usr/share/speed-dreams-2 \
    -DSD_LIBDIR=/usr/lib/speed-dreams-2 \
    -DOPTION_OFFICIAL_ONLY=ON

  make
}

package() {
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share"

  install -Dm644 "data/data/icons/icon.png" "${pkgdir}/usr/share/pixmaps/speed-dreams.png"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/speed-dreams.desktop"

  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/* "${pkgdir}/usr/share/doc/${pkgname}/"

  # Symlink for backwards compatibility
  cd "${pkgdir}/usr/bin"
  ln -s speed-dreams-2 speed-dreams
}

# vim:set ts=2 sw=2 et:
