# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adrián Chaves Fernández, aka Gallaecio <adriyetichaves@gmail.com>
# Contributor: Shahar Weiss <sweiss4@gmx.net> (build() code from his TORCS' PKGBUILD)
pkgname=speed-dreams
pkgver=2.1.0
_pkgver=2.1.0-r5799
pkgrel=2
pkgdesc="A racing simulator with rich graphics and physics"
arch=('i686' 'x86_64')
url="http://speed-dreams.sourceforge.net/"
license=('GPL2')
depends=('sdl' 'freealut' 'freeglut' 'libpng' 'libxi' 'libxmu' 'libxrandr' 'plib>=1.8.3' 'libjpeg' 'zlib' 'enet' 'speed-dreams-data' 'libvorbis' 'glu')
makedepends=('cmake' 'tar' 'mesa')

noextract=("${pkgname}-src-base-${_pkgver}.tar.xz"
           "${pkgname}-src-unmaintained-${_pkgver}.tar.xz"
           "${pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
           "${pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
           "${pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz")
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-base-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-unmaintained-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz"
        "${pkgname}.desktop")
md5sums=('4e45e6ebf35b5afe52be4c4d24e670a7'
         '293ff3117d9b82d7a509bb935cdd4d4b'
         '1f4f32f726ee27fce30bbcd91c611b7f'
         '09efa3454b1b0812169b5714821279e8'
         '71f4be0d2b5589c32deb9b3686e69128'
         '634f8cb8f4f96686c8049c8a025e72d0')

prepare() {
  # extract these manually as bsdtar can't handle these
  tar xf "${pkgname}-src-base-${_pkgver}.tar.xz"
  tar xf "${pkgname}-src-unmaintained-${_pkgver}.tar.xz"
  tar xf "${pkgname}-src-wip-cars-and-tracks-${_pkgver}.tar.xz"
  tar xf "${pkgname}-src-hq-cars-and-tracks-${_pkgver}.tar.xz"
  tar xf "${pkgname}-src-more-hq-cars-and-tracks-${_pkgver}.tar.xz"
}

build() {
  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSD_BINDIR=/usr/bin \
    -DSD_DATADIR=/usr/share/speed-dreams-2 \
    -DSD_LIBDIR=/usr/lib/speed-dreams-2 \
    -DOPTION_OFFICIAL_ONLY=ON

  make
}

package() {
  make DESTDIR="${pkgdir}/" install

  rm -r "${pkgdir}/usr/share"

  install -Dm644 "data/data/icons/icon.png" "${pkgdir}/usr/share/pixmaps/speed-dreams.png"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/speed-dreams.desktop"

  mkdir -p "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/* "${pkgdir}/usr/share/doc/${pkgname}/"

  # Symlink for backwards compatibility
  cd "${pkgdir}/usr/bin"
  ln -s speed-dreams-2 speed-dreams
}

# vim:set ts=2 sw=2 et:
