# $Id: PKGBUILD 164477 2016-03-03 13:24:42Z foutrelis $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-glib
pkgver=0.32
pkgrel=1
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'libcacard' 'libpulse' 'usbredir' 'libsoup'
	 'cairo' 'libjpeg')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils'
	     'vala' 'sdl' 'python2-six' 'pixman')
options=('!emptydirs')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2")
md5sums=('09930abb5ebe1b25b0f6988eb5716f98')

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr \
    --disable-static --enable-vala --with-gtk=no \
    --with-audio=pulse \
    --with-coroutine=gthread --sbindir=/usr/bin \
    --enable-smartcard
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
}
