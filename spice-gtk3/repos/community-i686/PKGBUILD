# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
pkgver=0.21
pkgrel=3
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'gtk3' 'libcacard' 'libpulse' 'usbredir')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'qemu' 'spice-protocol' 'usbutils' 'vala')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2"
	"usb-fix.patch::https://bugs.freedesktop.org/attachment.cgi?id=86818")
sha256sums=('73d418ffa97d6cf5edb1920d46f1d51fc40d14a8d4edfab3be6041e25062c564')
sha256sums=('73d418ffa97d6cf5edb1920d46f1d51fc40d14a8d4edfab3be6041e25062c564'
            '1b3c7d20ebd3644faf95e387f7d2696fcd773539db8ffdda2e316a7c516ce6d2')

prepare() {
  cd "$srcdir/spice-gtk-$pkgver"
  sed -i 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py
  patch -p1 <$srcdir/usb-fix.patch
}

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr --disable-static --enable-vala --with-gtk=3.0
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
}
