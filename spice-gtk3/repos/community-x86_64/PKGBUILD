# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk3
pkgver=0.23
pkgrel=1
pkgdesc="Gtk client and libraries for SPICE remote desktop servers (gtk3 version)"
arch=('i686' 'x86_64')
url="http://spice-space.org"
license=('LGPL2.1')
depends=('celt0.5.1' 'dbus-glib' 'gtk3' 'libcacard' 'libpulse' 'usbredir')
makedepends=('gobject-introspection' 'intltool' 'python2-pyparsing' 'spice-protocol' 'usbutils' 'vala')
source=("http://www.spice-space.org/download/gtk/spice-gtk-$pkgver.tar.bz2")
sha256sums=('05e9e5d915a99005a812846f733bba177ef3b1a9de9356690047250fb1b1fa3a')
sha256sums=('bfdfa6cb12c40327b63167fe7191ce94e7f2a272473c87df31790c25d60cc81d')

prepare() {
  cd "$srcdir/spice-gtk-$pkgver"
  sed -i 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py
  # Disable broken tests
  sed -i 's/ tests//' Makefile.{am,in}
}

build() {
  cd "$srcdir/spice-gtk-$pkgver"
  PYTHON=python2 ./configure --prefix=/usr \
    --disable-static --enable-vala --with-gtk=3.0 \
    --with-coroutine=gthread
  make
}

package() {
  cd "$srcdir/spice-gtk-$pkgver"
  make DESTDIR="$pkgdir/" install
}
