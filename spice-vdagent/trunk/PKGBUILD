# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: mutantmonkey <aur@mutantmonkey.in>

pkgname=spice-vdagent
pkgver=0.17.0
pkgrel=3
pkgdesc="Spice agent for Linux guests"
arch=('x86_64')
url="https://www.spice-space.org/"
license=('GPL')
depends=('alsa-lib' 'dbus' 'glib2' 'libpciaccess' 'libxfixes' 'libxinerama' 'libxrandr' 'systemd')
makedepends=('spice-protocol')
backup=('etc/conf.d/spice-vdagentd')
source=("https://www.spice-space.org/download/releases/$pkgname-$pkgver.tar.bz2")
sha256sums=('f14a8bd8cdee10641aabd9ba32461a5844eab0fddb2a10c1d31386e7a9f7b33d')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's|/lib/udev/rules.d|$(libdir)/udev/rules.d|' Makefile.{am,in}
	sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|
	        s|/usr/sbin/spice-vdagentd|/usr/bin/spice-vdagentd|' data/spice-vdagentd.service
	sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|' data/spice-vdagentd.1.in
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin \
	            --with-session-info=systemd --with-init-script=systemd
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir/var"
	install -dm755 "$pkgdir/etc/conf.d/"
	echo 'SPICE_VDAGENTD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-vdagentd"
}
