# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: loqs
# Contributor: Paulo Matos <paulo@matos-sorge.com>
# Contributor: b4283 <unknown@unknowndomain.com>

pkgname=spike
pkgver=1.0.0
pkgrel=2
pkgdesc='Spike, a RISC-V ISA Simulator'
arch=(x86_64)
url='https://github.com/riscv/riscv-isa-sim'
license=(BSD)
groups=(risc-v)
depends=(dtc)
replaces=(fesvr)
conflicts=(fesvr)
makedepends=(git)
source=("git+https://github.com/riscv/riscv-isa-sim#commit=2710fe575e7e6a4e2418224f8d254d5ca31f6c0e")
b2sums=('SKIP')

prepare() {
  cd riscv-isa-sim
  git cherry-pick -n b3855682c2d744c613d2ffd6b53e3f021ecea4f3
}

build() {
  cd riscv-isa-sim
  ./configure --prefix=/usr
  make LDFLAGS="$LDFLAGS -s -w"
}

package() {
  cd riscv-isa-sim
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
