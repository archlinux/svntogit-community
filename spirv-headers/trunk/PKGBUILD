# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=SPIRV-Headers
pkgname=spirv-headers
pkgver=1.5.5.sdk.1.3.204.0.r1.g6a55fad
pkgrel=1
pkgdesc="SPIR-V Headers"
arch=(any)
url="https://www.khronos.org/registry/spir-v/"
license=(MIT)
makedepends=(cmake git)
# From either https://github.com/KhronosGroup/SPIRV-Tools/blob/master/DEPS
# or https://github.com/KhronosGroup/SPIRV-LLVM-Translator/blob/master/spirv-headers-tag.conf
# using whatever is newest for latest release
_commit=6a55fade62dec6a406a5a721148f88a2211cbefa
source=(git+https://github.com/KhronosGroup/${_pkg}.git#commit=${_commit})
sha512sums=(SKIP)

pkgver() {
  cd ${_pkg}
  _full_ver="$(grep "SPIRV-Headers VERSION" CMakeLists.txt | awk '{print $3}' | tr --delete '()')"
  _sub_ver="$(git describe --tags --exclude '*raytracing*' | sed 's/\([^-]*-g\)/r\1/')"
  echo "${_full_ver}.${_sub_ver}" | sed 's/-/./g;s/-/+/g;s|/|_|g'
}

build() {
  cmake -B build -S ${_pkg} -DCMAKE_INSTALL_PREFIX=/usr .
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${_pkg}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
