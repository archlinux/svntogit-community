# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=spotify-launcher
pkgver=0.4.2
pkgrel=1
pkgdesc="Client for spotify's apt repository in Rust for Arch Linux"
url='https://github.com/kpcyrd/spotify-launcher'
arch=('x86_64')
license=('MIT' 'Apache')
depends=('sequoia-sqv' 'zenity' 'alsa-lib>=1.0.14' 'gtk3' 'libxss' 'desktop-file-utils' 'openssl' 'nss' 'at-spi2-atk' 'libcurl-gnutls' 'libsm')
makedepends=('cargo')
backup=('etc/spotify-launcher.conf')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
b2sums=('88de045cc56783dccb95ab305c331a3560a1485dfc286f04ec7e4c5c961509b5c7ae9d32caf594b263df0f728c8e3d1cf1bbb8b7803592b16e0f0d6b45d68e82'
        'SKIP')
options=('!lto')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --frozen
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/spotify-launcher
  install -Dm 644 data/pubkey_7A3A762FAFD4A51F.gpg \
    "${pkgdir}/usr/share/spotify-launcher/keyring.pgp"

  install -Dm644 contrib/spotify-launcher.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm644 contrib/icons/spotify-linux-512.png "${pkgdir}/usr/share/pixmaps/spotify-launcher.png"
  install -Dm644 contrib/spotify-launcher.conf -t "${pkgdir}/etc"

  for size in 22 24 32 48 64 128 256 512; do
    install -Dm644 "contrib/icons/spotify-linux-${size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/spotify-launcher.png"
  done
}

# vim: ts=2 sw=2 et:
