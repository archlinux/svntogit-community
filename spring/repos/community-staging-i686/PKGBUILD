# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=103.0
pkgrel=7
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'jdk8-openjdk' 'mesa')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=(http://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${pkgver}_src.tar.lzma
        gold-linker.patch
        0001-fix-build-with-curl-7.50.0.patch)
md5sums=('e71a3dc16262b06b159157ecd52bbbe5'
         '4837a3a273e346d159c3a9ad75ac1a1e'
         '8e4652c98c66016959fdd0dd1f6fc283')

prepare() {
  cd spring_$pkgver

  # https://springrts.com/mantis/view.php?id=5413
  patch -Np1 -i ../gold-linker.patch

  patch -Np1 -d tools/pr-downloader <../0001-fix-build-with-curl-7.50.0.patch

}

build() {
  cd spring_${pkgver}

  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DJAVA_HOME=/usr/lib/jvm/java-8-openjdk \
    -DCMAKE_SKIP_RPATH=YES
  make
}

package() {
  cd spring_${pkgver}

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.spring' > "$pkgdir/etc/spring/datadir"
}

# vim sw=2:ts=2 et:
