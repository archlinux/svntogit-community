# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=105.0.1
_commit=8581792
pkgrel=1
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl2' 'libunwind'
         'libxcursor' 'curl' 'shared-mime-info' 'desktop-file-utils' 'libx11')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python' 'jdk8-openjdk' 'mesa' 'git' 'gcc10')
optdepends=('python: python-based bots'
            'java-runtime: java-based bots')
source=(git+https://github.com/spring/spring.git#commit=$_commit
        spring-gcc11.patch)
sha512sums=('SKIP'
            'b30c666828fd2a1acdd64b505928f7d732479d6985590ea0d8926f3a19dc4b7aa95c80326e72f283d6de1ff17b9763ea0cbc88c71da0d6a77a2db301da9dce49')

prepare() {
  cd spring
  git submodule update --init

  patch -Np1 -i "$srcdir"/spring-gcc11.patch
}

build() {
  cd spring

  export CXX=g++-10
  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share/spring \
    -DJAVA_HOME=/usr/lib/jvm/java-8-openjdk \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd spring

  DESTDIR="$pkgdir" make install

  install -d "$pkgdir/etc/spring"
  echo '$HOME/.spring' > "$pkgdir/etc/spring/datadir"
}

# vim sw=2:ts=2 et:
