# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.247
pkgrel=1
pkgdesc="A free cross-platform lobby client for the Spring RTS project."
arch=('i686' 'x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('hicolor-icon-theme' 'wxgtk' 'curl' 'libtorrent-rasterbar' 'boost-libs'
         'alure' 'libsm')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer') 
makedepends=('boost' 'asio' 'cmake' 'git')
install=springlobby.install
source=(git://github.com/springlobby/springlobby.git#tag=${pkgver})
md5sums=('SKIP')

prepare() {
  cd $srcdir/${pkgname}
  git submodule update --init
}

build() {
  cd $srcdir/${pkgname}

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr
  make 
}

package() {
  cd $srcdir/${pkgname}

  make DESTDIR=$pkgdir install
}

# vim: sw=2:ts=2 et:
