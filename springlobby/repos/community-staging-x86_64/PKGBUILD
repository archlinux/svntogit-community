# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.267
pkgrel=3
pkgdesc="A free cross-platform lobby client for the Spring RTS project"
arch=('x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('hicolor-icon-theme' 'wxgtk3' 'curl' 'libtorrent-rasterbar' 'boost-libs'
         'alure' 'libsm')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer')
makedepends=('boost' 'cmake')
source=(http://springlobby.info/tarballs/springlobby-${pkgver}.tar.bz2)
sha512sums=('6127bd7377f9a843b23df6dae7a07af1122f6ec5fa13cd8d16c0c7a3cc84e1f7af2fb7df929f904ee0b4e3abf12ae1e525ffb39b2afa0215433209054cf0de5f')

build() {
  cd $srcdir/${pkgname}-${pkgver}

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver}

  make DESTDIR=$pkgdir install
}

# vim: sw=2:ts=2 et:
