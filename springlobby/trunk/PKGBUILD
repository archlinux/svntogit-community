# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.270
pkgrel=3
pkgdesc="A free cross-platform lobby client for the Spring RTS project"
arch=('x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('hicolor-icon-theme' 'wxgtk3' 'curl' 'libtorrent-rasterbar' 'boost-libs'
         'alure' 'libsm')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer')
makedepends=('boost' 'cmake' 'git')
source=("git+https://github.com/springlobby/springlobby.git#tag=$pkgver"
        https://github.com/springlobby/springlobby/commit/252c4cb156c1442ed9b4faec3f26265bc7c295ff.patch
        "git+https://github.com/spring/pr-downloader.git")
sha512sums=('SKIP'
            '02347b50bf7ac73f128623edc318c534bfda7cd88415f46e6407fc46d4971b5809a7abf608e4c683f6c91879a8c9d399aa982e3c8db99109cc397c64220c2dab'
            'SKIP')

prepare() {
  cd $pkgname

  patch -Np1 -i "${srcdir}"/252c4cb156c1442ed9b4faec3f26265bc7c295ff.patch

  git submodule init
  git config submodule."src/downloader/lib".url "$srcdir"/pr-downloader
  git submodule update

  mkdir build
}

build() {
  cd $pkgname/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $pkgname/build

  DESTDIR="$pkgdir" make install
}

# vim: sw=2:ts=2 et:
