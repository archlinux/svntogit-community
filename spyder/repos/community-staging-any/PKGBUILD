# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=5.1.5
pkgrel=3
pkgdesc="The Scientific Python Development Environment"
arch=(any)
url="https://www.spyder-ide.org/"
license=(MIT)
makedepends=(python-sphinx)
depends=(
    python-atomicwrites
    python-chardet
    python-cloudpickle
   	python-cookiecutter
    python-diff-match-patch
    python-intervaltree
    ipython
    python-jedi
    python-jsonschema
    python-keyring
    jupyter-nbconvert
    python-numpydoc
    python-parso
    python-pexpect
    python-pickleshare
    python-psutil
    python-pygments
    python-pylint
    python-pyqt5
    python-pyqtwebengine
    qt5-webkit
    python-lsp-server
    python-lsp-black
    python-pyls-spyder
    python-xdg
    python-pyzmq
    python-qdarkstyle
    python-qstylizer
    python-qtawesome
    python-qtconsole
    python-qtpy
    python-rtree
    python-setuptools
    python-sphinx
    python-spyder-kernels
    python-textdistance
    python-three-merge
    python-watchdog
    autopep8
    flake8
    python-pycodestyle
    python-pydocstyle
    python-pyflakes
    python-rope
    yapf
)
# Starting from autopep8, required optdeps of python-language-server
optdepends=(
    'cython: run Cython files in the IPython Console'
    'python-matplotlib: 2D/3D plotting in the IPython Console'
    'python-numpy: support for N-dimensional arrays in the Variable Explorer'
    'python-pandas: support for DataFrames and Series in the Variable Explorer'
    'python-scipy: support for Matlab workspace in the Variable Explorer'
    'python-sympy: symbolic mathematics in the IPython Console'
)
checkdepends=(
    python-pytest
    python-pytest-qt
    python-pytest-mock
    python-pytest-cov
    python-pytest-xvfb
    python-pytest-order
    python-pytest-lazy-fixture
    python-flaky
    python-pandas
    python-scipy
    python-sympy
    python-pillow
    python-matplotlib
    cython
    git
    tk
)
source=(https://github.com/spyder-ide/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        fix-pylint-2.10.patch)
sha512sums=('2204fe46ad3e4e14bebb51ef6a53074b657f6d970553decd0b2f9d0a7fba71b2f14b4efc3248bb4fb944860e85851e4a8add475ff12c6466e864be75b8cc38bb'
            '7837cb8af529df9f63041a883348a199d549d60bc7ea65913ca07728a087c503830aa2f09e80198f05c3b3b8d0c5de096d5d021e8c148b63d45d7b338d4b3e0e')

prepare() {
  cd ${pkgname}-${pkgver}
  # Remove pylint limit, we have a patched lsp (https://github.com/spyder-ide/spyder/pull/16565)
  patch -p1 -i ../fix-pylint-2.10.patch
  # Allow our python libraries versions
  #sed "s|JEDI_REQVER = '=|JEDI_REQVER = '>=|" -i spyder/dependencies.py
  #sed "s|PARSO_REQVER = '=|PARSO_REQVER = '>=|" -i spyder/dependencies.py
  sed "s|QDARKSTYLE_REQVER = '=|QDARKSTYLE_REQVER = '>=|" -i spyder/dependencies.py
  #sed "s|jedi==|jedi>=|" -i setup.py
  #sed "s|parso==|parso>=|" -i setup.py
  sed "s|qdarkstyle==|qdarkstyle>=|" -i setup.py
  # Allow our Qt version
  sed "s|'pyqt5<5.13'|'pyqt5'|" -i setup.py
  sed "s|'pyqtwebengine<5.13'|'pyqtwebengine'|" -i setup.py
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  # Required per above patching
  #sed "s|jedi =|jedi >=|" -i binder/environment.yml
  #sed "s|parso =|parso >=|" -i binder/environment.yml
  sed "s|qdarkstyle =|qdarkstyle >=|" -i binder/environment.yml
  sed "s|pyqt <5.13|pyqt|" -i binder/environment.yml
  #sed "s|jedi =|jedi >=|" -i requirements/conda.txt
  #sed "s|parso =|parso >=|" -i requirements/conda.txt
  sed "s|qdarkstyle =|qdarkstyle >=|" -i requirements/conda.txt
  sed "s|pyqt <5.13|pyqt|" -i requirements/conda.txt
  # Failures from Qt5.12-targeted tests, non-CI tests, etc.
  # Some even segfaults, but upstream does not support us
  python runtests.py --color=yes || echo "Tests failed"
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t ${pkgdir}/usr/share/licenses/${pkgname}/
  # Install icon file
  install -Dm644 spyder/images/spyder.svg -t "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}"/usr/bin/spyder_win_post_install.py
}
