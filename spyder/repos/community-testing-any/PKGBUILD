# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=4.1.5
pkgrel=3
pkgdesc="The Scientific Python Development Environment"
arch=(any)
url="https://www.spyder-ide.org/"
license=(MIT)
makedepends=(python-setuptools python-sphinx)
depends=(
    python-atomicwrites
    python-chardet
    python-cloudpickle
    python-diff-match-patch
    python-intervaltree
    ipython
    python-jedi
    python-keyring
    jupyter-nbconvert
    python-numpydoc
    python-parso
    python-pexpect
    python-pickleshare
    python-psutil
    python-pygments
    python-pylint
    python-pyqt5
    python-pyqtwebengine
    qt5-webkit
    python-language-server
    python-xdg
    python-pyzmq
    python-qdarkstyle
    python-qtawesome
    python-qtconsole
    python-qtpy
    python-sphinx
    python-spyder-kernels
    python-watchdog
)
optdepends=(
    'cython: run Cython files in the IPython Console'
    'python-matplotlib: 2D/3D plotting in the IPython Console'
    'python-numpy: support for N-dimensional arrays in the Variable Explorer'
    'python-pandas: support for DataFrames and Series in the Variable Explorer'
    'python-scipy: support for Matlab workspace in the Variable Explorer'
    'python-sympy: symbolic mathematics in the IPython Console'
)
checkdepends=(
    python-pytest
    python-pytest-qt
    python-pytest-mock
    python-pytest-cov
    python-pytest-xvfb
    python-pytest-ordering
    python-pytest-lazy-fixture
    python-mock
    python-flaky
    python-pandas
    python-scipy
    python-sympy
    python-pillow
    python-matplotlib
    cython
    git
    tk
)
source=(https://github.com/spyder-ide/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        spyder3_to_spyder.patch)
sha256sums=('98f24bd782e2c5d2fcd1033b433692e85dada9f095f86547ba66728e5a26a65f'
            '42b51d2696f06e4ad69be7379e913fd90e5c09c3e8244b6f38ef127ae86cfe89')

prepare() {
  cd ${pkgname}-${pkgver}
  # Replace spyder3 with spyder
  patch -p1 -i ../spyder3_to_spyder.patch
  mv scripts/spyder{3,}.desktop
  mv scripts/spyder{3,}.appdata.xml
  # Allow our jedi/parso/pyls versions
  sed -i "s|JEDI_REQVER = '=0.17.1'|JEDI_REQVER = '>=0.17.1'|" -i spyder/dependencies.py
  sed -i "s|PARSO_REQVER = '=0.7.0'|PARSO_REQVER = '>=0.7.0'|" -i spyder/dependencies.py
  sed -i "s|PYLS_REQVER = '>=0.34.0;<1.0.0'|PYLS_REQVER = '>=0.34.0'|" -i spyder/dependencies.py
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  # Required per above patching
  sed -i "s|jedi==0.17.1|jedi>=0.17.0|" -i setup.py
  sed -i "s|parso==0.7.0|parso>=0.7.0|" -i setup.py
  sed -i "s|python-language-server[all]>=0.34.0,<1.0.0|python-language-server[all]>=0.34.0|" -i setup.py
  sed -i "s|jedi =0.17.1|jedi >=0.17.0|" -i requirements/conda.txt
  sed -i "s|parso =0.7.0|parso >=0.7.0|" -i requirements/conda.txt
  sed -i "s|jedi =0.17.1|jedi >=0.17.0|" -i binder/environment.yml
  sed -i "s|parso =0.7.0|parso >=0.7.0|" -i binder/environment.yml
  sed -i "s|python-language-server >=0.34.0,<1.0.0|python-language-server >=0.34.0|" -i binder/environment.yml
  # Some tests failures but upstream does not support us
  python runtests.py || echo "Tests failed"
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t ${pkgdir}/usr/share/licenses/${pkgname}/
  # Install icon file
  install -Dm644 spyder/images/spyder.svg -t "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}"/usr/bin/spyder_win_post_install.py
}
