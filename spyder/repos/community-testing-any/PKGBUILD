# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=3.3.6
pkgrel=1
pkgdesc='The Scientific Python Development Environment (Python 3 version)'
arch=('any')
url='https://www.spyder-ide.org/'
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${pkgname}/archive/v${pkgver}.tar.gz"
        "bug_54508_missing_pyqt4_qtwebkit.patch"
        "spyder3_to_spyder.patch")
sha256sums=('aa3e46e30da987a2532854d3ba30143f7876bddb3be1b0d5ced11a7ab7abea83'
            '28d4aa8938d809c299ab6bf6b00a2f82677f9a98a9763b7127970881b76276f5'
            'bca64095b2409676c4dd3dddc9e19190f36cfd30494966af1eba3de876c1f2aa')
makedepends=('python-setuptools' 'python-sphinx')
depends=('python-pyqt5' 'qt5-webkit' 'python-qtconsole' 'python-qtpy' 'python-qtawesome'
         'python-pyflakes' 'python-pyzmq' 'python-pygments' 'python-psutil' 'python-pickleshare'
         'jupyter' 'jupyter-nbconvert'
         'python-rope' 'python-jedi' 'python-pylint' 'python-pycodestyle' 'python-sphinx'
         'python-opengl' 'python-spyder-kernels<1.0' 'icu' 'gtk-update-icon-cache'
         'desktop-file-utils')
optdepends=('python-numpy: N-dimensional arrays'
            'python-scipy: signal/image processing'
            'python-h5py: HDF5 support'
            'python-matplotlib: interactive 2D/3D data plotting'
            'python-sympy: symbolic mathematics for the IPython console'
            'python-pandas: view and edit DataFrames and Series in the Variable Explorer')
conflicts=('spyder3')
replaces=('spyder3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # Forced QT API to pyqt5 (see https://bugs.archlinux.org/task/54508)
  echo "Applying patches bug_54508_missing_pyqt4_qtwebkit.patch"
  patch -p1 -i "../bug_54508_missing_pyqt4_qtwebkit.patch"
  # Replace spyder3 with spyder
  rm "scripts/spyder.desktop"
  patch -p1 -i "../spyder3_to_spyder.patch"
  mv "scripts/spyder3.desktop" "scripts/spyder.desktop"
  mv "scripts/spyder3.appdata.xml" "scripts/spyder.appdata.xml"
}

build() {
  cd "${pkgname}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
  # Install icon and desktop file
  install -D -m644 "spyder/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}
