# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgbase='spyder'
pkgname=('spyder' 'spyder3')
pkgver=2.3.9
pkgrel=1
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
makedepends=('python2-setuptools' 'python-setuptools' 'python-sphinx' 'python2-sphinx')
arch=('any')
url='https://github.com/spyder-ide/spyder/'
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${pkgbase}/archive/v${pkgver}.tar.gz"
        "python2.patch")
sha256sums=('8343147e98d3e6e93090cc51d0d4d03ea6f4d7e3a26defe37236eb3f36780868'
            '58a30ff9401c528176690f2081aed2fe3185c5baaf54ee6a1c127e9421bab96a')

package_spyder() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 2.x series.'
  depends=('python2-pyqt4' 'python2-pyflakes' 'python2-pyzmq' 'python2-pygments' 'ipython2-notebook'
           'icu' 'gtk-update-icon-cache')
  optdepends=('python2-pylint: powerful code analysis'
              'python2-rope: editor code completion, calltips and go-to-definition'
              'python2-sphinx: rich text help on the object inspector'
              'python2-numpy: N-dimensional arrays'
              'python2-scipy: signal/image processing'
              'python2-psutil: memory/CPU usage in the status bar'
              'python2-h5py: HDF5 support'
              'python2-matplotlib: interactive 2D/3D data plotting'
              'python2-pep8: real-time code style analysis'
              'python2-sympy: symbolic mathematics for the IPython console'
              'python2-jedi: editor code completion, calltips and go-to-definition'
              'python2-pandas: view and edit DataFrames and Series in the Variable Explorer')
  [ -d "build" ] && rm -rf "build"
  cp -r "${pkgbase}-${pkgver}" build
  cd build
  patch -p1 -i "../python2.patch"
  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyderlib/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}

package_spyder3() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 3.x series.'
  depends=('python-pyqt4' 'python-pyflakes' 'python-pyzmq' 'python-pygments' 'jupyter'
           'icu' 'gtk-update-icon-cache')
  optdepends=('python-pylint: powerful code analysis'
              'python-rope: editor code completion, calltips and go-to-definition'
              'python-sphinx: rich text help on the object inspector'
              'python-numpy: N-dimensional arrays'
              'python-scipy: signal/image processing'
              'python-psutil: memory/CPU usage in the status bar'
              'python-h5py: HDF5 support'
              'python-matplotlib: interactive 2D/3D data plotting'
              'pep8: real-time code style analysis'
              'python-sympy: symbolic mathematics for the IPython console'
              'python-jedi: editor code completion, calltips and go-to-definition'
              'python-pandas: view and edit DataFrames and Series in the Variable Explorer')
  [ -d "build" ] && rm -rf "build"
  cp -r "${pkgbase}-${pkgver}" build
  cd build
  python3 setup.py build
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyderlib/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}
