# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgbase='spyder'
pkgname=('spyder2' 'spyder3')
pkgver=3.1.4
pkgrel=3
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
makedepends=('python2-setuptools' 'python-setuptools' 'python-sphinx' 'python2-sphinx')
arch=('any')
url='https://github.com/spyder-ide/spyder/'
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${pkgbase}/archive/v${pkgver}.tar.gz"
        "python2.patch"
        "python2-optional-nbconvert.patch"
        "python2-pep8.patch"
        "python3-pep8.patch"
        "bug_54508_missing_pyqt4_qtwebkit.patch"
        "bug_51437_spyder2.patch")
sha256sums=('bcd75ebe65309a5f9184eedc59c415fe0d09e9b54a2b51bc31199a6095cd0b85'
            '4fb415fb358371da4a8b9524712de9338d391eed3bdabc5e217cf54f558771b5'
            'f6afdd08971a2022649af0be70cac67aa4d95fff06ade57f708693cffe073528'
            'dbebf909bf59027d22247e3e3c1736b5ec9337a879358f8117a967bb03e913b2'
            'f4eba4c414e655f0f2797e9864aa199607a6706f6b245aca56a54b250febc382'
            '28d4aa8938d809c299ab6bf6b00a2f82677f9a98a9763b7127970881b76276f5'
            'ae077fc95bc6861402a90c55bc11b3a2dda3fde4005e9d7ad65df3b1a120d8c1')

prepare() {
  cd "${pkgbase}-${pkgver}"
  # Forced QT API to pyqt5 (see https://bugs.archlinux.org/task/54508)
  patch -p1 -i "../bug_54508_missing_pyqt4_qtwebkit.patch"
  # Replace spyder with spyder2 (see https://bugs.archlinux.org/task/51437)
  patch -p1 -i "../bug_51437_spyder2.patch"
  mv "img_src/spyder.png" "img_src/spyder2.png"
  mv "scripts/spyder.desktop" "scripts/spyder2.desktop"

  # Prepare sources for spyder2 build
  cp -r "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/spyder2-${pkgver}"
  cd "${srcdir}/spyder2-${pkgver}"
  patch -p1 -i "../python2.patch"
  patch -p1 -i "../python2-pep8.patch"
  patch -p1 -i "../python2-optional-nbconvert.patch"

  # Prepare sources for spyder3 build
  cp -r "${srcdir}/${pkgbase}-${pkgver}" "${srcdir}/spyder3-${pkgver}"
  cd "${srcdir}/spyder3-${pkgver}"
  patch -p1 -i "../python3-pep8.patch"
}

package_spyder2() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 2.x series.'
  depends=('python2-pyqt5' 'qt5-webkit' 'python2-qtconsole' 'python2-qtpy' 'python2-qtawesome'
           'python2-pyflakes' 'python2-pyzmq' 'python2-pygments' 'python2-psutil' 'python2-pickleshare'
           'python2-rope' 'python2-jedi' 'python2-pylint' 'python2-pycodestyle' 'python2-sphinx'
           'icu' 'gtk-update-icon-cache')
  provides=('spyder')
  conflicts=('spyder')
  replaces=('spyder')
  optdepends=('python2-numpy: N-dimensional arrays'
              'python2-scipy: signal/image processing'
              'python2-h5py: HDF5 support'
              'python2-matplotlib: interactive 2D/3D data plotting'
              'python2-sympy: symbolic mathematics for the IPython console'
              'python2-pandas: view and edit DataFrames and Series in the Variable Explorer')
  install="${pkgname}.install"
  cd "${pkgname}-${pkgver}"
  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "spyder/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Change main executable name
  mv "${pkgdir}/usr/bin/spyder" "${pkgdir}/usr/bin/${pkgname}"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}

package_spyder3() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 3.x series.'
  depends=('python-pyqt5' 'qt5-webkit' 'python-qtconsole' 'python-qtpy' 'python-qtawesome'
           'python-pyflakes' 'python-pyzmq' 'python-pygments' 'python-psutil' 'python-pickleshare'
           'jupyter' 'jupyter-nbconvert'
           'python-rope' 'python-jedi' 'python-pylint' 'python-pycodestyle' 'python-sphinx'
           'icu' 'gtk-update-icon-cache')
  optdepends=('python-numpy: N-dimensional arrays'
              'python-scipy: signal/image processing'
              'python-h5py: HDF5 support'
              'python-matplotlib: interactive 2D/3D data plotting'
              'python-sympy: symbolic mathematics for the IPython console'
              'python-pandas: view and edit DataFrames and Series in the Variable Explorer')
  cd "${pkgname}-${pkgver}"
  python3 setup.py build
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "spyder/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
  # Add symlink /usr/bin/spyder to /usr/bin/spyder3 (see https://bugs.archlinux.org/task/51437)
  ln -s "/usr/bin/spyder3" "${pkgdir}/usr/bin/spyder"
}
