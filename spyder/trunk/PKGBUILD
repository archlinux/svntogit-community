# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgbase='spyder'
pkgname=('spyder' 'spyder3')
pkgver=2.3.2
pkgrel=1
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
makedepends=('python2-setuptools' 'python-setuptools' 'python-sphinx' 'python2-sphinx')
arch=('any')
url='http://code.google.com/p/spyderlib/'
license=('MIT')
install="${pkgbase}.install"
source=("https://bitbucket.org/spyder-ide/spyderlib/downloads/${pkgbase}-${pkgver}.zip"
        "pylint2.patch"
        "python2.patch")
sha256sums=('f4f71ffe8590ffa8813622efb6705e52897552624726df317e0589ae121240bb'
            'f8836efa74ac9b8fce389a2708bb253083bbe042aabec124659839d5b3e167dd'
            '05f194438176cfa03cb76f38e5f64723b72a39d6771d5d4a927adf07a0ec67f1')

package_spyder() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 2.x series.'
  depends=('python2-pyqt4' 'python2-pyflakes' 'python2-pyzmq' 'python2-pygments' 'gtk-update-icon-cache')
  optdepends=('python2-pylint: powerful code analysis'
              'ipython2: enhanced Python interpreter'
              'python2-rope: editor code completion, calltips and go-to-definition'
              'python2-sphinx: rich text help on the object inspector'
              'python2-numpy: N-dimensional arrays'
              'python2-scipy: signal/image processing'
              'python2-psutil: memory/CPU usage in the status bar'
              'python2-h5py: HDF5 support'
              'python2-matplotlib: interactive 2D/3D data plotting'
              'pep8-python2: real-time code style analysis'
              'python2-sympy: symbolic mathematics for the IPython console')
  [ -d "build" ] && rm -rf "build"
  cp -r "${pkgbase}-${pkgver}" build
  cd build
  patch -p1 -i "../python2.patch"
  patch -p1 -i "../pylint2.patch"
  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyderlib/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}

package_spyder3() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 3.x series.'
  depends=('python-pyqt4' 'python-pyflakes' 'python-pyzmq' 'python-pygments' 'gtk-update-icon-cache')
  optdepends=('python-pylint: powerful code analysis'
              'ipython: enhanced Python interpreter'
              'python-rope: editor code completion, calltips and go-to-definition'
              'python-sphinx: rich text help on the object inspector'
              'python-numpy: N-dimensional arrays'
              'python-scipy: signal/image processing'
              'python-psutil: memory/CPU usage in the status bar'
              'python-h5py: HDF5 support'
              'python-matplotlib: interactive 2D/3D data plotting'
              'pep8: real-time code style analysis'
              'python-sympy: symbolic mathematics for the IPython console')
  [ -d "build" ] && rm -rf "build"
  cp -r "${pkgbase}-${pkgver}" build
  cd build
  python3 setup.py build
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyderlib/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}
