# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor : sebikul <sebikul@gmail.com>

pkgname=sqlheavy
pkgver=0.1.1
pkgrel=1
epoch=1
pkgdesc="GObject SQLite wrapper"
arch=('x86_64')
url='https://code.google.com/p/sqlheavy/'
license=('LGPL2.1')
depends=('gtk2' 'sqlite3')
makedepends=('gobject-introspection' 'vala')
source=("http://sqlheavy.googlecode.com/files/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}-no-utils.patch")
sha256sums=('e8689f5fd4e0baf98eada2b3811262bb99877c2e8586fd21ad2a7ad3acc59031'
            '57956925990bf8fb3b75e8e7dff5038d803c0c171013d7401474a5490afd7960')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../${pkgname}-no-utils.patch
}

build() {
  cd ${pkgname}-${pkgver}

  autoreconf -vif
  ./configure --prefix='/usr' --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/man
}

# vim: ts=2 sw=2 et:
