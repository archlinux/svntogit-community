# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Thomas Hipp <thomashipp at gmail dot com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname="sqlite-replication"
pkgver=3.30.1.4
pkgrel=1
_pkgver="${pkgver%.*}"
_replication="${pkgver##*.}"
pkgdesc="Replication Enabled SQL Database Engine in a C Library"
arch=('x86_64')
license=('custom:Public Domain')
url="https://github.com/canonical/sqlite"
depends=('readline' 'zlib')
makedepends=('tcl')
source=("https://github.com/canonical/sqlite/archive/version-${_pkgver}+replication${_replication}.tar.gz"
        license.txt)
sha256sums=('e9ab0542396be10e1b2951423f13b3e4d118a18e5d4ef39dc271e7dd17d3910f'
            '4e57d9ac979f1c9872e69799c2597eeef4c6ce7224f3ede0bf9dc8d217b1e65d')

build() {
  export CPPFLAGS="$CPPFLAGS -DSQLITE_ENABLE_COLUMN_METADATA=1 \
                             -DSQLITE_ENABLE_UNLOCK_NOTIFY \
                             -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
                             -DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
                             -DSQLITE_SECURE_DELETE \
                             -DSQLITE_MAX_VARIABLE_NUMBER=250000 \
                             -DSQLITE_MAX_EXPR_DEPTH=10000"

  # build sqlite
  cd sqlite-version-${_pkgver}-replication${_replication}
  ./configure --prefix=/usr \
	--libdir=/usr/lib/$pkgname \
	--includedir=/usr/include/$pkgname \
	--disable-static \
	--disable-amalgamation \
	--enable-fts3 \
	--enable-fts4 \
	--enable-fts5 \
	--enable-rtree \
	--enable-json1 \
	--enable-replication
  make
}

package() {
  cd sqlite-version-${_pkgver}-replication${_replication}
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/license.txt" "${pkgdir}/usr/share/licenses/${pkgbase}/license.txt"

  rm -rf "${pkgdir}/usr/bin"
  rm -rf "${pkgdir}/usr/lib/tcl"*
}
