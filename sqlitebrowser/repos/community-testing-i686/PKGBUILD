# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=sqlitebrowser
pkgver=3.7.0
pkgrel=2
pkgdesc="SQLite Database browser is a light GUI editor for SQLite databases, built on top of Qt"
arch=('i686' 'x86_64')
url="http://sqlitebrowser.org/"
license=('GPL')
depends=('qt5-base' 'sqlite' 'libxkbcommon-x11')
makedepends=('cmake' 'qt5-tools')
install=sqlitebrowser.install
#source=(https://github.com/sqlitebrowser/sqlitebrowser/archive/sqlb-$pkgver.tar.gz)
source=("$pkgname-$pkgver.tar.gz::https://github.com/sqlitebrowser/sqlitebrowser/archive/v$pkgver.tar.gz")
md5sums=('1033f076944316a713d4831bf581cf3a')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|Icon=.*|Icon=sqlitebrowser|' distri/sqlitebrowser.desktop
}

build() {
  cd $srcdir/$pkgname-$pkgver
  cmake -DUSE_QT5=TRUE -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make install DESTDIR=$pkgdir
  install -Dm0644 distri/sqlitebrowser.desktop $pkgdir/usr/share/applications/sqlitebrowser.desktop
  install -dm0755 $pkgdir/usr/share/icons/
  install -m0644 images/sqlitebrowser.svg $pkgdir/usr/share/icons/
}
