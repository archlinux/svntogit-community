# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=sqlx-cli
pkgver=0.6.2
pkgrel=1
pkgdesc='Command-line utility for SQLx, the Rust SQL toolkit'
license=('Apache' 'MIT')
arch=('x86_64')
url="https://github.com/launchbadge/sqlx"
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/launchbadge/sqlx/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('d5bab0f4ab6843bd8ab3884d0bb4d4166aa0c82b97edcc6134c94fad076d50bf43d24182aa37c23e0e82ea5e78a7db4ef7b6f878e3112b7224865e0ff98d2716')
options=('!lto')

build() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo build --locked --release
}

check() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo test --locked
}

package() {
    cd "sqlx-$pkgver"
    install -Dm755 "target/release/cargo-sqlx" "$pkgdir/usr/bin/cargo-sqlx"
    install -Dm755 "target/release/sqlx" "$pkgdir/usr/bin/sqlx"
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
