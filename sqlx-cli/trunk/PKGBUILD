# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=sqlx-cli
pkgver=0.5.10
pkgrel=1
pkgdesc='Command-line utility for SQLx, the Rust SQL toolkit'
license=('Apache' 'MIT')
arch=('x86_64')
url="https://github.com/launchbadge/sqlx"
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::https://github.com/launchbadge/sqlx/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('225c7b1e2eba70894ae70b6e3599f0eac62094900150eae5f36fa941934f35a01a3bf38a0c963683fcbf4bb5f8a9cd92745bc10c52f12d859e2620c48e04a82f')
options=('!lto')

build() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo build --locked --release
}

check() {
    cd "sqlx-$pkgver/sqlx-cli"
    cargo test --locked
}

package() {
    cd "sqlx-$pkgver"
    install -Dm755 "target/release/cargo-sqlx" "$pkgdir/usr/bin/cargo-sqlx"
    install -Dm755 "target/release/sqlx" "$pkgdir/usr/bin/sqlx"
    install -Dm644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
    install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
