# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Original TU: Jeff Mickey <j@codemac.net>
# Contributor: ciccio.a

pkgname=squashfs-tools
pkgver=4.3
pkgrel=6
pkgdesc="Tools for squashfs, a highly compressed read-only filesystem for Linux."
url="http://squashfs.sourceforge.net"
license=("GPL")
arch=('x86_64')
depends=('zlib' 'lzo' 'xz' 'lz4')
source=("http://downloads.sourceforge.net/sourceforge/squashfs/squashfs${pkgver}.tar.gz"
	"https://sources.debian.org/data/main/s/squashfs-tools/1:4.3-6/debian/patches/0003-CVE-2015-4645_and_CVE-2015-4646.patch"
	"https://gitweb.gentoo.org/repo/gentoo.git/plain/sys-fs/squashfs-tools/files/squashfs-tools-4.3-sysmacros.patch")
sha512sums=('854ed7acc99920f24ecf11e0da807e5a2a162eeda55db971aba63a03f0da2c13b20ec0564a906c4b0e415bd8258b273a10208c7abc0704f2ceea773aa6148a79'
            '84bf4dc961387fd72a97229eda5432c2ba97211541131cb5a5032d7a807a675d5423473b9df2ad5341514dcb864daaeadca8ae9c8543eb3efb499f5baf838bb0'
            'c21466b2f0aa6d6ae65b3693148274250bc42d8e7ea5142d6d4048d663ff4343dc539822320cf732d88c5bac048766a9771e1d2b739b1176cb9fafa96c4d306a')

prepare() {
    cd "$srcdir"/squashfs${pkgver}/${pkgname}
    patch -p2 -i ../../squashfs-tools-4.3-sysmacros.patch
    patch -p2 -i ../../0003-CVE-2015-4645_and_CVE-2015-4646.patch
}

build() {
    cd "$srcdir"/squashfs${pkgver}/${pkgname}
    make XZ_SUPPORT=1 LZO_SUPPORT=1 LZMA_XZ_SUPPORT=1 LZ4_SUPPORT=1
}

package() {
    cd "$srcdir"/squashfs${pkgver}/${pkgname}
    install -Dm755 mksquashfs "$pkgdir"/usr/bin/mksquashfs
    install -m755 unsquashfs "$pkgdir"/usr/bin/unsquashfs
}
