# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=sratom
pkgname=(sratom sratom-docs)
pkgver=0.6.12
pkgrel=1
pkgdesc="An LV2 Atom RDF serialisation library"
arch=(x86_64)
url="https://drobilla.net/software/sratom.html"
license=(ISC)
makedepends=(doxygen lv2 meson python-sphinx python-sphinx-lv2-theme serd sord)
options=(debug)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('e677945f59494970c8c723319216cf6543ed38c590b6db94e5e928e830004e96dc957a2cf2fb0b76278cf55f0d62ea03ba705fbc449424c467e29593eaa948dc'
            'SKIP')
b2sums=('76a01c84e418ee3ffeb1e0e44214e9b02ab58b9b572372ee43309650a7a674a55e49384e8b62657af30933b7bdb02faf38ff100030e2ec86947972cacf32db69'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}


package_sratom() {
  depends=(glibc libserd-0.so libsord-0.so lv2)
  provides=(libsratom-0.so)
  optdepends=('sratom-docs: for developer documentation')

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_sratom-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/"sratom{-0,}
}
