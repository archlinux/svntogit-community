# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=srrdb-terminal-client
pkgver=1.1.7
pkgrel=2
pkgdesc="A terminal client to access srrdb.com"
arch=("x86_64")
url="https://github.com/hashworks/srrdb-Terminal-Client"
license=("GPL3")
depends=('glibc')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('9fe7a03cd53d96a7afab1679e156e6a4bb5aae648415bd60955971a0ad533bc9')

prepare(){
	cd "srrdb-Terminal-Client-${pkgver}"
	mkdir -p build/
}

build() {
	cd "srrdb-Terminal-Client-${pkgver}"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
	go build \
		-trimpath \
		-buildmode=pie \
		-mod=readonly \
		-modcacherw \
		-ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X main.VERSION=\"${pkgver}\" -X main.BUILD_DATE=\"$(date -d@"${SOURCE_DATE_EPOCH}" +"%Y-%m-%d_%H:%M:%S")\"" \
		-o build .
}

check() {
	cd "srrdb-Terminal-Client-${pkgver}"
	go test .
}

package() {
	cd "srrdb-Terminal-Client-${pkgver}"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 "build/srrdb-Terminal-Client" "${pkgdir}/usr/bin/srrdb"
}
