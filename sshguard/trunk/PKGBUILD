# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=sshguard
pkgver=1.5
pkgrel=11
pkgdesc="Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with iptables"
arch=('i686' 'x86_64')
#url="http://sourceforge.net/projects/sshguard/"
url="http://www.sshguard.net/"
license=('GPL')
depends=('iptables')
options=(zipman)
install=sshguard.install
source=("http://downloads.sourceforge.net/sourceforge/sshguard/sshguard-$pkgver.tar.bz2"
	sshguard.rc
	sshguard.conf.d
	sshguard.service
	sshguard-journalctl)
sha256sums=('b537f8765455fdf8424f87d4bd695e5b675b88e5d164865452137947093e7e19'
            'a810da878e40be8616a93c9984e7dbd3700367d665b78b8aa2498228bb33cb6e'
            '66f28e2cc81e5686f8fd5c0d5a5015d7feecbe018a259f4032cfbacf6fe8d1c3'
            '2b2a3c1140349296c3629e77f74c3b29f4ef2ce5f77cb3a811cbed5b46d23028'
            '9491c4cd23d789dd04af551e16dc123f1517ca02ad3539f25715bbeec70ee21e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|^DAYNO.*|DAYNO       [0-9]?[1-9]|' src/parser/attack_scanner.l
  ./configure --prefix=/usr --with-firewall=iptables
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  strings src/sshguard | grep -E "^`which iptables`[[:space:]]" >/dev/null
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir/usr" install
  install -Dm0644 "$srcdir/sshguard.service" "$pkgdir/usr/lib/systemd/system/sshguard.service"
  install -Dm0755 "$srcdir/sshguard.rc" "$pkgdir/etc/rc.d/sshguard"
  install -Dm0644 "$srcdir/sshguard.conf.d" "$pkgdir/etc/conf.d/sshguard"
  install -Dm0755 "$srcdir/sshguard-journalctl" "$pkgdir/usr/lib/systemd/scripts/sshguard-journalctl"
}
