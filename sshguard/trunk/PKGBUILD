# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=sshguard
pkgver=1.6.4
pkgrel=1
pkgdesc="Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with iptables"
arch=('i686' 'x86_64')
url="http://www.sshguard.net/"
license=('GPL')
depends=('iptables')
install=sshguard.install
source=("http://downloads.sourceforge.net/sourceforge/sshguard/sshguard-$pkgver.tar.gz"
	sshguard.service
	sshguard-journalctl)
sha256sums=('6c4d3be2acf6349b4ac5d6fff4bbcd8fa988c82876d848cbbd0c7c99bc0438c7'
            '77b5cb4d00a34f1a0f03272464bac66793389ba05e89be6b7bfb78bdff0b0e92'
            'ce7c3f56edb1b43e0a1629ed646d667d7196808ba82edf7c9e1c3c9c03fca2ba')
sha256sums=('654d5412ed010e500e2715ddeebfda57ab23c47a2bd30dfdc1e68c4f04c912a9'
            '77b5cb4d00a34f1a0f03272464bac66793389ba05e89be6b7bfb78bdff0b0e92'
            'ce7c3f56edb1b43e0a1629ed646d667d7196808ba82edf7c9e1c3c9c03fca2ba')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|^DAYNO.*|DAYNO       [0-9]?[1-9]|' src/parser/attack_scanner.l
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sbindir=/usr/bin --with-firewall=iptables
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 "$srcdir/sshguard.service" "$pkgdir/usr/lib/systemd/system/sshguard.service"
  install -Dm0755 "$srcdir/sshguard-journalctl" "$pkgdir/usr/lib/systemd/scripts/sshguard-journalctl"
}
