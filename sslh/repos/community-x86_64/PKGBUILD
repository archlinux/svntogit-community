# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Le_suisse <lesuisse.dev+aur at gmail dot com>
# Contributor: Jason Rodriguez <jason-aur@catloaf.net>

pkgname=sslh
pkgver=1.20
pkgrel=2
pkgdesc='SSL/SSH/OpenVPN/XMPP/tinc port multiplexer'
arch=('x86_64')
url='https://www.rutschle.net/tech/sslh/README.html'
license=('GPL2')
makedepends=('systemd')
depends=('glibc' 'libcap' 'libconfig' 'pcre' 'systemd-libs')
backup=('etc/sslh.cfg')
install=$pkgname.install
source=("https://www.rutschle.net/tech/sslh/$pkgname-v$pkgver.tar.gz"{,.asc}
        'sslh.cfg'
        'sslh.service'
        'sslh-select.service'
        'sslh-fork.service')
validpgpkeys=('CDDDBADBEA4B72748E007D326C056F7AC7934136') # Yves Rutschle <yves@rutschle.net>
md5sums=('6a69c6128d0349e5fb22167675d18aee'
         'SKIP'
         '67a119213538aabf5d70a756ae7a99d0'
         'ecbb46c46874d7b620202926d36b8478'
         '2b98633ee61bc5a809a4f75479628b2f'
         'ca5ec0adf9149f1db4e09af659391659')

build() {
  cd $pkgname-v$pkgver
  make VERSION=\"v$pkgver\" USELIBCAP=1 USESYSTEMD=1 all systemd-sslh-generator
}

package() {
  # default arch config
  install -Dm 644 sslh.cfg "$pkgdir/etc/sslh.cfg"
  # manually install to have both ssl-fork and ssl-select
  cd $pkgname-v$pkgver
  install -Dm 755 sslh-fork "$pkgdir/usr/bin/sslh-fork"
  install -Dm 755 sslh-select "$pkgdir/usr/bin/sslh-select"
  ln -s sslh-fork "$pkgdir/usr/bin/sslh"
  # install manpage
  install -Dm 644 sslh.8.gz "$pkgdir/usr/share/man/man8/sslh.8.gz"
  ln -s sslh.8.gz "$pkgdir/usr/share/man/man8/sslh-fork.8.gz"
  ln -s sslh.8.gz "$pkgdir/usr/share/man/man8/sslh-select.8.gz"
  # install examples files
  install -Dm 644 basic.cfg "$pkgdir/usr/share/doc/$pkgname/basic.cfg"
  install -Dm 644 example.cfg "$pkgdir/usr/share/doc/$pkgname/example.cfg"
  # systemd
  install -dm 755 "$pkgdir"/usr/lib/systemd/{system,system-generators}
  install -Dm 755 systemd-sslh-generator "$pkgdir/usr/lib/systemd/system-generators/systemd-sslh-generator"
  cd "$pkgdir"
  install -Dm 644 "$srcdir"/sslh{,-fork,-select}.service usr/lib/systemd/system
}

# vim:set ts=2 sw=2 et:
