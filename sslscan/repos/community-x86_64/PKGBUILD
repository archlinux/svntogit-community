# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.0.14
pkgrel=1
pkgdesc="A fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=('x86_64')
license=('GPL3')
depends=('openssl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('bb496fffbd4e130bae8b9272d1303bfa0a681ff88aa3471e4b3f56bece2072ad8f8f41c19c4325de3d5772e785c1ab7491d3520ac3a44a7a170636ae4e13ed0d')
b2sums=('392a5f043172aa5800c40e1a86c198421de7fb4ddf92f8504e0b6270c70f4c58f0527cfaaff4892469d763d970b044168ab2a37fa4d836cddda88e5e9fd04586')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
