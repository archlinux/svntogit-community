# $Id$
# Maintainer: David Runge <dave@sleepmap.de>

pkgname=ssmtp
pkgver=2.64
pkgrel=10
pkgdesc="Extremely simple MTA to get mail off the system to a mailhub"
arch=('x86_64')
license=('GPL')
url="https://tracker.debian.org/pkg/ssmtp"
depends=('openssl' 'inetutils')
conflicts=('smtp-forwarder')
provides=('smtp-forwarder')
backup=('etc/ssmtp/ssmtp.conf' 'etc/ssmtp/revaliases')
options=('!makeflags' '!emptydirs')
source=("http://ftp.debian.org/debian/pool/main/s/ssmtp/${pkgname}_${pkgver}.orig.tar.bz2"
        'opessl_crypto.patch'
        'build-idempotency.patch')
sha512sums=('911752e744420adf58552b9cf3a6e558d8b39103e5ad33a9ed4429b9a3a2091f7674aac11fe9bbd66747a61cdabe6bd638b80efcaadc86b057c12e2e235cca72'
            '841fae386d155d989bb7202a2010107c5742ccff423aed50613d4ec7eb82a23cfa4818f08dc248d28c8db8569a27e3722181ad1282903cc93b8a1bfd9381ef75'
            'f66a5b344cd0da8d7be116a634a73681372628447a4d566024b3ccdad4092fe54b2b6ff529575cf340a09f62d55debe680d136c24739901606c7a2551037fddc')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/opessl_crypto.patch"
  patch -p1 -i "${srcdir}/build-idempotency.patch"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --enable-md5auth \
              --enable-ssl\
              --enable-inet6
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  # auto-answer inputs
  yes | make prefix="${pkgdir}/usr" \
             mandir="${pkgdir}/usr/share/man/man8" \
             etcdir="${pkgdir}/etc" install

  # man
  install -vDm644 "${pkgname}.conf.5" \
    "${pkgdir}/usr/share/man/man5/${pkgname}.conf.5"

  # don't use /usr/sbin
  mv "${pkgdir}/usr/sbin" "${pkgdir}/usr/bin"
  # symlinks for smtp-forwarder
  ln -sv ssmtp "${pkgdir}/usr/bin/sendmail"
  ln -sv ssmtp "${pkgdir}/usr/bin/newaliases"
  ln -sv ssmtp "${pkgdir}/usr/bin/mailq"

  # set ownership and permissions for configuration and exectuables
  # FS#57787
  chgrp -R mail "${pkgdir}/etc/${pkgname}" \
    "${pkgdir}/usr/bin/"{${pkgname},sendmail,newaliases,mailq}
  chmod -R 640 "${pkgdir}/etc/${pkgname}"
  # setgid executables
  chmod 2755 "${pkgdir}/usr/bin/"{${pkgname},sendmail,newaliases,mailq}
}
