# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Mantas M. <grawity@gmail.com>

pkgname=sssd
pkgver=1.16.0
pkgrel=3
pkgdesc="System Security Services Daemon"
arch=('x86_64')
url="https://pagure.io/SSSD/sssd"
license=('GPL3')
depends=(
  'bind'          # for nsupdate
  'c-ares'
  'cyrus-sasl-gssapi'
  'ding-libs'
  'libnl'
  'libunistring'
  'nss'
  'smbclient'     # for libndr-nbt
  'nfsidmap'
  'http-parser'
  'jansson'
  'curl'
)
makedepends=(
  'docbook-xsl'
  'doxygen'
  'python'
  'python2'
  'samba'         # for libndr-nbt headers
  'systemd'
)
source=("https://releases.pagure.org/SSSD/$pkgname/$pkgname-$pkgver.tar.gz"
        "0007-Update-SMB-IDMAP-INTERFACE-VERSION.patch")
sha1sums=('fff74b3798e163a0fe311bdfc4588524afe6dd87'
          'd89f98e921fee2a492830ca55cc573d0b4dcea53')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  for f in "${source[@]}"; do
    if [[ $f == *.patch ]]; then
      msg2 "Applying $f"
      patch -p1 < "$srcdir/$f"
    fi
  done
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr                                 \
    --sbindir=/usr/bin                            \
    --sysconfdir=/etc                             \
    --localstatedir=/var                          \
    --libexecdir=/usr/lib/sssd                    \
    --datadir=/usr/share                          \
    --enable-pammoddir=/usr/lib/security          \
    --with-initscript=systemd                     \
    --with-os=fedora                              \
    --with-pid-path=/run                          \
    --with-python2-bindings                       \
    --with-python3-bindings                       \
    --with-syslog=journald                        \
    --without-selinux                             \
    --without-semanage                            \
    --with-systemdunitdir=/usr/lib/systemd/system \
    ;
  sed -i '/\<HAVE_KRB5_SET_TRACE_CALLBACK\>/d' config.h
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install
  rm -rf "$pkgdir"/etc/rc.d
  rm -rf "$pkgdir"/lib
  rm -rf "$pkgdir"/run
  rm -f "$pkgdir"/usr/lib/ldb/modules/ldb/memberof.la
  find "$pkgdir"/usr -depth -type d \
    -exec rmdir --ignore-fail-on-non-empty {} \;

  cd "$srcdir"
  rm -rf "$pkgdir/etc/systemd" # remove the drop-in

  sed '1 s/python$/python2/' -i "$pkgdir"/usr/bin/sss_obfuscate
}

# vim: ts=2:sw=2:et:nowrap
