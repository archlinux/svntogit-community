# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=stack
pkgname=(stack haskell-stack)
pkgver=1.1.0
pkgrel=1
pkgdesc="The Haskell Tool Stack"
url="https://github.com/commercialhaskell/stack"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=("ghc=7.10.3" "haskell-aeson" "haskell-ansi-terminal" "haskell-async"
             "haskell-attoparsec" "haskell-base16-bytestring" "haskell-base64-bytestring"
             "haskell-binary-tagged" "haskell-blaze-builder" "haskell-byteable"
             "haskell-conduit" "haskell-conduit-extra" "haskell-cryptohash"
             "haskell-cryptohash-conduit" "haskell-edit-distance" "haskell-either"
             "haskell-enclosed-exceptions" "haskell-errors" "haskell-exceptions"
             "haskell-extra" "haskell-fast-logger" "haskell-filelock" "haskell-fsnotify"
             "haskell-gitrev" "haskell-hashable" "haskell-hastache" "haskell-http-client"
             "haskell-http-client-tls" "haskell-http-conduit" "haskell-http-types"
             "haskell-hpack" "haskell-lifted-base" "haskell-microlens"
             "haskell-monad-control" "haskell-monad-logger" "haskell-mtl"
             "haskell-open-browser" "haskell-optparse-applicative" "haskell-optparse-simple"
             "haskell-path" "haskell-path-io" "haskell-persistent" "haskell-persistent-sqlite"
             "haskell-persistent-template" "haskell-project-template" "haskell-resourcet"
             "haskell-retry" "haskell-safe" "haskell-semigroups" "haskell-split" "haskell-stm"
             "haskell-streaming-commons" "haskell-tar" "haskell-temporary" "haskell-text"
             "haskell-text-binary" "haskell-transformers-base" "haskell-unix-compat"
             "haskell-unordered-containers" "haskell-vector" "haskell-vector-binary-instances"
             "haskell-yaml" "haskell-zip-archive" "haskell-zlib")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('58cce7048438bc452a81384493b4644034d5a0b456acd51bf7c19098a9cf406a')

prepare() {
    sed -e 's/zip-archive >=0.2.3.7 && <0.3,/zip-archive >=0.2.3.7 \&\& <0.4,/' \
        -i ${pkgbase}-${pkgver}/${pkgbase}.cabal
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-disable-git-info -f-integration-tests -f-static
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_stack() {
    depends=('gmp' 'libffi' 'zlib')
    optdepends=('ghc')
    install="stack.install"

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    "${pkgdir}"/usr/bin/stack --bash-completion-script /usr/bin/stack > stack_completion_script
    install -Dm644 stack_completion_script "${pkgdir}/usr/share/bash-completion/completions/stack"
}

package_haskell-stack() {
    pkgdesc="The Haskell Tool Stack (docs and libraries)"
    depends=("${makedepends[@]}" 'stack')
    install="haskell-stack.install"

    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgbase}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
    rm -rf "${pkgdir}/usr/bin"
}
