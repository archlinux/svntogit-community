# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgbase=stack
pkgname=(stack haskell-stack)
pkgver=1.1.2
pkgrel=7
pkgdesc="The Haskell Tool Stack"
url="https://github.com/commercialhaskell/stack"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=("ghc=8.0.1" "haskell-aeson" "haskell-ansi-terminal" "haskell-async"
             "haskell-attoparsec" "haskell-base-compat" "haskell-base16-bytestring"
             "haskell-base64-bytestring" "haskell-binary-tagged" "haskell-blaze-builder"
             "haskell-byteable" "haskell-conduit" "haskell-conduit-extra" "haskell-cryptohash"
             "haskell-cryptohash-conduit" "haskell-edit-distance" "haskell-either"
             "haskell-enclosed-exceptions" "haskell-errors" "haskell-exceptions" "haskell-extra"
             "haskell-fast-logger" "haskell-filelock" "haskell-fsnotify" "haskell-generic-deriving"
             "haskell-gitrev" "haskell-hashable" "haskell-hastache" "haskell-hit"
             "haskell-http-client" "haskell-http-client-tls" "haskell-http-conduit"
             "haskell-http-types" "haskell-hpack" "haskell-lifted-base" "haskell-microlens"
             "haskell-monad-control" "haskell-monad-logger" "haskell-monad-unlift" "haskell-mtl"
             "haskell-open-browser" "haskell-optparse-applicative" "haskell-optparse-simple"
             "haskell-path" "haskell-path-io" "haskell-persistent" "haskell-persistent-sqlite"
             "haskell-persistent-template" "haskell-project-template"
             "haskell-regex-applicative-text" "haskell-resourcet" "haskell-retry" "haskell-safe"
             "haskell-semigroups" "haskell-split" "haskell-stm" "haskell-streaming-commons"
             "haskell-tar" "haskell-temporary" "haskell-text" "haskell-text-binary" "haskell-tls"
             "haskell-transformers-base" "haskell-unix-compat" "haskell-unordered-containers"
             "haskell-vector" "haskell-vector-binary-instances" "haskell-yaml"
             "haskell-zip-archive" "haskell-zlib")
source=("http://hackage.haskell.org/packages/archive/${pkgbase}/${pkgver}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('fc836b24fdeac54244fc79b6775d5edee146b7e552ad8e69596c7cc2f2b10625')

prepare() {
    sed -e 's/zip-archive >=0.2.3.7 && <0.3,/zip-archive >=0.2.3.7 \&\& <0.4,/' \
        -e 's/binary ==0.7.*,/binary >= 0.7 \&\& < 0.9,/' \
        -e 's/generic-deriving >=1.9.0 && <1.10,/generic-deriving >=1.9.0 \&\& <1.11,/' \
        -e 's/process >=1.2.0.0 && <1.3,/process >=1.2.0.0 \&\& <1.5,/' \
        -e 's/template-haskell >=2.9.0.0 && <2.11,/template-haskell >=2.9.0.0 \&\& <2.12,/' \
        -e 's/time >=1.4.2 && <1.6,/time >=1.4.2 \&\& <1.7,/' \
        -e 's/transformers >=0.3.0.0 && <0.5,/transformers >=0.3.0.0 \&\& <0.6,/' \
        -e 's/transformers >=0.4.2.0 && <0.5/transformers >=0.4.2.0 \&\& <0.6/' \
        -e 's/monad-unlift >=0.1.2.0 && <0.2,/monad-unlift >=0.1.2.0 \&\& <0.3,/' \
        -i ${pkgbase}-${pkgver}/${pkgbase}.cabal
}

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-disable-git-info -f-integration-tests -f-static
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package_stack() {
    depends=('gmp' 'libffi' 'zlib')
    optdepends=('ghc')
    install="stack.install"

    cd "${srcdir}/${pkgbase}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    "${pkgdir}"/usr/bin/stack --bash-completion-script /usr/bin/stack > stack_completion_script
    install -Dm644 stack_completion_script "${pkgdir}/usr/share/bash-completion/completions/stack"
}

package_haskell-stack() {
    pkgdesc="The Haskell Tool Stack (docs and libraries)"
    depends=("${makedepends[@]}" 'stack')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgbase}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
    rm -rf "${pkgdir}/usr/bin"
}
