# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kevin Song <kevin.s05@gmail.com>

pkgname=starship
pkgdesc='The cross-shell prompt for astronauts'
pkgver=0.55.0
pkgrel=1
arch=(x86_64)
url=https://starship.rs/
license=(ISC)
depends=(
  gcc-libs
  glibc
  libgit2.so
  openssl
)
optdepends=(
  'noto-fonts-emoji: emoji support for terminals'
  'powerline-fonts: powerline symbols for terminals'
)
makedepends=(
  git
  rust
)
checkdepends=(python)
_tag=09b12a52dd915d1391e593589f10f718a12e0f8d
source=(git+https://github.com/starship/starship.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cargo fetch \
    --locked \
    --manifest-path starship/Cargo.toml
}

pkgver() {
  cd starship
  git describe --tags | sed 's/^v//'
}

build() {
  cargo build \
    --release \
    --frozen \
    --features notify-rust \
    --manifest-path starship/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --features notify-rust \
    --manifest-path starship/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --features notify-rust \
    --offline \
    --no-track \
    --path starship \
    --root "${pkgdir}"/usr
    install -Dm 644 starship/LICENSE -t "${pkgdir}"/usr/share/licenses/starship/
    install -dm 755 "${pkgdir}"/usr/share/{bash-completion/completions,fish/vendor_completions.d,zsh/site-functions}/
    ./starship/target/release/starship completions bash > "${pkgdir}"/usr/share/bash-completion/completions/starship
    ./starship/target/release/starship completions fish > "${pkgdir}"/usr/share/fish/vendor_completions.d/starship.fish
    ./starship/target/release/starship completions zsh > "${pkgdir}"/usr/share/zsh/site-functions/_starship
}

# vim: ts=2 sw=2 et:
