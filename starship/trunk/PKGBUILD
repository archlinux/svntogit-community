# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Kevin Song <kevin.s05@gmail.com>

pkgname=starship
pkgdesc='The cross-shell prompt for astronauts'
pkgver=1.14.2
_tag=c7949706a15736c38eab1477c6dccbae54c608ef
pkgrel=1
arch=(x86_64)
url=https://starship.rs/
_url="https://github.com/$pkgname/$pkgname"
license=(ISC)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  cmake
  git
  rust
)
checkdepends=(python)
optdepends=('ttf-font-nerd: Nerd Font Symbols preset')
source=("git+$_url.git#tag=$_tag")
b2sums=('SKIP')

prepare() {
  cargo fetch \
    --locked \
    --target $CARCH-unknown-linux-gnu \
    --manifest-path starship/Cargo.toml
}

pkgver() {
  cd starship
  git describe --tags | sed 's/^v//'
}

_pkgver() {
  curl -fs https://api.github.com/repos/$pkgname/$pkgname/git/ref/tags/v$pkgver | jq -r .object.sha
}

build() {
  export CARGO_TARGET_DIR=target
  CFLAGS+=" -ffat-lto-objects"
  cargo build \
    --release \
    --frozen \
    --manifest-path starship/Cargo.toml
}

check() {
  cargo test \
    --frozen \
    --manifest-path starship/Cargo.toml
}

package() {
  install -Dm 755 target/release/starship -t "${pkgdir}"/usr/bin/
  install -Dm 644 starship/LICENSE -t "${pkgdir}"/usr/share/licenses/starship/
  install -dm 755 "${pkgdir}"/usr/share/{bash-completion/completions,fish/vendor_completions.d,zsh/site-functions}/
  ./target/release/starship completions bash > "${pkgdir}"/usr/share/bash-completion/completions/starship
  ./target/release/starship completions fish > "${pkgdir}"/usr/share/fish/vendor_completions.d/starship.fish
  ./target/release/starship completions zsh > "${pkgdir}"/usr/share/zsh/site-functions/_starship
}

# vim: ts=2 sw=2 et:
