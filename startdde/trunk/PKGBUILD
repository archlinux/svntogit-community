# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.1.15
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-wm-switcher')
makedepends=('cmake' 'coffee-script' 'deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'go' 'python2' 'git')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/startdde/archive/$pkgver.tar.gz")
sha512sums=('2bcc361b8ced8bb97db300f53964408fc77f866cfa7334bf7061ec7ff9482914295da8fc2212b88673d5db2277c0a04d685446965f27b9058000fd91e90c1b60')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify
}

build() {
  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/lib/systemd "$pkgdir"/usr/lib/systemd
  rmdir "$pkgdir"/lib
  ln -sf ../dde-readahead.service "$pkgdir"/usr/lib/systemd/system/multi-user.target.wants/dde-readahead.service
}
