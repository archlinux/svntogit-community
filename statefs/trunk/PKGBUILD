# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=statefs
pkgver=0.3.31.1
pkgrel=1
pkgdesc="Syntetic filesystem to expose system state"
arch=('x86_64')
url="https://git.merproject.org/mer-core/statefs"
license=('LGPL')
depends=('fuse' 'cor' 'boost-libs')
makedepends=('git' 'cmake' 'boost')
source=("git+https://git.merproject.org/mer-core/statefs.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  mkdir build

  export CXXFLAGS="$CXXFLAGS -pthread"
}

build() {
  cd build
  cmake -DVERSION=$pkgver \
        -DENABLE_USER_SESSION=ON \
        -DSYSTEMD_USER_UNIT_DIR=/usr/lib/systemd/user \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ../statefs
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
