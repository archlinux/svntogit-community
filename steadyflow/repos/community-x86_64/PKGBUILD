# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: M0Rf30
# Contributor: Dan Serban
# Contributor: zbito <ztokarczyk at gmail dot com

pkgname=steadyflow
pkgver=0.2.0
pkgrel=6
pkgdesc="Simple download manager for GNOME"
arch=('x86_64' 'i686')
url='https://launchpad.net/steadyflow'
license=('GPL3')
depends=('gtk3' 'gvfs' 'libgee' 'libnotify')
makedepends=('cmake' 'vala')
source=(https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.xz
        fixes.patch)
sha256sums=('ab1f23d52be78c952979ee0bb1778b8bf4664145c6fb28db219d516aae7dddb6'
            '24b76cd43e1f0a091f9180ec33a9e8c86c1de40eb8a58fcfe1d9e5df79ccc3ef')

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 -i ../fixes.patch
}

build() {
	cd $pkgname-$pkgver
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCOMPILE_GSETTINGS_ON_INSTALL=OFF -DWANT_AYATANA=OFF
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
