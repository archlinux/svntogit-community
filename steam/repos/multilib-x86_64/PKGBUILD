# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>

pkgname=steam
pkgver=1.0.0.56
pkgrel=1
pkgdesc="Valve's digital software delivery system"
url='http://steampowered.com/'
arch=('x86_64')
license=('custom')
depends=('bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl' 'dbus' 'freetype2' 'gdk-pixbuf2' 'ttf-font' 'zenity')
depends_x86_64=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss' 'lib32-alsa-plugins')
optdepends=('steam-native-runtime: steam native runtime support')
source=(http://repo.steampowered.com/${pkgname}/pool/${pkgname}/s/${pkgname}/${pkgname}_${pkgver}.tar.gz
        steam-runtime.sh
        steam-native.sh
        alsa_sdl_audiodriver.patch)
sha512sums=('17fdd26b829a5860520d955326af88ebacda18183fbf9a70cbb38a9c593cc6c3e85237ec0be598e80a3e1d2bc4354a4933e372321ce0b8716ac481cfb3d7a5e0'
            'da40f8e814f547f6a14901d753a2ffed1a4cf83df9200ee86e83a470b57b3103c3fbfb7cc77166ff1db646cbb306d6a41e79450cc1ea113d68a2defc55168ee2'
            '32a12cb011eff4d9d4caf54a5c04f622b6301977258e86597be9c4622f87b0ea329bb31e963ecb56ce738ccc2e4d1068fb2b65bdd5ac1758bcd25296d4dab6d6'
            '5c6117d86cb95071246434851bae3bf7d1c2174b0c8744e33e9c98ccc22efae0c956e1d7e3969c572aa96259b1b8164992d265541bce49a24daa12555d825fd5')
prepare() {
  cd ${pkgname}
  patch -Np1 < "${srcdir}/alsa_sdl_audiodriver.patch"

  # apply roundups for udev rules
  sed -r 's|("0666")|"0660", TAG+="uaccess"|g' -i lib/udev/rules.d/60-steam-input.rules
  sed -r 's|("misc")|\1, OPTIONS+="static_node=uinput"|g' -i lib/udev/rules.d/60-steam-input.rules
  sed -r 's|(, TAG\+="uaccess")|, MODE="0660"\1|g' -i lib/udev/rules.d/60-steam-vr.rules

  # separated runtime/native desktop files
  cp steam{,-native}.desktop
  sed -r 's|(Name=Steam)|\1 (Runtime)|' -i steam.desktop
  sed -r 's|(/usr/bin/steam)|\1-runtime|' -i steam.desktop
  sed -r 's|(Name=Steam)|\1 (Native)|' -i steam-native.desktop
  sed -r 's|(/usr/bin/steam)|\1-native|' -i steam-native.desktop
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install

  install -Dm 755 "${srcdir}/steam-runtime.sh" "${pkgdir}/usr/bin/steam-runtime"
  install -Dm 755 "${srcdir}/steam-native.sh" "${pkgdir}/usr/bin/steam-native"
  install -d "${pkgdir}/usr/lib/steam"
  mv "${pkgdir}/usr/bin/steam" "${pkgdir}/usr/lib/steam/steam"
  ln -sf /usr/bin/steam-runtime "${pkgdir}/usr/bin/steam"

  install -Dm 644 steam-native.desktop -t "${pkgdir}/usr/share/applications"
  install -Dm 644 COPYING steam_subscriber_agreement.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 debian/changelog -t "${pkgdir}/usr/share/doc/${pkgname}"

  # blank steamdeps because apt-get
  ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

  install -Dm 644 lib/udev/rules.d/60-steam-input.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-input.rules"
  install -Dm 644 lib/udev/rules.d/60-steam-vr.rules \
    "${pkgdir}/usr/lib/udev/rules.d/70-steam-vr.rules"
}

# vim: ts=2 sw=2 et:
