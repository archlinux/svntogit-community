# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>
pkgname=steam
pkgver=1.0.0.51
pkgrel=1
pkgdesc="Digital distribution client bootstrap package"
arch=("i686" "x86_64")
url="http://steampowered.com/"
license=('custom')
install=steam.install
source=("http://repo.steampowered.com/$pkgname/pool/$pkgname/s/$pkgname/${pkgname}_$pkgver.tar.gz"
        'alsa_sdl_audiodriver.patch'
        '80-steam-controller-permission.rules')
md5sums=('eb4eeb7b7088f7434d65a09c768f266b'
         'ac0e03d70f1101331598b2b32ed9bac3'
         'aa9b2c4bff29841764ca11ac8fb6446e')
if [[ "$CARCH" == "x86_64" ]]; then
  optdepends=(
  'lib32-mesa-dri: for open source driver users'
  'lib32-catalyst-utils: for AMD Catalyst users'
  'lib32-nvidia-utils: for NVIDIA proprietary blob users'
  'lib32-alsa-plugins: for pulseaudio on some games'
  )
fi
prepare() {
    patch -d "$pkgname" -Np1 -i "$srcdir/alsa_sdl_audiodriver.patch"
}

package() {
  depends=(
    'bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl'
    'dbus' 'freetype2'  'gdk-pixbuf2' 'ttf-font' 'zenity'
  )
  if [[ "$CARCH" == x86_64 ]]; then
    depends+=(lib32-libgl lib32-gcc-libs lib32-libx11 lib32-libxss)
  fi

  # Install license
  make -C "$pkgname" DESTDIR="$pkgdir" install
  
  install -Dm644 "$pkgdir/usr/share/doc/steam/steam_install_agreement.txt" "$pkgdir/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /bin/true "$pkgdir/usr/bin/steamdeps"

  install -Dm644 $srcdir/80-steam-controller-permission.rules $pkgdir/usr/lib/udev/rules.d/80-steam-controller-permission.rules
}
#vim: set ft=PKGBUILD sw=2 ts=2 et
