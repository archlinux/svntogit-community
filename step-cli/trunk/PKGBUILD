# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=step-cli
pkgver=0.15.3
pkgrel=2
pkgdesc="A zero trust swiss army knife for working with X509, OAuth, JWT, OATH OTP, etc."
url="https://github.com/smallstep/cli"
arch=(x86_64)
license=(Apache)
depends=(glibc)
makedepends=(go)
optdepends=("step-ca: setup CA/SSO/ACME server")
source=(https://github.com/smallstep/cli/releases/download/v${pkgver}/step-cli_${pkgver}.tar.gz
        "patch-go-sum.patch")
sha512sums=('0fd97fda7995a78a1c0e4d224175746d0a4d7bd3f8b97a581963b130500a0e6d44546c03d4abbe753bc1f7c03c7d7fd9c496b7d95f5070d35c7d6ad8a4ed9338'
            '62706fed223850eef3ee85f1245806caa31ffd1612f57428191f98c0ab04b3fd94176bf150df71fc500fb87c123aa899c0b533097994da4105d1c3d04515d699')

prepare() {
  sed -i "s/step/${pkgname}/g" "autocomplete/zsh_autocomplete"
  patch -p1 -i ./patch-go-sum.patch
}

build() {
  BUILD_DATE="$(date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +%Y-%m-%d)"

  go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" -X \"main.Version=${pkgver}\" -X \"main.BuildTime=${BUILD_DATE}\"" \
      -o step-cli cmd/step/main.go
}

check() {
  go test ./...
}

package() {
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 "autocomplete/bash_autocomplete" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 "autocomplete/zsh_autocomplete" "$pkgdir/usr/share/zsh/site-functions/_${pkgname}"
}
