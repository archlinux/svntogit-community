# Maintainer: Jakob Gruber <jakob.gruber@gmail.com>

pkgname=stone-soup
pkgver=0.27.1
pkgrel=3
pkgdesc='Open-source, single-player, role-playing roguelike game of exploration and treasure-hunting'
arch=('x86_64')
url='https://crawl.develz.org/'
depends=('lua51' 'ncurses' 'sdl2_image' 'sqlite' 'glu' 'freetype2' 'ttf-dejavu' 'python-yaml')
makedepends=('bison' 'flex' 'gendesk' 'mesa' 'pngcrush')
conflicts=('crawl' 'stone-soup-tile' 'stone-soup-git')
license=(custom GPL2)
install=stone-soup.install
source=("stone-soup-$pkgver.tar.xz::https://github.com/crawl/crawl/releases/download/$pkgver/stone_soup-$pkgver-nodeps.tar.xz")
md5sums=('ce8e390097fbfb3f56e8f6744ab64736')

package() {
    gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
            --exec 'crawl-tiles' --name 'Dungeon Crawl Stone Soup' PKGBUILD

    cd "stone_soup-${pkgver}/source"

    sed -i 's/INSTALL_UGRP := games:games/INSTALL_UGRP := root:root/' Makefile

    # first build and install tiles version

    make DESTDIR="${pkgdir}" \
         SAVEDIR="~/.crawl/" \
         DATADIR="/usr/share/${pkgname}/data" \
         TILES=y \
         EXTERNAL_LDFLAGS=$LDFLAGS \
         prefix=/usr \
         bin_prefix=bin \
         install

    # rename tiles executable to avoid clashing with console crawl

    mv "${pkgdir}/usr/bin/crawl" "${pkgdir}/usr/bin/crawl-tiles"

    # then build and install console version

    make DESTDIR="${pkgdir}" \
         SAVEDIR="~/.crawl/" \
         DATADIR="/usr/share/${pkgname}/data" \
         EXTERNAL_LDFLAGS=$LDFLAGS \
         prefix=/usr \
         bin_prefix=bin \
         install

    # install crawl license

    install -D -m644 ../LICENSE \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    # install icon and .desktop file

    install -D -m644 dat/tiles/stone_soup_icon-32x32.png \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -D -m644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
