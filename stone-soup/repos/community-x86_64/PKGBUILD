# Maintainer: Ivy Foster <iff@archlinux.org>
# Contributor: Jakob Gruber <jakob.gruber@gmail.com>

pkgname=stone-soup
pkgdesc='Open-source, single-player, role-playing roguelike game of exploration and treasure-hunting'
url='https://crawl.develz.org/'
license=(custom GPL2)

pkgver=0.28.0
pkgrel=1
arch=(x86_64)

# line-by-line: both versions, graphical, ncurses
depends=(
	lua51 python-yaml sqlite \
	freetype2 glu libpng sdl2_image ttf-dejavu \
	ncurses
)
makedepends=(bison flex gendesk mesa pngcrush)
conflicts=(crawl stone-soup-tile stone-soup-git)

source=("stone-soup-$pkgver.tar.xz::https://github.com/crawl/crawl/releases/download/$pkgver/stone_soup-$pkgver-nodeps.tar.xz")
# checksums provided by packager; take with a grain of salt
md5sums=(c5fd0d441a39b054a3968bf548db9f9a)

# Version 0.28.0 won't build without this
options=(!lto)

# make install actually rebuilds everything. Yikes.
# Thus the lack of a build() fucntion.
package() {
	gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
			--exec 'crawl-tiles' --name 'Dungeon Crawl Stone Soup' PKGBUILD

	cd "stone_soup-${pkgver}/source"

	sed -i 's/INSTALL_UGRP := games:games/INSTALL_UGRP := root:root/' Makefile

	# first build and install tiles version

	make DESTDIR="${pkgdir}" \
		SAVEDIR="~/.crawl/" \
		DATADIR="/usr/share/${pkgname}/data" \
		TILES=y \
		EXTERNAL_LDFLAGS="$LDFLAGS" \
		prefix=/usr \
		bin_prefix=bin \
		install

	# rename tiles executable to avoid clashing with console crawl

	mv "${pkgdir}/usr/bin/crawl" "${pkgdir}/usr/bin/crawl-tiles"

	# then build and install console version

	make DESTDIR="${pkgdir}" \
		SAVEDIR="~/.crawl/" \
		DATADIR="/usr/share/${pkgname}/data" \
		EXTERNAL_LDFLAGS="$LDFLAGS" \
		prefix=/usr \
		bin_prefix=bin \
		install

	# install crawl license

	install -D -m644 ../LICENSE \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	# install icon and .desktop file

	install -D -m644 dat/tiles/stone_soup_icon-32x32.png \
		"${pkgdir}/usr/share/pixmaps/${pkgname}.png"
	install -D -m644 "${srcdir}/${pkgname}.desktop" \
		"${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
