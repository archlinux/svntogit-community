# Maintainer: Jakob Gruber <jakob.gruber@gmail.com>

pkgname=stone-soup
pkgver=0.9.0
pkgrel=2
pkgdesc="An open-source, single-player, role-playing roguelike game of exploration and treasure-hunting"
arch=('i686' 'x86_64')
url='http://crawl.develz.org/'
depends=('lua' 'ncurses' 'sdl_image' 'sqlite3' 'libgl' 'mesa' 'freetype2')
makedepends=('bison' 'flex')
conflicts=('crawl' 'stone-soup-tile' 'stone-soup-git')
license=('custom')
install=('stone-soup.install')
source=("http://downloads.sourceforge.net/sourceforge/crawl-ref/stone_soup-${pkgver}-nodeps.tar.bz2"
        "${pkgname}.desktop")
md5sums=('3f73fb051215df3633a554d81e67a452'
         'afdacb4c39342e75674efb88eeb3da21')

build() {

    cd "${srcdir}/stone_soup-${pkgver}/source"

    # adjust makefile to use /usr/bin, owner root:root,
    # disable setgid on executable

    sed -i 's|bin_prefix    := bin|bin_prefix    := usr/bin|' makefile
    sed -i 's/INSTALL_UGRP := games:games/INSTALL_UGRP := root:root/' makefile
    sed -i 's|MCHMOD := 2755|MCHMOD := 755|' makefile

    # first build and install tiles version

    make DESTDIR="${pkgdir}" \
         SAVEDIR="~/.crawl/" \
         DATADIR="/usr/share/${pkgname}/data" \
         USE_UNICODE=y \
         TILES=y \
         install

    # rename tiles executable to avoid clashing with console crawl

    mv "${pkgdir}/usr/bin/crawl" "${pkgdir}/usr/bin/crawl-tiles"

    # then build and install console version

    make DESTDIR="${pkgdir}" \
         SAVEDIR="~/.crawl/" \
         DATADIR="/usr/share/${pkgname}/data" \
         USE_UNICODE=y \
         install

    # install crawl license

    install -D -m644 ../licence.txt \
        "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"

    # install icon and .desktop file

    install -D -m644 dat/tiles/stone_soup_icon-32x32.png \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -D -m644 "${srcdir}/${pkgname}.desktop" \
        "${pkgdir}/usr/share/applications/${pkgname}.desktop"

}

# vim: ts=4:sw=4:expandtab
