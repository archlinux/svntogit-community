# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=strawberry
pkgver=0.6.11
pkgrel=2
pkgdesc="A music player aimed at audio enthusiasts and music collectors"
arch=(x86_64)
url="https://www.strawbs.org/"
license=(GPL3)
depends=(chromaprint protobuf gst-plugins-base gst-plugins-good qt5-x11extras udisks2 libcdio libgpod libmtp fftw)
makedepends=(cmake boost qt5-tools)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
source=("https://github.com/jonaski/strawberry/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('84b71b33b5f80b7e86682e49f15289c2c00ab89d47e3722f762dda0d6965a201')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SYSTEM_TAGLIB=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
