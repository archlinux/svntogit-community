# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=stubby
pkgver=0.4.0
pkgrel=2
pkgdesc="DNS Privacy stub resolver"
arch=(x86_64)
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=(BSD)
backup=(etc/stubby/stubby.yml)
depends=(getdns libsystemd libyaml)
makedepends=(cmake)
source=(https://github.com/getdnsapi/stubby/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        stubby-fix-resolvconf-parsing.patch::https://github.com/getdnsapi/stubby/commit/1c037fad5a21d09e63ad32c77ec1f432ffaa9ef8.patch
        stubby-fix-tls_curves_list-parsing.patch::https://patch-diff.githubusercontent.com/raw/getdnsapi/stubby/pull/293.patch)
sha256sums=('8e6a4ba76f04b23612d58813c4998141b0cc6194432d87f8653f3ba5cf64152a'
            '5751a1a0b6dc1f5651b21d857d9164a8be249dfcb2a15ca555d792361dc6ccbc'
            '285c1c5fb2940de93edc0eabd470eeeab61e505dca1ca3da539526806ab998fb')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/^# appdata_dir:/appdata_dir:/g' -e 's:/etc/unbound/getdns-root.key:/etc/trusted-key.key:g' -i stubby.yml.example
  patch -p1 -i ../stubby-fix-resolvconf-parsing.patch
  patch -p1 -i ../stubby-fix-tls_curves_list-parsing.patch
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_BUILD_TYPE=Release
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  rmdir "${pkgdir}"/var{/run,}
  cd ${pkgname}-${pkgver}
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm644 systemd/${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
