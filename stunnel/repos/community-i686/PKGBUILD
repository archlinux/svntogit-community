# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=stunnel
pkgver=4.32
pkgrel=2
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL."
arch=('i686' 'x86_64')
url="http://www.stunnel.org"
license=('GPL')
depends=('openssl' 'tcp_wrappers')
install=stunnel.install
options=('!libtool')
source=(http://www.stunnel.org/download/stunnel/src/${pkgname}-${pkgver}.tar.gz
        'stunnel-nopem.patch'
        'stunnel')
md5sums=('72379c615c5a4986c7981d0941ed2e6b'
         'aec994e209405420eed197fd15ca09b0'
         'cb647c71ff4cb1e035b6e515d5f13ebf')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# don't create a certificate...
	patch -p1 -i ${srcdir}/stunnel-nopem.patch || return 1

	./configure --prefix=/usr \
	    --sysconfdir=/etc \
	    --with-tcp-wrappers \
	    --localstatedir=/var
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	for i in ca.pl importCA.sh; do
	    install -Dm755 tools/$i ${pkgdir}/usr/share/stunnel/$i
	done
	install -Dm755 ${srcdir}/stunnel ${pkgdir}/etc/rc.d/stunnel
	sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
	    -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
	    -e "s:nobody:stunnel:g" -e "s:nogroup:stunnel:g" \
	    -i ${pkgdir}/etc/stunnel/stunnel.conf-sample
	install -Dm644 tools/stunnel.cnf \
	    ${pkgdir}/etc/stunnel/stunnel.cnf || return 1
}
