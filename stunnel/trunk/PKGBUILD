# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=stunnel
pkgver=5.48
pkgrel=2
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL"
arch=('x86_64')
url="https://www.stunnel.org/"
license=('GPL')
depends=('openssl' 'libnsl')
makedepends=('systemd')
source=("https://www.stunnel.org/downloads/archive/5.x/$pkgname-$pkgver.tar.gz"{,.asc}
        'Makefile.patch'
        'stunnel.sysusers.conf'
        'stunnel.tmpfiles.conf')
sha512sums=('5e6669ecd6e9b49aa6ef82b9a4dc6a2193c975eef85262aba70c7f264ef1b4d15dc287a2baa94b71be063deddcd07a20a5347ed5280f044fc6f68c61429a24fa'
            'SKIP'
            '2129af2b2594473c48c565bdc714774b382c40ac1926d6ccbc07bbc29d006b89c9dbae71496b404ceeeeaaf22cf40055d557d8b37a6aea20a228699721b79db1'
            '8a7199b94cda5da8c0b99b25daf4bead1398a28975eb6813139744a8a962db3aa058bc4c9e7ee4121a52c62c2e2c2ed46d1739414927b210e0ac68da48a7ef5e'
            '8e06ec39547ce5991217e551e563f5d212742afda94da801ec266c632375aae489594d14815fe596319d9881eb5a58e32bf05baa69f3a9d1bae554419f5ac1bb')
validpgpkeys=('AC915EA30645D9D3D4DAE4FEB1048932DD3AAAA3') # Micha≈Ç Trojnara

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # don't create a certificate...
  patch -p0 <"$srcdir/Makefile.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --enable-ipv6 \
    --disable-libwrap

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 tools/stunnel.service "$pkgdir/usr/lib/systemd/system/stunnel.service"
  install -Dm644 "$srcdir/stunnel.sysusers.conf" "$pkgdir/usr/lib/sysusers.d/stunnel.conf"
  install -Dm644 "$srcdir/stunnel.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/stunnel.conf"

  sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
      -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
      -e "s:no\(body\|group\):stunnel:g" \
      -i "$pkgdir/etc/stunnel/stunnel.conf-sample"
}
