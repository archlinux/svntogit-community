# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=2.0
pkgrel=1
pkgdesc='Stunt Rally game with track editor, based on VDrift'
arch=('x86_64' 'i686')
license=('GPL3')
url='http://code.google.com/p/vdrift-ogre'
depends=('libvorbis' 'mygui' 'sdl' 'enet' 'hicolor-icon-theme' 'libxcursor' 'stuntrally-data')
makedepends=('cmake' 'boost')
install=stuntrally.install
source=("$pkgname-$pkgver.tar.gz::https://nodeload.github.com/$pkgname/$pkgname/tar.gz/$pkgver")
sha256sums=('ef3e645e4911f37c28925e3339c39555e849b7ac13511f009742030f531eed68')

build() {
  cd "$srcdir/stuntrally-$pkgver/"

  rm -rf build
  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DSHARE_INSTALL="share/stuntrally"
  make
}

package() {
  cd "$srcdir/stuntrally-$pkgver/build/"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/stuntrally/"
}

# vim:set ts=2 sw=2 et:
