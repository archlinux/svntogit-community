# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=2.6
pkgrel=3
pkgdesc='Stunt Rally game with track editor, based on VDrift'
arch=('x86_64' 'i686')
license=('GPL3')
url='http://stuntrally.tuxfamily.org'
depends=('libvorbis' 'mygui' 'sdl2' 'enet' 'gtk-update-icon-cache' 'stuntrally-data' 'bullet' 'openal')
makedepends=('cmake' 'boost' 'git')
install=stuntrally.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/stuntrally/stuntrally/archive/${pkgver}.tar.gz")
sha256sums=('8cc309d27d26f78dcc596440547d68b5e41d02ef830df2d52d38611d45ebbaca')

build() {
  cd "$srcdir/stuntrally-$pkgver/"

  rm -rf build
  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX="/usr" \
    -DSHARE_INSTALL="share/stuntrally"
  make
}

package() {
  cd "$srcdir/stuntrally-$pkgver/build/"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/stuntrally/"
}

# vim:set ts=2 sw=2 et:
