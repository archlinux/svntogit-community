# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=stylish-haskell
pkgver=0.6.5.0
pkgrel=6
pkgdesc="Haskell code prettifier"
url="https://github.com/jaspervdj/stylish-haskell"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=8.0.1" "haskell-aeson" "haskell-src-exts" "haskell-mtl"
             "haskell-optparse-applicative" "haskell-strict" "haskell-syb" "haskell-yaml")
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('aeee182f8b6a9492eedd12a45cd9a4abb677e95e1789ddd8681e699f27a5ea78')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}

    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
