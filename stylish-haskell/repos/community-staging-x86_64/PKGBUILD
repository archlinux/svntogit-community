# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=stylish-haskell
pkgver=0.10.0.0
pkgrel=7
pkgdesc="Haskell code prettifier"
url="https://github.com/jaspervdj/stylish-haskell"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-file-embed' 'haskell-optparse-applicative'
         'haskell-src-exts' 'haskell-strict' 'haskell-syb' 'haskell-hsyaml' 'haskell-hsyaml-aeson')
makedepends=('ghc' 'haskell-hunit' 'haskell-random' 'haskell-test-framework'
             'haskell-test-framework-hunit')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ec9675a49f26fa5b7935ad65e35b78c13cbbd14637a638b947791ebbffb4f119f076f3c55d7de7f3e7658f9f509c3597b9d82a1cb018784a6c3adfb6c3dc413a')

prepare() {
    cd $pkgname-$pkgver
    sed -i -e '/semigroups/d' $pkgname.cabal
}

build() {
    cd $pkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-old_base --ghc-option='-pie'
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $pkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
