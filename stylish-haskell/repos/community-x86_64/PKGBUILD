# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=stylish-haskell
pkgver=0.5.14.4
pkgrel=1
pkgdesc="Haskell code prettifier"
url="https://github.com/jaspervdj/stylish-haskell"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=7.10.3" "haskell-aeson" "haskell-cmdargs" "haskell-src-exts" "haskell-mtl"
             "haskell-strict" "haskell-syb" "haskell-yaml")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('023da2a5911868e6f00344e45314c79b8593e29f1e4ed75a791953f4a1bf6676')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}

    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
