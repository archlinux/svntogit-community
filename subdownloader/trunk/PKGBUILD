# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Thomas Jost <thomas.jost@gmail.com>

pkgname=subdownloader
pkgver=2.0.18
pkgrel=6
pkgdesc="Automatic download/upload of subtitles using fast hashing"
arch=('any')
url="http://www.subdownloader.net/"
license=('GPL3')
depends=('python2-pyqt4')
source=("https://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        "follow-download-links.patch")
sha512sums=('ecddf606ce374af43fff39c225ee896af4d8bd2384a2f300dcbb4b443377807edd5233a0531e95d338b0f9c84a274b3afd4109d9216510a5a4bc8fa17485b999'
            '09a03b7b06813f887e18b6774fc2d5f1d477b88fe98785fd03d85bcd33b4aedc6ee1cf233e8af7dd3ca7a524ca14e3a1f8d5e7163e803b0f534023f9b5ded5e9')

prepare(){
  cd "${pkgname}-${pkgver}"

  # See https://bugs.archlinux.org/task/48402
  patch -Np0 -i "$srcdir/follow-download-links.patch"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -e 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' \
        -e 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' \
        -i "${file}"
  done

  # remove empty, useless dir
  rmdir modules/mmpython/doc
}

package() {
  cd "${pkgname}-${pkgver}"

  # prepare dirs
  install -d "$pkgdir/usr/share/$pkgname" \
    "$pkgdir/usr/bin" \
    "$pkgdir/usr/share/man/man1/"

  # man page
  install -vDm644 ${pkgname}.1 "$pkgdir/usr/share/man/man1/${pkgname}.1"

  # freedesktop.org
  install -vDm644 "${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -vDm644 "gui/images/${pkgname}.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"

  # runtime and executables
  cp -av {FileManagement,gui,languages,locale,modules} \
    "${pkgdir}/usr/share/${pkgname}/"
  install -vDm 755 run.py "${pkgdir}/usr/share/${pkgname}/run.py"
  ln -s "/usr/share/${pkgname}/run.py" \
    "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
