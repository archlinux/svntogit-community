# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface-libdc
_pkgname=libdivecomputer-subsurface-branch
pkgver=4.7.8
pkgrel=1
pkgdesc='Library for communication with dive computers'
url='https://github.com/Subsurface-divelog/libdc'
license=('LGPL')
arch=('x86_64')
source=("https://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha256sums=('3ec4d68061f8a4ad988b2cefaadf1ea8f1a82adf84f146b9638185851180cdbe')

conflicts=('libdivecomputer')
provides=('libdivecomputer')
replaces=('libdivecomputer')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	autoreconf --install
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
