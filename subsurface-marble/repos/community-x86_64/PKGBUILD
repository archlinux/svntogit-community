# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface-marble
_pkgname=marble-subsurface-branch
pkgver=4.5.6
pkgrel=1
pkgdesc='Virtual Globe'
url='http://git.subsurface-divelog.org/'
license=('GPL' 'LGPL' 'FDL')
arch=('i686' 'x86_64')
makedepends=('cmake')
depends=('qt5-'{script,svg,tools,webkit} 'marble-data')
source=("http://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha1sums=('2112aeaf4562dc29bb942f18faea894544a3a6d8')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DQT5BUILD=ON \
		-DQTONLY=ON \
		-DBUILD_MARBLE_APPS=OFF \
		-DBUILD_MARBLE_EXAMPLES=OFF \
		-DBUILD_MARBLE_TESTS=OFF \
		-DBUILD_MARBLE_TOOLS=OFF \
		-DBUILD_TESTING=OFF \
		-DWITH_DESIGNER_PLUGIN=OFF \
		-DBUILD_WITH_DBUS=OFF \
		.
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}/usr"
	mv include subsurface
	install -d include
	mv subsurface include
        rm -fr share/{appdata,icons}

	# Provided by marble-data, avoids conflicts.
	rm share/marble/cmake/FindMarble.cmake
	rm -fr share/marble/data

	# Conflicts with libastro
	rm lib/libastro.so{,.1,.0.17.20}
}
