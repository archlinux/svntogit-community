# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface-marble
pkgver=4.4.1
pkgrel=2
pkgdesc='Virtual Globe'
url='http://git.subsurface-divelog.org/'
license=('GPL' 'LGPL' 'FDL')
arch=('i686' 'x86_64')
makedepends=('cmake')
depends=('qt5-'{script,svg,tools,webkit} 'marble-data')
source=("http://subsurface-divelog.org/downloads/${pkgname}-${pkgver}.tgz"
        'qt55.patch')
sha1sums=('4867a8219cd2dcf478414ad91ff4479ad2afc1a3'
          'ae862f9a740b4ccffb7a20f7c3711798e90cec14')

prepare() {
	cd "${srcdir}/${pkgname}"
	patch -p1 -i ../qt55.patch
}

build() {
	cd "${srcdir}/${pkgname}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DQT5BUILD=ON \
		-DQTONLY=ON \
		-DBUILD_MARBLE_APPS=OFF \
		-DBUILD_MARBLE_EXAMPLES=OFF \
		-DBUILD_MARBLE_TESTS=OFF \
		-DBUILD_MARBLE_TOOLS=OFF \
		-DBUILD_TESTING=OFF \
		-DWITH_DESIGNER_PLUGIN=OFF \
		-DBUILD_WITH_DBUS=OFF \
		.
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}/usr"
	mv include subsurface
	install -d include
	mv subsurface include
        rm -fr share/{appdata,icons}

	# Provided by marble-data, avoids conflicts.
	rm -fr share/marble/data
}
