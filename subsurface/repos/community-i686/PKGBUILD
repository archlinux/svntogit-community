# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.4.0
pkgrel=1
pkgdesc='Divelog program'
url='http://subsurface-divelog.org/'
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('git' 'cmake' 'asciidoc')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusbx' 'libgit2'
         'libdivecomputer' 'qt5-'{script,svg,tools,webkit})
source=("http://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz"
        'git://git.subsurface-divelog.org/marble#commit=aa18d0af6951debf3a3a90916f3f772784dbf326')
sha1sums=('6598c1f894bd0334d8fa3dfec13adfadc041d56c'
          'SKIP')

install=install

build() {
	# subsurface is Qt5-only so we need to build marble against it;
	# while we're at it we use Subsurface's custom marble branch.

	cd "${srcdir}/marble"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DQT5BUILD=ON \
		-DQTONLY=ON \
	        -DBUILD_MARBLE_APPS=OFF \
		-DBUILD_MARBLE_EXAMPLES=OFF \
	        -DBUILD_MARBLE_TESTS=OFF \
		-DBUILD_MARBLE_TOOLS=OFF \
	        -DBUILD_TESTING=OFF \
		-DWITH_DESIGNER_PLUGIN=OFF \
	        -DBUILD_WITH_DBUS=OFF \
		.
	make
	install -d pkg
	make DESTDIR="`pwd`/pkg" install

	cd "${srcdir}/${_pkgname}-${pkgver}"
	qmake-qt5 \
		-config release \
		SPECIAL_MARBLE_PREFIX=1 \
		LIBMARBLEDEVEL=../marble/pkg \
		LIBS+="-L`pwd`/../marble/pkg/usr/lib" \
		INCLUDEPATH+="`pwd`/../marble/pkg/usr/include" \

	make
}

package() {
	cd "${srcdir}/marble"
	make DESTDIR="${pkgdir}" install

	# remove conflicts with kdeedu-marble
	rm -fr "${pkgdir}"/usr/{include,share/{appdata,icons}}

	cd "${srcdir}/${_pkgname}-${pkgver}"
	make INSTALL_ROOT="${pkgdir}" install
}
