# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.5.1
pkgrel=1
pkgdesc='Divelog program'
url='http://subsurface-divelog.org/'
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('cmake' 'asciidoc')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusb' 'libgit2'
         'subsurface-libdc' 'subsurface-marble'
         'qt5-connectivity' 'grantlee-qt5')
source=("http://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha1sums=('a595c479d67fce28c4fbe19b4e85e93c8e11fc12')

install=install

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed 's:<marble:<subsurface/marble:g' -i qt-ui/globe.*
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DMARBLE_LIBRARIES=/usr/lib/libssrfmarblewidget.so \
		-DUSE_LIBGIT23_API=1 \
		.
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
