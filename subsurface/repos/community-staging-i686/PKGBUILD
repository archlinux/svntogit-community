# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.6.3
pkgrel=6
pkgdesc='Divelog program'
url='https://subsurface-divelog.org/'
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('cmake' 'asciidoc')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusb' 'libgit2'
         'subsurface-libdc' 'subsurface-marble'
         'qt5-connectivity' 'grantlee')
source=("https://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha256sums=('44c64397d5225522be0734b126c479566b4adfed6de1b1f95b978a02e4a3958d')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed 's:tree_flags:flags:g' -i core/git-access.c
	sed 's:GIT_MERGE_TREE_FIND_RENAMES:GIT_MERGE_FIND_RENAMES:g' -i core/git-access.c
	sed 's:<marble:<subsurface/marble:g' -i desktop-widgets/globe.*
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	install -d build
	cd build
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DMARBLE_LIBRARIES=/usr/lib/libssrfmarblewidget.so \
		-DMARBLE_INCLUDE_DIR=/usr/include/subsurface/marble \
		..
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	cd build
	make DESTDIR="${pkgdir}" install
}
