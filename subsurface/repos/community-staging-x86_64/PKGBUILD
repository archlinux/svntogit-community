# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.4.2
pkgrel=2
pkgdesc='Divelog program'
url='http://subsurface-divelog.org/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusb' 'libgit2'
         'subsurface-libdc' 'subsurface-marble')
source=("http://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz"
        'libgit2.patch')
sha1sums=('40dd3e3693170219d58dd9c1484274f724bedbea'
          'd36e5d3182254a0a5d1940308a7878febdcfdef8')

install=install

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -p1 -i ../libgit2.patch
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	qmake-qt5 \
		-config release \
		SPECIAL_MARBLE_PREFIX=1 \
		INCLUDEPATH+='/usr/include/subsurface' \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make INSTALL_ROOT="${pkgdir}" install
}
