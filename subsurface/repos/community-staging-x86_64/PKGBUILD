# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.4.1
pkgrel=4
pkgdesc='Divelog program'
url='http://subsurface-divelog.org/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusb' 'libgit2'
         'subsurface-libdc' 'subsurface-marble')
source=("http://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz"
        'revert-detect.patch'
        'qt-5.4.1.patch')
sha1sums=('7c33c734c59ced134f10c242c5f51b6d48cb3a0d'
          '06bee56a7d1c9b93628147f27febbd07e5745e46'
          '0f8ee15029561ff3cca86b937e43e6384df99218')

install=install

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -p1 -R -i ../revert-detect.patch
	patch -p1 -i ../qt-5.4.1.patch
}
  
build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	qmake-qt5 \
		-config release \
		SPECIAL_MARBLE_PREFIX=1 \
		INCLUDEPATH+='/usr/include/subsurface' \

	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make INSTALL_ROOT="${pkgdir}" install
}
