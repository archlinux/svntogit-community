# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface
_pkgname=Subsurface
pkgver=4.5.6
pkgrel=2
pkgdesc='Divelog program'
url='http://subsurface-divelog.org/'
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('cmake' 'asciidoc')
depends=('libzip' 'libxml2' 'libxslt' 'sqlite' 'libusb' 'libgit2'
         'subsurface-libdc' 'subsurface-marble'
         'qt5-connectivity' 'grantlee-qt5')
source=("http://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha1sums=('db74614fe66da19f8a07a375551e1ef032291b4a')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed 's:<marble:<subsurface/marble:g' -i qt-ui/globe.*
	sed 's:GIT_MERGE_TREE_FIND_RENAMES:GIT_MERGE_FIND_RENAMES:g' -i git-access.c
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	cmake \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DMARBLE_LIBRARIES=/usr/lib/libssrfmarblewidget.so \
		.
	make
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
