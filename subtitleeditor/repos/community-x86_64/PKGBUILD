# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=subtitleeditor
pkgver=0.52.1
pkgrel=3
pkgdesc="A GTK+3 tool to edit subtitles for GNU/Linux/*BSD"
arch=('i686' 'x86_64')
url="http://home.gna.org/subtitleeditor/"
license=('GPL3')
depends=('desktop-file-utils' 'enchant' 'gst-plugins-base' 'gstreamermm' 'gst-libav'
         'gtkmm3' 'hicolor-icon-theme' 'libsigc++' 'libxml++' 'xdg-utils')
makedepends=('intltool' 'patch')
changelog=$pkgname.changelog
source=(http://download.gna.org/$pkgname/0.52/$pkgname-$pkgver.tar.gz
        cxx11-fixes.patch)
sha256sums=('5b3e5fc1f12bcf0d4bcf5014249b6832181bd3b513ac04ee665ec979441412d5'
            '6e28116cc79553349eecde84a1f45dd50e4fabea58cdc0f809e4be4711ea6d76')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np1 -i ../cxx11-fixes.patch
  sed -i -e 's/-ansi -Wall -Wextra -Wconversion/-std=c++11/' configure
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package () {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
