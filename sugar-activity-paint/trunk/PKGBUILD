# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-paint
_pkgname=Paint
pkgver=66
pkgrel=1
pkgdesc="Sugar activity to create paintings or drawings"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Paint"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/honey/$_pkgname/$_pkgname-$pkgver.tar.bz2
        fix-drawing-area.patch)
sha256sums=('f9da78b564d22cbf53be69d715a69c582064da56dce9ed5c85a2c3e165e0ee08'
            '023c0eefe5168bab0152fe7c6df391d3dccabf806b17bd12ed3539d01e8c1066')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
  patch -Np1 -i ../fix-drawing-area.patch

  rm -r fill
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
