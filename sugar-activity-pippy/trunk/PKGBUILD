# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-pippy
_pkgname=Pippy
pkgver=71
pkgrel=1
pkgdesc="Sugar activity to learn and develop in Python programming language"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Pippy"
license=('GPL')
groups=('sugar-fructose')
depends=('python2-pybox2d' 'python2-pygame' 'sugar' 'vte3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('40d5fb56a2ee16793efaef91e0d89239c3d729c2a17aab1672e5c3c085afa6c5')

prepare() {
  cd $_pkgname-$pkgver
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python %s; sleep 1/python2 %s; sleep 1/' pippy_app.py
  rm -r library/box2d_*/ library/Box2D.py library/LICENSE
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
