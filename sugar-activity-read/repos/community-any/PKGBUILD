# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-read
_pkgname=Read
pkgver=117
pkgrel=1
pkgdesc="Sugar activity to read books"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Read"
license=('GPL')
groups=('sugar-fructose')
depends=('evince' 'python2-beautifulsoup3' 'sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('8cd39b4060b7a36a9029eec1b771ce4831d94ffffdeb3074be3d2b7965f293da')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' readdialog.py setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
