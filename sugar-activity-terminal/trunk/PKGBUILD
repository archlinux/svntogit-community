# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-terminal
_pkgname=terminal-activity
pkgver=45.2
pkgrel=1
pkgdesc="Terminal emulator for Sugar"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Terminal"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3' 'vte3')
source=($pkgname-$pkgver.tar.gz::https://github.com/sugarlabs/$_pkgname/archive/v$pkgver.tar.gz)
sha256sums=('d7a9f03a0340e8734f4c3ddf4370d1717744efc27b27616b40b9027e75174f9e')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' helpbutton.py setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
