# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-write
_pkgname=Write
pkgver=98.3
pkgrel=1
pkgdesc="Sugar activity to edit texts"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Write"
license=('GPL')
groups=('sugar-fructose')
depends=('abiword' 'gconf' 'sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('55caf7168f81b955a69498b5151d44c2923192048b34cc8bee4260d814cfb7e9')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' gridcreate.py setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
