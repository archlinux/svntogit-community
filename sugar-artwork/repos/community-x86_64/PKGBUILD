# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-artwork
pkgver=0.119
pkgrel=1
pkgdesc="Sugar icons and themes"
arch=('x86_64')
url="https://sugarlabs.org/"
license=('LGPL')
makedepends=('gtk2' 'gtk3' 'icon-naming-utils' 'python-empy' 'xorg-xcursorgen')
optdepends=('gtk2: GTK+ 2 theme'
            'gtk3: GTK+ 3 theme')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('01a027b64c44e4f5fa27933d803ce8358d22c04bc882b689e797d36a85f13718')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
