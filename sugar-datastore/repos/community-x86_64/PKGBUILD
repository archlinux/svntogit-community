# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-datastore
pkgver=0.112
pkgrel=1
pkgdesc="Sugar datastore service"
arch=('i686' 'x86_64')
url="https://sugarlabs.org/"
license=('GPL')
depends=('python2-dbus' 'python2-gobject' 'python2-xapian')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('034946ce20b44054acdbcc47a09cccd03cf8ef29dab885f8da51f3847751775f')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/copy-to-journal
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
