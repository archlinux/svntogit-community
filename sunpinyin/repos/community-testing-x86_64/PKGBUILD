# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >

pkgname=sunpinyin
pkgver=2.0.3.20150706
_commit=cf1810220bf3d3f5a8b990b0a9d3e9df21b1942c
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="Statistical Language Model based pinyin IME by Sun"
license=('LGPL')
url="http://sunpinyin.googlecode.com"
depends=('sqlite' 'make' 'gcc-libs')
optdepends=('sunpinyin-data: Statistical language model data from open-gram project for sunpinyin')
makedepends=('git' 'scons' 'intltool')
source=("git://github.com/sunpinyin/sunpinyin.git#commit=$_commit")
md5sums=("SKIP")

prepare() {
  cd "$srcdir/$pkgname"
  sed -i -e "1s|python|python2|" python/*.py python/importer/*.py
}

build() {
  cd "$srcdir/$pkgname"
  scons \
    --prefix=/usr
}

package() {
  cd "$srcdir/$pkgname"
  scons \
    --prefix=/usr \
    --install-sandbox="$pkgdir" \
    install
}
