# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor:  Achilleas Pipinellis <axilleas archlinux gr>

pkgname=supermin
pkgver=5.3.1
pkgrel=1
pkgdesc="Tool for creating supermin appliances"
arch=('x86_64')
url="http://people.redhat.com/~rjones/supermin/"
license=('GPL')
makedepends=('ocaml' 'ocaml-findlib')
depends=('e2fsprogs' 'pacman' 'pacman-contrib' 'cpio')
conflicts=('febootstrap<=3.21')
source=("https://download.libguestfs.org/${pkgname}/5.3-development/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('c904f62c46941cd7e1d19abd5e82491c46f40333faac4a131439cb2d95ec78ad38c227dadbbedd83317283b11d29d603fc65d2d5fe095d37bbd89e7e212f3e8a'
            'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
