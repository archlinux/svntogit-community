# Maintainer: Lukas1818 aur at lukas1818 dot de

pkgname=superslicer
pkgver=2.4.58.2
pkgrel=1
epoch=1
pkgdesc='G-code generator for 3D printers'
arch=(x86_64)
url=https://github.com/supermerill/SuperSlicer
license=(AGPL3)
options=(!emptydirs)
depends=(
  boost-libs
  cgal
  glew
  imath
  libspnav
  nlopt
  openvdb
  qhull
  wxgtk3
)
makedepends=(
  boost
  cereal
  cmake
  eigen
  git
  libigl
  ninja
  wxgtk2
)
optdepends=('superslicer-profiles: Predefined printer profiles')
_tag=6fcfe8fc9b70e2e5b0aefd122153c27815bb7c8f
source=(
  git+https://github.com/supermerill/SuperSlicer.git#tag=${_tag}
  superslicer-wxgtk3-wayland.patch
  superslicer-cereal.patch
  superslicer-openexr3.patch
  superslicer-boost1.79.patch
)
b2sums=('SKIP'
        '6b8abda2bfee9294bd5cd97380403680d6b2c6187af178e5eb4be2665280d1ff0d66036525b485e64d38d1d464756388fd4b53c6277d691079f51e6ee0c12372'
        'a652af374ca4cdd2b1db87bac9a1a6263a3c0df954c8afa4fddea3041659b159412fe3ab850d804a27dcb4b3ca8d64de511ef11769d353d7d2a0f7021dc38e99'
        'd351d05190977e55047504d13ab4172c7eaf7fb2077a61a448dd29a8a27084ee4b13284143102b7ea3dff0edc8b20675dff0014e9f26cd5f448e50c04a1d6779'
        '45ff3ab349a92b072eed57a511aead233e4356fcc9ab9382902af9702aee22f73c9a9f07a58420ca20c7c2cd41e81637f1d0114033c2fdb7f597cbcb484fa8ff')

prepare() {
  cd SuperSlicer
  patch -Np1 -i "$srcdir/superslicer-wxgtk3-wayland.patch"
  patch -Np1 -i "$srcdir/superslicer-cereal.patch"
  patch -Np1 -i "$srcdir/superslicer-openexr3.patch"
  patch -Np1 -i "$srcdir/superslicer-boost1.79.patch"
}

pkgver() {
  cd SuperSlicer
  git describe --tags
}

build() {
  cmake -S SuperSlicer -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DSLIC3R_ALPHA=OFF \
    -DSLIC3R_BUILD_TESTS=OFF \
    -DSLIC3R_FHS=ON \
    -DSLIC3R_GTK=3 \
    -DSLIC3R_STATIC=OFF \
    -DSLIC3R_WX_STABLE=ON
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  test ! -h "${pkgdir}/usr/share/SuperSlicer/resources" || rm "${pkgdir}/usr/share/SuperSlicer/resources"
}

# vim: ts=2 sw=2 et:
