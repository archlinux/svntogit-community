# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>

pkgname=supertuxkart
pkgver=0.6.2a
pkgrel=2
pkgdesc="A kart racing game featuring Tux and his friends"
arch=('i686' 'x86_64')
url="http://supertuxkart.sourceforge.net/"
license=('GPL2')
depends=('sdl>=1.2' 'libvorbis' 'freealut' 'libgl' 'freeglut')
makedepends=('plib>=1.8.4')
source=(http://downloads.sourceforge.net/project/$pkgname/SuperTuxKart/0.6.2/$pkgname-$pkgver-src.tar.bz2)
options=(force)
md5sums=('1672795016cc4964506706ac3287621e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --datadir=/usr/share

  # Fix compilation failure: http://bugs.gentoo.org/283490
  sed -i 's/-lGL/-lGL -lGLU/' config.status

  # those configure flags really mean nothing...
  sed -i "s#/usr/local#/usr#" src/file_manager.cpp
  sed -i "s#/games##" $(grep -Rl "/games" *)

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # fix executable location...
  install -dm755 ${pkgdir}/usr/bin
  mv ${pkgdir}/usr/supertuxkart ${pkgdir}/usr/bin/supertuxkart
  sed -i "s#usr/supertuxkart#usr/bin/supertuxkart#" \
    ${pkgdir}/usr/share/applications/supertuxkart.desktop
}

# vim:set ts=2 sw=2 et:
