# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Daniele Paolella <dp@mcrservice.it>

pkgname=supervisor
pkgver=3.3.5
pkgrel=1
pkgdesc="A system for controlling process state under UNIX"
arch=('any')
url="http://supervisord.org"
license=('custom')
depends=('python2-meld3' 'python2-setuptools')
backup=('etc/supervisord.conf')
source=("https://files.pythonhosted.org/packages/source/s/$pkgname/$pkgname-$pkgver.tar.gz"
        'supervisord.service'
        'supervisor.logrotate'
        'supervisord.conf')
sha256sums=('1b4d6d2e551dcb49e928cfffea857b8cca6b64a4a8fb755dcf86addf62866af0'
            '111f632081633dafae18ff069001e9ea78e9a24669ed75efc196f8d83d92f993'
            '7347d79c8b7f5cc59d5ffa31e4df64f92e89dee0dc84bdb3bdfcba212de93f4b'
            'fe0a60e34f67bf6ed7ca8731d9ce7657e00f37a43e0dfb96aa0df57adc4cd423')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	rm -rf "supervisor/medusa/"{debian,demo,docs,test}
	find -type f -name "*.py" -exec sed -i 's|/usr/bin/env python|/usr/bin/env python2|' {} \;
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py build
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	python2 setup.py install -O1 --skip-build --prefix=/usr --root="$pkgdir"
	install -Dm600 "$srcdir/supervisord.conf" "$pkgdir/etc/supervisord.conf"
	install -Dm644 "$srcdir/supervisor.logrotate" "$pkgdir/etc/logrotate.d/supervisor"
	install -Dm644 "$srcdir/supervisord.service" "$pkgdir/usr/lib/systemd/system/supervisord.service"
	install -d "$pkgdir/usr/share/licenses/$pkgname"
	install -d "$pkgdir/var/log/$pkgname"
	install -d "$pkgdir/etc/supervisor.d"
	mv LICENSES.txt COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname"
}
