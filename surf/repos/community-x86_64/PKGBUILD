# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Pierre Chapuis <catwell at archlinux dot us>
# Contributor: Ray Kohler <ataraxia937 at gmail dot com>
# Contributor: Michael Witten <mfwitten>

pkgname=surf
pkgver=0.4.1
pkgrel=4
pkgdesc="A simple web browser based on WebKit/GTK+."
arch=('i686' 'x86_64')
url='http://surf.suckless.org/'
license=('MIT')
depends=('libwebkit' 'xorg-xprop')
optdepends=('dmenu: URL-bar'
            'wget: default download handler')
makedepends=('pkgconfig')
install='surf.install'
source=("http://dl.suckless.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'config.h'
        'surf-0.4.1-link-xlib.patch')
md5sums=('28ac7a6d193d25844aa6b1db77fcbafa'
         'a1d40e130189343a74f83166410f35d7'
         '475fe2b51802aba40ad9cd6b75d550ed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/config.h" config.h

  # build fix from upstream (http://hg.suckless.org/surf/rev/2ea243e2ca82)
  patch -p1 -i ../surf-0.4.1-link-xlib.patch

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
