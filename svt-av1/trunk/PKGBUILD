# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=svt-av1
pkgver=0.7.5
pkgrel=1
pkgdesc='Scalable Video Technology AV1 encoder'
arch=(x86_64)
url='https://github.com/OpenVisualCloud/SVT-AV1'
license=(BSD)
makedepends=(
  cmake
  git
  nasm
)
source=(git+https://github.com/OpenVisualCloud/SVT-AV1.git#tag=9a321eb5f1233c46ac44472b30db8efb953fffd3)
md5sums=(SKIP)

pkgver() {
  cd SVT-AV1

  git describe --tags | sed 's/^v//'
}

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build

  sed '/CMAKE_BUILD_TYPE Release/d' -i SVT-AV1/CMakeLists.txt
}

build() {
  cd build

  export LDFLAGS="$LDFLAGS -Wl,-z,noexecstack"

  cmake ../SVT-AV1 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DNATIVE=OFF
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../SVT-AV1/LICENSE.md -t "${pkgdir}"/usr/share/licenses/svt-av1/
}

# vim: ts=2 sw=2 et:
