# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: jsteel <mail at jsteel dot org>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Tdy  <tdy@gmx.com>
# Contributor: Biginoz <biginoz at free.fr>
# Contributor: Archan Paul <paul.archan@gmail.com>

pkgname=sweethome3d
pkgver=5.0
pkgrel=1
pkgdesc="An interior design application to draw the plan of your house in a 3D environment"
arch=('any')
url="http://www.sweethome3d.com/"
license=('GPL')
depends=('java-runtime' 'shared-mime-info' 'desktop-file-utils')
source=("http://www.sweethome3d.com/downloads/SweetHome3D-${pkgver}.jar"
        "${pkgname}.sh"
        "${pkgname}.desktop"
        "${pkgname}.xml")
sha256sums=('c6d079227cc4545a7ff472209e20c88bffbda522cfdbb44b03870e2f580bf5b3'
            '0cbe84d4db89efd31254a034729f58cf2dd6d415210083584748049ccba0893a'
            '05e7f1a60753a7236faeb115ee42764f01a353107b9f3dc200b18b0856d4b7bd'
            'ec0ad1a0671f708af68ced46bec1f4ab377e24ca1a0a9984867ee5fe484f57c5')
install="${pkgname}.install"

package() {
  cd "${srcdir}"
  install -D -m 644 "SweetHome3D-${pkgver}.jar" "${pkgdir}/usr/share/java/${pkgname}.jar"
  install -D -m 644 "com/eteks/sweethome3d/viewcontroller/resources/help/images/applicationIcon.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -D -m 755 "${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m 644 "${pkgname}.xml" "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
  install -D -m 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
}
