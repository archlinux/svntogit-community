# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=6.6.2
pkgrel=1
pkgdesc='Prolog environment'
arch=('x86_64' 'i686')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
depends=('gmp' 'readline' 'openssl' 'libarchive')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library'
            'libjpeg: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft: for using the pl2xpce library')
#options=('!makeflags')
source=("http://www.swi-prolog.org/download/stable/src/pl-$pkgver.tar.gz")
sha256sums=('e4996e363a19353f8b4eb1c19284fa73612b0137b32ba3cb066210567260b6cd')

build() {
  cd "pl-$pkgver"

  ./configure --prefix=/usr --with-world
  cd src
  ./configure --disable-readline --prefix=/usr
  cd ..
  make
}

check() {
  make -C "pl-$pkgver" check || true
}

package() {
  make -C "pl-$pkgver" DESTDIR="$pkgdir" install

  # Fix for FS#20873
  chmod +x "$pkgdir/usr/lib/swipl-$pkgver/library/dialect/sicstus/swipl-lfr.pl"
}

# vim:set ts=2 sw=2 et:
