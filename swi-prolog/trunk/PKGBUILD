# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=6.4.1
pkgrel=2
pkgdesc='Prolog environment'
arch=('x86_64' 'i686')
url='http://www.swi-prolog.org/'
license=('GPL' 'LGPL')
depends=('gmp' 'readline' 'openssl' 'libarchive')
makedepends=('libxft' 'libjpeg' 'unixodbc' 'libxpm' 'libxinerama')
optdepends=('unixodbc: for using the odbc4pl library'
            'libjpeg: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft: for using the pl2xpce library')
options=('!makeflags')
source=("http://www.swi-prolog.org/download/stable/src/pl-$pkgver.tar.gz")
sha256sums=('48439529784a855ce9246419b51ebe5388acc28d1483a947c6a0397c3dc7f8eb')

build() {
  cd "pl-$pkgver"

  ./configure --prefix=/usr --with-world --enable-shared
  make
}

check() {
  make -C "pl-$pkgver" check || true
}

package() {
  make -C "pl-$pkgver" DESTDIR="$pkgdir" install

  # fix https://bugs.archlinux.org/task/20873
  chmod +x "$pkgdir/usr/lib/swipl-$pkgver/library/dialect/sicstus/swipl-lfr.pl"
}

# vim:set ts=2 sw=2 et:
