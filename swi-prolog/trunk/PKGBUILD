# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=8.4.2
pkgrel=2
pkgdesc='Prolog environment'
arch=(x86_64)
url='https://www.swi-prolog.org/'
license=(GPL LGPL)
depends=(gperftools libarchive libxcrypt libyaml qt5-base util-linux-libs)
makedepends=(cmake jdk-openjdk junit libjpeg libxft libxinerama libxpm ninja python-protobuf unixodbc)
optdepends=('java-runtime: for using Prolog from Java'
            'jdk-openjdk: for using Prolog from Java'
            'libjpeg: for using the pl2xpce library'
            'libxft: for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxpm: for using the pl2xpce library'
            'unixodbc: for using the odbc4pl library')
source=("$pkgname-$pkgver.tar.gz::https://www.swi-prolog.org/download/stable/src/swipl-$pkgver.tar.gz")
b2sums=('c0835bdbc09171ef00ccc52df14d07651ac42e36ab7bd6e6a72348050d3547a817bec13f2facd8fe051291dc3f0f80f64ed5c8a55e22dba314ccf896434013cf')

build() {
  cmake \
    -B build \
    -D CMAKE_BUILD_TYPE=Release \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D CMAKE_C_FLAGS="$CFLAGS -fPIC -ffile-prefix-map=$PWD= -w" \
    -D LIBEDIT_LIBRARIES=/usr/lib/libedit.so.0 \
    -D LIBEDIT_INCLUDE_DIR=/usr/include \
    -G Ninja \
    -S "swipl-$pkgver"
  swipl-$pkgver/scripts/pgo-compile.sh
  ninja -C build
}

check() {
  # skip the MQI test
  #sed -i '/mqi/d' build/CTestTestfile.cmake
  ctest --test-dir build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: www.swi-prolog.org/download/stable
