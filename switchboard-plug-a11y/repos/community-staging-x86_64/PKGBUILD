# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-a11y
pkgver=2.3.0
pkgrel=2
pkgdesc='Switchboard Universal Access Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-a11y
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=383d199080443c726f2618e4d007f627fec17991
source=(git+https://github.com/elementary/switchboard-plug-a11y.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-a11y

  git describe --tags
}

build() {
  arch-meson switchboard-plug-a11y build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
