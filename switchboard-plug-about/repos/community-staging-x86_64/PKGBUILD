# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=2.6.3
pkgrel=2
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=9cd759bb65f846daaf09055f272fbaa5d9e58705
source=(git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-about

  git describe --tags
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
