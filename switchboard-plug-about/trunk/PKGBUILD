# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-about
pkgver=6.1.0
pkgrel=2
pkgdesc='Switchboard About Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-about
license=(GPL3)
groups=(pantheon)
depends=(
  appstream
  fwupd
  glib2
  gtk3
  libgee
  libgranite.so
  libgtop
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=953cca72c0fa8877ed2f1c0d5f57394bdd8a6305
source=(git+https://github.com/elementary/switchboard-plug-about.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-about
  git describe --tags
}

build() {
  arch-meson switchboard-plug-about build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
