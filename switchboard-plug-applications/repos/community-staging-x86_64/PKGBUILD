# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-applications
pkgver=6.0.1
pkgrel=2
pkgdesc='Switchboard Applications Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-applications
license=(GPL3)
groups=(pantheon)
depends=(
  flatpak
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=fea8ee6c789a5d5bcc3699740a698e34cfba8d7d
source=(git+https://github.com/elementary/switchboard-plug-applications.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-applications

  git describe --tags
}

build() {
  arch-meson switchboard-plug-applications build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
