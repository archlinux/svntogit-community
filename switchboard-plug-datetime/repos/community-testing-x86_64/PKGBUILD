# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-datetime
pkgver=2.2.0
pkgrel=2
pkgdesc='Switchboard Date & Time Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-datetime
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=a7ec8cee73be667987879102d31edd319c5f8633
source=(git+https://github.com/elementary/switchboard-plug-datetime.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-datetime

  git describe --tags
}

build() {
  arch-meson switchboard-plug-datetime build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
