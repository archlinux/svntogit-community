# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-locale
pkgver=2.5.6
pkgrel=1
pkgdesc='Switchboard Locale Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-locale
license=(GPL3)
depends=(
  accountsservice
  glib2
  gnome-desktop
  gtk3
  libgee
  libgranite.so
  libibus
  libswitchboard-2.0.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
_tag=1deedb4af291f6bf3f36356d0b6a3488798dc075
source=(git+https://github.com/elementary/switchboard-plug-locale.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-locale

  git describe --tags
}

build() {
  arch-meson switchboard-plug-locale build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
