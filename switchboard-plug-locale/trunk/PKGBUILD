# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-locale
pkgver=2.5.8
pkgrel=2
pkgdesc='Switchboard Locale Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-locale
license=(GPL3)
depends=(
  accountsservice
  glib2
  gnome-desktop
  gtk3
  libgee
  libgranite.so
  libibus
  libswitchboard-2.0.so
  polkit
)
makedepends=(
  git
  meson
  vala
)
options=(debug)
_tag=f0ff718bd235355041da143320fc488e9887145a
source=(git+https://github.com/elementary/switchboard-plug-locale.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-locale

  git describe --tags
}

build() {
  arch-meson switchboard-plug-locale build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
