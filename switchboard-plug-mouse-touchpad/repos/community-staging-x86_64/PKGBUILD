# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-mouse-touchpad
pkgver=6.1.0
pkgrel=3
pkgdesc='Switchboard Mouse & Touchpad Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-mouse-touchpad
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
_tag=b8e148ecd908ba0fe65beb39bdb45e887819d55c
source=(git+https://github.com/elementary/switchboard-plug-mouse-touchpad.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-mouse-touchpad
  git describe --tags
}

build() {
  arch-meson switchboard-plug-mouse-touchpad build \
    -Dgnome_40=true
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
