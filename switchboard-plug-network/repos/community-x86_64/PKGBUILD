# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-network
pkgver=2.4.3
pkgrel=1
pkgdesc='Switchboard Network Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-network
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libnm
  libnma
  libswitchboard-2.0.so
  networkmanager
  polkit
)
makedepends=(
  git
  meson
  vala
)
_tag=ad47f912ea3faea86e9ec6f07a6c55a458e0f545
source=(git+https://github.com/elementary/switchboard-plug-network.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-network

  git describe --tags
}

build() {
  arch-meson switchboard-plug-network build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
