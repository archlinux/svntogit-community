# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-sharing
pkgver=2.1.6
pkgrel=1
pkgdesc='Switchboard Sharing Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-sharing
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gnome-user-share
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  switchboard
  vala
)
_tag=5d2b063e86d4ea46ba6d5681fe9d398cd6c6a96f
source=(git+https://github.com/elementary/switchboard-plug-sharing.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-sharing

  git describe --tags
}

build() {
  arch-meson switchboard-plug-sharing build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
