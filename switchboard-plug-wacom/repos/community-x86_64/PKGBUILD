# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-wacom
pkgver=1.0.1
pkgrel=1
pkgdesc='Switchboard wacom Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-wacom
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgranite.so
  libgudev-1.0.so
  libswitchboard-2.0.so
  libwacom
  libx11
  libxi
)
makedepends=(
  git
  meson
  vala
)
_tag=713085c2d57368e13e12e5df8c0392a3c34cf1bb
source=(git+https://github.com/elementary/switchboard-plug-wacom.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-wacom
  git describe --tags
}

build() {
  arch-meson switchboard-plug-wacom build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
