# $Id$
# Maintainer: Alexander RÃ¸dseth
# Contributor: Andrea Scarpino
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>

pkgname=sword
pkgver=1.6.2
pkgrel=7
pkgdesc="Library for Bible study programs"
arch=('x86_64' 'i686')
url="http://www.crosswire.org/sword/"
license=('GPL')
depends=('curl' 'clucene' 'swig')
makedepends=('cmake')
backup=('etc/sword.conf')
source=("http://www.crosswire.org/ftpmirror/pub/$pkgname/source/v1.6/$pkgname-$pkgver.tar.gz"
        "curl.patch")
sha256sums=('af76c7d54135c444b09eeaafb49229ef5201a4e1d44539d9341dceaeb60a87b9'
            '3c2676b6dc1d56b08b2532f46af32c54e91ea71ed92a5d7a30ee29ed7ff09124')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i ../curl.patch

  mkdir ../build
  cd ../build

  CXXFLAGS=-fpermissive cmake "../$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/lib/sword"
  mv "$pkgdir"/usr/lib/${pkgver}_icu_* \
    "$pkgdir/usr/lib/sword/"

  cd "$srcdir/$pkgname-$pkgver/locales.d/"
  for file in *.conf; do
    install -Dm644 "$file" \
      "$pkgdir/usr/share/sword/locales.d/$file"
  done

  cd ../include
  install -d "$pkgdir/usr/include/sword"
  install -Dm644 canon_{catholic{,2},synodalp}.h \
    "$pkgdir/usr/include/sword"

  cd ../samples
  install -Dm644 mods.d/globals.conf \
    "$pkgdir/usr/share/sword/mods.d/globals.conf"
  install -Dm644 recommended/sword.conf \
    "$pkgdir/etc/sword.conf"
}

# vim:set ts=2 sw=2 et:
