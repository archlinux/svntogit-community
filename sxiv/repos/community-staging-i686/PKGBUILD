# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Bert Muennich <muennich at informatik.hu-berlin.de>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=sxiv
pkgver=1.2
pkgrel=2
pkgdesc='Simple X Image Viewer'
arch=('i686' 'x86_64')
license=('GPL2')
install=sxiv.install
url='https://github.com/muennich/sxiv'
depends=('imlib2' 'desktop-file-utils')
source=(https://github.com/muennich/sxiv/archive/v$pkgver.tar.gz
        giflib-5.1.0.patch)
md5sums=('9b79d3c70693d9abbc66295cf6f281a0'
         '98a6c832655b90e57ff137df41a1fb90')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../giflib-5.1.0.patch
}

build() {
  make -C $pkgname-$pkgver
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir"/usr install
  install -Dm644 sxiv.desktop "$pkgdir"/usr/share/applications/sxiv.desktop
}
