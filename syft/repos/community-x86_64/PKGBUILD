# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=syft
pkgver=0.49.0
pkgrel=1
pkgdesc="CLI tool and library for generating a Software Bill of Materials from container images and filesystems"
arch=('x86_64')
url="https://github.com/anchore/syft"
license=('Apache')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/anchore/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('e48c357cb72678685f957d796b6d565836df4bad5ff6a1177d756ee6cc88e60dabae1513b0dacb1c1cbdd99c9641bd0662509098804e4bfc1183d1e1ee85a8d9')
b2sums=('15f4da31fd49de7fa9996bfaebc34e9b63cce646accac0f324b6ff79a1a965f673bf1594d7563ff36ccc975a6ab0b074fae2498701b07d7453c36a886aea693f')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

# disabled, needs docker
# check() {
#  cd "${pkgname}-${pkgver}"
#  go test -v ./...
# }

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/syft "${pkgdir}/usr/bin/${pkgname}"
}

