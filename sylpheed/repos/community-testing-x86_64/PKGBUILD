# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sylpheed
pkgver=3.7.0
pkgrel=2
pkgdesc="Lightweight and user-friendly e-mail client"
arch=('x86_64')
url="https://sylpheed.sraoss.jp/en/"
license=('GPL')
depends=('compface' 'gpgme' 'gtkspell' 'libnsl')
makedepends=('openssl')
source=("https://sylpheed.sraoss.jp/${pkgname}/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('490837528bf7ba9d26994cd5fff00b6e5390a127419b9d0efd9fc25c38be1291d55c5b8daebdf5ca9d9159a51c938449e76212328f3eae40cc039db88cb5caa4')

prepare() {
  cd "${pkgname}-${pkgver}"
  # patch for enchant >= 2.1.3
  # https://www.archlinux.org/todo/enchant-221-rebuild/
  sed -i 's,enchant/,enchant-2/,g' src/compose.c
  sed -i 's/ enchant/ enchant-2/g' configure
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-maintainer-mode \
              --enable-ldap
  make

  # Build Attachment-Tool Plug-in
  cd plugin/attachment_tool && make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" LDFLAGS+="/usr/lib/enchant-2" install

  # Install Attachment-Tool Plug-in
  cd plugin/attachment_tool
  make DESTDIR="$pkgdir/" install-plugin
}
