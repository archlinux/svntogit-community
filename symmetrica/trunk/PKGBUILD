# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=symmetrica
pkgver=2.0
pkgrel=7
pkgdesc="Algorithms for symmetric groups and classical groups"
arch=(x86_64)
url="http://www.algorithm.uni-bayreuth.de/en/research/SYMMETRICA/"
depends=(glibc)
license=(custom)
source=('http://www.algorithm.uni-bayreuth.de/en/research/SYMMETRICA/SYM2_0_tar.gz' 'symmetrica-int32.patch' 'LICENSE')
sha256sums=('bf52788dedc14c482e89f5e7efe8c60864a633314ddd446dd4602d5fdaca0ee2'
            'b68792fe9e0adf08addc71437b61c4f002edb20e06d795df63a38ca2dd40d1df'
            '0bf9103563587f077f2155d1e7f514f8389a3f9749ee3e690ce2d449a4bc699e')

prepare() {
  sed -i -e 's/-O2/$(CFLAGS)/' makefile
# fix integer overflow on 64 bits
  patch -p1 -i symmetrica-int32.patch

# disable banner
  sed -i 's|no_banner = FALSE|no_banner = TRUE|' de.c
}

build() {
  export CFLAGS="-fPIC $CFLAGS"
  make
  gcc -shared *.o -o libsymmetrica.so ${LDFLAGS}
}

package() {
  install -D -m 755 libsymmetrica.so -t "$pkgdir"/usr/lib/
  mkdir -p "$pkgdir"/usr/include/symmetrica/
  install -m 644 *.h "$pkgdir"/usr/include/symmetrica/

  mkdir -p "$pkgdir"/usr/share/licenses/symmetrica
  install LICENSE "$pkgdir"/usr/share/licenses/symmetrica
}
