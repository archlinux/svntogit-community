# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sympol
pkgver=0.1.9
pkgrel=6
pkgdesc="A C++ tool to work with symmetric polyhedra"
arch=(x86_64)
url="http://www.math.uni-rostock.de/~rehn/software/sympol.html"
license=(GPL2)
source=($pkgname-$pkgver::"https://github.com/tremlin/SymPol/archive/v$pkgver.tar.gz"
        cddlib-0.94k.patch
        sympol-fix-headers.patch)
depends=(boost-libs lrs cddlib)
makedepends=(cmake permlib eigen bliss)
sha256sums=('6753b8fb30745b66a0886e125c18b539417afcf62b804799eb220bd5a59ccc37'
            '23f85255ad1acbaf9c63134c6d331e0f3b8d06230a05e3f63e57399863579649'
            'f3ed90e6b9af5dae0728c52ce4bb9107f3040481cac6c08116dbf8c19bfe3971')

prepare() {
  cd SymPol-$pkgver
  sed -e '/(external/d' -i CMakeLists.txt # Don't use bundled cddlib and lrslib
  sed -e 's|lrsgmp|lrs|' -i sympol/CMakeLists.txt # Fix linking to system lrs
  rm -r external/{lrslib*,cddlib*}
  sed -e '/lrs_mp_close/d' -i sympol/raycomputationlrs.cpp # Fix build with lrs 0.5 (Fedora)
  patch -p1 -i ../cddlib-0.94k.patch # Fix build with cddlib 0.94k
  patch -p1 -i ../sympol-fix-headers.patch # Keep headers directory structure
}

build() {
  cmake -B build -S SymPol-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -I/usr/include/lrslib -DMA -DGMP -DBLISS_USE_GMP"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
