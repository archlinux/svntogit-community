# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sympow
pkgver=1.018.1
pkgrel=4
pkgdesc="A a package to compute special values of symmetric power elliptic
curve L-functions"
arch=(x86_64)
url="http://www.sagemath.org"
license=(GPL)
depends=(pari)
source=("http://mirrors.mit.edu/sage/spkg/upstream/$pkgname/$pkgname-$pkgver.tar.bz2" 'sympow.sh')
md5sums=('b6cbb7488870d70d92d11176049cb91b'
         '93f214e7f22df8ae525d9fbfbf82eeb1')

prepare() {
  cd $pkgname-$pkgver

  sed -e "s|../sympow|sympow_bin|g" -i generate.c
  sed -e "s|./sympow|sympow_bin|g" -i disk.c new_data
}

build() {
  cd $pkgname-$pkgver

  ./Configure
  make
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir"/usr/{bin,share/sympow}
  cp -r *.gp datafiles "$pkgdir"/usr/share/sympow
  cp sympow "$pkgdir"/usr/bin/sympow_bin
  cp new_data "$pkgdir"/usr/bin/new_data

  cd "$pkgdir"/usr/share/sympow/datafiles

  for file in *.txt; do
    "$srcdir"/$pkgname-$pkgver/sympow -txt2bin "$(grep -c AT $file)" <$file ${file/txt/bin}
  done

  install -Dm755 "$srcdir"/sympow.sh "$pkgdir"/usr/bin/sympow
}
