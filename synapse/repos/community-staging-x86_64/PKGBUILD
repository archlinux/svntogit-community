# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=synapse
pkgver=0.2.10
pkgrel=5
pkgdesc="A semantic file launcher"
arch=('i686' 'x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL3')
depends=('gtkhotkey' 'hicolor-icon-theme' 'json-glib' 'libgee06' 'libnotify' \
         'libunique' 'libzeitgeist' 'rest' 'xdg-utils')
makedepends=('intltool' 'vala')
optdepends=('banshee: banshee plugin'
            'bc: calculator plugin'
            'devhelp: documentation plugin'
            'gnome-screensaver: screensaver plugin'
            'gnome-dictionary: dictionary plugin'
            'openssh: ssh plugin'
            'pastebinit: pastebin plugin'
            'rhythmbox: rhythmbox plugin'
            'xnoise: xnoise plugin')
install=$pkgname.install
source=("https://launchpad.net/$pkgname-project/0.2/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        "fix-check-desktop.patch"
        "check-null-exec.patch")
sha1sums=('6e8a800bdbdded4e167734c8e49d95a9e44998ff'
          'b64fa4efc4efd01f77f84d19a7a63c10186d0211'
          'f494e5b36a77421114ce04b7085369abe5c00d68')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # XDG_CURRENT_DESKTOP fix
  patch -Np1 -i "$srcdir"/fix-check-desktop.patch

  # don't crash on empty Exec field
  patch -Np1 -i "$srcdir"/check-null-exec.patch

  # DSO fix
  export LDFLAGS="$LDFLAGS -lm"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
