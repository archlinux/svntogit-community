# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: aeolist <aeolist@hotmail.com>

pkgname=synbak
pkgver=3.8.2
pkgrel=1
pkgdesc="Meant to unify several backup methods in a single application while supplying a powerful reporting system"
arch=('any')
url='https://github.com/ugoviti/synbak'
license=('GPL')
depends=('bc' 'netcat')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/ugoviti/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('9f918022cbe6384c032df33d7714f07a9ed65adfe8c48e353be0273279034601')

build() {
  cd ${pkgname}-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
