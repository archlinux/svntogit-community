# $Id$
# Maintainer: Martin Wimpress <code@flexion.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: kozec <kozec at kozec dot com>

pkgname=syncthing-gtk
pkgver=0.8
pkgrel=1
epoch=1
pkgdesc='GTK3 based GUI and notification area icon for Syncthing'
arch=('any')
url='https://github.com/syncthing/syncthing-gtk'
license=('GPL2')
depends=('gtk3' 'libnotify' 'python2-dateutil' 'python2-gobject' 'python2-pyinotify' 'python2-cairo' 'syncthing>=0.12')
makedepends=('python2-setuptools')
optdepends=('python2-nautilus: file manager integration for Nautilus'
            'python2-caja: file manager integration for Caja'
            'librsvg: to shown menu icons correctly if not using traditional window header')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/syncthing/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('f1283e521066daae07967b704dfba3675f3599ad7fb10d73d0d66fe62e9b7e69')

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
