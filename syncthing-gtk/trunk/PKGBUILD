# $Id$
# Maintainer: Martin Wimpress <code@flexion.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: kozec <kozec at kozec dot com>

pkgname=syncthing-gtk
pkgver=0.9.2.7
pkgrel=5
epoch=1
pkgdesc='GTK3 based GUI and notification area icon for Syncthing'
arch=('any')
url='https://github.com/syncthing/syncthing-gtk'
license=('GPL2')
depends=('syncthing>=0.13' 'gtk3' 'libnotify' 'python2-pyinotify'
         'python2-bcrypt' 'python2-cairo' 'python2-dateutil' 'python2-gobject')
makedepends=('python2-setuptools')
optdepends=('python2-caja: file manager integration for Caja'
            'python2-nautilus: file manager integration for Nautilus'
            'nemo-python: file manager integration for Nemo')
source=($pkgname-$pkgver.tar.gz::https://github.com/syncthing/$pkgname/archive/v$pkgver.tar.gz
        fix-statusicon.patch
        kde-statusicon.patch
        fix-icon-path.patch)
sha256sums=('a500665ce2ebd04cf0f51e0b65690dddc258bc495a74b6311604fefe9f3e16ef'
            '5e9d4f89fb503d8761f435ebd03dbb126c0d5df8f00e4fd405fbdd8e5fb4a47c'
            '109d8c970045e60251fc64865f05322b23a0995ee6725be02905941cb3a1ae0d'
            '34b752396db02668016c2c2a57b475f4ade9533f50fb682db6f8a9d239a84653')

prepare() {
  cd $pkgname-$pkgver

  # Fix invisible tray icon
  patch -Np1 -i ../fix-statusicon.patch

  # Enable Gtk.StatusIcon in KDE
  patch -Np1 -i ../kde-statusicon.patch

  # Fix icon path on about dialog
  patch -Np1 -i ../fix-icon-path.patch
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  # Move filemanager extensions into their right place (FS#57123)
  mkdir -p "$pkgdir"/usr/share/{caja,nautilus,nemo}-python/extensions/
  mv "$pkgdir"/usr/share/syncthing-gtk/syncthing-plugin-caja.py "$pkgdir"/usr/share/caja-python/extensions/
  mv "$pkgdir"/usr/share/syncthing-gtk/syncthing-plugin-nautilus.py "$pkgdir"/usr/share/nautilus-python/extensions/
  mv "$pkgdir"/usr/share/syncthing-gtk/syncthing-plugin-nemo.py "$pkgdir"/usr/share/nemo-python/extensions/
}
