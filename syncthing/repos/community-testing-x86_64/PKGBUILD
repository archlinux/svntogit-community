# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Sauyon Lee <sauyonl@sauyon.com>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: korjjj <korjjj+aur@gmail.com>

pkgname=('syncthing' 'syncthing-relaysrv')
pkgver=0.13.10
pkgrel=1
arch=('i686' 'x86_64')
url="http://syncthing.net/"
license=('MPL')
depends=('glibc')
makedepends=('git' 'go' 'godep' 'inetutils' 'sed')
options=('!strip')
source=(https://github.com/syncthing/syncthing/releases/download/v$pkgver/syncthing-source-v${pkgver}.tar.gz{,.asc}
        syncthing-relaysrv.sysusers
        syncthing-relaysrv.tmpfiles)
validpgpkeys=('37C84554E7E0A261E4F76E1ED26E6ED000654A3E') # Syncthing Release Management
sha256sums=('f8847c6beb7e0bf685c5640e929e30f8ae4f9e63ddc905a6f62e18c7a3fab5dc'
            'SKIP'
            '6ab96a5949e223d315d49663734ad27b70c72469c691a61ac198cb7794cb5eaa'
            'f899998985f2d374af57f17ef6362c37a305467858195b74d1c1def71d97273f')

prepare() {
  cd "${srcdir}/${pkgbase}/cmd/relaysrv"
  sed -i -e 's|/usr/bin/relaysrv|/usr/bin/syncthing-relaysrv|' \
    etc/linux-systemd/syncthing-relaysrv.service

  cd "${srcdir}"
  install -d "src/github.com/syncthing"
  mv "${pkgbase}" "src/github.com/syncthing/${pkgbase}"
}

build() {
  export GOPATH="${srcdir}" GOROOT_FINAL="/usr/bin"
  cd "${srcdir}/src/github.com/syncthing/${pkgbase}"
  go run build.go -no-upgrade -version v${pkgver} build
  go run build.go -no-upgrade -version v${pkgver} build relaysrv
}

check() {
  export GOPATH="${srcdir}" GOROOT_FINAL="/usr/bin"
  cd "${srcdir}/src/github.com/syncthing/${pkgbase}"
  if [ "${CARCH}" == "x86_64" ] ; then
    go run build.go -no-upgrade test
  fi
}

package_syncthing() {
  pkgdesc="Open Source Continuous Replication / Cluster Synchronization Thing"
  install=$pkgname.install

  cd "${srcdir}/src/github.com/syncthing/${pkgbase}"
  install -Dm755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 "etc/linux-systemd/system/${pkgname}-resume.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}-resume.service"
  install -Dm644 "etc/linux-systemd/system/${pkgname}@.service" \
    "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
  install -Dm644 "etc/linux-systemd/user/${pkgname}.service" \
    "${pkgdir}/usr/lib/systemd/user/${pkgname}.service"

  # license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # man pages
  cd "${srcdir}/src/github.com/syncthing/${pkgname}/man"
  for file in $(find . -name '*.1' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man1/$file
  done
  for file in $(find . -name '*.5' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man5/$file
  done
  for file in $(find . -name '*.7' -print); do
    install -Dm644 $file "${pkgdir}"/usr/share/man/man7/$file
  done
}

package_syncthing-relaysrv() {
  pkgdesc='Relay server for Syncthing'
  install=${pkgname}.install

  cd "${srcdir}/src/github.com/syncthing/${pkgbase}"
  install -Dm755 relaysrv "${pkgdir}"/usr/bin/${pkgname}

  install -Dm644 cmd/relaysrv/etc/linux-systemd/${pkgname}.service \
    "${pkgdir}"/usr/lib/systemd/system/${pkgname}.service
  install -Dm644 "${srcdir}/${pkgname}.tmpfiles" \
    "$pkgdir/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm644 "${srcdir}/${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
