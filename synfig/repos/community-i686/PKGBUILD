# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=0.64.1
pkgrel=1
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg-compat' 'fontconfig' 'libpng'
	 'libtiff' 'libdv' 'libmng' 'cairo' 'pango' 'boost-libs')
makedepends=('boost')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
source=(http://downloads.sourceforge.net/project/synfig/releases/$pkgver/source/synfig-$pkgver.tar.gz
	build-fix.patch
	ffmpeg-0.8.patch
	ffmpeg-loop.patch)
md5sums=('548a5d51a48b2be0c3fcaebe917a5d88'
         'b5568b06fae1a7ee465b5380b1139b83'
         'ebf41046097cfd778dee9a9854561dae'
         'c6684c876c093f5e53d2c46592994e9b')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i $srcdir/build-fix.patch
  patch -Np1 -i $srcdir/ffmpeg-0.8.patch
#  patch -Np1 -i $srcdir/ffmpeg-loop.patch
  sed -i '1,1i#include <string.h>' src/modules/mod_png/trgt_png.cpp
}

build() {
  cd $srcdir/$pkgname-$pkgver
  export PKG_CONFIG_PATH=/usr/lib/ffmpeg-compat/pkgconfig:$PKG_CONFIG_PATH
  LDFLAGS="$LDFLAGS -Wl,-rpath -Wl,/usr/lib/ffmpeg-compat"
  CFLAGS="$CFLAGS -D__STDC_CONSTANT_MACROS"
  CXXFLAGS="$CXXFLAGS -D__STDC_CONSTANT_MACROS"
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
