# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.2.0
pkgrel=5
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++2.6' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg' 'fontconfig'
         'libpng' 'libtiff' 'libdv' 'libmng' 'cairo' 'pango' 'boost-libs' 'mlt'
         'fftw')
makedepends=('boost')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
#source=(https://download.tuxfamily.org/synfig/releases/$pkgver/synfig-$pkgver.tar.gz
source=(https://downloads.sourceforge.net/project/synfig/releases/$pkgver/source/synfig-$pkgver.tar.gz
        ffmpeg-3.0.patch synfig-gcc7.patch::"https://github.com/synfig/synfig/commit/8406fe91.patch")
md5sums=('d44fea7a16572ec638f2ca9f765bdc39'
         '62fb382cbf418f5e2f536ed73656cf89'
         '79c9be3599e156aa20f5fa3dd8e1d3ab')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/ffmpeg-3.0.patch
# fix build with GCC 7
  patch -p2 -i ../synfig-gcc7.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver

  CXXFLAGS="$CXXFLAGS -std=gnu++11"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --with-imagemagick \
    --with-magickpp \
    --with-libavcodec \
    --with-libdv

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
