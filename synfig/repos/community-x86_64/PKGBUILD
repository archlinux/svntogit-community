# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.2.0
pkgrel=1
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg' 'fontconfig'
         'libpng' 'libtiff' 'libdv' 'libmng' 'cairo' 'pango' 'boost-libs' 'mlt'
         'fftw')
makedepends=('boost')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
#source=(http://download.tuxfamily.org/synfig/releases/$pkgver/synfig-$pkgver.tar.gz
source=(http://downloads.sourceforge.net/project/synfig/releases/$pkgver/source/synfig-$pkgver.tar.gz
        ffmpeg-3.0.patch)
md5sums=('d44fea7a16572ec638f2ca9f765bdc39'
         '62fb382cbf418f5e2f536ed73656cf89')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/ffmpeg-3.0.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver

  CXXFLAGS="$CXXFLAGS -std=gnu++11"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --with-imagemagick \
    --with-magickpp \
    --with-libavcodec \
    --with-libdv

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
