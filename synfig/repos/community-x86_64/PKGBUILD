# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=1.4.3
pkgrel=1
pkgdesc='Professional vector animation program (CLI renderer only)'
arch=('x86_64')
url='https://www.synfig.org/'
license=('GPL3')
depends=('boost-libs' 'fftw' 'imagemagick' 'libjpeg-turbo' 'libmng' 'libxml++2.6' 'mlt6' 'openexr'
         'pango')
makedepends=('boost' 'etl' 'intltool')
source=("https://downloads.sourceforge.net/synfig/$pkgname-$pkgver.tar.gz")
sha256sums=('0c8d02ff6c77e468021a4497be34323d38246208d199ab1c3bbf65c9794752e7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
