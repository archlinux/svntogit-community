# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=0.64.3
pkgrel=2
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg-compat' 'fontconfig' 'libpng'
	 'libtiff' 'libdv' 'libmng' 'cairo' 'pango' 'boost-libs')
makedepends=('boost')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
source=(http://downloads.sourceforge.net/project/synfig/releases/$pkgver/source/synfig-$pkgver.tar.gz
	build-fix.patch
	ffmpeg-0.8.patch)
md5sums=('294904f9b976c62d7b5473134dbb9dfa'
         'b5568b06fae1a7ee465b5380b1139b83'
         'ebf41046097cfd778dee9a9854561dae')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/build-fix.patch"
  patch -Np1 -i "$srcdir/ffmpeg-0.8.patch"
  sed -i 's|.*set_remove_alpha.*||g' src/modules/mod_libavcodec/trgt_av.cpp
}

build() {
  cd $pkgname-$pkgver
  export PKG_CONFIG_PATH=/usr/lib/ffmpeg-compat/pkgconfig:$PKG_CONFIG_PATH
  LDFLAGS="$LDFLAGS -Wl,-rpath -Wl,/usr/lib/ffmpeg-compat"
  CFLAGS="$CFLAGS -D__STDC_CONSTANT_MACROS"
  CXXFLAGS="$CXXFLAGS -D__STDC_CONSTANT_MACROS"
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
