# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfigstudio
pkgver=1.0.2
pkgrel=4
pkgdesc="Professional vector animation program (GUI)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('gtkmm3' 'synfig' 'sdl_image')
makedepends=('openexr' 'imagemagick' 'xorg-fonts-100dpi' 'xorg-fonts-75dpi'
	     'xorg-fonts-misc' 'xorg-fonts-type1' 'intltool' 'etl')
#source=(http://download.tuxfamily.org/synfig/releases/$pkgver/synfigstudio-$pkgver.tar.gz)
source=(http://downloads.sourceforge.net/project/synfig/releases/$pkgver/source/synfigstudio-$pkgver.tar.gz
	build-fix.patch)
md5sums=('566eac351cd6f63d200be6fb6fe55767'
         '033d6ad90f2b2226ddf7cc930be41624')

prepare() {
  cd $srcdir/synfigstudio-$pkgver
  patch -p1 <$srcdir/build-fix.patch
}

build() {
  cd $srcdir/synfigstudio-$pkgver
  export PKG_CONFIG_PATH=/usr/lib/ffmpeg0.10/pkgconfig:$PKG_CONFIG_PATH
  LDFLAGS="$LDFLAGS -Wl,-rpath -Wl,/usr/lib/ffmpeg0.10"
  CFLAGS="$CFLAGS -D__STDC_CONSTANT_MACROS"
  CXXFLAGS="$CXXFLAGS -D__STDC_CONSTANT_MACROS -std=gnu++11"
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  sed -i 's#Gtk::IconSize::IconSize#Gtk::IconSize#' src/gui/dialogs/dialog_color.cpp
  make
}

package() {
  cd $srcdir/synfigstudio-$pkgver
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/pixmaps/$pkgname/*.mng
  install -m644 images/*.png -t $pkgdir/usr/share/pixmaps/$pkgname/
  rm -f $pkgdir/usr/share/mime/XMLnamespaces
  rm -f $pkgdir/usr/share/mime/aliases
  rm -f $pkgdir/usr/share/mime/generic-icons
  rm -f $pkgdir/usr/share/mime/globs
  rm -f $pkgdir/usr/share/mime/globs2
  rm -f $pkgdir/usr/share/mime/icons
  rm -f $pkgdir/usr/share/mime/magic
  rm -f $pkgdir/usr/share/mime/mime.cache
  rm -f $pkgdir/usr/share/mime/subclasses
  rm -f $pkgdir/usr/share/mime/treemagic
  rm -f $pkgdir/usr/share/mime/types
  rm -f $pkgdir/usr/share/mime/version
}
