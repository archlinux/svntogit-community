# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfigstudio
pkgver=0.64.0
pkgrel=1
pkgdesc="Professional vector animation program (GUI)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('gtkmm' 'synfig')
makedepends=('openexr' 'imagemagick' 'xorg-fonts-100dpi' 'xorg-fonts-75dpi'
	     'xorg-fonts-misc' 'xorg-fonts-type1' 'intltool')
install=synfigstudio.install
options=('!libtool')
source=(http://downloads.sourceforge.net/project/synfig/synfigstudio/$pkgver/synfigstudio-$pkgver.tar.gz)
md5sums=('bbda14359a07254b8efe5e706de192df')

build() {
  cd $srcdir/synfigstudio-$pkgver
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  sed -i 's#Gtk::IconSize::IconSize#Gtk::IconSize#' src/gui/dialogs/dialog_color.cpp
  make
}

package() {
  cd $srcdir/synfigstudio-$pkgver
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/pixmaps/$pkgname/*.mng
  install -m644 images/*.png -t $pkgdir/usr/share/pixmaps/$pkgname/
  rm -f $pkgdir/usr/share/mime/XMLnamespaces
  rm -f $pkgdir/usr/share/mime/aliases
  rm -f $pkgdir/usr/share/mime/generic-icons
  rm -f $pkgdir/usr/share/mime/globs
  rm -f $pkgdir/usr/share/mime/globs2
  rm -f $pkgdir/usr/share/mime/icons
  rm -f $pkgdir/usr/share/mime/magic
  rm -f $pkgdir/usr/share/mime/mime.cache
  rm -f $pkgdir/usr/share/mime/subclasses
  rm -f $pkgdir/usr/share/mime/treemagic
  rm -f $pkgdir/usr/share/mime/types
  rm -f $pkgdir/usr/share/mime/version
}
