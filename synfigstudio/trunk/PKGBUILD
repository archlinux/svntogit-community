# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfigstudio
pkgver=1.0
pkgrel=1
pkgdesc="Professional vector animation program (GUI)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('gtkmm' 'synfig')
makedepends=('openexr' 'imagemagick' 'xorg-fonts-100dpi' 'xorg-fonts-75dpi'
	     'xorg-fonts-misc' 'xorg-fonts-type1' 'intltool')
install=synfigstudio.install
source=(http://downloads.sourceforge.net/project/synfig/releases/$pkgver/source/synfigstudio-$pkgver.tar.gz
	bug699.patch)
md5sums=('73f5bf6bac4183512858de4856b9572c'
         'd1b095606377430e68c80797dcf87ced')

prepare() {
  cd $srcdir/synfigstudio-$pkgver
#  patch -Np1 -i "$srcdir/bug699.patch"
}

build() {
  cd $srcdir/synfigstudio-$pkgver
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  sed -i 's#Gtk::IconSize::IconSize#Gtk::IconSize#' src/gui/dialogs/dialog_color.cpp
  make
}

package() {
  cd $srcdir/synfigstudio-$pkgver
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/pixmaps/$pkgname/*.mng
  install -m644 images/*.png -t $pkgdir/usr/share/pixmaps/$pkgname/
  rm -f $pkgdir/usr/share/mime/XMLnamespaces
  rm -f $pkgdir/usr/share/mime/aliases
  rm -f $pkgdir/usr/share/mime/generic-icons
  rm -f $pkgdir/usr/share/mime/globs
  rm -f $pkgdir/usr/share/mime/globs2
  rm -f $pkgdir/usr/share/mime/icons
  rm -f $pkgdir/usr/share/mime/magic
  rm -f $pkgdir/usr/share/mime/mime.cache
  rm -f $pkgdir/usr/share/mime/subclasses
  rm -f $pkgdir/usr/share/mime/treemagic
  rm -f $pkgdir/usr/share/mime/types
  rm -f $pkgdir/usr/share/mime/version
}
