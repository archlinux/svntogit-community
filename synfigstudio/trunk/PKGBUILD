# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfigstudio
pkgver=1.0.1
pkgrel=1
pkgdesc="Professional vector animation program (GUI)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('gtkmm3' 'synfig')
makedepends=('openexr' 'imagemagick' 'xorg-fonts-100dpi' 'xorg-fonts-75dpi'
	     'xorg-fonts-misc' 'xorg-fonts-type1' 'intltool' 'etl')
install=synfigstudio.install
source=(http://download.tuxfamily.org/synfig/releases/$pkgver/synfigstudio-$pkgver.tar.gz)
md5sums=('8173da720908bd7d9f79081deded1017')

build() {
  cd $srcdir/synfigstudio-$pkgver
  export PKG_CONFIG_PATH=/usr/lib/ffmpeg-compat/pkgconfig:$PKG_CONFIG_PATH
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  sed -i 's#Gtk::IconSize::IconSize#Gtk::IconSize#' src/gui/dialogs/dialog_color.cpp
  make
}

package() {
  cd $srcdir/synfigstudio-$pkgver
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/pixmaps/$pkgname/*.mng
  install -m644 images/*.png -t $pkgdir/usr/share/pixmaps/$pkgname/
  rm -f $pkgdir/usr/share/mime/XMLnamespaces
  rm -f $pkgdir/usr/share/mime/aliases
  rm -f $pkgdir/usr/share/mime/generic-icons
  rm -f $pkgdir/usr/share/mime/globs
  rm -f $pkgdir/usr/share/mime/globs2
  rm -f $pkgdir/usr/share/mime/icons
  rm -f $pkgdir/usr/share/mime/magic
  rm -f $pkgdir/usr/share/mime/mime.cache
  rm -f $pkgdir/usr/share/mime/subclasses
  rm -f $pkgdir/usr/share/mime/treemagic
  rm -f $pkgdir/usr/share/mime/types
  rm -f $pkgdir/usr/share/mime/version
}
