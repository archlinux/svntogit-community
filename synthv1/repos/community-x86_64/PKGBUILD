# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=synthv1
pkgver=0.8.6
pkgrel=1
pkgdesc="An old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=('x86_64')
url="https://synthv1.sourceforge.io/synthv1-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
         'hicolor-icon-theme' 'shared-mime-info')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5ba4d68002fa316771e2848e37d3f30903ca73bf98d9e5a6b47901ff4b09c3b392397b133248fc2a9b25de2f8f31011bbbe44724a9a75f99a7289e2d09e4fe91')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
