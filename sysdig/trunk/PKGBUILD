# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgbase=sysdig
pkgname=(sysdig sysdig-dkms)
pkgver=0.27.1
pkgrel=5
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
makedepends=('cmake' 'pandoc' 'gtest' 'jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgbase-$pkgver.tar.gz"
        "bashcomp-location.patch"
        "glibc.patch")
sha256sums=('b9d05854493d245a7a7e75f77fc654508f720aab5e5e8a3a932bd8eb54e49bda'
            'aaee8a0ff414a24c5d5a479229324be1667bc5eb70702838f5d617fd986f947b'
            '9d9ee715500cb5c3709cf6a77e95421edaaae5f15dbb1c7c8ad2147cc9637939')

prepare() {
  cd "$srcdir"/$pkgbase-$pkgver
  patch -p1 -i "$srcdir"/bashcomp-location.patch
  patch -p1 -i "$srcdir"/glibc.patch
}

build() {
  cd "$srcdir"/$pkgbase-$pkgver
  rm -rf build
  mkdir build
  cd build

  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS} -Wno-deprecated-declarations" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make
}

package_sysdig() {
  optdepends=('sysdig-dkms: kernel module for live inspection')
  depends=('jsoncpp' 'libjsoncpp.so' 'luajit' 'curl' 'libcurl.so' 'jq' 'libb64' 'intel-tbb' 'grpc')

  cd "$srcdir"/$pkgbase-$pkgver/build
  make install DESTDIR="$pkgdir"
  rm -rf "$pkgdir"/usr/src

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 "$srcdir"/$pkgbase-$pkgver/{NOTICES,COPYING} "$pkgdir"/usr/share/licenses/$pkgname
}

package_sysdig-dkms() {
  pkgdesc="DKMS kernel module for sysdig"
  depends=('sysdig' 'dkms')

  cd "$srcdir"/$pkgbase-$pkgver/build
  make install DESTDIR="$pkgdir"
  rm -rf "$pkgdir"/usr/{share,bin}

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 "$srcdir"/$pkgbase-$pkgver/{NOTICES,COPYING} "$pkgdir"/usr/share/licenses/$pkgname
}
