# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.24.2
pkgrel=1
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('x86_64')
url="https://www.sysdig.com/"
license=('GPL2' 'Apache' 'MIT')
depends=('dkms' 'jsoncpp' 'luajit' 'curl' 'jq' 'libb64' 'intel-tbb')
makedepends=('cmake' 'pandoc')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "bashcomp-location.patch")
sha256sums=('cd925afd2fb0a26728611666e017d480afd49158c2d70714c7461a97c8820807'
            'aaee8a0ff414a24c5d5a479229324be1667bc5eb70702838f5d617fd986f947b')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 -i "$srcdir"/bashcomp-location.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
        -DCMAKE_CXX_FLAGS="-Wno-deprecated-declarations" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=OFF \
        -DBUILD_LIBSCAP_EXAMPLES=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 NOTICES COPYING "$pkgdir"/usr/share/licenses/$pkgname

  cd build
  make install DESTDIR="$pkgdir"

}
