# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Doppler <dopsi[at]dopsi[dot]ch>
# Contributor: Kyle Brooks <brookskd[at]gmail[dot]com>
# Contributor: bl4ckb0x <navkamal90[at]gmail[dot]com>

pkgname=systemc
pkgver=2.3.3
pkgrel=2
pkgdesc="Set of C++ classes and macros which provide an event-driven simulation interface for modeling and describing complex hardware systems"
url="http://www.accellera.org/downloads/standards/systemc"
arch=('x86_64')
license=('Apache')
depends=('gcc-libs')
makedepends=('cmake' 'ninja')
replaces=('systemc-cmake')
source=("http://accellera.org/images/downloads/standards/systemc/$pkgname-$pkgver.tar.gz")
sha512sums=('831255f8e76e1cf776e0407c92c7a0b70ba63cf6b1785a2df372a6394440944540a8ae62264ef5293a7b951bd8b37de089ef0dbc0ee2d0354e69ce552c020ca2')

prepare() {
  mkdir -p build
  sed -i 's/set (CMAKE_BUILD_TYPE Release/set (CMAKE_BUILD_TYPE ""/' $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake -GNinja -DCMAKE_CXX_STANDARD=17 -DCMAKE_INSTALL_PREFIX=/usr ../$pkgname-$pkgver
  ninja
}

check() {
  cd build
  ninja check
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
