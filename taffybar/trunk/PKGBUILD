# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=taffybar
pkgver=3.3.0
pkgrel=23
pkgdesc="A desktop bar similar to xmobar, but with more GUI"
url="http://github.com/taffybar/taffybar"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-configfile' 'haskell-hstringtemplate' 'haskell-x11'
         'haskell-ansi-terminal' 'haskell-broadcast-chan' 'haskell-dbus' 'haskell-dbus-hslogger'
         'haskell-dyre' 'haskell-either' 'haskell-enclosed-exceptions' 'haskell-gi-cairo'
         'haskell-gi-cairo-connector' 'haskell-gi-cairo-render' 'haskell-gi-gdk3'
         'haskell-gi-gdkpixbuf' 'haskell-gi-gdk3x11' 'haskell-gi-glib' 'haskell-gi-gtk3'
         'haskell-gi-gtk-hs' 'haskell-gi-pango' 'haskell-gtk-sni-tray' 'haskell-gtk-strut'
         'haskell-gi' 'haskell-gi-base' 'haskell-hslogger' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-multimap' 'haskell-old-locale'
         'haskell-optparse-applicative' 'haskell-rate-limit' 'haskell-regex-compat' 'haskell-safe'
         'haskell-scotty' 'haskell-split' 'haskell-status-notifier-item'
         'haskell-time-locale-compat' 'haskell-time-units' 'haskell-transformers-base'
         'haskell-tuple' 'haskell-utf8-string' 'haskell-xdg-basedir' 'haskell-xdg-desktop-entry'
         'haskell-xml' 'haskell-xml-helpers' 'xmonad' 'gtk3')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-ghc-9.patch)
sha256sums=('f5ad48f04c2a3f24204609787ef6a456352cb4be35adc5980ea1d5e1c366ef9d'
            '17085debb88e05077f847e152670fd7e847b9bb1035d40990990915c6e66d439')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../$pkgname-ghc-9.patch
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie' 

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $pkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
