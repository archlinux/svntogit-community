# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Scott Horowitz <stonecrest@gmail.com>

pkgname=tagpy
pkgver=0.94.7
pkgrel=6
pkgdesc="Python bindings for TagLib"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/tagpy"
license=('MIT')
depends=('python2' 'taglib' 'boost-libs')
makedepends=('setuptools' 'boost')
source=("http://pypi.python.org/packages/source/t/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('f2304a2e15511cce057c344ea5e15694')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i 's|boost_python-gcc42-mt|boost_python-mt|' setup.py
  find . -name '*.py' | xargs sed -i 's|/usr/bin/env python|/usr/bin/env python2|'
  
  ./configure.py --taglib-lib-dir=/usr/lib/ \
  	--taglib-inc-dir=/usr/include/taglib/ \
	--boost-inc-dir=/usr/include/boost/ \
	--boost-lib-dir=/usr/lib/ \
	--boost-python-libname=boost_python-mt
  python2 setup.py build
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py install --root=${pkgdir}

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
