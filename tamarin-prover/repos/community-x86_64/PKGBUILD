# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=tamarin-prover
pkgver=1.2.2
pkgrel=44
pkgdesc="The Tamarin prover for security protocol analysis"
url="http://tamarin-prover.github.io"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'graphviz' 'maude' "haskell-hunit" "haskell-binary-orphans"
         "haskell-blaze-builder" "haskell-blaze-html" "haskell-cmdargs" "haskell-conduit"
         "haskell-fclabels" "haskell-file-embed" "haskell-gitrev" "haskell-http-types"
         "haskell-lifted-base" "haskell-mtl" "haskell-resourcet" "haskell-safe"
         "haskell-shakespeare" "haskell-text" "haskell-threads" "haskell-wai" "haskell-warp"
         "haskell-yesod-core" "haskell-yesod-static" "haskell-tamarin-prover-utils"
         "haskell-tamarin-prover-term" "haskell-tamarin-prover-theory")
optdepends=('ocaml: for sapic support')
makedepends=('ghc' 'ocaml')
source=("tamarin-prover-$pkgver.tar.gz::https://github.com/tamarin-prover/tamarin-prover/archive/$pkgver.tar.gz"
        binary-orphans-0.1.8.0.patch)
sha256sums=('f9b2d3acc01b89f71d2b246a6b3010ebab71e4fe309b3be8a8eac213422b43de'
            'd2f700f4d5b0a3aaf239139bc4abba85acdc2ab86ae3ff7fe345b95679998011')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i '/cp sapic/d' plugins/sapic/Makefile
    patch -p1 -i ../binary-orphans-0.1.8.0.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-executable-dynamic --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -fthreaded -ftest-coverage -f-build-tests
    runhaskell Setup build

    cd plugins/sapic
    make -j1
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    install -Dm644 etc/filetype.vim "$pkgdir"/usr/share/vim/vimfiles/filetype.vim
    install -Dm644 etc/spthy.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/spthy.vim
    install -Dm644 etc/sapic.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/sapic.vim

    cd plugins/sapic
    install -Dm755 sapic "$pkgdir"/usr/bin/sapic
}
