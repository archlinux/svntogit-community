# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tim Yang <tdy@gmx.com>
# Contributor: StormBlast <stormblast@land.ru>

pkgname=tangerine-icon-theme
pkgver=0.26
pkgrel=5
pkgdesc="Ubuntu's twist on the Tango icon theme"
arch=('any')
url="http://launchpad.net/tangerine-icon-theme/"
license=('custom:cc-by-sa-2.5')
depends=('gnome-icon-theme')
makedepends=('intltool' 'gettext' 'imagemagick' 'icon-naming-utils' 'librsvg'
             'python')
source=(http://archive.ubuntu.com/ubuntu/pool/main/t/$pkgname/${pkgname}_$pkgver.tar.gz
        archlinux-icon-crystal-48.svg)
md5sums=('23fe120a4cd68513b3c10bcbb0a4c293'
         '566b77b3a0c7d8f9dd59096b2363a009')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  # Replace Ubuntu's logo with Arch Linux's
  for size in 16 22 24 32; do
    rsvg -w $size -h $size "$srcdir/archlinux-icon-crystal-48.svg" \
         "$pkgdir/usr/share/icons/Tangerine/${size}x${size}/places/start-here.png"
  done
  install -D -m644 "$srcdir/archlinux-icon-crystal-48.svg" \
          "$pkgdir/usr/share/icons/Tangerine/scalable/places/start-here.svg"

  # Copy over the license
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
