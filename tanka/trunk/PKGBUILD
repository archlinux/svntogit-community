# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=tanka
pkgver=0.19.0
pkgrel=1
pkgdesc="Flexible, reusable and concise configuration for Kubernetes"
arch=('x86_64')
url="https://github.com/grafana/tanka"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/grafana/tanka/archive/v${pkgver}.tar.gz")
sha512sums=('dc4851517a90a898b534d0785e56e052b380d4a2dff8bcdf82b8576017051b24296322c61af81cb457bb75f80633f1b8fbba0a89b6e922f274e520091cf32177')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/grafana "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/grafana/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/grafana/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}/cmd/tk/"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-s -w -linkmode=external -X github.com/grafana/tanka/pkg/tanka.CURRENT_VERSION=${pkgver}" -v .
  chmod +x ./tk
}

check() {
  cd "${PACKAGE_ROOT}"
  go test -v ./...
}

package() {
  cd "${PACKAGE_ROOT}/cmd/tk/"
  install -Dsm755 ./tk "${pkgdir}/usr/bin/tk"
}
