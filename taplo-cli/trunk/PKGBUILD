# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Firegem <mrfiregem [at] protonmail [dot] ch>

pkgname=taplo-cli
pkgver=0.7.0
pkgrel=1
pkgdesc='TOML toolkit written in Rust'
arch=('x86_64')
url=https://taplo.tamasfe.dev/cli/introduction.html
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
provides=("${pkgname%-cli}")
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
        "$pkgname-$pkgver-LICENSE.md::https://raw.githubusercontent.com/tamasfe/taplo/release-$pkgname-$pkgver/LICENSE.md")
b2sums=('af82ef776ad153d76a919c6c28bd5c03ac89aa7d8b13d2215204b071dd734990bc87ee8eab36cd80f7d9e67e76c47309555b384da8a647c413aeed09ff9b717d'
        '2b1282c2e1b54a67fd56894ae96a1447d954b52fecdf0730d8c3fe7a95ccf79245e738588a9bda7262eb0ef2507cdeded228346358894d4bbfc540ec90069964')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked --offline --all-features
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/${pkgname%-cli}
  install -Dm644  ../$pkgname-$pkgver-LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
