# $Id$
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=tarsnap
pkgver=1.0.36.1
pkgrel=1
pkgdesc='An online encrypted snapshotted backup service'
arch=('i686' 'x86_64')
url='http://www.tarsnap.com/'
license=('custom')
depends=('bzip2' 'openssl' 'xz>=5.0.0')
makedepends=('e2fsprogs')
source=("https://www.tarsnap.com/download/${pkgname}-autoconf-${pkgver}.tgz")
validpgpkeys=('38DAFD2CEEB3E78D8627F5486D0D5B8046B64BEB')
md5sums=('3759b06a0fa8d2c7a17ada78c0160380')
sha256sums=('a2909e01e2f983179d63ef2094c42102c92c716032864e66ef25ae341ea28690')

build() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  ./configure --prefix=/usr --sysconfdir="/etc/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
