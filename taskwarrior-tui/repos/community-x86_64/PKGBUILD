# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=taskwarrior-tui
pkgver=0.11.1
pkgrel=1
pkgdesc="A terminal user interface for taskwarrior"
arch=('x86_64')
url="https://github.com/kdheepak/taskwarrior-tui"
license=('MIT')
depends=('task')
makedepends=('rust' 'git' 'pandoc')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "git+https://github.com/kdheepak/taskwarrior-testdata")
sha256sums=('51a50eb3799c38cd7310f63f282cb202344b6d2026ff139ec99c8f367ecc59ea'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked --all-features
  pandoc "docs/$pkgname.1.md" --standalone --to=man -o "docs/$pkgname.1"
}

# https://github.com/kdheepak/taskwarrior-tui/pull/131
#check() {
#  cd "$pkgname-$pkgver"
#  export TASKRC="../taskwarrior-testdata/.taskrc"
#  export TASKDATA="../taskwarrior-testdata/.task"
#  cargo test --release --locked --all-features
#}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "docs/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
