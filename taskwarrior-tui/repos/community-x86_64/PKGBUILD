# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=taskwarrior-tui
pkgver=0.13.21
pkgrel=1
pkgdesc="A terminal user interface for taskwarrior"
arch=('x86_64')
url="https://github.com/kdheepak/taskwarrior-tui"
license=('MIT')
depends=('task')
makedepends=('rust' 'git' 'pandoc')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "git+https://github.com/kdheepak/taskwarrior-testdata")
sha256sums=('ba2222aecfc285a41440d7683370ce789bbfc9484fe9f0322f0ca36e917f82bf'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked --all-features
  pandoc "docs/$pkgname.1.md" --standalone --to=man -o "docs/$pkgname.1"
}

check() {
  cd "$pkgname-$pkgver"
  export TASKRC="../taskwarrior-testdata/.taskrc"
  export TASKDATA="../taskwarrior-testdata/.task"
  cargo test --release --locked --all-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "docs/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
