# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: James Reed <supplantr at archlinux dot info>

pkgname=tcplay
pkgver=1.1
pkgrel=2
pkgdesc="Free and simple TrueCrypt implementation based on dm-crypt."
arch=('i686' 'x86_64')
url="https://github.com/bwalex/tc-play"
license=('custom')
depends=('device-mapper')
makedepends=('cmake')
source=("https://github.com/bwalex/tc-play/archive/v${pkgver}.tar.gz"
        tc-play-1.1-fix-paths.patch)
sha256sums=('62346e852c6f781fffebf5c43e5967ceb0e36304626aa27e77c77378f8215d84'
            '007b960675400b7e0a10765952041dcc1943e5cb79367e2ed4938f2c5e64ea57')

prepare() {
  cd "tc-play-$pkgver"
  patch -Np1 -i ../tc-play-1.1-fix-paths.patch
}

build() {
  cd "tc-play-$pkgver"
  mkdir -p objdir
  cd objdir
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
}

package() {
  cd "tc-play-$pkgver/objdir"
  make DESTDIR="$pkgdir" install
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/tcplay/LICENSE
}
