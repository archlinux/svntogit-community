# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: James Reed <supplantr at archlinux dot info>

pkgname=tcplay
pkgver=2.0
pkgrel=2
pkgdesc="Free and simple TrueCrypt implementation based on dm-crypt."
arch=('i686' 'x86_64')
url="https://github.com/bwalex/tc-play"
license=('custom')
depends=('device-mapper')
makedepends=('cmake' 'udev')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bwalex/tc-play/archive/v${pkgver}.tar.gz"
        tc-play-1.1-fix-paths.patch)
sha256sums=('0e17890f37e862746364b10bad62bbd9f3e2b2811a24b07df12eff266534e42d'
            '007b960675400b7e0a10765952041dcc1943e5cb79367e2ed4938f2c5e64ea57')

prepare() {
  cd "tc-play-$pkgver"
  patch -Np1 -i ../tc-play-1.1-fix-paths.patch
  sed -i 's/-Wnested-externs/-Wnested-externs -fPIC/g' CMakeLists.txt
}

build() {
  cd "tc-play-$pkgver"
  mkdir -p objdir
  cd objdir
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
}

package() {
  cd "tc-play-$pkgver/objdir"
  make DESTDIR="$pkgdir" install
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/tcplay/LICENSE
}
