# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Danilo Bargen <aur at dbrgn dot ch>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=tealdeer
_binname=tldr
pkgver=1.5.0
pkgrel=1
pkgdesc="A fast tldr client in Rust"
arch=('x86_64')
url="https://github.com/dbrgn/tealdeer"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust')
provides=('tldr')
conflicts=('tldr')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2a942746942b6c1046013628c5d158d6ef494f216a9328d0d717f09dcdd0dddde5061f3b823cc69fdcf9058af8ea275732cb4972f0a7b5139b1027f66fe19911')
b2sums=('a4f0d5be805fb3a18b3195225500c375f42530dd250566f56dd9116b2d02a1e62cb1e098a035e50b1c6960d78be7084a747675297d285b7f943d1c3ec1bc266d')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$_binname"

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*

  # completions
  install -vDm644 bash_tealdeer "$pkgdir/usr/share/bash-completion/completions/$_binname"
  install -vDm644 fish_tealdeer "$pkgdir/usr/share/fish/vendor_completions.d/$_binname.fish"
  install -vDm644 zsh_tealdeer "$pkgdir/usr/share/zsh/site-functions/_$_binname"
}

# vim:set ts=2 sw=2 et:
