# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: J.W. Birdsong  <jwbirdsongATmailDOTcom>

pkgname=teamspeak3
pkgver=3.0.19.3
pkgrel=1
pkgdesc="TeamSpeak is software for quality voice communication via the Internet"
url="http://www.teamspeak.com/"
license=('custom')
depends=('qt5-base' 'libxkbcommon-x11')
optdepends=('libpulse')
arch=('i686' 'x86_64')
source=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_x86-${pkgver}.run"
        "http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_amd64-${pkgver}.run"
        'teamspeak3.desktop'
        'teamspeak3.png'
        'teamspeak3.launcher')
md5sums=('9660df59b87c2c96f948b6a999ad635c'
         '259f7e8408596cf46eeb3085abb0d766'
         '98e987a42511f159fa2228f5e0bffed1'
         '1d193af5808f4ca718c800da3f127ca9'
         '170fcbb1b468ce4f3dba7ee3fb2a1c6d')

[[ "$CARCH" == "i686" ]] && _TSARCH='x86'
[[ "$CARCH" == "x86_64" ]] && _TSARCH='amd64'

prepare() {
  mkdir archive && cd archive
  sh ../TeamSpeak3-Client-linux_${_TSARCH}-${pkgver}.run --tar -xf 2>/dev/null

  # Delete bundled Qt libs to use system-wide ones
  rm libQt5*

  # Fix FS#34190
  sed -i "/export QTDIR.*/d" ts3client_runscript.sh
  sed -i "/export QT_PLUGIN_PATH*/d" ts3client_runscript.sh
  
  # Fix FS#34189
  rm qt.conf

  # Fix permissions
  find -type d | xargs chmod 755
  find -type f | xargs chmod 644
  find -name *.so | xargs chmod 755
  chmod +x ts3client*
}

package() {
  install -d ${pkgdir}/{usr/bin/,opt/teamspeak3}

  cp -r archive/* ${pkgdir}/opt/teamspeak3/

  # Install Desktop File
  install -D -m644 $srcdir/teamspeak3.desktop ${pkgdir}/usr/share/applications/teamspeak3.desktop

  # Install Icon File
  install -D -m644 $srcdir/teamspeak3.png ${pkgdir}/usr/share/pixmaps/teamspeak3.png

  # Install Custom License
  #install -D -m644 ${pkgdir}/opt/teamspeak3/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # Install Client Launcher
  install -D -m755 $srcdir/teamspeak3.launcher ${pkgdir}/usr/bin/teamspeak3
}
# vim:set ts=2 sw=2 et:
