# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=ted
pkgver=2.22
pkgrel=1
pkgdesc='Easy rich text processor (with footnote support)'
arch=('i686' 'x86_64')
url="http://www.nllgg.nl/Ted/"
depends=('bash' 'libtiff' 'libxmu' 'libxpm' 'libpng' 'gtk2')
license=('GPL')
source=(ftp://ftp.nluug.nl/pub/editors/$pkgname/$pkgname-$pkgver.src.tar.gz)
md5sums=('08162c9164dfdde1d46e41ae64df16be')

build() {
  cd "$srcdir/Ted-$pkgver"
  sed -i "s|--with-GTK|--with-GTK --prefix=/usr/share|g" Makefile
  make compile.shared
  make package.shared
}

package() {
  # unpack package
  mkdir -p $pkgdir/usr/share
  cd "$pkgdir/usr/share"
  tar xzf $srcdir/Ted-$pkgver/tedPackage/*.tar.gz
  # move bin/* and share/*
  mv bin ../
  mv share/* ./
  rmdir share
  # move man pages
  cd "$pkgdir/usr/share/Ted/"
  mkdir -p "$pkgdir/usr/share/man/man1"
  mv Ted.1 rtf2pdf.1 rtf2ps.1 ../../share/man/man1/
  # fix shell script and move them to /usr/bin
  sed -i "s|usr/local/afm|usr/share/Ted/afm|g" gsafm.sh
  mv *.sh "$pkgdir/usr/bin/"
}
