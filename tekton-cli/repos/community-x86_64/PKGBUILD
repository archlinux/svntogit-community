# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=tekton-cli
pkgver=0.15.0
pkgrel=2
pkgdesc="CLI for interacting with the Tekton CI/CD pipeline"
arch=('x86_64')
url="https://github.com/tektoncd/cli"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}::git+https://github.com/tektoncd/cli#tag=v${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('2B68199819C7A977A9C7F5928E00E7B02807E02D')

build() {
  cd "${pkgname}/cmd/tkn"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-linkmode=external" .
  chmod +x ./tkn
  ./tkn completion bash > "${pkgname}-completion.bash"
  ./tkn completion zsh > "${pkgname}-completion.zsh"
}

check() {
  cd "${pkgname}/cmd/tkn"
  go test ./...
}

package() {
  cd "${pkgname}/cmd/tkn"
  install -Dsm755 ./tkn "${pkgdir}/usr/bin/tkn"
  install -Dm644 "${pkgname}-completion.bash" "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  install -Dm644 "${pkgname}-completion.zsh" "${pkgdir}/usr/share/zsh/site-functions/_tkn"
}
