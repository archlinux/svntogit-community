# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Contributor: nylocx <aur@nyloc.de>

_pkgname=TelegramQML
pkgname=telegramqml
pkgver=2.0.0
pkgrel=1
pkgdesc="Telegram API tools for QtQml and Qml"
arch=('x86_64')
url="https://github.com/Aseman-Land/TelegramQML"
license=('GPL')
depends=('qt5-base' 'qt5-declarative' 'qt5-multimedia'
         'qt5-webkit' 'qt5-imageformats' 'qt5-graphicaleffects'
         'qt5-quickcontrols' 'libqtelegram-ae')
source=("https://github.com/Aseman-Land/TelegramQML/archive/v${pkgver}.zip")
sha512sums=('2e6a649ed6d270ceb56b3e2e22adb5d8cdbb2986610bcc5c0dec326ee49581f03b5c57f21e6fb99ffd1917eb5b5a54dc4613b55233414f3fb647bf542a12eb22')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  mkdir -p build
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  qmake-qt5 -r .. QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}/build"
  make INSTALL_ROOT="${pkgdir}" install
}
