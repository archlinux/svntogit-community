# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Maintainer: Carsten Haitzler <raster@archlinux.org>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Paul Ezvan <paul@ezvan.fr>

pkgname=terminology
pkgver=1.12.1
pkgrel=1
pkgdesc="EFL based terminal emulator"
arch=('x86_64')
url="https://www.enlightenment.org/about-terminology"
license=('BSD')
depends=('efl')
makedepends=('meson')
source=("https://download.enlightenment.org/rel/apps/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f8ced9584c2e9ae87452ce7425fd25b2d3e122c7489785d2917890215c6b5aa9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"
  meson builddir --prefix=/usr
  ninja -C builddir
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C builddir install

  # install license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
