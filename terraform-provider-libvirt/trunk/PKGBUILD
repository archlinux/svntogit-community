# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=terraform-provider-libvirt
pkgver=0.6.3
pkgrel=2
pkgdesc="Terraform provider to provision infrastructure with Linux's KVM using libvirt"
arch=("x86_64")
url="https://github.com/dmacvicar/terraform-provider-libvirt"
license=('Apache')
depends=('glibc' 'terraform')
makedepends=('go' 'git' 'libvirt')
optdepends=('cdrtools: for generating cloud-init images')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dmacvicar/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('06f9283a30f6739dc879e592834a703e6e5e4766cd406ec51f40fb96f8b47291e2ef004a90e2724f61fc60405aa3927fd9ae8ccc097fba4546fb9fbdc1b1673d')
b2sums=('a62a5aee5c780d63cdea9f924e5e7abd21ee9af575f13ade6b894c2b58be5968cd05cd5019d3345f39d720bbfb346f95b1a4a93a6dd4853b6a59a58b4c0209ab')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -vp build
}

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o "build/${pkgname}"
}

package() {
  depends+=('libvirt.so' 'libvirt-lxc.so' 'libvirt-qemu.so')
  cd "${pkgname}-${pkgver}"
  # terraform unfortunately only accepts non-FHS compliant directories for plugins :(
  install -vDm 755 "build/$pkgname" \
    "$pkgdir/usr/share/terraform/plugins/registry.terraform.io/dmacvicar/libvirt/${pkgver}/linux_amd64/${pkgname}"
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # examples
  find examples/ -type f \( \
      -iname "*.cfg" -or \
      -iname "*.conf" -or \
      -iname "*.md" -or \
      -iname "*.mount" -or \
      -iname "*.network" -or \
      -iname "*.service" -or \
      -iname "*.tf" -or \
      -iname "*.xsl" -or \
      -iname "*id_rsa*" \
    \) -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
  find website/ -type f -iname "*.markdown" -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
