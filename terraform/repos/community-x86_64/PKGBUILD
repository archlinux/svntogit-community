# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.13.4
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go")
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz"
	"25555.patch")
sha512sums=('e02cb2a9967a923b918e64acd2ffa6f8a8a03676e5207d238a6cf215f4e5cb0e1db3ac9760792187757a8f18d9d23cbfd44c1ce5d6d5bacf70786ef473824dc3'
            'bb88abb0b57d2a1f779df2036fe529cb17c95fbfc8831a79fdb7a97a7790d3e7247998c2757d7dd7c6193dd48bc99893363363dcf2b26f6d1ecc6f4cb2568c2a')
changelog="CHANGELOG.md"

prepare() {
	cd "$pkgname-$pkgver"
	#  helper/schema: ensure test works with Go 1.15 error messages
	patch -p1 < "${srcdir}/25555.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o terraform-binary
}

check() {
	cd "${pkgname}-${pkgver}"
	go test -mod=vendor ./...
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
