# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.5
pkgrel=2
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('8b559475aeb8689f0495febeb9c95fed1a2b00126dd3ef68fef2625dbf1c603b682849bad9c9e59dd6d16d347c9b6c1509c9745ceb95c6ab909ec4c4d121203e')
changelog=CHANGELOG

prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hashicorp/
  mv "${pkgname}-${pkgver}" src/github.com/hashicorp/${pkgname}
}

build() {
  cd src/github.com/hashicorp/"${pkgname}"
  go build -o terraform-binary
}

package() {
  cd src/github.com/hashicorp/"${pkgname}"
  install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
