# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=terraform
pkgver=1.0.11
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go")
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz"
        "terraform.bash"
        "terraform.fish"
        "terraform.zsh")
sha512sums=('54ef909c0e2bbab7f85268005cb4896e7438bb5847c0f3a028ba4aec7fe4168ed4b1800105309d7fc6aff629f561a5b22c3ea0651cd24ada3a5901d228174129'
            'ea42bfc41288e5ad741fcddfbd5c1f8ba0088be0967b820db1e12e1dc13d1ba242b9cc1c247bf29c36ec59cdce76ff6703cf6b3f76cca5ec441a4927a01afe7d'
            '44b387fbd7b6abb8f524999d3d5f14fd4d5be54b2b34336a708c6a493c93c886a7230d3102601604f62cf95c53e91de683919213d0e1473ee84e606030d249e3'
            'ad991daf40f68c11fa66256177a04d97bd38d6a89c554d6261489d4de703852628ffc37429d862237ca24e15f6a7c915cfa027731189ac89a27f7b20c72ab4c9')
b2sums=('e4ffeb454be380aa806b529d433b46de51cf7f5ececffde28ec85d02ea026614ac88fedd397ca60ef9f28b07490c5c0d2c195b9db2e2ee87ec180dc2b611203a'
        'd047735bbb006e9afbaa4b18af7a7f16a205ec528e91caa61a0b663dd6e24ec1df999216f0b7bd06e3b3c087b37f6ce3aa3cf3a20c892a9cc1962d33ce1b0bcc'
        'd3655f23ba8893d0f8c6cd5e8b42fae473ccfbc77d97b7424aa1f0d0057bfed6cb4d2505e74dd89099c39af6bc03b92eddedae5bb2821ff905d06b5e311be1f0'
        'd58712c8203e4a58ec68738f3d22457547a55d230018408400fd1ca433346171b72fa4a87f05efc93213c71f9757d3a8072149ca4e6894355e79f8cfae3bd55b')

build() {
	cd "$pkgname-$pkgver"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o terraform-binary
}

check() {
	cd "${pkgname}-${pkgver}"
	go test -mod=readonly ./...
}

package() {
    install -Dm644 terraform.bash "$pkgdir/usr/share/bash-completion/completions/terraform"
    install -Dm644 terraform.fish "${pkgdir}/usr/share/fish/vendor_completions.d/terraform.fish"
    install -Dm644 terraform.zsh "$pkgdir/usr/share/zsh/site-functions/_terraform"

	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
