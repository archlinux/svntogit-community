# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=terraform
pkgver=0.11.0
pkgrel=1
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="http://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie" "godep" "git" "mercurial")
_gourl="github.com/hashicorp"
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha256sums=("2625ab24b71ccb8b9e1cf8794c3d3cd147313768066103fb970d58fa6a0a65fe")


prepare() {
	mkdir -p "$srcdir/src/$_gourl"
	rm -rf "$srcdir/src/$_gourl/$pkgname"
	mv -f "terraform-$pkgver" "$srcdir/src/$_gourl/$pkgname"
	msg2 "Fetching dependencies"
	cd "$srcdir/src/$_gourl/$pkgname"
	GOPATH="$srcdir" go get -u github.com/mitchellh/gox
	GOPATH="$srcdir" go get -u golang.org/x/tools/cmd/stringer
}


build() {
	msg2 "Build program"
	cd "$srcdir/src/$_gourl/$pkgname"
	GOPATH="$srcdir" PATH="$srcdir/bin:$PATH" TF_DEV=1 TF_QUICKDEV=1 make bin
}


package() {
	cd "$srcdir/bin"
	install -Dm755 terraform "$pkgdir/usr/bin/terraform"

	cd "$srcdir/src/$_gourl/$pkgname"
	# Zsh completion
	install -D contrib/zsh-completion/_terraform "$pkgdir/usr/share/zsh/site-functions/_terraform"
}
