# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: portals <portals at riseup.net>
# Contributor: novemberist
# Contributor: PyroDevil
# Contributor: giacomogiorgianni@gmail.com 

pkgname=tesseract-game
pkgver=1
pkgrel=1
pkgdesc='Smooth FPS with map editing, instagib, DM and CTF'
url='http://tesseract.gg/'
arch=('x86_64' 'i686')
license=('ZLIB')
depends=('libgl' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libpng' 'mesa' 'zlib')
makedepends=('subversion' 'gendesk' 'imagemagick')
source=('tesseract::svn://svn.tuxfamily.org/svnroot/tesseract/main#revision=1678'
        'tesseract_large.png::http://upload.wikimedia.org/wikipedia/commons/5/50/Truncated_tesseract_stereographic_%28tC%29.png'
        'tesseract.sh')
md5sums=('SKIP'
         '3f7098fad57081bb1af0ee4771e20ad3'
         'ab00bafb80c1c7764cba8f51120d4a9a')

prepare() {
  gendesk -f -n --pkgname tesseract --pkgdesc "$pkgdesc" --exec tesseract-game
  convert -transparent white -resize 48x48 tesseract_large.png tesseract.png 2> /dev/null
}

build() {
  make -C tesseract/src
}

package() {
  cd tesseract
  make -C src install
  install -d "$pkgdir/usr/share/tesseract-game/"
  cp -R media/ config/ "$pkgdir/usr/share/tesseract-game"
  install -Dm755 bin_unix/native_client "$pkgdir/usr/bin/tesseract-client"
  install -Dm755 bin_unix/native_server "$pkgdir/usr/bin/tesseract-server"
  install -Dm644 src/readme_tesseract.txt "$pkgdir/usr/share/licenses/tesseract-game/LICENSE"
  install -Dm755 ../tesseract.sh "$pkgdir/usr/bin/tesseract-game"
  install -Dm644 ../tesseract.desktop "$pkgdir/usr/share/applications/tesseract.desktop"
  install -Dm644 ../tesseract.png "$pkgdir/usr/share/pixmaps/tesseract.png"
}

# vim:set ts=2 sw=2 et:
