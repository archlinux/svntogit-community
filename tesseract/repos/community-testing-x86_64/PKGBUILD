# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

_langs=(afr ara aze bel ben bul cat ces chi_sim chi_tra chr dan deu ell eng enm
    epo equ est eus fin fra frk frm glg grc heb hin hrv hun ind isl ita ita_old
    jpn kan kor lav lit mal mkd mlt msa nld nor pol por ron rus slk slv spa
    spa_old sqi srp swa swe tam tel tgl tha tur ukr vie)

pkgname=tesseract
pkgver=4.0.0
pkgrel=2
pkgdesc="An OCR program"
arch=(x86_64)
url="https://github.com/tesseract-ocr/tesseract"
license=("APACHE")
depends=(libpng libtiff libjpeg-turbo zlib giflib gcc-libs leptonica)
makedepends=(icu cairo pango asciidoc)
install=tesseract.install
optdepends=('icu' 'cairo' 'pango'
	    $(for l in ${_langs[@]}; do echo tesseract-data-${l}; done))
source=($pkgname-$pkgver.tar.gz::https://github.com/tesseract-ocr/tesseract/archive/$pkgver.tar.gz
        https://github.com/tesseract-ocr/tessdata/raw/bf82613055ebc6e63d9e3b438a5c234bfd638c93/osd.traineddata
        fix_man_page_html.patch)
sha256sums=('a1f5422ca49a32e5f35c54dee5112b11b99928fc9f4ee6695cdc6768d69f61dd'
            '9cf5d576fcc47564f11265841e5ca839001e7e6f38ff7f7aacf46d15a96b00ff'
            'd5fa9a8605afa775255404acb882471f894dc0d4de87eb8333fd155d649b7235')

prepare() {
  cd "$srcdir"/$pkgname-${pkgver}
  patch -Np0 -i "${srcdir}/fix_man_page_html.patch"
}

build() {
  cd "$srcdir"/$pkgname-${pkgver}
  [ -x configure ] || ./autogen.sh
  [ -f Makefile ] || ./configure --prefix=/usr
  make
  make training
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" training-install
  mkdir -p "$pkgdir"/usr/share/tessdata
  install -Dm0644 "$srcdir"/osd.traineddata "$pkgdir"/usr/share/tessdata/osd.traineddata
}
