# $Id$
# Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=texmaker
pkgver=4.1
pkgrel=1
pkgdesc='Free cross-platform LaTeX editor'
arch=('i686' 'x86_64')
url="http://www.xm1math.net/texmaker/index.html"
license=('GPL')
depends=('poppler-qt5' 'qt5-webkit' 'desktop-file-utils')
optdepends=('ghostscript: LaTeX to HTML conversion command'
	    'ibus-qt5: accented characters support'
	    'poppler: PDF rendering support')
install="$pkgname.install"
source=("http://www.xm1math.net/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('97ef7f97e73d69283391e467e5758275')

build() {
  cd "$pkgname-$pkgver"
  qmake-qt5 PREFIX=/usr texmaker.pro
  make
}
package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -dm755 "$pkgdir/usr/share"
  make INSTALL_PATH="$pkgdir" INSTALL_ROOT="$pkgdir" PREFIX="/usr" install
}
