# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=texmaker
pkgver=5.1.3
pkgrel=1
pkgdesc='Free cross-platform LaTeX editor'
arch=(x86_64)
url='https://www.xm1math.net/texmaker/index.html'
license=(GPL)
depends=(qt6-declarative qt6-5compat texlive-core)
optdepends=('ghostscript: LaTeX to HTML conversion command')
source=(https://www.xm1math.net/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('bfa52e5e0e3c61d5258ea8c69ea527f6bd20f0e23fe2da43b80aee00b2609f61')

build() {
  cd $pkgname-$pkgver
  qmake6 PREFIX=/usr texmaker.pro
  make
}
package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share
  make INSTALL_PATH="$pkgdir" INSTALL_ROOT="$pkgdir" PREFIX="/usr" install
}
