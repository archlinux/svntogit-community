# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=texworks
pkgver=0.6.5
pkgrel=3
pkgdesc='Unicode-based, TeX-aware editor with integrated PDF viewer'
arch=('x86_64')
url='https://www.tug.org/texworks/'
license=('GPL')
depends=('hunspell' 'libsynctex' 'poppler-qt5' 'qt5-base' 'qt5-script' 'libpoppler-qt5.so')
makedepends=('cmake' 'lua' 'python' 'qt5-tools')
checkdepends=('gsfonts' 'poppler-data' 'xorg-server-xvfb')
optdepends=('gsfonts: correctly display PDF files without embedded fonts'
            'lua: Lua scripts support'
            'poppler-data: correctly display certain CJK documents'
            'python: Python scripts support'
            'texlive-core: typeset TeX documents')
source=("$pkgname-$pkgver.tar.gz::https://github.com/TeXworks/texworks/archive/release-$pkgver.tar.gz"
        "texworks-0.6.5_fix-compilation-with-qt-5.15.patch::https://github.com/TeXworks/texworks/commit/a5352a3a94e3685125650b65e6197de060326cc2.patch")
sha512sums=('d97bb7c3ad7bbb402be80556027ef19076dae91bb9449df2b45d54e89b492e32b8d6f109d1bda9388424af6218866d84a8c296c9bc2ef644cf2fac0d4b24939e'
            '8cdc834a2d0d8d843e326a825a13418c765079ec41c15f1fe5b7a3eb2c158928db594ba7d7e946dd72a936af4bf86a302dd5b042fad64d20fe033f2b64dea3e8')

prepare() {
	cd "texworks-release-$pkgver"
	patch --strip=1 --input="$srcdir/texworks-0.6.5_fix-compilation-with-qt-5.15.patch"
}

build() {
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	      -DCMAKE_BUILD_TYPE=None \
	      -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
	      -DTW_BUILD_ID='Arch Linux' \
	      -DWITH_PYTHON=ON \
	      -B build \
	      -S "texworks-release-$pkgver"
	make -C build
}

check() {
	cd build
	xvfb-run ctest --output-on-failure
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
