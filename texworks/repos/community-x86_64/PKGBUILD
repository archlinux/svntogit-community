# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=texworks
pkgver=0.6.6
pkgrel=3
pkgdesc='Unicode-based, TeX-aware editor with integrated PDF viewer'
arch=('x86_64')
url='https://www.tug.org/texworks/'
license=('GPL')
depends=('hunspell' 'libsynctex' 'poppler-qt5' 'qt5-base' 'qt5-declarative' 'qt5-script' 'libpoppler-qt5.so')
makedepends=('cmake' 'lua' 'python' 'qt5-tools')
checkdepends=('gsfonts' 'poppler-data' 'xorg-server-xvfb')
optdepends=('gsfonts: correctly display PDF files without embedded fonts'
            'lua: Lua scripts support'
            'poppler-data: correctly display certain CJK documents'
            'python: Python scripts support'
            'texlive-core: typeset TeX documents')
source=("$pkgname-$pkgver.tar.gz::https://github.com/TeXworks/texworks/archive/release-$pkgver.tar.gz")
sha512sums=('3c1f09d3bb1a2a32947abc079c8def1a5f8a12203ccc6d25a2dc98ae7c4506f83653e8cac467ee74669d48a152c69110250c925c2bf9e40bafd6ae123f68a891')

build() {
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	      -DCMAKE_BUILD_TYPE=None \
	      -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
	      -DTW_BUILD_ID='Arch Linux' \
	      -DWITH_PYTHON=ON \
	      -B build \
	      -S "texworks-release-$pkgver"
	make -C build
}

check() {
	cd build
	xvfb-run ctest --output-on-failure
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
