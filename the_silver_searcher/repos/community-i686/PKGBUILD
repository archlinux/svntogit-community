# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jeff Horelick <jdhore1@gmail.com>
# Contributor: skydrome <skydrome@i2pmail.org>

pkgname=the_silver_searcher
pkgver=0.19.2
pkgrel=1
pkgdesc='Code searching tool similar to Ack, but faster'
url='https://github.com/ggreer/the_silver_searcher'
license=('Apache')
depends=('pcre' 'zlib' 'xz')
arch=('x86_64' 'i686')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ggreer/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('e2de31b8b4745b4614d3a6fa4cd61130474b0e861e80b3784daee7ce0eeca300')

prepare() {
  cd "$pkgname-$pkgver"
  
  sed -i 's:configure:configure --prefix=/usr:' build.sh
}

build() {
  cd "$pkgname-$pkgver"

  ./build.sh
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
