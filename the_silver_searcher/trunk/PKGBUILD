# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jeff Horelick <jdhore1@gmail.com>
# Contributor: skydrome <skydrome@i2pmail.org>

pkgname=the_silver_searcher
pkgver=0.18.1
pkgrel=1
pkgdesc='Code searching tool similar to Ack, but faster'
url='https://github.com/ggreer/the_silver_searcher'
license=('Apache')
depends=('pcre' 'zlib' 'xz')
arch=('x86_64' 'i686')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ggreer/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('1f5cdacf955d5707cdb60f3f46aab3aae7fe96f105f00ab2d6a5a52d0aad5dc5')

prepare() {
  cd "$pkgname-$pkgver"
  
  sed -i 's:configure:configure --prefix=/usr:' build.sh
}

build() {
  cd "$pkgname-$pkgver"

  ./build.sh
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
