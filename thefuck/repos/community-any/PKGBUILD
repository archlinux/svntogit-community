# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=1.12
pkgrel=1
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-pathlib')
checkdepends=('python-pytest' 'python-mock')
source=("https://pypi.python.org/packages/source/t/thefuck/thefuck-$pkgver.tar.gz")
sha256sums=('e960ccb4181be01668815580d69e3eb42380a8b9989ba408ad31aa0f5bfe5c15')

build() {
  cd "${srcdir}/thefuck-$pkgver"
  python setup.py build
}

check() {
  # Tests not included in released tarball
  cd "${srcdir}/thefuck-$pkgver"
  # py.test
}

package() {
  cd thefuck-$pkgver
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
