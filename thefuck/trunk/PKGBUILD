# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=1.42
pkgrel=1
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-pathlib' 'python-psutil' 'python-six' 'python-colorama')
makedepends=('git')
checkdepends=('python-pytest-mock' 'python-mock')
install=thefuck.install
source=("git+https://github.com/nvbn/thefuck.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd thefuck
  python setup.py build
}

check() {
  cd thefuck
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test
}

package() {
  cd thefuck
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
