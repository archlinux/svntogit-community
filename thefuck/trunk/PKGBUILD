# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=1.13
pkgrel=1
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-pathlib' 'python-psutil')
checkdepends=('python-pytest' 'python-mock')
source=("https://pypi.python.org/packages/source/t/thefuck/thefuck-$pkgver.tar.gz")
sha256sums=('3d00222a2ee5eede2fafe69f16ac697fab6c29dcf0d83c3efbcc7bcb6dff145b')

build() {
  cd "${srcdir}/thefuck-$pkgver"
  python setup.py build
}

check() {
  # Tests not included in released tarball
  cd "${srcdir}/thefuck-$pkgver"
  # py.test
}

package() {
  cd thefuck-$pkgver
  python3 setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim:set ts=2 sw=2 et:
