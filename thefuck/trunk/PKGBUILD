# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=3.17
pkgrel=1
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-psutil' 'python-six' 'python-colorama' 'python-decorator')
makedepends=('python-pip')
checkdepends=('python-pytest-mock' 'python-mock' 'python-pexpect')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nvbn/thefuck/archive/$pkgver.tar.gz")
sha512sums=('acb50b88357f4a5de5bc9201b2f7676c4e912151aa643a67d7cdc921a5928b0ee62094bb5827ea4b2e5788212537abffa0515f4f0b7d3715d65919609b72fe3f')

build() {
  cd thefuck-$pkgver
  python setup.py build
}

check() {
  cd thefuck-$pkgver

  # Hack distribution test by installing it and set PYTHONPATH afterwards
  python setup.py install --root="$PWD/tmp_install" --optimize=1

  # Hack $HOME as it's / in the chroot and not writable
  cp tests/test_conf.py "$srcdir/test_conf.py.bak"
  sed -i "s|data = {}|data = {'HOME': '$srcdir'}|" tests/test_conf.py
  LC_CTYPE=en_US.utf8 PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test tests
  mv "$srcdir/test_conf.py.bak" tests/test_conf.py
}

package() {
  cd thefuck-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
