# $Id$
# Maintainer: Anatol Pomozov
# Contributor: Byron Clark <byron@theclarkfamily.name>

# This package contains only thrift compiler and C/C++ libraries
# for all the language binding one should use corresponding language-specific package manager:
# CPAN for perl, gem for ruby, pip for python etc... Or better to create arch packages that install
# the binding from the language specific managers python2-thrift, perl-thrift, ruby-thrift, ...

pkgname=thrift
pkgver=0.9.1
pkgrel=2
pkgdesc='Scalable cross-language services framework for IPC/RPC'
arch=(i686 x86_64)
url='http://thrift.apache.org/'
license=(APACHE)
depends=(gcc-libs openssl)
makedepends=(boost libevent emacs-nox)
source=(http://www.apache.org/dist/thrift/$pkgver/thrift-$pkgver.tar.gz)
sha1sums=('dc54a54f8dc706ffddcd3e8c6cd5301c931af1cc')

build() {
  cd thrift-$pkgver

  export CPPFLAGS="$CPPFLAGS -O2 -fno-strict-aliasing"
  # tests compilation is broken https://issues.apache.org/jira/browse/THRIFT-2545 enable tess with 0.9.2
  ./configure --prefix=/usr \
              --with-cpp \
              --with-c_glib \
              --without-haskell \
              --without-php \
              --without-ruby \
              --without-python \
              --without-erlang \
              --without-perl \
              --without-java \
              --without-c_sharp \
              --without-d \
              --without-php \
              --without-go \
              --without-tests
              
  make

  emacs -Q --batch -f batch-byte-compile contrib/thrift.el
}

package() {
  cd thrift-$pkgver
  make DESTDIR="$pkgdir" install

  install -m0644 -D contrib/thrift.vim "$pkgdir"/usr/share/vim/vimfiles/syntax/thrift.vim
  install -m0644 -D contrib/thrift.el "$pkgdir"/usr/share/emacs/site-lisp/thrift.el
  install -m0644 -D contrib/thrift.elc "$pkgdir"/usr/share/emacs/site-lisp/thrift.elc
}
