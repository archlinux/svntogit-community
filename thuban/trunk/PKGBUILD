# $Id$
# Contributor: dibblethewrecker dibblethewrecker.at.gmail.com

pkgname=thuban
pkgver=1.2.1
pkgrel=3
pkgdesc='Interactive Geographic Data Viewer with support for vector & raster data.'
arch=('i686' 'x86_64')
url='http://thuban.intevation.org/'
license=('GPL')
depends=('gdal' 'python-pysqlite-legacy' 'wxpython' 'wxgtk')
makedepends=('proj')
source=("http://wald.intevation.org/frs/download.php/408/Thuban-${pkgver}.tar.bz2" 
        'thuban.desktop')
md5sums=('7edc3b2b890960a3e3fe43008d948aab'
         '25b3d0a5a997c4f102bc9bed3853cdda')

build() {
  cd Thuban-${pkgver}

  sed -i 's_/usr/bin/python_/usr/bin/python2_' Thuban/Lib/fileutil.py
  
  python2 setup.py build
}

package() {
  cd Thuban-${pkgver}

  python2 setup.py install --root=${pkgdir} --prefix=/usr --optimize=1

  # install some freedesktop.org compatibility
  install -D -m644 ${srcdir}/thuban.desktop \
      ${pkgdir}/usr/share/applications/thuban.desktop
}
