# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=tilda
pkgver=1.1.8
pkgrel=1
pkgdesc="A Gtk based drop down terminal for Linux and Unix"
arch=('i686' 'x86_64')
url="https://github.com/lanoxx/tilda"
license=('GPL')
depends=('vte3' 'confuse')
source=(https://github.com/lanoxx/$pkgname/archive/$pkgname-$pkgver.tar.gz
        tab-count-fix.patch)
sha256sums=('f339974080db3fe6eb33d23c4fe3eca0b2d432a356808920f1c465695babb724'
            '5d80a76f904a2094cd7b156654ea6d1860c319fc6c18927f2fb342c0f477c110')

prepare() {
  cd $pkgname-$pkgname-$pkgver

  patch -Np1 -i ../tab-count-fix.patch
}

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
