# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Joost Bremmer <toost dot b at gmail dot com>

pkgname=timew
pkgver=1.4.3
pkgrel=2
pkgdesc="Timewarrior, A command line time tracking application"
arch=('x86_64')
url="https://timewarrior.net/"
license=('MIT')
makedepends=('cmake' 'gcc-libs' 'asciidoctor')
optdepends=('task: Taskwarrior integration'
            'python: Holiday refresh script')
install=$pkgname.install
source=("https://github.com/GothenburgBitFactory/timewarrior/releases/download/v${pkgver}/timew-${pkgver}.tar.gz")
sha256sums=('c4df7e306c9a267c432522c37958530b8fd6e5a410c058f575e25af4d8c7ca53')

build() {
    cd "${pkgname}-${pkgver}"
    cmake -DCMAKE_BUILD_TYPE=release -DCMAKE_INSTALL_PREFIX=/usr .
    make
}


package() {
    cd "${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" install

    # Install the refresh holiday scripts in /usr/bin/ for user convenience.
    chmod 755 "${pkgdir}"/usr/share/doc/timew/doc/holidays/refresh
    ln -s /usr/share/doc/timew/doc/holidays/refresh "${pkgdir}"/usr/bin/timew-refresh-holidays

    # remove useless man pages
    rm -r $pkgdir/usr/share/man/man1/CMakeFiles
}
