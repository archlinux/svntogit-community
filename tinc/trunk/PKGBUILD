# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: helios
# Contributor: Lothar Gesslein

pkgname=tinc
pkgver=1.0.16
pkgrel=1
pkgdesc="VPN (Virtual Private Network) daemon"
arch=('i686' 'x86_64')
url="http://www.tinc-vpn.org/"
license=('GPL')
depends=('lzo2' 'openssl')
install=tinc.install
source=("http://www.tinc-vpn.org/packages/tinc-$pkgver.tar.gz"
        "tincd.rc" "tincd.conf")
md5sums=('f1c7ed94878725fb2cf4efb02bf160da'
         '64bb53748d9051255f243003f0d511f5'
         '7c0276d4812cc5d3cc18142562a3aa82')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -dm755 "$pkgdir"/usr/share/tinc/examples
  tar xozvf doc/sample-config.tar.gz -C $pkgdir/usr/share/tinc/examples
  find "$pkgdir"/usr/share/tinc/examples -type f -exec chmod 644 {} +
  find "$pkgdir"/usr/share/tinc/examples -type d -exec chmod 755 {} +

  install -Dm755 "$srcdir/tincd.rc" "$pkgdir/etc/rc.d/tincd"
  install -Dm644 "$srcdir/tincd.conf" "$pkgdir/etc/conf.d/tincd.conf"
}
