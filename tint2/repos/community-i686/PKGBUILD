# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.11
pkgrel=8
pkgdesc='Basic, good-looking task manager for WMs'
arch=('x86_64' 'i686')
url='http://code.google.com/p/tint2/'
license=('GPL2')
conflicts=('ttm-svn' 'tint')
replaces=('tint')
depends=('gtk2' 'imlib2')
optdepends=('pygtk: for tint2wizard' 'python2')
makedepends=('pygtk' 'cmake' 'python2')
provides=('tint')
source=("http://tint2.googlecode.com/files/tint2-$pkgver.tar.bz2"
        'add-power-now-support.patch'
        'clock.patch'
        'launcher_apps_dir-v2.patch'
        'src-task-align.patch'
        'zombie-fix.patch')
md5sums=('6fc5731e7425125fa84a2add5cef4bff'
         '448beead6c9d44b864f99e08a027cb56'
         'bc0bab2979dacff551a97bdf2c2fdedc'
         '13218765dd684ae825967d3ffb4f4a75'
         'f0d7f51ec8dbf2e7b6bcca942f0fd6c0'
         'cdb83cd911e005a3529e5d1cd952a956')

prepare() {
  cd "$pkgname-$pkgver"

  # Applying all patches, even some that only applies partially.
  # Tint2 works fine with these, but a new release from upstream would be nice.
  for f in ../*.patch; do
    echo -e "--- ${f##*/} ---\n"
    patch -Np1 -i $f && echo "${f##*/} OK!" || echo "${f##*/} FAILED!"
    echo
  done

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' src/tint2conf/tintwizard.py
  sed -i 's_python _python2 _' src/tint2conf/main.c
}

build() {
  cd "$pkgname-$pkgver"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_TINT2CONF=1
  make 
}

package() {
  make -C "$pkgname-$pkgver/build" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
