# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=tinygo
pkgver=0.13.1
pkgrel=1
pkgdesc='Go compiler for small places. Microcontrollers, WebAssembly, and command-line tools. Based on LLVM.'
arch=(x86_64)
url='https://github.com/tinygo-org/tinygo'
license=(BSD)
options=(!strip)
depends=(llvm-libs clang lld)
makedepends=(go llvm git)
# use git as we need to fetch submodules
source=(git+https://github.com/tinygo-org/tinygo#tag=v$pkgver
        git+https://github.com/NordicSemiconductor/nrfx
        git+https://github.com/ARM-software/CMSIS
        git+https://github.com/avr-rust/avr-mcu
        git+https://github.com/tinygo-org/cmsis-svd
        git+https://github.com/llvm-mirror/compiler-rt
        git+https://github.com/CraneStation/wasi-libc
        disable_static_llvm.patch)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '02c67add4b961490c887c14ba6a6210ed6b180cf81d713e78bd4fe3eb1a35f7a')

prepare() {
  cd tinygo

  git submodule init
  git config -f .gitmodules 'submodule.lib/nrfx.url' "$srcdir/nrfx"
  git config -f .gitmodules 'submodule.lib/CMSIS.url' "$srcdir/CMSIS"
  git config -f .gitmodules 'submodule.lib/avr.url' "$srcdir/avr-mcu"
  git config -f .gitmodules 'submodule.lib/cmsis-svd.url' "$srcdir/cmsis-svd"
  git config -f .gitmodules 'submodule.lib/compiler-rt.url' "$srcdir/compiler-rt"
  git config -f .gitmodules 'submodule.lib/wasi-libc.url' "$srcdir/wasi-libc"
  git submodule sync
  git submodule update

  patch -p1 < ../disable_static_llvm.patch
}

build() {
  cd tinygo
  make
}

check() {
  cd tinygo
  # it reqreires a lot of dependencies. todo enable the tests
  # make test tinygo-test smoketest
}

package() {
  cd tinygo
  make release

  install -d "$pkgdir"/usr/bin "$pkgdir"/usr/lib/tinygo
  mv build/release/tinygo/* "$pkgdir"/usr/lib/tinygo
  ln -s /usr/lib/tinygo/bin/tinygo "$pkgdir"/usr/bin/tinygo
}
