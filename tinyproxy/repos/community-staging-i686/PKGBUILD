# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=tinyproxy
pkgver=1.8.4
pkgrel=3
pkgdesc='A light-weight HTTP proxy daemon for POSIX operating systems.'
arch=('i686' 'x86_64')
url='https://banu.com/tinyproxy/'
license=('GPL')
depends=(glibc)
makedepends=('asciidoc')
install="${pkgname}.install"
backup=('etc/tinyproxy/tinyproxy.conf')
validpgpkeys=('38871449E065D635620A621EC94F4938184F9034') # Michael Adam
source=("https://www.samba.org/~obnox/${pkgname}/release/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        'tinyproxy.tmpfiles.conf'
        'tinyproxy.service'
        config.patch)
md5sums=('7d3378ad9681be3423c491775f7d9e35'
         'SKIP'
         'b747d0f253ba7bb3f604e69a35a278bf'
         'e1e9212461cec588df219c35aba78d04'
         '9739e020c40abefd6e921e9cff854f35')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../config.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc/tinyproxy --sbindir=/usr/bin \
     --localstatedir=/var --enable-transparent
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 "${srcdir}/tinyproxy.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/tinyproxy.conf"

  install -Dm0644 "${srcdir}/tinyproxy.service" \
    "${pkgdir}/usr/lib/systemd/system/tinyproxy.service"
}
