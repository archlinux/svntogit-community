# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=tinyproxy
pkgver=1.10.0
pkgrel=2
pkgdesc='A light-weight HTTP proxy daemon for POSIX operating systems.'
arch=('x86_64')
url='https://banu.com/tinyproxy/'
license=('GPL')
depends=(glibc)
makedepends=('asciidoc')
backup=('etc/tinyproxy/tinyproxy.conf')
validpgpkeys=('38871449E065D635620A621EC94F4938184F9034') # Michael Adam
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        'tinyproxy.sysusers.conf'
        'tinyproxy.tmpfiles.conf'
        'tinyproxy.service'
        config.patch)
md5sums=('3a1321e4942abb9b6cb370e7c2a3c59e'
         'SKIP'
         'ca7855ca22a2676061ef8b246c8bf6f4'
         'b747d0f253ba7bb3f604e69a35a278bf'
         'e1e9212461cec588df219c35aba78d04'
         '9739e020c40abefd6e921e9cff854f35')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../config.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin \
     --localstatedir=/var --enable-transparent
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 "${srcdir}/tinyproxy.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/tinyproxy.conf"
  install -Dm0644 "${srcdir}/tinyproxy.sysusers.conf" "${pkgdir}/usr/lib/sysusers.d/tinyproxy.conf"

  install -Dm0644 "${srcdir}/tinyproxy.service" \
    "${pkgdir}/usr/lib/systemd/system/tinyproxy.service"
}
