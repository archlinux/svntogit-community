# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
pkgname=tinyssh
pkgver=20160812
pkgrel=3
pkgdesc='Small SSH server using NaCl / TweetNaCl'
url='https://tinyssh.org'
arch=('x86_64' 'i686')
license=('custom:Public Domain')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://mojzis.com/software/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	tinyssh@.socket tinyssh@.service tinysshgenkeys.service
        "${pkgname}-${pkgver}.tar.gz.asc::https://mojzis.com/software/${pkgname}/${pkgname}-${pkgver}.tar.gz.asc")
sha512sums=('54817e7e0da778eb4e404e165caff321ef3c8b13f1e1840c9fad1e8f6f64fee086019f4222199bf2b47e19754913400bb719443a944c612e987e3d3fdae14147'
            '9fbc5ae3b6d1df6d78eedf812fdd8fe115e81fc0811f4799d541e37239d53db22203e691026ed479a38c9f17043e52d43b44054be51ae03c2f12f3f235c5b83a'
            '9c9ba78c5b61f64f42f59cfe556519d9ab2c3e4d45f36d6f78c5e7728b050c494673239630dae66398006f3f20fb3a26f22899cd0728ed17e61c655b372236b3'
            '415cdfe649eedcbb11a263938c0a66bef42432210baa2e2c9e428d5e8e486fb4dedad53b9fdb9086bf97ca506909d3b3054aaca56f02c063e0b8d5fab4e56198'
            'SKIP')
validpgpkeys=('AADF2EDF5529F1702772C8A2DEC4D246931EF49B')

build () {
	cd "${srcdir}/${pkgname}-${pkgver//./}"
	make
}

package () {
	cd "${srcdir}/${pkgname}-${pkgver//./}"
	make install DESTDIR="${pkgdir}"

	# Nowadays /usr/sbin is a symlink to /usr/bin
	mv "${pkgdir}"/usr/{s,}bin

        install -m 755 -d "${pkgdir}/etc/tinyssh"

        install -m 755 -d "${pkgdir}/usr/lib/systemd/system"
	install -m 644 -t "${pkgdir}/usr/lib/systemd/system" \
		"${srcdir}/tinysshgenkeys.service" \
		"${srcdir}/tinyssh@.service" \
		"${srcdir}/tinyssh@.socket"

        install -Dm644 "$srcdir/$pkgname-$pkgver/LICENCE" "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
}
