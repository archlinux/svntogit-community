# $Id: PKGBUILD 148562 2015-12-05 17:09:20Z foutrelis $
# Maintainer: Alexander F RÃ¸dseth <rodseth@gmail.com>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=3.0.0
pkgrel=1
pkgdesc='Simple, small, C++ XML parser that can be easily integrated into other programs'
url='http://www.grinninglizard.com/tinyxml2/'
arch=('x86_64' 'i686')
license=('zlib')
depends=('gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git://github.com/leethomason/tinyxml2#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  cd "$pkgname"
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 readme.md "$pkgdir/usr/share/licenses/$pkgname/README.md"
}

# vim:set ts=2 sw=2 et:
