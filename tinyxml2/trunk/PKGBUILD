# $Id: PKGBUILD 148562 2015-12-05 17:09:20Z foutrelis $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=5.0.1
pkgrel=1
pkgdesc='Simple XML parser made for easy integration'
url='http://www.grinninglizard.com/tinyxml2/'
arch=('x86_64')
license=('zlib')
depends=('gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/leethomason/tinyxml2#commit=37bc3aca429f0164adf68c23444540b4a24b5778")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 readme.md "$pkgdir/usr/share/licenses/$pkgname/README.md"
}

# vim:set ts=2 sw=2 et:
