# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: toliman <altergan@hs-albsig.de>

pkgname=tipp10
pkgver=3.3.0
pkgrel=1
pkgdesc='Intelligent touch typing tutor'
arch=('x86_64')
url=https://gitlab.com/tipp10/tipp10
license=('GPL2')
depends=('qt6-multimedia')
makedepends=('cmake' 'qt6-tools')
source=("$url/-/archive/v$pkgver/tipp10-v$pkgver.tar.gz")
b2sums=('45f6a07f6ec0f7899a91de046e6fcc4fda5b70647e72f6914de135c43acae0acaa314523da676e9ddaff57b6695ad606df7b40210720498f3d416b6ffef06031')

build() {
  cd tipp10-v$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd tipp10-v$pkgver
  make DESTDIR="$pkgdir" install
}
