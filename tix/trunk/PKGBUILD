# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Brice MÃ©alier <mealier_brice@yahoo.fr>

pkgname=tix
pkgver=8.4.3
pkgrel=4
pkgdesc="Tk Interface eXtension, a powerful set of user interface components"
arch=(i686 'x86_64')
url="http://tix.sourceforge.net/"
license=("BSD")
depends=('tk' 'libx11')
source=(http://downloads.sourceforge.net/tix/Tix$pkgver-src.tar.gz
	tcl-tk-path.patch)
md5sums=('2b8bf4b10a852264678182652f477e59'
         'd4df48da39dd51872d58706a51bab505')

build() {
  cd $srcdir/Tix$pkgver

  patch -p1 <$srcdir/tcl-tk-path.patch

  export CFLAGS="$CFLAGS -DERR_IN_PROGRESS=2"

  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/Tix$pkgver

  make DESTDIR=$pkgdir install
  # move things around

  install -m755 tools/tixindex $pkgdir/usr/bin/tixindex
  rm -rf $pkgdir/usr/lib/Tix8.4/html
  rm -f $pkgdir/usr/lib/Tix8.4/{README.txt,license.terms}
  ln -s Tix$pkgver/libTix$pkgver.so $pkgdir/usr/lib/libTix$pkgver.so

  mkdir -p $pkgdir/usr/share/man/man1
  mkdir -p $pkgdir/usr/share/man/man3
  cp -p man/tixwish.1 $pkgdir/usr/share/man/man1
  cd $srcdir/Tix$pkgver/man
  for i in *.n; do
	  cp -p $i $pkgdir/usr/share/man/man3/${i%n}3
  done

  mkdir -p $pkgdir/usr/share/licenses/tix
  cp $srcdir/Tix$pkgver/license.terms  $pkgdir/usr/share/licenses/tix/
}

