# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=0.5
pkgrel=2
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-colorama' 'python-six' 'python-termcolor')
makedepends=('python-setuptools-markdown' 'python-setuptools-scm')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tldr-pages/tldr-python-client/archive/$pkgver.tar.gz")
sha512sums=('fe8a1764101a6228bcda01b935701aa475ea91626db4479cc796e61ef7743ecc559403a0b94a049285e1a2aed14a6eed74fe63dae9c789ba03cf17632b06edb9')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python setup.py pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
