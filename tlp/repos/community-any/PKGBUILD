# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Marc Schulte <bomba@nerdstube.de>

pkgbase=tlp
pkgname=('tlp' 'tlp-rdw')
pkgver=0.4.1
pkgrel=1
arch=('any')
url='http://linrunner.de/en/tlp/docs/tlp-linux-advanced-power-management.html'
license=('GPL2')
source=("https://github.com/linrunner/TLP/archive/${pkgver}.tar.gz"
        'tlp-arch.patch')
sha256sums=('21cff01ef53323086da9543bb6313988b25fcc1e788668f7f19feb0441082e93'
            '5787c1ed446d9292da13c39ff07bf921f9e81d98aeffcbaa742b1b7c0a54c279')

prepare() {
  cd TLP-${pkgver}

  patch -Np1 -i ../tlp-arch.patch
}

package_tlp() {
pkgdesc='Linux Advanced Power Management'
depends=('bash' 'hdparm' 'perl' 'rfkill' 'wireless_tools')
optdepends=('acpi_call: Sandy Bridge and newer ThinkPad battery functions'
            'acpid: ThinkPad wireless radio switch'
            'ethtool: Disable Wake On Lan'
            'iw: Wifi power save'
            'lsb-release: Display LSB release version in tlp-stat'
            'smartmontools: Display S.M.A.R.T. data in tlp-stat'
            'tp_smapi: ThinkPad battery functions')
conflicts=('laptop-mode-tools' 'pm-utils')
backup=('etc/default/tlp')

  cd TLP-${pkgver}

  make DESTDIR="${pkgdir}" install-tlp

# Systemd
  install -dm 755 "${pkgdir}"/usr/lib/systemd/system
  install -m 644 tlp.service "${pkgdir}"/usr/lib/systemd/system/
  install -m 644 tlp-sleep.service "${pkgdir}"/usr/lib/systemd/system/

# Manpages
  install -dm 755 "${pkgdir}"/usr/share/man/man{1,8}
  install -m 644 man/*.1 "${pkgdir}"/usr/share/man/man1/
  install -m 644 man/*.8 "${pkgdir}"/usr/share/man/man8/
}

package_tlp-rdw() {
pkgdesc='Linux Advanced Power Management - Radio Device Wizard'
depends=('networkmanager' 'tlp')

  cd TLP-${pkgver}

  make DESTDIR="${pkgdir}" install-rdw
}

# vim: ts=2 sw=2 et:
