# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Marc Schulte <bomba@nerdstube.de>

pkgbase=tlp
pkgname=('tlp' 'tlp-rdw')
pkgver=0.7
pkgrel=3
arch=('any')
url='http://linrunner.de/en/tlp/tlp.html'
license=('GPL2' 'GPL3')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/linrunner/TLP/archive/${pkgver}.tar.gz"
        'tlp-arch.patch'
        'fix-udev-event-processing-for-AC-BAT.patch'
        'fix-udev-event-processing-for-USB.patch')
sha256sums=('bd313f478f897a658f2ca7db9665b8ce6796431b952f7df9aa80e59072cea4ea'
            '833ed3348a46afee9c20119452c98d07caf050d955961beb4fdef76ff4e4a59a'
            '546057c61812d2e788b101c055d87137deb5fcec087e4e3c9a0c3a5ac615b556'
            '5ed8c2b391c2bc2a06f6d7a615fc24c3e4a6c4060fdee53221ff72eddfc843ae')

prepare() {
  cd TLP-${pkgver}

  patch -Np1 -i ../fix-udev-event-processing-for-AC-BAT.patch
  patch -Np1 -i ../fix-udev-event-processing-for-USB.patch
  patch -Np1 -i ../tlp-arch.patch
}

package_tlp() {
  pkgdesc='Linux Advanced Power Management'
  depends=('hdparm' 'iw' 'pciutils' 'rfkill' 'usbutils' 'util-linux')
  optdepends=('acpi_call: Sandy Bridge and newer ThinkPad battery functions'
              'ethtool: Disable Wake On Lan'
              'lsb-release: Display LSB release version in tlp-stat'
              'smartmontools: Display S.M.A.R.T. data in tlp-stat'
              'tp_smapi: ThinkPad battery functions'
              'x86_energy_perf_policy: Set energy versus performance policy on x86 processors')
  conflicts=('laptop-mode-tools' 'pm-utils')
  backup=('etc/default/tlp')

  cd TLP-${pkgver}

  export TLP_SBIN='/usr/bin'
  export TLP_ULIB='/usr/lib/udev'
  export TLP_SYSD='/usr/lib/systemd/system'
  export TLP_NO_INIT='1'
  export TLP_NO_PMUTILS='1'
  export TLP_WITH_SYSTEMD='1'

  make DESTDIR="${pkgdir}" install-tlp

  install -dm 755 "${pkgdir}"/usr/share/man/man{1,8}
  install -m 644 man/*.1 "${pkgdir}"/usr/share/man/man1/
  install -m 644 man/*.8 "${pkgdir}"/usr/share/man/man8/
}

package_tlp-rdw() {
  pkgdesc='Linux Advanced Power Management - Radio Device Wizard'
  depends=('networkmanager' 'tlp')

  cd TLP-${pkgver}

  make DESTDIR="${pkgdir}" install-rdw
}

# vim: ts=2 sw=2 et:
