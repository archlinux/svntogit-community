# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=tlsh
pkgname=('tlsh' 'python-tlsh' 'python2-tlsh')
pkgver=3.4.5
pkgrel=4
pkgdesc='Fuzzy matching library that generates a hash value which can be used for similarity comparisons'
url='https://github.com/trendmicro/tlsh'
arch=('x86_64')
license=('Apache')
makedepends=('cmake' 'python' 'python2' 'gcc-libs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/trendmicro/${pkgname}/archive/v${pkgver}.tar.gz
        dont-use-rpath.patch
        install-libraries.patch)
sha512sums=('039f9fb9fb467a9b22f85a621c46069bac1745da039657840e1ff1a53901392fcf29d3c953b0f47d831d022a5e2594925c726d4ff921614418a3bb023a4ebbdc'
            '6b7d32c24151c6513918875011339990bdf4638a4f03c166b5144efe10a4bb5bf1da0401951f78a75f7e197c81a03b4ae540e22cd3ce9f2f2c99f0436780d510'
            '448dc550df26c756b8fef7b774d193336d63f7fa2f01683308ddfd042edbe60677adad23c95e27b3d98ce7d6c734110cf218335bbc8bd0ce57dccf3eaa63a065')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 < "${srcdir}/install-libraries.patch"
  patch -p1 < "${srcdir}/dont-use-rpath.patch"
  cp -ra py_ext{,-py2}
  cp Testing/python_test.sh Testing/python2_test.sh
  sed 's|py_ext|py_ext-py2|g' -i Testing/python2_test.sh
}

build() {
  cd ${pkgbase}-${pkgver}
  mkdir -p build
  (cd build
    cmake .. \
      -DCMAKE_BUILD_TYPE=RELEASE \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr \
      -DCMAKE_INSTALL_LIBDIR:PATH=/usr/lib
    make
  )
  (cd bin
    cmake -E create_symlink tlsh_unittest tlsh
  )
  (cd py_ext
    python setup.py build
  )
  (cd py_ext-py2
    python2 setup.py build
  )
}

check() {
  cd ${pkgbase}-${pkgver}
  (cd build
    make test
  )
  Testing/python_test.sh
  Testing/python2_test.sh python2
}

package_tlsh() {
  depends=('gcc-libs')
  cd ${pkgbase}-${pkgver}
  install -Dm 755 bin/tlsh_unittest "${pkgdir}/usr/bin/tlsh_unittest"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  (cd build
    make DESTDIR="${pkgdir}" install
  )
}

package_python-tlsh() {
  depends=('python')
  cd ${pkgbase}-${pkgver}/py_ext
  python setup.py install -O1 --root="${pkgdir}" --skip-build
}

package_python2-tlsh() {
  depends=('python2')
  cd ${pkgbase}-${pkgver}/py_ext-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
}

# vim: ts=2 sw=2 et:
