# Maintainer: David Runge <dave@sleepmap.de>
pkgname=tmuxp
pkgver=1.4.0
pkgrel=4
pkgdesc="tmux session manager. built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('BSD')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux>=0.8.0' 'tmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tony/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('07086126ebdae6f6bfeac9e9e28f6a93d815273180a15016e6134d817cbff4da5ee3487410f27c89c33bdfa731f7ed4fda26cee7e78d0212a1eab0f660223f0b')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

## test_workspacebuilder.py does not return:
## https://github.com/tmux-python/tmuxp/issues/377
#check() {
#  cd "${pkgname}-${pkgver}"
#  python setup.py test
#}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES,README.rst}
}

# vim:set ts=2 sw=2 et:}
