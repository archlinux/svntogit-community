# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Baptiste Daroussin <baptiste.daroussin@gmail.com>

pkgname=tnftp
pkgver=20200705
pkgrel=1
pkgdesc='NetBSD FTP client with several advanced features'
url='ftp://ftp.netbsd.org/pub/NetBSD/misc/tnftp/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ncurses' 'libncursesw.so' 'dante' 'openssl')
source=(ftp://ftp.netbsd.org/pub/NetBSD/misc/tnftp/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('ba4a92b693d04179664524eef0801c8eed4447941c9855f377f98f119f221c03'
            'SKIP')
b2sums=('2d0909455725b43e8627b4fc9ce10e7e4e1ffc175d73ede304f071cea071f88075f156a0c7a2d5b1bc1b3f986e6502c35acd3b956787e6766a8ca765e3567af0'
        'SKIP')
validpgpkeys=(
  '2A8E22EDB07B5414548D8507A4186D9A7F332472'  # Luke Mewburn <lukem@NetBSD.org>
)

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --with-socks \
    --enable-editcomplete \
    --enable-ssl \
    --enable-ipv6
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  install -Dm 755 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog INSTALL NEWS README THANKS todo -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
