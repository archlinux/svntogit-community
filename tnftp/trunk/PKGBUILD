# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Baptiste Daroussin <baptiste.daroussin@gmail.com>

pkgname=tnftp
pkgver=20210827
pkgrel=1
pkgdesc='NetBSD FTP client with several advanced features'
url='https://ftp.netbsd.org/pub/NetBSD/misc/tnftp/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ncurses' 'libncursesw.so' 'dante' 'openssl')
source=(https://ftp.netbsd.org/pub/NetBSD/misc/tnftp/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('101901e90b656c223ec8106370dd0d783fb63d26aa6f0b2a75f40e86a9f06ea2'
            'SKIP')
b2sums=('fe1d4f9f972f349c18e98dbc40305c867511339cb661936fbd255c4a76d9df61f1eae7e723479ef67a96dd05886a079daa4b7367d0c8768d33bc08faa743e5e8'
        'SKIP')
validpgpkeys=(
  '2A8E22EDB07B5414548D8507A4186D9A7F332472'  # Luke Mewburn <lukem@NetBSD.org>
)

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --with-socks \
    --enable-editcomplete \
    --enable-ssl \
    --enable-ipv6
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  install -Dm 755 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ChangeLog INSTALL NEWS README THANKS todo -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
