# Maintainer: David Runge <dave@sleepmap.de>
pkgname=todoman
pkgver=3.7.0
pkgrel=2
pkgdesc="A simple, standards-based, cli todo (aka: task) manager"
arch=('any')
url="https://github.com/pimutils/todoman"
license=('ISC')
depends=('python-atomicwrites' 'python-click-log' 'python-configobj'
'python-dateutil' 'python-humanize' 'python-icalendar' 'python-parsedatetime'
'python-xdg' 'python-setuptools' 'python-tabulate' 'python-urwid')
checkdepends=('python-freezegun' 'python-hypothesis' 'python-pytest-runner')
optdepends=('bash-completion: auto-complete in bash'
            'python-click-repl: REPL command'
            'zsh-completions: auto-complete in zsh')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('61b17d1d56ea29e0e6d09f57e4a3e2be97894c187079cece97d56b6e1e1cbfddcc0066b8e72dd0b9c7e6870dd2b137ec2b41716d4c8848c6274c0685f8b17144')

prepare() {
  cd "$pkgname-$pkgver"
  # remove interfering options to pytest
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  # ignoring broken test: https://github.com/pimutils/todoman/issues/365
  py.test -vv -k 'not test_default_due2'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -vDm 644 {AUTHORS,CHANGELOG,CODE_OF_CONDUCT,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # license
  # wrong name: https://github.com/pimutils/todoman/issues/319
  install -vDm 644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # sample config
  install -vDm 644 "${pkgname}.conf.sample" \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
  # completions
  install -vDm 644 contrib/completion/bash/_todo \
    "${pkgdir}/usr/share/bash-completion/completions/todo"
  install -vDm 644 contrib/completion/zsh/_todo \
    "${pkgdir}/usr/share/zsh/site-functions/_todo"
}
