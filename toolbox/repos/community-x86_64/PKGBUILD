# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=toolbox
pkgver=0.0.91
pkgrel=1
pkgdesc='Unprivileged development environment'
arch=(x86_64)
url='https://github.com/containers/toolbox'
license=(APACHE)
depends=(podman bash)
makedepends=(go shellcheck go-md2man bash-completion ninja git meson)
_commit=387872c6a35dab29f3a58d6b63b8b39ff163c5f9 # tags/0.0.91
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd toolbox
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  arch-meson toolbox build -D profile_dir=/etc/profile.d
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
