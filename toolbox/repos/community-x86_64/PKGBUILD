# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=toolbox
pkgver=0.0.95
pkgrel=2
pkgdesc='Unprivileged development environment'
arch=(x86_64)
url='https://github.com/containers/toolbox'
license=(APACHE)
depends=(podman bash)
makedepends=(go shellcheck go-md2man bash-completion ninja git meson)
_commit=b3f0a50652ededa49fdcb73ca7a343beed9538bb    #refs/tags/0.0.95^{}
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd toolbox
  git cherry-pick -n 9840ca55a126b332f45f3fc9fbb22552e1fe2d3f
}

pkgver() {
  cd toolbox
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  arch-meson toolbox build -D profile_dir=/etc/profile.d
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
