# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=topcom
pkgver=0.17.10
pkgrel=1
pkgdesc='A package for computing Triangulations Of Point Configurations and Oriented Matroids'
arch=(x86_64)
url='https://www.wm.uni-bayreuth.de/de/team/rambau_joerg/TOPCOM/index.html'
license=(GPL2)
depends=(cddlib)
makedepends=()
source=(https://www.wm.uni-bayreuth.de/de/team/rambau_joerg/TOPCOM-Downloads/TOPCOM-${pkgver//./_}.tgz
        Makefile.patch)
sha256sums=('3ea0fbb08b58438c6b296b829965868c49d6e93688446117e9a0ed2e385dbcf6'
            '764fe33381d8a6bbf01bb23849dd0cac66b3bafbf29a81240b3951a00e5df3f0')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../Makefile.patch # Fix build system
  autoreconf -vif
}

build() {
  cd $pkgname-$pkgver
  CXXFLAGS+=" -I/usr/include/cddlib" \
  LDFLAGS+=" -lgmpxx -lgmp -lcdd" \
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
