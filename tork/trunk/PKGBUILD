# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>

_libname=tor
pkgname=${_libname}k
pkgver=0.33
pkgrel=5
pkgdesc="An anonymity manager for KDE"
arch=("i686" "x86_64")
url="http://sourceforge.net/projects/$pkgname/"
license=("GPL")
depends=("geoip" "kdelibs3" "${_libname}" "torsocks" "hicolor-icon-theme" "desktop-file-utils" "xdg-utils")
optdepends=("privoxy: For anonymous browsing with Konqueror")
options=("!emptydirs")
install=${pkgname}.install

source=(
  http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
  fix_net_port.patch
  fix_getuid.patch
)
sha1sums=(
  'cf94b9579cf24254fcb344f579088a241bbd4028'
  '70c450031bf5e603801451fc016e5cfc22cfd19c'
  '9fe1de823850e6a0d0a4a18c3b86955206f544bd'
)

build() {
  cd ${pkgname}-${pkgver}
  
  # Source the KDE and QT profiles
  source /etc/profile.d/kde3.sh
  #source /etc/profile.d/qt3.sh

  msg "Applying patch(es) ..."
  
  patch -Np1 -i ../fix_net_port.patch
  patch -Np1 -i ../fix_getuid.patch

  # Build
  msg "Compiling tork ..."
  ./configure --prefix=/opt/kde --mandir=/usr/share/man \
  --sysconfdir=/etc --with-external-geoip --without-arts

  # Fix --as-needed and --hash-style=gnu options not recognized error
  find . -name Makefile -exec sed -i "s/-Wl,//g" {} \;
  find . -name Makefile -exec sed -i "s/-Wl//g" {} \;
  find . -name Makefile -exec sed -i "s/--hash-style=gnu,//g" {} \;
  find . -name Makefile -exec sed -i "s/--hash-style=gnu//g" {} \;
  find . -name Makefile -exec sed -i "s/--as-needed,//g" {} \;
  find . -name Makefile -exec sed -i "s/--as-needed//g" {} \;

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
