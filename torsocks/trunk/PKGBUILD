# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Nicolas Pouillard <nicolas.pouillard@gmail.com>
# Contributor: Rorschach <r0rschach@lavabit.com>

pkgname=torsocks
pkgver=2.0.0
pkgrel=2
pkgdesc='Wrapper to safely torify applications'
arch=('i686' 'x86_64')
url='https://gitweb.torproject.org/torsocks.git/'
license=('GPL2')
depends=('tor')
makedepends=('git')
backup=("etc/tor/${pkgname}.conf")
source=("git+https://git.torproject.org/torsocks.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd $pkgname
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir/" install
  find "$pkgdir/usr/share" -maxdepth 1 -type f -delete
}
