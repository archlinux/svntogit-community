# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>

pkgname=tpm2-abrmd
pkgver=2.3.1
pkgrel=1
pkgdesc="Trusted Platform Module 2.0 Access Broker and Resource Management Daemon"
arch=(x86_64)
url="https://github.com/tpm2-software/tpm2-abrmd"
license=(BSD)
depends=(dbus glib2 tpm2-tss)
checkdepends=(cmocka ibm-sw-tpm2 iproute2)
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('144ab2b147cd18f892f3c05181efdbed2ac422fe8507eb42871ffe571340b6395431743a4f1995a7ce7a914fd5e7afd2b17328062c893a0403708c2c4d92e0cb'
            'SKIP')
validpgpkeys=(42007E876F248E04A3F2FE25AE4548D043DEC7C3) # Philip Tricca <philip.b.tricca@intel.com>

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-dbuspolicydir=/usr/share/dbus-1/system.d \
        $( ((CHECKFUNC)) && echo --enable-unit --enable-integration)
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    rm -r "${pkgdir}"/usr/lib/systemd/system-preset
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
