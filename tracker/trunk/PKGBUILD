# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=tracker
pkgver=0.10.19
pkgrel=2
pkgdesc="Powerful object database, tag/metadata database, search tool and indexer"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/tracker/"
license=('GPL')
depends=('upower' 'poppler-glib' 'gstreamer0.10-base' 'libgee'  'gnome-desktop' 'libnotify' 'xdg-utils' 'gtkhtml'
	 'libgsf' 'gnome-panel-bonobo' 'exempi' 'networkmanager')
makedepends=('intltool' 'gnome-panel' 'evolution' 'totem-plparser' 'icu')
# 'nautilus'
optdepends=('nautilus'
	    'enca'
	    'gtkhtml'
	    'gnome-panel'
	    'totem-plparser'
	    'icu'
	    'taglib'
	    'giflib')
options=('!libtool')
conflicts=('libtracker')
provides=("libtracker")
install=tracker.install
source=(http://ftp.gnome.org/pub/GNOME/sources/tracker/0.10/tracker-$pkgver.tar.bz2)
md5sums=('59f105fbe7248749aee7ae8d9432b6a9')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  export PYTHON=`which python2`
  for file in tests/functional-tests/*.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done

  ./configure --prefix=/usr --sysconfdir=/etc  --libexecdir=/usr/lib/$pkgname \
    --enable-video-extractor=gstreamer --disable-hal \
    --disable-miner-evolution --disable-nautilus-extension
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
