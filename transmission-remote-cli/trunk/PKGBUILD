# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>
# Contributor: fauno <fauno@kiwwwi.com.ar>
# Contributor: SanskritFritz

pkgname=transmission-remote-cli
pkgver=1.7.0
pkgrel=1
pkgdesc="Curses interface for the daemon of the BitTorrent client Transmission"
arch=('any')
url='https://github.com/fagga/transmission-remote-cli'
license=('GPL3')
depends=(python2 transmission-cli)
optdepends=('python2-geoip: Guess which country peers come from'
            'adns-python: Resolve IPs to host names')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fagga/transmission-remote-cli/tarball/v$pkgver")
sha256sums=('9db44b5045c1bca04f0304885e2216629a0562ee6f2d86ff74284b517311bd10')

build() {
  cd "$(ls -1td fagga-transmission-remote-cli-* | head -n 1)"
  sed -i "1s/python/python2/" transmission-remote-cli
}

package() {
  cd "$(ls -1td fagga-transmission-remote-cli-* | head -n 1)"

  install -Dm755 transmission-remote-cli "$pkgdir/usr/bin/transmission-remote-cli"
  install -Dm644 transmission-remote-cli.1 "$pkgdir/usr/share/man/man1/transmission-remote-cli.1"
  install -Dm755 completion/bash/transmission-remote-cli-bash-completion.sh \
    "$pkgdir/usr/share/bash-completion/completions/transmission-remote-cli"
}
