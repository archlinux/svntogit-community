# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: SÃ©bastien LEBEAU <sebcbi1 at gmail dot com>
# Contributor: Patrick Griffis <tingpign@fedoraproject.org>

pkgname=transmission-remote-gtk
pkgver=1.4.2
pkgrel=1
pkgdesc="GTK remote control for the Transmission BitTorrent client"
arch=(x86_64)
url="https://github.com/transmission-remote-gtk/transmission-remote-gtk"
license=(GPL2)
depends=(curl gtk3 libnotify libproxy geoip)
makedepends=(intltool autoconf-archive appstream-glib)
# The new maintainer does not sign releases
#source=(${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc})
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('e618e8f5fbca1cf42e9389428512f9d1ff161f6d26d004d35b9bf1a0278eae09')
#validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis <tingping@tingping.se>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-desktop-database-update
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
  make distcheck
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
