# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Renato Garcia <fgarcia.renato@gmail.com>

pkgname=trash-cli
pkgver=0.17.1.14
pkgrel=4
pkgdesc="Command line trashcan (recycle bin) interface"
arch=('any')
url="https://github.com/andreafrancia/${pkgname}"
license=('GPL')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "0001-Use-py-bytes-to-interact-with-ctypes.patch::${url}/pull/115.patch")
sha256sums=('8fdd20e5e9c55ea4e24677e602a06a94a93f1155f9970c55b25dede5e037b974'
            '71c674d0c36fad172fd39c75052ac0de1a3f4739e13cffb8ed78148f11eda0a3')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    # https://github.com/andreafrancia/trash-cli/issues/95
    patch -p1 -i ../0001-Use-py-bytes-to-interact-with-ctypes.patch
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py build
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
