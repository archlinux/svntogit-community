# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=treefrog-framework
pkgver=1.17.0
pkgrel=1
pkgdesc="High-speed C++ MVC Framework for Web Application"
arch=('x86_64')
url="https://github.com/treefrogframework/treefrog-framework"
license=('BSD')
depends=('qt5-declarative' 'libmongoc')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/treefrogframework/treefrog-framework/archive/v$pkgver.tar.gz")
sha512sums=('46c4de7468ba3ca9176b0bf1e1dcec4a4e6cd76d7a15ebd64b61e9a5fa734dccdaef1c85c74a135eefa8739b0f108b4cfcffbc3fd21ad6d5209d49f4997fcec7')

prepare() {
  cd $pkgname-$pkgver
  rm -r 3rdparty

  sed -i 's|LIBS += ../3rdparty/mongo-c-driver/libmongoc.a|LIBS += -lmongoc-1.0\nINCLUDEPATH += /usr/include/libbson-1.0 /usr/include/libmongoc-1.0|' src/corelib.pro
  sed -i 's|exit $RET||' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  
  cd src
  make
}

package() {
  cd $pkgname-$pkgver/src
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 ../copyright "$pkgdir"/usr/share/licenses/$pkgname/copyright
}
