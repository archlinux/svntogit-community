# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=treefrog-framework
pkgver=1.19.0
pkgrel=1
pkgdesc="High-speed C++ MVC Framework for Web Application"
arch=('x86_64')
url="https://github.com/treefrogframework/treefrog-framework"
license=('BSD')
depends=('qt5-declarative' 'libmongoc')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/treefrogframework/treefrog-framework/archive/v$pkgver.tar.gz")
sha512sums=('b59f7571fcd0693675381bb45c154164858960fed56c6b0204d1b738c179d3881e0b747e0f8258bf60e1bf608d0ce5376dc4c803e8864421dd52124cbfbe496f')

prepare() {
  cd $pkgname-$pkgver
  rm -r 3rdparty

  sed -i 's|LIBS += ../3rdparty/mongo-c-driver/libmongoc.a|LIBS += -lmongoc-1.0\nINCLUDEPATH += /usr/include/libbson-1.0 /usr/include/libmongoc-1.0|' src/corelib.pro
  sed -i 's|exit $RET||' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  
  cd src
  make
}

package() {
  cd $pkgname-$pkgver/src
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 ../copyright "$pkgdir"/usr/share/licenses/$pkgname/copyright
}
