# $Id$
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer:  Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=tremulous
pkgver=1.1.0
pkgrel=7
pkgdesc="A completely free online team based FPS/RTS hybrid game based on the ioquake3 engine."
url="http://tremulous.net/"
license=("GPL")
arch=('i686' 'x86_64')
depends=("sdl" "openal>=1.7.411" "tremulous-data=$pkgver")
makedepends=(subversion mesa)
source=('tremulous.launcher' 'tremded.launcher' 'tremulous.desktop' 'tremulous.xpm'
        "backport.patch::http://source.mercenariesguild.net/client/trunk/patches/backport-client-r1112.patch?revision=2"
        "fixfavorites.patch::http://source.mercenariesguild.net/client/trunk/patches/slacker-fixfavorites.patch?revision=15"
        "resolution.patch::http://source.mercenariesguild.net/client/trunk/patches/Lakitu7-backport_resolution_cvars.patch?revision=71"
        "sortping.patch::http://source.mercenariesguild.net/client/trunk/patches/Amanieu_sortping_fix_client.patch?revision=3")
md5sums=('5d2a7f0459affe970fe2b33b7ef18780'
         '8775c2d5b9ec6ce9b9d85ab79ec9879d'
         '9974bde01b95b6d967beb76c31c0e742'
         '7e3a881608f1c7c0ccece1e07fcf92d8'
         '7b3bf98d1ae3ea89da58e3c7775bf452'
         '510384ba8699aceac13aaa15d722ced7'
         '4d39ef28a0924bdc33234a01fb54f846'
         '85315bd6d35519bd231e9771552a8e49')

build() {
  cd $srcdir

  # Check out the revision recommended by the developers
  svn co svn://svn.icculus.org/tremulous/trunk/ --config-dir ./ -r 1114 tremulous-src || return 1

  # Compile Source
  cd tremulous-src

  # Add patches recommended by the developers
  patch -Np0 -i $srcdir/backport.patch     || return 1
  patch -Np0 -i $srcdir/fixfavorites.patch || return 1
  patch -Np0 -i $srcdir/resolution.patch   || return 1
  patch -Np0 -i $srcdir/sortping.patch     || return 1

  make || return 1
}

package() {
  cd $srcdir/tremulous-src

  install -dm755 $pkgdir/opt/tremulous/

  # Install Binaries
  local _arch=${CARCH/i686/x86}
  install -m755 build/release-linux-$_arch/*.$_arch $pkgdir/opt/tremulous

  cd $srcdir

  # Install Client Launcher
  install -D -m755 tremulous.launcher $pkgdir/usr/bin/tremulous

  # Install Server Launcher
  install -D -m755 tremded.launcher   $pkgdir/usr/bin/tremded

  # Install Desktop Icons
  install -D -m644 tremulous.desktop  $pkgdir/usr/share/applications/tremulous.desktop
  install -D -m644 tremulous.xpm    $pkgdir/usr/share/pixmaps/tremulous.xpm
}

# vim:set ts=2 sw=2 et:
