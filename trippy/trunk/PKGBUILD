# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=trippy
pkgver=0.6.0
pkgrel=1
pkgdesc='A network diagnostic tool inspired by mtr'
arch=('x86_64')
url='https://trippy.cli.rs'
license=('Apache')
depends=('gcc-libs' 'libcap')
makedepends=('rust')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://crates.io/api/v1/crates/$pkgname/$pkgver/download")
b2sums=('2ba4275df38e8a1d73b7ba631fdd97e3f3b3a9ec9d8c0e4ec2ac821f0c1ad1928845791db8e2aebcf960e8892c43bba337d181af61ee48bdc5a4d9e39aef8b76')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --locked --offline
}

check() {
  cd $pkgname-$pkgver
  cargo test --locked --offline
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 -t "$pkgdir"/usr/bin target/release/trip
}
