# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Hao Long <imlonghao@archlinuxcn.org>

pkgname=trivy
pkgver=0.29.2
pkgrel=1
pkgdesc='A Simple and Comprehensive Vulnerability Scanner for Containers, Suitable for CI'
arch=('x86_64')
url='https://github.com/aquasecurity/trivy'
license=('Apache')
depends=('device-mapper')
optdepends=('rpm: RHEL/CentOS based image support')
makedepends=('go' 'btrfs-progs')
provides=('trivy')
conflicts=('trivy')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('86f0e380ad5366e6cc4e51702797e773a8517c86048480e8fcdbb63b60213bf51b7c4b9b1a889ede4d49357e3e195e2797724d89ba95c3d55395c3ce774744da')

prepare() {
  cd ${pkgname}-${pkgver}

  find docs -type f -name "*.md" \
    -exec sed "s|\(../\)*imgs/\(.*\.[a-z]\{3\}\)|${url}/raw/main/docs/imgs/\2|g" -i {} \;

  rm -Rf docs/{build,imgs}
}

build() {
  cd ${pkgname}-${pkgver}/cmd/trivy

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-linkmode=external -s -w -X=main.version=${pkgver}" .
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 "cmd/trivy/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/doc"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
}
