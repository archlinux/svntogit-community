# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: birdflesh <antkoul at gmail dot com>

pkgname=trojita
pkgver=0.5
pkgrel=3
pkgdesc="A QT IMAP email client"
arch=('i686' 'x86_64')
url="http://trojita.flaska.net"
license=('GPL')
makedepends=('cmake' 'automoc4')
depends=('qtwebkit' 'qtkeychain-qt4')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('c583bb4d82620cda1c49e5f1b084f550'
         'SKIP')
validpgpkeys=('61AB87D6F66CE2FCD2D2E1F56A65DFA844722517')

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "build"
  make DESTDIR="$pkgdir" install
}
