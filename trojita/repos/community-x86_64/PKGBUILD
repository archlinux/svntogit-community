# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: birdflesh <antkoul at gmail dot com>

pkgname=trojita
pkgver=0.7
pkgrel=1
pkgdesc="A QT IMAP email client"
arch=('i686' 'x86_64')
url="http://trojita.flaska.net"
license=('GPL')
makedepends=('cmake' 'qt5-tools')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'qt5-webkit' 'qtkeychain' 'qt5-svg')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz{,.asc})
md5sums=('b2b5720be888ec6298a88798b113ad5f'
         'SKIP')
validpgpkeys=('61AB87D6F66CE2FCD2D2E1F56A65DFA844722517')

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt5 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "build"
  make DESTDIR="$pkgdir" install
}
