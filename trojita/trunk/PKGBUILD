# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: birdflesh <antkoul at gmail dot com>

pkgname=trojita
pkgver=0.7
pkgrel=2
pkgdesc="A Qt IMAP email client"
arch=(x86_64)
url="https://trojita.flaska.net"
license=(GPL)
makedepends=(cmake qt5-tools pinentry)
depends=(hicolor-icon-theme qt5-webkit qtkeychain qt5-svg qgpgme mimetic)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz{,.asc}
        trojita-gpgme.patch)
sha256sums=('e08da881721cafa00ecd772eaef6b99fd37d068cb703eeb532de0ed1ea0136d9'
            'SKIP'
            '9f99bd2d91cd3b345e2bf52c0d9929ea81147f6578b774c5d2b4f2e86aade2a8')
validpgpkeys=('61AB87D6F66CE2FCD2D2E1F56A65DFA844722517')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../trojita-gpgme.patch # suport gmgpe's gpgme++
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
