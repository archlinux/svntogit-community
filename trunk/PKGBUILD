# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

pkgname=rubberband
pkgver=3.1.1
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=(x86_64)
url="https://www.breakfastquay.com/rubberband/"
license=(GPL2)
depends=(gcc-libs glibc)
makedepends=(fftw java-environment ladspa meson libsamplerate libsndfile
vamp-plugin-sdk)
optdepends=(
  'ladspa-host: for LADSPA plugin'
  'vamp-host: for VAMP plugin'
  'vamp-plugin-sdk: for VAMP plugin'
)
provides=(librubberband.so librubberband-jni.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/breakfastquay/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('aef4de02b6fe250ab43d627d30720dab8aea3587b428ce76fe339d0b1f0e50da6ba7fa7c76f61306704dd2cfc24241f3d4108b6c155c3d12624eac859672f86c')
b2sums=('b84a87151d9d1da6ab0afaee6a05e67f1e91aade2573fb1eaa0cace165dc7ab7e31a1e6f8ab6aba287d2d594dd5e5c220f046d4efdc8923316b6a575f3ea8d39')

build() {
  local java_major=$(java --version 2>/dev/null |grep 'openjdk'| cut -d ' ' -f2| cut -d '.' -f1)
  local meson_options=(
    -Dfft=fftw
    -Dresampler=libsamplerate
    -Dextra_include_dirs="/usr/lib/jvm/java-$java_major-openjdk/include,/usr/lib/jvm/java-$java_major-openjdk/include/linux"
  )
  export JAVA_HOME="/usr/lib/jvm/java-$java_major-openjdk"

  arch-meson build $pkgname-$pkgver "${meson_options[@]}"
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  depends+=(libfftw3.so libsamplerate.so libsndfile.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{CHANGELOG,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
