# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Contributor: Dino Morelli < dino@ui3.info >

pkgname=glulxe-term
pkgver=0.5.4
pkgrel=1
epoch=1
pkgdesc='The Glulx VM reference interpreter'
arch=('x86_64')
url='http://www.eblong.com/zarf/glulx/'
license=('MIT')
depends=('libglkterm')
source=(${pkgname}-$pkgver}.tar.gz::"https://github.com/erkyrath/glulxe/archive/refs/tags/${pkgname%-term}-${pkgver}.tar.gz"
        "${pkgname}-flags.patch")
sha512sums=('d7ad7b5205843a10c66287ab0d170acddf032e092cac8aff8bf5d81c0b50798d195bbf99381730ef2002065b92ac6dd858c5e51c228879c053771d310a77e5af'
            '13e47bdf02f7e24aabfbd0fc9081a8c0fa322a993aaf8ac60ba050be80b235529276cd103f901766a551b42e9748271794facbc7261e4492761a09de3a85e63d')
b2sums=('e14fdbe609b14664d6af133d2960ff7803bbf7d430d42bb2cf77cfe6c1a829a4baf539092f5eb65728934e0344d3d22793674f19f8a5fa11a817360e8562fc5d'
        '9df919f922c6a58dc01afa03e6bfed25d3565bdbea04881cc801feeffc7054536f9d14df6d877a7d3909a4f228fe69810f52a09e6c262b5080170ccd296741a7')

prepare() {
	cd glulxe-glulxe-${pkgver}
	patch -Np1 -i ../${pkgname}-flags.patch
}

build() {
	cd glulxe-glulxe-${pkgver}
	make GLKINCLUDEDIR=/usr/include GLKLIBDIR=/usr/lib GLKMAKEFILE=Make.glkterm
}

package() {
	cd glulxe-glulxe-${pkgver}
	install -vDm755 glulxe "$pkgdir/usr/bin/glulxe-term"
	install -vDm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
