# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=xjadeo
pkgver=0.8.10
pkgrel=2
pkgdesc="A simple video player that is synchronized to jack transport"
arch=('x86_64')
url="http://xjadeo.sourceforge.net"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'imlib2' 'libglvnd' 'libx11' 'libxpm' 'libxv')
makedepends=('alsa-lib' 'ffmpeg' 'gendesk' 'glu' 'jack' 'liblo' 'libltc' 'portmidi')
backup=('etc/xjadeorc')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f50c3daa3abb6a7b7809f12a3d2a2d2682aa93f908366a751d2c6d9da06c07e0e0f5316500d5bcd93baf39bfc7a9d2930c8ec82ed0f225d1254c3387de39b613')
b2sums=('1f32fb7f85838354707110573ce8fd525a1fbbe01d27c5aee9f53a54560efd3b665ec8a8124929e5c2eb2f2e74a29b61d09cb421a7b70ecf938405c0048ae824')

prepare() {
  cd "${pkgname}-${pkgver}"
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --categories "AudioVideo;Audio"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  depends+=('libasound.so' 'libavcodec.so' 'libavformat.so' 'libavutil.so'
  'libfreetype.so' 'libjack.so' 'liblo.so' 'libltc.so' 'libportmidi.so'
  'libswscale.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  # config
  install -vDm 644 doc/xjadeorc -t "${pkgdir}/etc/"
  # desktop file
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  # icon
  install -vDm 644 "src/${pkgname}/icons/xjadeoH128.png" \
    "$pkgdir/usr/share/pixmaps/${pkgname}.png"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
