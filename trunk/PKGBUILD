# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Eric Johnson <eric@coding-zone.com>

pkgname=perl-mail-spf-query
_realname=Mail-SPF-Query
pkgver=1.999.1
pkgrel=13
pkgdesc="Perl module that provides SPF support"
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/${_realname}/"
depends=('perl-net-cidr-lite>=0.15' 'perl-net-dns>=0.46' 'perl-sys-hostname-long' 'perl-uri' 'perl>=5.10.0')
options=(!emptydirs)
source=(https://search.cpan.org/CPAN/authors/id/J/JM/JMEHNLE/mail-spf-query/${_realname}-${pkgver}.tar.gz)
sha256sums=('bc6409845e13f0f24611575f00e1d744a9272b0fced70b6616a4413aace7b59f')

build() {
  cd "$srcdir"/${_realname}-${pkgver}
  # install module in vendor directories
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/${_realname}-${pkgver}
  make install DESTDIR="$pkgdir"
}
