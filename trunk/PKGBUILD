# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pifpaf
pkgver=3.1.5
pkgrel=4
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-fixtures'
         'python-psutil' 'python-xattr')
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
makedepends=('python-setuptools')
checkdepends=('python-testrepository' 'python-requests' 'python-testtools' 'python-oslotest'
              'memcached' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'consul' 'couchdb' 'httpbin' 'rabbitmq')
source=("https://github.com/jd/pifpaf/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('075340303f17aa0067c1bc19ca861255efffb6c64f353f7334fa069dfc4456f067aa487ecfa45bb48291e3d9d949f801cead95f6e833d85086090f6c2fdef349')

export PBR_VERSION=$pkgver

prepare() {
  cd pifpaf-$pkgver
  echo "graft pifpaf/drivers/templates" > MANIFEST.in
}

build() {
  cd pifpaf-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  # TODO: fix test failures
  PYTHONPATH="$PWD/tmp_install/$site_packages" \
  PATH="$PWD/tmp_install/usr/bin:$PATH" \
  python setup.py testr || echo "Tests failed"
}

package() {
  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
