# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cppy
pkgver=1.2.0
pkgrel=1
pkgdesc="A collection of C++ headers which make it easier to write Python C extension modules"
url="https://github.com/nucleic/cppy"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest')
source=("https://github.com/nucleic/cppy/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6a7c67094784a954794f1c665ff3bbba2d597e2f1f202196203ad883a10aea11d972771ff6d22b0b244ecee274bf10a989624815aeb527cff6a5463889b761db')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd cppy-$pkgver
  python setup.py build
}

check() {
  cd cppy-$pkgver
  python -m pytest
}

package() {
  cd cppy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
