# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=2.3.0
pkgrel=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('python-cachetools' 'python-certifi' 'python-pyasn1-modules' 'python-rsa'
         'python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses' 'python-pyopenssl' 'python-pyu2f')
source=("https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7cdd62f73567bf74693054f6e577e580835a3af9c6638113feb606b0e70146046e697ee9b219de3fdcbd5858c7469b8b99336bb51ff4d6ccc6a744b19739baaa')

prepare() {
  sed -i 's|cachetools>=2.0.0,<5.0|cachetools>=2.0.0|' google-auth-library-python-$pkgver/setup.py
  sed -i 's|utils.int_from_bytes|int.from_bytes|' google-auth-library-python-$pkgver/google/auth/crypt/es256.py
}

build() {
  cd google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  cd google-auth-library-python-$pkgver
  python -m pytest tests
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
