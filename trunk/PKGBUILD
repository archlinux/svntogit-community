# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=1.23
pkgrel=2
pkgdesc="Simple NFO file viewer in GTK+ 3"
arch=('any')
url="https://otsaloma.io/nfoview/"
license=('GPL3')
depends=('gtk3' 'python-gobject')
optdepends=('terminus-font: recommended font')
source=("https://github.com/otsaloma/${pkgname}/archive/${pkgver}.tar.gz"
        'x-nfo.desktop')
sha512sums=('b94724e130cdf58b911634496c52361d008ebfafab3ac40a76612c4891737d4145596a6badc15fe4228c90c1cd7261e5a892e3e462070ebb55f71332b36345d7'
            'a800af55df103ee12ce7a8815780e15896d7ad3eee947760ff4486ffb57fea8cf4105b5ffc61dc81e849ddf03eeac77f593a4ffe4fd73b5ea026672c72dff108')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"

  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"

  install -Dm644 AUTHORS.md \
    "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS.md"
  install -Dm644 NEWS.md \
    "${pkgdir}/usr/share/doc/${pkgname}/NEWS.md"
  install -Dm644 README.md \
    "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm644 TODO.md \
    "${pkgdir}/usr/share/doc/${pkgname}/TODO.md"

  install -Dm644 "${srcdir}/x-nfo.desktop" \
    "${pkgdir}/usr/share/mimelnk/text/x-nfo.desktop"
}

# vim:set ts=2 sw=2 et:
