# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Bruno Pagani <archange@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

_pkg=arrow
_pkgname=pyarrow
pkgname="python-${_pkgname}"
pkgver=10.0.0
pkgrel=1
pkgdesc="Columnar in-memory analytics layer for big data â€” Python module."
arch=(x86_64)
url="https://arrow.apache.org"
license=(Apache)
depends=(arrow python-numpy python-setuptools-scm)
optdepends=('python-cffi: interact with C code'
            'python-pandas: Pandas integration'
            'python-fsspec: Filesystem Spec support')
makedepends=(git cmake cython python-build python-installer python-wheel python-cffi python-pandas)
checkdepends=(python-brotli python-hypothesis python-pandas python-pytest python-pytest-lazy-fixture python-pytz)
source=("https://archive.apache.org/dist/${_pkg}/${_pkg}-${pkgver}/apache-${_pkg}-${pkgver}".tar.gz{,.asc}
        git+https://github.com/apache/arrow-testing.git)
b2sums=('86aeca485d4da48c61917a291aa5b67a9dae29ae6c3d3b243fbb421abc5b3c4181e13875176679f034411713ae90074f79b14ddfb5bcdae27ed0e5b90e7cd31e'
        'SKIP'
        'SKIP')
validpgpkeys=(265F80AB84FE03127E14F01125BCCA5220D84079  # Krisztian Szucs (apache) <szucs.krisztian@gmail.com>
              08D3564B7C6A9CAFBFF6A66791D18FCF079F8007) # Kouhei Sutou <kou@cozmixng.org>


prepare() {
  cd "${srcdir}/apache-${_pkg}-${pkgver}/python"
  for _file in pyproject.toml requirements-build.txt requirements-wheel-build.txt; do
    sed -e '/oldest-supported-numpy/d' -i "${_file}"
  done
}

build() {
  cd "${srcdir}/apache-${_pkg}-${pkgver}/python"
  export CXXFLAGS="${CXXFLAGS} -I${srcdir}/apache-${_pkg}-${pkgver}/cpp/src"
  ARROW_HOME=/usr \
  PARQUET_HOME=/usr \
  PYARROW_BUNDLE_ARROW_CPP_HEADERS=0 \
  PYARROW_BUNDLE_PLASMA_EXECUTABLE=0 \
  PYARROW_WITH_HDFS=1 \
  PYARROW_WITH_FLIGHT=1 \
  PYARROW_WITH_DATASET=1 \
  PYARROW_WITH_PARQUET=1 \
  PYARROW_WITH_PLASMA=1 \
  PYARROW_WITH_TENSORFLOW=1 \
  PYARROW_WITH_ORC=1 \
  python -m build --wheel --no-isolation
}

check() {
  cd "${srcdir}/apache-${_pkg}-${pkgver}/python"
  local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
  # rename source to avoid name clash
  mv pyarrow _nopyarrow
  PYTHONPATH="${PWD}/build/lib.linux-x86_64-cpython-${python_version}" \
  ARROW_TEST_DATA="${srcdir}"/arrow-testing/data \
  ARROW_HOME=/usr \
  PARQUET_HOME=/usr \
  pytest -vv --color=yes -k 'not test_cython_api and not test_visit_strings and not test_env_var and not test_get_include'
  mv _nopyarrow pyarrow
}

package(){
  cd "${srcdir}/apache-${_pkg}-${pkgver}/python"
  ARROW_HOME=/usr \
  PARQUET_HOME=/usr \
  PYARROW_BUNDLE_ARROW_CPP_HEADERS=0 \
  PYARROW_BUNDLE_PLASMA_EXECUTABLE=0 \
  PYARROW_WITH_HDFS=1 \
  PYARROW_WITH_FLIGHT=1 \
  PYARROW_WITH_DATASET=1 \
  PYARROW_WITH_PARQUET=1 \
  PYARROW_WITH_PLASMA=1 \
  PYARROW_WITH_TENSORFLOW=1 \
  PYARROW_WITH_ORC=1 \
  python -m installer --destdir="${pkgdir}" dist/*.whl

  # drop tests from install
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  rm -rf "${pkgdir}/usr/lib/python${python_version}/site-packages/${_pkgname}/tests"
}
