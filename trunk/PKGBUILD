# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-pyzstd
_pyname=${pkgname#python-}
pkgver=0.15.3
pkgrel=1
pkgdesc='Python bindings to Zstandard (zstd) compression library'
arch=(x86_64)
url="https://github.com/animalize/$_pyname"
license=(LGPL)
depends=(python
         zstd)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
checkdepends=(python-tests)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('ac4edab5d3955343e8f7f287e62cd2882907d46bcba4b406a1e9f84aa2887472')

build() {
	cd "$_archive"
	python -m build -wn -C=--dynamic-link-zstd
}

check() {
	cd "$_archive"
	local _pyver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
	export PYTHONPATH="$PWD/build/lib.linux-$CARCH-$_pyver"
	# python -m pytest
	python setup.py test
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
