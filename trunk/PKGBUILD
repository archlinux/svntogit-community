# Maintainer : Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=openvdb
pkgver=4.0.2
pkgrel=3
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python: python module')
source=("https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz"
        22d09714f9a6a29c62668b533495ed2afd4251ef.patch)
sha512sums=('8e3e12583b81f9b9303b309e839c8321c67b67ff8a0e61bb56c87276bf25c0deff22f5a73c7c5b661b8e5df345bfdabfa6b0aea9bf16fce57d9c53a76751c75d'
            '702f42c33c6d6c9da7a85166823d6bc1e8789f328a2781639fea081f8944d29f785f2ea71419741a63f1177db94db8740dedeb7168fa7b5adb1a0a8cd387b342')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 < "${srcdir}/22d09714f9a6a29c62668b533495ed2afd4251ef.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build
  cmake .. \
    -DBLOSC_LOCATION=/usr/ \
    -DTBB_LOCATION=/usr/ \
    -DUSE_GLFW3=ON \
    -DGLFW3_LOCATION=/usr/ \
    -DILMBASE_NAMESPACE_VERSIONING=OFF \
    -DOPENEXR_NAMESPACE_VERSIONING=OFF \
    -DILMBASE_LOCATION=/usr/ \
    -DOPENEXR_LOCATION=/usr/ \
    -DCPPUNIT_LOCATION=/usr/include/cppunit \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=ON

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}

# vim:set sw=2 sts=2 et:
