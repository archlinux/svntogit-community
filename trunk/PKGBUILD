# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=3.1.0
pkgrel=3
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-tool-kit' 'ffmpeg' 'taglib' 'libcue' 'qt5-svg' 'gst-plugins-bad')
makedepends=('python' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-music/archive/$pkgver.tar.gz")
sha512sums=('29b3d9c242926523dd7e5c867b3c88af66f3f0e0ec5decd6bbcda094dae0bd517801695aa5c7e8e9427cbdbc7468b898d3641eeacdd324728d331b2f1e440c9d')

prepare() {
  sed -i 's/-0-2//g' deepin-music-$pkgver/music-player/music-player.pro
}

build() {
  cd deepin-music-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-music-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
