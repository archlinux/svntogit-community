# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: kokoko3k <kokoko3k at gmail dot com>
# Contributor: Ionut Biru <ibiru at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski at archlinux dot org>

pkgname=lib32-libva
pkgver=2.13.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux (32-bit)'
arch=(x86_64)
url='https://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=(lib32-libdrm lib32-libgl lib32-wayland lib32-libxext lib32-libxfixes libva)
makedepends=(mesa lib32-libglvnd)
optdepends=('lib32-libva-vdpau-driver: vdpau back-end for nvidia'
            'lib32-libva-intel-driver: back-end for intel cards')
source=("$pkgname-$pkgver.tar.gz::https://github.com/intel/libva/archive/$pkgver.tar.gz")
sha512sums=('226d09956754c9942348ed289f210268bad64dbba95d948b33ae063f7ecb276af9da083a48c03c3c79ad4305716d203898bc75a109945d064a5fe6fb43398dea')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${pkgname#*-}-$pkgver"
  ./autogen.sh --prefix=/usr --libdir='/usr/lib32' 
  make
}

package() {
  cd "${pkgname#*-}-$pkgver"
  make DESTDIR="${pkgdir}" install
  rm -rfv "${pkgdir}/usr/"{include,bin}
  install -m644 -D COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
