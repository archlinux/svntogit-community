# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Serge Victor <arch@random.re>

_pyname=flask-wtf
pkgname=python-flask-wtf
pkgver=0.14.2
pkgrel=4
pkgdesc='Simple integration of Flask and WTForms'
url='https://flask-wtf.readthedocs.io/'
arch=('any')
license=('BSD')
depends=('python-flask' 'python-wtforms' 'python-werkzeug')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-flask-babel' 'python-nose')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/lepture/flask-wtf/archive/v${pkgver}.tar.gz)
sha512sums=('b4b577d545f4b6b3c86288ec004455c0559c92582c1b8900e79c2b2beff98f86e4b20a0fbb3bc52f6711375f5097491cac4cc9f33a97f6caa2fd26376ece9ceb')

prepare() {
  sed -r 's|dist = get_distribution|#|' -i ${_pyname}-${pkgver}/docs/conf.py
  sed -r "s|(version =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
  sed -r "s|(release =)|\\1 '${pkgver}'  #|" -i ${_pyname}-${pkgver}/docs/conf.py
}

build() {
  cd ${_pyname}-${pkgver}
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pyname}-${pkgver}
  nosetests
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/flask-wtf.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
