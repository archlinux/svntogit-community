# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-pango
pkgver=1.50.12
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-libthai lib32-cairo lib32-libxft lib32-harfbuzz lib32-fribidi pango)
makedepends=(git meson)
_commit=3d3f165e77178b630d7dc2b733e64af513c99f5f  # tags/1.50.12^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pango
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pango
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson pango build \
    --libdir=/usr/lib32 \
    -D introspection=disabled
  meson compile -C build
}

# not running checks: validation breaks when deps
# (especially harfbuzz) don't match upstream CI

package() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{bin,include}
}

# vim:set sw=2 sts=-1 et:
