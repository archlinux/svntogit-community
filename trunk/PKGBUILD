# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: alphazo <alphazo@gmail.com>

pkgname=sshuttle
pkgver=0.71
pkgrel=1
pkgdesc="Transparent proxy server that works as a poor man's VPN. Forwards all TCP packets over ssh (and even DNS requests when using --dns option). Doesn't require admin privileges on the server side"
arch=('any')
url="https://github.com/sshuttle/sshuttle"
license=('GPL2')
depends=('python2' 'iptables' 'openssh' 'net-tools')
makedepends=('git')
backup=('etc/sshuttle/tunnel.conf' 'etc/sshuttle/prefixes.conf')
source=("git+https://github.com/sshuttle/$pkgname.git#tag=$pkgname-$pkgver"
        'sshuttle.service' 'arch-install.patch')
md5sums=('SKIP'
         'f2cd1660dcdb4e24b45b71e589da73f3'
         '15ed72e2b68dd07ef97abfdcb828d188')

prepare() {
  cd $pkgname/src
  patch -p1 -i "$srcdir/arch-install.patch"

  sed -i 's#/usr/bin/env python#/usr/bin/env python2#' stresstest.py
}

build() {
  cd $pkgname/src
  make
}

package() {
  cd $pkgname/src
  install -Dm755 sshuttle "$pkgdir/usr/bin/sshuttle"

  install -d "$pkgdir/usr/share/sshuttle"
  cp -r *.py compat "$pkgdir/usr/share/sshuttle"/

  # pandoc not present, so manpage was not built
  # install -Dm644 sshuttle.8 "$pkgdir/usr/share/man/man8/sshuttle.8"

  cd ..
  install -d "$pkgdir/etc/sshuttle"
  install -Dm644 packaging/{tunnel.conf,prefixes.conf} "$pkgdir/etc/sshuttle"
  install -Dm644 "$srcdir/sshuttle.service" "$pkgdir/usr/lib/systemd/system/sshuttle.service"
}
