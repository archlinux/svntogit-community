# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: Padfoot <padfoot at exemail dot com dot au>
# Contributor: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: josephgbr <rafael.f.f1 at gmail dot com>

pkgname=lib32-libvisual
pkgver=0.4.1
pkgrel=1
pkgdesc="Abstraction library that comes between applications and audio visualisation plugins (32-bit)"
arch=('x86_64')
url='https://sourceforge.net/projects/libvisual/'
license=('LGPL')
depends=('lib32-glibc' 'lib32-sdl12-compat' "${pkgname#lib32-}")
makedepends=('gcc-multilib')
source=("https://downloads.sourceforge.net/sourceforge/${pkgname#lib32-}/${pkgname#lib32-}-${pkgver}.tar.gz")
sha512sums=('c8e279bcb52144938faaafa9c16c44f20f9dc9e34e1c2fb1e9fa1ca0963f66e1da15a9318ad58aca3657ce5774a81e081530546ff65c6d3b7b09d4e6166c863d')

prepare(){
  cd "${srcdir}/${pkgname#lib32-}-${pkgver}"
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export LDFLAGS='-m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
}

build() {
  cd "${srcdir}/${pkgname#lib32-}-${pkgver}"
  ./configure --prefix='/usr' --libdir='/usr/lib32'
  make
}

package() {
  cd "${srcdir}/${pkgname#lib32-}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/"{include,share}
}
