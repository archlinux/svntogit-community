# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vis
pkgver=0.4
pkgrel=1
_pkgname_test=vis-test
_pkgver_test=0.2
pkgdesc='modern, legacy free, simple yet efficient vim-like editor'
arch=('i686' 'x86_64')
url='http://www.brain-dump.org/projects/vis/'
depends=('ncurses' 'libtermkey' 'lua' 'lua-lpeg' 'tre' 'file')
checkdepends=('vim')
license=('custom:ISC')
backup=('etc/vis/visrc.lua')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/martanne/${pkgname}/archive/v${pkgver}.tar.gz"
        "${_pkgname_test}-${_pkgver_test}.tar.gz::https://github.com/martanne/${_pkgname_test}/archive/v${_pkgver_test}.tar.gz")
sha256sums=('f11ba41cfb86dd39475960abfd12469de4da0ccfdb941f1d7680d89d987694c5'
            '26f3a82842e098f4004f5a8741ac501e209aa36704971b7e68d8104102c90788')

prepare() {
	cd "${pkgname}-${pkgver}/"

	rmdir test/
	mv "../${_pkgname_test}-${_pkgver_test}" test
}

build() {
	cd "${pkgname}-${pkgver}/"

	./configure \
		--prefix=/usr

	make
}

check() {
	cd "${pkgname}-${pkgver}/"

	make -C test/
}

package() {
	cd "${pkgname}-${pkgver}/"

	make DESTDIR="${pkgdir}" install

	install -D -m0644 lua/visrc.lua "${pkgdir}"/etc/vis/visrc.lua

	install -D -m0644 'LICENSE' "${pkgdir}/usr/share/licenses/vis/LICENSE"
}

