# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-subtests
pkgver=0.5.0
pkgrel=4
pkgdesc='unittest subTest() support and subtests fixture'
arch=('any')
license=('MIT')
url='https://github.com/pytest-dev/pytest-subtests'
depends=('python-pytest')
makedepends=('python-setuptools-scm')
source=("https://github.com/pytest-dev/pytest-subtests/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e3d7e90c3ab66950eb80d25049bc6f6f75b08b3da5ce0de5f3fc55d6a176a8d1fdcb19a19f7d9d137aeadcc82b214550474f76d5611bc5592ca649ddd8257a2a')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-subtests-$pkgver
  python setup.py build
}

check() {
  cd pytest-subtests-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-subtests-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
