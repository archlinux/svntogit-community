# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=hexter
pkgver=1.1.0
pkgrel=4
pkgdesc="A Yamaha DX7 modeling software synthesizer for the DSSI Soft Synth Interface"
arch=('x86_64')
url="https://github.com/smbolton/hexter"
license=('GPL2')
groups=('dssi-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc')
makedepends=('alsa-lib' 'dssi' 'glib2' 'gtk2' 'ladspa' 'liblo')
optdepends=('dssi: for jack-dssi-host as plugin host'
            'qtractor: plugin host')
source=("$pkgname-$pkgver.tar.gz::https://github.com/smbolton/${pkgname}/archive/version_${pkgver}.tar.gz"
        "${pkgname}-1.1.0-fix_configureac.patch"
        "${pkgname}-1.1.0-fix_segfault.patch")
sha512sums=('4e779bf386d79e4e4869e84d0ab600976b3b8f40ad96d79cde6315f378554a86ebf78f4e85e777ed7820842a840b508afdd19a62b42a7d2f65d16c26dfa146f2'
            'dba7b05fd7b6ec730507bf72f34338bd9e2151401359fde980713c742febefc3d01ef626497ac93a45227e8de4ce6aa3222e1bb1a085b1d235a7e5bd4a9bb461'
            '88369468b5bc416e5f1826b3a0d57e5ccac9369f8e53029315272ebbb37d64fdafeb172bd72058c8c87dc7c8a29d9acb824924982c42c11dde98ae1e0aaee3a6')
b2sums=('b71efacb4192a4749b03dfd7388d06141fa34db4ad6df5c75ac2d36d7863d05e7d834f23afd2911489f4577f6931fabe163ba650bee48a9da820f2be2f978dc3'
        'c0ff6dd0ab243d6d00adc75cdd75c2cb630bf295f7a2536a7a673656d2974a3c2d470e028e63a2abe2de2add5f2e7724643ea857c6f581b2b35ebd794baee9af'
        '302b263f5b18b5df5c8a691350e17b20aee93b0ce005f8a55d13664be922b66a8a1331053ff2f3e72ae9ac120e05b583405e9b5ff3146459c740bc9a30c154ef')

prepare() {
  mv -v "${pkgname}-version_${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../"${pkgname}-1.1.0-fix_configureac.patch"
  patch -Np1 -i ../"${pkgname}-1.1.0-fix_segfault.patch"
  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libasound.so' 'libgdk-x11-2.0.so' 'libglib-2.0.so'
  'libgobject-2.0.so' 'libgtk-x11-2.0.so' 'liblo.so' 'libpango-1.0.so'
  'libpangocairo-1.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
