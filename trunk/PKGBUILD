# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ruby-awesome_print
pkgver=1.9.2
pkgrel=1
pkgdesc='Pretty print your Ruby objects with style'
arch=(any)
url='https://github.com/awesome-print/awesome_print'
license=(MIT)
depends=(ruby)
makedepends=(ruby-rspec ruby-fakefs ruby-sqlite3 ruby-nokogiri)
options=(!emptydirs)
source=(https://github.com/awesome-print/awesome_print/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('70507b7a138bf9b3cfc7150327ff1cf53a75557ed59c4e8b5e0d9692ec0d0a06')

prepare() {
  cd awesome_print-$pkgver
  sed '/appraisal/d' -i awesome_print.gemspec
}

build() {
  local _gemdir="$(gem env gemdir)"
  cd awesome_print-$pkgver
  gem build awesome_print.gemspec
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "tmp_install/$_gemdir" \
    --bindir "tmp_install/usr/bin" \
    awesome_print-$pkgver.gem
  find "tmp_install/$_gemdir/gems/" \
    -type f \
    \( \
        -iname "*.o" -o \
        -iname "*.c" -o \
        -iname "*.so" -o \
        -iname "*.time" -o \
        -iname "gem.build_complete" -o \
        -iname "Makefile" \
    \) \
    -delete
  rm -r tmp_install/$_gemdir/cache
}

check() {
  local _gemdir="$(gem env gemdir)"
  cd awesome_print-$pkgver
  GEM_HOME="tmp_install/$_gemdir" rake spec
}

package() {
  cd awesome_print-$pkgver
  cp -a tmp_install/* "$pkgdir"/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
