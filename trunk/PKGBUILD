# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: jose <jose1711 [at] gmail (dot) com>

pkgname=gpsprune
pkgver=19.1
pkgrel=1
pkgdesc="View, edit and convert coordinate data from GPS systems"
arch=('any')
url="https://activityworkshop.net/software/gpsprune/"
license=('GPL')
depends=('desktop-file-utils' 'java-runtime')
optdepends=('gnuplot: draw charts'
            'gpsbabel: load data directly from GPS receivers'
            'perl-image-exiftool: save coordinates into JPEG files')
source=("https://activityworkshop.net/software/$pkgname/${pkgname}_$pkgver.jar"
        "https://activityworkshop.net/software/gpsprune/prunelogo.png"
        "$pkgname.sh"
        "$pkgname.desktop")
noextract=("${pkgname}_$pkgver.jar")
sha256sums=('fe0b387a15cbaca1bbb15e2caa6b67fb465516f62354560593bfe9203e183cb7'
            '9d08bebe643266d2be126597a6841b0364ab07b2d44fdde8c7ad2ec59088523c'
            'cd8b8f8fc54df03e7435e4849d734e7d9ef5291d8f75c6abab0bfe809fe6abd9'
            '744f83c1c6d76c3d339b41450ddc79a7c15fbaf6d637741fb2c09529a65a0311')

package() {
  install -Dm644 ${pkgname}_$pkgver.jar "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 prunelogo.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
}
