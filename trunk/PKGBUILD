# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Maintainer: Biru Ionut <ionut@archlinux.ro>

_pkgbasename=pango
pkgname=lib32-$_pkgbasename
pkgver=1.40.6+9+g92cc73c8
pkgrel=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-libthai lib32-libgl lib32-cairo lib32-libxft
         $_pkgbasename)
makedepends=(gcc-multilib gobject-introspection meson git)
options=('!emptydirs')
_commit=92cc73c898e4665fd739704417d487d137dd271b  # master
source=("git+https://git.gnome.org/browse/pango#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgbasename

  git describe --tags | sed 's/-/+/g'
}
 
 prepare() {
  cd $_pkgbasename

  mkdir ../build
  sed -i 's#"pango.modules"#"pango.modules-32"#' pango/modules.c
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd build

  meson --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32 \
    --buildtype=release ../$_pkgbasename
  ninja -v

#   ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc \
#       --localstatedir=/var \
#       --with-dynamic-modules=arabic-fc,arabic-lang,basic-fc,basic-win32,basic-x,basic-atsui,hangul-fc,hebrew-fc,indic-fc,indic-lang,khmer-fc,syriac-fc,tibetan-fc \
#       --disable-introspection
#   sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
#   make
}

package() {
  cd build

  DESTDIR="$pkgdir" ninja install
  rm -rf "$pkgdir"/etc
  rm -rf "$pkgdir"/usr/{bin/pango-view,share,include}
}
