# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=nautilus-terminal
pkgver=1.0
pkgrel=5
pkgdesc="An integrated terminal for Nautilus"
arch=('any')
url="http://projects.flogisoft.com/nautilus-terminal/"
license=('GPL')
depends=('python2-nautilus' 'vte3' 'python2-xdg')
source=(http://projects.flogisoft.com/$pkgname/download/${pkgname}_${pkgver}_src.tar.gz
        vte091.patch)
md5sums=('ba89ae9a4e5c632a03da7e342679ad94'
         '4a659e43bc9b127fbae41d18206bbf97')

prepare() {
  cd "$srcdir/${pkgname}_${pkgver}_src"

  sed -i 's|^#!/usr/bin/python$|#!/usr/bin/python2|' src/nautilus_terminal.py

  # Fix compatibility with vte 2.91
  patch -Np0 -i ../vte091.patch
}

package() {
  cd "$srcdir/${pkgname}_${pkgver}_src"
  ./install.sh --package "$pkgdir"

  # Remove doc files
  rm -r $pkgdir/usr/share/doc
}
