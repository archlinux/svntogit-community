# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Sven Kauber <celeon@gmail.com>

pkgname=siege
pkgver=2.70
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="An http regression testing and benchmarking utility"
url="http://www.joedog.org/JoeDog/Siege"
depends=('openssl')
options=(!libtool)
source=("http://www.joedog.org/pub/siege/siege-${pkgver}.tar.gz")
backup=(etc/siegerc etc/urls.txt)

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  install -d -m755 "$pkgdir/etc"
  install -d -m755 "$pkgdir/usr/share"
  make DESTDIR="$pkgdir" sysconfdir="$pkgdir/etc" install
  sed -i 's/^# file =$/file = \/etc\/urls.txt/' "$pkgdir/etc/siegerc"
}

md5sums=('835c7a0606851357ebf03084ff546310')
