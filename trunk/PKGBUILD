# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=0.9.8
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=(x86_64)
url="https://qxgedit.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base qt6-svg)
makedepends=(alsa-lib cmake qt6-tools)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('7ed449c66ced91de5ac9ee253d0683c8e09f1d6cc7de1050e67475ac80249ea40d0c5124453138f013f91ff4136deb5a0fd782a2710f5aadb0a4745664d30a67')
b2sums=('005a8f78616d76a580c43299b540dfad413290a2a1949914aadf62d39a3feb4f39df717fd9bbec668b4c6f476e71e54426cd19116b539fb7fb9f25c7f9d0991c')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  depends+=(libasound.so)

  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
