# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cmocka
url='http://cmocka.org/'
pkgver=1.1.0
pkgrel=1
pkgdesc='an elegant unit testing framework for C with support for mock objects'
arch=(i686 x86_64)
license=(Apache)
depends=(glibc)
makedepends=(cmake)
source=(https://cmocka.org/files/1.1/cmocka-$pkgver.tar.{xz,asc})
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>
sha1sums=('da18b3fd61ba6e567781ec2bdef6a4a0be0ae5e7'
          'SKIP')

build() {
  rm -rf build; mkdir build
  cd build
  cmake ../$pkgname-$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DUNIT_TESTING=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"
}
