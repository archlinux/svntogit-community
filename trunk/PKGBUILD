# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  Gour <gour@mail.inet.hr>

pkgname=emacs-muse
pkgver=3.20
pkgrel=3
pkgdesc="Publishing environment for Emacs"
url="http://mwolson.org/projects/EmacsMuse.html"
arch=('any')
license=('GPL3')
depends=('emacs' )
install=${pkgname}.install
source=(http://download.gna.org/muse-el/muse-$pkgver.tar.gz)
md5sums=('aa43de43f4437e7979ffbb708180b2e9')

build() {
  cd $srcdir/muse-$pkgver

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' experimental/muse-mathml.el

  cp Makefile.defs.default Makefile.defs
  sed -i -e "s|\(PREFIX.*= \)/usr/local|\1/usr|" Makefile.defs
  make
}

package() {
  cd $srcdir/muse-$pkgver
  make DESTDIR=$pkgdir install
}
