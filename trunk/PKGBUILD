# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: hexchain <i at hexchain.org>

pkgname=range-v3
pkgver=0.5.0
pkgrel=1
pkgdesc="Experimental range library for C++11/14/17"
arch=('any')
url='https://github.com/ericniebler/range-v3'
license=('custom')
makedepends=('cmake')
source=("https://github.com/ericniebler/range-v3/archive/${pkgver}.tar.gz")
sha512sums=('076be03b0f4113acb5fb4db83ed5e755bed60ff8df9a7b982b144949cce40f0bb0579ecca38c1f33643b63037b8d311afa6d613b50811e1191b63e2c42a0d4bf')

prepare() {
    cd "${srcdir}/range-v3-${pkgver}"
    rm -rf build && mkdir build
}

build() {
    cd "${srcdir}/range-v3-${pkgver}"

    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DRANGE_V3_TESTS=OFF \
        -DRANGE_V3_HEADER_CHECKS=OFF \
        -DRANGE_V3_EXAMPLES=OFF \
        -DRANGE_V3_PERF=OFF
    make
}

package() {
    cd "${srcdir}/range-v3-${pkgver}"
    install -Dm644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    cd build
    make DESTDIR="${pkgdir}/" install
}
