# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plette
pkgver=0.2.2
pkgrel=1
pkgdesc="Structured Pipfile and Pipfile.lock models"
url="https://github.com/sarugaku/plette"
license=('ISC')
arch=('any')
depends=('python-six' 'python-tomlkit')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-cerberus')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/plette/archive/$pkgver.tar.gz")
sha512sums=('bca1e6ed8b6d9fadbf9aabd46bdbe3faa95e143abcbb88da747b6bae8541089eb6137d5c9e9d1fe3f51d8e26463b192473e27ccc6e343a259611f3f7178b22bb')

build() {
  cd plette-$pkgver
  python setup.py build
}

check() {
  cd plette-$pkgver
  python setup.py pytest
}

package() {
  cd plette-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
