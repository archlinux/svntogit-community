# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-lrx
pkgver=0.1.0
pkgrel=2
pkgdesc="A command line jack application providing 2, 3, or 4-band, 4th order crossover filters."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
groups=('pro-audio')
depends=('clthreads' 'jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('af85b3559390f3efbde13e2f6fe3b7710692b011e2a0bcee87be841515484646c41651263f316c6dc82ec64f8c00e0f97223c7ff9e2f1d96ef3550e23ebefc7d')

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../{AUTHORS,README*}
  # examples
  install -t "${pkgdir}/usr/share/${pkgname}/examples" \
    -vDm644 ../examples/*
}
