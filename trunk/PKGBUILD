# Maintainer: David Runge <dvzrv@archlinux.org>

_name=hosc
pkgname=haskell-hosc
pkgver=0.18.1
pkgrel=9
pkgdesc="Haskell Open Sound Control"
arch=('x86_64')
url="https://github.com/rd--/hosc"
license=('GPL')
depends=('ghc-libs' 'haskell-blaze-builder' 'haskell-data-binary-ieee754' 'haskell-network')
makedepends=('ghc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rd--/hosc/archive/${pkgver}.tar.gz")
sha512sums=('76f09662e6edeb3bd0a58ea0d915a5da0b370c716e5c303b7f69f7e43214da2f4a8207fc1545841d175018004e2eaef7820796bcb010190b299b92de3b5c07f2')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir="/usr/share/doc/${pkgname}" \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid \
                             --datasubdir="$pkgname"
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd "$pkgname-$pkgver"
  runhaskell Setup test --show-details=direct
}

package() {
  cd "$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
