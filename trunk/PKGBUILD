# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Fangrui Song <i at maskray.me>

pkgname=ccls
pkgver=0.20190823.6
pkgrel=3
pkgdesc='C/C++/ObjC language server supporting cross references, hierarchies, completion and semantic highlighting'
arch=('x86_64')
url='https://github.com/MaskRay/ccls'
license=('Apache')
depends=('clang=11.0.0' 'llvm-libs' 'rapidjson')
makedepends=("cmake" "llvm")
conflicts=('ccls-git')
source=("https://github.com/MaskRay/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-CLANG_LINK_CLANG_DYLIB.patch::https://github.com/MaskRay/ccls/commit/a7a982ab9232b2730280439781f83f8055363c59.patch)
sha512sums=('c6df96e304f5b7381c67eda4c1bd05873a85fb0d0983b3abcc507987a402860aa6660079a0e7b125f5cda22047da936239480aa0b604cd2598f84a4b9e9c3bdd'
            '54eb68c64562b9cfbdaeacfc7c8292f9f85dc6bfd7d0e5ea7047ee0d72aacbe7b9f276d3d7beae94b5803018cd57e941a816c2614634c16b7c9b46a4929f0a25')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../$pkgname-CLANG_LINK_CLANG_DYLIB.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -H. -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_COMPILER=clang++ -DCLANG_LINK_CLANG_DYLIB=1
  cmake --build build
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
