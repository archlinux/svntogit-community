# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=messengerfordesktop
pkgver=2.0.9
pkgrel=1
pkgdesc='A simple & beautiful desktop client for Facebook Messenger'
arch=('i686' 'x86_64')
url='https://messengerfordesktop.com/'
license=('MIT')
depends=('electron')
makedepends=('apm' 'npm')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Aluxian/Messenger-for-Desktop/archive/v${pkgver}.tar.gz"
        "${pkgname}.js"
        'use-system-electron.patch')
sha256sums=('f0b68a2dd6bdfd5d7effa21747abb222b5716c2284ae62b9a5e8ae410f00ef9d'
            '55e6da47ddbf383fc8a6ef6e0aae5621b7800c8efe46422c1c352d6c029c1f45'
            '014ef8a03e38d4e5c5cab49cb6a2e9fbf81de9aecbfc66aeadf124844a541a66')

if [ "${CARCH}" == 'i686' ]; then
    _target=linux32
else
    _target=linux64
fi

prepare() {
    cd Messenger-for-Desktop-${pkgver}
    
    patch -Np1 -i "${srcdir}"/use-system-electron.patch
    sed -e 's|/opt/{{ name }}/||' \
        -i resources/linux/app.desktop \
        -i resources/linux/startup.desktop
}

build() {
    cd Messenger-for-Desktop-${pkgver}
    
    npm install
    cd src
    ATOM_HOME="${PWD}" apm install --production
    cd ..

    ./node_modules/.bin/gulp build:${_target}
}

package() {
    cd Messenger-for-Desktop-${pkgver}

    appdir=/usr/lib/${pkgname}

    install -dm755 "${pkgdir}"${appdir}
    cp -r build/${_target}/opt/${pkgname}/resources/app/* "${pkgdir}"${appdir}

    cp -r build/${_target}/usr/share "${pkgdir}"/usr

    install -Dm755 "${srcdir}"/${pkgname}.js "${pkgdir}"/usr/bin/${pkgname}

    install -Dm644 -t "${pkgdir}"/usr/share/licenses/${pkgname} LICENSE.md

    # Clean up
    find "${pkgdir}"${appdir}/node_modules \
        -name 'package.json' \
            -exec sed -e "s|${srcdir}/Messenger-for-Desktop-${pkgver}/src|${appdir}|" \
                -i {} \; \
        -or -name '.*' -prune -exec rm -r '{}' \; \
        -or -name '*.a' -exec rm '{}' \; \
        -or -name '*.cc' -exec rm '{}' \; \
        -or -name '*.h' -exec rm '{}' \; \
        -or -name '*.gyp' -exec rm '{}' \; \
        -or -name '*.gypi' -exec rm '{}' \; \
        -or -name '*.Makefile' -exec rm '{}' \; \
        -or -name '*.mk' -exec rm '{}' \; \
        -or -name '*.mm' -exec rm '{}' \; \
        -or -name 'appveyor.yml' -exec rm '{}' \; \
        -or -name 'benchmark' -prune -exec rm -r '{}' \; \
        -or -name 'bin' -prune -exec rm -r '{}' \; \
        -or -name 'bin.js' -exec rm '{}' \; \
        -or -name 'build.js' -exec rm '{}' \; \
        -or -name 'cli.js' -exec rm '{}' \; \
        -or -name 'deps' -prune -exec rm -r '{}' \; \
        -or -name 'doc' -prune -exec rm -r '{}' \; \
        -or -name 'docs' -prune -exec rm -r '{}' \; \
        -or -name 'example' -prune -exec rm -r '{}' \; \
        -or -name 'examples' -prune -exec rm -r '{}' \; \
        -or -name 'hunspell' -prune -exec rm -r '{}' \; \
        -or -name 'Makefile' -exec rm '{}' \; \
        -or -name 'obj.target' -prune -exec rm -r '{}' \; \
        -or -name 'test' -prune -exec rm -r '{}' \; \
        -or -name 'tests' -prune -exec rm -r '{}' \; \
        -or -name 'tools' -prune -exec rm -r '{}' \;
}
