# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=skopeo
pkgver=1.0.0
pkgrel=1
pkgdesc='A command line utility for various operations on container images and image repositories.'
arch=(x86_64)
url='https://github.com/projectatomic/skopeo'
license=(APACHE)
depends=(gpgme device-mapper)
makedepends=(go go-md2man btrfs-progs git)
backup=(etc/containers/policy.json
        etc/containers/registries.conf
        etc/containers/registries.d/default.yaml
        etc/containers/seccomp.json
        etc/containers/storage.conf)
_commit=bd162028cd83ceecd8915736f2d66d5ca73ee54a # tags/v1.0.0
source=(git+$url#commit=$_commit
        containers-storage.conf.5.md
        policy.json.5.md
        registries.conf
        registries.conf.5.md
        seccomp.json
        storage.conf)
sha256sums=('SKIP'
            '707268f7968a6e4c3be282dcac790f7d208527e6220e95232b11b25401c2c817'
            'a4dd89bb5d8dec2b3af3888f879144334aeeab31734e19793da498d355994cd9'
            'bf720633c4c9906e93665813bbf13a73a6cb0df2b412d81228c7673505513d1f'
            '38264ddcf9699b8f8d7b4823abc118fb01362f2d58836c01df13e3e08b801f9f'
            'cfa7dd2c35c8e9cba74a68810e1f377e448988ccb0a54b98232313967677326e'
            '4bcce6e85da1ea07086fe1a921469455c06a0fc083a21692e812643f1c2b22fa')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make binary-local BUILDTAGS='containers_image_ostree_stub'
  make docs
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  for conf in registries.conf storage.conf seccomp.json; do
    install -m644 "$srcdir/$conf" -t "$pkgdir/etc/containers/"
  done

  cd "$srcdir"
  install -d "$pkgdir/usr/share/man/man5/"
  for manpage in *.md; do
    go-md2man -in "$manpage" -out "$pkgdir/usr/share/man/man5/${manpage%*.md}"
  done
}
