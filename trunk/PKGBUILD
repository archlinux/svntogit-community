# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=jedi-language-server
pkgver=0.39.0
pkgrel=2
pkgdesc='Language server for Jedi'
arch=('any')
url=https://github.com/pappasam/jedi-language-server
license=('MIT')
depends=('python-docstring-to-markdown' 'python-jedi' 'python-pygls')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-lsp-jsonrpc' 'python-pyhamcrest' 'python-pytest')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        '0001-Migrate-to-python-lsp-jsonrpc.patch')
b2sums=('a001d324dbb17f5d60dcf60a282a66f0f25213e00e224ff96c63eeb648808fd488de8b48aa8564b1e162cb1ce6e953629a0d2524f2b01b3be9984fd68fdd5d45'
        '450ef97da26bcca81270b01b3fcd236124dbcb963f4adaae7bd6dbe7fcd781e630a016308ec487b85de3c2f1089a1cd2eeb6cda13c479d8ddbc0eaaf3280b3c0')

prepare() {
  cd $pkgname-$pkgver
  patch --forward --strip=1 --input=../0001-Migrate-to-python-lsp-jsonrpc.patch

  # Remove include list https://github.com/pypa/wheel/issues/92
  sed -i '/include = \["README.md"\]/d' pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $pkgname-$pkgver
  mkdir -p temp
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m installer --destdir=temp dist/*.whl
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" pytest tests
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/${pkgname//-/_}-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
