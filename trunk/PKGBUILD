# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.6.8
pkgrel=1
pkgdesc="A fast AppStream metadata generator"
arch=(x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg lmdb optipng)
makedepends=(dmd meson ninja mustache-d curl gir-to-d gobject-introspection docbook-xsl)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ximion/appstream-generator/archive/v$pkgver.tar.gz"
        appstream-generator-girtod-0.13.patch::"https://github.com/ximion/appstream-generator/commit/d0ce0258.patch")
sha256sums=('9aeaba7922f56c45306757bb45077dcb520ba046aa45a8125f0f217b7e71058d'
            'e187f0ec671c3fc19442875550640946c05fd68d09a284b5808fbb9691ea1943')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../appstream-generator-girtod-0.13.patch # Fix build with gir-to-d 0.13
}

build() {
  cd build
  meson ../$pkgname-$pkgver -Ddownload-js=false --prefix=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
