# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=conduit-extra
pkgname=haskell-conduit-extra
pkgver=1.2.0
pkgrel=1
pkgdesc="Batteries included conduit: adapters for common libraries."
url="https://github.com/snoyberg/conduit"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-async" "haskell-attoparsec" "haskell-blaze-builder" "haskell-conduit"
         "haskell-exceptions" "haskell-monad-control" "haskell-network" "haskell-primitive"
         "haskell-resourcet" "haskell-stm" "haskell-streaming-commons" "haskell-text"
         "haskell-transformers-base")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('494ab5e53f05dc6dfd0ed44506de5eae241b3fb20e9b431100c6090de0a3af552e204de5012130ae0486b0e175e03deac0303246dcbbee75385e171facc3363d')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
