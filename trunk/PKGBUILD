# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=openbve
_pkgname=OpenBVE
pkgver=1.6.0.0
pkgrel=1
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=('any')
url="https://openbve-project.net/"
license=('MIT' 'custom:public domain')
depends=('hicolor-icon-theme' 'libxi' 'mono' 'openal')
source=($_pkgname-$pkgver.tar.gz::https://github.com/leezer3/$_pkgname/archive/$pkgver.tar.gz
        $pkgname.sh
        $pkgname.desktop
        $pkgname.appdata.xml)
sha256sums=('7e6255f20b8ca9e0d506bd5e31cca4fa03de651181410d35dd775e0151448953'
            'cf0f2a28e65248f742d6bb80f3101e36f24097e699c4e5acdf3fd4dbb965d3c3'
            '8190945fa834ecdcf8a3f271264e5be2e288933ecde943060346e16537611c0f'
            '1ae1d4bf4dbe0d4e012fafef090d98b4c67ba87bee8981af0b2de11015b57df2')

build() {
  cd $_pkgname-$pkgver
  xbuild /p:Configuration=Release OpenBVE.sln
}

package() {
  cd $_pkgname-$pkgver

  # Binaries
  install -dm755 "$pkgdir/usr/lib/$pkgname/"
  cp -r bin_release/* "$pkgdir/usr/lib/$pkgname/"

  # Data
  install -dm755 "$pkgdir/usr/share/games/$pkgname/"
  mv "$pkgdir/usr/lib/$pkgname/Data" "$pkgdir/usr/share/games/$pkgname/"
  ln -s ../../share/games/$pkgname/Data "$pkgdir/usr/lib/$pkgname/Data"

  # Executable
  install -Dm755 ../$pkgname.sh "$pkgdir/usr/bin/$pkgname"

  # Desktop file and icon
  install -Dm644 ../$pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
  install -Dm644 assets/Menu/logo.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/$pkgname.png"
  install -Dm644 graphics/logo_icon.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/$pkgname.svg"

  # License
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 licenses/* Readme.md \
    "$pkgdir/usr/share/licenses/$pkgname/"
}
