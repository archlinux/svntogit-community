# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-gtk2' 'qtcurve-qt4' 'qtcurve-qt5')
pkgver=1.8.18.git20151017
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('i686' 'x86_64')
url='https://github.com/QtCurve/qtcurve'
license=('LGPL')
groups=('qtcurve')
makedepends=('extra-cmake-modules' 'gtk2' 'qt4' 'qt5-svg' 'qt5-x11extras' 'kdelibs4support' 'kdoctools')
#source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
source=("http://quickgit.kde.org/?p=qtcurve.git&a=snapshot&h=036943575bbe15cc822a1fbca2c96391e5a04fd7&fmt=tgz")
md5sums=('a664ec255a4fb5f77f1ecb57649d3260')

build() {
  cd $pkgbase

  mkdir build
  cd build
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT4_ENABLE_KDE=false
  make
}

package_qtcurve-utils() {
  depends=('libx11' 'gcc-libs')
  cd $pkgbase/build/lib/utils
  make DESTDIR="$pkgdir" install
}

package_qtcurve-gtk2() {
  depends=('qtcurve-utils' 'gtk2')
  cd $pkgbase/build/gtk2
  make DESTDIR="$pkgdir" install
  cd ../lib/cairo
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt4() {
  depends=('qtcurve-utils' 'qt4')
  cd $pkgbase/build/qt4
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt5() {
  depends=('qtcurve-utils' 'qt5-svg' 'qt5-x11extras')
  cd $pkgbase/build/qt5
  make DESTDIR="$pkgdir" install
}
