# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgbase='spyder'
pkgname=('spyder' 'spyder3')
pkgver=3.1.3
pkgrel=1
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
makedepends=('python2-setuptools' 'python-setuptools' 'python-sphinx' 'python2-sphinx')
arch=('any')
url='https://github.com/spyder-ide/spyder/'
license=('MIT')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${pkgbase}/archive/v${pkgver}.tar.gz"
        "python2.patch"
        "python2-pep8.patch"
        "python3-pep8.patch")
sha256sums=('b8d4a8bd6793242be9c5cc6a9f6fd8c4f47f08be4689df15fc2680899ffbf2c3'
            '4fb415fb358371da4a8b9524712de9338d391eed3bdabc5e217cf54f558771b5'
            'dbebf909bf59027d22247e3e3c1736b5ec9337a879358f8117a967bb03e913b2'
            'f4eba4c414e655f0f2797e9864aa199607a6706f6b245aca56a54b250febc382')

package_spyder() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 2.x series.'
  depends=('python2-pyqt5' 'qt5-webkit' 'python2-qtconsole' 'python2-qtpy' 'python2-qtawesome'
           'python2-pyflakes' 'python2-pyzmq' 'python2-pygments' 'python2-psutil' 'python2-pickleshare'
           'ipython2-notebook'
           'python2-rope' 'python2-jedi' 'python2-pylint' 'python2-pycodestyle' 'python2-sphinx'
           'icu' 'gtk-update-icon-cache')
  optdepends=('python2-numpy: N-dimensional arrays'
              'python2-scipy: signal/image processing'
              'python2-h5py: HDF5 support'
              'python2-matplotlib: interactive 2D/3D data plotting'
              'python2-sympy: symbolic mathematics for the IPython console'
              'python2-pandas: view and edit DataFrames and Series in the Variable Explorer')
  [ -d "build" ] && rm -rf "build"
  cp -r "${pkgbase}-${pkgver}" build
  cd build
  patch -p1 -i "../python2.patch"
  patch -p1 -i "../python2-pep8.patch"
  python2 setup.py build
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyder/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}

package_spyder3() {
  pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features for Python 3.x series.'
  depends=('python-pyqt5' 'qt5-webkit' 'python-qtconsole' 'python-qtpy' 'python-qtawesome'
           'python-pyflakes' 'python-pyzmq' 'python-pygments' 'python-psutil' 'python-pickleshare'
           'jupyter' 'jupyter-nbconvert'
           'python-rope' 'python-jedi' 'python-pylint' 'python-pycodestyle' 'python-sphinx'
           'icu' 'gtk-update-icon-cache')
  optdepends=('python-numpy: N-dimensional arrays'
              'python-scipy: signal/image processing'
              'python-h5py: HDF5 support'
              'python-matplotlib: interactive 2D/3D data plotting'
              'python-sympy: symbolic mathematics for the IPython console'
              'python-pandas: view and edit DataFrames and Series in the Variable Explorer')
  [ -d "build" ] && rm -rf "build"
  cp -r "${pkgbase}-${pkgver}" build
  cd build
  patch -p1 -i "../python3-pep8.patch"
  python3 setup.py build
  python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # Install icon and desktop file
  install -D -m644 "scripts/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "spyder/images/spyder.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}/usr/bin/spyder_win_post_install.py"
}
