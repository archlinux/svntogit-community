# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Robin Candau <antiz@archlinux.org>

pkgname=dino
pkgver=0.4.2
pkgrel=2
pkgdesc="Modern XMPP (Jabber) chat client written in Vala"
arch=(x86_64)
url="https://dino.im"
license=(GPL3)
depends=(glib2 glib-networking gtk4 libadwaita gpgme libgee libgcrypt libsignal-protocol-c
         libsoup sqlite qrencode gspell libnice gstreamer gst-plugin-gtk
         gst-plugins-base gst-plugins-good libsrtp webrtc-audio-processing)
makedepends=(git cmake vala ninja)
source=(https://github.com/dino/dino/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('e1b1527028073b95c3abec41cb02ec60c1ee0a76da2c57bf2456afb8a3cd7eed'
            'SKIP')
validpgpkeys=(A32134DD19952FB12EC43E4501FABCC83FEA225E)

build() {
  cmake -G Ninja -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -Wno-dev
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}
