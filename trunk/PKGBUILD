# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-jacktools
pkgname=python-zita-jacktools
pkgver=1.6.0
pkgrel=1
pkgdesc="Audio tools running as a Jack client"
arch=(x86_64)
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=(GPL3)
depends=(gcc-libs glibc python-zita-audiotools)
makedepends=(fftw jack libsndfile python-build python-installer
python-setuptools python-wheel zita-convolver zita-jclient zita-resampler)
options=(debug)
source=(https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$_name-$pkgver.tar.bz2)
sha512sums=('c64bc5f44ce2ae6afa4100124bdff1a8f2f6295eaf07267210682d16c191c2651432df8ac8910a456150eb4521a23f20475cb2b5a71d10a6fe54d6d962ad63ab')
b2sums=('2b0f720f3dab8c4177408a7294cb1a4f1a742430b96dabdf17ada0c0293bd6ace15f307046c1b542e9c907764ab7fa4b94a0037e27d7c0e22d93add6504d72cc')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  depends+=(libfftw3f.so libjack.so libsndfile.so libzita-convolver.so
  libzita-jclient.so libzita-resampler.so)

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # examples
  find examples -type f \( -iname "*.py" -or -iname "*README" -or -iname "*presets" \) -exec install -vDm 644 {} "$pkgdir/usr/share/doc/$pkgname/"{} \;
}
