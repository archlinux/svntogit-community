# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-1
_patchver=2
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=2
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
        https://github.com/Singular/Sources/commit/4c1e7702.patch)
#source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
sha256sums=('8e1f67c0a940ae772231dc5f700ab2a1b08199be26d33323558fd08d629ea91f'
            '7948ca7ed8be45043d7afcc85da1a5efcedc11f91a09bbe6d086d398c6aff742')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  patch -Rp1 -i ../4c1e7702.patch # Revert commit that causes regresions in sagemath https://trac.sagemath.org/ticket/24735
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info
}
