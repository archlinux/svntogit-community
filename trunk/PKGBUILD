# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=aeson-diff
pkgname=haskell-aeson-diff
pkgver=1.1.0.13
pkgrel=27
pkgdesc="Extract and apply patches to JSON documents"
url="https://github.com/ysangkok/aeson-diff"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-edit-distance-vector' 'haskell-scientific'
         'haskell-vector' 'haskell-optparse-applicative' 'haskell-yaml')
makedepends=('ghc' 'haskell-glob' 'haskell-quickcheck' 'haskell-cabal-doctest' 'haskell-doctest'
             'hlint')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('78ae5da41f4962871bc3dd0a2cc79abf4964e639eb7cdb5b5cb65f5a4dfabeddd9c3fc9fe4446f2ae1b3dde1fe888091205540a852da451e76743efee07febe2')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
