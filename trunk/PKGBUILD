# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-pyftpdlib
pkgname=('python-pyftpdlib' 'python2-pyftpdlib')
pkgver=1.5.2
pkgrel=1
pkgdesc='Very fast asynchronous FTP server library'
arch=('any')
url='https://github.com/giampaolo/pyftpdlib/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-pyopenssl' 'python2-pyopenssl')
source=("https://pypi.io/packages/source/p/pyftpdlib/pyftpdlib-$pkgver.tar.gz")
sha512sums=('b458518eb7a57137d5f677bb4f38f4285f5efed4d8c0b4919c78b42350579407664219f4f289a0d6f307bb33869b73033ee82ad2ae777dc9f5a6d38a1af09a2a')

prepare() {
  cp -a pyftpdlib-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py build

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyftpdlib-$pkgver
  python setup.py ptr || warning "Tests failed"

  cd "$srcdir"/pyftpdlib-$pkgver-py2
  python2 setup.py ptr || warning "Tests failed"
}

package_python-pyftpdlib() {
  depends=('python')
  optdepends=('python-pyopenssl: FTPS support')

  cd pyftpdlib-$pkgver
  python setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyftpdlib() {
  depends=('python2')
  optdepends=('python2-pyopenssl: FTPS support')

  cd pyftpdlib-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/ftpbench{,2}
}
