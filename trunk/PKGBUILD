# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=ser2net
pkgver=3.5
pkgrel=1
pkgdesc="A proxy that allows telnet/tcp connections to be made to serial ports"
arch=(x86_64)
url="https://sourceforge.net/projects/ser2net"
license=('GPL')
depends=('glibc')
backup=(etc/ser2net.conf)
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
	ser2net.service)
sha256sums=('ba9e1d60a89fd7ed075553b4a2074352902203f7fbd9b65b15048c05f0e3f3be'
            'b093e1f5bf7dd826816828623a5c6858f83a9f0dcb1a9785f545b29efd1c320c')

build() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir"/${pkgname}-${pkgver/rc/-rc}
  make DESTDIR="$pkgdir" install
  install -Dm0644 ser2net.conf "$pkgdir"/etc/ser2net.conf
  install -Dm0644 "$srcdir"/ser2net.service "$pkgdir"/usr/lib/systemd/system/ser2net.service
}
