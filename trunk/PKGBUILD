# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=vamp-aubio-plugins
pkgver=0.5.1
pkgrel=1
pkgdesc="Onset detection, pitch tracking, note tracking and tempo tracking plugins using Paul Brossier's aubio library"
arch=('x86_64')
url="https://code.soundsoftware.ac.uk/projects/vamp-aubio-plugins"
license=('GPL2')
depends=('vamp-plugin-sdk' 'aubio')
makedepends=('python2')
source=("http://aubio.org/pub/vamp-aubio-plugins/vamp-aubio-plugins-$pkgver.tar.bz2" vamp-aubio-i686.patch)
sha256sums=('3af69df7e1b1ae412abcea692523fed9f5a1d5a3fe2785b4af581018e179ecbe'
            '1b09949d2d2c4ab71e363ecd0f216ad9a780a8d0a10d8049026c606da15f70f1')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../vamp-aubio-i686.patch # Fix i686 build
}

build() {
  cd $pkgname-$pkgver
  python2 waf configure
  python2 waf build -v
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 build/vamp-aubio.so "$pkgdir"/usr/lib/vamp/vamp-aubio.so
  install -m644 vamp-aubio.cat vamp-aubio.n3 -t "$pkgdir"/usr/lib/vamp/
}
