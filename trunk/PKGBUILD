# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=('lua-say' 'lua51-say' 'lua52-say' 'lua53-say')
pkgver=1.4.0
pkgrel=1
_rockrel=1
pkgdesc='Lua string hashing library, useful for internationalization'
arch=('any')
url='https://github.com/lunarmodules/say'
license=('MIT')
makedepends=('luarocks'
             # https://github.com/luarocks/luarocks/issues/1275
             'lua51'
             'lua52'
             'lua53'
             'lua')
_archive="say-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('b81e7519ebdff47e1aee69b2a53eda201824cdd7a15e743ff322b8e4fde350cb')

build() {
    cd "$_archive"
    for v in 5.1 5.2 5.3 5.4; do
        mkdir -p "$v/"
        luarocks make --pack-binary-rock --lua-version="$v" --deps-mode=none rockspecs/$_archive-$_rockrel.rockspec
        mv "$_archive-$_rockrel".*.rock "$v/"
    done
}

_package() {
    pkgdesc+=" for Lua $1"
    cd "$_archive"
    luarocks install --lua-version="$1" --tree="$pkgdir/usr/" --deps-mode=none --no-manifest "$1"/*.rock
    install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua51-say() {
    _package 5.1
}

package_lua52-say() {
    _package 5.2
}

package_lua53-say() {
    _package 5.3
}

package_lua-say() {
    _package 5.4
}
