# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: AUR Perl <aurperl@juster.info>
# Generator  : CPANPLUS::Dist::Arch 1.15

pkgname=perl-yaml-tiny
pkgver=1.74
pkgrel=1
pkgdesc="Read/Write YAML files with as little code as possible"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-pod-coverage')
url='https://search.cpan.org/dist/YAML-Tiny'
source=("https://search.cpan.org/CPAN/authors/id/E/ET/ETHER/YAML-Tiny-$pkgver.tar.gz")
sha512sums=('2da59bf583b09a60e8e1bac7c21fc4300ae3ee2092ffd77cbf3778c65c7d0bdf68fa3616665d276f4d4df2437526d36d497ac53be79baa182f84f37640bfcad4')

build() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
}

check() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/YAML-Tiny-${pkgver}"
  make install DESTDIR="${pkgdir}"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# vim:set ts=2 sw=2 et:
