# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Nishit Joseph (reachjlight at gmail dot com)
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgbase=python-openpyxl
pkgname=('python2-openpyxl' 'python-openpyxl')
pkgver=2.5.6
_pkgtag='260d5b0b9caf'
pkgrel=1
pkgdesc="A Python library to read/write Excel 2007 xlsx/xlsm files"
arch=('any')
url="http://openpyxl.readthedocs.org/"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python-jdcal' 'python2-jdcal' 'python-et-xmlfile' 'python2-et-xmlfile')
source=("https://bitbucket.org/openpyxl/openpyxl/get/${pkgver}.tar.bz2"
        "LICENCE")
md5sums=('cfa9e29ec4246e0f34144ccb66ef98f0'
         'c8afe530744e932b892358e6eb5bea9b')

prepare() {
  cd "$srcdir"
  cp -a openpyxl-openpyxl-${_pkgtag} openpyxl-openpyxl-py2-${_pkgtag}
  cd openpyxl-openpyxl-py2-${_pkgtag}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  msg "Building Python2"
  cd "$srcdir"/openpyxl-openpyxl-py2-${_pkgtag}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/openpyxl-openpyxl-${_pkgtag}
  python setup.py build
}

package_python2-openpyxl() {
  depends=('python2-jdcal' 'python2-et-xmlfile')
  optdepends=('python2-pillow: needed to include images')

  cd "$srcdir"/openpyxl-openpyxl-py2-${_pkgtag}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

package_python-openpyxl() {
  depends=('python-jdcal' 'python-et-xmlfile')
  optdepends=('python-pillow: needed to include images')

  cd "$srcdir"/openpyxl-openpyxl-${_pkgtag}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

