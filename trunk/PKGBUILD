# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-file-mmagic
pkgver=1.30
pkgrel=5
pkgdesc="Guess file type"
arch=(any)
license=('PerlArtistic')
url="https://search.cpan.org/dist/File-MMagic"
depends=('perl')
options=(!emptydirs)
source=("https://search.cpan.org/CPAN/authors/id/K/KN/KNOK/File-MMagic-${pkgver}.tar.gz")
sha256sums=('cf0c1b1eb29705c02d97c2913648009c0be42ce93ec24b36c696bf2d4f5ebd7e')

build() {
  cd "${srcdir}/File-MMagic-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/File-MMagic-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
