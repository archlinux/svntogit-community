# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=httptools
pkgname=python-$_pkgname
pkgver=0.2.0
pkgrel=1
pkgdesc='Fast HTTP parser'
arch=('x86_64')
url='https://github.com/MagicStack/httptools'
license=('MIT')
depends=('python' 'http-parser')
makedepends=('git' 'python-setuptools' 'python-wheel' 'cython')
#source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
source=("git+$url#tag=v$pkgver"
        'git+https://github.com/nodejs/llhttp.git')
sha512sums=('SKIP'
            'SKIP')

prepare() {
  #cd $_pkgname-$pkgver
  cd $_pkgname

  sed -i 's|CYTHON_DEPENDENCY =.*|CYTHON_DEPENDENCY = "Cython"|' setup.py
  sed -i 's|../../vendor/http-parser/http_parser.h|/usr/include/http_parser.h|' $_pkgname/parser/cparser.pxd

  git rm vendor/http-parser

  git submodule init
  git config submodule.vendor/llhttp.url "$srcdir"/llhttp
  git submodule update
}

build() {
  #cd $_pkgname-$pkgver
  cd $_pkgname

  python setup.py build build_ext --use-system-http-parser
}

package() {
  #cd $_pkgname-$pkgver
  cd $_pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
