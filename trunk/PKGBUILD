# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinxu.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>
# Contributor: Valentine Sinitsyn <e_val@inbox.ru>
# Contributor: Ryszard Knop <dragoon@dragonic.eu>

pkgname=lib32-libnm
pkgver=1.42.2
pkgrel=1
pkgdesc="NetworkManager client library (32-bit)"
url="https://networkmanager.dev/"
arch=(x86_64)
license=(LGPL)
depends=(
  lib32-gcc-libs
  lib32-glib2
  lib32-nspr
  lib32-nss
  lib32-systemd
  lib32-util-linux
  libnm
)
makedepends=(
  git
  lib32-dbus
  lib32-jansson
  lib32-libndp
  lib32-libpsl
  lib32-polkit
  libxslt
  meson
)
provides=(libnm.so)
_commit=58928a3188f54f4f09f9945b3737d0c996e3643f  # tags/1.42.2^0
source=("git+https://gitlab.freedesktop.org/NetworkManager/NetworkManager.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd NetworkManager
  git describe --tags | sed 's/-dev/dev/;s/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd NetworkManager
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  local meson_options=(
    # system paths
    --libdir=/usr/lib32
    -D dbus_conf_dir=/usr/share/dbus-1/system.d

    # platform
    -D dist_version="$pkgver-$pkgrel"
    -D libaudit=no
    -D session_tracking_consolekit=false
    -D suspend_resume=systemd
    -D modify_system=true
    -D selinux=false

    # features
    -D bluez5_dun=false
    -D concheck=false
    -D ebpf=false
    -D iwd=false
    -D modem_manager=false
    -D nm_cloud_setup=false
    -D nmcli=false
    -D nmtui=false
    -D ovs=false
    -D ppp=false
    -D teamdctl=false

    # configuration plugins
    -D config_plugins_default=keyfile
    -D ifcfg_rh=false
    -D ifupdown=false

    # handlers for resolv.conf
    -D netconfig=no
    -D config_dns_rc_manager_default=symlink

    # miscellaneous
    -D vapi=false
    -D docs=false
    -D introspection=false
    -D tests=no
    -D more_asserts=no
    -D more_logging=false
    -D qt=false
  )

  arch-meson NetworkManager build "${meson_options[@]}"
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  rm -r "${pkgdir}"/{etc,usr/{bin,include,lib,lib32/NetworkManager,share},var}
}

# vim:set sw=2 sts=-1 et:
