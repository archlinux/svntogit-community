# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor:  Yangtse Su<yangtsesu@gmail.com>
# Contributor:  Jekyll Wu<adaptee [at] gmail [dot] com>
# Contributor: riverscn<riverscn at gmail.com>
# Contributor: rainy<rainylau at gmail.com>
# Contributor: Lee.MaRS<leemars at gmail.com>

pkgname=ibus-libpinyin
pkgver=1.11.1
pkgrel=5
pkgdesc="Intelligent Pinyin engine based on libpinyin for IBus"
arch=('x86_64')
license=('LGPL')
url="https://github.com/libpinyin/ibus-libpinyin"
depends=('ibus' 'libpinyin' 'lua53' 'opencc' 'python-xdg')
makedepends=('boost' 'git' 'gnome-common' 'intltool')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libpinyin/ibus-libpinyin/archive/$pkgver.tar.gz")
sha512sums=('48691952a10f4ab545d8e233dc3cd75fc54270c03a687820048152636df26d233ee0fba89e4ec2ccd5a6e410b41e556872e3dd2468d59d44b3440e904b93528b')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/lua5.1/lua5.3/' configure.ac
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib/$pkgname --enable-boost --enable-opencc
  make
}

package() {
  cd $pkgname-$pkgver
  make NO_INDEX=true DESTDIR="$pkgdir" install
}
