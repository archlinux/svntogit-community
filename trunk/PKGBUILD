# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: Aurélien Chabot <contact@aurelienchabot.fr>
# Contributor: Étienne Deparis <etienne@depar.is>

pkgname=progress
pkgdesc="Shows running coreutils basic commands and displays stats"
pkgver=0.16
pkgrel=1
arch=('x86_64')
license=('GPL3')
url="https://github.com/Xfennec/progress"
depends=('ncurses')
replaces=('cv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Xfennec/progress/archive/v$pkgver.tar.gz")
sha512sums=('11fef1b973e899f4b605513f341cfc412c7c62b138d2b87cfb8b57fe7c46449cdac1e1b5c1cdc77d7cd905a26e5cf85c4ff88d892c58c8c839e95cab07596eb3')

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX="/usr" install
}
