# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Current Maintainer:  Patrick Palka <patrick@parcs.ath.cx>
# Previous Maintainer:  Loui Chang <louipc.ist at gmail com>
# Contributor: Andreas Baumann <abaumann at yahoo dot com>
# Development: http://closure.ath.cx/aur-dev

pkgname=cgit
pkgver=0.9
pkgrel=2
pkgdesc="A web interface for git written in plain C"
arch=('i686' 'x86_64')
url="http://hjemli.net/git/cgit"
license=('GPL')
depends=('openssl')
makedepends=('git' 'zlib' 'curl' 'asciidoc')
install=cgit.install
source=("http://hjemli.net/git/cgit/snapshot/$pkgname-$pkgver.tar.bz2")
md5sums=('b55f953c5c8a418bca72b3de3c297270')

build() {
  cd "$pkgname-$pkgver"

  make get-git
  make
  make doc-man
}

package() {
  cd "$pkgname-$pkgver"

  make CGIT_SCRIPT_PATH=/srv/http/cgit DESTDIR="$pkgdir" install install-man
  install -vDm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -vd "$pkgdir/var/cache/$pkgname"
}

md5sums=('b55f953c5c8a418bca72b3de3c297270')
