# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: red_over_blue
# Contributor: neri

pkgname=povray
pkgver=3.7.0.9
_v=${pkgver%.*.*} # 3.7
pkgrel=2
epoch=2
pkgdesc='Script based raytracer for creating 3D graphics'
arch=('x86_64')
license=('AGPL3')
url='http://povray.org/' # bad https cert
depends=('boost-libs' 'libpng' 'libtiff' 'openexr')
makedepends=('boost' 'git' 'glu')
backup=("etc/povray/$_v/povray.conf"
        "etc/povray/$_v/povray.ini")
source=("$pkgname-$pkgver.tar.gz::https://github.com/POV-Ray/povray/archive/v$pkgver.tar.gz"
         povray-openexr3.patch)
sha512sums=('14c5f6b477721afe14d0ee7c5b8945ade4e74eb1fcfc36e7113d1280b8bb71d08cc214a95377e98d00bacf74fb344a4756257c677c7a3cd943ad1f31c3bac6c4'
            '1af541eea5e6371c5c9fb64b622be44b1f3eeb12c0a28280116da10eff50d1010bd3ea738f2d81c394e7aed7bc37fedb106bd70b435965c761ae50c2454eee6e')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < "$srcdir"/povray-openexr3.patch # Fix build with OpenEXR 3

  cd unix
  sed 's/automake --w/automake --add-missing --w/g' -i prebuild.sh
  sed 's/dist-bzip2/dist-bzip2 subdir-objects/g' -i configure.ac
  ./prebuild.sh

  cd ..
  ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    LIBS="-lboost_system -lboost_thread" \
    COMPILED_BY='Arch Linux' \
    --sysconfdir=/etc \
    --prefix=/usr

  make CXXFLAGS+="-std=c++11 -lboost_system -lboost_thread -w"
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir"/usr/share/{doc/,}"$pkgname-$_v"
  cp -r icons include ini scenes scripts "$pkgdir/usr/share/$pkgname-$_v"
  cp -r doc "$pkgdir/usr/share/doc/$pkgname-$_v"
  install -Dm755 unix/povray "$pkgdir/usr/bin/povray"
  install -Dm644 povray.conf "$pkgdir/etc/povray/$_v/povray.conf"
  install -Dm644 povray.ini "$pkgdir/etc/povray/$_v/povray.ini"
  install -Dm644 povray.1 "$pkgdir/usr/share/man/man1/povray.1"
}

# getver: povray.org/documentation
# vim:set ts=2 sw=2 et:
