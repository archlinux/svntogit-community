# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jeffrey Zhang <zhang.lei.fly@gmail.com>

_name=esbonio
pkgname=python-esbonio
pkgver=0.15.0
pkgrel=1
pkgdesc='Language server for working with Sphinx projects'
arch=('any')
url=https://github.com/swyddfa/esbonio
license=('MIT')
depends=('python-appdirs' 'python-sphinx' 'python-pygls' 'python-pyspellchecker'
         'python-setuptools' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
#checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('1ebddcc9683ecd619e377e84ef232f529de5bef2af49ff295d1826a8afd0d32e')
b2sums=('b1558fa544d90df40e91bccb076cf2bda446e226b999bc6536a403e8d2a6b21c8ed5928b8b05585e4436788145aaff654411eac42f91acdc6130c21916374370')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

#check() {
#  cd $_name-$pkgver
#  pytest
#}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
