# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-vector
pkgver=0.11.0.0
pkgrel=6
pkgdesc="Efficient Arrays"
url="http://hackage.haskell.org/package/vector"
license=("custom:BSD3")
arch=('i686' 'x86_64')
makedepends=()
# deps adjusted to specific versions in package()
depends=("ghc" "haskell-primitive")
source=("http://hackage.haskell.org/packages/archive/vector/${pkgver}/vector-${pkgver}.tar.gz")
md5sums=('a08605271abb3ed815135572a42b8524')

prepare() {
  sed -e 's/base >= 4.3 && < 4.9/base >= 4.3 \&\& < 4.10/' \
      -e 's/ghc-prim >= 0.2 && < 0.5/ghc-prim >= 0.2 \&\& < 0.6/' \
      -i vector-${pkgver}/vector.cabal
}

build() {
  cd "$srcdir"/vector-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
  depends=("ghc=${_ghcver}" "haskell-primitive")

  cd "$srcdir"/vector-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm755 "$pkgdir"/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html "$pkgdir"/usr/share/doc/ghc/html/libraries/vector
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE
}
