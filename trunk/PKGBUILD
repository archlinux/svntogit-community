# $Id$
# Maintainer: Martin Wimpress <code@flexion.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: kozec <kozec at kozec dot com>

pkgname=syncthing-gtk
pkgver=0.9.2.7
pkgrel=1
epoch=1
pkgdesc='GTK3 based GUI and notification area icon for Syncthing'
arch=('any')
url='https://github.com/syncthing/syncthing-gtk'
license=('GPL2')
depends=('syncthing>=0.13' 'gtk3' 'libnotify' 'python2-pyinotify'
         'python2-bcrypt' 'python2-cairo' 'python2-dateutil' 'python2-gobject')
makedepends=('python2-setuptools')
optdepends=('python2-nautilus: file manager integration for Nautilus'
            'python2-caja: file manager integration for Caja'
            'librsvg: to shown menu icons correctly if not using traditional window header')
source=($pkgname-$pkgver.tar.gz::https://github.com/syncthing/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('a500665ce2ebd04cf0f51e0b65690dddc258bc495a74b6311604fefe9f3e16ef')

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
