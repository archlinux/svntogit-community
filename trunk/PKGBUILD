# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=openmotif
pkgver=2.3.3
pkgrel=2
pkgdesc="Open Motif"
arch=('i686' 'x86_64')
url="http://www.motifzone.org/"
license=('GPL')
depends=(libxext libxp libxft libxt libxmu libpng libjpeg)
makedepends=(xbitmaps xproto printproto xextproto automake)
conflicts=(lesstif)
options=('!libtool')
#source=(http://www.motifzone.org/files/public_downloads/openmotif/2.3/$pkgver/openmotif-$pkgver.tar.gz)
source=(ftp://ftp.ics.com/openmotif/2.3/$pkgver/openmotif-$pkgver.tar.gz)
md5sums=('fd27cd3369d6c7d5ef79eccba524f7be')

build() {
  _automakever=1.11

  cd $srcdir/openmotif-$pkgver

  sed -i 's|.*demos.*||g' Makefile.in Makefile.am
  sed -i 's|doc \\|doc|g' Makefile.in Makefile.am
  sed -i 's#if (!png_check_sig(sig, 8))#if (png_sig_cmp(sig, 0, 8))#' lib/Xm/Png.c

  ln -s -f /usr/share/automake-${_automakever}/compile compile
  ln -s -f /usr/share/libtool/config/config.guess config.guess
  ln -s -f /usr/share/libtool/config/config.sub config.sub
  ln -s -f /usr/share/automake-${_automakever}/depcomp depcomp
  ln -s -f /usr/share/automake-${_automakever}/install-sh install-sh
  ln -s -f /usr/share/automake-${_automakever}/missing missing
  ln -s -f /usr/share/libtool/config/ltmain.sh ltmain.sh
  touch NEWS AUTHORS

  [ $NOEXTRACT -eq 1 ] || autoreconf -i
  [ $CARCH == "x86_64" ] && export CFLAGS="$CFLAGS -fPIC"
  [ $NOEXTRACT -eq 1 ] || ./configure --with-x --prefix=/usr
  make -j1

  make DESTDIR=$pkgdir install
  # cleanup
  (cd $pkgdir/usr/share/man/man3/ && mv Shell.3 OpenMotif-Shell.3)
}
