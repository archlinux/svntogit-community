# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Jonas Heinrich <onny@project-insnaity.org>
# Contributor: Lee Watson <aur@revthefox.co.uk>
# Contributor: Tobias Hunger <tobias DOT hunger AT gmail DOT com>
# Contributor: Stefan Tatschner <stefan@sevenbyte.org>

pkgname=gitlab-shell
pkgver=5.0.6
pkgrel=1
epoch=1
pkgdesc="Git management software"
arch=('any')
url="https://gitlab.com/gitlab-org/gitlab-shell/"
license=('MIT')
depends=('ruby2.3' 'redis' 'git')
options=('!strip')
install=gitlab-shell.install
backup=("etc/webapps/${pkgname}/config.yml"
        "etc/webapps/${pkgname}/secret"
        "etc/webapps/${pkgname}/gitconfig"
        "etc/webapps/${pkgname}/authorized_keys"
        "etc/webapps/${pkgname}/")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitlab-shell/repository/archive.tar.gz?ref=v${pkgver}"
        "gitlab-shell.gitconfig")
md5sums=('691d43206a641e0da2d7bbb1d373c39b'
         '6b917256b13a1685e9a3cc73e68152d0')

_datadir="/usr/share/webapps/${pkgname}"
_etcdir="/etc/webapps/${pkgname}"
_homedir="/var/lib/gitlab"
_logdir="/var/log/gitlab"
_srcdir="gitlab-shell-v${pkgver}"

prepare() {
  cd "${srcdir}/${_srcdir}"-*

  sed -e "s|user: git|user: gitlab|" \
      -e "s|/home/git|${_homedir}|" \
      -e "s|# log_file: .*|log_file: \"${_logdir}/gitlab-shell.log\"|" \
      -e "s|# host:|host:|" \
      -e "s|# port:|port:|" \
      -e "s|socket:|# socket:|" \
      config.yml.example > config.yml
}

package() {
  cd "${srcdir}/${_srcdir}"-*

  install -dm755 -o root -g root "${pkgdir}${_datadir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}"
  install -dm750 -o 105 -g 105 "${pkgdir}${_homedir}/satellites"
  install -dm700 -o 105 -g 105 "${pkgdir}${_homedir}/.ssh"
  install -dm750 -o 105 -g 105 "${pkgdir}${_etcdir}"
  install -dm770 -o 105 -g root "${pkgdir}${_logdir}"

  # .gitconfig
  install -Dm644 -o 105 -g 105 "${srcdir}/gitlab-shell.gitconfig" "${pkgdir}${_etcdir}/.gitconfig"
  ln -fs "${_etcdir}/gitconfig" "${pkgdir}${_homedir}/.gitconfig"

  # .gitlab_shell_secret
  touch "${pkgdir}${_etcdir}/secret"
  chmod 640 "${pkgdir}${_etcdir}/secret"
  chown root:105 "${pkgdir}${_etcdir}/secret"
  ln -fs "${_etcdir}/secret" "${pkgdir}${_datadir}/.gitlab_shell_secret"

  # config.yml
  mv config.yml "${pkgdir}${_etcdir}"
  ln -fs "${_etcdir}/config.yml" "${pkgdir}${_datadir}/config.yml"

  cp -r VERSION bin hooks lib spec support "${pkgdir}${_datadir}"
  ln -fs "${_datadir}" "${pkgdir}/${_homedir}/"

  install -dm2770 -o 105 -g 105 "${pkgdir}${_homedir}/repositories"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

  for file in ${pkgdir}/usr/share/webapps/gitlab-shell/{bin,hooks}/*; do
    sed -i s/ruby/ruby-2.3/g ${file}
  done
}

# vim:set ts=2 sw=2 et:
