# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-glanceclient
pkgname=(python-glanceclient python2-glanceclient)
pkgver=2.5.0
pkgrel=2
pkgdesc="OpenStack Image API Client Library"
arch=('any')
url="http://docs.openstack.org/developer/python-glanceclient"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-babel' 'python2-babel' 'python-prettytable' 'python2-prettytable'
             'python-keystoneclient' 'python2-keystoneclient' 'python-requests'
             'python2-requests' 'python-warlock' 'python2-warlock' 'python-oslo-utils'
             'python2-oslo-utils' 'python-oslo-i18n' 'python2-oslo-i18n')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock'
              'python2-requests-mock')
options=('!emptydirs')
source=("git+https://git.openstack.org/openstack/python-glanceclient#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a python-glanceclient{,-py2}
}

build() {
  cd "$srcdir"/python-glanceclient
  python setup.py build

  cd "$srcdir"/python-glanceclient-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-glanceclient
  python setup.py testr

  cd "$srcdir"/python-glanceclient-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-glanceclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
           'python-requests' 'python-warlock' 'python-oslo-utils' 'python-oslo-i18n')

  cd "$srcdir"/python-glanceclient
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-glanceclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-prettytable' 'python2-keystoneclient'
           'python2-requests' 'python2-warlock' 'python2-oslo-utils' 'python2-oslo-i18n')

  cd "$srcdir"/python-glanceclient-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mv "$pkgdir"/usr/bin/glance{,2}
}

# vim:set ts=2 sw=2 et:
