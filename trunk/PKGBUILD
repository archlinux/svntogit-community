# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=xtrabackup
pkgver=2.4.6
_boost_ver=1.59.0
pkgrel=2
pkgdesc='Non-blocking backup tool for MySQL'
arch=('i686' 'x86_64')
url='http://www.percona.com/software/percona-xtrabackup/'
license=('GPL')
depends=('libaio' 'libev' 'libgcrypt' 'curl' 'perl-dbd-mysql')
makedepends=('cmake' 'python-sphinx' 'zlib' 'vim' 'libedit' 'libevent')
optdepends=('qpress: for compressed backups')
source=(https://www.percona.com/downloads/XtraBackup/Percona-XtraBackup-$pkgver/source/tarball/percona-xtrabackup-$pkgver.tar.gz
        http://sourceforge.net/projects/boost/files/boost/${_boost_ver}/boost_${_boost_ver//./_}.tar.gz)
sha256sums=('1e21ab097550901d8f2fa3dc37402ba6a994afa0722760f8f19cb369565e5e8b'
            '47f11c8844e579d02691a607fbd32540104a9ac7a2534a8ddaef50daf502baac')
build() {
  cd percona-xtrabackup-$pkgver
  cmake -DBUILD_CONFIG=xtrabackup_release \
        -DBUILD_TESTING=OFF \
        -DCMAKE_BUILD_TYPE=Release \
        -DMYSQL_DATADIR=/var/lib/mysql \
        -DMYSQL_UNIX_ADDR=/run/mysqld/mysqld.sock \
        -DWITH_LIBEVENT=system \
        -DWITH_EDITLINE=bundled \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_BOOST="../boost_${_boost_ver//./_}"
  make
}

package() {
  cd percona-xtrabackup-$pkgver
  make DESTDIR="$pkgdir" install

  cd  "$pkgdir"
  rm -rf usr/xtrabackup-test
  install -dm755 usr/share
  mv build/$pkgname/src/percona-xtrabackup-$pkgver/man usr/share
  rm -rf build
}
