# Maintainer: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Olivier Médoc <o_medoc@yahoo.fr>
# Contributor: Ashok `ScriptDevil` Gautham <ScriptDevil.Arch@gmail.com>

pkgname=libdbi-drivers
pkgver=0.8.3_1
_pkgver=0.8.3-1
pkgrel=2
pkgdesc="Database drivers for libdbi (MySQL, PostgreSQL, and SQLite)"
url="http://libdbi-drivers.sourceforge.net/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('libdbi')
makedepends=('postgresql-libs' 'libmysqlclient' 'sqlite2' 'sqlite3')
optdepends=("postgresql-libs: To work with postgresql using libdbdpgsql "
			"sqlite3: To work with sqlite3 using libdbdsqlite3"
			"libmysqlclient: To work with mysql using libdbdmysql"
			"sqlite2: To work with sqlite2 using libdbdsqlite")
options=(!libtool)
install=libdbi-drivers.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}/${pkgname}-${_pkgver}/${pkgname}-${_pkgver}.tar.gz")
md5sums=('4de79b323162a5a7652b65b608eca6cd')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  ./configure --prefix=/usr \
  	--disable-docs \
  	--with-libdbi \
	--with-mysql \
	--with-pgsql \
	--with-sqlite \
	--with-sqlite3
  make
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR=${pkgdir} install
}

