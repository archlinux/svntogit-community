# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.2.6
pkgrel=2
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3c59f8fad572d7d6f38d6885c876995811a61690b3f2d8626ec13036fb462bf1db03df6530ecd21f55c271f3fc97dd2604dd09215291e42eaccfac2f6809d2f0')
b2sums=('5bfe7eab8658a469f0952c817d14fa1800c7672e54f4a07ac28bc8472399443241cf36d34a07f0072eaf9c9ab6598d7fe82969f49d40cdaa12325d7fed6b425c')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 kube-linter -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
