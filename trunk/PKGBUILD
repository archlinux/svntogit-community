# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mitch Bigelow <ipha00@gmail.com>

pkgname=pixiewps
pkgver=1.3
pkgrel=1
pkgdesc='Offline bruteforce of the WPS pin exploiting the low or non-existing entropy of some APs'
url='https://github.com/wiire/pixiewps'
arch=('x86_64')
license=('GPL3')
depends=('glibc')
optdepends=('reaver: authentication session key support'
            'bully: authentication session key support')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wiire-a/pixiewps/archive/v${pkgver}.tar.gz)
sha512sums=('535e1b5de0e6db8c23062fd867236e710778a5d3f4bfcf20eb24476cd51b20e1024f889558db65e200611970d5f20d53d334cfb6d0b552d5b4ae0270dfc2bbf8')

build() {
  cd ${pkgname}-${pkgver}
  make -C src
}

package() {
  cd ${pkgname}-${pkgver}
  make -C src DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 pixiewps.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
