# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=5.0.1
pkgrel=98
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        '0001-linux-5.6.patch')
sha512sums=('f79b4db917cce2611bd6964d00ae0e162fc500fa7ca76a987145456a9ee81296c776d2b83cf6492a4224c4e4fd95df3ad95a25c1c14d2d4e6865f5bbd639be14'
            '5ddbd2d968bc5b01dbc99648cec81ea072675d7fe7198835649d5b581997945d32bd842dd7ebf60185aa84722b94575ac7aeb5e4616945e22fae53272907305b')

prepare() {
  cd deepin-anything-$pkgver
  patch -Np1 < ../0001-linux-5.6.patch
}

build() {
  cd deepin-anything-$pkgver
  make -C kernelmod kdir=/usr/src/linux
}

package() {
  depends=('linux')

  cd deepin-anything-$pkgver
  local extradir=/usr/lib/modules/$(</usr/src/linux/version)/extramodules
  install -Dt "$pkgdir$extradir" -m644 kernelmod/*.ko
  find "$pkgdir" -name '*.ko' -exec strip --strip-debug {} +
  find "$pkgdir" -name '*.ko' -exec xz {} +
}
