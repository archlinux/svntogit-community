# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.dot.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=proj
pkgver=5.1.0
pkgrel=1
pkgdesc='Cartographic Projections library'
arch=('x86_64')
url="http://trac.osgeo.org/proj/"
license=('MIT')
makedepends=('autoconf')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/OSGeo/proj.4/archive/$pkgver.tar.gz
        http://download.osgeo.org/proj/proj-datumgrid-1.7.zip)
sha256sums=('823becbbfce80a6a53ae9290008e38634c46261d6aa504b9f48ac92a147f39f4'
            'ffcad4453fac3b4a13ca678ef343e688496e18019f35709ef03c5f200e9fb85a')

prepare() {
  bsdtar -xzvf "${srcdir}"/proj-datumgrid-1.7.zip -C "${srcdir}"/proj.4-$pkgver/nad
}

build(){
  cd "${srcdir}"/$pkgname.4-$pkgver

  autoreconf -if
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname.4-$pkgver

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
