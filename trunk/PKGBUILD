# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-sub-name
_cpanname=Sub-Name
pkgver=0.26
pkgrel=4
pkgdesc="(re)name a sub"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
url="https://search.cpan.org/dist/$_cpanname"
source=("https://search.cpan.org/CPAN/authors/id/E/ET/ETHER/$_cpanname-$pkgver.tar.gz")
md5sums=('0eb109dfa223e056c07d84c9fbda1762')

build() {
  cd "$srcdir/$_cpanname-$pkgver"
  perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_cpanname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$_cpanname-$pkgver"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
