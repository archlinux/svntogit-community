# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-freezegun
pkgname=('python-freezegun' 'python2-freezegun')
pkgver=0.3.15
pkgrel=3
pkgdesc="Let your Python tests travel through time"
arch=('any')
license=('Apache')
url="https://github.com/spulec/freezegun"
makedepends=('python-setuptools' 'python2-setuptools' 'python-dateutil' 'python2-dateutil')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock'
              'python-dateutil' 'python2-dateutil')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/spulec/freezegun/archive/$pkgver.tar.gz")
sha512sums=('7b89fa2ba56cbcf274e33eea868d8f3653b0f2c47e91b1df9f72b65a0f1cb04f6364a00d9666afbc629707c5eaa238f36e9ef36b79dacd74d7fd00faea4a1a29')

prepare() {
  cp -a freezegun-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/freezegun-$pkgver
  python setup.py build

  cd "$srcdir"/freezegun-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/freezegun-$pkgver
  # https://github.com/spulec/freezegun/issues/250
  pytest || echo "Tests failed"

  cd "$srcdir"/freezegun-$pkgver-py2
  pytest2
}

package_python-freezegun() {
  depends=('python-dateutil')

  cd freezegun-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-freezegun() {
  depends=('python2-dateutil')

  cd freezegun-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
