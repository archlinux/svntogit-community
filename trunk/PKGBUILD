# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Alejandro Valdes <alejandrovaldes at live dot com>

pkgname=ncspot
pkgver=0.12.0
pkgrel=2
pkgdesc="Cross-platform ncurses Spotify client written in Rust, inspired by ncmpc and the likes."
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/hrkfdn/ncspot"
license=('BSD')
depends=('ncurses' 'openssl' 'libpulse' 'libxcb' 'ncurses' 'dbus' 'hicolor-icon-theme')
makedepends=('cargo' 'git' 'pulseaudio' 'portaudio' 'python' 'pkgconf' 'ueberzug')
optdepends=('pulseaudio: PulseAudio backend'
            'portaudio: PortAudio backend'
            'ueberzug: display album art in terminal (X11)')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hrkfdn/ncspot/archive/v${pkgver}.tar.gz"
        'ncspot.install')
b2sums=('babf8796ce7f7d8055a5c345a36572237cf573f9bb4560fa5e2a7be28efa17fefa2add9278c85f0e139523aa787fe915aa6f03fa0432787eef838e5c14685414'
        '82b5aabd5aa33d94abbeb1ca978d6685af10ef548dca678cdb5865226b4c17111508ea9cc685634a118a6e9e62142884efb31da23dff4e7c6eb53abc5b2a0b34')
options=('!lto')
install=ncspot.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --all-features
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "misc/ncspot.desktop" "${pkgdir}/usr/share/applications/ncspot.desktop"
  install -Dm 644 "images/logo.svg" "${pkgdir}/usr/share/icons/hicolor/scalable/apps/ncspot.svg"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

