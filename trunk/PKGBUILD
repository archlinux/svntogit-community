# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.8.4
pkgrel=1
pkgdesc="An open source client for Windows Remote Desktop Services"
arch=('x86_64')
url="https://www.rdesktop.org/"
license=('GPL3')
depends=('libao' 'libsamplerate' 'xorg-xrandr' 'pcsclite' 'libgssglue')
source=(https://github.com/rdesktop/rdesktop/archive/v${pkgver}.tar.gz rdesktop-send_physical_buttons.diff)
sha512sums=('31f2418bf12164e64bf3fa44089ab49d3ea18ed5b02b09b12b98f0e4f9f331d57c17eed8912df76209bc10c34671aa082f39f18b9e87b5bcc23b1aa90a5d103e'
            'a51d3d8a048cc5b67d872e9bfee91bfecd055c4bbf202cca3e768eb51dfd12e30e55ccee5431cec6ba20de818e1f0b94d4fe953f43828ee8b84f1fb7e4ffccec')

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"
  
  ./bootstrap
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
