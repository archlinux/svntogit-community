# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=gpg-crypter
pkgver=0.3.5
pkgrel=6
pkgdesc='Graphical front-end to GnuPG(GPG) using the GTK2 toolkit and libgpgme'
arch=('i686' 'x86_64')
url="http://gpg-crypter.sourceforge.net/"
license=('GPL')
depends=('gtk2' 'gpgme')
makedepends=('gendesk')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('be33140ae205b8a3f37217c1c2c0737a')

build() {
  cd "$srcdir"
  touch _.png; gendesk; rm _.png

  cd "$pkgname-$pkgver"
  export CFLAGS="$CFLAGS -D_LARGEFILE_SOURCE -D_FILE_OFFSET_BITS=64"
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

# icon
  install -Dm644 ${srcdir}/$pkgname-$pkgver/pixmaps/$pkgname.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png

# .desktop file
  install -Dm644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop

# delete unneeded icon
  rm -rf ${pkgdir}/usr/share/$pkgname
}
