# Maintainer: Ivy Foster <iff@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: alive4ever <alive4ever at live.com>
# Contributor: Earnest

pkgname=opendoas
pkgver=6.8.2
pkgrel=1
pkgdesc='Run commands as super user or another user'
url='https://github.com/Duncaen/OpenDoas'
license=(custom:ISC)
arch=(x86_64)
depends=(pam)
provides=(doas)
replaces=(doas)
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
install=opendoas.install
backup=(etc/pam.d/doas)
sha256sums=('4e98828056d6266bd8f2c93e6ecf12a63a71dbfd70a5ea99ccd4ab6d0745adf0'
            'SKIP')
validpgpkeys=(C4B6FBA98211F7EFF35DEB73335C1D17EC3D6E35) # Duncan Overbruck <mail@duncano.de>

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr --with-timestamp
	make
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
