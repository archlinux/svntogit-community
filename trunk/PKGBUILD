# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: razer <razer[at]neuf[dot]fr>
# Contributor: pzl <alsoelp@gmail.com>
# Contributor: Dennis Fink <dennis.fink@c3l.lu>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>

pkgname=python-cairosvg
pkgver=2.0.2
pkgrel=1
pkgdesc="A Simple SVG Converter for Cairo"
arch=('any')
url="http://cairosvg.org/"
license=('LGPL3')
depends=('python-cairocffi' 'python-lxml' 'python-tinycss' 'python-cssselect' 'python-pillow')
makedepends=('git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-isort'
              'python2-pytest-isort' 'python-pytest-flake8' 'python2-pytest-flake8'
              'python-pytest-cov' 'python2-pytest-cov' 'gnu-free-fonts')
source=("git+https://github.com/Kozea/CairoSVG.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd CairoSVG
  git submodule update --init
}

build() {
  cd CairoSVG
  python setup.py build
}

check() {
  cd CairoSVG
  python setup.py test
}

package() {
  cd CairoSVG
  python setup.py install --root="$pkgdir/" --optimize=1
}
