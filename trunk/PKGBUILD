# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=mc
pkgver=4.8.29
pkgrel=1
pkgdesc='A file manager that emulates Norton Commander'
arch=('x86_64')
url='https://midnight-commander.org/'
license=('GPL')
depends=(
  'e2fsprogs'
  'glib2'
  'gpm'
  'libssh2'
  'slang'
  'which'
)
makedepends=(
  'aspell'
  'libxt'
  'libx11'
  'unzip'
)
optdepends=(
  'aspell: spelling corrections'
  'cabextract: ucab extfs'
  'cdparanoia: audio extfs'
  'cdrkit: iso9660 extfs'
  'cvs: CVS support'
  'gawk: hp48+ extfs'
  'mtools: a+ extfs'
  'p7zip: support for 7zip archives'
  'perl: needed by several extfs scripts'
  'python: to access uc1541 or s3 storage'
  'python-boto: s3+ extfs'
  'python-pytz: s3+ extfs'
  'samba: VFS support'
  'unace: uace extfs'
  'unarj: uarj extfs'
  'unrar: urar extfs'
  'unzip: open zip archives'
  'zip: uzip extfs'
)
backup=(
  'etc/mc/edit.indent.rc'
  'etc/mc/filehighlight.ini'
  'etc/mc/mc.ext'
  'etc/mc/mc.keymap'
  'etc/mc/mc.menu'
  'etc/mc/mcedit.menu'
  'etc/mc/sfs.ini'
)
options=('!emptydirs')
source=(
  "http://ftp.midnight-commander.org/${pkgname}-${pkgver}.tar.xz"
  'mc-python3.patch'
)
sha512sums=('0f55300769c28b2d6e24af1459eadb5505ba64cc7330b190562f260b74cf0e8acc277b680c5d8d10b6bcf2e429f54195bb4150f787269419f09ba61cba719e27'
            '516c25cdc482afba6b0fa78737e4e10815acdc4d528a908e118025e44f44be0bb86b90b5044d4675e835d8ff64be551b5d48943c2c2ddd9716c34b39b1e627bc')
b2sums=('52dcb069033615518e11fbf693817138a2ff0813fb097488ece292b625dbbcbed940ad7504e485f3e526444a7fb471748377d6d03d2a6c8c44f687f8a8fc4611'
        'b0e20d89c7c1ba25f76f564481ed7e2b9f8614b460dc170bb3d1998411c3d7945dee5200e9a2db81f706a8bdbffffb72665a0c43353b520c02fcf5fee4d251db')

prepare() {
  cd "$pkgname-$pkgver"

  # port to Python 3
  # ref: https://github.com/MidnightCommander/mc/pull/149
  patch -p1 -i "$srcdir/mc-python3.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-aspell \
    --with-screen=slang \
    --with-x

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # FS#50889: Replace mc.keymap symlink with target file to fix backup mechanism.
  rm "$pkgdir/etc/mc/mc.keymap"
  cp "$pkgdir"/etc/mc/mc{.default,}.keymap
}
