# Maintainer: Ivy Foster <iff@archlinux.org>
# Contributor: Alex Szczuczko <alex at szc dot ca>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Imanol Celaya <ilcra1989@gmail.com>

pkgname=bmake
pkgver=20211001
pkgrel=1
pkgdesc='Portable version of the NetBSD make build tool'
arch=(x86_64)
url='https://www.crufty.net/help/sjg/bmake.html'
license=(BSD)

# upstream recommends using python for improved meta2deps script
depends=(python)

source=(
	"https://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz"
	"https://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz.asc"
)

# checksum source: http://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz.sha1
sha1sums=(
	6f28706a067ec98d2de03cdba6b1af072fc718d7
	SKIP
)
# Key seems to be available only from
# <https://www.crufty.net/help/sjg/bmake.html>
validpgpkeys=(7E228507C26D8DC164F531BFBA54C8AF755A2A99)

build() {
	cd bmake
	./boot-strap --prefix=/usr op=build
}

# op=build also runs unit tests; thus no check()

package() {
	cd bmake

	# Fix directory permissions on install
	install -d "$pkgdir/usr/bin" "$pkgdir/usr/share/licenses/$pkgname"

	./boot-strap --install-destdir="$pkgdir" --prefix=/usr op=install

	rm -rf "$pkgdir/usr/share/man/cat1"
	install -D -m 644 bmake.1 "$pkgdir/usr/share/man/man1/bmake.1"

	head -n70 main.c >"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
