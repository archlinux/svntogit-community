# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.1
pkgrel=1
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('MIT' 'GPL2')
depends=('gtk2' 'libmng' 'curl' 'librsvg' 'desktop-file-utils' 'libnsbmp' 'libnsgif' 'libcss' 'libwebp' 'libdom' 'lcms') # 'libharu' 'gstreamer0.10'
makedepends=('js' 're2c' 'gendesk' 'netsurf-buildsystem' 'libglade' 'addinclude')
install="$pkgname.install"
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://download.$pkgname-browser.org/$pkgname/releases/source/$pkgname-$pkgver-src.tar.gz"
        'netsurf.sh')
sha256sums=('f0dbcc5d80bf03d706aa8b28a322aa7f169a40813848c2d1505691f6e2c7ef00'
            '1ef12b2e2bc68f9eff8465646b7359e3cb483e1042aed750cc283a70dd4c540c'
            '7aef20cc7b2e2e7225237f2f94b5a0074caeb8ec07075c58429b0345ca566f8d')

prepare() {
  cd "$pkgname-$pkgver"

  addinclude image/rsvg.c stdio
  addinclude image/mng.c stdio
  addinclude image/png.c stdio

  gendesk -f --pkgname "$pkgname" --pkgdesc "$pkgdesc" --exec 'netsurf %U' \
    --genericname 'Web Browser' --comment 'Lightweight web browser' \
    --mimetypes 'text/html;application/xhtml+xml;x-scheme-handler/http;x-scheme-handler/https;x-scheme-handler/geo'
}

build() {
  cd "$pkgname-$pkgver"

  make \
    PREFIX=/usr \
    TARGET=gtk \
    NETSURF_USE_WEBP=YES \
    NETSURF_USE_VIDEO=NO
}

package() {
  cd "$pkgname-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir"
  mv "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/bin/$pkgname.elf"
  install -Dm755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "../$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
