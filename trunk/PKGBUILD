# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openpgp-card-tools
pkgver=0.9.0
pkgrel=3
pkgdesc="CLI tool to inspect, manage and use OpenPGP cards"
arch=(x86_64)
url="https://gitlab.com/openpgp-card/openpgp-card"
license=(Apache)
depends=(ccid gcc-libs glibc gmp)
makedepends=(bzip2 cargo clang nettle pcsclite)
source=($url/-/archive/tools/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('91d7dc60f061da6cc70802f3caa1833470db3d0f60c64806f0a9712e990ed046b813858a373d37afc163f8c4af08ccf5982d1c834d6d8e82df15e1bef9110e43')
b2sums=('a3b30835e9df4db2c0b4b83dce2ac9be9f525ca68ed46fe7307a22dfa6ae77e04377a2600cd93cc12db839c93a599fc48846a9990ced77cf0accdc2167dddc38')

prepare() {
  cd $pkgname-v$pkgver
  # add --locked once there is a lock file: https://gitlab.com/openpgp-card/openpgp-card/-/issues/63
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd $pkgname-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --frozen --release --bin opgpcard
}

check() {
  cd $pkgname-v$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen --all-features
}

package() {
  depends+=(
    bzip2 libbz2.so
    nettle libhogweed.so libnettle.so
    pcsclite libpcsclite.so
  )

  cd $pkgname-v$pkgver
  install -vDm 755 target/release/opgpcard -t "$pkgdir/usr/bin/"
  install -vDm 644 {tools/scripting,README}.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
