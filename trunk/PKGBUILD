# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-factory-boy
pkgver=2.12.0
pkgrel=2
pkgdesc="A versatile test fixtures replacement based on thoughtbot's factory_bot for Ruby"
url="https://github.com/FactoryBoy/factory_boy"
license=('MIT')
arch=('any')
depends=('python-faker')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-flask' 'python-django' 'python-sqlalchemy'
              'python-flask-sqlalchemy' 'python-mongoengine' 'python-mongomock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FactoryBoy/factory_boy/archive/$pkgver.tar.gz"
        $pkgname-remove-double-connection-to-mongodb.patch::https://github.com/FactoryBoy/factory_boy/commit/bc84ff8bfd.patch)
sha512sums=('33236bc02d5331998bb210af498bea46fc9540e66af32db8fdab563193b7ae8870e7cc01c93c24a03ee23ba64bac6ca27a72bb51a264d1c7b6bf9b098c65d5d2'
            '538cd5e6fa251ca24ef7966a8fe8c609c9904f77b8d285390483d065f4159fc74adfe6b1cc400fc99a7199ce2f6d12b7ca15b8724194ffcda12b597c4e444f87')

prepare() {
  cd factory_boy-$pkgver
  patch -Np1 -i ../$pkgname-remove-double-connection-to-mongodb.patch
  # Use mongomock
  sed -i '/serverselectiontimeoutms/a is_mock=True' tests/test_mongoengine.py
}

build() {
  cd factory_boy-$pkgver
  python setup.py build
}

check() {
  cd factory_boy-$pkgver
  python setup.py pytest
}

package() {
  cd factory_boy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
