# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=brick
pkgname=haskell-brick0.71
pkgver=0.71.1
pkgrel=33
pkgdesc="A declarative terminal user interface library (legacy 0.71.* version)"
url="https://github.com/jtdaugherty/brick"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-vty' 'haskell-data-clist' 'haskell-dlist' 'haskell-microlens'
         'haskell-microlens-th' 'haskell-microlens-mtl' 'haskell-config-ini' 'haskell-vector'
         'haskell-contravariant' 'haskell-text-zipper' 'haskell-word-wrap')
makedepends=('ghc' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f2fe3c32fe663e1c6d7688ebd058d792c5faf30a32997bbc45b828e2f10ec0c60720329a76b2035e05e28951a347602e0e751a125c8765c1f9d98696bd4b0564')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-demos
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    TERM=screen runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
