# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: <francois . archlinux . org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Igor Scabini <furester@gmail.com>

pkgname=perl-image-size
_cpanname=Image-Size
pkgver=3.230
pkgrel=1
pkgdesc="Read the dimensions of an image in several popular formats"
arch=(any)
url="http://search.cpan.org/dist/${_cpanname}/"
license=('LGPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJRAY/${_cpanname}-$pkgver.tar.gz")
md5sums=('17b5cc123a522aa3c4ca842094d0c0ba')

build() {
  cd $srcdir/${_cpanname}-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd $srcdir/${_cpanname}-$pkgver
  make install DESTDIR="$pkgdir"
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
