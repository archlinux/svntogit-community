# Maintainer: Anatol Pomozov
# Contributor: László Várady <laszlo.varady93@gmail.com>

pkgname=rocksdb
pkgver=6.15.5
pkgrel=1
pkgdesc='Embedded key-value store for fast storage'
arch=(x86_64)
url="https://rocksdb.org/"
license=(GPL2 Apache)
depends=(bzip2 gcc-libs gflags jemalloc lz4 snappy zlib zstd)
makedepends=(cmake ninja)
#checkdepends=('python2')
source=(rocksdb-$pkgver.tar.gz::https://github.com/facebook/rocksdb/archive/v$pkgver.tar.gz)
sha256sums=('d7b994e1eb4dff9dfefcd51a63f86630282e1927fc42a300b93c573c853aa5d0')

build() {
  cd rocksdb-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_BZ2=ON -DWITH_LZ4=ON -DWITH_SNAPPY=ON -DWITH_ZLIB=ON -DWITH_ZSTD=ON \
        -DUSE_RTTI=ON -DWITH_JEMALLOC=ON -DFAIL_ON_WARNINGS=OFF -DPORTABLE=ON \
	-DWITH_BENCHMARK_TOOLS=OFF \
	-GNinja -S . -B build
  ninja -C build
}

check() {
  cd rocksdb-$pkgver
  # currently upstream enables tests for Debug configuration only
  # ninja -C build check
}

package() {
  cd rocksdb-$pkgver
  DESTDIR="$pkgdir" ninja -C build install
}
