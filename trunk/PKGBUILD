# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: JÃ¶rg Thalheim <joerg@higgsboson.tk>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>

pkgname=python-mpd2
pkgver=3.1.0
pkgrel=1
pkgdesc='Python library which provides a client interface for the Music Player Daemon'
url=https://github.com/Mic92/python-mpd2
license=(GPL3)
depends=(python)
makedepends=(
  git
  python-setuptools
)
arch=(any)
_tag=84ca21ccd7362dce023f8f67d7002338574de54f
source=(git+https://github.com/Mic92/python-mpd2.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd python-mpd2

  git describe --tags | sed 's/^v//'
}

build() {
  cd python-mpd2

  python setup.py build
}

package() {
  cd python-mpd2

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
