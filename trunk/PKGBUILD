# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Steven Allen <steven@stebalien.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Contributor: Amr Hassan <amr.hassan@gmail.com>

pkgbase=python-pylast
pkgname=('python-pylast' 'python2-pylast')
pkgver=1.4.2
pkgrel=0
pkgdesc='A Python interface to Last.fm and Libre.fm'
arch=('any')
url='https://github.com/pylast/pylast'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/pylast/pylast-${pkgver}.tar.gz")
sha256sums=('e4bd8f9c8eef5662b2b03d61846c4c13b56a7b1c169476fe96c1a0e58d66da3b')

prepare() {
  cp -r pylast-${pkgver} python2-pylast-${pkgver}
}

build() {
  cd pylast-${pkgver}

  python setup.py build

  cd ../python2-pylast-${pkgver}

  python2 setup.py build
}

package_python-pylast() {
  depends=('python-six')

  cd pylast-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-pylast() {
  depends=('python2-six')

  cd python2-pylast-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
