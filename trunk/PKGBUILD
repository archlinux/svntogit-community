# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Zerial <fernando@zerial.org>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: Jose Riha <jose1711 gmail com>

pkgname=dd_rescue
pkgver=1.99
pkgrel=1
pkgdesc="A dd version that is very useful for data-recovery"
url="http://www.garloff.de/kurt/linux/ddrescue/"
license=("GPL")
depends=('glibc')
makedepends=('openssl-1.0')
optdepends=('lzo: for libddr_lzo'
            'openssl-1.0: for libddr_crypt')
arch=('x86_64' 'i686')
source=("http://www.garloff.de/kurt/linux/ddrescue/$pkgname-${pkgver}.tar.bz2")
sha512sums=('b33f6118fc996252ceafa90d43ff01cd5b54dd03eec9c852d006f15192cbffa99e289ea81c5c10ac91cfa2a26ab2f9e32a70e73d6a5183f9e2d973a75de45b85')

prepare() {
  cd $pkgname-$pkgver

  # Don't install to /usr/lib64
  sed -i '/LIB = lib64/d' Makefile
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" INSTALLDIR="$pkgdir/usr/bin" install
}
