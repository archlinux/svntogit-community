# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=planarity
pkgver=3.0.0.5
pkgrel=2
pkgdesc="A library for implementing graph algorithms"
arch=(x86_64)
url="https://github.com/graph-algorithms/edge-addition-planarity-suite"
license=(BSD)
depends=(glibc)
makedepends=()
source=($pkgname-$pkgver.tar.gz::"https://github.com/graph-algorithms/edge-addition-planarity-suite/archive/Version_$pkgver.tar.gz")
md5sums=('2fd16fb8bc5d8343b61fd94005475ed1')

build() {
  cd edge-addition-planarity-suite-Version_$pkgver
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd edge-addition-planarity-suite-Version_$pkgver
  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE.TXT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
