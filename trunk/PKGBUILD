# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Ian Brunelli <ian@brunelli.me>

pkgname=wofi
pkgver=1.2.2
pkgrel=1
pkgdesc='launcher for wlroots-based wayland compositors'
arch=('x86_64')
url='https://hg.sr.ht/~scoopta/wofi'
license=('GPL3')
depends=('wayland' 'gtk3')
makedepends=('meson')
source=("${pkgname}-v${pkgver}.tar.gz::https://hg.sr.ht/~scoopta/wofi/archive/v${pkgver}.tar.gz")
# We have to SKIP the validity check to avoid an issue with hg.sr.ht:
# https://todo.sr.ht/~sircmpwn/hg.sr.ht/33
sha256sums=('SKIP')

build() {
    cd "${pkgname}-v${pkgver}"
    meson build
    ninja -C build
}

package() {
    cd "${pkgname}-v${pkgver}"
    install -Dm755 -t "${pkgdir}/usr/bin/" build/wofi
    install -Dm644 -t "${pkgdir}/usr/share/man/man1/" man/*.1
    install -Dm644 -t "${pkgdir}/usr/share/man/man3/" man/*.3
    install -Dm644 -t "${pkgdir}/usr/share/man/man5/" man/*.5
    install -Dm644 -t "${pkgdir}/usr/share/man/man7/" man/*.7
}
