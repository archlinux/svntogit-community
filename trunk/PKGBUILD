# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=usbredir
pkgver=0.12.0
pkgrel=1
pkgdesc="USB traffic redirection protocol"
arch=('x86_64')
url="https://www.spice-space.org/usbredir.html"
license=('GPL2' 'LGPL2.1')
depends=('libusb' 'glib2')
makedepends=('meson')
source=(https://spice-space.org/download/usbredir/$pkgname-$pkgver.tar.xz)
sha256sums=('fbb44025bf55e1ce8d84afc7596bfa47c8a36cd603c6fa440f9102c1c9761e6d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  arch-meson . build
  meson compile -C build
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  meson test -C build --print-errorlogs
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  meson install -C build --destdir="$pkgdir"
}
