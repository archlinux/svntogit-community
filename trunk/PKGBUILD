# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=nsjail
pkgver=3.0
pkgrel=4
pkgdesc="Light-weight process isolation tool"
arch=(x86_64)
url="https://nsjail.com"
license=(Apache)
depends=(protobuf libnl)
makedepends=(git)
source=("https://github.com/google/nsjail/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'git+https://github.com/google/kafel.git#commit=8e69b8efae415cde3debffbb1e379d9e7a16835a'
        replace-YYUSE-with-attribute-unused-in-src-parser.y.patch)
sha256sums=('cfa66d3ed136b2e221752287b95e544915e8a6760aa866f023b604d14a374919'
            'SKIP'
            '9773c57a2eaa3460a0656a5fdc3603d141aac34ded53e18e29a890fd5b7a71f8')

prepare() {
    # https://github.com/google/kafel/pull/28
    patch -Np1 -d kafel <replace-YYUSE-with-attribute-unused-in-src-parser.y.patch
    mv kafel ${pkgname}-${pkgver}
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 nsjail -t "${pkgdir}"/usr/bin/ 
}
