# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-class-factory-util
pkgver=1.7
pkgrel=10
pkgdesc="Provide utility methods for factory classes"
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/Class-Factory-Util/"
options=(!emptydirs)
depends=('perl')
makedepends=('perl-module-build')
source=(https://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Class-Factory-Util-1.7.tar.gz)
sha256sums=('6c516b445b44f87363fb3a148431d31e9ecb5e6f21fb6481c89b2406b6692e26')

build() {
  cd "$srcdir"/Class-Factory-Util-${pkgver}
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir"/Class-Factory-Util-${pkgver}
  make install DESTDIR="$pkgdir"

  # remove perllocal.pod and .packlist
  find "$pkgdir" -name perllocal.pod -delete
  find "$pkgdir" -name .packlist -delete
}
