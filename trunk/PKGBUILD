# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-base-modulebuild
pkgver=1.14
pkgrel=1
pkgdesc="A Module::Build subclass for building Alien:: modules and their libraries"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Base-ModuleBuild"
depends=('perl-alien-build' 'perl-archive-extract' 'perl-capture-tiny' 'perl-file-chdir'
         'perl-html-parser' 'perl-module-build' 'perl-path-tiny' 'perl-shell-config-generate'
         'perl-shell-guess' 'perl-sort-versions' 'perl-uri')
makedepends=('perl-test2-suite')
checkdepends=('perl-acme-alien-dontpanic' 'perl-inline' 'perl-inline-c' 'perl-inline-cpp')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Base-ModuleBuild-$pkgver.tar.gz")
sha512sums=('6fcfb7e5525972d5ca565441717143b036ed9c4079700426df2e96d76a91baeaf103ac406a8fa86dfd820929041525d7bb66c32762258cfb7752fb7634171fbf')

build() {
  cd Alien-Base-ModuleBuild-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Alien-Base-ModuleBuild-$pkgver
  perl Build test
}

package() {
  cd Alien-Base-ModuleBuild-$pkgver
  perl Build install destdir="$pkgdir"
}
