# Maintainer:

pkgname=python-pytest-console-scripts
_pyname=pytest-console-scripts
pkgver=1.3.1
pkgrel=2
pkgdesc='Pytest plugin for testing console scripts'
arch=(any)
license=(MIT)
url='https://github.com/kvas-it/pytest-console-scripts'
depends=(python-pytest python-setuptools)
makedepends=(python-build python-installer python-setuptools-scm python-wheel)
source=(https://pypi.python.org/packages/source/p/$_pyname/$_pyname-$pkgver.tar.gz)
sha512sums=('344e9379976d9a7541f9683f7646b47f7fc25d2ab8d941351811ada7fddd77497cc24f4bc6a6d5ab9388dd5b289ed125bcbfad869009fb3b80aaaa1b951d656d')

build() {
  cd $_pyname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
