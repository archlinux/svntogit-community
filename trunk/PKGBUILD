# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.20.0
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=(x86_64)
url="https://github.com/pymupdf/PyMuPDF"
license=(AGPL3)
depends=(glibc gumbo-parser jbig2dec libmupdf openjpeg2 python)
makedepends=(freetype2 libjpeg-turbo python-distro python-build python-installer python-setuptools python-wheel swig systemd)
checkdepends=(python-fonttools python-pillow python-pytest)
options=(debug)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('f6bae4da11b143cec441302ed19032d1d008fe6846d7ae219665064f2307be8d0990dda0ff3a8c7e63ccc223bb71401c6f8d4bb21df7ab321aec98c4683d4686')
b2sums=('f81380a3b43e229d283d18b90d09b68acf8235708c7ae2adb130d3c014586ca02ecc30ab0dff7ba368a53202772592c85920bfd703435ac2c2f5622eefd8a58c')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"

  cd $_name-$pkgver
  mkdir -vp $_test_dir
  # install to test dir for testing
  python -m installer --destdir="$_test_dir" dist/*.whl

  export PYTHONPATH="$_test_dir/$_site_packages:$PYTHONPATH"
  pytest -vv -c /dev/null tests/
}

package() {
  depends+=(libfreetype.so libjpeg.so)

  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
