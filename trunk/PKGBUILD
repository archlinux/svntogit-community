# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Allen Li <darkfeline at abagofapples.com>

pkgbase=flake8
pkgname=(flake8 python2-flake8)
pkgver=3.7.4
pkgrel=1
epoch=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://flake8.pycqa.org"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyflakes' 'python2-pyflakes'
             'python-pycodestyle' 'python2-pycodestyle' 'python-mccabe' 'python2-mccabe'
             'python2-enum34' 'python2-configparser' 'python-entrypoints' 'python2-entrypoints'
             'python2-functools32' 'python2-typing')
checkdepends=('python-mock' 'python2-mock' 'python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/PyCQA/flake8/archive/$pkgver.tar.gz")
sha512sums=('52ee28f9084fcb7ffc0c9a0035f89f09aded8e21f7b90fcb0daf48412a3d92dd07a6b2033435d9fca4df835d318901f5d19a2d0613b5099df06cca3b46df65f3')

prepare() {
  sed -i -e 's/, *< *[0-9=.]*//' flake8-$pkgver/setup.py
  cp -a flake8-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flake8-$pkgver
  python setup.py build

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/flake8-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_flake8() {
  depends=('python-pyflakes' 'python-mccabe' 'python-pycodestyle' 'python-entrypoints')
  provides=('flake8-python3')
  replaces=('flake8-python3')
  conflicts=('flake8-python3')

  cd "$srcdir"/flake8-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flake8() {
  depends=('python2-pyflakes' 'python2-mccabe' 'python2-pycodestyle' 'python2-entrypoints'
           'python2-enum34' 'python2-configparser' 'python2-functools32' 'python2-typing')
  provides=('flake8-python2')
  replaces=('flake8-python2')
  conflicts=('flake8-python2')

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/flake8{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
