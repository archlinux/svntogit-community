# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgbase=python-pygit2
pkgname=('python-pygit2' 'python2-pygit2')
pkgver=0.28.0
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.27.0')
makedepends=('python-cffi' 'python2-cffi' 'python-setuptools' 'python2-setuptools' 'git' 'python-six' 'python2-six')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz")
sha256sums=('9d44c30a661916bfbae9dee0a80cec83a9acdf66fdcf7cfe1be45d423508e871')

package_python-pygit2() {
  depends+=('python-cffi' 'python-six')
  replaces=('python-libgit2')
  conflicts=('python-libgit2')

  cd "pygit2-$pkgver"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  depends+=('python2-cffi' 'python2-six')
  replaces=('python2-libgit2')
  conflicts=('python2-libgit2')

  cd "pygit2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
