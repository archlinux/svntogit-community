# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-toposort
pkgver=1.8
pkgrel=1
pkgdesc='Implements a topological sort algorithm'
arch=('any')
license=('Apache')
url='https://gitlab.com/ericvsmith/toposort'
depends=('python')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
source=("https://gitlab.com/ericvsmith/toposort/-/archive/$pkgver/toposort-$pkgver.tar.bz2")
sha512sums=('c8ccdf71dd1d53bff93b69da4558f0e2e23103aa20d0db7dd1a0aa5c33371ccb1bb699502d5a286c7a81d71f71809bc79d454a61d14712f5940044755b556f9c')

build() {
  cd toposort-$pkgver
  python -m build -nw
}

check() {
  cd toposort-$pkgver
  make test
}

package() {
  cd toposort-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
