# $Id$
# Contributor: Andrew Burkett <burkett.andrew@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=launchy
pkgver=2.5
pkgrel=3
pkgdesc="Indexes the programs in your start menu and can launch your documents, project files, folders, and bookmarks with just a few keystrokes!"
arch=('i686' 'x86_64')
url="http://www.launchy.net/"
license=('GPL')
depends=('qt4' 'xdg-utils')
makedepends=('gcc' 'boost')
source=("http://downloads.sourceforge.net/launchy/launchy-${pkgver}.tar.gz"
	fix-linking.patch)
sha256sums=('93ec471423b77973a2118eaec64c7a1c05ce37b5bff41760336ebd14fc819500'
            'e1b9203ef5071d401ab6276a38270b651488c4cc481a60a98d2c17fb38114e7d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix linking against libX11
  patch -Np1 -i ../fix-linking.patch

  qmake-qt4 -r Launchy.pro
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
