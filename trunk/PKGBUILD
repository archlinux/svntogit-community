# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Hector Mtz-Seara <hseara.#at#.gmail*.*com>

pkgname=pyzo
pkgver=4.6.1
pkgrel=1
pkgdesc="Cross-platform Python IDE focused on interactivity and introspection, very suitable for scientific computing."
url="https://pyzo.org"
license=('BSD')
arch=('any')
depends=('python' 'pyside2')
makedepends=('python-setuptools')
optdepends=('python-pyqt5: to use PyQt5 as backend instead of PySide')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/pyzo/pyzo/archive/v${pkgver}.tar.gz"
        'pyzo.sh')
sha256sums=('8847449764efc3402485df2b88e9f0a5f9d2e06a8f9fd8560d1e15fce82ec7f3'
            'ad318a4669f37819b99f273d3a770f9fe0f1732a6cf39ca69e8fe79f878586f4')

build(){
    cd ${pkgname}-${pkgver}
    python setup.py build
}

package(){
    cd ${pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm444 ${pkgname}/license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    install -Dm644 pyzo/resources/pyzo.desktop -t "${pkgdir}"/usr/share/applications/
    for i in 16 32 48 64 128 256
        do  
    install -Dm644 pyzo/resources/appicons/pyzologo${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}x${i}/apps/pyzologo.png
    done

    install -Dm755 ../${pkgname}.sh "${pkgdir}"/usr/bin/${pkgname}
}
