# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: shamrok <szamrok@gmail.com>
# Contributor: scj <scj archlinux us>

pkgname=python-levenshtein
pkgver=0.20.8
pkgrel=1
pkgdesc="Python extension for computing string edit distances and similarities"
url="https://pypi.python.org/pypi/python-Levenshtein/"
license=('GPL')
arch=('x86_64')
depends=('python-rapidfuzz')
makedepends=('python-scikit-build' 'cython' 'rapidfuzz-cpp')
checkdepends=('python-pytest')
#source=("https://files.pythonhosted.org/packages/source/L/Levenshtein/Levenshtein-$pkgver.tar.gz")
source=("https://github.com/maxbachmann/Levenshtein/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3a248b5d1a18c8afeffaa637c831b07266dd36a094b713c18b7182a49de7df9a3b7316d00e9329e2ae49f92c601e32a61590338e30a7e4f33ffe0d76c6219061')

build() {
  cd Levenshtein-$pkgver
  python setup.py build
}

check() {
  cd Levenshtein-$pkgver
  PYTHONPATH=`echo "$PWD"/_skbuild/linux-x86_64-*/setuptools/lib.linux-x86_64-*` \
  pytest
}

package() {
  cd Levenshtein-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
