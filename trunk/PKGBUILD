# $Id$
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=ibus
pkgver=1.4.1
pkgrel=2
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('python2-dbus' 'gconf' 'dconf' 'python2-xdg' \
         'iso-codes' 'librsvg' 'python-notify' 'hicolor-icon-theme')
makedepends=('intltool' 'gobject-introspection')
optdepends=('notification-daemon')
options=('!libtool')
install=ibus.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz ibus-1389-dbus-address.patch)
sha1sums=('4cc045553885d35a118ec4b44c2694910209509e'
          'ee4eb1cf900a14d62adae932621ffb2f8d0a549d')

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=python2
  patch -p1 -i ../ibus-1389-dbus-address.patch
  sed -i 's|--pkg=ibus-1.0|--pkg-export=ibus-1.0|' src/Makefile.in
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
    --enable-gtk3
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/etc/xdg/autostart"
  ln -s /usr/share/applications/ibus.desktop "${pkgdir}/etc/xdg/autostart/ibus.desktop"
}
