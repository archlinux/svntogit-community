# Maintainer:

pkgname=python-autopage
pkgver=0.4.0
pkgrel=1
pkgdesc='A library to provide automatic paging for console output'
arch=(any)
url='https://github.com/zaneb/autopage'
license=(Apache)
depends=(python)
checkdepends=(python-pytest python-fixtures)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://pypi.python.org/packages/source/a/autopage/autopage-$pkgver.tar.gz)
sha512sums=('00dd0fc405c6cbeec962e0c6e85ff923c6c8caefd9cb793d668117ae6b25d1086253b4103bc541c35f3883caf7f72f0ef2f5758ca2ce3f7fbef39ac51a188b9c')

build() {
  cd autopage-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd autopage-$pkgver
  pytest
}

package() {
  cd autopage-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
