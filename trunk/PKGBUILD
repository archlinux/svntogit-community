# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-cairosvg
_pkgname=CairoSVG
pkgver=2.2.0
pkgrel=1
pkgdesc='SVG converter based on Cairo. It can export SVG files to PDF, PostScript and PNG files'
arch=('any')
url='http://cairosvg.org'
license=('LGPL3')
depends=('python' 'python-setuptools' 'python-cairocffi' 'python-pillow' 'python-cssselect2' 'python-defusedxml' 'python-tinycss2')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-isort')
replaces=('cairosvg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/CairoSVG/archive/$pkgver.tar.gz")
sha512sums=('6332a51b9a17a2875774f593065e8790ba0d734624a302d42c7f4e665a694e522190e65f84344368dbbc22525b10b73a934cc9167e35c0dc6d2e8f5c8279a935')

prepare() {
  cd $_pkgname-$pkgver

  cp -r cairosvg test/cairosvg_reference/
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

