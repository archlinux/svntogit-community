# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-app-static
pkgname=haskell-wai-app-static
pkgver=3.1.6.2
pkgrel=170
pkgdesc="WAI application for static serving"
url="http://www.yesodweb.com/book/web-application-interface"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-cryptonite'
         'haskell-file-embed' 'haskell-http-date' 'haskell-http-types' 'haskell-memory'
         'haskell-mime-types' 'haskell-old-locale' 'haskell-optparse-applicative'
         'haskell-unix-compat' 'haskell-unordered-containers' 'haskell-wai' 'haskell-wai-extra'
         'haskell-warp' 'haskell-zlib')
makedepends=('ghc' 'haskell-hspec' 'haskell-mockery' 'haskell-network' 'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('20c79810096f12d3901af3474a5ef4fe2385e639f97f2de803b09902d115321e3be7544cc3741b52b75a2710a0141bc19cb611fd4e6248ec60d11b522b9cb59c')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-print
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
