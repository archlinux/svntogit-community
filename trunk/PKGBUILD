# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=semigroupoids
pkgname=haskell-semigroupoids
pkgver=5.2.1
pkgrel=8
pkgdesc="Semigroupoids: Category sans id"
url="https://github.com/ekmett/semigroupoids"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-base-orphans" "haskell-bifunctors" "haskell-comonad" "haskell-contravariant"
         "haskell-distributive" "haskell-hashable" "haskell-semigroups" "haskell-tagged"
         "haskell-transformers-compat" "haskell-unordered-containers")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('735b90545a7f73eee921d94d661cd76fe22d4dbd949f5c976ef1a98c8d0ef50f40481816ee805aed9c5de12bef1d832de7be2adf651407782ccc073eec41c5a7')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -ftagged -fcomonad -fdoctests -fdistributive -fcontravariant -fcontainers -funordered-containers
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
