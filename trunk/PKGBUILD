# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=mozo
pkgname=${_pkgbase}
pkgver=${_ver}.0
pkgrel=2
pkgdesc="MATE menu editing tool"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('gtk3' 'mate-menus' 'python2-gobject')
makedepends=('intltool')
groups=('mate-extra' 'mate-extra-gtk3')
conflicts=("${_pkgbase}-gtk3")
replaces=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('bd9b1e70a6976940d40ef78f58c11a96ffa41a26')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
