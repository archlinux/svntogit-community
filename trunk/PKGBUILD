# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pkgname=llvmlite
pkgbase="python-${_pkgname}"
pkgname=("${pkgbase}")
pkgver=0.39.1
pkgrel=1
pkgdesc='A Lightweight LLVM Python Binding for Writing JIT Compilers'
arch=('x86_64')
makedepends=('cmake' 'llvm11' 'git' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools-scm')
checkdepends=('python-tests')
depends=('python' 'llvm11-libs')
url="https://github.com/numba/${_pkgname}"
license=('BSD')

source=("${_pkgname}::git+${url}#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
    cd "${srcdir}/${_pkgname}"
    python -m build --wheel --no-isolation
}

check() {
    cd "${srcdir}/${_pkgname}"
    python -m test || echo 'some tests failed!'
}

package() {
    cd "${srcdir}/${_pkgname}"

    python -m installer --destdir="$pkgdir" dist/*.whl

    install -D LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
