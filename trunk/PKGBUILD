# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.12
pkgrel=3
pkgdesc="Lightweight X11 tiled window manager written in Haskell"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.10.3' 'gmp' 'haskell-x11=1.6.1.2' 'sh' 'haskell-mtl=2.2.1'
         'haskell-utf8-string' 'haskell-extensible-exceptions=0.1.1.4'
         'haskell-data-default' 'haskell-setlocale')
makedepends=('gendesk')
optdepends=('xorg-xmessage: for displaying visual error messages')
install='xmonad.install'
options=('staticlibs')
source=("https://github.com/xmonad/xmonad/archive/v${pkgver}.tar.gz"
        "xmonad.svg")

md5sums=('c81d688c51fa50aea0e421f4755ee2fc'
         '72bfa5e62e4e44fe7fa59b6a7593d993')

prepare() {
  gendesk --pkgname "$pkgname" --pkgdesc "$pkgdesc"
}

build() {
  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup.lhs configure --ghc --enable-shared --enable-split-objs --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid --docdir=/usr/share/doc/${pkgname} 
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh

  runhaskell Setup.lhs copy --destdir=$pkgdir

  # Requires pandoc, regex-posix, Pretty and cabal
  #runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE
  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg
  install -D -m644 $srcdir/xmonad.desktop $pkgdir/usr/share/xsessions/xmonad.desktop
}
