# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libusb
pkgver=1.0.8
pkgrel=2
pkgdesc="Library to enable user space application programs to communicate with USB devices. (32-bit)"
arch=('x86_64')
url="http://libusb.sourceforge.net/"
license=('LGPL')
depends=('lib32-glibc' 'libusb')
makedepends=('gcc-multilib')
source=(http://downloads.sourceforge.net/libusb/libusb-${pkgver}.tar.bz2)
options=(!libtool)
md5sums=('37d34e6eaa69a4b645a19ff4ca63ceef')

build() {
  cd "${srcdir}/libusb-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32

  make
}

package () {
  cd "${srcdir}/libusb-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/include
}

