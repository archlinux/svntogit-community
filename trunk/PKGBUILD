# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgbase=python-pygit2
pkgname=(python-pygit2 python2-pygit2)
pkgver=0.21.4
pkgrel=1
pkgdesc='Python bindings for libgit2'
url="https://github.com/libgit2/pygit2"
depends=(libgit2)
makedepends=(python-cffi python2-cffi python-setuptools python2-setuptools)
arch=(i686 x86_64)
license=('GPL2')
source=("https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz"
        0001-Fix-data-type-of-options-in-init_repository.patch)
md5sums=('88a91e9e445d4b7a7a514528a7c7db5a'
         '5ac0dbb060f98031bf5de851f33a1125')

prepare() {
  cd "pygit2-$pkgver"
  patch -p1 -i ../0001-Fix-data-type-of-options-in-init_repository.patch
}

package_python-pygit2() {
  cd "pygit2-$pkgver"
  depends+=(python python-cffi)
  replaces=(python-libgit2)
  conflicts=(python-libgit2)
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  cd "pygit2-$pkgver"
  depends+=(python2 python2-cffi)
  replaces=(python2-libgit2)
  conflicts=(python2-libgit2)
  python2 setup.py install --root="$pkgdir" --optimize=1
}
