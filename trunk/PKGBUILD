# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: chiku <chirantan DOT mitra AT gmail DOT com>

pkgname=ucblogo
pkgver=6.0
pkgrel=7
pkgdesc='Portable interpreter for the Logo programming language'
url='https://www.cs.berkeley.edu/~bh/logo.html'
arch=('x86_64')
license=('GPL')
depends=('libx11' 'ncurses' 'bash' 'libsm')
source=("https://www.cs.berkeley.edu/~bh/downloads/$pkgname.tar.gz"
        "$pkgname-$pkgver-no-wxWidgets.patch"
        "$pkgname-$pkgver-no-texinfo.patch"
        "$pkgname-$pkgver-include-math.h-earlier.patch"
        'logo.sh')
sha256sums=('c659e989e9284efc0f904ebd7de24b559c2d7434a98bb9c92c23e6c11b49cef3'
            'e360ac71ac785fa8b00b89ff0c5bf8ed738114013143245d3bc0e62eb7d0333d'
            'e83fb9d656a9064685cde5ebc0ee36242ac848228380b0567fe683901435707c'
            '1e33b748bdf74550963c4052bf6feab8019cea34afa7b0d4eac71aa60c486583'
            '3dcf0a5472acda2343ff7d4942ff6589149929a50af92b0994fbb0520d2a3849')

prepare() {
  patch -Np1 -i "$pkgname-$pkgver-no-wxWidgets.patch"
  patch -Np1 -i "$pkgname-$pkgver-no-texinfo.patch"
  patch -Np0 -i "$pkgname-$pkgver-include-math.h-earlier.patch"
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  # csls/CVS interferes with make
  rm -r "$srcdir/$pkgname-$pkgver/csls/CVS"
  make prefix="$pkgdir/usr" INFODIR="$pkgdir/usr/share/info" install
  mv "$pkgdir/usr/bin/logo" "$pkgdir/usr/bin/logo.elf"
  install -Dm755 "$srcdir/logo.sh" "$pkgdir/usr/bin/logo"
  install -d "$pkgdir/usr/share/doc"
  mv "$pkgdir/usr/lib/logo/docs" "$pkgdir/usr/share/doc/$pkgname"
  rm "$pkgdir/usr/share/doc/$pkgname/usermanual.ps"
}

# vim:set ts=2 sw=2 et:
