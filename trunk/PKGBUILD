# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: chiku <chirantan DOT mitra AT gmail DOT com>

pkgname=ucblogo
pkgver=6.0
pkgrel=5
pkgdesc='Portable interpreter for the Logo programming language'
url='http://www.cs.berkeley.edu/~bh/logo.html'
arch=('x86_64' 'i686')
license=('GPL')
depends=('libx11' 'ncurses')
source=("http://www.cs.berkeley.edu/~bh/downloads/$pkgname.tar.gz"
        "$pkgname-$pkgver-no-wxWidgets.patch"
        "$pkgname-$pkgver-no-texinfo.patch"
        'logo.sh')
sha256sums=('c659e989e9284efc0f904ebd7de24b559c2d7434a98bb9c92c23e6c11b49cef3'
            'e360ac71ac785fa8b00b89ff0c5bf8ed738114013143245d3bc0e62eb7d0333d'
            'e83fb9d656a9064685cde5ebc0ee36242ac848228380b0567fe683901435707c'
            '3dcf0a5472acda2343ff7d4942ff6589149929a50af92b0994fbb0520d2a3849')

prepare() {
  patch -Np1 -i "$pkgname-$pkgver-no-wxWidgets.patch"
  patch -Np1 -i "$pkgname-$pkgver-no-texinfo.patch"
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # csls/CVS interferes with make
  rm -r "$srcdir/$pkgname-$pkgver/csls/CVS"
  make prefix="$pkgdir/usr" INFODIR="$pkgdir/usr/share/info" install
  mv "$pkgdir/usr/bin/logo" "$pkgdir/usr/bin/logo.elf"
  install -Dm755 "$srcdir/logo.sh" "$pkgdir/usr/bin/logo"
  install -d "$pkgdir/usr/share/doc"
  mv "$pkgdir/usr/lib/logo/docs" "$pkgdir/usr/share/doc/$pkgname"
  rm "$pkgdir/usr/share/doc/$pkgname/usermanual.ps"
}

# vim:set ts=2 sw=2 et:
