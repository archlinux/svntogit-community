# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-parsel
pkgname=('python-parsel' 'python2-parsel')
pkgver=1.3.1
pkgrel=1
pkgdesc="Parsel is a library to extract data from HTML and XML using XPath and CSS selectors"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/parsel"
makedepends=('python-cssselect' 'python2-cssselect' 'python-lxml' 'python2-lxml'
             'python-pytest-runner' 'python2-pytest-runner' 'python-six' 'python2-six'
             'python-w3lib' 'python2-w3lib')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/scrapy/parsel/archive/v$pkgver.tar.gz")
sha512sums=('ddb7c1b5221e3399c4d2fb6fec7695f8303be5f4f7aa45f5bb246f072354543023d9b950b933f47b9c3c3f547e6fd2e3b005039f7b39650fe9a3c54489c7f3f0')

prepare() {
  cp -a parsel-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/parsel-$pkgver
  python setup.py build

  cd "$srcdir"/parsel-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parsel-$pkgver
  python setup.py pytest

  cd "$srcdir"/parsel-$pkgver-py2
  python2 setup.py pytest
}

package_python-parsel() {
  depends=('python-cssselect' 'python-lxml' 'python-six' 'python-w3lib')

  cd parsel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-parsel() {
  depends=('python2-cssselect' 'python2-lxml' 'python2-six' 'python2-w3lib')

  cd parsel-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
