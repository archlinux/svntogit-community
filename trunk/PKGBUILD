# $Id$
# Maintainer: Anatol Pomozov <anatol pomozov at gmail> 
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Konstantin Plotnikov <kostyapl at gmail dot com>

pkgname=fossil
pkgver=1.31
_sha1=2e7c40dbdd68c5b21ef982d66bd24246f50a0110 # check project's timeline for it https://www.fossil-scm.org/fossil/timeline?y=ci
# NOTE: update _date_pkgver from http://www.fossil-scm.org/download.html
pkgrel=1
pkgdesc='Simple, high-reliability, distributed software configuration management'
arch=(i686 x86_64)
license=(BSD)
url='http://www.fossil-scm.org'
depends=(openssl zlib)
backup=(etc/xinetd.d/fossil)
source=(
  fossil.socket
  fossil.service
  fossil-xinetd
  fossil-$pkgver.tar.gz::https://www.fossil-scm.org/fossil/tarball/Fossil-$_sha1.tar.gz?uuid=$_sha1
)
sha256sums=('f885e17998dc1eece1688a75e516663462fe72a7f4f132def4132055777c7ff8'
            'c4973079facf209d3757db81df08f8d0843ede242f2a0c12281720da65e3b166'
            '2f9a141fadfb7cb5225f0d65a308949bbc592bff9d567b5c472edb413c81f5aa'
            '3ebf2b4ce6057a3ff9ddb44704daee2ffd0d271de33449b25c7ff980deae08a9')

build() {
  cd Fossil-$_sha1
  ./configure --prefix=/usr
  # headers and translate targets are problematic with parallel jobs
  #make -j1 bld bld/headers
  make
}

package() {
  install -Dm644 fossil-xinetd "$pkgdir"/etc/xinetd.d/fossil
  install -Dm644 fossil.socket "$pkgdir"/usr/lib/systemd/system/fossil.socket
  install -Dm644 fossil.service "$pkgdir"/usr/lib/systemd/system/fossil@.service

  cd Fossil-$_sha1
  install -Dm755 fossil "$pkgdir"/usr/bin/fossil
  install -Dm644 tools/fossil-autocomplete.bash "$pkgdir"/usr/share/bash-completion/completions/fossil
  install -Dm644 COPYRIGHT-BSD2.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
