# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Matthias Blaicher <matthias@blaicher.com>
# Contributor: Severen Redwood <severen@shrike.me>

pkgname=capnproto
pkgver=0.10.1
pkgrel=1
pkgdesc="Cap'n Proto serialization/RPC system"
arch=(x86_64)
url="https://capnproto.org/"
license=(MIT)
depends=(gcc-libs glibc openssl zlib)
makedepends=(cmake)
provides=(
  libcapnp.so
  libcapnp-json.so
  libcapnp-rpc.so
  libcapnpc.so
  libkj-async.so
  libkj-gzip.so
  libkj-http.so
  libkj-test.so
  libkj-tls.so
  libkj.so
)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('e97afc9799317e6f5c5221784a41c00eb65c61ba5092a0cdbc8c20b4c5088fc71f7ff0dc9eaed2b81492cffbf9d81a347f193b3f2dde30ee9057d68f921c4792')
b2sums=('d1d168ef3523feda1fed5f6c5c8dd1b32807273e6108610790ef82d3e6dffadc3a191317a0afa69e8433b1da140f09bc09c98c4ebbe36ee34021da0c4e058358')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{CONTRIBUTORS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
