# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Daniel Maslowski <info@orangecms.org>

pkgbase=pycosat
pkgname=(python-pycosat)
pkgver=0.6.4
pkgrel=2
arch=(x86_64)
url="https://github.com/ContinuumIO/pycosat"
pkgdesc="Python bindings to picosat (a SAT solver)"
license=(MIT)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
source=(https://github.com/ContinuumIO/$pkgbase/archive/$pkgver/$pkgbase-$pkgver.tar.gz)
sha256sums=('79ffa99156810e58876dd6374af0b7bff88f9a4580ca8b8468c414c5c2454bb8')

build() {
  cd $pkgbase-$pkgver
  python -m build --wheel --no-isolation
}

package_python-pycosat() {
  cd $pkgbase-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
