# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Valentin HÄƒloiu <vially.ichb+aur@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kevin Zuber <uKev@knet.eu>
# Contributor: Vsevolod Balashov <vsevolod@balashov.name>
# Contributor: David Runge <dave@sleepmap.de>

pkgbase=uwsgi
pkgname=(
  uwsgi
  uwsgi-plugin-rack
  uwsgi-plugin-psgi
  uwsgi-plugin-cgi
  uwsgi-plugin-python
  uwsgi-plugin-pypy
  uwsgi-plugin-php
  uwsgi-plugin-php-legacy
  uwsgi-plugin-lua51
  uwsgi-plugin-mono
  uwsgi-plugin-webdav
  uwsgi-plugin-zabbix
  uwsgi-plugin-notfound
)
pkgver=2.0.21
pkgrel=5
pkgdesc="A full stack for building hosting services"
arch=(x86_64)
url="https://uwsgi-docs.readthedocs.io/en/latest/"
license=(GPL2)
makedepends=(
  jansson
  libxcrypt
  libxml2
  lua51
  mono
  openssl
  pam
  pcre
  perl
  php-embed
  php-legacy-embed
  pypy
  python
  python-gevent
  python-greenlet
  ruby
  systemd
  util-linux
  zlib
)
source=(
  $pkgbase-$pkgver.tar.gz::https://github.com/unbit/$pkgbase/archive/refs/tags/$pkgver.tar.gz
  archlinux.ini
  emperor.ini
  emperor.uwsgi.service
  emperor.uwsgi.socket
  uwsgi_at.service
  uwsgi_at.socket
  uwsgi.logrotate
  uwsgi.sysusers
  uwsgi.tmpfiles
  $pkgbase-1.9.13-ruby2.0.patch
  $pkgbase-2.0.22-php8.2.patch
)
sha512sums=('36a9c1b87a4c3d08482b9045d2227f56b006acb53f38ddf1b510880ae5fc24c0177a077338ec8af3ef0b8f8e220bc4fc7f8311dab8066e13cbcbb616f736c795'
            '912b2d85cee0907454f2b1eee79fdeb7d7205c7b6a9f2a05cef969205fd4f3ccb9f3260e71a1c19cc910f6408e1e94da93fcca9978e8432273659b7caed34d9a'
            'd54c84838ddb2f389e115ebb81aa2583705c9e330f020e6583b496f9c271ed236b6820c2a065f2b55a79adbf13e262b9ff2428124a8044b8fa20ca29ca4930ac'
            '068ada6281fdeb504fed7e47dae2729d047ed810bb596e5268c1821ad275cd32110e801e93e7ba0ee2d92dbc39e1954180cf43d88d2990d42b802ceb54b50a9e'
            '592c85270f43b37e9c51aae126533830515b96fff9fcae969ee865fe173bd0550c7729530d5ff1e44ffee43cd5c78c96688ce091c677a4196379e24983c0180e'
            '29e51d3f441bd8af23c9378bca6cb38376bbbf31bb3b07f318a04ea5620cf25d35d7799ba4a8ecff12634dccd903eef27a6957b932370706e35549f8fbcd2455'
            'fb51a4d102a1bc7d9f7d4c4f310ee65076bcfbc94b47e2dafdf9b20e99a661c5f938185dcf698437d81409376f0276b966f09b13c8a29462c48612df43c3f3ef'
            '937878372aa1556f6ba62ad7148d1681288c94d6cff609368a9e861dd4d4524330006a08ae5993441f7d3101170e3a0a681a4c8b3c2c13b364b8b1f81cf25117'
            '9e9eab08199cf08810ec95b0697cd8817226986d1e3aeb54845c0140ea5887360580348f295060c0558fb2d6aaed26fec929ea36b28cfeaf2b3588ce40fec3df'
            '4def9dcd06cfc2c6ce554add9d9545a01f3bb2681f0a2a6fc4ba68f91011111803a955cc41b5e70832ca448b196109368fdf874e81b7ec0edee4b51f864e16f7'
            '162ca1be96282b32e8e6be919b6315bfd08954c491056958985bf99e7f5bdad3511665f059329d6cb6ef5d222f9aa11d4dc43c72e6a963ab941ada959fe8d964'
            'd62db75b0e27bbc014ec5e69c360de79f523f33bc1ab15fe7957bf8531635be1bb77200d1ad6d50184eba18c6d56bcdd284082c042792ef7822233128bc22fbc')
b2sums=('3c5fc3ce3a6a7f8f915bb302c0b775a75227f859dd2b141034e8b1aa5cb867b447ed532b3e8b537db80acc908fc26121b752e35ebc0bf9a542678843fd02ff54'
        '41a9213d55665c905f71f07c752a74fc7685b2f191381d24f3e1d9691a8dae538aa4344ebe771ffd77ba3c9cd7c52fd9eff54ff876bc399d92256cb86f5b7d53'
        '835a1036422e9821cd52ffdf5a6053bdd59fc700d473600bda60b6730746f6c0750c30c85f4139d3ea81c6b6dd10d6849f9406decee3179861c51aca90da6161'
        '80aa3c4d3f0310f5029ac01690d2ba718d8c86fa49cd1b4f3fc1bd5ae9eadaed6562bf0a48a0fd611e92f100a8594e08fb4bae58e25765fc2eb648cdf1c60b35'
        'b6838c0c91811d4b677bf0f558134c69015708c308ef7080df328f12b0b0d58ae6cdebe277c29b6d11c20cfeb0847a4934e3a8924323e57ed661d6e9f6656a34'
        '6587282d48c53cff862951503c560fd3bfe2fbfe18d56af7b5fbf8299b2b99125101a866005ef1fbabe6af9640c200c5121949b3723865d8597a948ed3d650e8'
        '24c61581bb922f8065a6ea73164ca5d0b6c63ab4ca7c265660b2ec257b69ff9dd4b0349572ff20f54950961b4286c77da36c1d2778698c80943cfd6ed6531b10'
        'a8b7d003f66ed5c5b1b60d57c05339af3dcdcf346eab52760810086e17f6936dd29ed7fd26281657bc86ac6721c989cbfbaa8e81f97111c807bcd06baece3bf2'
        '82e03ea95995cfa78abc4d930bd0c250f22788d9ca4dc0921cfbb44a965c0f851cbcc7a855e36ec6e6aa5bf33fbd313b4fe68a2f1c4b7935c2b297be25895324'
        '9759827f7619358383bf0e68c955522c0054620f742f5122a59888fadfc0bc20bd587eab7a77bc73e8ee4d20cf8f02625c07f241462e8ab9c08f5af509daaef3'
        '0068763ca5a2f1b60782f53070cbff8cf930e1463d9199b156b02413026bcdb3d923bffcbba063a26d620b8e734bdbd26a4c22776532546ad8c8e5c0c73bd7d6'
        '99ddfb9918395d9d04284c6ab5160986d1aa7cdb175237ff5d2af8438a7a78b535836dee3c16dd0b8c917a00fdc9e64880756595b3070b941c406d99580c102c')

prepare() {
  cd $pkgbase-$pkgver

  # allow using uwsgi-plugin-rack with ruby >= 2.0
  patch -Np1 -i "$srcdir"/uwsgi-1.9.13-ruby2.0.patch

  # fix default lua pkgconfig name
  sed -e 's/lua5.1/lua51/' -i plugins/lua/uwsgiplugin.py
  # move ruby plugin to new location and fix its name
  mv plugins/ruby{19,}
  sed -e 's/ruby19/ruby/' -i plugins/ruby/uwsgiplugin.py
  # remove the explicit lib version from the linking stage of the PHP plugin as
  # we do not provide /usr/lib/libphp8.so
  sed -e "s/ + php_version//" -i plugins/php/uwsgiplugin.py
  # Compatibility with PHP 8.2
  patch -p1 -i "$srcdir/$pkgbase-2.0.22-php8.2.patch"
  # duplicate the php plugin, so that we can modify it
  cp -av plugins/php{,_legacy}
  # link the php_legacy plugin against libphp-legacy.so
  sed -e 's/lphp/lphp-legacy/g; s/UWSGICONFIG_PHPPATH/UWSGICONFIG_PHP_LEGACY_PATH/g; s/NAME=.*/NAME="php_legacy"/g' -i plugins/php_legacy/uwsgiplugin.py
  sed -e 's/php_plugin/php_legacy_plugin/g' -i plugins/php_legacy/php_plugin.c
  # copy our custom build profile into place
  cp -v ../archlinux.ini buildconf/
}

build() {
  cd $pkgbase-$pkgver
  export UWSGICONFIG_PHP_LEGACY_PATH='php-config-legacy'
  python uwsgiconfig.py --verbose --build archlinux
}

package_uwsgi() {
  depends=(glibc jansson libcap.so libcrypt.so libpam.so libxml2
  libsystemd.so libuuid.so openssl pcre zlib)
  backup=('etc/uwsgi/emperor.ini')
  install=uwsgi.install

  cd $pkgbase-$pkgver
  install -vDm 755 "${pkgbase}" -t "$pkgdir/usr/bin/"
  install -vDm 644 ../uwsgi_at.service "$pkgdir"/usr/lib/systemd/system/uwsgi@.service
  install -vDm 644 ../uwsgi_at.socket "$pkgdir"/usr/lib/systemd/system/uwsgi@.socket
  install -vDm 644 ../emperor.uwsgi.service "$pkgdir"/usr/lib/systemd/system/emperor.uwsgi.service
  install -vDm 644 ../emperor.uwsgi.socket "$pkgdir"/usr/lib/systemd/system/emperor.uwsgi.socket
  install -vDm 644 ../emperor.ini -t "$pkgdir"/etc/uwsgi/
  install -vDm 644 ../uwsgi.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/uwsgi.conf
  install -vDm 644 ../uwsgi.logrotate "$pkgdir"/etc/logrotate.d/uwsgi
  install -vDm 644 ../uwsgi.sysusers "$pkgdir"/usr/lib/sysusers.d/uwsgi.conf
}

package_uwsgi-plugin-cgi() {
  pkgdesc+=" (CGI plugin)"
  depends=(glibc uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 cgi_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-rack() {
  depends=(glibc ruby uwsgi)
  pkgdesc="Ruby rack plugin"

  cd $pkgbase-$pkgver
  install -vDm 755 {rack,fiber,rbthreads}_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-psgi() {
  pkgdesc+=" (Perl psgi plugin)"
  depends=(glibc perl uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 psgi_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-python() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")

  pkgdesc+=" (Python plugin)"
  depends=(glibc python python-gevent python-greenlet uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 {asyncio,gevent,greenlet,python}_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
  install -vDm 644 uwsgidecorators.py -t "${pkgdir}${site_packages}"
  python -m compileall "${pkgdir}${site_packages}"
  python -O -m compileall "${pkgdir}${site_packages}"
}

package_uwsgi-plugin-pypy() {
  pkgdesc+=" (PyPy plugin)"
  depends=(glibc pypy uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 pypy_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
  install -vDm 644 uwsgidecorators.py -t "$pkgdir"/opt/pypy/site-packages/
  pypy -m compileall "$pkgdir"/opt/pypy/site-packages/
  pypy -O -m compileall "$pkgdir"/opt/pypy/site-packages/
}

package_uwsgi-plugin-lua51() {
  pkgdesc+=" (LUA plugin)"
  depends=(glibc lua51 uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 lua_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-php() {
  pkgdesc+=" (PHP plugin)"
  depends=(glibc php-embed uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 php_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-php-legacy() {
  pkgdesc+=" (PHP Legacy plugin)"
  depends=(glibc php-legacy-embed uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 php_legacy_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-mono() {
  pkgdesc+=" (mono plugin)"
  depends=(glibc mono uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 plugins/mono/uwsgi.dll -t "$pkgdir"/usr/lib/mono/2.0/
  install -vDm 755 mono_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-webdav() {
  pkgdesc+=" (WebDav plugin)"
  depends=(glibc libxml2 uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 webdav_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-zabbix() {
  pkgdesc+=" (zabbix plugin)"
  depends=(glibc uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 zabbix_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}

package_uwsgi-plugin-notfound() {
  pkgdesc+=" (notfound plugin)"
  depends=(uwsgi)

  cd $pkgbase-$pkgver
  install -vDm 755 notfound_plugin.so -t "$pkgdir"/usr/lib/uwsgi/
}
