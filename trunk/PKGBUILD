# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-unikey
pkgver=5.0.7
pkgrel=1
pkgdesc="Unikey engine support for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-unikey"
license=('GPL')
depends=('fcitx5-qt')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('c9843e18ee039deedbade110e34ae04af4e24e4721692f376361f1fef62a3f379b011a809c81b2a4b03c7357bbbf204cb4c9d333b12d5738506e92fab40437e4'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
