# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-oslo-serialization
pkgver=3.1.0
pkgrel=1
pkgdesc="Oslo Serialization library"
arch=('any')
url="https://docs.openstack.org/developer/oslo.serialization"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-msgpack' 'python-oslo-utils'
         'python-pytz')
makedepends=('python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.serialization/archive/$pkgver.tar.gz")
sha512sums=('449255ab5c07bd753942df4075725b8b0a6aa079ddf0a6f384ebe8751f3222df85eb63bd845fafe36ca64cf52f4fb0f51dae1f6d3a7faabf7981721eb9893b27')

export PBR_VERSION=$pkgver

build() {
  cd oslo.serialization-$pkgver
  python setup.py build
}

check() {
  cd oslo.serialization-$pkgver
  stestr run
}

package() {
  cd oslo.serialization-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
