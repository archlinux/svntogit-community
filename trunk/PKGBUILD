# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cymem
pkgver=2.0.6
pkgrel=1
pkgdesc="Cython memory pool for RAII-style memory management"
url="https://github.com/explosion/cymem"
license=('MIT')
arch=('x86_64')
depends=('cython' 'python')
makedepends=('python-pip' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/explosion/cymem/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1f4e560413ee574501c0d286fcf6766e96898a6becbf1071fc788cb0b7d259d19e1c27b2d3fa09f47a217eaebae79a718ebfd8ee328e429a570d3d88114a6933')

build() {
  cd cymem-$pkgver
  python setup.py build
}

check() {
  cd cymem-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" pytest
}

package() {
  cd cymem-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
