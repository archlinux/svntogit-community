# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: David Keogh <davekeogh@archlinux.us>

pkgname=pycharm-community-edition
pkgver=2017.3.2
pkgrel=1
pkgdesc='Python IDE for Professional Developers'
arch=('x86_64')
url='http://www.jetbrains.com/pycharm/'
license=('Apache')
depends=('gcc-libs' 'giflib' 'glibc' 'java-runtime=8' 'sh' 'ttf-font')
optdepends=('ipython2: IPython integration for Python 2'
            'ipython: IPython integration for Python 3'
            'python2: Python 2 support'
            'python: Python 3 support')
makedepends=('python2-setuptools' 'python-setuptools')
conflicts=('pycharm' 'pycharm-community' 'pycharm-professional')
provides=('pycharm')
source=("https://download.jetbrains.com/python/pycharm-community-${pkgver}.tar.gz"
        'pycharm.desktop')
sha256sums=('06fbe5ffca6e27842ba71ac62eba76a976f3ee2651568e151c59482ed486e4f6'
            '28e0d3200c721e61831d8246eba8f72485d3c8379d10bfa75ccb5331b6749480')

build() {
  cd pycharm-community-${pkgver}

  python2 helpers/pydev/setup_cython.py build_ext --inplace
  python3 helpers/pydev/setup_cython.py build_ext --inplace
  rm -rf bin/fsnotifier{,-arm} lib/libpty/linux/x86
}

package() {
  cd pycharm-community-${pkgver}

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,doc,licenses,pixmaps,pycharm}}
  cp -dr --no-preserve='ownership' bin helpers lib plugins "${pkgdir}"/usr/share/pycharm/
  cp -dr --no-preserve='ownership' help "${pkgdir}"/usr/share/doc/pycharm/
  cp -dr --no-preserve='ownership' license "${pkgdir}"/usr/share/licenses/pycharm/
  ln -s /usr/share/pycharm/bin/pycharm.sh $pkgdir/usr/bin/pycharm
  ln -s /usr/share/pycharm/bin/pycharm.png "${pkgdir}"/usr/share/pixmaps/
  install -m 644 ../pycharm.desktop -t "${pkgdir}"/usr/share/applications/
}

# vim: ts=2 sw=2 et:
