# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-perl-critic
pkgver=1.144
pkgrel=1
pkgdesc="Critique Perl source code for best-practices"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Perl-Critic"
depends=('perl-b-keywords' 'perl-config-tiny' 'perl-exception-class' 'perl-file-homedir'
         'perl-file-which' 'perl-io-string' 'perl-list-someutils' 'perl-module-build'
         'perl-module-pluggable' 'perl-ppi' 'perl-ppix-quotelike' 'perl-ppix-regexp'
         'perl-ppix-utilities' 'perl-pod-spell' 'perl-tidy' 'perl-readonly' 'perl-string-format'
         'perl-task-weaken')
makedepends=('perl-test-deep')
checkdepends=('perl-test-memory-cycle')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PE/PETDANCE/Perl-Critic-$pkgver.tar.gz")
sha512sums=('7416c04ff1592206a2ed281b08a62811b86be4045db33a8bb06d5525d72c329afe9d5ddf95bd97a3d17883ed45f4011764f656a03c13f0e5f6c61e456082d299')

build() {
  cd Perl-Critic-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Perl-Critic-$pkgver
  perl Build test
}

package() {
  cd Perl-Critic-$pkgver
  perl Build install destdir="$pkgdir"
}
