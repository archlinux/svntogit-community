# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
_pkgver=1.6.0
#_fixupver=2
pkgver=${_pkgver}
#pkgver=${_pkgver}.r${_fixupver}
pkgrel=1
pkgdesc='Full featured and easy to use XMPP (Jabber) client'
url='https://gajim.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'gtksourceview4' 'python-cairo' 'python-gobject' 'python-keyring' 'python-nbxmpp'
         'python-cryptography' 'python-precis_i18n' 'python-css-parser'
         'python-distro' 'hicolor-icon-theme' 'python-pillow' 'python-gssapi' 'python-netifaces'
         'python-packaging')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
optdepends=('farstream: start audio and video chat'
            'gst-plugins-good: for video/voice support'
            'gst-plugins-bad: for video/voice support'
            'gst-plugins-ugly: for video/voice support'
            'gst-libav: for video/voice support'
            'gst-python: for video/voice support'
            'gspell: for spell checking support'
            'geoclue: share current location'
            'libxss: measure idle time, in order to set auto status'
            'python-gnupg: encrypting chat messages with OpenPGP'
            'python-axolotl: OMEMO support'
            'python-qrcode: generate QR codes for OMEMO keys'
            'gsound: Notification Sounds')
source=(https://www.gajim.org/downloads/${_pkgver%.*}/gajim-${_pkgver}.tar.gz)
#source=(https://www.gajim.org/downloads/${_pkgver%.*}/gajim-${_pkgver}-${_fixupver}.tar.gz)
sha512sums=('0fc238102a67eea0721b3d2d717d60f518b19e39f1245f108b653214d154b6ba063c7481c4449d917f77551106e6199169b8a9a00285a217a2bc54cba632cc8c')
b2sums=('ce199a77f21f5fc430ce2e50a93e7465709b49b672413a928be29521c34693efa05b853f790942e86989eba78caad08fc538171f24b98c18f30859703684dba1')

build() {
  cd ${pkgname}-${_pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${pkgname}-${_pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim: ts=2 sw=2 et:
