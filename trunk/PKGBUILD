# $Id: PKGBUILD 264278 2017-10-25 15:45:36Z spupykin $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=psi-l10n
pkgver=1.3
pkgrel=2
pkgdesc="Language packs for Psi"
arch=('any')
url="http://psi-plus.com"
license=('GPL2')
depends=('psi')
conflicts=('psi-plus-l10n')
replaces=('psi-plus-l10n')
makedepends=('qt5-tools')
options=('!strip' '!zipman')
source=("$pkgname-$pkgver.tar.gz::https://github.com/psi-im/psi-l10n/archive/$pkgver.tar.gz")
sha256sums=('122fb6e45e511fe96d8a84beb86e8e6fcb11500f468f9e97b52fddf193041378')

build() {
	cd psi-l10n-$pkgver/translations
	for langfile in *.ts; do
		lrelease-qt5 "$langfile"
	done
}

package() {
	cd psi-l10n-$pkgver/translations
	install -v -dm755 "$pkgdir/usr/share/psi-plus/translations"
	install -v -m644 *.qm "$pkgdir/usr/share/psi-plus/translations"
}
