# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.236
pkgrel=1
pkgdesc="An extremely fast Python linter, written in Rust"
arch=(x86_64)
url="https://github.com/charliermarsh/ruff"
license=(MIT)
depends=(gcc-libs glibc python)
makedepends=(cargo maturin python-installer)
options=(!lto)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('340242d90798f31debd558b36ebec8ba3b42e48965bdf8b1ebfb27ecfa66dae0be6195b17ed1e2da0c0e3eff83f4d680179ae7c261dd24dc9e76e3f9a24c5bfc')
b2sums=('9592320ca9df5f2954b43045d6f029da27629ec4be349d876c1c7db7616b115cda2f3a106b3555360d44eac9c3920984074ac4ed943a51e66f53982973adab44')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  local maturin_options=(
    --release
    --strip
    --locked
    --all-features
    --target $CARCH-unknown-linux-gnu
  )

  cd $pkgname-$pkgver

  maturin build "${maturin_options[@]}"
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
