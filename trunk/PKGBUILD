# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-script
pkgver=1.27
pkgrel=1
pkgdesc="Basic cross-platform tests for scripts"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test-Script"
depends=('perl-capture-tiny' 'perl-probe-perl' 'perl-test2-suite')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Test-Script-$pkgver.tar.gz")
sha512sums=('7215a6b22fb499a5963e823e25dad75ed42edc090bbf1f713938d1c36a5f04414aae4fd95ccd32ae98c3f5128fdf84cecc48e2d64cffad184e2b650b2e7e9be5')

build() {
  cd Test-Script-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Script-$pkgver
  make test
}

package() {
  cd Test-Script-$pkgver
  make DESTDIR="$pkgdir" install
}
