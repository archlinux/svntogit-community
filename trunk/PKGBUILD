# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=libsndfile
pkgname=lib32-$_pkgbasename
pkgver=1.0.25
pkgrel=3
pkgdesc="A C library for reading and writing files containing sampled sound (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('lib32-alsa-lib' 'lib32-flac' 'lib32-libvorbis' $_pkgbasename)
checkdepends=('python2')
makedepends=('gcc-multilib' 'libtool-multilib')
source=(http://www.mega-nerd.com/libsndfile/files/${_pkgbasename}-${pkgver}.tar.gz{,.asc})
validpgpkeys=(73571E85C19F4281D8C97AA86CA41A7743B8D6C8)
sha1sums=('e95d9fca57f7ddace9f197071cbcfb92fa16748e'
          'a93d26016730cb9fa214c753d079ee77dca1f326')

prepare() {
  cd ${_pkgbasename}-${pkgver}
  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' src/binheader_writef_check.py \
    src/create_symbols_file.py programs/test-sndfile-metadata-set.py
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  export GETCONF="getconf -v POSIX_V7_ILP32_OFFBIG"
  export GETCONF_DIR="/usr/lib32/getconf"

  ./configure --prefix=/usr --disable-sqlite --libdir=/usr/lib32
  make -C src
}

check() {
  cd ${_pkgbasename}-${pkgver}
  make check
}

package() {
  cd ${_pkgbasename}-${pkgver}
  make -C src DESTDIR="${pkgdir}" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA

  rm -rf "$pkgdir/usr/include"
}
