# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=performous
pkgver=0.6.1
pkgrel=10
pkgdesc='A free game like "Singstar", "Rockband" or "Stepmania"'
arch=('i686' 'x86_64')
url="http://performous.org/"
license=('GPL')
depends=('boost-libs>=1.47' 'sdl' 'jack' 'imagemagick' 'ffmpeg' 'glew>=1.7.0' 'libxml++' 'portaudio' 'portmidi' \
         'opencv' 'librsvg' 'libjpeg' 'libpng' 'cairo')
makedepends=('cmake' 'pkgconfig' 'help2man' 'boost>=1.47')
optdepends=('performous-freesongs: free songs for performous')
source=(http://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgver/Performous-$pkgver-Source.tar.bz2
        boost-filesystem-v3.patch)
md5sums=('451a759de77984b5a699e91107fe52e2'
         '42a8c825d80b0de16bd5752d2a80e585')

build() {
  cd ${srcdir}/Performous-${pkgver}-Source

  # fix to built against boost 1.46 and later, upstream (git) now support v3
  patch -Np1 -r - -i ../boost-filesystem-v3.patch

  mkdir -p build
  cd build

  # fix config loading with libxml++
  export LDFLAGS=${LDFLAGS/-Wl,--as-needed/}

  cmake -DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr ..

  make
}

package() {
  cd ${srcdir}/Performous-${pkgver}-Source/build

  make DESTDIR="$pkgdir" install
}
