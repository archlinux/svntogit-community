# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Matthias Blaicher <matthias@blaicher.com>
# Contributor: Severen Redwood <severen@shrike.me>

pkgname=capnproto
pkgver=0.10.2
pkgrel=3
pkgdesc="Cap'n Proto serialization/RPC system"
arch=(x86_64)
url="https://capnproto.org/"
license=(MIT)
depends=(gcc-libs glibc openssl zlib)
makedepends=(cmake)
provides=(
  libcapnp.so
  libcapnp-json.so
  libcapnp-rpc.so
  libcapnpc.so
  libkj-async.so
  libkj-gzip.so
  libkj-http.so
  libkj-test.so
  libkj-tls.so
  libkj.so
)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('079ab715ea19ac207f72bb4874ab6362c11dfb9128342ec937e3f4d27e4100530cf9e6a0275eaca1e45a1bb706b65179a526ea8cfceba93bb36cd6c4f1c0cf5b')
b2sums=('d78442bc122bc976fb636dfde694e10414bf3468ead903fac7ba59cbd81a49ce94969954fdc90f92f7f58f0af80f2da1f70de9b461d99fc6ae9a9e7aa9cd0500')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{CONTRIBUTORS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
