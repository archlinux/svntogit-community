# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.0.70
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('LGPL3')
provides=('deepin-qt5dxcb-plugin')
conflicts=('deepin-qt5dxcb-plugin')
replaces=('deepin-qt5dxcb-plugin')
options=('debug')
depends=('cairo' 'deepin-wayland' 'kwayland' 'qt5-x11extras')
makedepends=('expac' 'qt5-xcb-private-headers' 'libglvnd' 'libxcb')
groups=('deepin')
source=("https://github.com/linuxdeepin/qt5platform-plugins/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0a39733a013dc161946e93b9bace4a1f42c98f4d51741e80f911a53beec54bd6be919a39171b5478f2311f21871c4a621711c382d6a194f3f6f13e76c82646be')

prepare() {
  cd qt5platform-plugins-$pkgver

  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev

  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' xcb/linux.pri
  sed -i '/wayland/d' qt5platform-plugins.pro
}

build() {
  cd qt5platform-plugins-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5platform-plugins-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
