# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgbase=guvcview
pkgname=(guvcview guvcview-qt)
pkgver=2.0.5
pkgrel=4
pkgdesc="Simple interface for capturing and viewing video from v4l2 devices"
arch=('x86_64')
url="http://guvcview.sourceforge.net/"
license=('GPL3')
depends=('ffmpeg' 'gsl' 'gtk3' 'qt5-base' 'portaudio')
makedepends=('intltool')
source=("https://downloads.sourceforge.net/$pkgbase/$pkgbase-src-$pkgver.tar.gz"
        "ffmpeg4.patch")
sha256sums=('a86beb5993a8449ed3cbcc6ec2a238ef0b90138b6cbe2afab4456d37f44c41a0'
            'e4bb7fffafc77e9bb760fe33b4759acc311a4c35f6b92ededd7f247c8057f03a')

prepare() {
  cd $pkgbase-src-$pkgver

  # Fix build with ffmpeg4 (taken from Gentoo and extended)
  patch -Np1 -i ../ffmpeg4.patch

  cp -a ../$pkgbase{,-qt}-src-$pkgver
}

build() {
  # GTK+ version
  cd $pkgbase-src-$pkgver
  ./configure --prefix=/usr --disable-debian-menu
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  # Qt version
  cd ../$pkgbase-qt-src-$pkgver
  ./configure --prefix=/usr --disable-debian-menu --disable-gtk3 --enable-qt5
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_guvcview() {
  pkgdesc="Simple GTK+ interface for capturing and viewing video from v4l2 devices"
  depends=('ffmpeg' 'gsl' 'gtk3' 'portaudio')

  cd $pkgbase-src-$pkgver
  make DESTDIR="$pkgdir" install
}

package_guvcview-qt() {
  pkgdesc="Simple Qt interface for capturing and viewing video from v4l2 devices"
  depends=('ffmpeg' 'gsl' 'qt5-base' 'portaudio')
  conflicts=('guvcview')

  cd $pkgbase-qt-src-$pkgver
  make DESTDIR="$pkgdir" install
}
