# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=git-annex
pkgver=6.20180529
pkgrel=14
pkgdesc="Manage files with git, without checking their contents into git"
url="http://git-annex.branchable.com/"
license=("AGPL3")
arch=('x86_64')
depends=('git' 'rsync' 'ghc-libs' 'haskell-aeson' 'haskell-async' 'haskell-aws'
         'haskell-blaze-builder' 'haskell-bloomfilter' 'haskell-byteable' 'haskell-case-insensitive'
         'haskell-clientsession' 'haskell-concurrent-output' 'haskell-conduit' 'haskell-crypto-api'
         'haskell-cryptonite' 'haskell-data-default' 'haskell-dav' 'haskell-dbus'
         'haskell-disk-free-space' 'haskell-dlist' 'haskell-dns' 'haskell-edit-distance'
         'haskell-esqueleto' 'haskell-exceptions' 'haskell-fdo-notify' 'haskell-feed'
         'haskell-hinotify' 'haskell-hslogger' 'haskell-http-client' 'haskell-http-conduit'
         'haskell-http-types' 'haskell-ifelse' 'haskell-magic' 'haskell-memory'
         'haskell-monad-control' 'haskell-monad-logger' 'haskell-mountpoints'
         'haskell-network' 'haskell-network-info' 'haskell-network-multicast' 'haskell-network-uri'
         'haskell-old-locale' 'haskell-optparse-applicative' 'haskell-path-pieces'
         'haskell-persistent' 'haskell-persistent-sqlite' 'haskell-persistent-template'
         'haskell-quickcheck' 'haskell-random' 'haskell-regex-tdfa' 'haskell-resourcet'
         'haskell-safesemaphore' 'haskell-sandi' 'haskell-securemem' 'haskell-shakespeare'
         'haskell-socks' 'haskell-split' 'haskell-stm-chans' 'haskell-tagsoup'
         'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-tasty-rerun'
         'haskell-torrent' 'haskell-unix-compat' 'haskell-unordered-containers'
         'haskell-utf8-string' 'haskell-uuid' 'haskell-vector' 'haskell-wai' 'haskell-wai-extra'
         'haskell-warp' 'haskell-warp-tls' 'haskell-yesod' 'haskell-yesod-core' 'haskell-yesod-form'
         'haskell-yesod-static')
makedepends=('chrpath' 'ghc')
source=("git+https://git.joeyh.name/git/git-annex.git#tag=$pkgver"
        ghc-8.4.patch)
sha512sums=('SKIP'
            'b73ccbe31b1f792522f3e1a98c43cdd1d6bc92cb42a6ef4d71e1966465e55973b04dd4e23c5aecf6b09565a28c2ff43bcca32b50a4e8a2d1b71fc5f4633783cf')

prepare() {
  cd git-annex
  patch -p1 -i ../ghc-8.4.patch
}

build() {
  cd git-annex

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/$pkgname" \
    -fnetwork-uri -fconcurrentoutput -ftorrentparser \
    -f-androidsplice -f-android -fproduction -fpairing -fwebapp \
    -fassistant -fwebdav -fs3 -f-benchmark -fdbus -fmagicmime
  runhaskell Setup build
}

package() {
  cd git-annex
  runhaskell Setup copy --destdir="$pkgdir"
  make GHC="ghc -dynamic" BUILDER=true DESTDIR="$pkgdir" -j1 install-misc

  rm "$pkgdir"/usr/share/doc/git-annex/COPYRIGHT
  rmdir "$pkgdir"/usr/share/doc/git-annex "$pkgdir"/usr/share/doc
}
