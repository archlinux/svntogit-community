# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contributor: Jos? Luis Lafuente <jl@lafuente.me>
# Contributor: Arthur Vuillard <arthur@hashbang.fr>

pkgname='python-pew'
pkgver=1.2.0
pkgrel=7
pkgdesc="Python Env Wrapper, a set of tools to manage multiple virtual environments"
url="https://github.com/berdario/pew"
arch=('any')
license=('MIT')
depends=('python' 'python-virtualenv' 'python-virtualenv-clone')
makedepends=('python' 'python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/berdario/pew/archive/$pkgver.zip")
sha256sums=('58e4f04ba644080ef820c2e07d1704112727e392c2fd1d42cdc31e112ffc2345')

build(){
  cd "$srcdir/pew-$pkgver"
  python setup.py build
}

package() {
  cd "${pkgname##*-}-${pkgver}"
  #python3 setup.py install --prefix=/usr --root="$pkgdir"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -D -m644 "pew/shell_config/complete.fish" \
    "$pkgdir/usr/share/fish/vendor_completions.d/pew.fish"

  install -D -m644 "pew/shell_config/complete.bash" \
    "$pkgdir/usr/share/bash-completion/completions/pew"

  install -D -m644 "pew/shell_config/complete.zsh" \
    "$pkgdir/usr/share/zsh/site-functions/_pew"

  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
