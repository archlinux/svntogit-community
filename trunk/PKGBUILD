# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom
pkgver=0.8.0
pkgrel=1
pkgdesc='A graphical process/system monitor'
arch=('x86_64')
url='https://github.com/ClementTsang/bottom'
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('1595d74b086338ce740e53468ec627b5dd52ebaa49e490f2363dffcdd3b3b7d1f2c3ae357ff40ddfc79e0454bb280bf629566ab8c5f2f297688165d7d4478c99')
b2sums=('12b748fa9664998cfb617f2458bf1b297b1b33cb42854c082f702c75e301ae8859beab7b8746aa8671176948c9819f92ab34e09fbf558fbaaa338678505ba10f')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  BTM_GENERATE=true cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname-$pkgver"
  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs sample_configs "$pkgdir/usr/share/doc/$pkgname"
  rm -f "$pkgdir/usr/share/doc/$pkgname/docs/.gitignore"

  cd target/tmp/bottom

  # man page
  install -vDm644 -t "$pkgdir/usr/share/man/man1" manpage/btm.1

  # completions
  install -Dm644 completion/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
  install -Dm644 completion/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
  install -Dm644 completion/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}

# vim:set ts=2 sw=2 et:
