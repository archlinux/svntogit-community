# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=1.17
pkgrel=2
pkgdesc="A tool for monitoring webpages for updates"
arch=('any')
url="http://thp.io/2008/urlwatch/"
license=('BSD')
depends=('python2-futures')
source=("http://thp.io/2008/urlwatch/$pkgname-$pkgver.tar.gz")
md5sums=('fb15ca7d3723e3a385ee149d9b19c262')

prepare() {
  cd $pkgname-$pkgver

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' lib/urlwatch/*.py
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
