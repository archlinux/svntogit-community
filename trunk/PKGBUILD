# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-pyicu
pkgver=2.3
pkgrel=3
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
depends=('python' 'icu')
makedepends=('python-setuptools')
checkdepends=('python-fonttools' 'python-pytest')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('2bdbc7fb7c83e58c9d0d53dadf8fef92d8dec02e9eefebc75bac43c19f008ef2a3219d41b62c426c41a055bb4530b2f7ef567162660d6021b8d2bf9e99090f94')

build() {
  cd PyICU-$pkgver
  python setup.py build
}

check() {
  cd PyICU-$pkgver
  python setup.py test
}

package() {
  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
