# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=waylock
pkgver=0.3.3
pkgrel=2
pkgdesc="A simple screenlocker for wayland compositors"
arch=('x86_64')
url="https://github.com/ifreund/waylock"
license=('MIT')
depends=('gcc-libs' 'glibc')
makedepends=('git' 'pam' 'rust')
_commit=2391ef614783b06ce4b88b14fb4a636fbb2a91a6 # refs/tags/v0.3.3^{}
source=("git+https://github.com/ifreund/waylock#commit=${_commit}?signed")
md5sums=('SKIP')
validpgpkeys=('5FBDF84DD2278DB2B8AD8A5286DED400DDFD7A11') # Isaac Freund <ifreund@ifreund.xyz>

build() {
  cd "$pkgname"
  cargo build --release --locked --all-features --target-dir=target
}

check() {
  cd "$pkgname"
  cargo test --release --locked --target-dir=target
}

package() {
  depends+=('libpam.so')
  cd "$pkgname"
  install -vDm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {README.md,waylock.toml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
