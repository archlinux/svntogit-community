# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime
pkgver=2.10.1
pkgrel=5
pkgdesc='Bible study tool'
arch=('x86_64' 'i686')
url='http://www.bibletime.info/'
license=('GPL2')
depends=('sword' 'openssl' 'clucene' 'qtwebkit')
makedepends=('cmake' 'git')
install="$pkgname.install"
source=("git://github.com/bibletime/bibletime.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
	mkdir -p "$pkgname/build_dir"
	cd "$pkgname/build_dir"

	CXXFLAGS+=" -fpermissive"
	cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
      -DBT_FORCE_USE_QT4=TRUE \
      ..
	make
}

package() {
	make -C "$pkgname/build_dir" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
