# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-latex2mathml
pkgver=3.75.0
pkgrel=1
pkgdesc="Pure Python library for LaTeX to MathML conversion"
url="https://github.com/roniemartinez/latex2mathml"
license=('MIT')
arch=('any')
depends=('python-setuptools')
makedepends=('python-build' 'python-installer' 'python-poetry-core')
checkdepends=('python-pytest' 'python-multidict' 'python-xmljson')
source=("https://github.com/roniemartinez/latex2mathml/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d0cff5f642010882c8ec7e0ad5a87a6e70cc041b5d9534423368ce2fd4838020b4ddf1ce618c5e5bd4e147483d2d3626e237a078fda13cc5bda254c1b96648d6')

prepare() {
  cd latex2mathml-$pkgver
  sed -i '/--cov/d' pyproject.toml
}

build() {
  cd latex2mathml-$pkgver
  python -m build -nw
}

check() {
  cd latex2mathml-$pkgver
  pytest
}

package() {
  cd latex2mathml-$pkgver
  python -m installer -d "$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
