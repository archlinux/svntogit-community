# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Michael DÃ¼ll <mail@akurei.me>

pkgname=rtl-sdr
# they are just starting to play with tags
# consider using tags for the pkgver in the future
pkgver=20180429
_commit=9ca8780df71
pkgrel=1
pkgdesc="Driver for Realtek RTL2832U, allowing general purpose software defined radio (SDR)."
arch=('x86_64')
url="http://sdr.osmocom.org/trac/wiki/rtl-sdr"
license=('GPL')
depends=('libusb>=1.0')
makedepends=('git' 'cmake')
conflicts=('rtl-sdr-git')
options=('staticlibs') 
install=rtl-sdr.install
source=("git://git.osmocom.org/rtl-sdr.git#commit=$_commit"
        'rtlsdr.conf')
md5sums=('SKIP'
         '9c65be40f8916a220e3f55b819897331')

_gitname="rtl-sdr"

# todo: fix upstream's udev rule location and group

pkgver() {
  cd "$srcdir/$_gitname"
  git show -s --format="%ci" HEAD | sed -e 's/-//g' -e 's/ .*//'
}

build() {
  cd "$srcdir/$_gitname"
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr \
        -DDETACH_KERNEL_DRIVER=ON -Wno-dev ../
  make
}

package() {
  cd "$srcdir/$_gitname/build"
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$_gitname/rtl-sdr.rules" "$pkgdir/usr/lib/udev/rules.d/10-rtl-sdr.rules"
  install -Dm644 "$srcdir/rtlsdr.conf" "$pkgdir/etc/modprobe.d/rtlsdr.conf"
}

# vim:set ts=2 sw=2 et:
