# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

_name=dulwich
pkgname=python-dulwich
pkgver=0.21.0
pkgrel=1
pkgdesc='Pure-Python implementation of the Git file formats and protocols'
arch=('x86_64')
url=https://www.dulwich.io
license=('GPL')
depends=('python-urllib3')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
checkdepends=('python-gpgme' 'python-paramiko')
optdepends=('python-fastimport: for fast-import support'
            'python-gpgme: for PGP signature support'
            'python-idna: for HTTPS support via urllib3'
            'python-paramiko: for use as the SSH implementation'
            'python-pyopenssl: for HTTPS support via urllib3'
            'python-pyinotify: to watch for changes to refs')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('c22cc05f020a96ad35d94d6520f8070270bee0a37b575686d09c02798b25ed86')
b2sums=('d63185fe6e900823f5b295454a41be4f8508aa4c1757599186a0daab6f9b44e0036485d16d9f3dd34e3d1d191987cb0191254cee844caadadf9e43d0a2de0645')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python -m installer dist/*.whl
  test-env/bin/python -m unittest dulwich.tests.test_suite
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
