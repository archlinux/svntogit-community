# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=20211212
pkgrel=1
_commit=b56c994
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=('x86_64')
url='https://www.chiark.greenend.org.uk/~sgtatham/puzzles/'
license=(MIT)
depends=(gtk3)
makedepends=(cmake imagemagick)
source=(https://www.chiark.greenend.org.uk/~sgtatham/puzzles/puzzles-$pkgver.$_commit.tar.gz)
sha256sums=('0795a58fb0e958be178b5531f2e4793c18993907162fdf77b22e0106d62daa1a')

build() {
  cmake -B build -S puzzles-$pkgver.$_commit \
   -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 puzzles-$pkgver.$_commit/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

# Fix FS#14600 (name conflicts with other packages).
  cd "$pkgdir"/usr/bin
  for oldname in *; do
    newname=puzzles-$oldname
    mv "$pkgdir"/usr/bin/{$oldname,$newname}
    sed -e "s|Exec=$oldname|Exec=$newname|" -i "$pkgdir"/usr/share/applications/$oldname.desktop
  done
}
