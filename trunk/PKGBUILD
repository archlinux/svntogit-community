# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=python-qtawesome
_pkgname='qtawesome'
pkgver=0.6.0
pkgrel=1
pkgdesc='Iconic fonts in PyQt and PySide applications'
arch=('any')
url="https://github.com/spyder-ide/${_pkgname}/"
license=('MIT')
source=("python-${_pkgname}-${pkgver}.tar.gz"::"https://github.com/spyder-ide/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('573192ceb76f79f384e8cfb2d5e69ad63ae97cb89711d5e1d2a9373eb6cc2067')
makedepends=('python' 'python-setuptools')
depends=('python-six' 'python-qtpy')

build() {
  cd "${_pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pkgname}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  # Install license file
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
