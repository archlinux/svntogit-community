# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stijn Segers <francesco dot borromini at gmail dot com>

pkgname=freerdp
pkgver=1.0.1
pkgrel=8
pkgdesc="Free RDP client"
arch=('i686' 'x86_64')
url="http://freerdp.sourceforge.net"
license=('GPL')
depends=('openssl' 'libxcursor' 'libcups' 'alsa-lib' 'libxext' 'libxdamage'
	 'ffmpeg' 'libxkbfile' 'libxinerama' 'libxv' 'libpulse')
makedepends=('krb5' 'cmake' 'damageproto' 'xmlto' 'docbook-xsl')
conflicts=('freerdp-git')
changelog=${pkgname}.changelog
options=('!libtool')
source=(https://github.com/downloads/FreeRDP/FreeRDP/FreeRDP-$pkgver.tar.gz
	https://github.com/FreeRDP/FreeRDP/commit/c10866db66c0d462ea7c2c38bb01740bcfb4fcb4.patch)
md5sums=('c843185e0ff22ce8409954651718f338'
         '194fd599d0f706ecce772d5187e08385')

build() {
  cd `find ${srcdir}/ -type d -name freerdp-\*`
  patch -Np1 <$srcdir/c10866db66c0d462ea7c2c38bb01740bcfb4fcb4.patch
  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DWITH_PULSEAUDIO=ON \
	.
  make
}

package() {
  cd `find ${srcdir}/ -type d -name freerdp-\*`
  make DESTDIR="${pkgdir}" install
}
