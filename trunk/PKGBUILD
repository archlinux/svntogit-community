# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

# Upstream tests are pretty borked, especially the PyPi sources. Skip for now.
BUILDENV+=(!check)

_pyname=fakeredis
pkgname=python-fakeredis
pkgver=2.6.0
pkgrel=1
pkgdesc='Fake implementation of redis API (redis-py) for testing purposes'
arch=(any)
url="https://github.com/dsoftwareinc/${_pyname}-py"
license=(BSD MIT)
depends=(python-importlib-metadata
         python-redis
         python-six
         python-sortedcontainers)
makedepends=(python-{build,installer,wheel}
             python-packaging
             python-lupa
             python-poetry-core)
checkdepends=(python-pytest
              python-pytest-asyncio
              python-pytest-mock
              python-hypothesis)
optdepends=('python-packaging: for aioredis support'
            'python-lupa: for lua scripting support')
# _archive="$_pyname-py-$pkgver"
# source=("$url/archive/v$pkgver/$_archive.tar.gz")
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('3f8f72c5b990a70ae69444dd1cfe0c9805811a53752d3d21fdcda29255b00e51')

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	PYTHONPATH="$PWD" pytest
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
