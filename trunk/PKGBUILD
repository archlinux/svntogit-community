# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Farhad Shahbazi <farhad@enthusiasm.cc>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-bottle
pkgver=0.12.22
pkgrel=1
pkgdesc="A fast and simple micro-framework for small web-applications"
arch=(any)
url="https://bottlepy.org"
license=('MIT')
options=(!emptydirs)
depends=('python')
makedepends=('python-setuptools' 'git')
checkdepends=('python-mako' 'python-jinja' 'python-cherrypy' 'python-twisted'
              'python-tornado' 'python-paste' 'python-gevent' 'python-eventlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/defnull/bottle/archive/$pkgver.tar.gz")
sha512sums=('9ec50e0d8eda0b25075ed6effe28e16f4079db3c6f18052278dda24f3aa4dd663eb190fca4a30a5ea49a96f0a03784560ee524ea34c1cc4f96aa777ff9354bb3')

check() {
  cd "$srcdir"/bottle-$pkgver
  # https://github.com/bottlepy/bottle/issues/791
  python test/testall.py || echo "Tests failed"
}

build() {
  cd "$srcdir"/bottle-$pkgver
  python setup.py build
}

package() {
  cd bottle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
