# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-django
pkgver=4.5.2
pkgrel=2
pkgdesc="A Django plugin for py.test"
arch=('any')
license=('BSD')
url="https://github.com/pytest-dev/pytest-django"
depends=('python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-django' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-django/archive/v$pkgver.tar.gz"
        $pkgname-pytest-7.patch::https://github.com/pytest-dev/pytest-django/pull/996.patch)
sha512sums=('09ed5bfe850599e95190d9b15e9383d1249ea575a7b3921744583da6266dd2c8d3eb4f93c8de96d98e1a8e33d1639937470cdc6d11558fcaea57d014dce13500'
            '34768201cff7d0709a6a7cd885386f6381cfbc67d454a8b305cd9d2c8ab6c431e6c46ced3213f4dd6957a71200393e3d46a4520a46494e5f4f9576c795907a5b')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  patch -d pytest-django-$pkgver -p1 < $pkgname-pytest-7.patch
}

build() {
  cd pytest-django-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-django-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/$site_packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test tests
}

package() {
  cd pytest-django-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
