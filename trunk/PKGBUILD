# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=arpack
pkgver=3.1.5
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Fortran77 subroutines designed to solve large scale eigenvalue problems'
url='http://forge.scilab.org/index.php/p/arpack-ng/'
license=('BSD')
depends=('lapack' 'openmpi')
makedepends=('openmpi' 'gcc-fortran')
provides=('arpack-ng')
source=("http://forge.scilab.org/upload/$pkgname-ng/files/$pkgname-ng_$pkgver.tar.gz")
sha256sums=('a269ce7806bc77ced31ef794d23c3e3aff8543b65550fd7e3e24660f2a79d515')

build() {
  cd "$pkgname-ng-$pkgver"

  ./configure --prefix=/usr --enable-mpi
  make \
    F77="mpif77" \
    CFLAGS+=" `pkg-config --cflags ompi` " \
    LIBS+=" `pkg-config --libs ompi` "
}

package() {
  cd "$pkgname-ng-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: https://github.com/opencollab/arpack-ng #
# vim:set ts=2 sw=2 et:
