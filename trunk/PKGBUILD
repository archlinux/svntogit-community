# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=mediawiki
pkgname=('mediawiki' 'mediawiki-math')
pkgver=1.23.0
_basever=1.23
pkgrel=1
pkgdesc="MediaWiki engine"
arch=('any')
url="http://www.mediawiki.org/wiki/MediaWiki"
# + http://www.mediawiki.org/wiki/Extension:Math
license=("GPL")
depends=('php')
makedepends=('git')
optdepends=('texvc: for math rendering'
	    'python2'
	    'pcre: for regular expressions support'
	    'php-intl: to handle Unicode normalization'
	    'php-mysql: for MySQL database support'
	    'php-pgsql: for PostgreSQL database support'
	    'php-sqlite: for sqlite database support'
	    'php-apc: for cache support'
	    'php-xcache: for cache support'
	    'memcached: for cache support'
	    'php-gd: for thumbnails rendering'
	    'imagemagick: for thumbnails rendering'
	    'sendmail: for meail sending')
backup=(etc/webapps/mediawiki/.htaccess)
install=mediawiki.install
options=(!strip)
#	mediawiki-math-${_basever}::git+https://git.wikimedia.org/git/mediawiki/extensions/Math.git#branch=REL${_basever/./_}
source=("http://releases.wikimedia.org/mediawiki/${_basever}/mediawiki-$pkgver.tar.gz"
	"mediawiki-math-${_basever}.tar.gz::https://codeload.github.com/wikimedia/mediawiki-extensions-Math/legacy.tar.gz/REL${_basever/./_}"
	apache.example.conf)
md5sums=('15d73f28a66125935d44e3b968b81ef7'
         '2e292442d22c2478150ee592e7780d89'
         '99c25f04d4093f3333db1be7624fe4e7')

package_mediawiki() {
  cd $srcdir
  install -vdm0755 $pkgdir/usr/share/webapps
  install -vdm0755 $pkgdir/etc/webapps/mediawiki
  cp -a $pkgname-$pkgver $pkgdir/usr/share/webapps/mediawiki

  echo "deny from all" >> $pkgdir/etc/webapps/mediawiki/.htaccess
  ln -sf /etc/webapps/mediawiki/.htaccess $pkgdir/usr/share/webapps/mediawiki/.htaccess
  install -vDm0644 $srcdir/apache.example.conf $pkgdir/etc/webapps/mediawiki/apache.example.conf

  # move cache and images to /var
  install -vdm0755 -o http -g http $pkgdir/var/cache/mediawiki
  install -vdm0755 -o http -g http $pkgdir/var/lib/mediawiki

  cd $pkgdir/usr/share/webapps/mediawiki

  mv cache/.htaccess $pkgdir/var/cache/mediawiki/
  rmdir cache
  ln -sf /var/cache/mediawiki cache

  mv images/* $pkgdir/var/lib/mediawiki/
  mv images/.htaccess $pkgdir/var/lib/mediawiki/
  rmdir images
  ln -sf /var/lib/mediawiki images
}

package_mediawiki-math() {
  depends=('mediawiki' 'texvc')
  optdepends=()
  backup=()
  pkgdesc="MediaWiki math extension"
  unset install

  cd $srcdir
  install -vdm0755 $pkgdir/usr/share/webapps/mediawiki/extensions
#  cp -a mediawiki-math-${_basever} $pkgdir/usr/share/webapps/mediawiki/extensions/Math
  cp -a wikimedia-mediawiki-extensions-Math-d0e998f $pkgdir/usr/share/webapps/mediawiki/extensions/Math
  ln -s /usr/bin/texvc $pkgdir/usr/share/webapps/mediawiki/extensions/Math/math/texvc
}
