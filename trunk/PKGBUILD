# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgbase=luasocket
pkgname=(lua-socket lua51-socket lua52-socket)
pkgver=20190219
_commit=144fa01c2f204e3b1b13c834f2644d100dba701b
pkgrel=2
pkgdesc='Networking support library for the Lua language'
arch=('x86_64')
url='https://github.com/diegonehab/luasocket'
license=('MIT')
#options=('debug')
makedepends=('lua' 'lua51' 'lua52' 'git')
source=("git+https://github.com/diegonehab/luasocket.git#commit=${_commit}")
md5sums=('SKIP')

build() {
  cp -a luasocket luasocket-52
  cp -a luasocket luasocket-51

  msg2 'Building with lua 5.3'
  cd "$srcdir"/luasocket
  MYCFLAGS=$CFLAGS MYLDFLAGS=$LDFLAGS make LUAV=5.3 linux

  msg2 'Building with lua 5.1'
  cd "$srcdir"/luasocket-51
  find . -type f -name \*.[ch] -exec sed -i \
    -e 's|include "lua.h|include "lua5.1/lua.h|g' \
    -e 's|include "lualib.h|include "lua5.1/lualib.h|g' \
    -e 's|include "luaconf.h|include "lua5.1/luaconf.h|g' \
    -e 's|include "lauxlib.h|include "lua5.1/lauxlib.h|g' \
    {} \;
  sed -i 's|usocket.$(O)|usocket.$(O) compat.$(O)|' makefile
  MYCFLAGS=$CFLAGS MYLDFLAGS=$LDFLAGS make LUAV=5.1 linux

  msg2 'Building with lua 5.2'
  cd "$srcdir"/luasocket-52
  find . -type f -name \*.[ch] -exec sed -i \
    -e 's|include "lua.h|include "lua5.2/lua.h|g' \
    -e 's|include "lualib.h|include "lua5.2/lualib.h|g' \
    -e 's|include "luaconf.h|include "lua5.2/luaconf.h|g' \
    -e 's|include "lauxlib.h|include "lua5.2/lauxlib.h|g' \
    {} \;
  MYCFLAGS=$CFLAGS MYLDFLAGS=$LDFLAGS make LUAV=5.2 linux
}

package_lua-socket() {
  depends=('lua')
  cd luasocket
  make DESTDIR="$pkgdir/" LUAV=5.3 prefix=/usr install-unix
  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-socket() {
  depends=('lua51')
  replaces=('luasocket')
  conflicts=('luasocket')

  cd luasocket-51
  make DESTDIR="$pkgdir/" LUAV=5.1 prefix=/usr install-unix
  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-socket() {
  depends=('lua52')

  cd luasocket-52
  make DESTDIR="$pkgdir/" LUAV=5.2 prefix=/usr install-unix
  install -D -m0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
