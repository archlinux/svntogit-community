# Maintainer: David Runge <dvzrv@archlinux.org>

_name=imagick
pkgbase=php-imagick
pkgname=('php-imagick' 'php7-imagick')
pkgver=3.5.0
pkgrel=1
pkgdesc="PHP extension to create and modify images using the ImageMagick library"
arch=('x86_64')
url="https://github.com/imagick/imagick"
license=('PHP')
depends=('imagemagick' 'ttf-font')
makedepends=('librsvg' 'php' 'php7')
checkdepends=('ttf-dejavu')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/refs/tags/${pkgver}.tar.gz"
        "${_name}.ini")
sha512sums=('4c6e3cb3e902b89a0944d384f1b797705f12054bde6c823c565b7dfb5f33136a0106ee695d85297d73af48dbd2acca4d69537151ce31abb05eb737162e12df21'
            '1463505bd6b2572e21f6bbc242c2e0bf8b881b839ac38e38c230f09b4bf4c4698cafba1a026da8f615ee2b2980ab74dc68284afd70bb732db6fb70b5efba2bfc')
b2sums=('b4a2caab8b836480a528e9c197aadada19aaa107f3883dde5fdbdf7fff571da1535bef5cb38ae3ccf050a87156957cdfd5515d5abb7833828321c60dda3e7dc9'
        '291d68f50a2c173b857c0f132e1874f0682da3c7176f67b946a19a68256ab19f56a234bafa16711c3f8ef26c4bc7df04ea8afdecbcb984820b9bf3fd2a135edb')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgbase}-${pkgver}"
  # setting package version: https://bugs.archlinux.org/task/64185
  sed -e "s/@PACKAGE_VERSION@/${pkgver}/" \
      -i "${pkgbase}-${pkgver}/php_imagick.h" "${pkgbase}-${pkgver}/package.xml"

  cp -av "${pkgbase}-${pkgver}" "${pkgname[1]}-${pkgver}"
  (
    cd "$pkgname-${pkgver}"
    phpize
  )
  (
    cd "${pkgname[1]}-${pkgver}"
    phpize7
  )
}

build() {
  (
    cd "${pkgbase}-${pkgver}"
    ./configure --prefix=/usr
    make
  )
  (
    cd "${pkgname[1]}-${pkgver}"
    ./configure --prefix=/usr
    make
  )
}

check() {
  (
    cd "${pkgbase}-${pkgver}"
    export NO_INTERACTION="true"
    make -k test
  )
  (
    cd "${pkgname[1]}-${pkgver}"
    export NO_INTERACTION="true"
    make -k test
  )
}

package_php-imagick() {
  depends+=('php')
  backup=("etc/php/conf.d/${_name}.ini")

  cd "${pkgname}-${pkgver}"
  make INSTALL_ROOT="$pkgdir/" install
  ls -lah ..
  install -vDm 644 "../${_name}.ini" -t "${pkgdir}/etc/php/conf.d/"
  install -vDm 644 {ChangeLog,CREDITS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 examples/*.php \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

package_php7-imagick() {
  depends+=('php7')
  backup=("etc/php7/conf.d/${_name}.ini")

  cd "${pkgname}-${pkgver}"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "../${_name}.ini" -t "${pkgdir}/etc/php7/conf.d/"
  install -vDm 644 {ChangeLog,CREDITS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 examples/*.php \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
