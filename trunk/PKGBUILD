# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-tasks
pkgver=6.0.1
pkgrel=1
pkgdesc='The Pantheon tasks'
arch=(x86_64)
url=https://github.com/elementary/tasks
license=(GPL3)
groups=(pantheon)
depends=(
  clutter-gtk
  geoclue
  geocode-glib
  glib2
  gtk3
  libchamplain
  libclutter-1.0.so
  libecal-2.0.so
  libedataserver-1.2.so
  libgdata.so
  libgranite.so
  libhandy-1.so
  libical
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=ccbd2f44ddc0c6c0fe5e189faa03678342330840
source=(pantheon-tasks::git+https://github.com/elementary/tasks.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd pantheon-tasks

  git describe --tags
}

build() {
  arch-meson pantheon-tasks build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
