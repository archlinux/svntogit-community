# Maintainer: David Runge <dave@sleepmap.de>
_name=Nikola
pkgname=nikola
pkgver=8.0.2
pkgrel=1
pkgdesc="A static site and blog generator"
arch=('any')
url="https://getnikola.com/"
license=('MIT')
depends=('python-babel' 'python-blinker' 'python-doit' 'python-dateutil'
'python-docutils' 'python-logbook' 'python-lxml' 'python-mako'
'python-markdown' 'python-natsort' 'python-piexif' 'python-pillow'
'python-pygments' 'python-pyrss2gen' 'python-requests' 'python-setuptools'
'python-unidecode' 'python-yapsy')
makedepends=('python-aiohttp' 'python-ghp-import' 'python-husl'
'python-ipykernel' 'python-jinja' 'python-micawber' 'jupyter-notebook'
'python-phpserialize' 'python-pygal' 'python-pyphen' 'python-ruamel-yaml'
'python-toml' 'python-typogrify' 'python-watchdog')
checkdepends=('python-coverage' 'python-freezegun' 'python-pytest-cov')
optdepends=('jupyter-notebook: .ipynb support'
            'python-aiohttp: for `nikola auto`'
            'python-ghp-import: for upload to Github pages'
            'python-husl: optional color support'
            'python-ipykernel: .ipynb support'
            'python-jinja: jinja2 based themes'
            'python-micawber: for embedding media in posts'
            'python-phpserialize: for Wordpress import'
            'python-pygal: for SVG graph plotting'
            'python-pyphen: for hyphenation support'
            'python-ruamel-yaml: for YAML support'
            'python-toml: for TOML support'
            'python-typogrify: for typographical enhancements'
            'python-watchdog: for `nikola auto`')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4c5f472ad51e1976e8d4188a45a4aabf428b92d50792175b1e8c71681e1f38b7bdcb3cdf5af1907210871baaeba647d5fc74f4ac5f9a22d24d44f610a64f0b53')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # removing upper limit for python-markdown (seemingly compatible to 3.0.1)
  # https://github.com/getnikola/nikola/issues/3173
  sed -e 's/,<3.0.0//' -i requirements.txt
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # shell completions
  export PYTHONPATH="${pkgdir}/usr/lib/python3.7/site-packages:${PYTHONPATH}"
  "${pkgdir}/usr/bin/${pkgname}" tabcompletion \
    --shell bash \
    --hardcode-tasks \
    > "${pkgname}_bash"
  install -vDm 644 ${pkgname}_bash \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" tabcompletion \
    --shell zsh \
    --hardcode-tasks \
    > "${pkgname}_zsh"
  install -vDm 644 ${pkgname}_zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
