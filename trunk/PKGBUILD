# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.20
pkgrel=2
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('df92422426e7ebaf2c3a6b953de9fc9fcb461f7227835e728bbccf5b796995d818a39817913d6010fadb8b3618710f77f50e36eb6ce4176e7312b68c7d7f7e96')
changelog="CHANGELOG.md"

build() {
	cd "$pkgname-$pkgver"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-trimpath"
	go build -o terraform-binary
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
