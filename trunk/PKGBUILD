# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: shadyabhi <abhijeet.1989@gmail.com>

pkgbase=python-google-api-python-client
pkgname=('python2-google-api-python-client' 'python-google-api-python-client')
pkgver=1.4.2
pkgrel=2
pkgdesc="Google API Client Library for Python"
arch=('any')
url="https://github.com/google/google-api-python-client"
license=('Apache')
makedepends=('python2-setuptools' 'python-setuptools' 'python2-httplib2'
             'python2-oauth2client' 'python2-uritemplate' 'python2-six'
             'python-httplib2' 'python-oauth2client' 'python-uritemplate'
             'python-six')
source=("https://pypi.python.org/packages/source/g/google-api-python-client/google-api-python-client-${pkgver}.tar.gz")
md5sums=('7033985a645e39d3ccf1b2971ab7b6b8')

prepare() {
  cd "$srcdir"
  cp -a google-api-python-client-${pkgver} google-api-python-client-py2-${pkgver}
  cd google-api-python-client-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  cd "$srcdir/google-api-python-client-$pkgver"

  msg "Building Python2"
  cd "$srcdir"/google-api-python-client-py2-${pkgver}
  python2 setup.py build

  msg "Building Python3"
  cd "$srcdir"/google-api-python-client-${pkgver}
  python setup.py build
}
 
package_python2-google-api-python-client() {
  depends=('python2-httplib2' 'python2-oauth2client' 'python2-uritemplate' 'python2-six')
  cd "$srcdir/google-api-python-client-py2-$pkgver"
 
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  # Workaround for FS#47243
  _site_packages=`python2 -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod -R a+r "$pkgdir$_site_packages"
}

package_python-google-api-python-client() {
  depends=('python-httplib2' 'python-oauth2client' 'python-uritemplate' 'python-six')
  cd "$srcdir/google-api-python-client-$pkgver"
 
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  # Workaround for FS#47243
  _site_packages=`python -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod -R a+r "$pkgdir$_site_packages"
}

