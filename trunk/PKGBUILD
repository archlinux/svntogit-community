# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=gpgme
pkgname=ruby-gpgme
pkgver=2.0.15
pkgrel=2
pkgdesc='Ruby interface to GnuPG Made Easy (GPGME)'
url='https://github.com/ueno/ruby-gpgme'
arch=('x86_64')
license=('GPL2')
depends=('ruby' 'ruby-mini_portile2')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/ueno/ruby-gpgme/archive/v${pkgver}.tar.gz)
sha256sums=('53a0e1179169bce91eee4962b89fbc64fe6dc70b80fb268e544878818f0dfa74')
sha512sums=('f814e765fdd580300700d179d5e1bc0d29a1e2dcaaf32e73fdec118222c9e97db9b991fa20653f8ee8f771c9bbf04772ea8354561ee0c79cc6a3b1a775f69340')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec # don't give a fuck about rubys bla bla
}

build() {
  cd ${pkgname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.rdoc NEWS -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  find "${pkgdir}" \( -name gem_make.out -o -name mkmf.log \) -delete
}

# vim: ts=2 sw=2 et:
