# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-apispec
pkgver=6.0.1
pkgrel=1
pkgdesc="A pluggable API specification generator. Currently supports the OpenAPI Specification."
url="https://github.com/marshmallow-code/apispec"
license=('MIT')
arch=('any')
depends=('python-packaging')
optdepends=('python-yaml: for yaml support'
            'python-prance: for validation'
            'python-openapi-spec-validator: for validation')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-bottle' 'python-marshmallow'
              'python-flask' 'python-prance' 'python-yaml'
              'python-openapi-spec-validator')
source=("$pkgname-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec/archive/$pkgver.tar.gz")
sha512sums=('fabe14511a1dd598101977bcab70e5bdb1338f219f348f9584062d0c4c80de8adc1eb7f5e202a6a9d1e3fa11d775de2871f238585f6b6471bebdb3fe47bf0dc0')

build() {
  cd apispec-$pkgver
  python setup.py build
}

check() {
  cd apispec-$pkgver
  python setup.py pytest
}

package() {
  cd apispec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
