# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contriubutor: Jamie Magee <jamie dot magee at gmail dot com>

pkgname=gitsign
pkgver=0.3.1
pkgrel=1
pkgdesc='Keyless Git signing using Sigstore'
arch=('x86_64')
url='https://github.com/sigstore/gitsign'
license=(APACHE)
makedepends=(go)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
b2sums=('43779c71d8b0530788e31bb326935dab53b7732e68ec73ce33c705e1ecfd3d2c15ecaa46ae5b59e5c60cabb9ded7598cbe8d8e2d294e736fcf6e4b9c75117893')

build() {
  cd $pkgname-$pkgver
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make build-all
}

package() {
  cd $pkgname-$pkgver

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
