# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Emanuele Rossi <newdna1510@yahoo.it>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=choqok
pkgver=1.0
pkgrel=1
pkgdesc="A Twitter/identi.ca/laconica client for KDE"
url="http://choqok.gnufolks.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdebase-runtime' 'qjson' 'qoauth' 'qca-ossl')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
optdepends=('kdebase-konqueror: proxy support')
install="${pkgname}.install"
source=("http://choqok.gnufolks.org/pkgs/${pkgname}-${pkgver}.tar.bz2")
md5sums=('536e05d26dd5cea43c6d8dc17c283104')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKWEBKITPART_LIBRARIES=/usr/lib/kde4/
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
