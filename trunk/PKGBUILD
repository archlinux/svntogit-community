# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=patchmatrix
pkgver=0.24.0
pkgrel=1
pkgdesc="A JACK patchbay in flow matrix style"
arch=('x86_64')
url="https://git.open-music-kontrollers.ch/lad/patchmatrix/about/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'libglvnd' 'libx11')
makedepends=('glew' 'glu' 'jack' 'lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lad/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('35ac20aa74fddef9fab63193bc4501fae5aa09d0f99c6dde3cf5502fe663362119586a32f82eb43118a1a511717c608770575a343a50fc359c3b3a2ca4f4c11a'
            'SKIP')
b2sums=('0c9b304f9d72ee7cc7e9e6c21300a4e0d5f571eef8b726ec192005c5d5909300cf3d0fc8361a2250c98b9e662303afcc8185c9c0af6815cc91cf3acd4f39a312'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  cd "$pkgname-$pkgver"
  arch-meson build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  depends+=('libGLEW.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
