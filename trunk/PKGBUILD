# Maintainer: Anatol Pomazau
# Contributor: Ingo BÃ¼rk <admin at airblader dot de>

pkgname=xcb-util-xrm
pkgver=1.2
pkgrel=2
pkgdesc='XCB utility functions for the X resource manager'
arch=(x86_64)
url='https://github.com/Airblader/xcb-util-xrm'
license=(MIT)
depends=(xcb-util)
makedepends=(xorg-util-macros libx11)
source=(https://github.com/Airblader/xcb-util-xrm/releases/download/v${pkgver}/xcb-util-xrm-${pkgver}.tar.gz)
sha256sums=('c4a1d64d4a6973c649e3b6e3c418242f11e39c65d4d227d555d48f6df0558567')

build() {
  cd xcb-util-xrm-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd xcb-util-xrm-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

