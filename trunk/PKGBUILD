# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>

pkgname=lib32-systemd
_pkgbasename=systemd
pkgver=221
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('lib32-glib2' 'lib32-libgcrypt' 'lib32-xz' 'lib32-curl' 'lib32-libcap' 'lib32-bzip2' 'systemd')
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-libidn' 'lib32-acl' 'intltool' 'gperf')
source=("http://www.freedesktop.org/software/$_pkgbasename/$_pkgbasename-$pkgver.tar.xz")
md5sums=('b4d5a253841cf28a98b7ec99c45e3716')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  # LTO currently breaks the build because of libtool failures
  CFLAGS+=' -fno-lto'

  local timeservers=({0..3}.arch.pool.ntp.org)

  ./configure \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --enable-compat-libs \
    --disable-audit \
    --disable-tests \
    --disable-ima \
    --disable-kdbus \
    --disable-seccomp \
    --disable-pam \
    --disable-kmod \
    --disable-networkd \
    --disable-blkid \
    --disable-libiptc \
    --without-python \
    --disable-libcryptsetup \
    --with-sysvinit-path= \
    --with-sysvrcnd-path= \
    --with-ntp-servers="${timeservers[*]}"

  make
}

package() {
  cd "${srcdir}/systemd-${pkgver}"
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s systemd "$pkgdir/usr/share/licenses/lib32-systemd"
}
