# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Miguel de Val-Borro <miguel dot deval at gmail dot com>

pkgname=python-sphinx-bootstrap-theme
pkgver=0.8.1
pkgrel=4
pkgdesc="Sphinx Bootstrap Theme"
arch=(any)
url=http://ryan-roemer.github.io/sphinx-bootstrap-theme/
license=(MIT)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=ebf24f849c5ee0c5219009cdb1637b052e2588d7
source=(git+https://github.com/ryan-roemer/sphinx-bootstrap-theme.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd sphinx-bootstrap-theme
  git describe --tags | sed 's/^v//'
}

build() {
  cd sphinx-bootstrap-theme
  python setup.py build
}

package() {
  cd sphinx-bootstrap-theme
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/python-sphinx-bootstrap-theme/
}

# vim: ts=2 sw=2 et:
