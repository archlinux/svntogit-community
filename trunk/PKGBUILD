# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgbase=python-diff-match-patch
pkgname=('python-diff-match-patch' 'python2-diff-match-patch')
pkgver=20121119
pkgrel=4
pkgdesc="Robust algorithms to perform the operations required for synchronizing plain text"
arch=('any')
url="https://code.google.com/archive/p/google-diff-match-patch/"
license=('Apache')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=(https://pypi.python.org/packages/source/d/diff-match-patch/diff-match-patch-${pkgver}.tar.gz)
sha256sums=('9dba5611fbf27893347349fd51cc1911cb403682a7163373adacc565d11e2e4c')

prepare() {
  cd "${srcdir}"/diff-match-patch-${pkgver}

  sed -i 's/\/usr\/bin\/python2.4/\/usr\/bin\/python2.7/' python2/diff_match_patch/*
}

package_python-diff-match-patch() {
  depends=('python')

  cd "${srcdir}"/diff-match-patch-${pkgver}

  python setup.py install --root="${pkgdir}" -O1
}

package_python2-diff-match-patch() {
  depends=('python2')

  cd "${srcdir}"/diff-match-patch-${pkgver}

  rm -rf build # Installs python3 version otherwise
  python2 setup.py install --root="${pkgdir}" -O1
  chmod 644 "${pkgdir}"/usr/lib/python2.7/site-packages/diff_match_patch-20121119-py2.7.egg-info/*
}
