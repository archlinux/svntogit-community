# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)

pkgname=drumgizmo
pkgver=0.9.20
pkgrel=1
pkgdesc="Multichannel, multilayered, cross-platform drum plugin and stand-alone application"
arch=(x86_64)
url="https://www.drumgizmo.org"
license=(GPL3)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc libxext libx11)
makedepends=(alsa-lib jack libsmf libsndfile lv2 zita-resampler)
optdepends=(
  'alsa-lib: for drumgizmo standalone'
  'jack: for drumgizmo standalone'
  'lv2-host: for LV2 plugin'
)
source=($url/releases/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
sha512sums=('3444201961d74e7787bebe4006df6c257f6c1a57dd81d272b203a4a56ad609d3fa4e88224dfe4ea6a67f4db94b340666d018cb79fc376dd264398f98d66433e8'
            'SKIP')
b2sums=('c92c41fd9cfd09a42d6500ec7d5f3bffd7dd646ef11b88212393f91a06d0af596c4f627be037c3eb0d19fd3ae11448e43d7a40ae8928dea7fbdfb61cad057cdb'
        'SKIP')
validpgpkeys=('F39C94D556CCB995B1AE540E9EB89445BF071867') # DrumGizmo <info@drumgizmo.org>

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-lv2
  make
}

package() {
  depends+=(libsmf.so libsndfile.so libzita-resampler.so)

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
