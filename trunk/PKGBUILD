# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Felix Bühler <account at buehler dot rocks>

pkgname=newsflash
_pkgname=news_flash_gtk
pkgver=1.4.1
pkgrel=1
pkgdesc='Desktop application designed to complement an already existing web-based RSS reader account'
arch=('x86_64')
url='https://gitlab.com/news-flash/news_flash_gtk'
license=('GPL3')
depends=('libhandy' 'webkit2gtk')
makedepends=('appstream-glib' 'meson' 'rust')
source=("https://gitlab.com/news-flash/$_pkgname/-/archive/$pkgver/$_pkgname-$pkgver.tar.gz")
sha256sums=('ae6bbd09ff527dc8772d64ad27072ae20e89a15ba6466770c5cafad1d2dba01c')

prepare() {
  cd $_pkgname-$pkgver
  # Update open to avoid xdg-utils dependency
  sed -i 's/open = "1.4.0"/open = "1.7.0"/' Cargo.toml
}

build() {
  export FEEDLY_CLIENT_ID="boutroue"
  export FEEDLY_CLIENT_SECRET="FE012EGICU4ZOBDRBEOVAJA1JZYH"
  export PASSWORD_CRYPT_KEY="ypsSXCLhJngks9qGUAqShd8JjRaZ824wT3e"
  export MERCURY_PARSER_USER="newsflash"
  export MERCURY_PARSER_KEY="R4qcKEHpr9RTq6YuRAPkm9kMhjp4GuxiWG44VDk3Na4ZsN7F"
  export RUSTC_BOOTSTRAP=1
	
  arch-meson $_pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  ln -s com.gitlab.newsflash "$pkgdir/usr/bin/$pkgname"
}
