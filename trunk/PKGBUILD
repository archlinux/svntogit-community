# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=scribus
pkgver=1.5.4
pkgrel=9
pkgdesc="Desktop publishing software"
arch=('x86_64')
url="https://www.scribus.net/"
license=('GPL')
depends=('qt5-base' 'harfbuzz-icu' 'poppler' 'python2' 'hunspell' 'podofo' 'openscenegraph'
         'libcdr' 'libfreehand' 'libmspub' 'libpagemaker' 'libqxp' 'libvisio' 'libzmf') # 'graphicsmagick'
makedepends=('cmake' 'boost' 'mesa' 'qt5-tools')
optdepends=('tk: scripts based on tkinter'
            'hyphen-lang: hyphenation patterns for desired languages')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.asc}
        'https://github.com/scribusproject/scribus/commit/d867ec3c386baaed1b8e076dd70b278863411480.patch'
        'https://github.com/scribusproject/scribus/commit/7d4ceeb5cac32287769e3c0238699e0b3e56c24d.patch'
        'https://github.com/scribusproject/scribus/commit/76561c1a55cd07c268f8f2b2fea888532933700b.patch'
        'https://github.com/scribusproject/scribus/commit/8e05d26c19097ac2ad5b4ebbf40a3771ee6faf9c.patch'
        'scribus-poppler-0.70.patch'
        'scribus-poppler-0.73.patch')
sha256sums=('6480925250b2bb07028e2f378c02b67fe3e33206743671e03c07c701cd05da03'
            'SKIP'
            '0a782958360ad70145a588a5e1c0044d162344e96b4ff9b0aeb31388cc9b2e36'
            'b20bf0588b30d7777551ea6eb5744caf31ef31dd396b2b08cc4bddf9dd3a22cf'
            '29b09c7cc95e8a5432010b39a6c0478cb106a1bbb425747fb383092465476106'
            '824cbc38b02e15aa742b309a226549c3a6c4e3eb16059c239083145aa4ae6dab'
            '199b5ea2831d749b91e206f50e3d30a7b787c5e2b85676120e1e4825478f8737'
            'f09488ff56177fd360d588bf70f2960adeba80cb34cec692d4d967dc081fe4ad')
validpgpkeys=('5086B8D68E70FDDF4C40045AEF7B95E7F60166DA'  # Peter Linnell <plinnell@scribus.net>
              '757F5E9B13DD648887AD50092D47C099E782504E') # The Scribus Team (www.scribus.net) <the_scribus_team@scribus.net>

prepare() {
    cd ${pkgname}-${pkgver}

    # Poppler 0.64
    patch -p1 -i ../d867ec3c386baaed1b8e076dd70b278863411480.patch
    patch -p1 -i ../7d4ceeb5cac32287769e3c0238699e0b3e56c24d.patch
    patch -p1 -i ../76561c1a55cd07c268f8f2b2fea888532933700b.patch
    # Poppler 0.70
    patch -p1 -i ../8e05d26c19097ac2ad5b4ebbf40a3771ee6faf9c.patch
    patch -p1 -i ../scribus-poppler-0.70.patch
    # Poppler 0.71
    sed -e 's|GBool|bool|g' -e 's|gTrue|true|g' -e 's|gFalse|false|g' -i scribus/plugins/import/pdf/*
    # Poppler 0.72
    sed -e 's|getCString|c_str|g' -i scribus/plugins/import/pdf/*
    # Poppler 0.73
    patch -p0 -i ../scribus-poppler-0.73.patch
    # Fix python name
    sed \
        -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
        -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
        -i scribus/plugins/scriptplugin/{samples,scripts}/*

    mkdir -p build
}

build() {
    cd ${pkgname}-${pkgver}/build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_SKIP_RPATH=ON
        # FS#59800
        #-DWANT_GRAPHICSMAGICK=1
    make
}

package() {
    cd ${pkgname}-${pkgver}/build

    make DESTDIR="${pkgdir}" install

    cd ..

    install -Dm644 scribus.desktop -t "${pkgdir}"/usr/share/applications

    for i in 16x16 32x32 128x128 256x256 512x512 1024x1024
    do
        install -Dm644 resources/iconsets/artwork/icon_${i}.png "${pkgdir}"/usr/share/icons/hicolor/${i}/apps/scribus.png
    done

    # Use system hyphen
    rm -rf "${pkgdir}"/usr/share/scribus/dicts/hyph
    ln -sf /usr/share/hyphen "${pkgdir}"/usr/share/scribus/dicts/hyph
}
