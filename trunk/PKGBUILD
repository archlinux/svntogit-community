# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.10
pkgrel=2
pkgdesc='Lightweight and fast web browser'
arch=(x86_64)
url='https://www.netsurf-browser.org/'
license=(MIT GPL2)
depends=(curl gtk3 'libhubbub=0.3.7' libjpeg-turbo 'libnsutils=0.1.0'
         'libutf8proc>=2.5.0' 'libwapcaplet>=0.4.3' 'libdom>=0.4.1' libwebp)
# vim is needed only for xxd when building
makedepends=(check inetutils 'libcss>=0.9.1' 'libnsbmp>=0.1.6'
             'libnsgif>=0.2.1' 'nsgenbind>=0.8' perl-html-parser setconf vim)
source=("https://download.netsurf-browser.org/netsurf/releases/source-full/netsurf-all-$pkgver.tar.gz"
        netsurf.sh)
sha256sums=('495adf6b6614ce36fca6c605f7c321f9cb4a3df838043158122678ce2b3325b7'
            '34c1f41c02ff3791a1b734197d99ec7f58c403dee05f0f9b7b9b3509d32b51ac')

prepare() {
  cd "$pkgname-all-$pkgver/$pkgname"

  # Fix compilation issues
  sed -i 's:libutf8proc/::;s:UTF8PROC_CCC_VIRAMA:UTF8PROC_BIDI_CLASS_EN:' utils/idna.c
  # Use "netsurf" as the name of the executable in /usr/bin
  setconf frontends/gtk/res/netsurf-gtk.desktop 'Exec=netsurf %u'
}

build() {
  CFLAGS="$CFLAGS -w -Os -funroll-loops" \
    make -C "$pkgname-all-$pkgver/$pkgname" \
    NETSURF_UA_FORMAT_STRING='"NetSurf/%d.%d (%s; Arch Linux)"' \
    TARGET=gtk3 \
    LIBDIR=lib \
    INCLUDEDIR=include \
    PREFIX=/usr
}

package() {
  make -C "$pkgname-all-$pkgver/$pkgname" \
    TARGET=gtk3 \
    LIBDIR=lib \
    INCLUDEDIR=include \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    install

  # Launch script
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"

  cd "$pkgname-all-$pkgver/$pkgname"

  # Desktop icon and shortcut
  install -Dm644 frontends/gtk/res/$pkgname.xpm \
    "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 frontends/gtk/res/$pkgname-gtk.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # License
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
