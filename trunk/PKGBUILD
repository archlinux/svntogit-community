# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.10.0
pkgrel=2
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
checkdepends=('python-coverage' 'python-async_generator' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-asyncio/archive/v$pkgver.tar.gz")
sha512sums=('78160d1918c5d3d2172f74fa07bf4bf6d0bc416666011dd5b83b3a7f385ec30fc65d49c01bba5dd0e16ad9dcd9d72d8ab062526f3268d60cd2e24a35b5a8f883')

prepare() {
  cd pytest-asyncio-$pkgver
  # Don't treat DeprecationWarnings as errors
  sed -i '/filterwarnings = error/d' setup.cfg
}

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
