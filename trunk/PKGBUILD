# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>
# Contributor: David Scholl <djscholl@gmail.com> 
# Contributor: David Pretty <david.pretty@gmail.com> 

pkgname=python-rpy2
pkgver=2.9.5
pkgrel=1
pkgdesc="A very simple, yet robust, Python interface to the R Programming Language."
arch=(x86_64)
url="https://rpy2.bitbucket.io/"
license=(MPL GPL LGPL)
depends=(python-numpy r)
makedepends=(python-setuptools)
source=("https://pypi.io/packages/source/r/rpy2/rpy2-$pkgver.tar.gz")
sha256sums=('b91f8efca7d0929f2b2b3634946be892cba6c21f92acdf737399e7eedf4532db')

prepare() {
  cd rpy2-$pkgver
  sed -i "s:Rlapack:lapack:" setup.py
  sed -i "s:os.path.join(RHOME.strip(), 'include'):'/usr/include/R':" setup.py
  sed -i "s:os.path.join(RHOME.strip(), 'include'):'/usr/include/R':" setup.py
}

package() {
  cd rpy2-$pkgver

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
