# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=pluma
pkgver=1.24.2
pkgrel=1
pkgdesc="A powerful text editor for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('iso-codes' 'mate-desktop' 'zenity' 'gtksourceview3' 'libpeas' 'python' 'gettext')
makedepends=('itstool' 'gobject-introspection' 'python' 'mate-common' 'yelp-tools' 'autoconf-archive')
optdepends=('python-gobject: to use the python plugins')
groups=('mate-extra')
conflicts=('pluma-gtk3')
replaces=('pluma-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha512sums=('588640744f8a0bec08d0c51c0309380a57cb6319c98124885e30d05e7dd8488ba7bd5acec1bf21fb0d6832626ae8925b24cd241202265af1a631a91313cdad2b')

build() {
    	cd "${pkgname}-${pkgver}"
    	./autogen.sh
    	PYTHON=/usr/bin/python ./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--enable-gtk-doc=no \
        	--enable-python

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
