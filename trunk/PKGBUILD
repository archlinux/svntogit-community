# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-multi-writer
pkgver=3.26.0
pkgrel=1
pkgdesc="Write an ISO file to multiple USB devices at once"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/MultiWriter"
license=('GPL')
groups=('gnome-extra')
depends=('gtk3' 'libcanberra' 'libgusb' 'udisks2')
makedepends=('appstream-glib' 'docbook-sgml' 'docbook-utils' 'intltool' 'meson' 'perl-sgmls')
optdepends=('gnome-icon-theme-extras: show device icons')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('aae3f4ffd8fd907cf75ea09c6a8167400388a8584d21ddaec0c252d8196e2bcf')

prepare() {
	mkdir build
	cd $pkgname-$pkgver
	sed -i 's|libexec|lib/gnome-multi-writer|' src/meson.build
}

build() {
	cd build
	meson ../$pkgname-$pkgver \
		--prefix=/usr \
		--buildtype=release
	ninja
}

package() {
	cd build
	DESTDIR="$pkgdir" ninja install
}
