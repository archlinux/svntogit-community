# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: fauno <fauno@parabola.nu>

pkgname=miniupnpd
pkgver=2.3.2
pkgrel=1
pkgdesc='Lightweight UPnP IGD daemon'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
backup=('etc/miniupnpd/miniupnpd.conf')
depends=('iptables' 'net-tools' 'util-linux' 'util-linux-libs' libuuid.so 'libcap-ng' libcap-ng.so)
makedepends=('lsb-release' 'procps-ng')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz{,.sig}
        miniupnpd.service)
sha512sums=('f0956b8f2e52fc9257f6512d780d17c920f6635afaeee604056f3b2afccbfda3f5e65d95d16f1d883bd9e7f4e372856cae620fabcf79c7c87e3f56bbcc089acc'
            'SKIP'
            '6387f646d91257e50159f66c5168830c5c845e1de739e52cc86647baafaa96ed3a5d00b6c7e46080c3dd998a486d81183c3c11ce9bf32e2f6d9fdd40b0ea8bef')
b2sums=('fe451773be25fd7f5941f89b35a4c94f0bebd5ece93d3b3c526c3c0e1c436773a2c403eb407cde397565ef4ec8f50441a64890bb89a56a9006a6c4556e707cef'
        'SKIP'
        '3db5bae2f4624f436a509dfda79183468b7d2112b5351b70f972319f3223722b1d2b5ac44516bb8f76995f04af0bb76b1e3a19e9afe011f5b104aae5f2058638')
validpgpkeys=(
  '751E9FF6944A3B36A5432216DB511043A31ACAAF' # miniupnp (miniupnp !) <miniupnp@free.fr>
)

prepare() {
  cd ${pkgname}-${pkgver}
  sed '/gzip/d' -i Makefile.linux
  sed 's|/sbin/sysctl|sysctl|g' -i configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --ipv6 \
    --leasefile
  CONFIG_OPTIONS="--ipv6 --leasefile" make -f Makefile.linux config.h
  make -f Makefile.linux
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" PREFIX=/ INSTALLPREFIX=/usr SBININSTALLDIR=/usr/bin -f Makefile.linux install

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 ../miniupnpd.service -t "${pkgdir}/usr/lib/systemd/system"

  sed -i 's:/s\?bin/iptables:/usr/bin/iptables:
          s:eth0:"`cat /etc/miniupnpd/miniupnpd.conf | '"awk -F= '/^ext_ifname/ { print \$2 }'"'`":' "${pkgdir}"/etc/miniupnpd/*.sh
  sed -i -e "s/^uuid=[-0-9a-f]*/uuid=00000000-0000-0000-0000-000000000000/
             s/make genuuid/uuidgen/" "${pkgdir}/etc/miniupnpd/miniupnpd.conf"

  rm -r "${pkgdir}/etc/init.d"
}

# vim: ts=2 sw=2 et:
