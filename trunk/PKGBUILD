# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Matt Harrison <matt@mistbyte.com>

pkgname=mediaelch
pkgver=2.10.0
pkgrel=1
pkgdesc='Media Manager for handling Movies, TV Shows and Concerts/Music Videos for Kodi'
url='https://www.mediaelch.de/mediaelch/'
arch=('x86_64')
license=('LGPL3')
depends=('glibc' 'gcc-libs' 'libmediainfo' 'qt5-declarative' 'qt5-multimedia'
         'qt5-svg' 'quazip-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=(https://github.com/Komet/MediaElch/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('e219fc929d200164eb3cc76988fd1ad547a952d9cc80c91c1df83b2602d53f5a')
b2sums=('14ff36a5d31959454c4341187ccc4a41b09b43939c37db18b4ae19dfd79f5871cb7b731de0454dd662fbe3f9bbc8d0be1b98b4f63a18e11fe305f0261df59cd2')

build() {
  cd MediaElch-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DDISABLE_UPDATER=ON \
    -DUSE_EXTERN_QUAZIP=ON \
    -Wno-dev \
    -G Ninja
  ninja -C build
}

package() {
  cd MediaElch-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
