# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-parse-recdescent
pkgver=1.967015
pkgrel=1
pkgdesc="Recursive descent parser generator"
arch=(any)
url="https://search.cpan.org/dist/Parse-RecDescent"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0')
makedepends=('perl-module-build')
options=('!emptydirs')
LC_NUMERIC=C
provides=("perl-parse-recdescent=`printf %.6f ${pkgver}`")
source=(https://search.cpan.org/CPAN/authors/id/J/JT/JTBRAUN/Parse-RecDescent-$pkgver.tar.gz)
sha256sums=('1943336a4cb54f1788a733f0827c0c55db4310d5eae15e542639c9dd85656e37')

build() {
  cd  "$srcdir"/Parse-RecDescent-$pkgver
  perl Build.PL installdirs=vendor destdir="$pkgdir/"
  perl Build
}

package() {
  cd  "$srcdir"/Parse-RecDescent-$pkgver
  perl Build install
  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
