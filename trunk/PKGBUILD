# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Matthias Bauch <matthias.bauch@gmail.com>
# Contributor: Laszlo Papp <djszapi2 at gmail com>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

pkgname=openocd
pkgver=0.8.0
pkgrel=1
pkgdesc='Debugging, in-system programming and boundary-scan testing for embedded target devices'
arch=('i686' 'x86_64')
url='http://openocd.berlios.de'
license=('GPL')
depends=('libftdi' 'libftdi-compat' 'libusb' 'libusb-compat')
options=(!strip)
install=openocd.install
_features=(amtjtagaccel arm-jtag-ew at91rm9200 buspirate ep93xx ftdi gw16012 jlink oocd_trace opendous osbdm parport presto_libftdi remote-bitbang rlink stlink ti-icdi ulink usbprog vsllink)
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-${pkgver/_/-}.tar.bz2)
md5sums=('6d83c34763a5f1d1ac7ad83c5a11f4fb')

prepare() {
  cd $pkgname-${pkgver/_/-}
  sed -i 's|ftdi_new();|(void*)12345;|g' configure{,.ac}
}

build() {
  cd $pkgname-${pkgver/_/-}
  libtoolize
  autoreconf
  ./configure --prefix=/usr ${_features[@]/#/--enable-} --disable-werror
  make
}

package() {
  cd $pkgname-${pkgver/_/-}
  make DESTDIR="$pkgdir" install
  (cd $pkgdir/usr/share/openocd/scripts/target && mv 1986*.cfg 1986be1t.cfg)
}
