# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sean V Kelley <seanvk@posteo.de>

pkgname=libva-utils
pkgver=2.17.1
pkgrel=1
pkgdesc="Intel VA-API Media Applications and Scripts for libva"
arch=(x86_64)
url="https://github.com/intel/libva-utils"
license=(custom)
depends=(libva)
makedepends=(mesa meson)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6ea5993c3eba230a979fa9d35b4cad8df06d4474a773dc0918033bf50353f966')

build() {
    arch-meson ${pkgname}-${pkgver} build
    meson compile -C build
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
    install -Dm644 ${pkgname}-${pkgver}/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
