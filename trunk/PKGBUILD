# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-mu1
pkgver=0.2.2
pkgrel=4
pkgdesc="A simple JACK app used to organise stereo monitoring"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-mu1-doc/quickguide.html"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'clxclient' 'clthreads' 'jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('72d3d21ad8c8e597ea54de3c91abfb5783a24dd2e2a316afd2875f761ce51b3e88a010f183b9e0880fffbc61d2ba4ec6ab23e2486fe90058caa4418fd70c21e9')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # strip march=native
  sed -i '/native/d' Makefile
  # fix png install dir
  sed -e 's/png\ \$(DESTDIR)\$(SHARED)/png\ -t\ \$(DESTDIR)\$(SHARED)/' -i Makefile
  # fix binary install
  sed -e 's|zita-mu1 $(DESTDIR)$(BINDIR)|zita-mu1 $(DESTDIR)$(BINDIR)/$(PROGRAM)|' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 ../{AUTHORS,README}
  # html
  install -t "${pkgdir}/usr/share/doc/${pkgname}/html" \
    -vDm644 ../doc/html/*
}
# vim:set ts=2 sw=2 et:
