# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=userpath
pkgname=python-$_pkgname
pkgver=1.3.0
pkgrel=1
pkgdesc='Cross-platform tool for adding locations to the user PATH, no elevated privileges required!'
arch=('any')
url='https://github.com/ofek/userpath'
license=('MIT' 'Apache')
depends=('python' 'python-click')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('e7ad8e855f4ebd908aa774f0e726e1de5a84b162789a9e6ddfed18518f40c762dd817a787e79f260be41f4b1ddbf763d1764de2f3b3c087348b39f66b106f156')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
  install -Dm 644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

# vim:set ts=2 sw=2 et:
