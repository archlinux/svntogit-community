# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=gopass
pkgver=1.8.6
pkgrel=3
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/gopasspw/gopass"
license=('MIT')
makedepends=('go-pie')
optdepends=('xdotool: for typing passwords selected by dmenu'
            'xsel: clipboard support'
            'xclip: clipboard support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gopasspw/gopass/archive/v${pkgver}.tar.gz")
sha512sums=('7acdb095cf8913954044c3206eab020f197542334798c6b94724709c3953b12f7e5b73ce8c1aa4bfd5df3acf1f424b64bfa94f9a8f68fa7116ed14c8a0f2e6f0')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src
  mv "${pkgname}-${pkgver}"/vendor/* src/
  mkdir -p src/github.com/gopasspw
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/gopasspw/gopass
}

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  make build
  make completion
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
