# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jeffrey Zhang <zhang.lei.fly@gmail.com>

_name=esbonio
pkgname=python-esbonio
pkgver=0.15.0
pkgrel=3
pkgdesc='Language server for working with Sphinx projects'
arch=('any')
url=https://github.com/swyddfa/esbonio
license=('MIT')
depends=('python-appdirs' 'python-sphinx' 'python-pygls' 'python-pyspellchecker'
         'python-setuptools' 'python-typing_extensions')
makedepends=('python-build' 'python-installer' 'python-setuptools'
             'python-wheel')
#checkdepends=('python-pytest-lsp')
source=("$url/archive/esbonio-language-server-v$pkgver/$_name-$pkgver.tar.gz"
        'esbonio-pygls1.patch::https://github.com/swyddfa/esbonio/pull/484.patch')
b2sums=('2bf04b28612a7c1aba46b17af02c1ee1a34674e713fe97d0b8723bd94b9415b04bcd4b3a33c67625ff7f76a79d9b166856d41b42357bc7658c9542e1cd00d990'
        'c6cca70ce79fc6dc95c68d4cfd1d64dc7c68eb1cdb50020ed0eed7b5b500d55912dc6c3f24baf8e575ca76273d56aa64bb1440059c944a133d129c665338166a')

prepare() {
  cd esbonio-esbonio-language-server-v$pkgver
  patch --forward --strip=1 --input=../esbonio-pygls1.patch
}

build() {
  cd esbonio-esbonio-language-server-v$pkgver/lib/esbonio
  python -m build --wheel --skip-dependency-check --no-isolation
}

#check() {
#  cd esbonio-esbonio-language-server-v$pkgver/lib/esbonio
#  python -m venv --system-site-packages test-env
#  test-env/bin/python -m installer dist/*.whl
#  PATH="$PWD/test-env/bin:$PATH" test-env/bin/python -m pytest
#}

package() {
  cd esbonio-esbonio-language-server-v$pkgver/lib/esbonio
  python -m installer --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s "$site_packages"/$_name-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
