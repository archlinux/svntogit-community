# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-annotation
pkgname=('python-zope-annotation' 'python2-zope-annotation')
pkgver=4.6.0
pkgrel=2
pkgdesc="Object annotation mechanism"
arch=('any')
url="https://github.com/zopefoundation/zope.annotation"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-component' 'python2-zope-component'
             'python-zope-location' 'python2-zope-location' 'python-zope-proxy'
             'python2-zope-proxy')
checkdepends=('python-zope-configuration' 'python2-zope-configuration' 'python-zope-testing'
              'python2-zope-testing' 'python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.annotation/archive/$pkgver.tar.gz")
sha512sums=('d366e37ccebe646c3260f6341503689f97b170de6e0aa127f93c437ec22deb15a7ae3f6e13f8fb1b667f87a1ed4ffb7f38cd6ce4e91406a0c8ccc0e356ef0795')

prepare() {
  cp -a zope.annotation-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.annotation-$pkgver
  python setup.py build

  cd "$srcdir"/zope.annotation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.annotation-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.annotation-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-annotation() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-component'
           'python-zope-location' 'python-zope-proxy')

  cd "$srcdir"/zope.annotation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-annotation() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-component'
           'python2-zope-location' 'python2-zope-proxy')

  cd "$srcdir"/zope.annotation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
