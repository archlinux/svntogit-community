# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: CosmicHorror <LovecraftianHorror@pm.me>

pkgname=cargo-auditable
pkgver=0.6.0
pkgrel=1
pkgdesc="A cargo-subcommand to make production Rust binaries auditable"
arch=('x86_64')
url="https://github.com/rust-secure-code/cargo-auditable"
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ddbe99d6e042a084d9cf50d78c337644f81f0def3499c3c33f16a0c3f4aa70367b20dda536607fbeae3e4ef4760c732ae508d3a348f9dbff6c95ce5a4319737a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
