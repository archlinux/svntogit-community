# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=i3status-rust
pkgver=0.14.1
pkgrel=2
pkgdesc='Resourcefriendly and feature-rich replacement for i3status, written in pure Rust'
arch=('x86_64')
url='https://github.com/greshake/i3status-rust'
license=('GPL3')
depends=('libpulse')
makedepends=('rust')
optdepends=('alsa-utils: for the volume block'
            'bluez: for the bluetooth block'
            'curl: for the weather block'
            'fakeroot: for the pacman block to show pending updates'
            'ibus: for the ibus block'
            'kdeconnect: for the kdeconnect block'
            'lm_sensors: for the temperature block'
            'networkmanager: for the networkmanager block'
            'powerline-fonts: for all themes using the Powerline arrow char'
            'pulseaudio: for the volume block'
            'speedtest-cli: for the speedtest block'
            'ttf-font-awesome: for the awesome icons'
            'upower: for the battery block')
source=("https://github.com/greshake/i3status-rust/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('5251aef5c91c200561c3e0ff8f4794ea8d30bb4036cdf67d24436b2b267965c2b4dddb2a2e4d4a60194866212a9e96ee96fc794bcc9cdbd198a6005b84a0e8ea')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 -t "${pkgdir}"/usr/bin target/release/i3status-rs
  install -Dm644 -t "${pkgdir}/usr/share/man/man1" man/i3status-rs.1

  for example in example_config.toml example_icon.toml example_theme.toml; do
    install -Dm644 "${example}" -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  done
}
