# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.11485
pkgrel=3
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses spirv-tools zlib)
makedepends=(git cmake clang lld llvm python libunwind spirv-headers vc-intrinsics)
options=(!emptydirs)
source=(${url}/archive/igc-${pkgver}.tar.gz
        fix-zlib-linking.patch
        ${pkgname}-fix-BiF-caching.patch::https://github.com/intel/intel-graphics-compiler/commit/12c99343388eba6e6275856b25e0fa8978585dfb.patch
        ${pkgname}-wrap-getNumArgOperands.patch::https://github.com/intel/intel-graphics-compiler/commit/1d5ceafdbc005f3adc58be5af1dc92f068908e16.patch
        ${pkgname}-update-raytracing-llvm14.patch::https://github.com/intel/intel-graphics-compiler/commit/d9535cc5c3e54b21d56c492d4e21cb13c80b9b7a.patch
        ${pkgname}-fix-getElementType.patch::https://github.com/intel/intel-graphics-compiler/commit/49f4d4e3e904179bf3a0b880732b92f8bfb2c64a.patch)
sha256sums=('a2bcb5a96c64691b21ab6b573a2b09dbe1354272281cf640f4d43ecd7753e25f'
            '41ad4d72b4f5e00161d736c8f51c9a2421548d226511cd133143408fccc4bfd0'
            '474d5ad34fa4c7c447190470346f8a944f105a11b1c95c2cded1fc28934fea48'
            'a98519cdc6fd896aa59cdae10da41e12d45a73d073646104c76c09ac6c9b5c76'
            '16b77d68da5f832c67650135f673bf33ff70dd84ce9cf6fae502588b566ec6c9'
            '43d94e5408142d401e35394add9ecdf2a283740a4cca7691fd3689626482006b')

prepare() {
  patch -p1 -d ${pkgname}-igc-${pkgver} < fix-zlib-linking.patch
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-fix-BiF-caching.patch
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-wrap-getNumArgOperands.patch
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-update-raytracing-llvm14.patch
  patch -p1 -d ${pkgname}-igc-${pkgver} < ${pkgname}-fix-getElementType.patch
}

build() {
  export CFLAGS="$CFLAGS -Wno-error=odr"
  export CXXFLAGS="$CXXFLAGS -Wno-error=odr"
  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='14.0.6' \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__USE_PREINSTALLED_SPRIV_HEADERS=ON \
    -DIGC_OPTION__SPIRV_TOOLS_MODE=Prebuilds \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DIGC_OPTION__VC_INTRINSICS_MODE=Prebuilds \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
