# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=5.1.2
pkgrel=5
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('x86_64')
url="https://kmymoney.org/"
license=('GPL')
depends=('aqbanking' 'gwenhywfar' 'boost-libs' 'libalkimia' 'sqlcipher' 'qt5-base' 'qt5-webengine' 'qgpgme' 'karchive'
         'kcoreaddons' 'kconfig' 'kwidgetsaddons' 'ki18n' 'kcompletion' 'kcmutils' 'kitemmodels'
         'kitemviews' 'kservice' 'kwallet' 'kiconthemes' 'kxmlgui' 'kidentitymanagement'
         'ktextwidgets' 'knotifications' 'kio' 'kholidays' 'kactivities'
         'kqtquickcharts' 'kdiagram' 'kcontacts' 'libical' 'libakonadi' 'shared-mime-info')
makedepends=('boost' 'extra-cmake-modules' 'kdoctools' 'doxygen')
optdepends=('perl: for financequote.pl')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/office/kmymoney/-/commit/a06d2fe1.patch
        4f8a333c.patch)
validpgpkeys=('D69A745A55331F44F404D8258D4DE062AA2EB01C')
sha256sums=('37bdc4e763a2849b9f739f73e38b389c02bde1c1b1844edcfa7e3ab1d5bf7b3b'
            'SKIP'
            '822a1e7a1c7692b50f0c7f9dc76f6548cb83b0fac86f93a10105818d7a037d80'
            '6e98eb3ef809019f71b131e6c4901dee5ced49fbf4c01a8dacce060f39e2a497')

prepare() {
  patch -d $pkgname-$pkgver -p1 < a06d2fe1.patch # Fix build with akonadi 21.12
  patch -d $pkgname-$pkgver -p1 < 4f8a333c.patch # Fix build with gwenhywfar 5.8
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  mkdir build
  cd build

  cmake ../ \
    -DENABLE_WEBENGINE=ON \
    -DCMAKE_SKIP_RPATH=YES \
    -Wno-dev
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver/build

  make DESTDIR="${pkgdir}" install
}
