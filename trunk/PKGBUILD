# Maintainer:
# Contributor: travisghansen <travisghansen@yahoo.com>
# Contributor: aleiphoenix <aleiphoenix@gmail.com>

pkgname=librabbitmq-c
pkgver=0.9.0
pkgrel=1
pkgdesc="A RabbitMQ(amqp) library written in C-language"
arch=('x86_64')
license=('MIT')
depends=('popt' 'openssl')
makedepends=('cmake' 'docbook-xsl' 'doxygen' 'xmlto')
url="https://github.com/alanxz/rabbitmq-c"
source=("$pkgname-$pkgver.zip::https://github.com/alanxz/rabbitmq-c/archive/v$pkgver.zip")
sha512sums=('0cac0e26b4caca3de22b468200a20c38c8ace1a824442faa0855d93922b0c3a649fc2067366f6694eca0be635187ca9a9cfe47643472f4abb3d67028768f49fb')

build() {
  cd ${pkgname:3}-$pkgver
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TOOLS=ON \
    -DBUILD_TOOLS_DOCS=ON \
    -DBUILD_TESTS=OFF \
    -DENABLE_SSL_SUPPORT=ON \
    -DBUILD_API_DOCS=ON
  make
}

package() {
  cd ${pkgname:3}-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
