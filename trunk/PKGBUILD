# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michal Bozon <michal.bozon__at__gmail.com>

pkgbase=python-yaml
pkgname=(python-yaml python2-yaml)
pkgver=5.3
pkgrel=1
pkgdesc='Python bindings for YAML, using fast libYAML library'
url='https://pyyaml.org/wiki/PyYAML'
arch=('x86_64')
license=('MIT')
makedepends=('python' 'python2' 'libyaml' 'cython' 'cython2')
source=(https://pyyaml.org/download/pyyaml/PyYAML-${pkgver}.tar.gz)
sha512sums=('9124bf4d58ebe925f430fd6ed749e5a3311a54d536a035d39794c485843ddb80692fd7f7bac05797507128b933297ff730d967f49e7237c7c567086a83ee9e1f')

prepare() {
  # Force cython rebuild
  rm PyYAML-$pkgver/ext/_yaml.c

  cp -a PyYAML-$pkgver{,-py2}
}

build() {
  msg2 "Building python..."
  (cd PyYAML-$pkgver
    python setup.py --with-libyaml build
  )
  msg2 "Building python2..."
  (cd PyYAML-$pkgver-py2
    python2 setup.py --with-libyaml build
  )
}

check() {
  msg2 "Checking python..."
  (cd PyYAML-$pkgver
    python setup.py test
  )
  msg2 "Checking python2..."
  (cd PyYAML-$pkgver-py2
    python2 setup.py test
  )
}

package_python-yaml() {
  depends=('python' 'libyaml')

  cd PyYAML-$pkgver
  python setup.py  --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

package_python2-yaml() {
  depends=('python2' 'libyaml')

  cd PyYAML-$pkgver-py2
  python2 setup.py --with-libyaml install --prefix=/usr --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm 644 CHANGES README -t "${pkgdir}"/usr/share/doc/${pkgname}
}

# vim: ts=2 sw=2 et:
