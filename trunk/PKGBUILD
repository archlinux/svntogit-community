# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-genesis-plus-gx
pkgver=1111
pkgrel=1
pkgdesc='Sega MS/GG/MD/CD core'
arch=('x86_64')
url='https://github.com/libretro/Genesis-Plus-GX'
license=('BSD' 'LGPL2.1')
groups=('libretro')
depends=('glibc')
makedepends=('git')
_commit='afa38e3e37f542b597e000cee973acabe3b2fd9e'
source=("libretro-genesis-plus-gx::git+https://github.com/libretro/Genesis-Plus-GX.git#commit=${_commit}"
        'https://raw.githubusercontent.com/libretro/libretro-super/master/dist/info/genesis_plus_gx_libretro.info')
sha256sums=('SKIP'
            'ef74514ba5836e93dba7eb17d092590d1c34fbca644a1c3ec55a832a8d1f8717')

pkgver() {
  cd libretro-genesis-plus-gx

  git rev-list --count HEAD
}

build() {
  cd libretro-genesis-plus-gx

  make -f Makefile.libretro
}

package() {
  cd libretro-genesis-plus-gx

  install -Dm 644 genesis_plus_gx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 ../genesis_plus_gx_libretro.info -t "${pkgdir}"/usr/share/libretro/info/

  install -Dm 644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libretro-genesis-plus-gx/
}

# vim: ts=2 sw=2 et:
