# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Wouter Wijsman <wwijsman@live.nl>
# Contributor: Duck Hunt <vaporeon@tfwno.gf>

pkgname=libretro-ppsspp
pkgver=34041
pkgrel=1
pkgdesc='Sony PlayStation Portable core'
arch=(x86_64)
url=https://github.com/libretro/ppsspp
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glew
  glibc
  libgl
  libpng
  libretro-core-info
  libzip
  ppsspp-assets
  sdl2
  snappy
  zlib
  zstd
)
makedepends=(
  cmake
  git
  libglvnd
  ninja
  python
)
_commit=32c717c94f68d1b751a6ed7ae54eb0b7a848af2b
source=(
  libretro-ppsspp::git+https://github.com/hrydgard/ppsspp.git#commit=${_commit}
  git+https://github.com/Kingcom/armips.git
  git+https://github.com/hrydgard/ppsspp-ffmpeg.git
  ppsspp-glslang::git+https://github.com/hrydgard/glslang.git
  git+https://github.com/hrydgard/ppsspp-lang.git
  ppsspp-miniupnp::git+https://github.com/hrydgard/miniupnp.git
  git+https://github.com/Tencent/rapidjson.git
  git+https://github.com/KhronosGroup/SPIRV-Cross.git
  armips-filesystem::git+https://github.com/Kingcom/filesystem.git
  libretro-ppsspp-assets-path.patch
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'c72640695e3e3b9512f13d71956872cea34b41cf28bd31c7dbd8062e1166d5ae8f248b01d6f5c73dd96d3ed5d5d59eab69c5619272525aefaa322cc2ca8b8541')

pkgver() {
  cd libretro-ppsspp

  git rev-list --count HEAD
}

prepare() {
  cd libretro-ppsspp

  patch -Np1 -i ../libretro-ppsspp-assets-path.patch

  git config --global protocol.file.allow always

  for submodule in ffmpeg assets/lang ext/glslang ext/miniupnp; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../ppsspp-${submodule#*/}
    git submodule update ${submodule}
  done
  for submodule in ext/{armips,rapidjson,SPIRV-Cross}; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../${submodule#*/}
    git submodule update ${submodule}
  done

  cd ext/armips

  for submodule in ext/filesystem; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../../../armips-${submodule#*/}
    git submodule update ${submodule}
  done
}

build() {
  cmake -S libretro-ppsspp -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_SKIP_RPATH=ON \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -DHEADLESS=OFF \
    -DLIBRETRO=ON \
    -DMOBILE_DEVICE=OFF \
    -DSIMULATOR=OFF \
    -DUNITTEST=OFF \
    -DUSE_SYSTEM_LIBZIP=ON \
    -DUSE_SYSTEM_SNAPPY=ON \
    -DUSE_SYSTEM_ZSTD=ON \
    -DUSING_QT_UI=OFF \
    -Wno-dev
  cmake --build build
}

package() {
  install -Dm 644 build/lib/ppsspp_libretro.so -t "${pkgdir}"/usr/lib/libretro/
  install -Dm 644 libretro-ppsspp/LICENSE.TXT -t "${pkgdir}"/usr/share/licenses/libretro-ppsspp/
}

# vim: ts=2 sw=2 et:
