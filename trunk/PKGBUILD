# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Rafael Ferreira <josephgbr@archlinux.info>

pkgname=lucene++
pkgver=3.0.5
pkgrel=3
pkgdesc='C++ port of the popular Java Lucene library'
arch=(i686 x86_64)
url='https://github.com/luceneplusplus/LucenePlusPlus'
license=('GPL' 'Apache')
depends=('boost-libs')
makedepends=('boost' 'cmake')
source=(https://github.com/luceneplusplus/LucenePlusPlus/archive/rel_$pkgver.tar.gz)
md5sums=('da785510716a759d7d37a2ee52cb8140')

prepare() {
  mkdir build
  sed -i \
    -e 's#SET(LUCENE++_VERSION_REVISION.*#SET(LUCENE++_VERSION_REVISION "5")#' \
    -e 's#SET(LUCENE++_VERSION_PATCH.*#SET(LUCENE++_VERSION_PATCH "0")#' \
    $srcdir/LucenePlusPlus-rel_$pkgver/CMakeLists.txt
}

build() {
  cd build

  cmake ../LucenePlusPlus-rel_$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DBoost_LIBRARY_DIR=/usr/lib \
    -DENABLE_CUSTOM_ALLOCATOR='FALSE'

  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
