# $Id$
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Chris Salzberg <chris@dejimata.com>
# Contributor: Leonidas Spyropoulos <artafinde@gmail.com>

pkgname=neomutt
pkgver=20171013
pkgrel=2
pkgdesc='a version of mutt with added features'
url='http://www.neomutt.org/'
license=('GPL')
arch=('x86_64')
depends=('mime-types' 'notmuch-runtime' 'lua')
optdepends=('urlview: for url menu'
            'python: keybase.py')
makedepends=('git' 'gnupg' 'libxslt')
source=("https://github.com/neomutt/neomutt/archive/$pkgname-$pkgver.tar.gz"
        "https://github.com/neomutt/neomutt/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz.sig")
sha256sums=('5a7c6302d623b620db03d923a7cd7cf967123a67fc42c117a0dd4b45612e9a55'
            'SKIP')
validpgpkeys=('86C2397270DD7A561263CA4E5FAF0A6EE7371805') # Richard Russon (flatcap) <rich@flatcap.org>

prepare() {
    cd "$pkgname-$pkgname-$pkgver"
    autoreconf -i
}

build() {
    cd "$pkgname-$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-debug \
        --enable-gpgme \
        --enable-lua \
        --enable-notmuch \
        --with-gss=/usr \
        --with-ssl=/usr \
        --with-sasl \
        --with-curses=/usr \
        --with-idn \
        --with-gdbm
    make
}

package() {
    cd "$pkgname-$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
