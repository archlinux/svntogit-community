# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xed
pkgver=3.2.6
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=(x86_64)
url="https://github.com/linuxmint/${pkgname}"
license=(GPL)
groups=(x-apps)
depends=(gspell gtksourceview4 libpeas xapp)
makedepends=(meson samurai gobject-introspection intltool itstool)
optdepends=('python: required for some plugins')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6676d86d78c1faebbace385cf49cdb24ba8ddd445e8cdb02e9d72453b735d265')
b2sums=('746552298adb8432853bfcf561bbfb99774a00cf017db0c3c3809a736d0bd93f7cef4ae5bbbe77da995cea85a6d00d469156bb52bc23a933a1059aeceac2701b')

build() {
  arch-meson ${pkgname}-${pkgver} build
  samu -C build
}

package(){
  DESTDIR="${pkgdir}" samu -C build install
}
