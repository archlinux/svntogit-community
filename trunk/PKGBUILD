# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mubashshir <ahmubashshir@gmail.com>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>

pkgname=python-confuse
pkgver=2.0.0
pkgrel=1
pkgdesc='Painless YAML config files for Python'
arch=(any)
url=https://github.com/beetbox/confuse
license=(MIT)
depends=(python-yaml)
makedepends=(
  git
  python-flit
  python-pip
  python-poetry
)
checkdepends=(python-nose)
_tag=46504692f9f6c26e248a90243062ccca1fd29992
source=(git+https://github.com/beetbox/confuse.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd confuse
  git describe --tags | sed 's/^v//'
}

build() {
  cd confuse
  python -m flit build --format wheel
}

check() {
  cd confuse
  python -m nose
}

package() {
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="${pkgdir}" --ignore-installed --no-deps confuse/dist/*.whl
  install -Dm 644 confuse/LICENSE -t "${pkgdir}"/usr/share/licenses/python-confuse/
}

# vim: ts=2 sw=2 et:
