# $Id$
# Maintainer: Morten Linderud <foxboron@archlinux.org>
# Contibutor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor:  Pawel "kTT" Salata <rockplayer.pl@gmail.com>

pkgbase=python-pycountry
pkgname=(python-pycountry python2-pycountry)
pkgver=18.5.26
pkgrel=3
pkgdesc="ISO country, subdivision, language, currency and script definitions and their translations"
arch=('any')
url="https://pypi.org/project/pycountry/"
license=('LGPL2.1')
makedepends=('python2' 'python2-setuptools'
	     'python' 'python-setuptools')
checkdepends=('python2-pytest' 'python2-pytest-runner'
	      'python-pytest' 'python-pytest-runner')
source=("https://pypi.io/packages/source/p/pycountry/pycountry-${pkgver}.tar.gz")
sha512sums=('9760175a7926347920542a2fd2420cc3d4f36ac11df5aa6f7aaabbd9b46dd9cb61801933133a589ed35fd2e279db2a70e98340d8cf8856987dc88d6fdb715437')

prepare(){
  cp -r pycountry-${pkgver}{,-py2}
}

build(){
  cd "${srcdir}/pycountry-${pkgver}"
  python setup.py build

  cd "${srcdir}/pycountry-${pkgver}-py2"
  python2 setup.py build
}

check(){
  cd "${srcdir}/pycountry-${pkgver}"
  python setup.py pytest

  cd "${srcdir}/pycountry-${pkgver}-py2"
  python2 setup.py pytest
}


package_python2-pycountry() {
  depends=(python python2-lxml)
  cd "${srcdir}/pycountry-${pkgver}"
  python2 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}

package_python-pycountry() {
  depends=(python python-lxml)
  cd "${srcdir}/pycountry-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
