# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=backcall
pkgname=python-backcall
pkgver=0.1.0
pkgrel=2
pkgdesc='Backwards compatible callback APIs'
arch=('any')
url=https://github.com/takluyver/backcall
license=('BSD')
depends=('python')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('38ecd85be2c1e78f77fd91700c76e14667dc21e2713b63876c0eb901196e01e4')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" python tests/test_callback_prototypes.py
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
