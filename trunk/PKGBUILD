# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=freeradius
pkgver=3.0.15
pkgrel=1
pkgdesc="The premier open source RADIUS server"
arch=('i686' 'x86_64')
url="http://www.freeradius.org/"
license=('GPL')
depends=('krb5' 'pth' 'net-snmp' 'postgresql-libs' 'libmariadbclient' 'talloc' 'libpcap')
makedepends=('unixodbc' 'python2' 'curl' 'json-c')
optdepends=('unixodbc: for ODBC'
	    'python2: for Python'
	    'json-c: rlm_rest module'
	    'curl: for REST')
options=('!makeflags')
install=$pkgname.install
source=("ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2"
	freeradius.tmpfiles
	freeradius.service freeradius-openssl-1.1.patch)
sha256sums=('23267d8505e7b2909f5bdbf3938ca077c1fe122290dc969304d4f3b594f7e3ba'
            '08a084e143b263e8acbb8e4740a485f868647e047f3b6ae72b9b925d0416714f'
            '968cfd22f59e2a8ebe44955251a1f510fcdfdeba901798e12d3646d4439e0b1c'
            '406d22b7e15017163f3736a0a91c6ef8c327fd5a19af74b3519d16a4f7f205fd')

prepare() {
  cd freeradius-server-$pkgver
#  patch -p1 -i ../freeradius-openssl-1.1.patch
}

build() {
  cd "$srcdir"/freeradius-server-$pkgver
  ./configure --with-system-libtool --with-system-libltdl \
	      --prefix=/usr --enable-heimdal-krb5 \
	      --localstatedir=/var \
	      --sysconfdir=/etc \
	      --sbindir=/usr/bin \
	      --libdir=/usr/lib/freeradius \
	      --with-udpfromto
  make
}

package() {
  cd "$srcdir"/freeradius-server-$pkgver
  make install R="$pkgdir"
  chmod o+r "$pkgdir"/etc/raddb/*
  mv "$pkgdir"/etc/raddb "$pkgdir"/etc/raddb.default
  rm -rf "$pkgdir"/var/run

  install -Dm0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -Dm0644 "$srcdir"/$pkgname.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
