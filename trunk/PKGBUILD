# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=sdcv
pkgver=0.5.0beta4
pkgrel=1
pkgdesc="StarDict Console Version"
arch=('i686' 'x86_64')
url="http://sdcv.sourceforge.net/"
license=('GPL')
depends=('glib2' 'readline' 'zlib')
makedepends=('cmake')
provides=('stardict')
changelog=$pkgname.changelog
source=(https://sourceforge.net/projects/$pkgname/files/$pkgname/$pkgname-0.5.0-beta4-Source.tar.bz2)
sha256sums=('19e7ec25de76fc877d35d25542080907b4f89e44d8746f704a24304155483bad')

build() {
  cd "${srcdir}"/$pkgname-0.5.0-beta4-Source

  mkdir build
  cd build

  cmake ../ -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
  make lang
}

package() {
  cd "${srcdir}"/$pkgname-0.5.0-beta4-Source/build

  make DESTDIR="${pkgdir}" install
}
