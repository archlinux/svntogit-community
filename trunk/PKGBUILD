# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.204
pkgrel=1
pkgdesc="An extremely fast Python linter, written in Rust"
arch=(x86_64)
url="https://github.com/charliermarsh/ruff"
license=(MIT)
depends=(gcc-libs glibc python)
makedepends=(cargo maturin python-installer)
options=(!lto debug)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('1e50a21423bdb177bcb1518c6bb02fff8c2e087ca7521a14070746f6833773daf098a496ed308930d36ce897aed959a5972d8a5e83b5f8372262ab41b8c840fd')
b2sums=('89974bcb122b61cb6b513b9f07a998c909627793e0eb3c84946ac97ff9c59f7c8fb2eb3f87b554554dc1bc64636aff311893c6af0a642713cc097636f24bc33b')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  local maturin_options=(
    --release
    --strip
    --locked
    --all-features
    --target $CARCH-unknown-linux-gnu
  )

  cd $pkgname-$pkgver

  maturin build "${maturin_options[@]}"
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
