# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-hangul
pkgver=5.0.8
pkgrel=1
pkgdesc="Hangul Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-hangul"
license=('GPL')
depends=('fcitx5' 'libhangul')
makedepends=('extra-cmake-modules')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('616fe1714dc833eaeebf13ee2c4b59b6e9de7a3aabdf44a7e6649340158a04a2d64f7cea60b892445bd667c1d878812d1868d4dbeab33ccf4b231d71399fd06e'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
