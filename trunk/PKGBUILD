# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.16.1
pkgrel=5
pkgdesc='Consumer grade GIS software'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack/wiki'
license=('GPL3')
depends=('alglib' 'gdal' 'proj' 'qt5-tools' 'qt5-webengine' 'quazip' 'routino>=3.1')
makedepends=('cmake')
source=($pkgname-$pkgver.tar.gz::https://github.com/Maproom/qmapshack/archive/refs/tags/V_$pkgver.tar.gz)
sha256sums=('b407c0710c77e87c9c1f9e1e0d9d6b9055b28dda41b77f9c69ca10a9a6f4e0d5')

build() {
  cmake -B build -S $pkgname-V_$pkgver \
      -DCMAKE_BUILD_TYPE=None \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
