# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.9.8
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=(x86_64)
url="https://qsampler.sourceforge.io/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libgig libgig.so liblscp liblscp.so
linuxsampler qt6-base qt6-svg)
makedepends=(cmake qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('4731eb86663261f09503a3f7c930b7def86f35ac4693b5a4514c16440957c693848522e87f41ba17f415ef6575b60f865f4d44c204020869ee37b682339e5fcf')
b2sums=('bd6431198e9143ff4123f0ed06e1e880891a7168412fe7fd0b09d5ef8be8a45027560fdb109d395a1fd66a1f6fdbf1c8b3dc673ec2a3d462f157048b0983142a')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
