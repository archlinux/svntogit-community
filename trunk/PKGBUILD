# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.5.1
pkgrel=1
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://ibus.googlecode.com'
depends=('ibus' 'anthy' 'python2' 'pygtk' 'gtk-update-icon-cache')
makedepends=('swig' 'intltool')
options=('!libtool')
install=$pkgname.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)

build() {
  cd $srcdir/$pkgname-$pkgver

  # python2 fix
  for file in setup/ibus-setup-anthy.in engine/ibus-engine-anthy.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir/ install
}

sha512sums=('ba5456d4b16c168ff90945c948109bd3bb9d64c145ae746a471789ef74a1e657a9f891e198ecf64b733c03880f81a28cc9cd2c1fa82ad006eb0ac6e7e5c2c3a5')
