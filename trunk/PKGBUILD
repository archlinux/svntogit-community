# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-meta
pkgname=haskell-hspec-meta
pkgver=2.7.8
pkgrel=128
pkgdesc="A version of Hspec which is used to test Hspec itself"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-setenv' 'haskell-random' 'haskell-quickcheck-io'
         'haskell-ansi-terminal' 'haskell-clock' 'haskell-call-stack')
makedepends=('ghc' 'uusi')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('f3a6dc3159090b505b67eb9a852b7a56ef19985984e7326ffc7812af56d87c9418d5ad69e181bf777cdee39c146575a5fec2174dd94c2a3926a15ebcb45f22d0')

prepare() {
    cd $_hkgname-$pkgver
    gen-setup
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
