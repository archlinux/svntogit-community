# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-email-validator
pkgname=(python-email-validator python2-email-validator)
pkgver=1.0.3
pkgrel=3
pkgdesc="A robust email syntax and deliverability validation library for Python 2.x/3.x."
arch=('any')
url="https://github.com/JoshData/python-email-validator"
license=('custom:CC0')
makedepends=('python-setuptools' 'python2-setuptools' 'python-dnspython' 'python2-dnspython'
             'python-idna' 'python2-idna')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/JoshData/python-email-validator/archive/v$pkgver.tar.gz")
sha512sums=('1e816fdaa796aa92963f0fd90c8b6d111c6484a45416365095d08adc9243a7f8c6a90fb07df9b2e60c664f9b756252d6589baf137f5e0e60833d8759363fdc26')

prepare() {
  cp -a python-email-validator-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-email-validator-$pkgver
  python setup.py build

  cd "$srcdir"/python-email-validator-$pkgver-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/python-email-validator-$pkgver
  python email_validator/__init__.py --tests < test_pass.txt

  cd "$srcdir"/python-email-validator-$pkgver-py2
  python2 email_validator/__init__.py --tests < test_pass.txt
}

package_python-email-validator() {
  depends=('python-dnspython' 'python-idna')

  cd "$srcdir"/python-email-validator-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-email-validator() {
  depends=('python2-dnspython' 'python2-idna')

  cd "$srcdir"/python-email-validator-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/email_validator{,2}
}

# vim:set ts=2 sw=2 et:
