# Maintianer: Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_pypiname=pyopencl
pkgbase=python-pyopencl
pkgname=('python2-pyopencl' 'python-pyopencl' 'pyopencl-headers')
pkgver=2018.2.2
pkgrel=1
epoch=1
pkgdesc="A complete, object-oriented language binding of OpenCL to Python"
arch=('x86_64')
url="https://mathema.tician.de/software/pyopencl"
license=('MIT' 'Apache' 'BSD')
depends=('opencl-icd-loader' 'opencl-headers' 'mesa' 'boost-libs')
makedepends=('ctags' 'boost' 'python-setuptools' 'python2-setuptools' 'python-mako' 'python2-mako'
             'python-numpy' 'python2-numpy' 'python-cffi' 'python2-cffi' 'git' 'pybind11')
 checkdepends=('python-six' 'python2-six' 'python-appdirs' 'python2-appdirs')
source=("git+https://github.com/inducer/pyopencl.git?signed#tag=v${pkgver}.signed")
validpgpkeys=("900A958D9A0ACA58B1468F2471AA298BCA171145") # Andreas Ratchke
sha256sums=('SKIP')

build() {
    # we use git clone here to ensure the submodules intialize properly
    # FIXME: I could do something more thorough later down the line
    git clone pyopencl{,-python2}
 
    cd "pyopencl"
    python3 ./configure.py --python-exe=python3 --cl-pretend-version=1.2 # --boost-python-libname=boost_python3
    make
 
    cd "${srcdir}/pyopencl-python2"
    python2 ./configure.py  --python-exe=python2 --cl-pretend-version=1.2 # --boost-python-libname=boost_python
    make
}

check(){
    cd pyopencl
    make tests

    cd "$srcdir/pyopencl-python2"
    make tests
}

package_python-pyopencl() {
    depends+=('python' 'python-numpy' 'python-mako' 'python-pytools'
        'pyopencl-headers' 'python-setuptools' 'python-cffi')

    cd pyopencl
    python3 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

    rm -fr "${pkgdir}"/usr/include

    install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyopencl() {
    depends+=('python2' 'python2-numpy' 'python2-mako' 'python2-pytools'
        'pyopencl-headers' 'python2-setuptools' 'python2-cffi')

    cd pyopencl-python2
    python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

    rm -fr "${pkgdir}/usr/include/"

    install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_pyopencl-headers() {
    cd pyopencl
 
    install -Dm644 pyopencl/cl/*.cl -t "${pkgdir}"/usr/include/pyopencl 
    install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
