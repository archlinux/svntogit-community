# Maintainer: Caleb Maclennan <caleb@alerque.com
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=cocogitto
_bin=cog
pkgver=5.1.0
pkgrel=2
pkgdesc='Set of CLI tools for the conventional commit and semver specifications'
arch=(x86_64)
url="https://github.com/$pkgname/$pkgname"
license=(MIT)
depends=(git
         gcc-libs
         libgit2
         libgit2.so
         zlib)
makedepends=(cargo)
_archive="$pkgname-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('662bb909c6468124ad19a90bc07a4f24345167486bd19542375ead7a8f20de02')

prepare() {
	cd "$_archive"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
	mkdir completions
}

build() {
	cd "$_archive"
	cargo build --frozen --release
	local compgen="target/release/$_bin generate-completions"
	$compgen bash > "completions/$_bin"
	$compgen fish > "completions/$_bin.fish"
	$compgen zsh  > "completions/_$_bin"
}

check() {
	cd "$_archive"
	# Test suite is not atomic, relies on user environment such as git user configs
	# cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/bash-completion/completions/" "completions/$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/fish/vendor_completions.d/" "completions/$_bin.fish"
	install -Dm0644 -t "$pkgdir/usr/share/zsh/site-functions/" "completions/_$_bin"
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
