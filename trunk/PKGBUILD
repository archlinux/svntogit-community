# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgbase=lib32-glib2
pkgname=(lib32-glib2)
pkgver=2.76.2
pkgrel=1
pkgdesc="Low level core library - 32-bit"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL)
arch=(x86_64)
depends=(
  glib2
  lib32-libffi
  lib32-pcre2
  lib32-util-linux
  lib32-zlib
)
makedepends=(
  gettext
  git
  gtk-doc
  lib32-dbus
  lib32-libelf
  meson
  python
  shared-mime-info
  util-linux
)
checkdepends=(
  desktop-file-utils
  lib32-glib2
)
options=(
  debug
)
_commit=41ae5b5632ce9f6b2b5613ec1912821f769166c2  # tags/2.76.2^0
source=(
  "git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
  "git+https://gitlab.gnome.org/GNOME/gvdb.git"
  0002-glocalfile-Sum-apparent-size-only-for-files-and-syml.patch
  0003-tests-file-Do-not-rely-on-du-bytes-behaviour.patch
  gio-querymodules-32.hook
)
b2sums=('SKIP'
        'SKIP'
        '6bcbcba60208162f7221701d6a642eabfc92c2fc6a476bcb42da5967577f8f0c75b688d149be01c9c48cd644aafa7fbdd63d9086385b8f7607fc981756d71a68'
        '257bf37d304cc161dedcde0a2c4d01e297f8263cde48b49d3ee47ca95a8fb9ad44bbb9bf99da51ec766ffb6f9d502e0a8fdc6b86346e6755373ee515e23b9419'
        '678ea2d010fd64b6c55106510096363c54c357d65615c666e9cc3a0e280c0878257a45e646dd88f6bdd0623f7268c4afd2d4f98f82a5489bbfc028c5864252f1')

pkgver() {
  cd glib
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glib

  # fix test suite issues with coreutils >=9.2
  # https://gitlab.gnome.org/GNOME/glib/-/merge_requests/3358
  git apply -3 ../0002-glocalfile-Sum-apparent-size-only-for-files-and-syml.patch
  git apply -3 ../0003-tests-file-Do-not-rely-on-du-bytes-behaviour.patch

  git submodule init
  git submodule set-url subprojects/gvdb "$srcdir/gvdb"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    --libdir=/usr/lib32
    -D glib_debug=disabled
    -D gtk_doc=false
    -D man=false
    -D selinux=disabled
    -D sysprof=disabled
  )

  export CC="gcc -m32 -mstackrealign"
  export CXX="g++ -m32 -mstackrealign"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  # Produce more debug info: GLib has a lot of useful macros
  CFLAGS+=" -g3"
  CXXFLAGS+=" -g3"

  arch-meson glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --no-suite flaky --no-suite slow --print-errorlogs
}

package_lib32-glib2() {
  depends+=(
    libffi.so
    libmount.so
  )
  provides+=(libg{lib,io,module,object,thread}-2.0.so)

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{lib,share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
  touch "$pkgdir/usr/lib32/gio/modules/.keep"
}

# vim:set sw=2 sts=-1 et:
