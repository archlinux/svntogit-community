# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=psi-plugins
pkgver=1.3
pkgrel=2
pkgdesc="Additional plugins for Psi"
arch=('x86_64')
url="https://psi-im.org"
license=('GPL2')
depends=('qt5-base' 'qt5-webkit' 'qt5-multimedia' 'psi' 'qt5-webengine')
makedepends=('libotr' 'tidyhtml')
optdepends=('libotr: for OTR plugin'
            'tidyhtml: for OTR plugin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/psi-im/plugins/archive/$pkgver.tar.gz"
        'psiplugin.pri')
sha256sums=('aef310c98c5a43399505a71a6b61f6ad64c97e0529d398462ce669249b4ffb9b'
            '2edc9f76e032e25044f0d16376194127c07b3372195482236901222ebec11a75')

prepare() {
    cp -v psiplugin.pri plugins-$pkgver/
}

build() {
    cd plugins-$pkgver
    # Kill broken plugins
    rm -rf generic/juickplugin
    for plugin in generic/*; do
        if [ -d "$plugin" ]; then
            (cd "$plugin" && qmake-qt5 PREFIX="/usr" QMAKE_STRIP= && make)
        fi
    done
}

package() {
    cd plugins-$pkgver
    for plugin in generic/* unix/*; do
        if [ -d "$plugin" ]; then
            (cd "$plugin" && make INSTALL_ROOT="$pkgdir" install)
        fi
    done
}
