# $Id: PKGBUILD 97778 2013-09-29 16:22:13Z jlichtblau $
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=python-html2text
pkgver=2014.12.29
pkgrel=2
pkgdesc="A HTML to markdown-structured text converter"
arch=('any')
url="https://pypi.python.org/pypi/html2text/"
license=('GPL3')
depends=('python-setuptools')
provides=('html2text')
replaces=('html2text')
source=(https://pypi.python.org/packages/source/h/html2text/html2text-$pkgver.tar.gz)
sha256sums=('c3977dfe6fd1ba0d4091f85963306488b3e9e236cfe60d8821158ce5a7fcb619')

build() {
  cd "${srcdir}"/html2text-$pkgver
  python setup.py build
}

check() {
  cd "${srcdir}"/html2text-$pkgver
  PYTHONPATH=$PYTHONPATH:. python setup.py test --verbose
}

package() {
  cd "${srcdir}"/html2text-${pkgver}
  python setup.py install --root="${pkgdir}"
}
