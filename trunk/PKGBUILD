# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: red_over_blue
# Contributor: neri

pkgname=povray
pkgver=3.7
pkgrel=1
pkgdesc='Script based raytracer for creating 3D graphics'
arch=('x86_64' 'i686')
license=('custom')
url='http://povray.org/'
depends=('libtiff' 'libpng' 'boost-libs')
makedepends=('boost')
backup=("etc/povray/povray.conf"
        "etc/povray/povray.ini")
source=("https://github.com/POV-Ray/$pkgname/archive/$pkgver-stable.zip"
        'povray3.7.0_rc3-user-conf.patch'
        'distribution-license.txt'
        'LICENSE'
        'source-license.txt')
sha256sums=('e81e82de8b07ab6cf6f4134830dc2d0156aedfe9cdd9e223e6e4aee7f4b5bdc0'
            '95b04ec4973e6e8a80fc83df2e3caff0b4d6699c4154144f3017d42cab7236e0'
            'bcf6893c741568bbff6839bb7310b0c43af8cb54bae792da21123e1026f91ae9'
            '9162060797de4f7f91ccf5e1fac5ec3378a0b0fed382393797ba3f4f27896402'
            '7d80992314f7947f5f672a4ad8b1d072a6179f01fb0228a88e4224a8bd45de96')

prepare() {
  cd "$pkgname-$pkgver-stable"

  cd unix
  sed 's/automake --w/automake --add-missing --w/g' -i prebuild.sh
  sed 's/dist-bzip2/dist-bzip2 subdir-objects/g' -i configure.ac
  ./prebuild.sh
  cd ..
  ./bootstrap
  patch -p1 < "$srcdir/povray3.7.0_rc3-user-conf.patch"
}

build() {
  cd "$pkgname-$pkgver-stable"

  ./configure LIBS="-lboost_system -lboost_thread" COMPILED_BY='Arch Linux' --prefix=/usr --sysconfdir=/etc
  make CXXFLAGS+="-w -lboost_system -lboost_thread"
  gzip -f povray.1
}

package() {
  cd "$pkgname-$pkgver-stable"

  mkdir -p "$pkgdir/usr/share/$pkgname-3.7"
  cp -r icons include ini scenes scripts "$pkgdir/usr/share/$pkgname-3.7"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname-3.7"
  cp -r doc "$pkgdir/usr/share/doc/$pkgname-3.7"
  install -Dm755 unix/povray "$pkgdir/usr/bin/povray"
  install -Dm644 povray.conf "$pkgdir/etc/povray/povray.conf"
  install -Dm644 povray.ini "$pkgdir/etc/povray/povray.ini"
  install -Dm644 povray.1.gz "$pkgdir/usr/share/man/man1/povray.1.gz"
  install -Dm644 ../LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 ../distribution-license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/distribution-license.txt"
  install -Dm644 ../source-license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/source-license.txt"
}

# vim:set ts=2 sw=2 et:
