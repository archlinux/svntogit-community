# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=5.002
pkgrel=1
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/projects/verilator/wiki/Intro'
arch=('x86_64')
license=('LGPL')
depends=('perl')
optdepends=('systemc')
# lsb-release is used by configure script to enable usage of c++17
makedepends=('python' 'systemc' 'lsb-release')
source=("https://github.com/verilator/verilator/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e7608155ca7ca9a4533262649323ebb2963193804b7b554d9c1997217f615fa9d8f3dcb6824d792806cfc7866b728e671e9ba1b62d0b00a1927954132665dd3a')

prepare() {
  cd verilator-$pkgver
  autoconf
}

build() {
  cd verilator-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator-$pkgver
  make test
}

package() {
  cd verilator-$pkgver
  make install DESTDIR="$pkgdir"
}
