# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: PyroPeter <abi1789@googlemail.com>
# Contributor: BlackEagle <ike.devolder@scarlet.be>
# Contributor: chochem <chochem@gmail.com>

pkgname=xmlstarlet
pkgver=1.4.2
pkgrel=1
pkgdesc="A set of tools to transform, query, validate, and edit XML documents"
arch=('i686' 'x86_64')
url="http://xmlstar.sourceforge.net/"
license=('MIT')
depends=('libxslt>=1.1.9')
source=("xmlstarlet-$pkgver.tar.gz::http://sourceforge.net/projects/xmlstar/files/xmlstarlet/$pkgver/xmlstarlet-$pkgver.tar.gz/download")
md5sums=('ee8df42efd2417f74a1f5e3b7fe9e023')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  local xsltlibs=$(pkg-config --libs libxslt libexslt)
  local xmllibs=$(pkg-config --libs libxml-2.0)

  LIBXSLT_PREFIX=/usr \
	LIBXML_PREFIX=/usr \
	LIBXSLT_LIBS="${xsltlibs}" \
	LIBXML_LIBS="${xmllibs}" \
	./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install

  install -Dm0644 Copyright $pkgdir/usr/share/licenses/$pkgname/COPYING
  ln -s xml ${pkgdir}/usr/bin/xmlstarlet
}
