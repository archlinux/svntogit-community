# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=2.5.2
pkgrel=3
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'twisted' 'python-mock' 'python2-mock' 'python-yaml' 'python2-yaml' 'python-pexpect' 'python2-pexpect')
source=("http://pypi.python.org/packages/source/p/pytest/pytest-$pkgver.tar.gz")

prepare() {
  cp -r pytest-${pkgver}{,-py2}
}

build() {
  cd "$srcdir/pytest-${pkgver}"
  python setup.py build

  cd "$srcdir/pytest-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/pytest-${pkgver}"
  python setup.py test

  cd "$srcdir/pytest-${pkgver}-py2"
  # Tests fail with 2.5.2: https://bitbucket.org/hpk42/pytest/issue/493/example-failure-in-pytests-own-testcase
  #python2 setup.py test
}

package_python-pytest() {
  depends=('python' 'python-py' 'python-setuptools')

  cd pytest-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pytest() {
  depends=('python2' 'python2-py' 'python2-setuptools')

  cd pytest-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "${pkgdir}/usr/bin/py.test" "${pkgdir}/usr/bin/py.test2"
}

sha512sums=('d291f229f4c0a2132c437dde6ca04bf87701fe270c2fa314104badad9221ab866251890c66387d23fefd3ab1f928ee3b290dac21e3d62e7660af8903e85a2f36')
