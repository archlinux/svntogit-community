# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-media-sdk
pkgname=('intel-media-sdk' 'libmfx')
pkgver=21.1.3
pkgrel=1
pkgdesc='API to access hardware-accelerated video on Intel Gen graphics hardware platforms'
arch=('x86_64')
url='https://software.intel.com/en-us/media-sdk/'
license=('MIT')
makedepends=('libdrm' 'libva' 'wayland' 'intel-media-driver'
             'cmake' 'libpciaccess' 'libx11' 'libxcb' 'python'
             'opencl-headers' 'opencl-clhpp' 'ocl-icd' 'intel-compute-runtime')
source=("https://github.com/Intel-Media-SDK/MediaSDK/archive/intel-mediasdk-${pkgver}.tar.gz"
        "https://github.com/Intel-Media-SDK/MediaSDK/commit/f6925886f27a39eed2e43c5b7b6c342d00f7a970.patch")
sha256sums=('718e68e3be78fcdc2dea0e3cd4714c38df2d451d7c8afef840c9c07e82bd4cb9'
            'ec78fdcd4a492ce75c81129d3834763ff04e4a73fbc16f6e5890e5cb8c191fdb')

prepare() {
    cd MediaSDK-intel-mediasdk-${pkgver}
    patch -p1 -i ../f6925886f27a39eed2e43c5b7b6c342d00f7a970.patch
}

build() {
    cmake -B build -S "MediaSDK-intel-mediasdk-${pkgver}" \
        -DBUILD_ALL:BOOL='ON' \
        -DBUILD_TOOLS:BOOL='ON' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DENABLE_ITT:BOOL='OFF' \
        -DENABLE_OPENCL:BOOL='ON' \
        -DENABLE_WAYLAND:BOOL='ON' \
        -DENABLE_X11_DRI3:BOOL='ON' \
        -Wno-dev
    make -C build
}

check() {
    make -C build test
}

package_intel-media-sdk() {
    depends=('libdrm' 'libva' 'wayland' "libmfx=${pkgver}" 'intel-media-driver')
    optdepends=('ocl-icd: for rotate_opencl plugin'
                'intel-compute-runtime: for rotate_opencl plugin')
    
    make -C build DESTDIR="$pkgdir" install
    ln -s ../share/mfx/samples/libcttmetrics.so "${pkgdir}/usr/lib/libcttmetrics.so"
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
    
    # remove core component libmfx
    [ -d 'libmfx' ] && rm -rf libmfx
    mkdir -p libmfx/{include,lib/pkgconfig}
    mv "${pkgdir}/usr/include/mfx" libmfx/include
    mv "${pkgdir}/usr/lib/libmfx.so"* libmfx/lib
    mv "${pkgdir}/usr/lib/pkgconfig/"{,lib}mfx.pc libmfx/lib/pkgconfig
    rm -d "${pkgdir}/usr/include"
}

package_libmfx() {
    pkgdesc='Intel Media SDK dispatcher library'
    depends=('gcc-libs')
    
    mv libmfx "${pkgdir}/usr"
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
