# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=racket
pkgname=(racket racket-docs racket-minimal)
pkgver=7.0
pkgrel=1
pkgdesc="A full-spectrum language with DrRacket IDE.  Formerly known as PLT Scheme."
arch=('x86_64')
url="http://racket-lang.org/"
license=('GPL3' 'LGPL3' 'custom')
depends=('gtk3')
makedepends=('gsfonts' 'sqlite')
options=('!strip' '!emptydirs')
source=("http://download.racket-lang.org/installers/${pkgver}/${pkgname}-${pkgver}-src.tgz")
# "https://mirror.racket-lang.org/installers/${pkgver}/racket-minimal-${pkgver}-src.tgz"
sha1sums=('67bb624f5ae810b4ebf85e8f3acfaeda9c95d66e')

prepare() {
  cd "${pkgbase}-${pkgver}"
  echo "Icon=drracket" >> share/pkgs/drracket/drracket/drracket.desktop
}

build() {
  cd "${pkgbase}-${pkgver}/src"
  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared
  make
}

package_racket() {
  cd "${pkgbase}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING-libscheme.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -Dm644 ../share/pkgs/drracket/drracket/drracket.desktop "${pkgdir}/usr/share/applications/drracket.desktop"
  install -d "${pkgdir}"/usr/share/icons/hicolor/{16x16,32x32,48x48,256x256}/apps
  ln -s /usr/share/racket/pkgs/icons/plt-16x16.png "${pkgdir}/usr/share/icons/hicolor/16x16/apps/drracket.png"
  ln -s /usr/share/racket/pkgs/icons/plt-32x32.png "${pkgdir}/usr/share/icons/hicolor/32x32/apps/drracket.png"
  ln -s /usr/share/racket/pkgs/icons/plt-48x48.png "${pkgdir}/usr/share/icons/hicolor/48x48/apps/drracket.png"
  ln -s /usr/share/racket/pkgs/icons/plt-logo-red-diffuse.png "${pkgdir}/usr/share/icons/hicolor/256x256/apps/drracket.png"

  # sloppy
  find "${pkgdir}/usr/share/doc/" -delete
}

package_racket-docs() {
  pkgdesc="Docs for the full-spectrum language formerly known as PLT Scheme."
  # grumble grumble this should be possible
  #arch=('any')
  depends=()
  cd "${pkgbase}-${pkgver}/src"
  # there is an "install-html" target
  # but it doesn't do anything with /usr/share/docs
  make DESTDIR="${pkgdir}" install
  find "$pkgdir" -not -regex '.*share/doc.*' \
    -not -name 'share' -not -name 'usr' -not -name 'racket-docs' -delete

  install -Dm644 COPYING-libscheme.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_racket-minimal() {
  pkgdesc="Minimal Racket installation, without DrRacket."
  depends=('libffi')
  conflicts=('racket')
  provides=('racket')

  # The -minimal tarball uses the SAME extract path.
  # 99% identical sources anyway, manually apply the difference.
  # Thankfully these builds happen in the given order
  # so we can save some time and reuse the previous build.
  cd "${pkgbase}-${pkgver}"
  echo '((root "pkgs/racket-lib"))' > share/links.rktd
  _libhash=$(grep -oP '\("racket-lib".*?#f\)\)' share/pkgs/pkgs.rktd)
  echo "#hash($_libhash)" > share/pkgs/pkgs.rktd
  cd share/pkgs
  find . -not -name '.' -not -name '*pkgs.rktd' -not -regex '.*/racket-lib.*' -delete
  cd ../../src

  make DESTDIR="${pkgdir}" install

  find "${pkgdir}/usr/share/doc/" -delete

  install -Dm644 COPYING-libscheme.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
