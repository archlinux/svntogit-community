# Maintainer: Leonidas Spyropoulos <artafinde@archlinux.org>
# Contributor: Sergey Kostyuchenko <derfenix@gmail.com>

pkgname=corectrl
pkgver=1.3.3
pkgrel=1
pkgdesc="Core control application"
url="https://gitlab.com/corectrl/corectrl"
license=('GPL3')
arch=('x86_64')
depends=(procps-ng hwdata quazip-qt5 qt5-charts qt5-quickcontrols2 qt5-base
         botan qt5-svg qt5-xmlpatterns qt5-base dbus polkit fmt pugixml hicolor-icon-theme)
makedepends=(cmake qt5-tools extra-cmake-modules ninja easyloggingpp)
checkdepends=(catch2 trompeloeil)
optdepends=(
	'vulkan-tools: For vulkaninfo'
	'mesa-utils: For glxinfo'
	'util-linux: For lscpu'
)
source=("https://gitlab.com/corectrl/corectrl/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.bz2")
sha256sums=('d3422c7e9cf1e49ae1a482069a903fc8ccbab17545259d333beb0ea631d6162d')
b2sums=('5faaf27890e9654485b9c8cc6e4e80ad41a707a407b4944cacd477a1e02f287835e79b234d6b95592e0dc3678472619a5f31cf345b51280b127d2e1f048ed937')

build() {
  cmake \
    -Bbuild \
    -GNinja \
    -S "${pkgname}-v${pkgver}" \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='lib' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -DBUILD_TESTING=ON \
    -Wno-dev
  cmake --build build --verbose
}

check () {
  CTEST_OUTPUT_ON_FAILURE=1 ninja -C build test
}

package() {
  depends+=(
    fmt libfmt.so
  )
  DESTDIR="${pkgdir}" cmake --install build
}

# vim:set ts=2 sw=2 et:
