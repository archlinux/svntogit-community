# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>

pkgbase=python-pysocks
pkgname=(python-pysocks python2-pysocks)
pkgver=1.6.5
pkgrel=2
pkgdesc="SOCKS4, SOCKS5 or HTTP proxy (Anorov fork PySocks replaces socksipy)"
arch=('any')
license=('BSD')
url="https://github.com/Anorov/PySocks"
makedepends=('python' 'python2' 'git')
checkdepends=('lib32-glibc' 'python2-twisted' 'python2-tornado')
source=("git+https://github.com/Anorov/PySocks.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a PySocks{,-py2}
}

build() {
  cd "$srcdir"/PySocks
  python setup.py build

  cd "$srcdir"/PySocks-py2
  python2 setup.py build
}

# Tests disabled as it requires external services to run
check_disabled() {
  cd "$srcdir"/PySocks-py2/test
  python2 socks4server.py &
  _SOCKS4SERVER=$!
  python2 httpproxy.py &
  _HTTPPROXY=$1

  ./mocks start

  sleep 1

  python2 sockstest.py

  cd "$srcdir"/PySocks/test
  python sockstest.py

  ./mocks shutdown

  kill $_SOCKS4SERVER $_HTTPPROXY
}

package_python-pysocks() {
  depends=('python')

  cd PySocks
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python2-pysocks() {
  depends=('python2')

  cd PySocks-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
