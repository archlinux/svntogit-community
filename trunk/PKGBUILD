# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=osinfo-db-tools
pkgver=1.9.0
pkgrel=1
pkgdesc='Tools for managing the osinfo database'
arch=('x86_64')
url='https://libosinfo.org/'
license=('GPL')
depends=('json-glib' 'libarchive' 'libsoup' 'libxslt')
makedepends=('meson')
checkdepends=('python-pytest' 'python-requests')
source=("https://releases.pagure.org/libosinfo/$pkgname-$pkgver.tar.xz"{,.asc})
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF' # Daniel P. Berrange
              '09B9C8FF223EF113AFA06A39EE926C2BDACC177B') # Fabiano Fidêncio <fidencio@redhat.com>
sha256sums=('255f1c878bacec70c3020ff5a9cb0f6bd861ca0009f24608df5ef6f62d5243c0'
            'SKIP')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
