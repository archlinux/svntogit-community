# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributor: Dan Beste <dan.ray.beste@gmail.com>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>
# Contributor: stef204 <https://aur.archlinux.org/account/stef204>

pkgname=borgmatic
pkgver=1.3.24
pkgrel=1
pkgdesc='Wrapper script for Borg backup software that creates and prunes backups'
arch=('any')
url=https://torsion.org/borgmatic
license=('GPL3')
depends=('borg' 'python-colorama' 'python-pykwalify' 'python-ruamel-yaml' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('ca49f15e0eab8650550ce031a9c71a547acaaff15abd3448a6a2e55ca09e83ca')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's,root/.local,usr,' sample/systemd/$pkgname.service
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system sample/systemd/*
}

# vim: ts=2 sw=2 et:
