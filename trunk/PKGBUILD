# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contriutor: Eric Belanger <eric@archlinux.org>
# Contributor: Jo Christian Bergskås <jcberg@gmail.com>

pkgname=fceux
pkgver=2.2.2
pkgrel=3
url='http://www.fceux.com/'
pkgdesc='Fast and ultra-compatible NES/Famicom emulator'
arch=('x86_64' 'i686')
license=('GPL')
depends=('sdl' 'gtk2' 'gd' 'desktop-file-utils' 'lua51' 'libgl' 'shared-mime-info')
makedepends=('scons' 'mesa-libgl' 'glu')
source=("https://downloads.sourceforge.net/project/fceultra/Source%20Code/$pkgver%20src/$pkgname-$pkgver.src.tar.gz")
sha256sums=('804d11bdb4a195f3a580ce5d2d01be877582763378637e16186a22459f5fe5e1')

build() {
  cd "$pkgname-$pkgver"

  scons
}

package() {
  cd "$pkgname-$pkgver"

  scons install -i --prefix="$pkgdir/usr"

  # scons install creates usr/share/pixmaps as a regular file, wtf?
  rm -f "$pkgdir/usr/share/pixmaps"

  install -d "$pkgdir/usr/share/doc/$pkgname"
  cp -r documentation/* "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm644 changelog.txt "$pkgdir/usr/share/doc/$pkgname/"

  # desktop shortcut
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
