# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=Agda
pkgname=agda
pkgver=2.6.1.2
pkgrel=22
pkgdesc="A dependently typed functional programming language and proof assistant"
url="https://wiki.portal.chalmers.se/agda/"
license=("custom")
arch=('x86_64')
depends=('ghc-libs' 'ghc' 'haskell-aeson' 'haskell-async' 'haskell-blaze-html' 'haskell-boxes'
         'haskell-cpphs' 'haskell-data-hash' 'haskell-edit-distance' 'haskell-equivalence'
         'haskell-geniplate-mirror' 'haskell-gitrev' 'haskell-hashable'
         'haskell-hashtables' 'haskell-ieee754' 'haskell-murmur-hash' 'haskell-regex-tdfa'
         'haskell-split' 'haskell-strict' 'haskell-unordered-containers' 'haskell-uri-encode'
         'haskell-zlib')
optdepends=('agda-stdlib: for standard library')
makedepends=('alex' 'happy' 'ghc' 'haskell-filemanip')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz"
        Agda-ghc-8.10.3.patch::https://github.com/agda/agda/commit/76278c23d447b49f59fac581ca4ac605792aabbc.patch)
sha512sums=('5ab35d9b581ed918481dd25619739dfa43ea3d86b9522989fe8c83b3aa8749bee81fb12e7cd6b4270c4890a3719386c62b220163324407a1201273d1dfe9d615'
            '6e9069daa725efb75394b10eb6b637b891c5cff52dc4ae169f6b49ee0601f82a0dca65326a0747affa25670722e447b3b9f4893da6e7788277951af0c9fb22ee')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../Agda-ghc-8.10.3.patch
    sed -i '/fno-warn-overlapping-patterns/a \        , "-dynamic"' src/full/Agda/Compiler/MAlonzo/Compiler.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fcpphs -f-debug -f-enable-cluster-counting
    LC_CTYPE=en_US.UTF-8 runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    LD_LIBRARY_PATH="$PWD/dist/build" runhaskell Setup copy --destdir="${pkgdir}"

    # Symlink to avoid writing to this place when agda-lib is present
    install -dm755 "$pkgdir"/usr/share/agda/lib/_build/$pkgver/agda
    ln -s ../../../prim "$pkgdir"/usr/share/agda/lib/_build/$pkgver/agda/

    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
