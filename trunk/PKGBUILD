# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Martchus <martchus@gmx.net>

pkgname=qt6ct
pkgver=0.6
pkgrel=3
pkgdesc='Qt 6 Configuration Utility'
arch=(x86_64)
url='https://github.com/trialuser02/qt6ct'
license=(BSD)
depends=(qt6-svg)
makedepends=(cmake qt6-tools)
source=(https://github.com/trialuser02/qt6ct/releases/download/$pkgver/qt6ct-$pkgver.tar.xz
        https://github.com/trialuser02/qt6ct/commit/6d5dab5f.patch)
sha256sums=('b44b22fc2301c36ae4d20adfdd6f624484ea753f6f061ce1d511a102a83a6cc3'
            '00264e3ff6bb11ed7d365a92106268049f195b57d86c2411887a9644342735a9')
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 6d5dab5f.patch # Fix crash at startup
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
