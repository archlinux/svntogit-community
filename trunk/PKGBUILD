# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=acpica
pkgver=20211217
pkgrel=1
pkgdesc="ACPI tools, including Intel ACPI Source Language compiler"
arch=(x86_64)
url="https://acpica.org"
license=(GPL2)
depends=(glibc)
makedepends=(flex bison)
provides=(iasl)
conflicts=(iasl)
replaces=(iasl)
options=(!makeflags)
source=(${pkgname}-unix-${pkgver}.tar.gz::https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz)
sha256sums=('2511f85828820d747fa3e2c3433d3a38c22db3d9c2fd900e1a84eb4173cb5992')

build() {
  cd acpica-unix-${pkgver}
  make
}

package() {
  cd acpica-unix-${pkgver}
  make DESTDIR="${pkgdir}" install
}
