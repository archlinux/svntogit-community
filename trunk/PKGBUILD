# $Id: PKGBUILD 160945 2012-06-07 12:04:14Z juergen $
# Maintainer: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=1.9.0
pkgrel=1
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu')
makedepends=('mesa')
source=(http://scummvm.org/frs/scummvm/${pkgver}/scummvm-${pkgver}.tar.xz)
md5sums=('3255706992edaf30380ce8a49cb305b7')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --disable-debug \
    --enable-release \
    --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 dists/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}
