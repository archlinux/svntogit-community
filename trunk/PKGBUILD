# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-chinese-addons
pkgver=5.0.9
pkgrel=1
pkgdesc="Addons related to Chinese, including IME previous bundled inside fcitx4"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-chinese-addons"
license=('GPL')
conflicts=('fcitx')
depends=('curl' 'fcitx5-qt' 'libime' 'opencc' 'qt5-webkit')
makedepends=('boost' 'extra-cmake-modules' 'fcitx5-lua' 'ninja')
optdepends=('fcitx5-lua: Lua and imeapi support from pinyin')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-${pkgver}_dict.tar.xz"{,.sig})
sha512sums=('abf7152cef089f34bd5147a9e69cdb73119428d6665e66a9cc75d475a55bac90ae1083574ca0ab2e587155118f211cf416e09b4908959502f6d7d8b43fbf13c4'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build() {
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$pkgver
  ninja test
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
