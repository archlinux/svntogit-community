# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=monero-gui
pkgver=0.15.0.5
pkgrel=1
pkgdesc="QT GUI wallet for Monero: the secure, private, untraceable peer-to-peer currency"
license=('BSD')
arch=('x86_64')
url="https://getmonero.org/"
depends=('boost-libs' 'libunwind' 'openssl' 'readline' 'zeromq' 'pcsclite' 'hidapi' 'unbound'
    'qt5-base' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-svg' 'qt5-xmlpatterns')
makedepends=('git' 'cmake' 'boost' 'qt5-tools')
source=(
    "git+https://github.com/monero-project/monero-gui#tag=v${pkgver}"
    "git+https://github.com/monero-project/monero"
    "https://github.com/monero-project/monero-gui/commit/5872bc8a2bdbf1f0b94374dc6f9c0f6f34aa65b9.patch"
)
sha512sums=('SKIP'
            'SKIP'
            '6322e7dd9142da44a5e1b2102856c84a505c16fa988be36664adad2ae0a1aef068cb4b363a28a5cc51e61666cd3bcfca047394a8603d54c229cccf1ae4131053')

prepare() {
  cd "${pkgname}"
  git submodule init
  git config submodule.monero.url "$srcdir/monero"
  git submodule update
  patch --forward --strip=1 --input="${srcdir}/5872bc8a2bdbf1f0b94374dc6f9c0f6f34aa65b9.patch"
}

build() {
  cd "${pkgname}"
  ARCH=default QT_SELECT=5 ./build.sh
}

package() {
  cd "${pkgname}"
  install -Dm755 build/release/bin/monero-wallet-gui -t "${pkgdir}/usr/bin"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
