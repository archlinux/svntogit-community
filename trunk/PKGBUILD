# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=42.8
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/DejaDup"
license=(GPL)
depends=(duplicity gvfs libhandy libsecret org.freedesktop.secrets python-gobject python-pydrive)
makedepends=(appstream-glib itstool meson vala)
source=(https://gitlab.gnome.org/World/$pkgname/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('5e7e7206db6aa57dbb9b948df041774e8ba8915685ceb6324a747776c9067467')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
