# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.9.4
pkgrel=1
pkgdesc='Automate Clojure projects'
arch=(any)
url='https://github.com/technomancy/leiningen'
license=(EPL)
depends=(java-environment java-runtime)
makedepends=(git)
optdepends=('leiningen-completions: completion scripts'
            'rlwrap: for readline support')
source=("git+$url#commit=ee8b70e7858d61fd2fc7e0562e38df32f1585f7a" # tag: 2.9.4
        "$url/releases/download/$pkgver/leiningen-$pkgver-standalone.zip"{,.asc})
noextract=("leiningen-$pkgver-standalone.zip")
validpgpkeys=('20242BACBBE95ADA22D0AFD7808A33D379C806C3') # Phil Hagelberg <phil@hagelb.org>
sha256sums=('SKIP'
            '0e3c339480347df0445317d329accbd4a578ebbd8d91e568e661feb1b388706c'
            'SKIP')

package() {
  install -Dm755 $pkgname/bin/lein-pkg "$pkgdir/usr/bin/lein"
  install -Dm644 $pkgname/doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 "$srcdir/leiningen-$pkgver-standalone.zip" \
                 "$pkgdir/usr/share/java/leiningen-$pkgver-standalone.jar"
}

# getver: github.com/technomancy/leiningen/releases
# vim: ts=2 sw=2 et:
