# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hlint
pkgver=3.3.1
pkgrel=35
pkgdesc="Source code suggestions"
url="http://community.haskell.org/~ndm/hlint/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-cmdargs' 'haskell-cpphs'
         'haskell-data-default' 'haskell-extra' 'haskell-file-embed' 'haskell-filepattern'
         'haskell-ghc' 'haskell-ghc-lib-parser-ex' 'haskell-hscolour' 'haskell-refact'
         'haskell-syb' 'haskell-uniplate' 'haskell-unordered-containers' 'haskell-utf8-string'
         'haskell-vector' 'haskell-yaml')
conflicts=('haskell-hlint')
replaces=('haskell-hlint')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        hlint-extra-1.7.10.patch::https://github.com/ndmitchell/hlint/commit/bbfcccad636ed61fa7ade1369ebd6ab6fb64df32.patch)
sha512sums=('46fabb26361cfd71ceda302669fc4f55d28dffc2a98732be48d7b6c244dd5f546bce1a8179a75487753e6113ad062e7840a4e5feee8a432936fdd65a20283a13'
            '5a5c23f532f3c0480972fd88e6063c0bc0ac4d14392c1118a7b14481389563d3ac8b932a5a55abae638e95bd3ecc6e85719724a94ea11883c0edee5f4ed7e6a1')

prepare() {
    patch -d $pkgname-$pkgver -p1 < hlint-extra-1.7.10.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -fgpl -fthreaded
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.BSD3"
}
