# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Dct Mei <dctxmei@yandex.com>
# Contributor: Eric S. Londres <elondres@stevens.edu>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Cillian Berragan <cjberragan@gmail.com>
# Contributor: Felix Golatofski <contact@xdfr.de>

pkgname=nyxt
pkgver=3.0.0
pkgrel=1
pkgdesc='A keyboard-driven web browser designed for power users'
arch=('x86_64')
url='https://nyxt.atlas.engineer'
license=('BSD')
depends=(
  'glibc'
  'hicolor-icon-theme'
  'enchant'
  'glib-networking'
  'gobject-introspection-runtime'
  'gsettings-desktop-schemas'
  'libfixposix'
  'webkit2gtk-4.1'
  'zstd'
)
# If someday Nyxt works with other Lisps, replace 'sbcl' with 'common-lisp'.
# NOTE: sbcl provides both common-lisp and cl-asdf.
makedepends=(
  'git'
  'sbcl'
  'cl-asdf'
)
optdepends=(
  'gstreamer: for HTML5 audio/video'
  'gst-plugins-base: for HTML5 audio/video'
  'gst-plugins-good: for HTML5 audio/video'
  'gst-plugins-bad: for HTML5 audio/video'
  'gst-plugins-ugly: for HTML5 audio/video'
)
options=('!strip' '!makeflags')
source=("$pkgname-$pkgver.tar.xz::https://github.com/atlas-engineer/nyxt/releases/download/$pkgver/nyxt-$pkgver-source-with-submodules.tar.xz")
sha512sums=('1411f9b18eb65de8efdf46e850d4d78c67c2b5ee3ffbf7e9b1c67f6fda2ed7ad154414dc9d842a1fd4cf78a3619d9da560327e658bb0735a117be50aca87f4e8')
b2sums=('04fcaffa852c3035142a6b48c853083e9b9516d3e7682988806a19b2dab54d0ac288a8e4464da779f8facefca2eec63f31be7f0906567540c5e3754a91642f15')

build() {
  make all
}

package() {
  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" licenses/*
}
