# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.52.0
pkgrel=1
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=(any)
url="https://github.com/pennersr/django-allauth"
license=(MIT)
depends=(python-django python-openid python-requests python-requests-oauthlib
python-pyjwt)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
source=($_name-$pkgver.tar.gz::https://github.com/pennersr/$_name/archive/$pkgver.tar.gz)
sha512sums=('252eaeb9922d6ba188f109513067529a9167927d675c71c5b2ddbcf2fcb0de35e1f8a84df5219d721019e1d9f5631f410700ca7c8b3993569227f002eab443b1')
b2sums=('624842186fd6cc25cc947a06191e07f42711ef772814f26e8b10bab28f3e66bef61118bda62b37804a1042dd8e2d902a88c4281d248858e1323c0594e14d19f1')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  python manage.py test allauth
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
