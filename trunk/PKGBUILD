# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=xboard
pkgver=4.6.2
pkgrel=3
pkgdesc="Graphical user interfaces for chess"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/xboard/"
license=('GPL3')
depends=('libxaw' 'gnuchess' 'texinfo' 'xdg-utils'
	 'xorg-fonts-75dpi' 'xorg-fonts-100dpi')
makedepends=('gendesk')
optdepends=('perl')
install=${pkgname}.install
source=("http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('6e6d119fac60d68139d7836426212efb')

build() {
  cd "${srcdir}"
  gendesk

  cd "${pkgname}-${pkgver}"
  sed -i 's/fairymax/gnuchess/g' xboard.h xboard.conf.in
  ./configure --prefix=/usr --mandir=/usr/share/man --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -Dm644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${srcdir}/${pkgname}.png \
    ${pkgdir}/usr/share/pixmaps/${pkgname}.png
  install -Dm755 ${srcdir}/${pkgname}-${pkgver}/cmail \
    ${pkgdir}/usr/bin/cmail
}
