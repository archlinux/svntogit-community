# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=python-adblock
pkgver=0.5.0
pkgrel=1
pkgdesc="Brave's adblock library in Python"
arch=("x86_64")
url="https://github.com/ArniDagur/python-adblock"
license=("MIT")
depends=()
makedepends=("python-setuptools" "python-dephell" "rust" "maturin")
checkdepends=("python-pytest")
source=("$pkgname-$pkgver.tar.gz::https://github.com/ArniDagur/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('35ab20170bcfe447efee4d988508d99477cc23b95a901383bf606ca2960148a3')

export PYTHONHASHSEED=0

build() {
    cd $pkgname-$pkgver
    maturin build --release --strip
    cargo build --release --locked --all-features --target-dir=target
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgname-$pkgver"
  mkdir -vp testdir
  export PIP_CONFIG_FILE=/dev/null
  pip install --isolated \
              --root="testdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  export PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
  pytest -v -k 'not test_version_numbers_all_same'
}

package() {
    cd $pkgname-$pkgver
    install -Dm755 target/release/libadblock.so -t "$pkgdir/usr/lib"
    export PIP_CONFIG_FILE='/dev/null'
    pip install --isolated \
              --root="$pkgdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
    install -Dm0644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
}

