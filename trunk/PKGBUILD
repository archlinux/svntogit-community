# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=guake
pkgver=0.7.2
pkgrel=1
pkgdesc='Drop-down terminal for GNOME'
arch=('any')
url="http://guake-project.org/"
license=('GPL')
depends=('python2-notify' 'vte' 'python2-gconf' 'python2-dbus' 'python2-keybinder2' 'python2-xdg' 'libutempter')
makedepends=('intltool' 'gnome-common')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/Guake/$pkgname/archive/$pkgver.tar.gz
        default-value.patch
        keybinder.patch
        unbind.patch)
md5sums=('3d1430cd6e57488d8f9acd0c79d25c4b'
         '59862f5c81b14f24a99f39d11c28f3ce'
         'f06c8c50f11bf5e3ea47548ed11384cc'
         '6a370e5ab6c1542cac75820710359581')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Set default value
  patch -Np1 -i ../default-value.patch

  # Replace GlobalHotkeys with the keybinder module
  patch -Np1 -i ../keybinder.patch
  patch -Np1 -i ../unbind.patch

  # Python2 fix
  sed -i 's|/usr/bin/env python|/usr/bin/python2|' src/guake/prefs.py
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
               --disable-static --disable-schemas-install \
               --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
               PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
