# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

_pkgbasename=libxinerama
pkgname=lib32-$_pkgbasename
pkgver=1.1.3
pkgrel=2
pkgdesc="X11 Xinerama extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxext' $_pkgbasename)
makedepends=('xorg-util-macros' gcc-multilib)
source=(${url}/releases/individual/lib/libXinerama-${pkgver}.tar.bz2)
sha256sums=('7a45699f1773095a3f821e491cbd5e10c887c5a5fce5d8d3fced15c2ff7698e2')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXinerama-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXinerama-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
