# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=ruff
pkgver=0.0.184
pkgrel=1
pkgdesc="An extremely fast Python linter, written in Rust"
arch=(x86_64)
url="https://github.com/charliermarsh/ruff"
license=(MIT)
depends=(gcc-libs glibc python)
makedepends=(cargo maturin python-installer)
options=(!lto debug)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('724765ead26a81f6fc8a7beb5b71a266eeb233a7df3192cca71ad4d7fc6024854a89d57f9bc07a792009236dc5a50d6071a478438dad00e75a44b7e5c2e5e39d')
b2sums=('34a82d8abdfba5452f970a98747b88c1b486c11faadecf0cf4cf64cc9b749c88a54ba56e3c93f9c77d13ca627149d52adb25de299273c2f7ebc70ad7d4890599')

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  local maturin_options=(
    --release
    --strip
    --locked
    --all-features
    --target $CARCH-unknown-linux-gnu
  )

  cd $pkgname-$pkgver

  maturin build "${maturin_options[@]}"
}

check() {
  cd $pkgname-$pkgver

  cargo test --frozen --all-features
}

package() {
  cd $pkgname-$pkgver

  python -m installer --destdir="$pkgdir" target/wheels/*.whl
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
