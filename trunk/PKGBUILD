# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=nemo-qt-components
pkgver=2.0.15
pkgrel=2
pkgdesc="Qt Quick Components"
arch=('i686' 'x86_64')
url="https://github.com/nemomobile/qt-components"
license=('BSD' 'FDL')
depends=('qt5-declarative')
makedepends=('git' 'qt5-quick1')
source=("git+https://github.com/nemomobile/qt-components.git#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd qt-components

  ./configure -meego -nomake tests
  make
}

package() {
  cd qt-components

  make INSTALL_ROOT="${pkgdir}" install

  install -Dm644 header.BSD "$pkgdir/usr/share/licenses/$pkgname/header.BSD"
}
