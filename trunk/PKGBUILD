# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: MThinkCpp <mtc.maintainer[at]outlook.com>
pkgname='libc++'
pkgver=3.3
pkgrel=1
pkgdesc='A new implementation of the C++ standard library, targeting C++11.'
url='http://libcxx.llvm.org'
license=('custom:University of Illinois/NCSA Open Source License')
arch=('i686' 'x86_64')
depends=('glibc' 'gcc-libs') # gcc-libs for libsupc++ binary
makedepends=('clang' 'subversion' 'cmake' 'gcc') # gcc provides libsupc++ headers
source=("http://www.llvm.org/releases/${pkgver}/libcxx-${pkgver}.src.tar.gz")
md5sums=('SKIP')

build() {
  mkdir -p libcxx-${pkgver}.src/build
  cd libcxx-${pkgver}.src/build
  CC=clang CXX=clang++ cmake -G "Unix Makefiles" -DLIBCXX_CXX_ABI=libcxxabi \
    -DLIBCXX_LIBCXXABI_INCLUDE_PATHS=/usr/include \
    -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr "$srcdir/libcxx-${pkgver}.src"
  make
}
package() {
  install -Dm644 libcxx-${pkgver}.src/LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/license.txt"
  cd libcxx-${pkgver}.src/build
  make DESTDIR="$pkgdir" install
}
