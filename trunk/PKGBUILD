# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=dragonfly-reverb
_name=DragonflyReverb
pkgver=3.2.6
pkgrel=1
pkgdesc="A concert hall reverb and a room reverb"
arch=(x86_64)
url="https://michaelwillis.github.io/dragonfly-reverb/"
license=(GPL3)
groups=(lv2-plugins pro-audio vst-plugins)
depends=(gcc-libs glibc libglvnd libx11)
makedepends=(freeverb3 gendesk jack libsamplerate lv2)
checkdepends=(lv2lint)
optdepends=(
  'jack: for standalone applications'
  'lv2-host: for LV2 plugins'
  'vst-host: for VST plugins'
)
options=(debug)
source=(https://github.com/michaelwillis/$pkgname/releases/download/$pkgver/$pkgname-source-v$pkgver.tar.gz)
sha512sums=('0c1b9023e0ecfff07119fd4b48035622570f5c7a85ce6f10bc771a3df3b75bd13a36ef570bfa1f3ba912ef0608a7a71ec606621098cc56919066a0633afcd06b')
b2sums=('e9d39f00f21866f22cf0b3a93121755b3eecf50c7c2da2fab71c26523ee5a155ad788a48a03f205130d297803f7223867fcfaf99119f960a28e4be8bf6848a39')
_names=(DragonflyEarlyReflections DragonflyHallReverb DragonflyPlateReverb DragonflyRoomReverb)

prepare() {
  declare -A _description=(
    ["DragonflyEarlyReflections"]="A free stereo-to-stereo early reflections reverb effect"
    ["DragonflyHallReverb"]="A free stereo-to-stereo concert hall reverb effect"
    ["DragonflyPlateReverb"]="A free stereo-to-stereo plate reverb effect"
    ["DragonflyRoomReverb"]="A free stereo-to-stereo room reverb effect"
  )
  declare -A _generic=(
    ["DragonflyEarlyReflections"]="Early Reflections Reverb"
    ["DragonflyHallReverb"]="Concert Hall Reverb"
    ["DragonflyPlateReverb"]="Plate Reverb"
    ["DragonflyRoomReverb"]="Room Reverb"
  )

  cd $_name-Source-v$pkgver

  # explicitly remove vendored freeverb as it causes issues: https://github.com/michaelwillis/dragonfly-reverb/issues/129
  rm -frv common/freeverb/

  for name in "${_names[@]}"; do
    gendesk -n \
            --pkgname io.github.michaelwillis.$name \
            --name $name \
            --exec $name \
            --pkgdesc "${comments[$name]}" \
            --genericname "${generic[$name]}" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  export SYSTEM_FREEVERB3="true"
  make -C $_name-Source-v$pkgver
}

check() {
  local _plugin_name=""
  declare -A _urls=(
    ["DragonflyHallReverb"]='https://github.com/michaelwillis/dragonfly-reverb'
    ["DragonflyEarlyReflections"]='urn:dragonfly:early'
    ["DragonflyPlateReverb"]='urn:dragonfly:plate'
    ["DragonflyRoomReverb"]='urn:dragonfly:room'
  )

  for _plugin_name in "${_names[@]}"; do
    lv2lint -Mpack -s "lv2_generate_ttl" -I $_name-Source-v$pkgver/bin/$_plugin_name.lv2/ "${_urls[$_plugin_name]}"
  done
}

package() {
  depends+=(libfreeverb3.so)

  cd $_name-Source-v$pkgver
  for name in "${_names[@]}"; do
    install -vDm 755 bin/$name -t "$pkgdir/usr/bin"
    install -vDm 755 bin/$name-vst.so -t "$pkgdir/usr/lib/vst"
    install -vDm 755 bin/$name.lv2/*.so -t "$pkgdir/usr/lib/lv2/$name.lv2/"
    install -vDm 644 bin/$name.lv2/*.ttl -t "$pkgdir/usr/lib/lv2/$name.lv2/"
  done
  install -vDm 644 *.desktop -t "$pkgdir/usr/share/applications/"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
