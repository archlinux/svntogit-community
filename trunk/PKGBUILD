# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>
# Contributor: William Turner <willtur.will@gmail.com>

# Upstream (still/again) has circular dependencies in the test suite
# https://github.com/typemytype/booleanOperations/issues/64
BUILDENV+=(!check)

_pyname=booleanOperations
pkgname=python-${_pyname,,}
pkgver=0.9.0
pkgrel=7
pkgdesc='Boolean operations on paths'
arch=(any)
url="https://github.com/typemytype/$_pyname"
license=(MIT)
_pydeps=(pyclipper
         fonttools)
depends=(python
         "${_pydeps[@]/#/python-}")
checkdepends=(python-defcon
	      python-fontpens
	      python-pytest)
makedepends=(python-{build,installer,wheel}
             python-setuptools-scm)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.zip")
sha256sums=('8cfa821c32ad374fa120d6b2e0b444ebeac57c91e6631528645fa19ac2a281b8')

build() {
	cd "$_archive"
	python -m build -wn
}

check() {
    cd "$_archive"
    PYTHONPATH=Lib pytest tests
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
