# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Firmicus <firmicus āt gmx dōt net>
# Contributor: flying sheep <flying-sheep@web.de>

pkgname=python-fonttools
pkgver=4.24.4
pkgrel=1
pkgdesc="A library to manipulate font files from Python"
url="https://github.com/fonttools/fonttools"
license=("MIT")
arch=('x86_64')
depends=("python")
makedepends=("cython" "python-setuptools")
optdepends=("python-lxml: faster backend for XML files reading/writing"
	    "python-fs: to read/write UFO source files, "
	    "python-brotli: to compress/decompress WOFF 2.0 web fonts"
	    "python-zopfli: faster backend fom WOFF 1.0 web fonts compression"
	    "python-scipy: for finding wrong contour/component order between different masters"
	    "python-matplotlib: for visualizing DesignSpaceDocument and resulting VariationModel"
	    "python-unicodedata2: for displaying the Unicode character names when dumping the cmap table with ttx"
	    "python-sympy: for symbolic font statistics analysis"
	    "python-pyqt5: for drawing glyphs with Qt's QPainterPath"
	    "python-reportlab: to drawing glyphs as PNG images"
            "python-lz4: for graphite type tables in ttLib/tables")
source=("https://github.com/fonttools/fonttools/archive/${pkgver}/fonttools-${pkgver}.zip")
sha512sums=('419227943c200ce372d27d70a998ff8ec6c2ee075df4ae4407253b35f1c3956dabdd8373235accde69f5e654a4ede30ed9406c8eac05469a5dc84a7a252f5e08')

build() {
  cd "$srcdir"/fonttools-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package() {
  cd "$srcdir"/fonttools-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D -m755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
