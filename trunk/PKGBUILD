# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributer: Arthur Zamarin <arthurzam@gmail.com>
# Contributer: Martin Wimpress <code@flexion.org>

pkgname=blueman
pkgver=2.0
pkgrel=5
pkgdesc="GTK+ Bluetooth Manager"
arch=('i686' 'x86_64')
url="https://github.com/blueman-project/blueman"
license=('GPL')
depends=('bluez' 'bluez-libs' 'gtk3' 'libnotify' 'python-cairo' 'python-dbus' 'python-gobject')
makedepends=('cython' 'intltool')
optdepends=('dnsmasq: Network Access Point (NAP) support'
            'networkmanager: Dial Up Networking (DUN) and Personal Area Networking (PAN) support'
            'pulseaudio-bluetooth: audio devices support')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/blueman-project/$pkgname/archive/$pkgver.tar.gz
        fix-infinite-loop.patch)
sha256sums=('3f6dc827c4410f128f75a15672af8dc76c5c041f6d639d5ab19b15abeb42ff74'
            '25eb611bd7817e82240565caf2ac14a5841cd56e09be702a77ca3d7737cd5ac5')

prepare() {
  cd $pkgname-$pkgver

  # Fix infinite loop if RecentConns has only invalid items
  # https://github.com/blueman-project/blueman/commit/9619d3b5
  patch -Np1 -i ../fix-infinite-loop.patch

  # Fix path for bluetoothd
  sed -i 's|/usr/sbin/bluetoothd|/usr/lib/bluetooth/bluetoothd|' apps/blueman-report

  # Fix non-standard icon names
  sed -i 's/gtk-find/edit-find/' data/ui/device-list-widget.ui
  sed -i 's/mouse/input-mouse/' blueman/services/Input.py

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/$pkgname \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
