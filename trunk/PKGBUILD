# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-schema
pkgver=6.2.0
pkgrel=1
pkgdesc="zope.interface extension for defining data schemas"
arch=('any')
url="https://github.com/zopefoundation/zope.schema"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')
checkdepends=('python-zope-testrunner' 'python-zope-testing' 'python-zope-i18nmessageid')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.schema/archive/$pkgver.tar.gz")
sha512sums=('0de6705aec12d92f0a51fbe59afe5bf7fd16497bdaa759c5f171f0348fc32902bbdfbe7dea21de44182a83bc40dad56077d75843af0b5324ed11afd28e146c76')

build() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.schema-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
