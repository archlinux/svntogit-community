# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Wagner <Andreas dot Wagner at em dot uni-frankfurt dot de>

pkgname=schroot
pkgver=1.6.10
pkgrel=30
pkgdesc="Allows users to execute shell commands under different root filesystems. (Successor to dchroot)."
url="https://packages.qa.debian.org/s/schroot.html"
license=('GPL3')
depends=('pam' 'lockdev' 'boost-libs' 'e2fsprogs')
makedepends=('boost' 'cppunit')
optdepends=('btrfs-progs-unstable' 'lvm2')
arch=('x86_64')
conflicts=('dchroot')
replaces=('dchroot')
provides=('schroot' 'sbuild' 'dchroot')
backup=('etc/schroot/schroot.conf'
	'etc/schroot/arch32/config'
	'etc/schroot/arch32/copyfiles'
	'etc/schroot/arch32/mount'
	'etc/schroot/arch32/nssdatabases')
source=("http://http.debian.net/debian/pool/main/s/$pkgname/${pkgname}_${pkgver}.orig.tar.xz"
	"http://http.debian.net/debian/pool/main/s/schroot/schroot_$pkgver-3+deb9u1.debian.tar.xz"
	"https://src.fedoraproject.org/rpms/schroot/raw/rawhide/f/schroot-gcc8-assert-fix.patch"
	'arch32-example'
	'arch32-config'
	'arch32-copyfiles'
	'arch32-mount'
	'arch32-nssdatabases'
	'pam.d.schroot.patch'
	'schroot-cppunit.patch')

prepare() {
	cd ${pkgname}-${pkgver}
	cat "$srcdir"/debian/patches/series | while read p; do
		patch -p1 -i "$srcdir"/debian/patches/$p
	done
        # fix configure with cppunit 1.4
	patch -p1 -i ../schroot-cppunit.patch
	# https://bugs.archlinux.org/task/73481
	patch -p1 -i ../schroot-gcc8-assert-fix.patch
	./bootstrap
}

build() {
	cd ${pkgname}-${pkgver}
	export CFLAGS="$CFLAGS -ffat-lto-objects"
	export CXXFLAGS="$CXXFLAGS -ffat-lto-objects"
	export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-dchroot \
		--enable-lvm-snapshot \
		--enable-btrfs-snapshot \
		--with-bash-completion-dir=/usr/share/bash-completion/completions/ \
		BTRFS=/sbin/btrfs \
		BTRFSCTL=/sbin/btrfsctl \
		LVCREATE=/sbin/lvcreate \
		LVREMOVE=/sbin/lvremove
		# --enable-csbuild
	make
}

check() {
	cd ${pkgname}-${pkgver}
#	make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="$pkgdir" install
	install -dm 755 "$pkgdir"/etc/schroot/arch32
	install -m 644 "$srcdir"/arch32-{config,mount,copyfiles,nssdatabases} "$pkgdir"/etc/schroot/arch32
	rename 'arch32-' '' "$pkgdir"/etc/schroot/arch32/*
	install -m 644 "$srcdir"/arch32-example "$pkgdir"/etc/schroot/chroot.d/arch32.conf.example
	patch -i "$srcdir"/pam.d.schroot.patch "$pkgdir"/etc/pam.d/schroot
}
sha256sums=('3ce8dfd9cb97b099e4b6d4ccec421d6cc8c9ef84574681e928a12badb5643d0b'
            '56bc82fc8ae7f6ca7eef506ccc1dca1211b2c84d83efc50d24670b8bdb9ea8bb'
            '7a15406681e5b06431033ad3fe3212a28c7af7dd9ea3a3e945f4bb95d800bd86'
            '5ae07ef1b4525e9a6d9793d9d50cbf6d3f577f0a4001a2cde668d6817b03a6d5'
            '11730e71d6974a2172a86f2219ff0b394524567f4d75f11b7424dced2b40ed9b'
            'e67bb203b623ae9ecd4fe034546759c60780393d85bf87a907a94f114e425942'
            '8e6ea4cc6befc95a7a37edc590619a2ef0a060ad38c4d0a1830bb00c43becdb0'
            '2d564a13fbf330465aa00e442dcf6845263253be96f4776075be738b0018ef38'
            'c7a46fa1c901b41c1d1928771bf4036e1fdb779f6a8241d2acf9fc0d72cd7572'
            '604a0b312f33a07e309d7857faf4edcc2985827da08a927c1d267dbb0946a240')
