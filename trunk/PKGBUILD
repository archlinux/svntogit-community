# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=eslint
pkgver=8.30.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='https://eslint.org'
license=('MIT')
depends=('acorn' 'nodejs')
makedepends=('npm' 'rsync')
options=('!emptydirs')
source=("https://github.com/$pkgname/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('a6f170d51fe6b0d12c22a41ead2669d7dee818bb75880538904264e3ebb0f8786c8a91c516dca4cbfa15ce1c65c481775eaf568876b3e02f008c97193ed9023f')

prepare() {
  cd $pkgname-$pkgver
  npm install
}

check() {
  cd $pkgname-$pkgver
  node Makefile mocha
}

package() {
  cd $pkgname-$pkgver

  npm prune --production

  _npmdir=/usr/lib/node_modules/$pkgname
  install -d "$pkgdir"/usr/bin
  ln -s $_npmdir/bin/$pkgname.js "$pkgdir"/usr/bin/$pkgname
  install -Dt "$pkgdir"/$_npmdir/bin bin/$pkgname.js
  rsync -r --exclude .cache --exclude .package-lock.json conf lib messages \
    node_modules package.json "$pkgdir"/$_npmdir
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE

  # Experimental dedup
  rm -r "$pkgdir"/$_npmdir/node_modules/acorn
}
