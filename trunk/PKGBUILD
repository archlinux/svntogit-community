# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-gdstk
pkgver=0.9.6
pkgrel=1
pkgdesc="Python module for creating GDSII stream files, usually CAD layouts"
url="https://github.com/heitzmann/gdstk"
license=('custom:BSL')
arch=('x86_64')
depends=('python-numpy')
makedepends=('python-setuptools' 'cmake')
checkdepends=('python-pytest-runner')
source=("https://github.com/heitzmann/gdstk/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c752cb51e79a270749339c33bb16675bd3f716cded5793650fb10877328d320cdfa3caeb8ad8be643ea422c241cf09aba7aa3fba8103b8cd239b7c4e29ce8b8a')

build() {
  cd gdstk-$pkgver
  python setup.py build
}

check() {
  cd gdstk-$pkgver
  python setup.py pytest
}

package() {
  cd gdstk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
