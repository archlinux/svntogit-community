# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jim Bridgewater <jwbwater at gmail dot com>
# Contributor: delta48 <dark.magician.48[at]gmail[dot]com> 

pkgbase=python-pyperclip
pkgname=('python-pyperclip' 'python2-pyperclip')
_pypiname=pyperclip
pkgver=1.5.11
pkgrel=2
pkgdesc="A cross-platform clipboard module for Python"
arch=('any')
url="http://coffeeghost.net/2010/10/09/pyperclip-a-cross-platform-clipboard-module-for-python/"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'xclip')
checkdepends=('xorg-server-xvfb')
source=("https://pypi.python.org/packages/source/p/$_pypiname/$_pypiname-$pkgver.zip"
        LICENSE.txt)
md5sums=('800b0e2c8aefee2e026da0acf3379a2d'
         'b6cd06fd72984ac1f8428337aec8cff7')

prepare() {
  cp -a $_pypiname-$pkgver{,-py2}
}

check() {
  cd "$srcdir/$_pypiname-$pkgver"
  xvfb-run python tests/basicTests.py

  cd "$srcdir/$_pypiname-$pkgver-py2"
  xvfb-run python2 tests/basicTests.py
}

package_python-pyperclip() {
  depends=('python' 'xclip')

  cd "$srcdir/$_pypiname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-pyperclip() {
  depends=('python2' 'xclip')

  cd "$srcdir/$_pypiname-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
