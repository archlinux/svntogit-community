# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gnome-mplayer
pkgver=1.0.9
pkgrel=4
pkgdesc='A simple MPlayer GUI.'
arch=('x86_64')
url='https://sites.google.com/site/kdekorte2/gnomemplayer'
license=('GPL')
depends=('mplayer' 'dbus-glib' 'libnotify' 'gmtk')
makedepends=('nautilus' 'nemo' 'caja' 'gconf')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kdekorte/${pkgname}/archive/v${pkgver}.tar.gz"
        "caja.patch")
md5sums=('b8cd76d54fef7e2a18484b8721fbcb94'
         '60e40a9a3de78f82a99582bc70aa9f91')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np0 -i "${srcdir}/caja.patch"
  autoreconf -fi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed -i 's@Exec=gnome-mplayer %U@Exec=gnome-mplayer %F@' \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
