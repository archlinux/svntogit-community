# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kubeconform
pkgver=0.6.1
pkgrel=1
pkgdesc="FAST Kubernetes manifests validator, with support for Custom Resources!"
url="https://github.com/yannh/kubeconform"
arch=("x86_64")
license=("Apache")
depends=('glibc')
makedepends=("go" "git")
source=("${pkgname}-${pkgver}::https://github.com/yannh/kubeconform/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('4f78dfe258caf7b87c710fc3be9d2b0239527fd1ff5fa5e66ed34ae5017a323024e1116abb795a0862046df3b3d7d12f69e01d44fdbbccec67bd1ef25b7f5c15')
b2sums=('22cac29ce08db95b21fb3ed3f624f92a35aceb534054f770f9fe07ad93957c1e1111f987624109847ebd71512225a0431d2087b0d22e4fd44ae37579dc43cbc2')
options=('!lto')

build() {
  cd "${pkgname}-${pkgver}/cmd/kubeconform"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags="-w -s -linkmode=external" .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "cmd/kubeconform/kubeconform" "${pkgdir}/usr/bin/${pkgname}"
}
