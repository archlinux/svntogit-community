# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

pkgbase=python-networkx
pkgname=(python-networkx python2-networkx)
_pythonname=networkx
pkgver=1.8.1
pkgrel=3
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="http://networkx.github.io"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://github.com/networkx/networkx/archive/${_pythonname}-${pkgver}.tar.gz"
        python-3.4.patch)
md5sums=('f7961b12b5db92109befa1d3de1b1e0e'
         'fb33088811cba7e6d959d8770990f018')

prepare() {
  cd ${_pythonname}-${_pythonname}-$pkgver
  patch -p1 -i ../python-3.4.patch

  cd ..
  cp -r ${_pythonname}-${_pythonname}-$pkgver{,-py2}
}

build() {
  cd "$srcdir/${_pythonname}-${_pythonname}-$pkgver"
  python3 setup_egg.py build

  cd "$srcdir/${_pythonname}-${_pythonname}-$pkgver-py2"
  python2 setup_egg.py build
}

check() {
  cd "$srcdir/${_pythonname}-${_pythonname}-$pkgver"
  python3 setup_egg.py nosetests || true  # Tests fail randomly

  cd "$srcdir/${_pythonname}-${_pythonname}-$pkgver-py2"
  python2 setup_egg.py nosetests
}

package_python-networkx() {
  depends=('python')
  optdepends=('python-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
              'python-scipy: Provides flexible drawing of graphs.'
              'python-pyparsing: Required for pydot, GML file reading.'
              'python-yaml: Required for YAML format reading and writing.')

  cd "$srcdir/${_pythonname}-${_pythonname}-$pkgver"
  python3 setup_egg.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  mv "$pkgdir/usr/share/doc/${_pythonname}-$pkgver" "$pkgdir/usr/share/doc/$pkgname"
}

package_python2-networkx() {
  depends=('python2')
  optdepends=('python2-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
              'python2-scipy: Provides flexible drawing of graphs.'
              'python2-pygraphviz: Provides graph drawing and graph layout algorithms.'
              'python2-pydot: Provides graph drawing and graph layout algorithms.'
              'python2-pyparsing: Required for pydot, GML file reading.'
              'python2-yaml: Required for YAML format reading and writing.')

  cd "$srcdir/${_pythonname}-${_pythonname}-$pkgver-py2"
  python2 setup_egg.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  mv "$pkgdir/usr/share/doc/${_pythonname}-$pkgver" "$pkgdir/usr/share/doc/$pkgname"
}
