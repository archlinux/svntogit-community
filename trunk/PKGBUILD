# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=e3
pkgver=2.81
pkgrel=1
pkgdesc='Tiny editor without dependencies, written in assembly'
arch=('x86_64' 'i686')
url='http://sites.google.com/site/e3editor/'
license=('GPL2')
makedepends=('nasm')
options=('!strip')
source=("https://sites.google.com/site/e3editor/Home/$pkgname-$pkgver.tar.gz")
sha256sums=('2f22d33a25b7fe45ee7acc399f62c2a54ebd8e6a8aad87dd68a745a021568139')

build() {
  cd "$pkgname-$pkgver"

  sed -i 's|$(BINDIR)/e3 |e3 |' Makefile
  make clean
  if [[ $CARCH = i686 ]]; then
    make EXMODE=_ 32
  else
    make EXMODE=_ 64
  fi
}

package() {
  make -C "$pkgname-$pkgver" PREFIX="$pkgdir/usr" MANDIR="$pkgdir/usr/share/man/man1" install
}

# vim:set ts=2 sw=2 et:
