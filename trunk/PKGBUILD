# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: nfnty
pkgname='kea'
pkgver=1.2.0
pkgrel=3
pkgdesc='High-performance, extensible DHCP server engine from ISC, supporting both DHCPv4 and DHCPv6'
arch=('i686' 'x86_64')
url='http://kea.isc.org'
license=('custom:MPL2.0')
depends=('botan' 'boost-libs' 'log4cplus' 'libmariadbclient' 'postgresql-libs')
optdepends=('mariadb: Lease information database'
            'postgresql: Lease information database'
	    'python: To use kea-shell')
makedepends=('boost'
             'postgresql' # Needed for some headers
             'python' # kea-shell
	     'libxslt' 'docbook-xsl' 'elinks') # Doc
# Checks are disabled for now
#checkdepends=('gtest' 'postgresql' 'mariadb' 'python' 'procps-ng') # procps-ng needed for 'pgrep'
backup=('etc/kea/kea.conf' 'etc/kea/kea-ca.conf' 'etc/kea/keactrl.conf')
source=(https://ftp.isc.org/isc/kea/${pkgver}/kea-${pkgver}.tar.gz{,.asc}
        fix-scripts-include-path.patch
        add-missing-header.patch
        LICENSE
        tmpfiles-kea.conf
        kea-dhcp4.service
        kea-dhcp6.service)
sha512sums=('d90571027edbce9eeb75d9ac889865dc24c2fd3ab17e44e23337ff623ca098ffd02560f01273da3c28f13734d95d5950110bc116bed527a295a9dc70addcc240'
            'SKIP'
            '81874fd3a12f55ea23593b7248c7653450c3a6e5ba8e79c7c5fb40ab685b0dc5d4e29e50f10000bd9018200731dba35f15e5d86c8005c94d14865d73e19983e5'
            '79e2ad71d1742450f7b488326fdec8a62141622c8c927b1ed9db168052856ce848f80af70415cf03ce666441fab59eefbf3359ed47c6f326a19ace7db4b5328e'
            '32a402845f82f89a8357208be666b58672d70cf30fc264f3093b89b4201bf73b57877a080ed123c9beb8e50cd5396092a6286c33e511b595efb1b43e196e3be1'
            '13a473bea4f8a1ba40f5667b880010590c5fddecaa76c360113e977c9ab3898558141cda17000def7b1c592b7dc6f8d5a508752351c601f6484abd775b60d214'
            '24fdd95fc25bcd6aaf21cec598f3d09715418705aadbb2403792b62f4d8789b1897aa65f5e68184612d0a2c1c6be4123491db2fc3b1d602f6e7c5cd6c2d278eb'
            'f4d54483034ac5d2815a5a06cec84a20aa2849484630d7eeed4d9f2bd7cb96814f80d1f06da2f10b43eabdba0f79f4fb7befe06171001b7d1617844fe8813dae')
validpgpkeys=('BE0E9748B718253A28BB89FFF1B11BF05CF02E57') # Internet Systems Consortium, Inc. (Signing key, 2017-2018) <codesign@isc.org>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # The include path is set incorrectly (it uses undefined $prefix instead of /usr).
  # Also, it fallbacks to the absolute path inside the chrooted build dir,
  # which will certainly not exist on the target system...
  patch -p1 < "${srcdir}"/fix-scripts-include-path.patch
  # Backported from the git repo, without this header hooks may fail to build.
  patch -p1 < "${srcdir}"/add-missing-header.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
      --prefix='/usr' \
      --sbindir='/usr/bin' \
      --libexecdir='/usr/bin' \
      --sysconfdir='/etc' \
      --localstatedir='/var' \
      --with-dhcp-mysql \
      --with-dhcp-pgsql \
      --enable-shell \
      --enable-generate-docs
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Disable for now, some tests fail (for instance dhcp-ddns.sigterm_test)
  #make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # Handle /var/run -> /run symlink
  rmdir "${pkgdir}"/var/run/kea
  rmdir "${pkgdir}"/var/run
  install -Dm644 "${srcdir}"/tmpfiles-kea.conf "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  # Systemd service files
  install -Dm644 "${srcdir}"/kea-dhcp4.service "${pkgdir}"/usr/lib/systemd/system/kea-dhcp4.service
  install -Dm644 "${srcdir}"/kea-dhcp6.service "${pkgdir}"/usr/lib/systemd/system/kea-dhcp6.service
  # License, see https://bugs.archlinux.org/task/31547
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
