# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=synthv1
pkgver=0.9.28
pkgrel=1
pkgdesc="Old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=(x86_64)
url="https://synthv1.sourceforge.io/synthv1-index.html"
license=(GPL2)
groups=(lv2-plugins pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme lv2-host qt6-base qt6-svg)
makedepends=(alsa-lib cmake jack liblo lv2 qt6-tools)
optdepends=(
  'alsa-lib: for standalone application'
  'jack: for standalone application'
  'liblo: for standalone application'
  'new-session-manager: for session management using the standalone application'
  'qt6-wayland: for native wayland support'
)
source=(https://download.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha512sums=('adc17711b42bcd3b92249a25c230990338ea6a6312897b064502b98bcb1df51a2d998e387c20abbef2dd6a59a5c1a202375c8fb39d2f9f9c282df3783c79550d')
b2sums=('0edab997024a5d21153e05edb78608c2815d62269107f878d08e574db055b105b5bf18c8d1d538563bf4d93cf5b7f1855cf5d31d6c0cd2e0714fc5430decff0c')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
# vim:set ts=2 sw=2 et:
