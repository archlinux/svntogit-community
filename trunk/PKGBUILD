# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_name=starlette
pkgname=python-$_name
pkgver=0.25.0
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=(any)
url="https://github.com/encode/starlette"
license=(BSD)
depends=(python python-anyio python-typing-extensions)
makedepends=(python-build python-installer python-hatchling python-wheel)
checkdepends=(
  python-aiosqlite
  python-databases
  python-pytest
  python-trio

  # optdepends
  python-itsdangerous
  python-jinja
  python-python-multipart
  python-pyyaml
  python-httpx

  # not specified,but required
  python-sqlalchemy
)
optdepends=(
  'python-itsdangerous: for session middleware support'
  'python-jinja: for jinja templates'
  'python-python-multipart: for form parsing'
  'python-pyyaml: for schema generator'
  'python-httpx: for test client'
)
source=($_name-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz)
sha512sums=('ecff4905c093ffd21a4b102d4ca1dba22ceebd8e73f53f984fbe58daa7a0bf9c3de9a41b1672837602142a4e14787f3b59291d05b9cb6a0dd034d58ddacd9eb1')
b2sums=('82ed915a2ff7824a99e0f15f16a0723b8799ef43ab5b25dbbf7ee7bff7a3a1fc555e802a82950b11eb752a39e23fdf0870090a99bed568bfa82e3ecbcf42f5f3')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv -o 'markers=filterwarnings' -p no:warnings
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
