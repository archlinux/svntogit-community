# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgname=python-pytest
pkgver=6.1.2
pkgrel=3
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="https://pytest.org/"
depends=('python-attrs' 'python-iniconfig' 'python-more-itertools' 'python-packaging' 'python-pluggy'
         'python-py' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-argcomplete' 'python-hypothesis' 'python-mock' 'python-nose' 'python-requests'
              'python-xmlschema'
              # Unlisted, but actually used. Check again after a while:
              'python-decorator' 'python-docutils' 'python-jinja' 'python-numpy' 'python-pexpect'
              'python-pygments' 'python-twisted' 'python-pytest-xdist')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('3524c99b6e2cd3be456de0fe7130d15a2ada96d52835a0786925082a513c7d42a5422bb0d64a8e7fac22880b2441a18918853fdfde2c819d48d6f0ce0d4e09df')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-$pkgver
  python setup.py build
}

check() {
  cd pytest-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD"/build/lib python src/pytest/__main__.py
}

package() {
  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
