# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgname=python-faker
pkgver=13.7.0
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='https://faker.readthedocs.io/en/master/'
license=('MIT')
provides=("python-fake-factory=$pkgver")
conflicts=('python-fake-factory')
replaces=('python-fake-factory')
depends=('python-dateutil')
makedepends=('python-setuptools' 'python-pytest')
checkdepends=('python-validators' 'python-ukpostcodeparser' 'python-random2'
              'python-freezegun' 'python-pillow')
source=("$pkgname-$pkgver.tar.gz::https://github.com/joke2k/faker/archive/v$pkgver.tar.gz")
sha512sums=('f922f5945748e7f9d1f042c81cdd0c40f7e6550cae67e8eb91f133d5b02b56067cfd59ed6e4325451e672b38c15fc75ae34440ecb97d29bdc8d2b05d1e288be9')

prepare() {
  sed -i -e 's/==/>=/' faker-$pkgver/setup.py
}

build() {
  cd faker-$pkgver
  python setup.py build
}

check() {
  cd faker-$pkgver
  python setup.py egg_info
  python -m pytest
}

package() {
  cd faker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}
