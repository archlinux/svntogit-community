# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=aeson-compat
pkgname=haskell-aeson-compat
pkgver=0.3.7
pkgrel=2
pkgdesc="Compatibility layer for aeson"
url="https://hackage.haskell.org/package/${_hkgname}"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc" "haskell-aeson" "haskell-attoparsec" "haskell-attoparsec-iso8601"
         "haskell-base-compat" "haskell-exceptions" "haskell-hashable" "haskell-scientific"
         "haskell-semigroups" "haskell-text" "haskell-tagged" "haskell-time-locale-compat"
         "haskell-unordered-containers" "haskell-vector")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('b04ce7b048d0fc15c7fab2a29f20afba6e38c921601af68a0b3fc211c2b08b659536e22930a1a6094985cc3e3b98282e2019c32b95aab9b931ca9fa83b9dff09')

prepare() {
    # nats is an empty package in new GHC
    sed -e '/nats/d' \
        -i ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
