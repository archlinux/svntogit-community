# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=yubikey-touch-detector
pkgver=1.7.1
pkgrel=1
pkgdesc='A tool that can detect when your YubiKey is waiting for a touch'
arch=('x86_64')
url='https://github.com/maximbaz/yubikey-touch-detector'
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
optdepends=('gnupg: for GPG'
            'openssh: for SSH'
            'libnotify: for desktop notifications')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        "${url}/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz.asc")
sha256sums=('18c50b3150d089171d3d152e19ba93ca5d9093d982fc67a8f05903b6d04a4e16'
            'SKIP')
validpgpkeys=('EB4F9E5A60D32232BB52150C12C87A28FEAC6B20')

build() {
    cd "${pkgname}-${pkgver}"
    make build
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
