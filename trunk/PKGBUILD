# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

pkgname=sonic-visualiser
pkgver=4.3
pkgrel=2
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('bzip2' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd'
'libmad' 'libx11' 'opusfile' 'qt5-base' 'qt5-svg' 'speex')
makedepends=('alsa-lib' 'capnproto' 'fftw' 'libfishsound' 'libid3tag' 'liblo'
'liblrdf' 'liboggz' 'libpulse' 'libsamplerate' 'libsndfile' 'meson' 'portaudio'
'rubberband' 'serd' 'sord')
provides=('vamp-host')
source=("${pkgname}-${pkgver}.tar.gz::https://bintray.com/${pkgname}/${pkgname}/download_file?file_path=${pkgname}-${pkgver}.tar.gz")
sha512sums=('cbe0edee9f346545222e05156e4536710db932d6ec335687e91f2f4b24582e7f47703a3ecade87574ead8fa78dca821fd9ff2eb0d6e44c2b9c33c8d053fd738d')
b2sums=('4c9bbfc751f94dfc0e8c93a8ba791b0a4bf949fe1f528a081da583ad720dcb25b3e6f84e31104f11ab5003b338b62a1ca1a9572bc9ec820d0a4269d25cbe0e04')

build() {
  cd "${pkgname}-${pkgver}"
  meson --prefix='/usr' \
        --buildtype plain \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        -Dintrospection=enabled \
        build
  ninja -C build
}

package() {
  depends+=('libasound.so' 'libcapnp.so' 'libfftw3.so' 'libfftw3f.so'
  'libfishsound.so' 'libid3tag.so' 'libkj.so' 'liblo.so' 'liblrdf.so'
  'liboggz.so' 'libportaudio.so' 'libpulse.so' 'librubberband.so'
  'libsamplerate.so' 'libserd-0.so' 'libsndfile.so' 'libsord-0.so')
  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build
  # samples
  install -vDm 644 samples/* -t "${pkgdir}/usr/share/${pkgname}/samples"
  # templates
  install -vDm 644 templates/* -t "${pkgdir}/usr/share/${pkgname}/templates"
  # docs
  install -vDm 644 {CHANGELOG,CITATION,README{,_OSC}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
