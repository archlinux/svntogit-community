# Maintainer: Anatol Pomozov <anatol pomozov at gmail> 
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Konstantin Plotnikov <kostyapl at gmail dot com>

pkgname=fossil
pkgver=2.18
_tag=84f25d7eb10c0714109d69bb2809abfa8b4b5c3d73b151a5b10df724dacd46d8
pkgrel=1
pkgdesc='Simple, high-reliability, distributed software configuration management'
arch=(x86_64)
license=(BSD)
url='https://www.fossil-scm.org'
depends=(openssl zlib sqlite)
makedepends=(tcl)
optdepends=(tcl)
backup=(etc/xinetd.d/fossil)
source=(
  fossil.socket
  fossil.service
  fossil-xinetd
  https://fossil-scm.org/home/tarball/$_tag/fossil-src-$pkgver.tar.gz
  remove_sqlite1_option.patch::'https://www2.fossil-scm.org/home/vpatch?from=d862cb71d6266449&to=8af827342f4c4a77'
)
sha256sums=('f885e17998dc1eece1688a75e516663462fe72a7f4f132def4132055777c7ff8'
            'c4973079facf209d3757db81df08f8d0843ede242f2a0c12281720da65e3b166'
            '2f9a141fadfb7cb5225f0d65a308949bbc592bff9d567b5c472edb413c81f5aa'
            '300c1d5cdd6224ec6e8c88ab3f38d50f80e4071b503731b75bd61274cf310733'
            '8707b243505535b7ae8822225f701b0f4feab203b11c838899839520835a3661')

prepare() {
  cd fossil-src-$pkgver
  patch -p0 < ../remove_sqlite1_option.patch # SQLITE3_ENABLE_JSON1 is a no-op option now
}

build() {
  cd fossil-src-$pkgver
  ./configure --prefix=/usr --json --disable-internal-sqlite --with-tcl=/usr --with-tcl-private-stubs=1
  # headers and translate targets are problematic with parallel jobs
  #make -j1 bld bld/headers
  make
}

package() {
  install -Dm644 fossil-xinetd "$pkgdir"/etc/xinetd.d/fossil
  install -Dm644 fossil.socket "$pkgdir"/usr/lib/systemd/system/fossil.socket
  install -Dm644 fossil.service "$pkgdir"/usr/lib/systemd/system/fossil@.service

  cd fossil-src-$pkgver
  install -Dm755 fossil "$pkgdir"/usr/bin/fossil
  install -Dm644 tools/fossil-autocomplete.bash "$pkgdir"/usr/share/bash-completion/completions/fossil
  install -Dm644 tools/fossil-autocomplete.zsh "$pkgdir"/usr/share/zsh/site-functions/_fossil
  install -Dm644 COPYRIGHT-BSD2.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
