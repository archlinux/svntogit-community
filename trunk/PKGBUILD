# $Id$
# Maintainer: Dan Printzell <arch@vild.io>

pkgname=('dfmt')
pkgver=0.5.0
pkgrel=4
_pkgcommit=fef85e388a41add75020675ab33ed7e55c3efe85
pkgdesc="Dfmt is a formatter for D source code"
arch=('x86_64')
url="https://github.com/dlang-community/dfmt"
license=("custom")
groups=('dlang')
makedepends=('dmd' 'git')
depends=('libphobos')

source=(
	"git+https://github.com/dlang-community/dfmt#commit=$_pkgcommit"
	"git+https://github.com/dlang-community/libdparse"
)
sha256sums=(
	'SKIP'
	'SKIP'
)

prepare() {
	cd "$srcdir/dfmt"

	git submodule init
	git config submodule.libdparse.url "$srcdir/libdparse"
	git submodule update
}

build() {
	cd "$srcdir/dfmt"
	make
}

package() {
	# binaries
	install -Dm755 "$srcdir/dfmt/bin/dfmt" "$pkgdir/usr/bin/dfmt"

	# license
	install -Dm644 "$srcdir/dfmt/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
