# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgbase=suil
pkgname=(suil suil-docs)
pkgver=0.10.18
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=(x86_64)
url="https://drobilla.net/software/suil.html"
license=(ISC)
makedepends=(doxygen gtk2 gtk3 lv2 meson python-sphinx python-sphinx-lv2-theme qt5-base qt5-x11extras)
source=(https://download.drobilla.net/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('2c4630ef37f13c0e344eadfc8fdcfe70538abdabad1ad0865c43f9f5947fdbc3bdf56bb90e7a58f20d3e641ab9b7a7a670799920aca04374d7c992c52b93f8f3'
            'SKIP')
b2sums=('739a25fa46227f0316a25efe0741f303698869f41322d525670589ad01f951bc52155a638f15b20ce715e14cb42a01fb022f60426c44abe05d988c679d27f604'
        'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard <d@drobilla.net>

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  arch-meson $pkgname-$pkgver build -D cocoa=disabled
  meson compile -C build
}

check() {
  meson test -C build
}

package_suil() {
  depends=(gcc-libs glibc)
  optdepends=(
    'glib2: support for wrapping X11 and Qt5 in GTK3 and GTK2'
    'gtk2: support for wrapping X11 and Qt5 in GTK2 and GTK2 in Qt5'
    'gtk3: support for wrapping X11 and Qt5 in GTK3'
    'libx11: support for wrapping X11 directly and in GTK3 and GTK2'
    'qt5-base: support for wrapping X11 in Qt5 and Qt5 in GTK3 and GTK2'
    'qt5-x11extras: support for wrapping X11 in Qt5'
  )
  provides=(libsuil-0.so)

  meson install -C build --destdir "$pkgdir"
  (
    cd "$pkgdir"
    _pick $pkgname-docs usr/share/doc
  )
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,NEWS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}

package_suil-docs() {
  pkgdesc+=" - documentation"

  mv -v $pkgname/* "$pkgdir"
  mv -v "$pkgdir/usr/share/doc/$pkgbase"{-0,}
  find "$pkgdir" -type f -iname "*.buildinfo" -delete
}
