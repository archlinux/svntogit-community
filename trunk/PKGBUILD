# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gupnp-tools
pkgver=0.10.3
pkgrel=1
pkgdesc="Utilities and demos to work with UPnP"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=('x86_64')
license=('GPL')
depends=('gtksourceview4' 'gupnp' 'gupnp-av')
makedepends=('meson')
options=('debug')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('457f4d923935b078415cd2ba88d78db60079b725926b7ee106e4565efe3204de')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
