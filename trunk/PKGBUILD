# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: stonecrest <stonecrest[at]gmail[dot]com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=mac
pkgver=9.04
_pkgver="${pkgver//./}"
pkgrel=1
pkgdesc='APE codec and decompressor'
arch=('x86_64')
url='https://www.monkeysaudio.com/index.html'
license=('custom')
depends=('gcc-libs')
makedepends=('python-html2text')
source=(
  "$pkgname-$pkgver.zip::https://monkeysaudio.com/files/MAC_${_pkgver}_SDK.zip"
  "$pkgname-$pkgver-license.html::https://www.monkeysaudio.com/license.html"
)
sha512sums=('d104421e9bfa8c36dee89bd292c06629ff39a33e84569a5578f4a255432238a0cf09a7d03c5f7df3daf0e41d5cf3ac88f3b58581daf09a94935de91056334681'
            'cd2e6dd9d89044a8e4e125d035ebb933593bb6868f77607136c6897ed77606c71584ba3f5587e64b3f9a18277ba799d82cc12c91b146b952ee02beabe04d2fa5')
b2sums=('d4d4c93c6ca9453b2724da57efabe87f1e9789ef07b8e39eb3c292f0e6524d8f1a294c5a21592504c4f02a7a6c4e1f6e908741c79a5f25cc50f35ce2ea7670b0'
        '6f8ab6ca6e7b367a901c083c2390c1fd175cc0aee9b86b6280a7883797525172ad5f6e3b4293ca288009adc550cd1646a54b74bdbaae40759fa3be551d16de50')

build() {
  make -C Source/Projects/NonWindows

  # generate license
  html2text --ignore-links "$pkgname-$pkgver-license.html" \
    | sed -n '/^## License$/,$p' \
    > LICENSE.md
}

package() {
  make DESTDIR="$pkgdir" prefix=/usr install -C Source/Projects/NonWindows

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE.md
}
