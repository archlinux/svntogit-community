# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=tribler
_pkgver=7.2.0-rc1
pkgver=${_pkgver//-/_}
pkgrel=3
pkgdesc='Privacy enhanced BitTorrent client with P2P content discovery'
url='https://www.tribler.org/'
arch=('any')
license=('LGPL3')
depends=('libsodium' 'libtorrent-rasterbar' 'python2-pyqt5' 'qt5-svg' 'phonon-qt5-backend'
         'python2-apsw' 'python2-cherrypy' 'python2-cryptography' 'python2-decorator'
         'python2-feedparser' 'python2-chardet' 'python2-m2crypto' 'python2-netifaces'
         'python2-plyvel' 'python2-twisted' 'python2-configobj' 'python2-matplotlib'
         'python2-networkx' 'python2-psutil' 'python2-scipy' 'python2-libnacl'
         'python2-pony' 'python2-meliae' 'python2-pillow')
optdepends=('vlc: for internal video player')
makedepends=('python2-setuptools' 'git')
source=("https://github.com/Tribler/tribler/releases/download/v$_pkgver/Tribler-v$_pkgver.tar.xz")
sha512sums=('4c8ea0d08dd55f6b870e666385875b71b97ba07fc0f6d77a0e71c751a3f9ae0c28a7c3093e61141068d188b14ac6664f4fa67be41a82914f5cbc2ea1928ba048')

prepare() {
  cd $pkgname

  # Fix tribler path
  sed -i 's|/opt/tribler|/usr/share/tribler|g' systemd/anontunnel_helper@.service
  sed -i 's|/opt/tribler|/usr/share/tribler|g' systemd/tribler.service

  # Fix version info
  sed -e "s|version_id =.*|version_id = \"${_pkgver%_*}\"|g" \
      -e "s|build_date =.*|build_date = \"$(date)\"|g" \
        -i Tribler/Core/version.py
}

build () {
  cd $pkgname

  python2 setup.py build
}

package() {
  cd $pkgname

  # Install python modules
  python2 setup.py install --root="$pkgdir" --optimize=1

  # Install binary files/assets
  install -dm 755 "$pkgdir"/usr/{bin,share/tribler}
  cp -dr --no-preserve=ownership Tribler "$pkgdir"/usr/share/tribler
  cp -dr --no-preserve=ownership TriblerGUI "$pkgdir"/usr/share/tribler
  ln -s Tribler/Core/CacheDB/schema_sdb_v*.sql "$pkgdir"/usr/share/tribler/Tribler

  install -dm 755 "$pkgdir"/usr/share/{applications,pixmaps}
  install -Dm 644 Tribler/Main/Build/Ubuntu/tribler.desktop "$pkgdir"/usr/share/applications
  install -Dm 644 Tribler/Main/Build/Ubuntu/tribler.xpm "$pkgdir"/usr/share/pixmaps
  install -Dm 644 Tribler/Main/Build/Ubuntu/tribler_big.xpm "$pkgdir"/usr/share/pixmaps
  install -Dm 755 debian/bin/tribler "$pkgdir"/usr/bin
  install -Dm 644 logger.conf "$pkgdir"/usr/share/tribler/
  install -Dm 644 run_tribler.py "$pkgdir"/usr/share/tribler/
  install -Dm 644 check_os.py "$pkgdir"/usr/share/tribler/

  cp -dr --no-preserve=ownership twisted "$pkgdir"/usr/share/tribler

  # Remove test folders
  find "$pkgdir" -type d -name "test" -name "tests" -exec rm -rf {} \;

  # Install systemd files
  install -Dm 644 systemd/anontunnel_helper@.service "$pkgdir"/usr/lib/systemd/system/anontunnel_helper@.service
  install -Dm 644 systemd/tribler.service "$pkgdir"/usr/lib/systemd/system/tribler.service
}

