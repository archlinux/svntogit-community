# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Tyler Dence <tyzoid@archlinux32.org>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgbase=nextcloud-client
# Put config files for libcloudproviders integration in a separate package as
# there is no simple way yet to disable it.
# See: https://gitlab.gnome.org/World/libcloudproviders/-/issues/17
pkgname=(nextcloud-client nextcloud-client-cloudproviders)
# Upstream does not provide signed tarballs (yet)
# https://github.com/nextcloud/desktop/issues/1510
pkgver=3.2.2
pkgrel=1
arch=(x86_64)
url='https://nextcloud.com/'
license=(GPL)
makedepends=(libcloudproviders openssl sqlite
             qt5-quickcontrols2 qt5-svg qt5-tools qt5-webengine qt5-websockets qtkeychain
             extra-cmake-modules kio
             cmocka doxygen git python-sphinx)
source=("$pkgbase::git+https://github.com/nextcloud/desktop.git?signed#tag=v$pkgver")
validpgpkeys=(
  A26B951528EA1BA1678C7AE5D406C75CEE1A36D6  # one of keys controlled by github.com/camilasan
  42E775EAA3E47F9E0D5CDB0E00819E3BF4177B28  # controlled by github.com/misch7
  17166A5275C5FA6F1A48FD78074BBBCB8DECC9E2  # https://github.com/er-vin.gpg
  01D736173523DFC7A7B55258C2FBEF0E1C7E72D0  # https://github.com/allexzander
  267BF70F7905C2723B0243267D0F74F05C22F553  # https://github.com/mgallien
  3A877D9A896A057948059DEDAD2C27357B2CB11D  # https://github.com/FlexW
)
sha256sums=('SKIP')

prepare() {
  # tmpdir for check()
  mkdir tmpdir

  # Use system GNUInstallDirs.cmake so that we can benefit from
  # https://gitlab.kitware.com/cmake/cmake/merge_requests/3735
  rm -v $pkgbase/cmake/modules/GNUInstallDirs.cmake
}

build() {
  cd $pkgbase

  # bundled breakpad in libcrashreporter-qt submodule is too old and does not build with glibc >= 2.26
  # Upstream fix: https://github.com/google/breakpad/commit/bddcc58860f522a0d4cbaa7e9d04058caee0db9d
  cmake -B build -S . \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_CRASHREPORTER=OFF \
    -DUNIT_TESTING=ON

  # TODO: fix installation of PDF and HTML documents
  # WIP at https://github.com/yan12125/desktop/tree/doc-install-path
  make -C build all doc-man
}

check() {
  cd $pkgbase/build

  # Tests fail if $TMPDIR is too small; specify an alternative for machines
  # with a small /tmp partition.
  # Use UTC as TestCookies is sensitive to the timezone
  # https://github.com/nextcloud/desktop/blob/v3.2.2/test/testcookies.cpp#L24
  TMPDIR="$srcdir/tmpdir" TZ=UTC ARGS="--rerun-failed --output-on-failure" make test
}

package_nextcloud-client() {
  pkgdesc='Nextcloud desktop client'
  depends=(openssl sqlite qtkeychain qt5-svg qt5-webengine qt5-websockets xdg-utils libcloudproviders
           qt5-graphicaleffects qt5-quickcontrols2)
  optdepends=(
    'kio: integration with Dolphin'
    'nemo-python: integration with Nemo'
    'python-nautilus: integration with Nautilus'
    'python-caja: integration with Caja'
    'nextcloud-client-cloudproviders: cloudproviders support'
  )
  backup=('etc/Nextcloud/sync-exclude.lst')

  cd $pkgbase/build

  make DESTDIR="$pkgdir" install

  rm -v "$pkgdir"/usr/share/cloud-providers/com.nextcloudgmbh.Nextcloud.ini
  rm -v "$pkgdir"/usr/share/dbus-1/services/com.nextcloudgmbh.Nextcloud.service
  cd "$pkgdir"/usr/share && rmdir -p cloud-providers dbus-1/services
}

package_nextcloud-client-cloudproviders() {
  pkgdesc='cloudproviders support for the Nextcloud desktop client'
  depends=(nextcloud-client)

  cd $pkgbase/build
  make -C shell_integration/libcloudproviders DESTDIR="$pkgdir" install
}
