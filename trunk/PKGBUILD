# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>

pkgname=freeradius
pkgver=2.1.10
pkgrel=1
pkgdesc="The premier open source RADIUS server."
arch=('i686' 'x86_64')
url="http://www.freeradius.org/"
license=("GPL")
depends=('openssl' 'heimdal' 'pth' 'libldap>=2.4.18' 'net-snmp' 'pam' \
	 'postgresql-libs>=8.4.1' 'libtool' 'libmysqlclient' 'gdbm' 'shadow')
optdepends=('libpcap' 'unixodbc' 'python')
options=(!libtool emptydirs !makeflags)
install=$pkgname.install
source=(ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2 \
	radiusd)
md5sums=('8ea2bd39460a06212decf2c14fdf3fb8'
         '655d2d6764e0730d64e5d92e75794b51')

build() {
  cd $srcdir/freeradius-server-$pkgver

  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  # Fix compilation with heimdal >= 1.3.1
  # Don't mess around with KRB5_DEPRECATED which is used internally :o
  sed -i 's/ -DKRB5_DEPRECATED//' src/modules/rlm_krb5/Makefile.in || return 1

  ./configure --with-system-libtool --with-system-libltdl \
	      --prefix=/usr --enable-heimdal-krb5 \
	      --localstatedir=/var \
	      --sysconfdir=/etc \
	      --libdir=/usr/lib/freeradius

  make || return 1
  make install R=$pkgdir || return 1

  install -D -m755 ../radiusd $pkgdir/etc/rc.d/radiusd && \
  chmod o+r $pkgdir/etc/raddb/* && \
  mv $pkgdir/etc/raddb $pkgdir/etc/raddb.default
}
