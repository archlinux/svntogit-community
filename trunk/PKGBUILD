# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-tenacity
pkgname=('python-tenacity' 'python2-tenacity')
pkgver=4.12.0
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
makedepends=('python-setuptools' 'python2-setuptools' 'python2-futures' 'python2-monotonic'
             'python-pbr' 'python2-pbr')
checkdepends=('python-nose' 'python2-nose' 'python-tornado' 'python2-tornado')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/tenacity/archive/$pkgver.tar.gz")
sha512sums=('d8cb4a9d3ad1d59b1642394ca4a7c43426f6a1db3aaceb4864de562f9063af124312a144e751190ea9c35167827438e6b43f1d54a607d021de1e9c3650682c2d')

prepare() {
  cp -a tenacity-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/tenacity-$pkgver
  python setup.py build

  cd "$srcdir"/tenacity-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/tenacity-$pkgver
  python setup.py nosetests

  cd "$srcdir"/tenacity-$pkgver-py2
  python2 setup.py nosetests --ignore-files '.*asyncio.py'
}

package_python-tenacity() {
  depends=('python-six')
  optdepends=('python-tornado: for Tornado coroutines support')

  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-tenacity() {
  depends=('python2-six' 'python2-futures' 'python2-monotonic')
  optdepends=('python2-tornado: for Tornado coroutines support')

  cd tenacity-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
