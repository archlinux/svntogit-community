# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=('python-pyinotify' 'python2-pyinotify')
pkgver=0.9.2
pkgrel=1
pkgdesc='Python module used for monitoring filesystems events on Linux platforms with inotify.'
arch=('any')
url="http://github.com/seb-m/pyinotify"
license=('custom:MIT')
makedepends=('python' 'python2')
source=("http://seb.dbzteam.org/pub/pyinotify/releases/pyinotify-${pkgver}.tar.gz")
md5sums=('7d344e1efe3fe342e2e052774c9779e2')

build() {
  cp -r pyinotify pyinotify2

  cd pyinotify

  python setup.py build

  cd ${srcdir}/pyinotify2

  python2 setup.py build
}

package_python-pyinotify() {
  depends=('python')

  cd pyinotify

  python setup.py install --root=${pkgdir} -O1

  install -d ${pkgdir}/usr/share/licenses/python-pyinotify
  install -m644 COPYING \
    ${pkgdir}/usr/share/licenses/python-pyinotify
}

package_python2-pyinotify() {
  depends=('python2')
  replaces=('pyinotify')

  cd pyinotify2

  sed \
    -e 's_#!/usr/bin/env python_&2_' \
    -i python2/pyinotify.py

  python2 setup.py install --root=${pkgdir} -O1

  install -d ${pkgdir}/usr/share/licenses/python2-pyinotify
  install -m644 COPYING \
    ${pkgdir}/usr/share/licenses/python2-pyinotify
}
