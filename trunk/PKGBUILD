# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>
pkgname=('python-cherrypy' 'python2-cherrypy')
pkgver=3.2.0
pkgrel=3
pkgdesc="A pythonic, object-oriented web development framework"
arch=('any')
url="http://www.cherrypy.org"
license=('BSD')
makedepends=('python' 'python2')
source=(http://download.cherrypy.org/cherrypy/$pkgver/CherryPy-$pkgver.tar.gz 
	license)
md5sums=('e5c1322bf5ce962c16283ab7a6dcca3f'
         '22365dc6b0e6835b53da009aa36af017')

build() {
  cp -r CherryPy-$pkgver CherryPy2-$pkgver

  cd CherryPy-$pkgver
  python ./setup.py build

  cd $srcdir/CherryPy2-$pkgver
  sed \
    -e 's_#! /usr/bin/env python_&2_' \
    -i py2/cherrypy/cherryd
  sed \
    -e 's_#!/usr/bin/python_&2_' \
    -i py2/cherrypy/test/sessiondemo.py
  python2 ./setup.py build

}

package_python-cherrypy() {
  depends=('python')

  cd CherryPy-$pkgver

  python ./setup.py install --root=$pkgdir --optimize=1

  install -D -m644 $srcdir/license $pkgdir/usr/share/licenses/python-cherrypy/license
}

package_python2-cherrypy() {
  depends=('python2')

  cd CherryPy2-$pkgver

  python2 ./setup.py install --root=$pkgdir --optimize=1

  install -D -m644 $srcdir/license $pkgdir/usr/share/licenses/python2-cherrypy/license
}
