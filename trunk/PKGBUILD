# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Simon Parzer <simon.parzer@gmail.com>
pkgname=grafx2
pkgver=2.4
pkgrel=2
_pkgrev=2035
pkgdesc='Pixelart-oriented painting program'
arch=('x86_64' 'i686')
url='http://code.google.com/p/grafx2/'
license=('GPL2')
install=grafx2.install
depends=('sdl_ttf' 'sdl_image' 'lua' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('gendesk')
options=('!emptydirs')
source=("http://grafx2.googlecode.com/files/$pkgname-$pkgver.$_pkgrev-src.tgz")
sha256sums=('5818caea63cd3cf3318baf01dd20f81ec61e4514443000c80a41b68ab3f17a6b')
_name=('GrafX2')
_genericname=('Drawing program')
_exec=('grafx2 %U')
_mimetype=('image/bmp;image/gif;image/png;image/x-tga;image/tiff')
_categories=('Graphics;2DGraphics;RasterGraphics')

build() {
  cd "$srcdir"

  gendesk -n
  cd "$pkgname/src"
  make
}

package() {
  cd "$srcdir/$pkgname/src"

  make prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/$pkgname/doc/README.txt" \
    "$pkgdir/usr/share/doc/$pkgname/readme.txt"
}

# vim:set ts=2 sw=2 et:
