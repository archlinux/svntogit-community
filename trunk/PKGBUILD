# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-timeout
pkgname=('python-pytest-timeout' 'python2-pytest-timeout')
pkgver=1.3.2
pkgrel=1
pkgdesc='py.test plugin to abort hanging tests'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-timeout'
makedepends=('python-pytest' 'python2-pytest' 'mercurial')
checkdepends=('python-pexpect' 'python2-pexpect')
source=("hg+https://bitbucket.org/pytest-dev/pytest-timeout#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pytest-timeout{,-py2}
}

build() {
  cd "$srcdir"/pytest-timeout
  python setup.py build

  cd "$srcdir"/pytest-timeout-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-timeout
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-timeout-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-timeout() {
  depends=('python-pytest')

  cd pytest-timeout
  python setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-timeout() {
  depends=('python2-pytest')

  cd pytest-timeout-py2
  python2 setup.py install --root="$pkgdir"/ --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
