# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Caleb Maclennan <caleb@alerque.com>

pkgname=python-scikit-learn
pkgver=1.0
pkgrel=1
pkgdesc="A set of python modules for machine learning and data mining"
arch=('x86_64')
url='https://scikit-learn.org'
license=('BSD')
depends=('python-scipy' 'python-joblib' 'python-threadpoolctl')
optdepends=('python-matplotlib: plotting capabilities')
makedepends=('python-setuptools' 'cython' 'openmp')
options=(!emptydirs)
source=("scikit-learn-${pkgver}.tar.gz::https://github.com/scikit-learn/scikit-learn/archive/${pkgver}.tar.gz")
sha256sums=('fa478eaeb5dd92d0ac32f84e26b0de9dcfc52a64210887e4805a292927cc7102')

build() {
  cd scikit-learn-$pkgver
  python setup.py build
}

package() {
  cd scikit-learn-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # See FS#49651
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r doc/tutorial "$pkgdir"/usr/share/doc/$pkgname/tutorial
}
