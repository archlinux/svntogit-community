# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Carl George < arch at cgtx dot us >

pkgbase=python-smmap
_pypiname=smmap2
pkgname=("python-smmap" "python2-smmap")
pkgver=2.0.3
pkgrel=1
pkgdesc="A pure git implementation of a sliding window memory map manager"
arch=("any")
url="https://github.com/gitpython-developers/smmap"
license=("BSD")
makedepends=("python-setuptools" "python2-setuptools")
checkdepends=("python-nose" "python2-nose")
source=("${pkgbase}-${pkgver}.tar.gz::https://pypi.org/packages/source/s/${_pypiname}/${_pypiname}-${pkgver}.tar.gz"
        "${pkgbase}-${pkgver}.tar.gz.asc::https://pypi.org/packages/source/s/${_pypiname}/${_pypiname}-${pkgver}.tar.gz.asc")
sha512sums=('f5623247fbdb6b0e94bbfe7104ffd84ee4563a0c32781256d92d46fd23e05280d7a0e3952a573b6834e253e9b68175784babc36d91ab61edf6d0d072830e1fde'
            'SKIP')
validpgpkeys=('4477ADC5977D7C60D2A7E3789FEE1C6A3B07188F') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
    cp -a "${srcdir}/${_pypiname}-${pkgver}"{,-py2}
}

build() {
    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py build
}

check() {
    cd "${srcdir}/${_pypiname}-${pkgver}"
    nosetests3

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    nosetests2
}

package_python-smmap() {
    depends=("python")

    cd "${srcdir}/${_pypiname}-${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-smmap() {
    depends=("python2")

    cd "${srcdir}/${_pypiname}-${pkgver}-py2"
    python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
