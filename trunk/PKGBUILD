# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sympow
pkgver=1.018.1
pkgrel=5
pkgdesc="A a package to compute special values of symmetric power elliptic curve L-functions"
arch=(x86_64)
url="http://www.sagemath.org"
license=(GPL)
depends=(pari)
source=("http://mirrors.mit.edu/sage/spkg/upstream/$pkgname/$pkgname-$pkgver.tar.bz2" 'sympow.sh'
        sympow-datafiles.patch sympow-initialize-tacks.patch)
sha256sums=('6cd4559e177502c44dbce3b1a6aeab3e6f4d24df1f21e19aaf25f3a0e2d1f042'
            'cc9afc9c4cc1b9b539b2051bda960a661809475e68389f4e9c56bd72268e16ee'
            'c3028de720d5069f89f520d86c3ab6dec686f476c8b19a7a0bd83a7db4abf0cd'
            '61c79418d5bb173a3976a3c351a7a1ae1ddba4e2e8e183d7246ba5013efe1e31')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../sympow-datafiles.patch # Fedora
  patch -p1 -i ../sympow-initialize-tacks.patch # fix segfaults
}

build() {
  cd $pkgname-$pkgver

  ./Configure
  make
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir"/usr/{bin,share/sympow}
  cp -r *.gp datafiles "$pkgdir"/usr/share/sympow
  cp sympow "$pkgdir"/usr/bin/sympow_bin
  cp new_data "$pkgdir"/usr/bin/new_data

  install -Dm755 "$srcdir"/sympow.sh "$pkgdir"/usr/bin/sympow
}
