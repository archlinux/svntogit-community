# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=unicode-transforms
pkgname=haskell-unicode-transforms
pkgver=0.4.0.1
pkgrel=46
pkgdesc="Unicode transforms (normalization NFC/NFD/NFKC/NFKD)"
url="https://github.com/composewell/unicode-transforms/"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-unicode-data')
makedepends=('ghc' 'uusi' 'haskell-hspec' 'haskell-quickcheck' 'haskell-split' 'haskell-text-icu')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('3d1347209d2b50fefac1b8bef3a4a82ab8e5f2addc361a338399225e37c933c1e5495f7a7c658c052d818dcc4bee20b48aa37c351fbfdaa926d98ae80f9dab14')

prepare() {
    cd $_hkgname-$pkgver
    uusi -u unicode-data $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir=$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
            -f-dev -fhas-icu -fhas-llvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
