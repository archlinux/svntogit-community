# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=5.1.1
pkgrel=2
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('x86_64')
url="https://kmymoney.org/"
license=('GPL')
depends=('aqbanking' 'gwenhywfar' 'boost-libs' 'libalkimia' 'sqlcipher' 'qt5-base' 'qgpgme' 'karchive'
         'kcoreaddons' 'kconfig' 'kwidgetsaddons' 'ki18n' 'kcompletion' 'kcmutils' 'kitemmodels'
         'kitemviews' 'kservice' 'kwallet' 'kiconthemes' 'kxmlgui' 'kidentitymanagement'
         'ktextwidgets' 'knotifications' 'kio' 'kdoctools' 'kholidays' 'kross' 'kactivities'
         'kqtquickcharts' 'kdiagram' 'kdewebkit' 'kcontacts' 'libical' 'libakonadi' 'shared-mime-info')
makedepends=('boost' 'extra-cmake-modules' 'doxygen')
optdepends=('perl: for financequote.pl')
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kmymoney-qsortfilterproxymodel-crash.patch)
validpgpkeys=('D69A745A55331F44F404D8258D4DE062AA2EB01C')
sha256sums=('df7b9f78e8596f99d28295ca738708f0655ee057789dfd921c7b1bab96578298'
            'SKIP'
            'c21c3c6d2c9af63b7d2dd63b51cd9746a9b3952d2230b19bd59569ce8fb0fe55')

prepare() {
  patch -d $pkgname-$pkgver -p1 < kmymoney-qsortfilterproxymodel-crash.patch # FS#70367, rebase of de390aed4652a0e9ff0bffcade33827a6283aaa1
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  mkdir build
  cd build

  cmake ../ \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=YES \
    -Wno-dev
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver/build

  make DESTDIR="${pkgdir}" install
}
