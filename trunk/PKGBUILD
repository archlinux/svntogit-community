# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Alexander Phinikarides <alexisph _AT_ gmail _DOT_ com>

pkgbase='python-pyroute2'
_pkgbase="${pkgbase//python-/}"
pkgname=("python-${_pkgbase}" "python2-${_pkgbase}")
pkgdesc="A pure Python netlink and Linux network configuration library"
pkgver=0.5.7
pkgrel=1
arch=('any')
url="https://docs.pyroute2.org/"
license=('GPL2' 'Apache')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha512sums=('6f948eb99a96ad57de7612565da0eb1e14fb59a1defc88e2bac930d77ae5efcff7268a0109cf608cbd2c6afc81d99aab18ad3b4257731946ee6ba9608cd5ac00')

prepare() {
  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py check

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 setup.py check
}

package_python-pyroute2() {
  depends=('python')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-pyroute2() {
  depends=('python2')

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # those are conflicting, if you actually need the command line utils
  # use the py3 package to obtain them
  rm -f "${pkgdir}"/usr/bin/{pyroute2-cli,ss2}
}

# vim:set ts=2 sw=2 et:
