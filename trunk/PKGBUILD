# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: mlq <mlq@pwmt.org>

pkgbase=girara
pkgname=(girara-common girara-gtk2 girara-gtk3)
pkgver=0.1.4
pkgrel=1
pkgdesc='User interface library'
arch=('i686' 'x86_64')
url="http://pwmt.org/projects/girara"
license=('custom')
makedepends=('gtk3' 'gtk2')
source=(http://pwmt.org/projects/girara/download/girara-$pkgver.tar.gz)
md5sums=('f348fbe96ceda4a89142cec92de08d25')

build() {
  true
}

package_girara-common() {
  depends=()
pkgdesc='User interface library - common files'

  cd "$srcdir/girara-$pkgver"

  make clean

  # install header
  make DESTDIR="$pkgdir/" install-headers

  # install language files
  make DESTDIR="$pkgdir/" -C po install

  # remove pkgconfig files
  rm -r $pkgdir/usr/lib/pkgconfig

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_girara-gtk2() {
  depends=('gtk2' 'girara-common')
  provides=('girara')

  cd "$srcdir/girara-$pkgver"

  make clean
  make GIRARA_GTK_VERSION=2

  # install files
  make GIRARA_GTK_VERSION=2 DESTDIR="$pkgdir/" install

  # remove common files
  rm -r "$pkgdir/usr/include"
  rm -r "$pkgdir/usr/share"

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_girara-gtk3() {
  depends=('gtk3' 'girara-common')
  provides=('girara')

  cd "$srcdir/girara-$pkgver"

  make clean
  make GIRARA_GTK_VERSION=3

  # install files
  make GIRARA_GTK_VERSION=3 DESTDIR="$pkgdir/" install

  # remove common files
  rm -r "$pkgdir/usr/include"
  rm -r "$pkgdir/usr/share"

  # install license
  install -D -m664 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
