# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Linus Sj√∂gren <thelinx@unreliablepollution.net>
# Contributor: Eric Forgeot < http://anamnese.online.fr >, dreeze
pkgname=love
pkgver=11.1
pkgrel=1
pkgdesc="An open-source 2D game engine which uses the versatile Lua scripting language to create dynamic gaming experiences"
arch=(x86_64)
url="http://love2d.org/"
license=('zlib')
depends=('luajit' 'physfs' 'freetype2' 'devil' 'mpg123' 'openal' 'libvorbis' 'libmodplug' 'sdl2' 'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils' 'libtheora')
source=("https://bitbucket.org/rude/love/downloads/love-${pkgver}-linux-src.tar.gz")
sha512sums=('f4df860508b75e4bb7c0ee8a2ee4406a7c8f3ce65cdbca9ed8a0cd90df81507ba7ec9a9d45279488047cc16b6e51940edff54e7a34de63bf852dbec5115b4c03')

build() {
  cd "$srcdir"/love-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/love-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm0644 "license.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
