# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Schubert <mschu.dev at gmail>

pkgbase=python-pysocks
pkgname=(python-pysocks python2-pysocks)
pkgver=1.7.0
pkgrel=1
pkgdesc="SOCKS4, SOCKS5 or HTTP proxy (Anorov fork PySocks replaces socksipy)"
arch=('any')
license=('BSD')
url="https://github.com/Anorov/PySocks"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('lib32-glibc' 'python2-twisted' 'python2-tornado')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Anorov/PySocks/archive/$pkgver.tar.gz")
sha512sums=('280c9d1f147cd665a9989dc143e0b1f43bbc0575dc0b6b92472bc4b8cf2315f37dba74c5e77ff0b3c3ede49ee5c1e53d6a330bcb3c8d822023378c1cfb8cac0f')

prepare() {
  cp -a PySocks-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PySocks-$pkgver
  python setup.py build

  cd "$srcdir"/PySocks-$pkgver-py2
  python2 setup.py build
}

# Tests disabled as it requires external services to run
check_disabled() {
  cd "$srcdir"/PySocks-$pkgver-py2/test
  python2 socks4server.py &
  _SOCKS4SERVER=$!
  python2 httpproxy.py &
  _HTTPPROXY=$1

  ./mocks start

  sleep 1

  python2 sockstest.py

  cd "$srcdir"/PySocks-$pkgver/test
  python sockstest.py

  ./mocks shutdown

  kill $_SOCKS4SERVER $_HTTPPROXY
}

package_python-pysocks() {
  depends=('python')
  provides=('python-socks')
  replaces=('python-socks')

  cd PySocks-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python2-pysocks() {
  depends=('python2')
  provides=('python2-socks')
  replaces=('python2-socks')

  cd PySocks-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
