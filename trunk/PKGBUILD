# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Ray Rashif <schivmeister@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=xsensors
pkgver=0.70
pkgrel=5
pkgdesc="X11 interface to lm_sensors"
arch=('x86_64')
url="http://www.linuxhardware.org/xsensors/"
license=('GPL')
depends=('gtk2' 'lm_sensors')
makedepends=('imagemagick' 'gendesk')
changelog=$pkgname.changelog
source=(http://www.linuxhardware.org/xsensors/$pkgname-$pkgver.tar.gz
	remove-unused-variables.patch
	replace-deprecated-gtk.patch)
md5sums=('4f8fb83cfd03c0cc34967a73c6021531'
         '8ca558b7f7e4a5f83f63498668c4ad8f'
         'd9ee6799d45adc7c62273f32085bec5f')

build() {
  cd "$srcdir"
  gendesk -n --pkgname "$pkgname" --genericname="Hardware Monitor" \
          --comment="View hardware health" --categories="System;Monitor;GTK"

  cd "$pkgname-$pkgver"

  # patches from debian
  patch -Np1 -i ${srcdir}/remove-unused-variables.patch
  patch -Np1 -i ${srcdir}/replace-deprecated-gtk.patch

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

  convert "$pkgdir/usr/share/pixmaps/xsensors/default.xpm" \
    "$pkgdir/usr/share/pixmaps/xsensors.png"
  install -Dm644 "$srcdir/xsensors.desktop" \
    "$pkgdir/usr/share/applications/xsensors.desktop"
}
