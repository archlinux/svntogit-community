# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-lexicon
pkgver=2.0.0
pkgrel=1
pkgdesc="Powerful dict subclass(es) with aliasing & attribute access"
url="https://github.com/bitprophet/lexicon"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-relaxed')
source=("https://github.com/bitprophet/lexicon/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bcd6d6a8e2e91b3de9c2ade4fb9eb11ece54eefdd73076fe43261427c2a94c72acb335593d7c098419d9ee95c2b1dceac1b6594ef4a6e73e940fb7af355b291a')

build() {
  cd lexicon-$pkgver
  python setup.py build
}

check() {
  cd lexicon-$pkgver
  python -m pytest
}

package() {
  cd lexicon-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
