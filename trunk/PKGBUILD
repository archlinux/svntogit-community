# $Id$
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=uptimed
pkgver=0.4.0
pkgrel=1
pkgdesc="A system uptime record daemon"
arch=(x86_64)
#url="http://podgorny.cz/uptimed/"
url="https://github.com/rpodgorny/uptimed"
license=('GPL')
depends=()
makedepends=('systemd')
backup=(etc/uptimed.conf)
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/rpodgorny/uptimed/archive/v$pkgver.tar.gz")
md5sums=('6c50b0671e7efed89efb4b956dc0f5e5')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make install DESTDIR="$pkgdir"
  mv "$pkgdir"/etc/uptimed.conf-dist "$pkgdir"/etc/uptimed.conf
}
