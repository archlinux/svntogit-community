# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-data
pkgname=haskell-generic-data
pkgver=0.7.0.0
pkgrel=9
pkgdesc="Deriving instances with GHC.Generics and related utilities"
url="https://github.com/Lysxia/generic-data"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-contravariant' 'haskell-show-combinators')
makedepends=('ghc' 'haskell-generic-lens' 'haskell-one-liner' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('0af703a1573acb9856fbd94e46b1c2aae13f9e02a7c3f95c3134ca9d0bb2512852f465aa5740021b2c59e1104d8a45d7e9965de782746bc0842c08424ee6afc1')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
