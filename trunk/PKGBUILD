# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cliff
pkgver=3.8.0
pkgrel=2
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="https://docs.openstack.org/cliff/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
         'python-stevedore' 'python-yaml')
makedepends=('git')
checkdepends=('python-stestr' 'python-mock' 'python-sphinx' 'python-testscenarios')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz")
sha512sums=('a4749446067c9307e766cb9e2255b06325722da2af1c576c072c5af2e7982fe9d455e6ff8b2ec1277390edb1efb8e530663fd4e66dcc3e0e910243d1e880be2e')

export PBR_VERSION=$pkgver

build() {
  cd cliff-$pkgver
  python setup.py build
}

check() {
  cd cliff-$pkgver
  stestr run
}

package() {
  cd cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
