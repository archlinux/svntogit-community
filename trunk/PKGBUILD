# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=0.13.7
pkgrel=1
pkgdesc="A minimally intrusive build tool for Scala projects"
arch=('any')
url='http://www.scala-sbt.org/'
license=('custom' 'APACHE')
depends=('java-runtime-headless' 'sh')
source=("http://repo.typesafe.com/typesafe/ivy-releases/org.scala-sbt/sbt-launch/${pkgver}/sbt-launch.jar"
        'LICENSE' 'LICENSE_Scala' 'sbt.sh' 'sbt.conf.sh' 'sbt.conf.csh')
md5sums=('7341059aa30c953021d6af41c89d2cac'
         '0cc49331142988765ce73782a5839c57'
         '4a09aae439532404ef76d0ba1db3342c'
         'd7b78b04e20ceb05c6870757bce6d3d9'
         'd3691d3866b0f4e35d4cdc15fac495ef'
         'db3701bd6facf736100efa5871847564')

package() {
  install -d "${pkgdir}"/usr/share/java/${pkgname}
  install -m755 sbt-launch.jar "${pkgdir}"/usr/share/java/${pkgname}

  install -d "${pkgdir}"/usr/bin/
  install -Dm755 "${srcdir}"/sbt.sh "${pkgdir}"/usr/bin/sbt

  install -d "${pkgdir}"/etc/profile.d
  install -Dm644 "${srcdir}"/sbt.conf.sh "${pkgdir}"/etc/profile.d/sbt.sh
  install -Dm644 "${srcdir}"/sbt.conf.csh "${pkgdir}"/etc/profile.d/sbt.csh

  install -Dm644 "${srcdir}"/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 "${srcdir}"/LICENSE_Scala \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE_Scala
}
