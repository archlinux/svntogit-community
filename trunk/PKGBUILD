# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-subunit2sql
pkgname=(python-subunit2sql python2-subunit2sql)
pkgver=1.8.0
pkgrel=1
pkgdesc="Command to Read a subunit file or stream and put the data in a SQL DB"
arch=('any')
url="https://pypi.python.org/pypi/subunit2sql"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-alembic'
             'python2-alembic' 'python-stevedore' 'python2-stevedore' 'python-sqlalchemy'
             'python2-sqlalchemy' 'python-oslo-config' 'python2-oslo-config' 'python-dateutil'
             'python2-dateutil' 'python-subunit' 'python2-subunit' 'python-oslo-db'
             'python2-oslo-db')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-mock' 'python2-mock'
              'python-testresources' 'python2-testresources' 'python-testscenarios'
              'python2-testscenarios' 'python-oslo-concurrency' 'python2-oslo-concurrency')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack-infra/subunit2sql/archive/$pkgver.tar.gz")
sha512sums=('af77a24237f08ab18d5118c133596de13b7a72a79895f3ef71faa12a5ef5033bd2734b592c4944fcc48b8605cb5d0904968a0b0625828631d21d3f4621502d80')

prepare() {
  cp -a subunit2sql-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/subunit2sql-$pkgver
  python setup.py build

  cd "$srcdir"/subunit2sql-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/subunit2sql-$pkgver
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/subunit2sql-$pkgver-py2
  PYTHON=python2 python2 setup.py testr || warning "Tests failed"
}

package_python-subunit2sql() {
  depends=('python-six' 'python-pbr' 'python-alembic' 'python-stevedore' 'python-sqlalchemy'
           'python-oslo-config' 'python-dateutil' 'python-subunit' 'python-oslo-db')

  cd "$srcdir"/subunit2sql-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-subunit2sql() {
  depends=('python2-six' 'python2-pbr' 'python2-alembic' 'python2-stevedore' 'python2-sqlalchemy'
           'python2-oslo-config' 'python2-dateutil' 'python2-subunit' 'python2-oslo-db')

  cd "$srcdir"/subunit2sql-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/sql2subunit{,2}
  mv "$pkgdir"/usr/bin/subunit2sql{,2}
  mv "$pkgdir"/usr/bin/subunit2sql-db-manage{,2}
  mv "$pkgdir"/usr/bin/subunit2sql-graph{,2}
}

# vim:set ts=2 sw=2 et:
