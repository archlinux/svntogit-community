# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sirius Bakke <sirius\at/nonline.org>

pkgname=pam_mount
pkgver=2.16
pkgrel=4
pkgdesc="A PAM module that can mount volumes for a user session"
arch=('x86_64')
url="http://pam-mount.sourceforge.net/"
license=('GPL')
depends=('pcre' 'util-linux' 'libhx' 'libxml2' 'openssl' 'cryptsetup')
backup=('etc/security/pam_mount.conf.xml')
options=(!emptydirs)
source=("https://downloads.sourceforge.net/project/pam-mount/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('ae0207ea6c96189414f906a7b0880abc7751a98e26b3d0375c9afab3722471e7'
            'SKIP')
validpgpkeys=(2F8BFE96D14F8B560FBC1F1DA28BDFEEB5A2B2FF) # Jan Engelhardt <jengelh@inai.de>

build() {
  cd -- "$srcdir/$pkgname-$pkgver"
  aclocal
  libtoolize
  automake --add-missing
  autoreconf
  ./configure \
	--prefix=/usr \
	--with-ssbindir=/usr/bin \
	--sbindir=/usr/bin \
	--with-slibdir=/usr/lib \
	--sysconfdir=/etc \
	--localstatedir=/var
  make
}

package() {
  cd -- "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
