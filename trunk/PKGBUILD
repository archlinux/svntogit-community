# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgbase=python-pyicu
pkgname=('python-pyicu' 'python2-pyicu')
pkgver=2.0.3
pkgrel=2
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'icu')
checkdepends=('python-fonttools' 'python2-fonttools' 'python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
sha512sums=('a08a34772fc8205660320d7ea1bea52cd9c21ff9edf0aef1059c9439406d4b69ab434b35347dce8928cff8051600d55cb71bcc74eb89f203037be629d83e044e')

prepare() {
  cp -a PyICU-$pkgver{,-py2}
}

build() {
  cd "$srcdir/"PyICU-$pkgver
  python setup.py build

  cd "$srcdir/"PyICU-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir/"PyICU-$pkgver
  python setup.py test

  cd "$srcdir/"PyICU-$pkgver-py2
  python2 setup.py test || true # https://github.com/ovalhub/pyicu/issues/61
}

package_python-pyicu() {
  depends=('python' 'icu')

  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyicu() {
  depends=('python2' 'icu')

  cd PyICU-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
