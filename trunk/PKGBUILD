# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-olefile
pkgname=('python-olefile' 'python2-olefile')
_pyname=olefile
pkgver=0.46
pkgrel=1
pkgdesc='Python library to parse, read and write Microsoft OLE2 files (formerly OleFileIO_PL)'
url='https://www.decalage.info/olefile'
arch=('any')
license=('BSD')
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=(${_pyname}-${pkgver}.tar.gz::https://github.com/decalage2/olefile/archive/v${pkgver}.tar.gz)
sha512sums=('e70975cdda11004ab331908085e0cbd9cf3e44f49fa1453a7af5373bfed0a98729bb7600fee7a1042be6c40c58649f3650972d9e4520bbab218cf715f6264f98')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    py.test
  )
  (cd ${_pyname}-${pkgver}-py2
    py.test2
  )
}

package_python-olefile() {
  depends=('python')
  replaces=('python3-olefileio')
  conflicts=('python3-olefileio')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md doc/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"

}

package_python2-olefile() {
  depends=('python2')
  replaces=('python2-olefileio')
  conflicts=('python2-olefileio')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md doc/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
