# $Id$
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=haproxy
pkgver=1.6.5
pkgrel=4

pkgdesc='Reliable, high performance TCP/HTTP load balancer'
url='http://haproxy.org/'
arch=('i686' 'x86_64')
license=('GPL')

depends=('openssl' 'pcre' 'zlib' 'lua')

backup=('etc/haproxy/haproxy.cfg')

install=haproxy.install

source=(http://haproxy.org/download/${pkgver%.*}/src/haproxy-$pkgver.tar.gz
        haproxy.cfg
        gcc6-fix.patch
        CVE-2016-5360-reqdeny.patch)

md5sums=('5290f278c04e682e42ab71fed26fc082'
         '02241a8ca257d12f7eb131e2d7da3f3b'
         'c4b4fba82b6f33270ed57514ed3f53e8'
         '24c814e197627f30b41f89ae10b8520f')

prepare() {
  cd haproxy-$pkgver
  sed -i 's:/usr/sbin/haproxy:/usr/bin/haproxy:' src/haproxy-systemd-wrapper.c
  patch -p1 -i "$srcdir"/gcc6-fix.patch
  patch -p1 -i "$srcdir"/CVE-2016-5360-reqdeny.patch
}

build() {
  cd haproxy-$pkgver

  make CPU=generic TARGET=linux2628 \
    USE_GETADDRINFO=1 \
    USE_OPENSSL=1 \
    USE_PCRE=1 USE_PCRE_JIT=1 \
    USE_ZLIB=1 \
    USE_LUA=1

  for contrib in halog iprange systemd; do
    make -C contrib/$contrib SBINDIR=/usr/bin
  done
}

package() {
  cd haproxy-$pkgver
  make \
    EXTRA=haproxy-systemd-wrapper \
    PREFIX="$pkgdir"/usr \
    SBINDIR="$pkgdir"/usr/bin \
    DOCDIR="$pkgdir"/usr/share/$pkgname \
    install

  for contrib in halog iprange; do
    install -Dm755 contrib/$contrib/$contrib "$pkgdir"/usr/bin/$contrib
  done

  install -Dm644 ../haproxy.cfg "$pkgdir"/etc/haproxy/haproxy.cfg
  install -Dm644 contrib/systemd/haproxy.service \
    "$pkgdir"/usr/lib/systemd/system/haproxy.service

  install -d "$pkgdir"/usr/share/haproxy/examples/errorfiles
  install -m644 examples/*.cfg "$pkgdir"/usr/share/haproxy/examples/
  install -m644 examples/errorfiles/*.http \
    "$pkgdir"/usr/share/haproxy/examples/errorfiles/

  install -Dm644 examples/haproxy.vim \
    "$pkgdir"/usr/share/vim/vimfiles/syntax/haproxy.vim
}
