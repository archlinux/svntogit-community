# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=6.0.1.79
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpeg' 'kcodecs' 'libcue' 'qt5-svg' 'taglib' 'vlc')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-music/archive/$pkgver.tar.gz"
        qt-5.15.patch)
sha512sums=('10bbd535378d55ace45f546d13a74f2284e7ca3de0ec6fa545739a299b69929d69b5711a3903c149ffe033b3d1e99f12652d5488937192deac5073a309b3cdf8'
            '8e9d74a78c010431cf5428561ffb59006658281da9f0856c5dfdd4d854e40f4b03d5799f8b0cce81c08c393edf2c225505e76e3ad010c0a3834edeb3c208e853')

prepare() {
  cd deepin-music-$pkgver/music
  patch -Np2 -i "$srcdir"/qt-5.15.patch

  sed -i '/#include <QPainter>/a #include <QPainterPath>' music-player/view/widget/soundpixmapbutton.cpp
}

build() {
  cd deepin-music-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-music-$pkgver
  DESTDIR="$pkgdir" ninja install
}
