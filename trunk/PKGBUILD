# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-twisted
pkgname=('python-pytest-twisted' 'python2-pytest-twisted')
pkgver=1.12
pkgrel=1
pkgdesc='A twisted plugin for py.test'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-twisted'
makedepends=('python-pytest' 'python2-pytest' 'python-greenlet' 'python2-greenlet'
             'python-decorator' 'python2-decorator' 'python-twisted' 'python2-twisted')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-twisted/archive/v$pkgver.tar.gz")
sha512sums=('3fb5bdea9632ebd830637be8bef15432859e4689e41d2f87258dc5404734d98b76d26de494e80464f4321db6016836851daff5ff4eaf083fa7159cc8381e35bf')

prepare() {
  cp -a pytest-twisted-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-twisted-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test

  cd "$srcdir"/pytest-twisted-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2
}

package_python-pytest-twisted() {
  depends=('python-pytest' 'python-greenlet' 'python-decorator' 'python-twisted')

  cd pytest-twisted-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pytest-twisted() {
  depends=('python2-pytest' 'python2-greenlet' 'python2-decorator' 'python2-twisted')

  cd pytest-twisted-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
