# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=('python-stevedore' 'python2-stevedore')
pkgver=0.13
pkgrel=1
pkgdesc="Manage dynamic plugins for Python applications"
arch=('any')
url="https://github.com/dreamhost/stevedore"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
options=('!emptydirs')
source=(http://pypi.python.org/packages/source/s/stevedore/stevedore-$pkgver.tar.gz)
sha256sums=('6ab2ee6fbcca22f1867021269569d1657c5801efd8a425742d36a7414cc8d2ac')

package_python-stevedore() {
  depends=('python')

  cd "$srcdir/stevedore-$pkgver"
  python3 setup.py install --root="$pkgdir" -O1
}

package_python2-stevedore() {
  depends=('python2')

  cd "$srcdir/stevedore-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
