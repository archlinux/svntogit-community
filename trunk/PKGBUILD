# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Vincent Cappe <vcappe at gmail dot com>
# Contributor: Jeff Bailes <thepizzaking at gmail dot com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=liboggz
pkgver=1.1.1
pkgrel=3
pkgdesc='Simple programming interface for reading and writing Ogg files and streams'
arch=('i686' 'x86_64')
url="http://www.xiph.org/oggz/"
license=('custom:BSD')
depends=('libogg' 'sh')
makedepends=('doxygen' 'pkg-config')
options=('!libtool')
source=("http://downloads.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('3649a657f8e368b881ba8b1b57381ff7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-static=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  # license
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # bash completion
  install -Dm644 bash-completion/oggz "${pkgdir}/usr/share/bash-completion/completions/oggz"
}
