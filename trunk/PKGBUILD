# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=3.4.6
pkgrel=2
pkgdesc="Music playback and management application"
url="https://wiki.gnome.org/Apps/Rhythmbox"
arch=(x86_64)
license=(GPL)
depends=(
  dconf
  grilo
  gst-plugins-base
  gst-plugins-good
  json-glib
  libgudev
  libnotify
  libpeas
  libsoup
  media-player-info
  python-gobject
  tdb
  totem-plparser
)
makedepends=(
  brasero
  git
  gobject-introspection
  gtk-doc
  libdmapsharing
  libgpod
  libmtp
  lirc
  meson
  vala
  yelp-tools
  zeitgeist
)
checkdepends=(
  check
  xorg-server-xvfb
)
optdepends=(
  'brasero: Audio CD Recorder plugin'
  'grilo-plugins: Grilo media browser plugin'
  'gst-libav: Extra media codecs'
  'gst-plugins-bad: Extra media codecs'
  'gst-plugins-ugly: Extra media codecs'
  'gvfs-mtp: Portable Players - Android plugin'
  'libdmapsharing: DAAP Music Sharing plugin'
  'libgpod: Portable Players - iPod plugin'
  'libmtp: Portable Players - MTP plugin'
  'lirc: LIRC plugin'
  'zeitgeist: Zeitgeist logging plugin'
)
_commit=1b14819fc3937d9f03edd8dac092228cb4589240  # tags/v3.4.6^0
source=("git+https://gitlab.gnome.org/GNOME/rhythmbox.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Simplify setup in check()
  git cherry-pick -n 2d36c6b1895c7478bd73bd82c9ba1e896c7bb55d

  # Fix tests
  git cherry-pick -n 37c9c9f46753fbac63dc6ca06b4a36822c896fbe \
                     6e8e2754f1ebfa77dfa09d814663990e68db622d
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs -t 3
}

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim:set sw=2 sts=-1 et:
