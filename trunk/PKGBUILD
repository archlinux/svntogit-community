# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: quininer <quininer@live.com>
pkgbase='alacritty'
pkgname=('alacritty' 'alacritty-terminfo')
pkgdesc="A cross-platform, GPU-accelerated terminal emulator"
_pkgver=0.4.1
pkgver="$_pkgver"
pkgrel=1
arch=('x86_64')
url="https://github.com/jwilm/alacritty"
license=('Apache')
makedepends=('rust' 'cargo' 'cmake' 'fontconfig' 'ncurses' 'desktop-file-utils' 'gdb' 'libxcb')
checkdepends=('ttf-dejavu') # for monospace fontconfig test
source=("${pkgbase}-${_pkgver}.tar.gz::https://github.com/jwilm/${pkgbase}/archive/v${_pkgver}.tar.gz")
sha256sums=('ee807f03a4014e7f989cf649a7c8209c0c5f57297812a39f4b163f9580b9a539')

build(){
  cd "$pkgbase-$_pkgver"
  env CARGO_INCREMENTAL=0 cargo build --release --locked
}

check(){
  cd "$pkgbase-$_pkgver"
  env CARGO_INCREMENTAL=0 cargo test --release
}

package_alacritty() {
  depends=('freetype2' 'fontconfig' 'libxi' 'libxcursor' 'libxrandr')
  optdepends=('alacritty-terminfo: terminfo for alacritty')

  cd "$pkgbase-$_pkgver"
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "extra/linux/alacritty.desktop"

  install -D -m755 "target/release/alacritty" "$pkgdir/usr/bin/alacritty"
  install -D -m644 "extra/alacritty.man" "$pkgdir/usr/share/man/man1/alacritty.1"
  install -D -m644 "extra/linux/io.alacritty.Alacritty.appdata.xml" "$pkgdir/usr/share/appdata/io.alacritty.Alacritty.appdata.xml"
  install -D -m644 "alacritty.yml" "$pkgdir/usr/share/doc/alacritty/example/alacritty.yml"
  install -D -m644 "extra/completions/alacritty.bash" "$pkgdir/usr/share/bash-completion/completions/alacritty"
  install -D -m644 "extra/completions/_alacritty" "$pkgdir/usr/share/zsh/site-functions/_alacritty"
  install -D -m644 "extra/completions/alacritty.fish" "$pkgdir/usr/share/fish/vendor_completions.d/alacritty.fish"
  install -D -m644 "extra/logo/alacritty-term.svg" "$pkgdir/usr/share/pixmaps/Alacritty.svg"
}

package_alacritty-terminfo() {
  pkgdesc="Terminfo files for the alacritty terminal emulator"
  depends=('ncurses')

  cd "$pkgbase-$_pkgver"

  install -dm 755 "$pkgdir/usr/share/terminfo/a/"
  tic -o "$pkgdir/usr/share/terminfo" -xe alacritty,alacritty-direct extra/alacritty.info
}
