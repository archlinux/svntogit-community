# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=tftpy
pkgname=('python-tftpy')
pkgver=0.8.0
pkgrel=3
pkgdesc='Pure Python TFTP library'
url='https://github.com/msoulier/tftpy'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/msoulier/tftpy/archive/${pkgver}.tar.gz)
sha256sums=('890bf36454355aa06bb267b317b37ffa6ea1d85a95b873b896befd20a61a3548')
sha512sums=('4501c21d923348799bf6f4f2dd0b476171d99d993fae72c6560c23f091460602db774e3d18c2c62c55e3bae05669499a660a221e3db20151d5925431ecb4a224')

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgbase}-${pkgver}
  make test PY=python
}

package_python-tftpy() {
  depends=('python')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README ChangeLog  -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv "${pkgdir}/usr/bin/tftpy_client"{.py,}
  mv "${pkgdir}/usr/bin/tftpy_server"{.py,}
}

# vim: ts=2 sw=2 et:
