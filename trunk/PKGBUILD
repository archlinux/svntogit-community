# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-user-accounts
pkgver=2.4.3
pkgrel=2
pkgdesc='Switchboard User Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-useraccounts
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  libgee
  libgranite.so
  libhandy-1.so
  libpwquality
  libswitchboard-2.0.so
  polkit
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
_tag=a4203ee2b33dc93e4797c7cf49c3bed60ebb8169
source=(switchboard-plug-user-accounts::git+https://github.com/elementary/switchboard-plug-useraccounts.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd switchboard-plug-user-accounts

  git describe --tags
}

build() {
  arch-meson switchboard-plug-user-accounts build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set sw=2 sts=-1 et:
