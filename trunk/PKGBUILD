# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=cftime
pkgname=python-${_pkg}
pkgver=1.6.2
pkgrel=2
pkgdesc="Time-handling functionality from python-netcdf4"
arch=(x86_64)
url="https://unidata.github.io/cftime"
license=(MIT)
depends=(python-numpy)
makedepends=(cython python-setuptools)
checkdepends=(python-pytest)
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(https://github.com/Unidata/cftime/archive/v${pkgver}rel/${pkgname}-${pkgver}.tar.gz)
sha256sums=('b9da5ec5b3abb5aff806d40f84580896ef566821806c8f28e6cb21284f8c377b')

build() {
  cd ${_pkg}-${pkgver}rel
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}rel
  sed -i '/--cov/d' setup.cfg
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="${PWD}"/build/lib.linux-${CARCH}-cpython-${python_version/./} pytest -vv --color=yes
}

package() {
  cd ${_pkg}-${pkgver}rel
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
