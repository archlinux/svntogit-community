# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=simde
pkgver=0.7.4
pkgrel=1
pkgdesc="Implementations of SIMD instruction sets"
arch=(any)
url="https://github.com/simd-everywhere/simde"
license=(MIT)
makedepends=(meson)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('9add192021014f503699dedff8644ad8079a6381302fe56b91950a3b498b58ba7d069a4779007738edfad1ec57dbce02d462bd833a517240e8ff992e3867868a')
b2sums=('553e986a345a7ab832995b4079adbc51287d3c22186a278255d2e5cc4a131c9c93c06fb21754601dd974d16b3fe3e455078c35f761e22f584c1b1af8c70b996c')

build() {
  arch-meson build $pkgname-$pkgver
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
