# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-i18n
pkgver=5.0.0
pkgrel=1
pkgdesc="Oslo i18n library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.i18n/$pkgver"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel')
checkdepends=('python-mock' 'python-oslotest' 'python-oslo-config')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.i18n/archive/$pkgver.tar.gz")
sha512sums=('2cda1e96108b06c4f60df8044777f22dc146d6f5a762dec0199430cd1ae76f372aacd5e8acef6e6ead2430e4cb28ac8d986c052fa1da6efbf44b259c89c73529')

export PBR_VERSION=$pkgver

build() {
  cd oslo.i18n-$pkgver
  python setup.py build
}

check() {
  cd oslo.i18n-$pkgver
  stestr run
}

package() {
  cd oslo.i18n-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
