# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsbeautifier
pkgname=('python-jsbeautifier' 'python2-jsbeautifier')
pkgver=1.6.11
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
makedepends=('python-setuptools' 'python2-setuptools' 'python-editorconfig' 'python2-editorconfig'
             'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/beautify-web/js-beautify.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a js-beautify{,-py2}
}

build() {
  cd "$srcdir"/js-beautify/python
  python setup.py build

  cd "$srcdir"/js-beautify-py2/python
  python2 setup.py build
}

check() {
  cd "$srcdir"/js-beautify/python
  python setup.py test

  cd "$srcdir"/js-beautify-py2/python
  python2 setup.py test
}

package_python-jsbeautifier() {
  depends=('python-six' 'python-editorconfig')

  cd js-beautify/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jsbeautifier() {
  depends=('python2-six' 'python2-editorconfig')

  cd js-beautify-py2/python
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/js-beautify{,2}
}

# vim:set ts=2 sw=2 et:
