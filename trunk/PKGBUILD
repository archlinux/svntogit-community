# Contributor: Cristian Maureira <saint [at] archlinux.cl>
# Previous Contributor: Dr.Egg <rwhite @ archlinux . us>

pkgname=musescore
pkgver=0.9.6.3
pkgrel=3
pkgdesc="A music score editor written in Qt4"
arch=('i686' 'x86_64')
url="http://www.musescore.org/en/"
license=('GPL')
depends=('qt>=4.5.0' 'openssl' 'libsndfile' 'qtscriptgenerator')
makedepends=('cmake' 'doxygen')
optdepends=('portaudio' 'jack-audio-connection-kit')
conflicts=('musescore-svn' 'mscore')
source=(http://downloads.sourceforge.net/project/mscore/mscore/mscore-${pkgver}/mscore-${pkgver}.tar.bz2 'paths.patch' 'QtEvent.patch')
md5sums=('1bcd2e80ba968d0d3116c06d4d5d3e32'
         'e5011154d217f633f21b4bb70e589510'
         '086ddd7ddfe43ec387a56d7dfe3e9815')

build() {
  cd $srcdir/mscore-${pkgver}
  export QT_PLUGINS_DIR=/usr/lib/qt/plugins
  [ -d build ] && make clean
  patch -p1 < $srcdir/paths.patch
  patch -p1 < $srcdir/QtEvent.patch
  sed -i 's+${Mscore_INSTALL_NAME}man+doc/mscore+' \
    "$srcdir"/mscore-$pkgver/mscore/manual/CMakeLists.txt
  make PREFIX=/usr release
}
package() {
  cd $srcdir/mscore-${pkgver}
  make DESTDIR="$pkgdir" install
  # avoid file conflict with "qtscriptgenerator" 
  for _i in gui network core uitools xml
  do 
    rm $pkgdir/usr/lib/qt/plugins/script/libqtscript_${_i}.so
  done
}
