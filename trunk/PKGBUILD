pkgname=intel-oneapi-basekit
pkgver=2022.3.0.8767
_urlver=18852
pkgrel=1
pkgdesc="Intel oneAPI Base Toolkit for Linux"
arch=('x86_64')
url='https://software.intel.com/content/www/us/en/develop/tools/oneapi.html'
license=("custom")
source=("https://registrationcenter-download.intel.com/akdlm/irc_nas/${_urlver}/l_BaseKit_p_${pkgver}_offline.sh")
b2sums=('0a88727daafca8118eab1505f7b82f280c35390be636d2cc66d5a9a8bd3a559f9bd8b0acabee48f96bafc47ae2dba9d13ff90e40174ffada8a98192dd2b79de5')
depends=()
noextract=("l_BaseKit_p_${pkgver}_offline.sh")
optdepends=('libnotify: VTune GUI'
            'glib2: VTune GUI'
            'gtk3: VTune GUI'
            'at-spi2-atk: VTune GUI'
            'libdrm: VTune GUI'
            'libxcb: VTune GUI'
            'xdg-utils: VTune GUI'
            'nss: Advisor GUI')
provides=('intel-oneapi-mkl' 'intel-oneapi-dnnl' 'intel-oneapi-tbb' 'intel-oneapi-dpl'
          'intel-oneapi-ccl' 'intel-oneapi-dpcpp-cpp-compiler' 'intel-oneapi-dal'
          'intel-oneapi-vpl' 'intel-oneapi-ipp' 'intel-oneapi-ippcp' 'intel-oneapi-advisor'
          'intel-oneapi-vtune' 'intel-oneapi-fpga-group')

prepare() {
  chmod +x "${srcdir}/l_BaseKit_p_${pkgver}_offline.sh"
}

build() {
  cd "${srcdir}"

  ./"l_BaseKit_p_${pkgver}_offline.sh" \
    --extract-folder "${srcdir}" --extract-only \
    --remove-extracted-files no --log "${srcdir}"/extract.log
}

package() {
  cd "${srcdir}"

  # we have to run as a user different from root
  # otherwise the installer wants to write to /opt, /var
  # which is not possible in fakeroot
  runuser -u builduser -- "${srcdir}/l_BaseKit_p_${pkgver}_offline"/install.sh \
    --silent --eula accept \
    --components all \
    --install-dir "${pkgdir}"/opt/intel/oneapi \
    --log-dir "${srcdir}"/ --ignore-errors
}
